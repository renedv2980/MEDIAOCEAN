*          DATA SET ACMASTER   AT LEVEL 118 AS OF 07/31/20                      
*CATALP ACMASTER                                                                
ACMASTER TITLE '- ACCOUNT SYSTEM OFFLINE CONTROLLER'                            
********************************************************************            
*  MNAS:LEV 115:11/15/2019: DSRD-24207:EXPENSE JOBS ON JOBS REPORT *            
********************************************************************            
ACMASTER CSECT                                                                  
         PRINT NOGEN                                                            
         ENTRY SSB                                                              
         ENTRY UTL                                                              
         ENTRY DCBIN                                                            
         ENTRY DCBOUT                                                           
         ENTRY DCBWORK                                                          
         ENTRY DCBRCVT                                                          
         ENTRY DCBRCVC                                                          
         ENTRY CORELIST                                                         
         USING *,RF                                                             
         OC    VMASTC,VMASTC                                                    
         JNZ   MVSENTRY                                                         
         BASR  RF,0                                                             
         AHI   RF,ACGO-*                                                        
         BR    RF                                                               
                                                                                
VMASTC   DC    V(MASTC)                                                         
                                                                                
MVSENTRY NBASE 0,**FILC**,WORK=V(ACSAVE)                                        
         GOTOR ACGO,0                                                           
MVSEXIT  XBASE ,                   RETURN TO MVS                                
                                                                                
         LTORG                                                                  
                                                                                
ACGO     NMOD1 0,**FILC**                                                       
         L     R9,AGLOBALS                                                      
         USING GLOBALS,R9          R9=A(GLOBAL LITERALS)                        
         L     RA,VACWORKC                                                      
         USING ACWORKD,RA          RA=A(GLOBAL WORKING STORAGE)                 
         L     RC,AMONACC                                                       
         USING ACMD,RC             RC=A(MONACC WORKING STORAGE)                 
         L     R5,ADMASTC                                                       
         USING MASTD,R5            R5=A(MASTER WORKING STORAGE)                 
         NI    ACMINDS2,FF-(ACMISLAV)                                           
         USING SSBD,SSB                                                         
*                                                                               
         LTR   R1,R1                                                            
         BZ    INIRUN10                                                         
         ST    R1,ADPARM           SAVE CALLING PARAMETER LIST                  
         OI    ACMINDS2,ACMISLAV   SET RUNNING IN SLAVE MODE                    
         EJECT                                                                  
***********************************************************************         
* HANDLE DDLINK SERVER CALLS                                          *         
***********************************************************************         
                                                                                
         CLI   0(R1),X'DD'         TEST CALLED FROM DDLINK                      
         BNE   INIRUN06                                                         
         OI    ACMINDS5,ACMIDDLK   YES - SET GLOBAL FLAG                        
         OI    ACMINDS,ACMIEMUD    SET EMULATED FILE                            
         SR    R2,R2                                                            
         ICM   R2,7,1(R1)                                                       
         USING LP_D,R2             R2=A(LP_D)                                   
         ST    R2,ACMALP_D         SET A(LP_D) FOR APPLICATION                  
         L     R3,LP_ARUNP                                                      
         USING RUNPARMD,R3                                                      
         MVC   ACMMODE2,RUNPMODE   SET RUNNER MODE                              
         ICM   R3,7,RUNPARUN                                                    
         USING RUNFACSD,R3         R3=A(RUNNER FACILITIES)                      
         L     R5,RMASTC           R5=A(RUNNER MASTC)                           
         ST    R5,ADMASTC                                                       
                                                                                
         CLI   ACMMODE2,RRUNSTRQ   TEST RUN FIRST MODE                          
         BNE   INIRUN02                                                         
         MVC   RCPROG,MCPROG       SET SERVER PROGRAM CODE                      
                                                                                
         MVC   PROG01,RCPROG                                                    
         GOTOR RGPHS,DMCB,0,(C'E',PHASE01)                                      
         OC    ACSPECS,0(R1)                                                    
         BNZ   *+6                                                              
         DC    H'0'                CAN'T LOAD 01 PHASE                          
                                                                                
         MVC   PROG02,RCPROG                                                    
         GOTOR RGPHS,DMCB,0,(C'E',PHASE02)                                      
         OC    ACAPPLIC,0(R1)                                                   
         BNZ   *+6                                                              
         DC    H'0'                CAN'T LOAD 02 PHASE                          
                                                                                
         LHI   R0,QTSAROFF         LOAD TSAROFF                                 
         ICM   R0,B'1110',T00A                                                  
         GOTOR RGPHS,DMCB,0,(R0)                                                
         OC    ACMVTSAR,0(R1)                                                   
         BNZ   *+6                                                              
         DC    H'0'                                                             
                                                                                
         L     RE,ADCOMFAC         EXTRACT ADDRESSES FROM RUNNER'S              
         L     RF,LP_ACOM          COPY OF COMFACS FOR LOCAL USE                
         MVC   CLINKIO-COMFACSD(,RE),CLINKIO-COMFACSD(RF)                       
         MVC   CCALLOV-COMFACSD(,RE),CCALLOV-COMFACSD(RF)                       
         MVC   CSOFDAT-COMFACSD(,RE),CSOFDAT-COMFACSD(RF)                       
         MVC   CMASTC-COMFACSD(,RE),CMASTC-COMFACSD(RF)                         
         MVC   ACMSOFT,CSOFDAT-COMFACSD(RF)                                     
                                                                                
         MVI   ACMMODE,0                                                        
         MVI   MODE,0              CALL APPLICATION TO PROCESS                  
         GOTOR GO                                                               
         B     MASTX                                                            
                                                                                
INIRUN02 CLI   ACMMODE2,RRUNREQQ   TEST PROCESS WORK MODE                       
         BNE   MASTX                                                            
         GOTOR SETRUN              SET USER RUN VALUES                          
         GOTOR SETLNG              SET LANGUAGE DEPENDENT VALUES                
                                                                                
         LA    R0,UTL                                                           
         LA    R1,UTLL                                                          
         LR    RF,R1                                                            
         L     RF,MCUTL                                                         
         MVCL  R0,RE               SAVE CALLING SYSTEM UTL                      
         L     RF,MCSSB                                                         
         MVC   SSB(SSBL),0(RF)     SET CALLING SYSTEM SSB                       
                                                                                
         MVI   RCTRACE,NO          SET TRACE IF RUNNER TRACE IS ON              
         CLI   MCTRACE,NO                                                       
         BE    *+8                                                              
         MVI   RCTRACE,YES                                                      
         GOTOR DATAMGR,ACMPARA,ACMDMDTF,ACMACFIL                                
         MVC   ADACCFIL,12(R1)     A(ACCMST DTF)                                
         GOTOR (RF),(R1),ACMDMDTF,ACMACDIR                                      
         MVC   ACMADDIR,12(R1)     A(ACCDIR DTF)                                
                                                                                
         MVI   RCFLONLY,YES        GO TO EXECUTE I/O AND RUN SPECS              
         GOTOR ACREPORT                                                         
         MVI   RCFLONLY,NO                                                      
         MVI   MODE,PROCSPEC       PASS SPEC FIRST MODE TO APPLICATION          
         GOTOR GO                                                               
         CLI   FCFLTUSD,YES        TEST FILTERING USED TRANSACTIONS             
         BNE   *+16                                                             
         CLI   RCRERUN,YES         TEST IF THIS IS A RERUN                      
         BNE   *+8                                                              
         MVI   FCFLTUSD,NO         RESET FILTER IF IT IS                        
         GOTOR GETCPY              READ COMPANY RECORD AND SET VALUES           
         TM    ACMINDS,ACMINEWO    TEST NEW OFFICES COMPANY                     
         BZ    *+8                                                              
         GOTOR GETOFN              YES-GET THE OFFICE NAMES                     
         GOTOR PROFILES,DMCB,ADCOMP GET COMPANY PROFILES                        
         GOTOR SETOFF              ESTABLISH OFFICE                             
                                                                                
         MVI   MODE,RUNFRST        PASS RUN FIRST MODE TO APPLICATION           
         GOTOR GO                                                               
                                                                                
         GOTOR SETCUR              BUILD CURRENCY TABLE IF REQUIRED             
         GOTOR DATCON,ACMPARA,(4,RCDATE),(0,ACMWRK)                             
         GOTOR (RF),(R1),(4,RCDATE),(3,ACMDAYB)                                 
         MVC   THISMNTH(1),ACMWRK+1 BUILD MONTH FILTER FIELD                    
         PACK  ACMDUB,ACMWRK+2(2)                                               
         CVB   R1,ACMDUB                                                        
         LA    R1,HEXCODE(R1)                                                   
         MVC   THISMNTH+1(1),0(R1)                                              
         OC    ACMAJCSP,ACMAJCSP   TEST JOBBER COLUMN SPEC RESOLVED             
         BZ    INIRUN04                                                         
         LHI   R0,0                                                             
         TM    ACMINDS2,ACMIFJC    TEST FOR FIELD BASED JOBCOL SPEC             
         BO    *+8                 YES                                          
         LHI   R0,255                                                           
         GOTOR ACMAJOBL,ACMPARA,((R0),ACMAJCSP),ACMACOLL,ADCOMFAC               
                                                                                
INIRUN04 L     R1,ADPARM                                                        
         SR    RF,RF                                                            
         ICM   RF,1,4(R1)          RF=NUMBER OF REQUEST CARDS                   
         BNZ   *+6                                                              
         DC    H'0'                                                             
         STC   RF,MCRQNUM                                                       
         SR    RE,RE                                                            
         ICM   RE,7,5(R1)          RE=A(REQUEST DATA)                           
                                                                                
         MVC   QRECORD,0(RE)       SET REQUEST CARDS                            
         MVC   QRECORD2,SPACES                                                  
         CLI   MCRQNUM,1                                                        
         BE    *+10                                                             
         MVC   QRECORD2,L'QRECORD(RE)                                           
                                                                                
         MHI   RF,L'QRECORD        AND REQUEST STACK                            
         L     R0,ADQSTACK                                                      
         LA    R1,8*L'QRECORD      MAKE SURE TO CLEAR WHAT IS LEFT              
         ICM   RF,8,SPACES         CLEAR TO SPACES                              
         MVCL  R0,RE                                                            
         B     NXTREQ02                                                         
         DROP  R2,R3                                                            
         EJECT                                                                  
***********************************************************************         
* HANDLE OTHER SLAVED MODE CALLS                                      *         
***********************************************************************         
                                                                                
INIRUN06 L     R5,0(R1)            P1=A(MASTC)                                  
         ST    R5,ADMASTC                                                       
         LA    R0,UTL                                                           
         LA    R1,UTLL                                                          
         L     RE,MCUTL                                                         
         LR    RF,R1                                                            
         MVCL  R0,RE               SAVE CALLING SYSTEM UTL                      
         L     RF,MCUTL                                                         
         MVC   4(1,RF),MCS2SENO    SET ACCOUNT SE NUMBER                        
         L     RF,MCSSB                                                         
         MVC   SSB(SSBL),0(RF)     SET CALLING SYSTEM SSB                       
         CLI   0(R1),0                                                          
         BE    INIRUN10                                                         
         L     RF,4(R1)            SET CALLING REQUEST CARDS                    
         MVC   QRECORD,0(RF)                                                    
         MVC   QRECORD2,L'QRECORD(RF)                                           
         L     RF,ADQSTACK                                                      
         MVC   0(L'QRECORD,RF),QRECORD                                          
         MVC   L'QRECORD(L'QRECORD2,RF),QRECORD2                                
         MVI   MCRQNUM,2                                                        
         B     NXTREQ02                                                         
         EJECT                                                                  
***********************************************************************         
* REGULAR RUN INTITIALIZATION/OTHER SLAVED MODE RUN INITIALIZATION    *         
***********************************************************************         
                                                                                
INIRUN10 GOTOR SETRUN              SET RUN CONTROL VALUES                       
         GOTOR SETLNG              SET LANGUAGE DEPENDENT VALUES                
         MVI   RCFLONLY,YES        GO TO EXECUTE I/O AND RUN SPECS              
         GOTOR ACREPORT                                                         
         MVI   RCFLONLY,NO                                                      
         MVI   MODE,PROCSPEC       PASS SPEC FIRST MODE TO APPLICATION          
         GOTOR GO                                                               
         CLI   FCFLTUSD,YES        TEST FILTERING USED TRANSACTIONS             
         BNE   *+16                                                             
         CLI   RCRERUN,YES         TEST IF THIS IS A RERUN                      
         BNE   *+8                                                              
         MVI   FCFLTUSD,NO         RESET FILTER IF IT IS                        
         GOTOR OPNFIL                                                           
         GOTOR GETCPY              READ COMPANY RECORD AND SET VALUES           
         GOTOR STRLOG              PRINT START LOGOS                            
*        GOTOR LINEUP              DO PAPER LINE-UP                             
         TM    ACMINDS,ACMINEWO    TEST NEW OFFICES COMPANY                     
         BZ    *+8                                                              
         GOTOR GETOFN              YES-GET THE OFFICE NAMES                     
         GOTOR PROFILES,DMCB,ADCOMP GET COMPANY PROFILES                        
         GOTOR SETOFF              ESTABLISH OFFICE                             
         MVI   ACMMODE,0                                                        
         MVI   MODE,RUNFRST        FIRST TIME HOOK                              
         GOTOR GO                                                               
         GOTOR SETCUR              BUILD CURRENCY TABLE IF REQUIRED             
         GOTOR DATCON,ACMPARA,(4,RCDATE),(0,ACMWRK)                             
         GOTOR (RF),(R1),(4,RCDATE),(3,ACMDAYB)                                 
         MVC   THISMNTH(1),ACMWRK+1 BUILD MONTH FILTER FIELD                    
         PACK  ACMDUB,ACMWRK+2(2)                                               
         CVB   R1,ACMDUB                                                        
         LA    R1,HEXCODE(R1)                                                   
         MVC   THISMNTH+1(1),0(R1)                                              
         OC    ACMAJCSP,ACMAJCSP   TEST JOBBER COLUMN SPEC RESOLVED             
         BZ    INIRUN12                                                         
         LHI   R0,0                                                             
         TM    ACMINDS2,ACMIFJC    TEST FOR FIELD BASED JOBCOL SPEC             
         BO    *+8                 YES                                          
         LHI   R0,255                                                           
         GOTOR ACMAJOBL,ACMPARA,((R0),ACMAJCSP),ACMACOLL,ADCOMFAC               
                                                                                
INIRUN12 CLI   FCTSTACK,YES        TEST APPLICATION WANTS A STACK               
         BNE   INIRUNX                                                          
         TM    ACMINDS,ACMIEMUD                                                 
         BNZ   *+6                                                              
         DC    H'0'                CAN ONLY STACK IF NEW FILE                   
         LHI   R0,ACMTSNUM         YES - ACQUIRE STORAGE FOR IT                 
         SLL   R0,2                                                             
         GETMAIN R,LV=(0)                                                       
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
         ST    R1,ACMTSADR                                                      
                                                                                
INIRUNX  TM    ACMINDS2,ACMISLAV   TEST SLAVE MODE                              
         BNZ   MASTX                                                            
         EJECT                                                                  
***********************************************************************         
* CONTROL READING OF SYSTEM RECOVERY FILE                             *         
***********************************************************************         
                                                                                
GETRCV   CLI   FCRDRCVR,NO         TEST READING RECOVERY FILE                   
         BE    GETRCVX                                                          
         GOTOR RCVGET                                                           
         CLI   FCRDREQS,YES        TEST CALLER WANTS REQUESTS TOO               
         BNE   RUNEND              NO - ALL DONE                                
GETRCVX  DS    0H                                                               
         EJECT                                                                  
***********************************************************************         
* CALL MASTER TO GET NEXT REQUEST RECORD                              *         
***********************************************************************         
                                                                                
NXTREQ   TM    ACMINDS2,ACMISLAV   TEST SLAVE MODE                              
         BNZ   MASTX               YES                                          
         TM    ACMINDS3,ACMIMULT   TEST MULTI REQUEST                           
         BNZ   NXTREQ02            YES                                          
         CP    RCRQTOT,RCENNUM     TEST ALL REQUESTS PROCESSED                  
         BNL   RUNEND                                                           
         GOTOR MCVREQST            GET NEXT REQUEST CARD                        
         CLI   MCREQREC,C'/'                                                    
         BE    RUNEND                                                           
         MVC   QRECORD,MCIO        EXTRACT REQUEST CARD                         
         MVC   QRECORD2,SPACES                                                  
         CLI   MCRQNUM,2           TEST IF TWO REQUEST CARDS PASSED             
         BL    *+10                                                             
         MVC   QRECORD2,MCIO+L'MCREQREC                                         
                                                                                
         L     R0,ADQSTACK         CLEAR REQUEST STACK TO SPACES                
         LHI   R1,MCRQCOLS*L'MCREQREC                                           
         SR    RE,RE                                                            
         LHI   RF,C' '                                                          
         SLL   RF,32-8                                                          
         MVCL  R0,RE                                                            
         L     R0,ADQSTACK         MOVE REQUEST CARDS TO STACK                  
         SR    R1,R1                                                            
         IC    R1,MCRQNUM                                                       
         MHI   R1,L'MCREQREC                                                    
         LA    RE,MCIO                                                          
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
                                                                                
         CLI   QCOMPANY,FF         TEST SPECIAL ALL COMPANY REQUEST             
         BNE   NXTREQ02                                                         
         MVI   RCCOMPFL,X'40'      SET TO START READING ALL COMPANIES           
         OI    ACMINDS3,ACMIMULT   SET MULTI-REQUEST                            
                                                                                
NXTREQ02 AP    RCRQTOT,ACMPONE                                                  
         MVC   QPROG,RCPROG                                                     
         L     RE,ADQSTACK         COPY CARDS 1 AND 2 FROM STACK                
         MVC   ACQPROG-ACQD(L'ACQPROG,RE),RCPROG                                
         OI    ACMINDS3,ACMIRSFD   RESOLVE SOFT DATES                           
NXTREQX  DS    0H                                                               
         EJECT                                                                  
***********************************************************************         
* INITIALIZE FOR NEW REQUEST                                          *         
***********************************************************************         
                                                                                
INIREQ   CLI   QCOMPANY,C' '       IF NO COMPANY ON CARD                        
         BNE   *+10                ASSUME HEXCOMP                               
         MVC   QCOMPANY,RCCOMPFL                                                
         CLC   QCOMPANY,RCCOMPFL   RE-INITIALIZE FOR NEW COMPANY                
         BE    INIREQ07                                                         
         TM    ACMINDS3,ACMIMULT   TEST SPECIAL ALL COMPANY REQUEST             
         BZ    INIREQ04                                                         
         IC    R1,RCCOMPFL         YES - BUMP TO NEXT COMPANY                   
         AHI   R1,1                                                             
         STC   R1,RCCOMPFL                                                      
         LA    R1,KEY                                                           
         USING CPYRECD,R1                                                       
         MVC   CPYKEY,SPACES                                                    
         MVC   CPYKCPY,RCCOMPFL                                                 
         GOTOR HIGHRTRN                                                         
         L     R1,ACMABUF          POINT TO RECORD                              
         CLI   CPYKCPY+1,FF        END OF FILE RECORD                           
         BE    *+12                SO TURN OFF ACMIMULT                         
         CLI   DMCB+8,0            TEST GOOD I/O                                
         BE    *+12                   YES                                       
         NI    ACMINDS3,FF-(ACMIMULT) NO, THEN DONE                             
         J     NXTREQ                                                           
                                                                                
         MVC   RCCOMPFL,CPYKCPY                                                 
         B     INIREQ06                                                         
         DROP  R1                                                               
                                                                                
INIREQ04 MVC   RCCOMPFL,QCOMPANY                                                
                                                                                
INIREQ06 GOTOR GETCPY                                                           
         TM    ACMINDS,ACMINEWO    TEST NEW OFFICES COMPANY                     
         BZ    INIREQ07                                                         
         GOTOR GETOFN              GET NEW OFFICE NAMES BUFFER                  
                                                                                
INIREQ07 DS    0H                                                               
* FOR AA, AB AND AC REPORTS ONLY                                                
* RESOLVE SOFT DATES BEFORE PROCRQST MODE                                       
* DSFAC-267                                                                     
                                                                                
         CLC   =C'AA',MCPROG                                                    
         BE    INIREQ07A                                                        
         CLC   =C'AB',MCPROG                                                    
         BE    INIREQ07A                                                        
         CLC   =C'AC',MCPROG                                                    
         BNE   INIREQ07OTH         ALL OTHER REPORTS                            
                                                                                
INIREQ07A DS   0H                                                               
         TM    ACMINDS3,ACMIRSFD   RESOLVE SOFT DATES?                          
         BZ    INIREQ07B                                                        
         NI    ACMINDS3,FF-ACMIRSFD                                             
         GOTOR RESDAT              RESOLVE SOFT DATES                           
                                                                                
INIREQ07B DS   0H                                                               
         GOTOR SETDAT              SET REQUEST DATES                            
                                                                                
         MVI   MODE,PROCRQST       PASS PROCESS REQ MODE TO APPLICATION         
         GOTOR GO                                                               
         B     INIREQ09            SKIP REGULAR PROCESSING                      
                                                                                
* ALL OTHER (NON-AA/AB/AC) REPORTS PROCESS AS BEFORE:                           
* PROCRQST FIRST, THEN RESDAT, SETDAT                                           
INIREQ07OTH DS 0H                                                               
         MVI   MODE,PROCRQST       PASS PROCESS REQ MODE TO APPLICATION         
         GOTOR GO                                                               
         TM    ACMINDS3,ACMIRSFD   RESOLVE DATES                                
         BZ    INIREQ08                                                         
         NI    ACMINDS3,FF-ACMIRSFD                                             
         GOTOR RESDAT              RESOLVE SOFT DATES                           
                                                                                
INIREQ08 GOTOR SETDAT              SET REQUEST DATES                            
                                                                                
INIREQ09 DS    0H                                                               
*&&UK*&& CLI   SYSPROF+2,C'O'                                                   
*&&UK*&& BNE   *+8                                                              
*&&UK*&& GOTOR SETOFN              SET OFFICE/COPPANY NAME                      
         TM    ACMINDS5,ACMIAPPL   TEST APPLICATION HAS SET LIST                
         BO    INIREQ10                                                         
         GOTOR SETLST                                                           
                                                                                
INIREQ10 ICM   R1,15,ACMANXL       SET NEXT UNIT/LEDGER CODE                    
         BZ    *+10                                                             
         MVC   QUNIT(QACCOUNT-QUNIT),0(R1)                                      
         L     RF,VEXTRAS                                                       
         MVC   REQCARD-RUNXTRAD(L'REQCARD,RF),QRECORD                           
         GOTOR SETRQF              SET REQUEST FILTERS                          
                                                                                
INIREQ12 GOTOR REQIND              SET REQUEST INDICATORS                       
                                                                                
         NI    ACMINDS3,FF-(ACMIAGRP+ACMIAPRS)                                  
         MVC   ACMPRSN,SPACES                                                   
         CLC   =C'1R',QUNIT        PERSON LIST OF ACCOUNTS                      
         BNE   INIREQ16                                                         
         CLI   MCRQNUM,3                                                        
         BL    INIREQ16                                                         
         LHI   R0,2                3RD AND 4TH REQUEST CARD                     
         LHI   RF,4                CHECK IN FOUR PLACES ON EACH                 
         LA    RE,ACQCARD3-ACQD    START WITH 3RD CARD                          
         A     RE,ADQSTACK                                                      
         CLI   0(RE),ACQPRSN       CHECK FOR PERSON CODE                        
         BE    INIREQ14                                                         
         AHI   RE,15                                                            
         BCT   RF,*-12                                                          
         LHI   RF,4                CHECK IN FOUR PLACES NEXT CARD               
         LA    RE,ACQCARD4-ACQD    NOW TRY 4TH REQUEST CARD                     
         A     RE,ADQSTACK                                                      
         BCT   R0,*-24                                                          
         B     INIREQ16                                                         
                                                                                
         USING CPYELD,R2                                                        
INIREQ14 L     R2,ADCMPEL                                                       
         TM    CPYSTAT5,CPYSNCST   USING NEW COST SYSTEM?                       
         BZ    *+8                                                              
         OI    ACMINDS3,ACMIAPRS                                                
         MVC   ACMPRSN,1(RE)       MOVE IN CODE                                 
         DROP  R2                                                               
                                                                                
INIREQ16 CLI   QACCOUNT+2,C'='     TEST FOR ACCOUNT GROUP REQUEST               
         BNE   INIREQ20                                                         
         CLI   QACCOUNT+0,C'G'     QACCOUNT HAS FORMAT GX=YYY WHERE             
         BNE   INIREQ20            X=GROUP TYPE AND Y IS CODE OR 'ALL'          
         GOTOR SETGRP              SET UP ACCOUNT GROUP TABLES                  
         BNZ   INIREQ18                                                         
         MVI   SKIPSPEC,C'E'       PRINT REQUEST ERROR REPORT                   
         GOTOR ACREPORT                                                         
         B     NXTREQ                                                           
                                                                                
INIREQ18 OI    ACMINDS3,ACMIAGRP   SET GROUP REQUEST IN PROCESS                 
         SAM31                     ENTER XA MODE                                
                                                                                
         USING GRPTABD,R3                                                       
         L     R3,ACMAGRPS                                                      
         MVC   QACCOUNT+3(L'GRPTCOD),GRPTCOD                                    
         MVC   QOFFICE,GRPTOFF     SET OFFICE CODE IN REQUEST                   
         DROP  R3                                                               
                                                                                
         SAM24                     EXIT XA MODE                                 
                                                                                
         USING OFFALD,R2                                                        
INIREQ20 GOTOR SETOFF                                                           
         JNE   NXTREQ                                                           
         TM    ACMINDS,ACMINEWO    TEST NEW OFFICE SYSTEM                       
         BZ    INIREQ22                                                         
         L     R2,ACMAOFL                                                       
         MVC   OFFACOMF,ADCOMFAC                                                
         MVC   OFFAOFFC,QOFFICE                                                 
         NI    OFFAINDS,X'FF'-OFFAIXOL                                          
         TM    QOFFICE,X'40'       TEST EXCLUDE                                 
         BO    *+12                                                             
         OI    OFFAOFFC,X'40'                                                   
         OI    OFFAINDS,OFFAIXOL   SET EXCLUDE OPTION                           
         MVI   OFFAACT,OFFAREQ     SET TO VALIDATE REQUESTED OFFICE             
         GOTOR ADOFFAL,OFFALD                                                   
         BE    *+6                                                              
         DC    H'0'                                                             
         DROP  R2                                                               
                                                                                
INIREQ22 MVC   RCLANG,RCLANGSV     SET LANGUAGE                                 
         TM    QLANG,X'0F'         TEST REQUEST OVERRIDE LANGUAGE SET           
         BZ    INIREQ24                                                         
         MVC   RCLANG,QLANG        CAN BE X'01'-X'7F' OR C'1'-C'9'              
         TM    RCLANG,X'F0'        TEST C'1'-C'9'                               
         BNO   INIREQ24                                                         
         NI    RCLANG,X'0F'        YES - MAKE BINARY                            
                                                                                
INIREQ24 CLC   MCLANG,RCLANG       TEST CHANGE OF LANGUAGE                      
         BE    INIREQ26                                                         
         MVC   MCLANG,RCLANG       YES - SET LANGUAGE IN MASTC                  
         GOTOR SETLNG              AND SET LANGUAGE DEPENDENT VALUES            
                                                                                
INIREQ26 MVI   FCSEQ,FCSEQACC      SET FILE SEQUENCE TO ACCOUNT                 
         CLI   QSEQ,C' '           TEST OFFICE SEQUENCE REQUESTED               
         BNH   *+10                                                             
         MVC   FCSEQ,QSEQ          OVERRIDE SEQUENCE FROM REQUEST               
         MVI   ACMHIST,0                                                        
*&&US                                                                           
         CLI   QHIST,C' '          INCLUDE HISTORY FILE?                        
         BNH   *+8                                                              
         OI    ACMHIST,ACMHTRN     SET READ HISTORY FILE                        
         CLI   QHIST,C'N'          TREAT AS NOT PEELED ITEMS                    
         BNE   *+8                                                              
         OI    ACMHIST,ACMHNOT                                                  
*&&                                                                             
         L     R2,ADCMPEL                                                       
         USING CPYELD,R2                                                        
         TM    CPYSTAT5,CPYSNCST   USING NEW COST SYSTEM?                       
         BNO   INIREQ28                                                         
         MVI   ACMCAM,C'1'         DEFAULT METHOD IS 1                          
         CLI   QMTHD,C' '          METHOD REQUESTED                             
         BE    INIREQ28                                                         
         MVC   ACMCAM,QMTHD        SET ALLOCATION METHOD                        
                                                                                
INIREQ28 OI    ACMCFIND,ACMCFINT   INITIALIZE CONTRA FILTER CODE                
         GOTOR FLTCON                                                           
*                                                                               
         MVI   SKIPSPEC,C'R'       VALIDATE REQUEST AND PRINT DETAILS           
         GOTOR ACREPORT                                                         
         OC    VISGENQ,VISGENQ                                                  
         BZ    INIREQ29                                                         
         GOTOR VISGENQ,DMCB,C'SUSS',0  SUSPEND SHARED ENQUEUES FOR TASK         
INIREQ29 TM    ADLSTREC,X'80'      TEST LIST RECORD VALID                       
         BZ    INIREQ30                                                         
         TM    ACMINDS2,ACMISLAV   TEST IN SLAVE MODE                           
         BNZ   MASTX                                                            
         B     NXTREQ                                                           
                                                                                
INIREQ30 MVI   MODE,REQFRST                                                     
         GOTOR GO                                                               
         GOTOR SETCUR              SET-UP CURRENCY VALUES                       
                                                                                
         CLC   =C'1R',QUNIT        PERSONNEL REPORTS ONLY                       
         BNE   INIREQ32                                                         
         CLI   FCPERSEC,FCPERCLI   TEST CLIENT ORIENTED SECURITY                
         BE    *+12                                                             
         CLI   FCPERSEC,FCPERBTH   CHECK 1R AND 1C OFFICE?                      
         BNE   INIREQ32            NO                                           
         OI    ACMCFIND,ACMCFOFF   SET TO FILTER ON CONTRA OFFICE               
INIREQ32 DS    0H                                                               
*&&UK                                                                           
         CLI   QBILTYPE,C' '       TEST BILLING TYPE FILTER PRESENT             
         BE    INIREQX                                                          
         CLI   FCGETOPT,NO         TEST GETOPT NO-OPED                          
         BNE   INIREQX                                                          
         MVI   FCGETOPT,FCGETOJB                                                
*&&                                                                             
INIREQX  DS    0H                                                               
         EJECT                                                                  
***********************************************************************         
* READ WORK CODE RECORDS IF REQUIRED                                  *         
***********************************************************************         
                                                                                
GETWCO   CLI   FCRDWORK,YES                                                     
         BNE   GETWCOX                                                          
         GOTOR WCOGET              GET WORK CODES                               
GETWCOX  DS    0H                                                               
                                                                                
***********************************************************************         
* READ COMMENT RECORD IF REQUIRED                                     *         
***********************************************************************         
                                                                                
GETSCM   CLI   FCRDCOMM,YES                                                     
         BNE   GETSCMX                                                          
         GOTOR SCMGET                                                           
GETSCMX  DS    0H                                                               
                                                                                
***********************************************************************         
* READ LIST RECORD IF REQUIRED                                        *         
***********************************************************************         
                                                                                
GETLST   CLI   FCRDACL,YES                                                      
         BNE   GETLSTX                                                          
         GOTOR LSTGET                                                           
GETLSTX  DS    0H                                                               
                                                                                
***********************************************************************         
* READ SORTED OUTPUT FILE IF REQUIRED                                 *         
***********************************************************************         
                                                                                
GETSRT   CLI   FCRDACC,YES                                                      
         BNE   GETSRTX                                                          
         CLI   INFILE,YES                                                       
         BNE   GETSRTX                                                          
         GOTOR SRTGET                                                           
         J     ENDREQ                                                           
GETSRTX  DS    0H                                                               
                                                                                
***********************************************************************         
* READ ACCOUUNTS AND TRANSACIONS                                      *         
***********************************************************************         
                                                                                
GETULA   GOTOR ULAGET                                                           
         BL    ENDREQ                                                           
         BH    NXTGRP                                                           
                                                                                
***********************************************************************         
* READ ORDER RECORDS IF REQUIRED                                      *         
***********************************************************************         
                                                                                
GETORD   CLI   FCRDORD,YES                                                      
         BNE   GETORDX                                                          
         GOTOR ORDGET                                                           
GETORDX  DS    0H                                                               
         EJECT                                                                  
***********************************************************************         
* HANDLE END OF REQUEST                                               *         
***********************************************************************         
                                                                                
ENDREQ   MVI   MODE,REQLAST                                                     
         GOTOR GO                                                               
         OC    ACMANXL,ACMANXL                                                  
         BZ    ENDREQ02            NO MORE UNIT/LEDGERS                         
         L     RE,ACMANXL                                                       
         AHI   RE,2                                                             
         SR    R1,R1                                                            
         IC    R1,ACMANXL                                                       
         XC    ACMANXL,ACMANXL                                                  
         LTR   R1,R1                                                            
         BZ    ENDREQ02            END OF UNIT/LEDGER LIST GET NEXT REQ         
         ST    RE,ACMANXL          PROCESS NEXT UNIT/LEDGER                     
         BCTR  R1,0                                                             
         STC   R1,ACMANXL          NUMBER REMAINING                             
         MVI   FORCEHED,NO                                                      
         MVC   FORCEHED+1(4),FORCEHED                                           
         B     INIREQ10                                                         
                                                                                
ENDREQ02 CLI   ACMMODE,0           CHECK FOR APPLICATION SIGNAL                 
         BE    ENDREQ04                                                         
         MVC   MODE,ACMMODE        TO RESET MODE                                
         MVI   ACMMODE,0                                                        
         CLI   MODE,REQFRST        AND PROCESS REQUEST                          
         BE    INIREQ                                                           
                                                                                
ENDREQ04 TM    ACMINDS3,ACMIAGRP   TEST ACCOUNT GROUP REQUEST                   
         BNZ   NXTGRP                                                           
         TM    ACMINDS2,ACMISLAV   TEST SLAVE MODE                              
         BNZ   MASTX                                                            
         B     NXTREQ              ELSE IGNORE NEW MODE & GET NEXT REQ          
                                                                                
NXTGRP   LH    RF,ACMAGRPN         SET NEXT ACCOUNT GROUP CODE                  
         SHI   RF,1                                                             
         BNP   NXTREQ                                                           
         STH   RF,ACMAGRPN                                                      
         L     R3,ACMAGRPS                                                      
         AHI   R3,GRPTABL          POINT TO NEXT GROUP ENTRY                    
         ST    R3,ACMAGRPS                                                      
         B     INIREQ09            GO AND PROCESS NEXT GROUP                    
                                                                                
MASTX    TM    ACMINDS5,ACMIDDLK   TEST CALLED FROM DDLINK SERVER               
         JNZ   EXIT                                                             
         LA    R0,MCUTL                                                         
         LA    R1,UTLL                                                          
         LR    RF,R1                                                            
         LA    RF,UTL                                                           
         MVCL  R0,RE               RESTORE CALLER'S SAVED UTL                   
         J     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* HANDLE PROGRAM TERMINATION                                          *         
***********************************************************************         
                                                                                
RUNEND   MVI   MODE,RUNLAST        LAST TIME HOOK                               
         GOTOR GO                                                               
         CLI   ACMMODE,0           CHECK FOR APPLICATION SIGNAL                 
         BE    RUNEND02                                                         
         MVC   MODE,ACMMODE        TO RESET MODE                                
         MVI   ACMMODE,0                                                        
         CLI   MODE,REQFRST        AND PROCESS REQUEST                          
         BE    INIREQ                                                           
                                                                                
RUNEND02 GOTOR CLOFIL              CLOSE FILES ETC.                             
         CLI   MODE,PROCMANT       SPECIAL MAINTANCE MODE                       
         JNE   EXIT                                                             
         MVI   MODE,PROCMANT       MAINTANCE MODE                               
         GOTOR GO                                                               
         J     EXIT                RETURN TO CALLER                             
         DROP  RB                                                               
                                                                                
AGLOBALS DC    A(GLOBALS)                                                       
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO READ COMPANY RECORD                                      *         
***********************************************************************         
                                                                                
GETCPY   NTR1  BASE=*,LABEL=*                                                   
         MVC   KEY,SPACES                                                       
         MVC   KEY(CPYKEND),RCCOMPFL                                            
         GOTOR READ                                                             
         GOTOR GETCPYR                                                          
         MVC   RCSVCOMP,RCCOMPFL                                                
         GOTOR SETCPY              SET COMPANY ELEMENT ADDRESSES                
                                                                                
         L     RE,ADCMPNAM                                                      
         MVC   SVCPYNAM,0(RE)                                                   
                                                                                
         ST    R1,ACMAEOR          SAVE END OF RECORD ADDRESS                   
         L     RF,VEXTRAS                                                       
         MVC   AISDDS-RUNXTRAD(16,RF),ACMISDDS                                  
         L     R1,ADCMPEL                                                       
         MVC   ACMPROD,CPYPROD-CPYELD(R1)                                       
         MVC   ALPHAID,CPYALPHA-CPYELD(R1)                                      
         NI    ACMINDS,FF-(ACMINEWO)                                            
         TM    CPYSTAT4-CPYELD(R1),CPYSOFF2                                     
         BZ    GETCPY04                                                         
         OI    ACMINDS,ACMINEWO    SET NEW OFFICE SYSTEM IN USE                 
                                                                                
* GET SYSTEM CONTROL PROFILE--FOR ORIGIN NAME OVERRIDE, REPLACE                 
* COMPANY NAME ELEMENT WITH NEW ELEMENT CONTAINING ORIGIN NAME                  
                                                                                
GETCPY04 XC    ACMPFKEY,ACMPFKEY   GET SYSTEM CONTROL PROFILE                   
         MVI   ACMPFSYS,C'A'                                                    
         MVC   ACMPFPGM,=C'0000'                                                
         MVC   ACMPFAGY,ALPHAID                                                 
         GOTOR GETPROF,ACMPARA,ACMPFKEY,ACMWRK,(X'FF',DATAMGR),        *        
               (10,ACMAPRF)                                                     
         MVC   SYSPROF,ACMWRK                                                   
         TM    ACMINDS3,ACMIMULT   TEST MULTI COMPANY REQUEST                   
         BNZ   GETCPY10                                                         
*&&US*&& CLI   SYSPROF+2,YES       TEST ORIGIN NAME OVERRIDE                    
*&&US*&& BNE   GETCPY10                                                         
*&&UK*&& CLI   SYSPROF+2,NO                                                     
*&&UK*&& BE    GETCPY10                                                         
                                                                                
GETCPY06 XC    ACMWRK,ACMWRK                                                    
         LA    R2,ACMWRK           BUILD A NEW NAME ELEMENT                     
         USING NAMELD,R2                                                        
         MVI   NAMEL,NAMELQ                                                     
         MVC   NAMEREC(L'MCORIGIN),MCORIGIN                                     
         LA    RE,MCORIGIN+L'MCORIGIN-1                                         
         LHI   R1,L'MCORIGIN       FIND LENGTH OF ORIGIN NAME                   
         CLI   0(RE),C' '                                                       
         BH    GETCPY08                                                         
         BCTR  RE,0                                                             
         BCT   R1,*-10                                                          
         B     GETCPY10            NO ORIGIN NAME                               
                                                                                
GETCPY08 AHI   R1,NAMLN1Q                                                       
         STC   R1,NAMLN                                                         
         GOTOR ACMVHELO,ACMPARA,(C'D',ACMACSYS),('NAMELQ',ADCOMP),0             
         GOTOR (RF),(R1),(C'P',ACMACSYS),ADCOMP,NAMELD,0                        
         GOTOR SETCPY                                                           
         ST    R1,ACMAEOR          RESET EOR                                    
         AHI   R1,1                RESET ADUNIT IN CASE RECORD GREW             
         ST    R1,ADUNIT                                                        
                                                                                
GETCPY10 LA    R1,KEY              ATTACH BUFFER OF MEDIA ELEMENTS              
         USING PMDRECD,R1                                                       
         XC    PMDKEY,PMDKEY       WE NEED TO TACK ON MEDIA ELEMENTS            
         MVI   PMDKTYP,PMDKTYPQ                                                 
         MVC   PMDKCPY,RCCOMPFL                                                 
         GOTOR HIGH                                                             
         B     GETCPY14                                                         
GETCPY12 GOTOR SEQ                                                              
GETCPY14 CLC   KEY(PMDKMED-PMDRECD),KEYSAVE                                     
         BNE   GETCPYX                                                          
         GOTOR GETUNTR             (USE UNIT AREA FOR READING)                  
         L     RE,ADUNIT                                                        
         AH    RE,DATADISP                                                      
         L     R1,ACMAEOR                                                       
         SR    RF,RF                                                            
         IC    RF,1(RE)                                                         
         BCTR  RF,0                                                             
         EX    RF,*+8              EXTRACT ELEMENT                              
         B     *+10                                                             
         MVC   0(0,R1),0(RE)                                                    
         LA    R1,1(RF,R1)                                                      
         MVI   0(R1),0                                                          
         ST    R1,ACMAEOR                                                       
         AHI   R1,1                                                             
         ST    R1,ADUNIT           ADJUST UNIT ADDRESS                          
         B     GETCPY12                                                         
                                                                                
GETCPYX  J     EXIT                                                             
         DROP  R1,R2,RB                                                         
         EJECT                                                                  
***********************************************************************         
* SUB-ROUTINE TO BUILD DMDALINK PARAMETER LIST AND TO CALL DMDALINK   *         
* TO GET A RECORD                                                     *         
*                                                                     *         
* AT ENTRY, R3=A(DIRECTORY KEY)                                       *         
* ON EXIT, R3=A(RECORD) R2 IS CLOBBERED                               *         
***********************************************************************         
                                                                                
         USING TRNRECD,R3                                                       
DALINK   NTR1  BASE=*,LABEL=*                                                   
         LA    R2,ACMDMP1          ACCMST PARAMETER LISTS                       
         LA    R1,ACMDAP1                                                       
         TM    TRNKSTAT,TRNSARCH                                                
         BZ    *+12                                                             
         LA    R2,ACMDMP2          ACCARC PARAMETER LISTS                       
         LA    R1,ACMDAP2                                                       
         MVC   ACMDA,TRNKDA                                                     
         ST    R3,12(R2)           SET A(I/O AREA)                              
         ICM   R2,8,ACMEFFS        SET SPECIAL DALINK CALL                      
         GOTOR ACMDALNK                                                         
*&&UK                                                                           
         TM    ACMINDS5,ACMI2NDC   TEST SECOND CURRENCY REQUIRED                
         BZ    DALINKX                                                          
         L     RE,ADCMPEL                                                       
         MVC   WORK(L'CPYCURR),CPYCURR-CPYELD(RE)                               
         MVC   WORK+L'CPYCURR(L'CPYCURRS),CPYCURRS-CPYELD(RE)                   
         LA    R0,TOBAAOUT                                                      
         TM    CPYSTAT7-CPYELD(RE),CPYSSCNV TEST CONVERSION RUN                 
         BZ    *+8                                                              
         LA    R0,TOBAACVS                                                      
         GOTOR VTOBACCO,DMCB,((R0),WORK),TRNRECD,ADCOMFAC,0                     
*&&                                                                             
DALILNKX TM    8(R2),X'FF'         TEST FOR ANY ERROR                           
         JZ    EXIT                NO                                           
         DC    H'0'                                                             
         DROP  R3,RB                                                            
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO PASS SAVED MODES TO APPLICATION PROGRAM                  *         
* NTRY - R1=A(SAVED MODE LIST)                                        *         
*        R0=NUMBER OF MODES TO PASS                                   *         
***********************************************************************         
                                                                                
GOSAVE   ST    RE,ACMSVRE                                                       
GOSAVE02 MVC   MODE,0(R1)                                                       
         CLI   MODE,0                                                           
         JE    *+8                 NO MODE TO PASS                              
         GOTOR GO                                                               
         MVI   0(R1),0             TURN OFF SAVED MODE                          
         AHI   R1,1                NEXT MODE                                    
         BRCT  R0,GOSAVE02                                                      
GOSAVEX  L     RE,ACMSVRE                                                       
         BR    RE                                                               
                                                                                
***********************************************************************         
* ROUTINE TO PASS CONTROL TO APPLICATION PROGRAM WITH MODE SET        *         
***********************************************************************         
                                                                                
GO       NTR1  BASE=*,LABEL=*                                                   
         CLI   FCRESET,YES         TEST FOR KEY RESET FEATURE                   
         BNE   GO02                                                             
         CLI   MODE,PROCSPEC       NOTHING HAS BEEN DONE AT PROCSPEC            
         BE    GO02                                                             
                                                                                
         GOTOR DATAMGR,DMCB,DMKEY,ACMACDIR,ACMLKEY,ACMLKEY                      
                                                                                
GO02     CLI   MODE,LEVAFRST       PASS HIGH LEVELS ONLY WHEN                   
         BNE   *+12                WHEN WE HAVE ACCOUNT THAT QUALIFIES          
         MVI   ACMLLMDE+2,LEVALAST IF I PASS FIRST SAVE LAST                    
         B     GO04                                                             
         CLI   MODE,LEVBFRST                                                    
         BNE   *+12                                                             
         MVI   ACMLLMDE+1,LEVBLAST                                              
         B     GO04                                                             
         CLI   MODE,LEVCFRST                                                    
         BNE   GO04                                                             
         MVI   ACMLLMDE,LEVCLAST                                                
                                                                                
GO04     CLI   RCTRACE,YES                                                      
         BNE   GO08                                                             
         LA    R4,P                                                             
         LHI   R3,L'P                                                           
         L     R1,MCBXAREA         CHECK FOR BIG PRINT                          
         USING BOXD,R1                                                          
         ICM   RF,15,BOXWIDTH                                                   
         L     R1,VBIGPRNT                                                      
         USING BIGPRNTD,R1                                                      
         LA    R2,XSPACES                                                       
         CR    RF,R3               IS WIDTH MORE THAN L'P                       
         BNH   *+12                IF NOT USE P                                 
         LA    R4,XP               USING BIG PRINT                              
         LHI   R3,L'XP                                                          
                                                                                
         BCTR  R3,0                                                             
         EX    R3,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),0(R2)       SPACES TO PRINT LINE                         
         MVC   0(10,R4),=C'MODE=X''  '''                                        
         MVI   SKIPSPEC,YES                                                     
         GOTOR HEXOUT,ACMPARA,MODE,7(R4),1,=C'TOG'                              
         SR    RF,RF                                                            
         IC    RF,MODE                                                          
         BCTR  RF,R0                                                            
         SLL   RF,3                                                             
         A     RF,ACMAMDE                                                       
         MVC   20(8,R4),0(RF)                                                   
         LA    RE,30(R4)                                                        
         ICM   RF,15,ADHEIRA                                                    
         BZ    GO06                                                             
         MVC   0(2,RE),=C'A='                                                   
         MVC   2(L'ACTKACT,RE),ACTKACT-ACTRECD(RF)                              
         AHI   RE,L'ACTKACT+1                                                   
         CLI   0(RE),C' '                                                       
         BH    *+8                                                              
         BCT   RE,*-8                                                           
         ICM   RF,15,ADHEIRB                                                    
         BZ    GO06                                                             
         MVC   1(3,RE),=C',B='                                                  
         MVC   4(L'ACTKACT,RE),ACTKACT-ACTRECD(RF)                              
         AHI   RE,L'ACTKACT+3                                                   
         CLI   0(RE),C' '                                                       
         BH    *+8                                                              
         BCT   RE,*-8                                                           
         ICM   RF,15,ADHEIRC                                                    
         BZ    GO06                                                             
         MVC   1(3,RE),=C',C='                                                  
         MVC   4(L'ACTKACT,RE),ACTKACT-ACTRECD(RF)                              
         AHI   RE,L'ACTKACT+3                                                   
         CLI   0(RE),C' '                                                       
         BH    *+8                                                              
         BCT   RE,*-8                                                           
         ICM   RF,15,ADHEIRD                                                    
         BZ    GO06                                                             
         MVC   1(3,RE),=C',D='                                                  
         MVC   4(L'ACTKACT,RE),ACTKACT-ACTRECD(RF)                              
                                                                                
GO06     GOTOR ACREPORT                                                         
                                                                                
GO08     L     RF,VEXTRAS                                                       
         MVC   VLISTREC-RUNXTRAD(L'VLISTREC,RF),ADLSTREC                        
                                                                                
         CLC   MODE,FCJOBBER       TEST JOBBER MODE                             
         BNE   GO10                                                             
         OC    ACMAJCSP,ACMAJCSP   TEST ANY COLUMN SPECS FOUND                  
         BZ    GO10                                                             
         L     R2,ACMAJOBB                                                      
         USING JBLOCKD,R2          CALL JOBBER TO GET ESTIMATE VALUES           
         MVC   JBAJOB,ADACC                                                     
         MVC   JBACOLS,ACMACOLL                                                 
         MVC   JBACOM,ADCOMFAC                                                  
         MVC   JBAGOBLK,ADGOBLOC                                                
         MVC   JBAIO,ACMAJOBI                                                   
         XC    JBASCH,JBASCH                                                    
         MVC   JBAKEY,LASTIO                                                    
         MVC   JBGETOPT,GETOPT                                                  
         MVC   JBACOLTB,ACMACOL                                                 
         MVC   JBLCOLTB,ACMLCOL                                                 
         MVC   JBAOPVTB,ACMAOPV                                                 
         MVC   JBLOPVTB,ACMLOPV                                                 
         MVC   BYTE,JBSELFUN        JBGET1R FROM SCRIBE                         
         NI    BYTE,JBGET1R                                                     
         MVI   JBSELFUN,JBGETDE     FOR MCS GET DETAILS ONLY                    
         TM    ACMINDS5,ACMIJMSE                                                
         BZ    *+8                                                              
         OI    JBSELFUN,JBGETSE     IF REQUESTED CHECK 'SELECTED'               
         OC    JBSELFUN,BYTE                                                    
         MVC   JBORICLI,ACMAJCSP    SET ORIGINAL COLUMN LIST                    
*&&UK                                                                           
         USING COMFACSD,R1                                                      
         L     R1,ADCOMFAC                                                      
         MVC   JBCSHVAL,CCASHVAL                                                
         DROP  R1                                                               
*&&                                                                             
         GOTOR ACMAJOBR,ACMPARA,JBLOCKD                                         
         CLI   JBERROR,0                                                        
         BE    *+6                                                              
         DC    H'0'                DIE ON JOBBER ERROR                          
         DROP  R2                                                               
                                                                                
GO10     GOTOR ACAPPLIC,ACMPARA,ACWORKD                                         
*&&US                                                                           
         CLI   FCPRORAT,C'B'       TEST BILLING INFO BLOCK BUILT                
         BNE   GO12                NO - WRITE PTA ELEMENTS TO RECOR             
         CLI   MODE,PROCTRNS                                                    
         BE    *+12                                                             
         CLI   MODE,WRITRANS                                                    
         BNE   GO12                                                             
         GOTOR CNVPTA              CONVERT TO PTAEL'S                           
GO12     DS    0H                                                               
*&&                                                                             
         CLI   RCWRITE,YES         CHECK IF WRITE = NO OPTION IS ON             
         BNE   GOX                                                              
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO WRITE BACK LEDGER RECORD                                 *         
***********************************************************************         
                                                                                
PUTLDG   CLI   MODE,WRITLEDG                                                    
         BNE   PUTLDGX                                                          
         L     RE,ADLEDGER                                                      
         MVC   KEY(L'ACTKCULA),0(RE)                                            
         L     R0,ACMATRN                                                       
         LHI   R1,ACMMAXRL                                                      
         LR    RF,R1                                                            
         MVCL  R0,RE               SAVE LEDGER RECORD IN TRANS AREA             
         GOTOR READ                                                             
         GOTOR GETLDGR             RE-READ OLD VERSION                          
                                                                                
         L     RE,ADLEDGER         EXTRACT LEDGER RECORD LENGTH                 
         MVC   ACMDUB(2),LDGRLEN-LDGRECD(RE)                                    
         LR    R0,RE                                                            
         L     RE,ACMATRN                                                       
         LH    R1,ACMDUB                                                        
         LR    RF,R1                                                            
         MVCL  R0,RE               RESTORE NEW VERSION                          
         L     RE,ADLEDGER                                                      
         AH    RE,ACMDUB                                                        
         BCTR  RE,0                                                             
         MVI   0(RE),0             SET END OF RECORD INDICATOR                  
         GOTOR PUTACTR                                                          
         B     GOX                                                              
PUTLDGX  DS    0H                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO WRITE BACK MEDIA RECORD                                  *         
***********************************************************************         
                                                                                
PUTMED   CLI   MODE,WRITMEDS                                                    
         BNE   PUTMEDX                                                          
         SR    R0,R0                                                            
         L     R1,ADCOMP                                                        
         AH    R1,DATADISP                                                      
PUTMED02 CLI   0(R1),0             TEST E-O-R                                   
         BE    PUTMEDX                                                          
                                                                                
         USING PMDELD,R1                                                        
         CLI   PMDEL,PMDELQ                                                     
         BNE   PUTMED04                                                         
         LA    RF,KEY                                                           
         USING PMDRECD,RF                                                       
         XC    PMDKEY,PMDKEY       BUILD KEY OF MEDIA RECORD                    
         MVI   PMDKTYP,PMDKTYPQ                                                 
         MVC   PMDKCPY,RCCOMPFL                                                 
         MVC   PMDKMED,PMDCODE                                                  
         DROP  RF                                                               
         GOTOR HIGH                                                             
         CLC   KEY(PMDKEND),KEYSAVE TEST MEDIA RECORD FOUND                     
         BE    *+6                                                              
         DC    H'0'                                                             
         GOTOR GETUNTR                                                          
                                                                                
         L     RE,ADUNIT                                                        
         AH    RE,DATADISP                                                      
         SR    RF,RF                                                            
         IC    RF,1(RE)                                                         
         BCTR  RF,0                RF=L'ELEMENT-1                               
         EX    RF,*+8                                                           
         B     *+10                                                             
         CLC   0(0,RE),0(R1)       TEST ELEMENT CHANGED                         
         BE    PUTMED04                                                         
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,RE),0(R1)       MOVE IN NEW ELEMENT & WRITE BACK             
         GOTOR PUTACTR                                                          
                                                                                
PUTMED04 IC    R0,1(R1)            BUMP TO NEXT MEDIA ELEMENT                   
         AR    R1,R0                                                            
         B     PUTMED02                                                         
PUTMEDX  DS    0H                                                               
         EJECT                                                                  
***********************************************************************         
* CONTROL WRITING OF ACCOUNTS AND TRANSACTIONS                        *         
***********************************************************************         
                                                                                
PUTANY   CLI   MODE,WRITACC        DOES USER WANT TO WRITE                      
         BE    PUTANY04            BACK THE LAST ACCOUNT                        
         CLI   MODE,WRITLEVA                                                    
         BE    PUTANY02                                                         
         CLI   MODE,WRITLEVB                                                    
         BE    PUTANY02                                                         
         CLI   MODE,WRITLEVC                                                    
         BE    PUTANY02                                                         
         CLI   MODE,WRITRANS       DOES USER WANT TO UPDATE                     
         BNE   GOX                 THE LAST TRANSACTION                         
         GOTOR PUTTRNR                                                          
         B     GOX                                                              
                                                                                
PUTANY02 GOTOR PUTACTR                                                          
         GOTOR READ                                                             
         B     GOX                                                              
                                                                                
PUTANY04 GOTOR PUTACTR                                                          
                                                                                
GOX      CLI   FCRESET,YES         TEST TO RESET IS KEY                         
         JNE   EXIT                                                             
         CLI   MODE,PROCSPEC                                                    
         JE    EXIT                                                             
                                                                                
         OC    ACMLKEY(L'ACCKEY),ACMLKEY                                        
         JZ    EXIT                                                             
         GOTOR DATAMGR,DMCB,DMKEY,ACMACDIR,DMWORK,DMWORK                        
         CLC   ACMLKEY(L'ACCKEY),DMWORK                                         
         JE    EXIT                                                             
         GOTOR ACMISDDS,ACMPARA,ISREAD,ACMABUF2,0,ACMADDIR,ACMLKEY,0            
         OC    8(2,R1),8(R1)       TEST FOR ERRORS                              
         JZ    EXIT                                                             
         TM    9(R1),X'04'         TEST FOR EOF                                 
         BO    *+6                 YES                                          
         DC    H'0'                DUMP ON DISK ERROR                           
         J     EXIT                                                             
         DROP  R1,RB                                                            
         EJECT                                                                  
***********************************************************************         
* DATAMGR INTERFACE TO FILE                                           *         
***********************************************************************         
                                                                                
HIGHRTRN NTR1  ,                                                                
         LA    RF,DMRDHI                                                        
         OI    ACMINDS3,ACMIRTRN                                                
         J     GODM                                                             
                                                                                
READRTRN NTR1  ,                                                                
         LA    RF,DMREAD                                                        
         OI    ACMINDS3,ACMIRTRN                                                
         J     GODM                                                             
                                                                                
READ     NTR1  ,                                                                
         LA    RF,DMREAD                                                        
         J     GODM                                                             
                                                                                
HIGH     NTR1  ,                                                                
         LA    RF,DMRDHI                                                        
         MVC   KEYSAVE,KEY                                                      
         J     GODM                                                             
                                                                                
SEQ      NTR1  ,                                                                
         MVC   KEYSAVE,KEY                                                      
SEQSKIP  SR    RF,RF               THIS CODE ALLOWS PSEUDO READ                 
         IC    RF,KEY+14           SEQUENTIAL. TRANSACTIONS WILL                
         AHI   RF,1                BE OMITTED AS WE SKIP FROM                   
         STC   RF,KEY+14           ACCOUNT TO ACCOUNT                           
         LA    RF,DMRDHI                                                        
                                                                                
GODM     MVC   ACMTRACE,KEY                                                     
         L     R2,ACMABUF                                                       
         XC    0(L'ACTKEY,R2),0(R2)                                             
         CLI   KEY+14,0                                                         
         JE    *+10                                                             
         MVC   0(L'ACTKEY,R2),SPACES                                            
         MVC   0(L'ACTKCULA,R2),KEY                                             
         GOTOR DATAMGR,DMCB,(X'88',(RF)),ACMACFIL,(R2),(R2)                     
         MVC   KEY,0(R2)                                                        
         L     RF,DMCB                                                          
         CLC   DMRDHI,0(RF)        TEST DMRDHI                                  
         JE    *+14                                                             
         CLC   DMREAD,0(RF)        TEST DMREAD                                  
         JNE   GODM02                                                           
         TM    ACMINDS3,ACMIRTRN   TEST CALLER WANTS CONTROL BACK               
         JZ    GODM02                                                           
         NI    ACMINDS3,FF-(ACMIRTRN)                                           
         CLI   RCTRACE,YES         TEST TRACE OPTION ACTIVE                     
         JNE   *+8                                                              
         GOTOR DMTRACE                                                          
*&&UK*&& J     GODMX                                                            
*&&US*&& J     EXIT                CALLER MUST CHECK DMCB+8 FOR ERROR           
                                                                                
GODM02   CLC   KEY(UNTKEND),KEYSAVE                                             
         JE    *+12                                                             
         MVI   DMCB+8,0                                                         
         J     DMCHECK                                                          
         TM    ACTRSTA-ACTRECD(R2),ACTSDELT                                     
         JO    SEQSKIP             SKIP DELETED RECORDS                         
         J     DMCHECK                                                          
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO PUT ACCOUNT RECORDS                                      *         
***********************************************************************         
                                                                                
PUTACTR  NTR1  ,                                                                
         LA    R1,LASTIO           ADDRESS LAST IO DETAILS                      
         CLI   MODE,WRITLEVA                                                    
         JNE   *+8                                                              
         LA    R1,LEVAIO                                                        
         CLI   MODE,WRITLEVB                                                    
         JNE   *+8                                                              
         LA    R1,LEVBIO                                                        
         CLI   MODE,WRITLEVC                                                    
         JNE   *+8                                                              
         LA    R1,LEVCIO                                                        
         L     RF,0(R1)                                                         
         GOTOR DATAMGR,DMCB,DMWRT,ACMACFIL,(RF),(RF)                            
         J     DMCHECK                                                          
                                                                                
*&&US                                                                           
***********************************************************************         
* ROUTINE TO CALL PRORATA TO CONVERT PTAEL'S                          *         
***********************************************************************         
                                                                                
CNVPTA   NTR1  ,                                                                
         L     R2,ADTRANS                                                       
         SH    R2,DATADISP                                                      
         GOTOR ACMAPRAT,ACMPARA,(X'A0',(R2)),ADGOBLOC,ADCOMFAC,0,      *        
               ACMAPROB,ACMAPRO2                                                
         J     EXIT                                                             
*&&                                                                             
                                                                                
***********************************************************************         
* ROUTINE TO PUT TRANSACTION RECORDS                                  *         
***********************************************************************         
                                                                                
         USING TRNRECD,R2                                                       
PUTTRNR  NTR1  ,                                                                
         L     R2,ADTRANS                                                       
         SH    R2,DATADISP                                                      
         MVC   ACMDUB+0(2),TRNRLEN                                              
         L     RE,ACMABUF                                                       
         MVC   ACMDUB+2(2),TRNRLEN-TRNRECD(RE)                                  
         LR    R0,RE               MOVE TRANSACTION TO BUFFER AREA              
         LHI   R1,ACMMAXRL                                                      
         LA    RE,TRNRECD                                                       
         SR    RF,RF                                                            
         ICM   RF,3,TRNRLEN                                                     
         MVCL  R0,RE                                                            
         GOTOR DATAMGR,DMCB,DMWRT,ACMACFIL,TRNRECD,TRNRECD                      
         TM    ACMINDS,ACMIEMUD    TEST EMULATED FILE                           
         JNZ   DMCHECK                                                          
         CLC   ACMDUB(2),ACMDUB+2  TEST RECORD LENGTH CHANGED                   
         JE    DMCHECK                                                          
         GOTOR (RF),(R1),(X'88',DMREAD)                                         
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* TEST FOR AND REPORT ON DATAMGR ERRORS                               *         
***********************************************************************         
                                                                                
DMCHECK  CLI   DMCB+8,0            TEST FOR DATAMGR ERRORS                      
         JE    DMCHECK1                                                         
         TM    DMCB+8,X'02'        DELETED RECORD?                              
         JZ    DMCHECK2            NO SO GO TO ERROR                            
         TM    DMCB+0,X'08'        READ FOR DELETED?                            
         JZ    DMCHECK2            NO SO GO TO ERROR                            
                                                                                
DMCHECK1 CLI   RCTRACE,YES         TEST TRACE OPTION ON                         
         JNE   *+8                                                              
         GOTOR DMTRACE             YES - TRACE THE CALL                         
*&&UK                                                                           
         L     RF,DMCB                                                          
         CLC   DMREAD(3),0(RF)     TEST READ(SEQ/HIGH) ISSUED                   
         JE    GODMX                                                            
*&&                                                                             
         J     EXIT                                                             
                                                                                
DMCHECK2 CLI   RCTRACE,YES         TEST TRACE OPTION ON                         
         JNE   DMCHECKX            NO - IGNORE ERROR                            
         MVC   ACMWRK(L'DMERR1),DMERR1                                          
         J     DMCHECK6                                                         
                                                                                
DMCHECK4 MVC   ACMWRK(L'DMERR2),DMERR2                                          
                                                                                
DMCHECK6 LHI   R0,L'DMERR1                                                      
         GOTOR LOGIO,ACMPARA,1,((R0),ACMWRK)                                    
         MVC   ACMWRK(L'DMERR3),DMERR3                                          
         UNPK  ACMWRK+19(4),RCRQTOT                                             
         OI    ACMWRK+22,X'F0'                                                  
         BASR  RE,RF                                                            
         MVC   ACMWRK(L'DMERR3),SPACES                                          
         BASR  RE,RF                                                            
         GOTOR DMTRACE                                                          
         MVI   MODE,DISKERR        TELL APPLICATION PROGRAM THE NEWS            
         GOTOR GO                                                               
         CLI   MODE,GOTONXTR       USER CAN OPT TO GO ONTO NEXT REQUEST         
         JNE   EXIT                THE DEFAULT VALUE IS TO IGNORE THEM          
                                                                                
DMCHECKX L     RD,4(,RD)           UNWIND WITHOUT EXIT                          
         CLC   =C'FILC',0(RD)      MATCH ON MONACC LABLE                        
         JE    ENDREQ              GOT ENDREQ RB NOW                            
         LM    RE,RC,12(RD)                                                     
         J     DMCHECKX                                                         
                                                                                
*&&UK                                                                           
GODMX    TM    ACMINDS5,ACMI2NDC   TEST SECOND CURRENCY REQUIRED                
         JZ    EXIT                                                             
         L     RE,ADCMPEL                                                       
         MVC   WORK(L'CPYCURR),CPYCURR-CPYELD(RE)                               
         MVC   WORK+L'CPYCURR(L'CPYCURRS),CPYCURRS-CPYELD(RE)                   
         LA    R0,TOBAAOUT                                                      
         TM    CPYSTAT7-CPYELD(RE),CPYSSCNV TEST CONVERSION RUN                 
         JZ    *+8                                                              
         LA    R0,TOBAACVS                                                      
         GOTOR VTOBACCO,DMCB,((R0),WORK),('TOBAIOLD',ACMABUF),         *        
               ADCOMFAC,0                                                       
         J     EXIT                                                             
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* TRACE A DATAMGR CALL                                                *         
***********************************************************************         
                                                                                
DMTRACE  NTR1  ,                                                                
         LA    R1,DMCB             R1=PARM LIST FOR FILE READS                  
         TM    ACMHIST,ACMHPRC     PROCESSING HISTORY                           
         JZ    *+8                                                              
         LA    R1,ACMHPRM          R1=PARM LIST FOR HISTORY READS               
         ST    R1,ACMIOPRM         SAVE ADDRESS OF PARM LIST                    
         LA    R0,DMRDHI                                                        
         CLC   0(4,R1),=A(ISREAD)  TEST FOR ISDDS CALL                          
         JE    DMTRACE2            YES                                          
         LA    R0,DMRSEQ                                                        
         CLC   0(4,R1),=A(ISRDSEQ)                                              
         JNE   DMTRACE4            NO                                           
                                                                                
* BUILD A DATAMGR PARAMETER LIST FOR AN ISDDS CALL                              
                                                                                
DMTRACE2 L     RE,4(R1)            A(KEY)                                       
         L     RF,16(R1)           A(IO AREA)                                   
         MVC   8(1,R1),9(R1)       SHIFT ISDDS INDICATORS                       
         ST    R0,0(R1)            SET COMMAND                                  
         LA    R0,ACMACFIL                                                      
         TM    ACMINDS,ACMIEMUD    TEST NEW FILE                                
         JZ    *+8                                                              
         LA    R0,ACMACDIR                                                      
         TM    ACMHIST,ACMHPRC                                                  
         JZ    *+8                                                              
         LA    R0,ACMACHST                                                      
         ST    R0,4(R1)            SET FILE NAME                                
         STCM  RE,7,9(R1)          A(KEY)                                       
         ST    RF,12(R1)           A(IO AREA)                                   
                                                                                
DMTRACE4 LA    R4,P                                                             
         LHI   R3,L'P                                                           
         L     R1,MCBXAREA         CHECK FOR BIG PRINT                          
         USING BOXD,R1                                                          
         ICM   RF,15,BOXWIDTH                                                   
         L     R1,VBIGPRNT                                                      
         USING BIGPRNTD,R1                                                      
         LA    R2,XSPACES                                                       
         CR    RF,R3               IS WIDTH MORE THAN L'P                       
         JNH   *+12                IF NOT USE P                                 
         LA    R4,XP               USING BIG PRINT                              
         LHI   R3,L'XP                                                          
         BCTR  R3,0                                                             
         BASR  RF,0                                                             
         EX    R3,8(RF)                                                         
         J     *+10                                                             
         MVC   0(0,R4),0(R2)       SPACES TO PRINT LINE                         
         L     R1,ACMIOPRM                                                      
         LM    RE,RF,0(R1)                                                      
         MVC   00(7,R4),0(RE)      COMMAND                                      
         MVC   08(7,R4),0(RF)      FILE                                         
         ICM   R0,15,MCACTIOS                                                   
         CVD   R0,ACMDUB                                                        
         OI    ACMDUB+L'ACMDUB-1,X'0F'                                          
         UNPK  P+16(6),ACMDUB                                                   
         GOTOR HEXOUT,ACMPARA,ACMTRACE,23(R4),24,=C'TOG'                        
         L     RE,ACMIOPRM                                                      
         L     R0,12(RE)                                                        
         GOTOR (RF),(R1),(R0),76(R4),24                                         
         GOTOR (RF),(R1),DMCB+8,130(R4),1                                       
         MVI   SKIPSPEC,YES                                                     
         GOTOR ACREPORT                                                         
         J     EXIT                                                             
                                                                                
EXITLO   LHI   R0,0                                                             
         J     EXITCC                                                           
EXITEQ   LHI   R0,1                                                             
         J     EXITCC                                                           
EXITHI   LHI   R0,2                                                             
                                                                                
EXITCC   CHI   R0,1                                                             
                                                                                
EXIT     XIT1  ,                                                                
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO INITIALIZE RUN CONTROL VALUES                            *         
***********************************************************************         
                                                                                
SETRUN   NTR1  BASE=*,LABEL=*                                                   
         TM    ACMINDS5,ACMIDDLK   TEST CALLED FROM DDLINK SERVER               
         BNZ   SETRUN02                                                         
         BASR  RE,0                                                             
         AHI   RE,ENDREQ-*                                                      
         STM   R0,RF,MCREQLST                                                   
         BASR  RE,0                                                             
         AHI   RE,RUNEND-*                                                      
         STM   R0,RF,MCRUNLST                                                   
                                                                                
         TM    ACMINDS2,ACMISLAV   TEST SLAVE MODE                              
         BNZ   SETRUN02                                                         
         MVI   MCRQCOL,MCRQCMLT    SET MULTI-COLUMN DEFINITION                  
         MVI   MCRQCOL1,ACQCONT1+1-ACQCARD1                                     
         MVI   MCRQCOL2,ACQCONT2+1-ACQCARD2                                     
         MVI   MCRQCOL3,ACQCONT3+1-ACQCARD3                                     
         MVI   MCRQCOL4,ACQCONT4+1-ACQCARD4                                     
         MVI   MCRQCOL5,ACQCONT5+1-ACQCARD5                                     
         MVI   MCRQCOL6,ACQCONT6+1-ACQCARD6                                     
         MVI   MCRQCOL7,ACQCONT7+1-ACQCARD7                                     
         MVI   MCRQCHAR,QCNTQ      AND CONTINUATION CHARACTER                   
         GOTOR MCVRUNST            CALL MASTER TO READ CONTROL CARDS            
                                                                                
SETRUN02 L     RF,VEXTRAS                                                       
         USING RUNXTRAD,RF                                                      
         MVC   ORIGINAM,MCORIGIN                                                
         MVC   ORIGINAD,MCORIGAD                                                
         DROP  RF                                                               
         MVC   RCCTRY,MCCTRY       SET COUNTRY/LANGUAGE/CURRENCY                
         CLI   RCCTRY,0            TEST COUNTRY SET                             
         BNE   *+8                                                              
*&&UK*&& MVI   RCCTRY,CTRYGBR      NO - SET DEFAULT COUNTRY CODE                
*&&US*&& MVI   RCCTRY,CTRYUSA                                                   
         CLI   RCCTRY,CTRYSCA      CONVERT SCANDINAVIA INTO UK                  
         BNE   *+8                                                              
         MVI   RCCTRY,CTRYGBR                                                   
*&&UK*&& MVC   RCOUNTRY,=C'UK'                                                  
*&&US*&& MVC   RCOUNTRY,=C'US'                                                  
*&&UK*&& MVI   ACMSYS,ACMSEUR                                                   
*&&US*&& MVI   ACMSYS,ACMSUSA                                                   
         MVC   RCCURR,MCAGCURR                                                  
         MVC   RCLANG,MCLANG       SET/SAVE LANGUAGE CODE                       
         MVC   RCLANGSV,MCLANG                                                  
         MVC   RCTRACE,MCTRACE                                                  
         MVC   RCDUMP,MCDUMP                                                    
         MVC   RCWRITE,MCWRITE                                                  
         MVC   RCINPUT,MCINPUT                                                  
         MVC   RCPOSTNG,MCPOSTNG                                                
         MVC   RCDATE,MCDATE                                                    
         MVC   RCFFPARM,MCFFPARM                                                
         MVC   RCLDGFLT,MCACLDGE                                                
         MVC   RCRERUN,MCRERUN                                                  
         MVC   ORIGINUM,MCORIGID                                                
         MVC   RCJOB,MCJOB         SET CURRENT MVS JOB NAME                     
         MVC   ALPHAID,MCUSER                                                   
         CLI   MCMAXLIN,0          TEST MAXLINES= CONTROL CARD READ             
         BE    *+10                                                             
         MVC   MAXLINES,MCMAXLIN   YES - SET MAXLINES VALUE                     
         TM    ACMINDS2,ACMISLAV   TEST SLAVE MODE                              
         BZ    SETRUN04                                                         
                                                                                
         MVC   VACCEMU,MCVACEMU    EXTRACT NON-LINKED ADDRESSES                 
         MVC   CARDS,MCVCARDS                                                   
         MVC   DATAMGR,MCVDMGR                                                  
         MVC   DATCON,MCVDTCON                                                  
         MVC   HEXOUT,MCVHEXOU                                                  
         MVC   ADSQUASH,MCVSQSHR                                                
         MVC   LOADER,MCVLOADR                                                  
         MVC   LOGIO,MCVLOGIO                                                   
         MVC   LOGO,MCVLOGO                                                     
         MVC   LOGOC,MCVLOGOC                                                   
         MVC   REMOTEC,MCVREMOT                                                 
         MVC   ADDICTAT,MCVDICT                                                 
         MVC   ACMISDDS,MCVISDDS                                                
         MVC   ACMDALNK,MCVDALNK                                                
         MVC   ACMNQDQ,MCVNQDQ                                                  
                                                                                
         USING COMFACSD,RF                                                      
         L     RF,ADCOMFAC                                                      
         MVC   CDATAMGR,DATAMGR                                                 
         MVC   CDICTATE,ADDICTAT                                                
         DROP  RF                                                               
                                                                                
         TM    ACMINDS5,ACMIDDLK                                                
         BNZ   SETRUN06                                                         
         MVC   RCCOMPFL,MCS2AGYB   ASSUME SECOND SYSTEM IS ACCOUNT              
         L     RF,ADPARM           INITIALISE FOR SLAVE MODE                    
         L     RF,4(RF)                                                         
         MVC   RCPROG,0(RF)        SET APPLICATION PROGRAM NUMBER               
                                                                                
         MVC   PROG01,RCPROG                                                    
         MVC   MCDUB,PROG01                                                     
         GOTOR MCVLOADM,DMCB,0                                                  
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   ACSPECS,4(R1)                                                    
                                                                                
         MVC   PROG02,RCPROG                                                    
         MVC   MCDUB,PROG02                                                     
         GOTOR MCVLOADM,DMCB,0                                                  
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   ACAPPLIC,4(R1)                                                   
         BNZ   SETRUN08                                                         
                                                                                
SETRUN04 MVC   ACSPECS,MCAPHAS1    TRANSFER MASTER VALUES TO GLOBAL W/S         
         MVC   RCPROG,MCPROG                                                    
         MVC   RCPHASE,MCACPHSE                                                 
         MVC   RCSPECNO,MCNUMBER                                                
         MVC   ACAPPLIC,MCAPHAS2                                                
         MVC   INFILE,MCACIFIL                                                  
         MVC   OUTFILE,MCACOFIL                                                 
         MVC   WORKFILE,MCACWFIL                                                
         MVC   UTL+4(1),MCIDSENO   SET SE NUMBER IN UTL                         
                                                                                
SETRUN06 MVC   RCCOMPFL,MCIDAGYB                                                
                                                                                
SETRUN08 MVC   RCPROGGP,RCPROG     SET PROGRAM FOR GETPROF CALL                 
         LA    R1,SRADJLST         R1=A(QPROG ADJUSTMENT TABLE)                 
         LHI   R0,SRADJNUM         R0=NUMBER OF TABLE ENTRIES                   
         LTR   R0,R0                                                            
         BZ    SETRUN10                                                         
         CLC   RCPROG,0(R1)        MATCH PROGRAM TO TABLE ENTRY                 
         BE    *+16                                                             
         AHI   R1,L'SRADJLST                                                    
         BCT   R0,*-14                                                          
         B     *+16                                                             
         MVC   RCPROG,2(R1)        SET PROGRAM FROM TABLE                       
         MVC   RCPROGGP,4(R1)      SET GETPROG PROGRAM VALUE                    
                                                                                
SETRUN10 CLI   ACMSYS,ACMSEUR      EUROPE ?                                     
         BNE   SETRUN12            NO                                           
*&&UK                                                                           
         MVI   ACMFIND,2           DEFAULT IS PRINT ONLY                        
         MVC   PRINT,VPPGPRT                                                    
         CLC   MCACFCHE,SPACES     SET FICHE VALUES                             
         BE    SETRUN12                                                         
         CLI   MCACFCHE,C'B'       TEST 'BOTH'                                  
         BNE   *+14                                                             
         MVC   PRINT,VPRINTF                                                    
         MVI   ACMFIND,3                                                        
         CLI   MCACFCHE,C'C'       TEST 'COM'                                   
         BNE   *+14                                                             
         MVI   ACMFIND,1                                                        
         MVC   PRINT,VPRINTF                                                    
*&&                                                                             
SETRUN12 DS    0H                                                               
*&&UK                                                                           
         USING COMCTRLD,RF         RF=A(FICHE CONTROL BLOCK)                    
         L     RF,VCOMC                                                         
         MVI   COMSTART,0          SET FICHE NOT OPEN                           
         MVC   COMIO,ACMFIND       SET FICHE OUTPUT TYPE                        
         DROP  RF                                                               
*&&                                                                             
         TM    ACMINDS2,ACMISLAV   DON'T FUSS WITH REMOTEC IF SLAVED            
         BNZ   SETRUN14                                                         
         CLI   ACMSYS,ACMSEUR                                                   
         BNE   SETRUN14                                                         
         L     RF,REMOTEC          SET UP REPORT TITLE 'APP,FORMS'              
         USING REMOTED,RF                                                       
         OC    REMOTKEY,REMOTKEY                                                
         BZ    SETRUN14                                                         
         CLI   REMOTFLG,X'FA'      TEST IF NEW STYLE LAYOUT                     
         BNL   SETRUN14            YES MASTER HAS DONE THE BUSINESS             
         MVC   REMOTFRM,REMOTKEY   NO THEN TIDY UP OLD FIELDS                   
         MVI   REMOTSYS,C'A'                                                    
         MVC   REMOTPRG,RCPROG                                                  
         MVI   REMOTKEY+3,C','                                                  
         DROP  RF                                                               
                                                                                
SETRUN14 TM    ACMINDS5,ACMIDDLK   TEST CALLED FROM DDLINK SERVER               
         BNZ   SETRUNX             YES                                          
                                                                                
         MVC   MCDUB,SPACES        LOAD TSAROFF                                 
         MVC   MCDUB(4),=C'T00A'                                                
         MVI   WORK,QTSAROFF                                                    
         GOTOR HEXOUT,DMCB,WORK,MCDUB+4,1                                       
         GOTOR MCVLOADM,DMCB,0                                                  
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   ACMVTSAR,4(R1)                                                   
                                                                                
         MVC   MCDUB,SPACES        LOAD SOFDAT                                  
         MVC   MCDUB(4),=C'T00A'                                                
         MVI   WORK,QSOFDAT                                                     
         GOTOR HEXOUT,DMCB,WORK,MCDUB+4,1                                       
         GOTOR MCVLOADM,DMCB,0                                                  
         BE    *+6                                                              
         DC    H'0'                                                             
         L     RF,ADCOMFAC                                                      
         MVC   CSOFDAT-COMFACSD(,RF),4(R1)                                      
         MVC   ACMSOFT,4(R1)                                                    
                                                                                
SETRUNX  J     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO RESOLVE SOFT DATE EXPRESSIONS IN REQUEST CARDS           *         
***********************************************************************         
                                                                                
         USING SOFDATD,R1                                                       
RESDAT   NTR1  BASE=*,LABEL=*                                                   
         MVC   SVCPY,QCOMPANY                                                   
         GOTOR DATCON,ACMPARA,(4,RCDATE),(0,ACMDUB)                             
         LA    R1,ACMWRK                                                        
         XC    SOFDATD(SOFDATL),SOFDATD                                         
         MVC   SOFACOM,ADCOMFAC                                                 
         L     RE,ADCMPEL                                                       
         MVC   SOFACFST,CPYSFST-CPYELD(RE)                                      
         MVC   SOFTODAY,ACMDUB                                                  
         MVI   SOFSYSN,X'06'       SET ACC SYSTEM                               
         L     RF,ACMSOFT          RF=A(SOFDAT)                                 
                                                                                
         USING DATTABD,R2          R2=A(DATE TABLE)                             
         LA    R2,DATTAB           RESOLVE SOFT DATES                           
RESDAT02 SR    RE,RE                                                            
         ICM   RE,3,DATDDSP                                                     
         BZ    RESDAT16                                                         
         A     RE,ADQSTACK         RE=A(DATE FIELD)                             
         MVI   SOFIINDS,0          CLEAR INDICATOR BYTE                         
                                                                                
         TM    DATINDS,DATIDUE     TEST DUE DATE FILTER                         
         BZ    *+12                                                             
         CLI   QAPFLAG,QAPFDUE     TEST QAPFLAG VALUE                           
         BNE   RESDAT14                                                         
                                                                                
         USING ACQTYP1,RE                                                       
         TM    DATINDS,DATIFLT     TEST SCRIBE DATE FILTER                      
         BZ    RESDAT04                                                         
         CLI   ACQTYP1,ACQDATE     TEST FILTER TYPE IS DATE                     
         BNE   RESDAT14                                                         
         LA    RE,ACQDTSTR         POINT TO START DATE                          
                                                                                
RESDAT04 LHI   R0,4                                                             
         TM    DATINDS,DATIYMO     TEST YEAR/MONTH ONLY                         
         BO    *+8                                                              
         LHI   R0,6                                                             
                                                                                
         TM    0(RE),X'F0'         TEST SOFT START DATE                         
         BZ    RESDAT06                                                         
         TM    DATINDS,DATITWO     TEST PERIOD (TWO DATES)                      
         BZ    RESDAT14            NO SOFT DATE TO PROCESS                      
         AR    RE,R0                                                            
         OI    SOFIINDS,SOFIIONE   SET RESOLVE SINGLE DATE                      
         TM    0(RE),X'F0'         TEST SOFT END DATE                           
         BNZ   RESDAT14                                                         
         DROP  RE                                                               
                                                                                
RESDAT06 MVI   SOFITYPE,SOFITSD1   SET YYMM ONLY                                
         MVI   SOFOTYPE,SOFITSD1                                                
         TM    DATINDS,DATIYMO     TEST YEAR/MONTH ONLY                         
         BO    RESDAT08                                                         
         CLI   4(RE),C' '          TEST YEAR/MONTH ONLY INPUT                   
         BE    RESDAT08                                                         
         MVI   SOFITYPE,SOFITSD2   SET YYMMDD                                   
         MVI   SOFOTYPE,SOFOTSD2                                                
                                                                                
RESDAT08 ST    RE,SOFAINP          SET START OF SOFT DATE FIELD                 
         TM    SOFIINDS,SOFIIONE   TEST SINGLE DATE ALREADY SET                 
         BO    RESDAT12            ALREADY SET                                  
         TM    DATINDS,DATITWO     TEST PERIOD (TWO DATES)                      
         BO    RESDAT10                                                         
         OI    SOFIINDS,SOFIIONE   SET RESOLVE SINGLE DATE                      
         B     RESDAT12                                                         
                                                                                
RESDAT10 AR    RE,R0               POINT TO SECOND DATE IN PERIOD               
         TM    0(RE),X'F0'         TEST SECOND DATE IS SOFT                     
         BZ    RESDAT12            YES                                          
         OI    SOFIINDS,SOFIIONE   SET RESOLVE SINGLE DATE                      
                                                                                
RESDAT12 GOTOR (RF),(R1)           CALL SOFDAT TO RESOLVE DATE(S)               
         BZ    RESDAT14                                                         
         DC    H'0'                                                             
                                                                                
RESDAT14 AHI   R2,DATTABL          BUMP TO NEXT TABLE ENTRY                     
         B     RESDAT02                                                         
                                                                                
RESDAT16 L     RE,ADQSTACK         COPY CARDS 1 AND 2 FROM STACK                
         MVC   QRECORD,ACQCARD1-ACQD(RE)                                        
         MVC   QRECORD2,ACQCARD2-ACQD(RE)                                       
         MVC   QCOMPANY,SVCPY                                                   
                                                                                
RESDATX  J     EXIT                                                             
         DROP  R1,R2                                                            
         EJECT                                                                  
                                                                                
***********************************************************************         
* ROUTINE TO INITIALISE REQUEST DATES                                 *         
***********************************************************************         
                                                                                
SETDAT   NTR1  BASE=*,LABEL=*                                                   
         XC    ACMTSTR,ACMTSTR     GET TRANSACTION,                             
         MVC   ACMTEND,ACMEFFS                                                  
         XC    ACMHSTR,ACMHSTR     HISTORY,                                     
         MVC   ACMHEND,ACMEFFS                                                  
         XC    ACMMSTR,ACMMSTR     FILTER,                                      
         MVC   ACMMEND,ACMEFFS                                                  
         MVC   ACMCMSTR(L'ACMCMSTR+L'ACMCMEND),SPACES                           
         XC    ACMFDTE,ACMFDTE     AND FISCAL DATES READY                       
         NI    ACMINDS,FF-ACMIMOSR NO MOS FILTER SPECIFIED                      
                                                                                
         CLI   QMOSEND,C' '        IS MONTH OF SERVICE USED                     
         BE    SETDAT06                                                         
         OI    ACMINDS,ACMIMOSR    MOS FILTER SPECIFIED                         
         MVC   ACMWRK(4),QMOSEND CONVERT IT TO MMM/YY AND PACKED                
         MVC   ACMWRK+4(2),=C'01'                                               
         GOTOR DATCON,ACMPARA,(0,ACMWRK),(18,ACMCMEND)                          
         GOTOR DATCON,ACMPARA,(0,ACMWRK),(1,ACMWRK+6)                           
         MVC   ACMMEND,ACMWRK+6                                                 
         CLI   QMOSSTRT,C' '       IS START MONTH USED                          
         BE    SETDAT02            IF NOT GO TO FISCAL YEAR ROUTINE             
         MVC   ACMWRK(4),QMOSSTRT  CONVERT IT TO MMM/YY AND PACKED              
         MVC   ACMWRK+4(2),=C'01'                                               
         GOTOR DATCON,ACMPARA,(0,ACMWRK),(18,ACMCMSTR)                          
         GOTOR DATCON,ACMPARA,(0,ACMWRK),(1,ACMWRK+6)                           
         MVC   ACMMSTR,ACMWRK+6                                                 
                                                                                
SETDAT02 MVC   ACMFDTE(1),ACMMEND  GET YEAR FROM M.O.S. DATE                    
         MVI   ACMFDTE+1,1         FORCE JANUARY AS MONTH                       
         L     R2,ADCMPEL          GET ADDRESS OF COMPANY ELEMENT               
         USING CPYELD,R2                                                        
         CLI   CPYSFST,0           WAS A STARTING MONTH SPECIFIED ?             
         BE    SETDAT04            NO, LEAVE AS '01' & CHECK TO START           
         MVC   ACMWRK(1),CPYSFST                                                
         NI    ACMWRK,X'0F'                                                     
         SR    R1,R1                                                            
         IC    R1,ACMWRK           YES, ISOLATE IT                              
         TM    CPYSFST,X'F0'       WAS IT A NUMBER ?                            
         BO    *+8                 YES, SKIP OVER                               
         AHI   R1,15               NO, ADD CONVERSION FACTOR                    
         STC   R1,ACMFDTE+1        UPDATE FIELD WITH MONTH                      
                                                                                
SETDAT04 CLC   ACMFDTE,ACMMEND     HOW DOES IT COMPARE TO M.O.S. END?           
         BNH   SETDAT06            EQUAL OR LOW IS OKAY                         
         MVC   ACMWRK(1),ACMFDTE   BACK UP ONE YEAR                             
         MVI   ACMWRK+1,X'01'      JANUARY                                      
         MVI   ACMWRK+2,X'01'      FIRST                                        
         GOTOR DATCON,ACMPARA,(1,ACMWRK),(0,ACMWRK+3)                           
         GOTOR ADDAY,ACMPARA,ACMWRK+3,ACMWRK+9,-1                               
         GOTOR DATCON,ACMPARA,(0,ACMWRK+9),(1,ACMWRK)                           
         MVC   ACMFDTE(1),ACMWRK   PUT THE DATE WHERE WE CAN GET TO IT          
         DROP  R2                                                               
                                                                                
SETDAT06 XC    ACMASTR,ACMASTR     CLEAR ACTIVITY START & END                   
         MVC   ACMAEND,ACMEFFS                                                  
         NI    ACMINDS3,FF-(ACMIACTF)                                           
         CLC   QACTSTRT,SPACES                                                  
         BE    SETDAT08                                                         
         OI    ACMINDS3,ACMIACTF                                                
         GOTOR DATCON,DMCB,(0,QACTSTRT),(2,ACMASTR)                             
                                                                                
SETDAT08 CLC   QACTEND,SPACES                                                   
         BE    SETDAT10                                                         
         OI    ACMINDS3,ACMIACTF                                                
         GOTOR DATCON,DMCB,(0,QACTEND),(2,ACMAEND)                              
                                                                                
SETDAT10 XC    ACMDSTR,ACMDSTR     CLEAR DUE DATE START                         
         XC    ACMDSTR1,ACMDSTR1                                                
         MVC   ACMDEND,ACMEFFS     AND END                                      
         MVC   ACMDEND1,ACMEFFS                                                 
         NI    ACMINDS3,FF-(ACMIDUEF)                                           
                                                                                
         USING ACQTYP1,RE                                                       
         LA    R0,QDATETQ          NUMBER OF ENTRIES                            
         LA    RF,QDATETAB         DATE IN 3RD OR 4TH CARDS                     
SETDAT12 SR    RE,RE                                                            
         ICM   RE,3,0(RF)          GET DISPLACMENT INTO CARDS                   
         A     RE,ADQSTACK         ADD BASE TO DISPLACEMENT                     
         CLI   ACQTYP1,ACQDATE                                                  
         BNE   SETDAT14                                                         
         LA    R2,ACQDTSTR         START OF DATE                                
         CLI   ACQDTTYP,ACQDTDUE   DUE DATE                                     
         BE    SETDAT18                                                         
                                                                                
SETDAT14 AHI   RF,2                TRY NEXT ENTRY IN TABLE                      
         BCT   R0,SETDAT12                                                      
         DROP  RE                                                               
                                                                                
SETDAT16 CLI   QAPFLAG,QAPFDUE     TEST QDUEST/QDUEND PRESENT                   
         BNE   SETDAT22                                                         
         LA    R2,QDUEST                                                        
                                                                                
SETDAT18 CLC   0(L'QDUEST,R2),SPACES                                            
         BE    SETDAT20                                                         
         OI    ACMINDS3,ACMIDUEF                                                
         GOTOR DATCON,DMCB,(0,0(R2)),(2,ACMDSTR)                                
         GOTOR (RF),(R1),,(1,ACMDSTR1)                                          
                                                                                
SETDAT20 CLC   6(L'QDUEND,R2),SPACES                                            
         BE    SETDAT22                                                         
         OI    ACMINDS3,ACMIDUEF                                                
         GOTOR DATCON,DMCB,(0,6(R2)),(2,ACMDEND)                                
         GOTOR (RF),(R1),,(1,ACMDEND1)                                          
                                                                                
SETDAT22 DS    0H                                                               
*&&UK                                                                           
         XC    ACMEPDST,ACMEPDST                                                
         MVC   ACMEPDND,ACMEFFS                                                 
         NI    ACMINDS3,FF-(ACMIEPDF)                                           
         CLI   MCRQNUM,4                                                        
         BL    SETDAT26                                                         
         L     R2,ADQSTACK                                                      
         USING ACQD,R2                                                          
         CLC   ACQEPDST,SPACES                                                  
         BE    SETDAT24                                                         
         OI    ACMINDS3,ACMIEPDF                                                
         GOTOR DATCON,DMCB,(0,ACQEPDST),(1,ACMEPDST)                            
                                                                                
SETDAT24 CLC   ACQEPDND,SPACES                                                  
         BE    SETDAT26                                                         
         OI    ACMINDS3,ACMIEPDF                                                
         GOTOR DATCON,DMCB,(0,ACQEPDND),(1,ACMEPDND)                            
         DROP  R2                                                               
*&&                                                                             
SETDAT26 DS    0H                                                               
                                                                                
SETDATX  J     EXIT                                                             
         DROP  RB                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO READ A LIST RECORD                                       *         
***********************************************************************         
                                                                                
SETLST   NTR1  BASE=*,LABEL=*                                                   
         XC    ADLSTREC,ADLSTREC                                                
         XC    ACMANXL,ACMANXL                                                  
         NI    ACMINDS,FF-ACMIWLST SET WORK CODE LIST NOT REQUESTED             
         NI    ACMINDS4,FF-ACMIWCFL    SINGLE WORK CODE FILTER                  
         LA    R1,QSELECT                                                       
         CLI   0(R1),C'+'                                                       
         BE    SETLST02                                                         
         CLI   0(R1),C'-'                                                       
         BE    SETLST02                                                         
         LA    R1,QWRKLIST                                                      
         CLI   0(R1),C'+'                                                       
         BE    *+12                                                             
         CLI   0(R1),C'-'                                                       
         BNE   *+12                                                             
         OI    ACMINDS,ACMIWLST    SET WORK CODE LIST REQUESTED                 
         B     SETLST02                                                         
                                                                                
         CLC   QWRKLIST(2),SPACES                                               
         BNH   SETLSTX                                                          
         OI    ACMINDS4,ACMIWCFL   SINGLE WORKCODE FILTER                       
         B     SETLSTX                                                          
                                                                                
SETLST02 MVC   ACMDUB,0(R1)                                                     
         L     R2,ACMABUF                                                       
         USING LSTRECD,R2                                                       
         XC    LSTKEY,LSTKEY                                                    
         MVI   LSTKTYP,LSTKTYPQ                                                 
         MVC   LSTKCPY,RCCOMPFL                                                 
         MVC   LSTKLST,ACMDUB+1                                                 
         MVC   KEYSAVE,LSTKEY                                                   
         GOTOR DATAMGR,DMCB,(X'88',DMRDHI),ACMACFIL,LSTRECD,LSTRECD             
         CLC   LSTKEY(LSTKEND),KEYSAVE                                          
         BNE   SETLSTN                                                          
         L     R0,ACMALST                                                       
         ST    R0,ADLSTREC                                                      
         LA    RE,LSTRECD                                                       
         SR    RF,RF                                                            
         ICM   RF,3,LSTRLEN                                                     
         LR    R1,RF                                                            
         MVCL  R0,RE                                                            
                                                                                
         L     R2,ADLSTREC                                                      
         AH    R2,DATADISP                                                      
         SR    R0,R0                                                            
         USING LITELD,R2                                                        
SETLST04 CLI   LITEL,0                                                          
         BE    SETLSTN                                                          
         CLI   LITEL,LITELQ                                                     
         BE    *+14                                                             
         IC    R0,LITLN                                                         
         AR    R2,R0                                                            
         B     SETLST04                                                         
                                                                                
         CLI   LITTYPE,LITTLDG                                                  
         BE    SETLST06            LEDGER TYPE                                  
         MVI   LITUSE,LITUINCL                                                  
         CLI   ACMDUB,C'+'                                                      
         BE    *+8                                                              
         MVI   LITUSE,LITUEXCL                                                  
         B     SETLSTX                                                          
                                                                                
SETLST06 L     R2,ADLSTREC                                                      
         AH    R2,DATADISP                                                      
         SR    R0,R0                                                            
         USING LIDELD,R2                                                        
SETLST08 CLI   LIDEL,0                                                          
         BE    SETLSTN                                                          
         CLI   LIDEL,LIDELQ                                                     
         BE    *+14                                                             
         IC    R0,LIDLN                                                         
         AR    R2,R0                                                            
         B     SETLST08                                                         
         CLI   ACMDUB,C'-'                                                      
         BE    SETLSTN             LEDGER LIST CANNOT BE EXCLUDE                
         LA    RE,LIDDAUNT                                                      
         CLC   0(2,RE),SPACES      TEST FOR VALID UNIT/LEDGER                   
         BE    SETLSTN                                                          
         ST    RE,ACMANXL          SAVE ADDRESS OF NEXT U/L                     
         SR    R0,R0                                                            
         SR    R0,R0                                                            
         IC    R0,LIDLN                                                         
         SHI   R0,LIDDATA-LIDELD                                                
         SRL   R0,1                                                             
         BCTR  R0,0                                                             
         STC   R0,ACMANXL          HIGH ORDER HAS NUMBER REMAINING              
         B     SETLSTX                                                          
                                                                                
SETLSTN  OI    ADLSTREC,X'80'                                                   
                                                                                
SETLSTX  J     EXIT                                                             
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO SET LANGUAGE DEPENDENT VALUES                            *         
***********************************************************************         
                                                                                
SETLNG   NTR1  BASE=*,LABEL=*                                                   
         GOTOR DATCON,DMCB,(7,0),(X'FF',0)                                      
         SR    RE,RE                                                            
         ICM   RE,7,1(R1)                                                       
         MVC   DAYTABL,0(RE)       SET DAYS & MONTHS IN ACWORKD                 
         ICM   RE,7,5(R1)                                                       
         MVC   MONTHS,0(RE)                                                     
         TM    ACMINDS2,ACMISLAV   MASTER APPLICATION CONTROLS PRINTING         
         BNZ   SETLNGX                                                          
         L     R1,MCBXAREA                                                      
         USING BOXD,R1                                                          
         MVI   BOXSYS,6            SET ACCOUNTING                               
         MVI   BOXDDCTL,X'80'      SET WANT DATA DICTIONARY                     
         MVC   BOXLANG,RCLANG      SET OUTPUT LANGUAGE                          
SETLNGX  J     EXIT                                                             
         DROP  R1,RB                                                            
         EJECT                                                                  
***********************************************************************         
* OPEN FILES AND INITIALISE BUFFERS                                   *         
***********************************************************************         
                                                                                
OPNFIL   NTR1  BASE=*,LABEL=*                                                   
         XC    ACMPARA(24),ACMPARA                                              
         GOTOR DATAMGR,ACMPARA,ACMDMDTF,ACMACFIL                                
         MVC   ADACCFIL,12(R1)     SAVE ADDRESS OF ACCOUNT DCB                  
                                                                                
         LA    R1,ACMWRK           BUILD FILE OPEN LIST                         
         OI    ACMINDS,ACMIEMUD    SET ACCOUNT FILE EMULATED                    
                                                                                
         MVI   0(R1),NO                                                         
         CLI   RCWRITE,NO          CHECK WRITE=NO, WRITE=YES DEFAULT            
         BE    OPNFIL04                                                         
         CLI   FCUPFILE,YES        TEST UPDATE ACCOUNT FILE                     
         BNE   OPNFIL04                                                         
         MVI   0(R1),C'U'          OPEN ACCDIR FOR UPDATE                       
OPNFIL04 MVC   1(7,R1),ACMACDIR                                                 
         AHI   R1,8                                                             
                                                                                
         MVI   0(R1),NO                                                         
         CLI   RCWRITE,NO          CHECK WRITE=NO, WRITE=YES DEFAULT            
         BE    OPNFIL06                                                         
         CLI   FCUPFILE,YES        TEST UPDATE ACCOUNT FILE                     
         BNE   OPNFIL06                                                         
         MVI   0(R1),C'U'          OPEN ACCMST FOR UPDATE                       
OPNFIL06 MVC   1(7,R1),ACMACMST                                                 
         AHI   R1,8                                                             
         MVI   0(R1),NO                                                         
         MVC   1(7,R1),ACMACARC    OPEN ACCARC                                  
         AHI   R1,8                                                             
*                                                                               
         MVI   0(R1),NO                                                         
         MVC   1(7,R1),ACMCTFIL                                                 
         AHI   R1,8                                                             
         MVI   0(R1),NO                                                         
         MVC   1(7,R1),ACMACRCV                                                 
*&&UK                                                                           
         CLI   FCUPFILE,YES        TEST WE ARE UPDATING MASTER FILES            
         BE    OPNFIL10                                                         
         CLI   FCRDRCVR,NO         OR READING RECOVERY FILE                     
         BE    OPNFIL28                                                         
         B     OPNFIL26                                                         
                                                                                
OPNFIL10 CLI   MCRECOVR,C'X'       TEST OFFLINE RECOVERY UK                     
         BE    OPNFIL26                                                         
*&&                                                                             
*&&US*&& CLI   MCRECOVR,YES        TEST OFFLINE RECOVERY WANTED                 
*&&US*&& BNE   OPNFIL26                                                         
         CLI   RCWRITE,NO          CHECK WRITE=NO, WRITE=YES DEFAULT            
         BE    OPNFIL26                                                         
         MVI   0(R1),C'U'          YES - OPEN ACCRCV FOR UPDATE                 
         AHI   R1,8                                                             
         NI    SSOSTAT2,FF-SSOSNRCV      MAKE THIS EXPLICT SHOWN                
         NI    SSOSTAT2,SSOSTRAC+SSOSHBDW+SSOSRSQF                              
         OI    SSOSTAT2,SSOSROLC                                                
         B     OPNFIL28                                                         
                                                                                
OPNFIL26 CLI   FCRDRCVR,NO         TEST READING RECOVERY FILE                   
         BE    OPNFIL28                                                         
         AHI   R1,8                YES - OPEN ACCRCV FOR READ ONLY              
                                                                                
OPNFIL28 CLI   FCUPDREQ,YES        TEST UPDATING REQUEST FILE                   
         BNE   OPNFIL30                                                         
         CLI   RCWRITE,NO          NOT IF WRITE=NO SET                          
         BE    OPNFIL30                                                         
         MVC   0(8,R1),=C'UACCREQ '                                             
         AHI   R1,8                                                             
                                                                                
OPNFIL30 MVI   0(R1),C'X'          SET END OF FILE OPEN LIST                    
         TM    ACMINDS2,ACMISLAV   NO SPECIAL FILES IN SLAVE MODE               
         BNZ   OPNFIL60                                                         
         GOTOR DATAMGR,DMCB,ACMDMOPN,ACMACSYS,ACMWRK,ACFILEC                    
                                                                                
         CLI   INFILE,YES          OPEN INPUT FILE IF REQUIRED                  
         BNE   OPNFIL52                                                         
         L     R2,ADIN                                                          
         OPEN  ((R2),(INPUT))                                                   
                                                                                
OPNFIL52 CLI   WORKFILE,YES        OPEN WORK FILE IF REQUIRED                   
         BNE   OPNFIL54                                                         
         L     R2,ADWORK                                                        
         OPEN  ((R2),(OUTIN))                                                   
                                                                                
OPNFIL54 CLI   OUTFILE,YES         OPEN OUTPUT FILE IF REQUIRED                 
         BNE   OPNFIL56                                                         
         L     R2,ADOUT                                                         
         OPEN  ((R2),(OUTPUT))                                                  
                                                                                
OPNFIL56 CLI   MCRECOVR,C'2'       TEST 2 SEQUENTIAL OUTPUT FILES               
         BE    *+12                                                             
         CLI   MCRECOVR,C'S'       TEST RECOVERY TO SEQUENTIAL FILE             
         BNE   OPNFIL58                                                         
         L     R2,ADRCVT                                                        
         OPEN  ((R2),(OUTPUT))     OPEN THE RECOVERY FILE                       
         NI    SSOSTAT2,FF-SSOSNRCV                                             
         OI    SSOSTAT2,SSOSRSQF                                                
         BASR  RE,0                                                             
         AHI   RE,PUTRCV-*                                                      
         STCM  RE,15,SSOSQRTN                                                   
         CLI   MCRECOVR,C'2'       TEST 2 SEQUENTIAL OUTPUT FILES               
         BNE   OPNFIL58                                                         
         L     R2,ADRCVC                                                        
         OPEN  ((R2),(OUTPUT))     OPEN THE RECOVERY COPY FILE                  
                                                                                
OPNFIL58 CLI   MCRECOVR,C'W'       TEST FACWK RECOVERY                          
         BNE   OPNFIL60                                                         
         GOTOR DATCON,DMCB,(4,RCDATE),(1,ACMWRK)                                
         NI    SSOSTAT2,255-SSOSNRCV                                            
         OI    SSOSTAT2,SSOSRWRK                                                
         L     R0,SSOFWBUF                                                      
         L     RF,SSOFWNDX                                                      
                                                                                
         USING UKRECD,RF           SET FACWRK KEY VALUES                        
         MVC   UKUSRID,ORIGINUM    USERID                                       
         MVI   UKSYSPRG,C'A'       SYSTEM/PROGRAM                               
         MVC   UKSYSPRG+1(L'RCPROG),RCPROG                                      
         MVC   UKSUBPRG,MCFACPAK   FACPAK SYSTEM CODE                           
         MVC   UKDAY,ACMWRK+2      DAY NUMBER                                   
         MVI   UKCLASS,C'R'        CLASS=RECOVERY                               
         MVI   UKFLAG,X'01'        FLAG DUPLICATES ALLOWED                      
         MVC   UKFILNO,ACMEFFS     SET UKFLAG IS VALID                          
         DROP  RF                                                               
                                                                                
OPNFIL60 TM    ACMINDS,ACMIEMUD    TEST NEW FILE                                
         BZ    OPNFILX             NO                                           
                                                                                
         GOTOR DATAMGR,ACMPARA,ACMDMDTF,ACMACDIR                                
         MVC   ACMADDIR,12(R1)     A(ACCDIR DTF)                                
                                                                                
OPNFILX  J     EXIT                                                             
         DROP  RB                                                               
         EJECT                                                                  
***********************************************************************         
* CLOSE FILES ISSUE FREEMAINS ETC.                                    *         
***********************************************************************         
                                                                                
         USING COMCTRLD,R2                                                      
CLOFIL   NTR1  BASE=*,LABEL=*                                                   
*&&UK*&& L     R2,VCOMC            SET FICHE TO BE CLOSED                       
*&&UK*&& MVI   COMSTART,2                                                       
         L     RF,ADBXAREA                                                      
         CLI   BOXSTAT-BOXD(RF),C'I' TEST INSIDE A BOX NOW                      
*&&UK*&& BNE   *+8                                                              
*&&US*&& BNE   CLOFIL02                                                         
         MVI   BOXREQ-BOXD(RF),C'C' YES - SET TO CLOSE BOX OFF                  
         GOTOR PRINT,DMCB,MCSPACES,BL01                                         
*&&UK*&& MVI   COMIO,2                                                          
*&&UK*&& MVI   COMSTART,1                                                       
         DROP  R2                                                               
                                                                                
         USING LOGOD,R2            R2=A(LOGO BLOCK)                             
CLOFIL02 CLI   RCREQSUM,YES        TEST END LOGOS REQUIRED                      
         BNE   CLOFIL04                                                         
         L     R2,LOGOC                                                         
         MVI   LOGOTYPE,C'E'                                                    
         ZAP   LOGOREQS,RCRQVAL                                                 
         GOTOR LOGO,DMCB,LOGOD     PRINT END LOGOS                              
         DROP  R2                                                               
                                                                                
CLOFIL04 TM    SSOSTAT2,SSOSRWRK   TEST FACWRK RECOVERY                         
         BZ    CLOFIL06                                                         
         L     R2,SSOFWBUF                                                      
         ICM   RF,15,SSOFWNDX                                                   
         BNZ   *+6                                                              
         DC    H'0'                FACWRK INDEX ADDRESS NOT SET                 
         LA    R0,CFPCLOSE         ISSUE REGULAR CLOSE                          
         CP    MCNDUMPS,ACMPZERO                                                
         BE    *+8                                                              
         LA    R0,CFPCLERR         OR CLOSE/ERROR IF DUMP PRODUCED              
         GOTOR DATAMGR,DMCB,(0,(R0)),CFFACWRK,(RF),ADCOMP,(R2)                  
         BE    *+6                                                              
         DC    H'0'                ERROR ON FACWRK FILE                         
                                                                                
         TM    ACMINDS7,ACMIKPFW   TEST KEEP FACWK FILE (DON'T UPDATE)          
         BNO   CLOFIL06                                                         
         LA    R0,CFPKEEP                                                       
*                                                                               
*        L     RF,MCSSB                                                         
         ICM   RF,15,SSOFWNDX                                                   
         GOTO1 DATAMGR,DMCB,(0,(R0)),CFFACWRK,(RF),ADCOMP,(R2)                  
         BE    CLOFIL06                                                         
         DC    H'0'                ERROR ON FACWRK FILE                         
                                                                                
CLOFIL06 GOTOR DATAMGR,DMCB,ACMDMCLS,ACMACSYS,,ACFILEC                          
                                                                                
         CLI   OUTFILE,YES         TEST OUTPUT FILE REQUESTED                   
         BNE   CLOFIL08                                                         
         L     R2,ADOUT                                                         
         L     R3,ADIO             WRITE OUT AN FF RECORD                       
         MVC   0(L'CFEOFREC,R3),CFEOFREC                                        
         PUT   (R2),(R3)                                                        
         CLOSE ((R2))              AND CLOSE THE FILE                           
                                                                                
CLOFIL08 CLI   MCRECOVR,C'2'       TEST 2 SEQUENTIAL OUTPUT FILES               
         BE    *+12                                                             
         CLI   MCRECOVR,C'S'       TEST RECOVERY TO SEQUENTIAL FILE             
         BNE   CLOFIL10                                                         
         L     R2,ADRCVT                                                        
         CLOSE ((R2))              CLOSE THE RECOVERY FILE                      
         CLI   MCRECOVR,C'2'       TEST 2 SEQUENTIAL OUTPUT FILES               
         BNE   CLOFIL10                                                         
         L     R2,ADRCVC                                                        
         CLOSE ((R2))              CLOSE THE RECOVERY COPY FILE                 
                                                                                
CLOFIL10 ICM   R1,15,ACMACOB                                                    
         BZ    CLOFIL12                                                         
         L     R0,ACMLCOB                                                       
         FREEMAIN RC,A=(1),LV=(0)                                               
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
                                                                                
CLOFIL12 ICM   R1,15,ACMACURT                                                   
         BZ    CLOFIL14                                                         
         LHI   R0,CURTABLN                                                      
         SLL   R0,8                                                             
         FREEMAIN RC,A=(1),LV=(0)                                               
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
                                                                                
CLOFIL14 ICM   R1,15,ACMTSADR                                                   
         BZ    CLOFIL16                                                         
         LHI   R0,ACMTSNUM                                                      
         SLL   R0,2                                                             
         FREEMAIN RC,A=(1),LV=(0)                                               
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
                                                                                
CLOFIL16 ICM   R1,15,ACMWCNMA                                                   
         BZ    CLOFIL18                                                         
         L     R0,=A(WCNTABL*ACMWCBN)                                           
         FREEMAIN RC,A=(1),LV=(0)                                               
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
                                                                                
CLOFIL18 ICM   R1,15,ACMAGRPA                                                   
         BZ    CLOFIL20                                                         
         L     R0,=A(GRPTABL*GRPTMAXN+1)                                        
         FREEMAIN RC,A=(1),LV=(0)                                               
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
                                                                                
CLOFIL20 ICM   R1,15,ACMAACTA                                                   
         BZ    CLOFIL22                                                         
         L     R0,=A(L'ACTKACT*ACMAACTM)                                        
         FREEMAIN RC,A=(1),LV=(0)                                               
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
                                                                                
         USING LDGTABD,R2                                                       
CLOFIL22 ICM   R2,15,ACMALDGT                                                   
         BZ    CLOFIL26                                                         
                                                                                
CLOFIL24 ICM   R1,15,LDGAFLT       FILTER TABLE                                 
         BZ    CLOFIL26                                                         
         L     R0,LDGTABSZ         SIZE OF TABLE                                
         FREEMAIN RC,A=(1),LV=(0)                                               
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
                                                                                
         AHI   R2,LDGTABQ          NEXT POSSIBLE ENTRY                          
         B     CLOFIL24                                                         
         DROP  R2                                                               
                                                                                
CLOFIL26 ICM   R1,15,ACMALDGT                                                   
         BZ    CLOFIL28                                                         
         LHI   R0,LDGTABQ*LDGTABN                                               
         FREEMAIN RC,A=(1),LV=(0)                                               
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
                                                                                
CLOFIL28 TM    ACMINDS2,ACMISLAV   TEST SLAVE MODE                              
         BNZ   CLOFIL40                                                         
         GOTOR ACMNQDQ,DMCB,(C'D',=C'ALL')                                      
         CP    MCNDUMPS,ACMPZERO   TEST FOR NORMAL TERMINATION                  
         BE    CLOFIL30                                                         
         GOTOR VPPGPRT,DMCB,(L'CFPCLERR,CFPCLERR)                               
         CLI   ACMSYS,ACMSUSA                                                   
         BE    CLOFIL40                                                         
         ABEND 664,DUMP                                                         
                                                                                
CLOFIL30 GOTOR VPPGPRT,DMCB,(L'CFPCLOSE,CFPCLOSE)                               
                                                                                
CLOFIL40 GOTOR VSHMUSS,DMCB,(0,=CL8'DETACH'),(0,=CL8'PRTQ')                     
                                                                                
CLOFILX  J     EXIT                                                             
         DROP  RB                                                               
         EJECT                                                                  
***********************************************************************         
* INITIALISE LOGO BLOCK AND PRINT START LOGOS                         *         
***********************************************************************         
                                                                                
STRLOG   NTR1  BASE=*,LABEL=*                                                   
         L     R2,LOGOC                                                         
         USING LOGOD,R2            R2=A(LOGO BLOCK)                             
         ICM   R1,15,ADCMPEL       R1=A(COMPANY ELEMENT)                        
         BNZ   *+6                                                              
         DC    H'0'                                                             
         CLC   LOGO1,SPACES        TEST LOGO SET                                
         BNE   STRLOG02                                                         
         USING CPYELD,R1                                                        
         MVC   LOGO1,CPYLOGO                                                    
                                                                                
STRLOG02 CLC   LOGONAME,SPACES     TEST AGENCY NAME SET                         
         BNE   STRLOG04                                                         
         ICM   R1,15,ADCMPNAM      R1=A(COMPANY NAME ELEMENT)                   
         BZ    STRLOG04                                                         
         USING NAMELD,R1                                                        
         SR    RE,RE                                                            
         IC    RE,NAMLN                                                         
         SHI   RE,NAMLN1Q+1                                                     
         CHI   RE,L'LOGONAME-1                                                  
         BNH   *+8                                                              
         LHI   RE,L'LOGONAME-1                                                  
         EX    RE,*+8                                                           
         B     STRLOG04                                                         
         MVC   LOGONAME(0),NAMEREC                                              
                                                                                
STRLOG04 CLC   LOGOADD,SPACES      TEST AGENCY ADDRESS SET                      
         BNE   STRLOG06                                                         
         ICM   R1,15,ADCMPADD      R1=A(COMPANY ADDRESS ELEMENT)                
         BZ    STRLOG06                                                         
         USING ADRELD,R1                                                        
         MVC   LOGOADD(L'ADRADD1),ADRADD1                                       
         CLI   ADRNUM,2                                                         
         BL    STRLOG06                                                         
         MVC   LOGOADD2(L'ADRADD2),ADRADD2                                      
         BE    STRLOG06                                                         
         MVC   LOGOADD3(L'ADRADD3),ADRADD3                                      
                                                                                
STRLOG06 CLI   RCREQSUM,YES        TEST LOGOS REQUIRED                          
         BNE   STRLOGX                                                          
         GOTOR LOGO,DMCB,LOGOC     PRINT START LOGOS                            
STRLOGX  J     EXIT                                                             
         DROP  R1,R2,RB                                                         
         EJECT                                                                  
***********************************************************************         
* PRINT LINEUP PATTERN IF REQUIRED                                    *         
***********************************************************************         
*&&DO                                                                           
LINEUP   NTR1  BASE=*,LABEL=*                                                   
         CLI   RCLINEUP,YES        TEST LINEUP REQUIRED                         
         BNE   LINEUPX                                                          
         CLI   ACMSYS,ACMSUSA                                                   
         BE    LINEUP02                                                         
                                                                                
         MVC   P,SPACES            SKIP TO NEW PAGE                             
         GOTOR PRINT,DMCB,P,BC01                                                
         GOTOR (RF),(R1),,BL09                                                  
         LHI   R0,2                                                             
         MVI   P+1,C'X'            PRINT 109 X'S TWICE                          
         MVC   P+2(108),P+1                                                     
         GOTOR (RF),(R1),,BL01                                                  
         BCT   R0,*-2                                                           
         B     LINEUPX                                                          
                                                                                
LINEUP02 L     RF,REMOTEC                                                       
         USING REMOTED,RF                                                       
         OC    REMOTKEY,REMOTKEY   TEST DIRECT TO PRTQUE                        
         BNZ   LINEUPX                                                          
         OC    MCREMPQK,MCREMPQK   TEST SOON JOB                                
         BNZ   LINEUPX                                                          
         DROP  RF                                                               
                                                                                
         LHI   R2,3                PRINT LINEUP 3 TIMES                         
LINEUP04 MVC   P,SPACES            SKIP TO NEW PAGE                             
         GOTOR PRINT,DMCB,P,BC01                                                
         GOTOR (RF),(R1),,BL12                                                  
         MVI   P,C'X'              PRINT 86 X'S THREE TIMES                     
         MVC   P+1(85),P                                                        
         LHI   R0,3                                                             
         GOTOR (RF),(R1),,BL01                                                  
         BCT   R0,*-2                                                           
         BCT   R2,LINEUP04                                                      
                                                                                
LINEUPX  MVC   P,SPACES                                                         
         J     EXIT                                                             
         DROP  RB                                                               
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO SET REQUEST LEVEL OPTION INDICATORS FOR REVERSES, DRAFTS *         
* AND XJOBS                                                           *         
***********************************************************************         
                                                                                
REQIND   NTR1  BASE=*,LABEL=*                                                   
         OI    ACMINDS,ACMITRVY+ACMITRVN                                        
         XC    ACMAREVT,ACMAREVT                                                
         LHI   R0,RIREVNUM                                                      
         LTR   R0,R0                                                            
         BZ    REQIND06                                                         
         NI    ACMINDS,FF-(ACMITRVY+ACMITRVN)                                   
                                                                                
         LA    R1,RIREVTAB         R1=A(REVERSAL OVERRIDE PROG TABLE)           
         USING REVTABD,R1                                                       
REQIND02 CLC   REVTPROG,SPACES     TEST DEFAULT ENTRY REACHED                   
         BE    REQIND04                                                         
         CLC   REVTPROG,QPROG      MATCH REQUEST TO TABLE                       
         BE    REQIND04                                                         
         AHI   R1,REVTABL                                                       
         BCT   R0,REQIND02                                                      
         B     REQIND02                                                         
                                                                                
REQIND04 ST    R1,ACMAREVT         SAVE A(REVTAB ENTRY)                         
         TM    REVTINDS,REVTRQST   TEST REQUEST CARD OVERRIDE                   
         BZ    REQIND06                                                         
         CLI   QREVERSE,QREVINCL   REVERSED & NON-REVERSED                      
         BE    *+8                                                              
         CLI   QREVERSE,QREVBOTH                                                
         BNE   *+12                                                             
         OI    ACMINDS,ACMITRVY+ACMITRVN                                        
         B     REQIND06                                                         
         CLI   QREVERSE,QREVNONE   NON-REVERSED ONLY                            
         BNE   *+12                                                             
         OI    ACMINDS,ACMITRVN                                                 
         B     REQIND06                                                         
         CLI   QREVERSE,QREVONLY   REVERSED ONLY                                
         BNE   REQIND06                                                         
         OI    ACMINDS,ACMITRVY                                                 
         DROP  R1                                                               
                                                                                
REQIND06 OI    ACMINDS2,ACMIDRAY+ACMIDRAN                                       
         XC    ACMADRAT,ACMADRAT   CLEAR DRAFT TABLE ENTRY ADCON                
         LHI   R0,RIDRANUM                                                      
         LTR   R0,R0               NO DRAFT TABLE ENTRIES                       
         BZ    REQIND14                                                         
         NI    ACMINDS2,FF-(ACMIDRAY+ACMIDRAN)                                  
                                                                                
         LA    R1,RIDRATAB         R1=A(DRAFT OVERRIDE PROG TABLE)              
         USING DRATABD,R1                                                       
REQIND08 CLC   DRATPROG,SPACES     TEST DEFAULT ENTRY REACHED                   
         BE    REQIND10                                                         
         CLC   DRATPROG,QPROG      MATCH REQUEST TO TABLE                       
         BE    REQIND10                                                         
         AHI   R1,DRATABL                                                       
         BCT   R0,REQIND08                                                      
         B     REQIND08                                                         
                                                                                
REQIND10 ST    R1,ACMADRAT         SAVE A(RIDRATAB ENTRY)                       
         TM    DRATINDS,DRATRQST   TEST REQUEST CARD OVERRIDE                   
         BZ    REQIND14                                                         
                                                                                
         CLI   QDRAFT,QDRABOTH     DRAFT AND LIVE                               
         BNE   *+12                                                             
         OI    ACMINDS2,ACMIDRAY+ACMIDRAN                                       
         B     REQIND14                                                         
         CLI   QDRAFT,QDRANONE     NON-DRAFT(LIVE) ONLY                         
         BNE   *+12                                                             
         OI    ACMINDS2,ACMIDRAN                                                
         B     REQIND14                                                         
         CLI   QDRAFT,QDRAONLY     DRAFT ONLY                                   
         BNE   REQIND14                                                         
         OI    ACMINDS2,ACMIDRAY                                                
                                                                                
REQIND14 OI    ACMINDS2,ACMIXJBY+ACMIXJBN                                       
         XC    ACMAXJBT,ACMAXJBT   CLEAR XJOB TABLE ENTRY ADCON                 
         LHI   R0,RIXJBNUM                                                      
         LTR   R0,R0               NO DRAFT TABLE ENTRIES                       
         BZ    REQIND19                                                         
         NI    ACMINDS2,FF-(ACMIXJBY+ACMIXJBN)                                  
                                                                                
         LA    R1,RIXJBTAB         R1=A(XJOB OVERRIDE PROG TABLE)               
         USING XJBTABD,R1                                                       
REQIND16 CLC   XJBTPROG,SPACES     TEST DEFAULT ENTRY REACHED                   
         BE    REQIND18                                                         
         CLC   XJBTPROG,QPROG      MATCH REQUEST TO TABLE                       
         BE    REQIND18                                                         
         AHI   R1,XJBTABL                                                       
         BCT   R0,REQIND16                                                      
         B     REQIND16                                                         
                                                                                
REQIND18 ST    R1,ACMAXJBT         SAVE A(RIXJBTAB ENTRY)                       
         TM    XJBTINDS,XJBTRQST   TEST REQUEST CARD OVERRIDE                   
         BZ    REQIND19                                                         
                                                                                
         CLI   QXJOB,QXJBOTH       BOTH XJOB AND REGULAR JOB                    
         BNE   *+12                                                             
         OI    ACMINDS2,ACMIXJBY+ACMIXJBN                                       
         B     REQIND19                                                         
         CLI   QXJOB,QXJNONE       NON-XJOB ONLY                                
         BNE   *+12                                                             
         OI    ACMINDS2,ACMIXJBN                                                
         B     REQIND19                                                         
         CLI   QXJOB,QXJONLY       X-JOB ONLY                                   
         BNE   REQIND19                                                         
         OI    ACMINDS2,ACMIXJBY                                                
                                                                                
*&&US                                                                           
REQIND19 OI    ACMINDS5,ACMIDJBY+ACMIDJBN                                       
         XC    ACMADJBT,ACMADJBT   CLEAR DJOB TABLE ENTRY ADCON                 
         LHI   R0,RIDJBNUM                                                      
         LTR   R0,R0               NO DRAFT TABLE ENTRIES                       
         BZ    REQIND20                                                         
         NI    ACMINDS5,FF-(ACMIDJBY+ACMIDJBN)                                  
                                                                                
         LA    R1,RIDJBTAB         R1=A(DJOB OVERRIDE PROG TABLE)               
         USING DJBTABD,R1                                                       
REQIND1A CLC   DJBTPROG,SPACES     TEST DEFAULT ENTRY REACHED                   
         BE    REQIND1B                                                         
         CLC   DJBTPROG,QPROG      MATCH REQUEST TO TABLE                       
         BE    REQIND1B                                                         
         AHI   R1,DJBTABL                                                       
         BCT   R0,REQIND1A                                                      
         B     REQIND1A                                                         
                                                                                
REQIND1B ST    R1,ACMADJBT         SAVE A(RIDJBTAB ENTRY)                       
         TM    DJBTINDS,DJBTRQST   TEST REQUEST CARD OVERRIDE                   
         BZ    REQIND20                                                         
*                                                                               
         USING ACQD,R3                                                          
         L     R3,ADQSTACK                                                      
         CLI   ACQDJOB,ACQDJBO     BOTH DJOB AND REGULAR JOB                    
         BNE   *+12                                                             
         OI    ACMINDS5,ACMIDJBY+ACMIDJBN                                       
         B     REQIND20                                                         
         CLI   ACQDJOB,ACQDJNO     NON-DRAFT JOBS ONLY                          
         BNE   *+12                                                             
         OI    ACMINDS5,ACMIDJBN                                                
         B     REQIND20                                                         
         CLI   ACQDJOB,ACQDJON     DRAFT JOBS ONLY                              
         BNE   REQIND20                                                         
         OI    ACMINDS5,ACMIDJBY                                                
*&&                                                                             
REQIND20 DS    0H                                                               
*&&UK                                                                           
         OI    ACMINDS4,ACMICLOY+ACMICLON                                       
         XC    ACMACLOT,ACMACLOT   CLEAR CLOSED TABLE ENTRY ADCON               
         LHI   R0,RICLONUM                                                      
         LTR   R0,R0               NO DRAFT TABLE ENTRIES                       
         BZ    REQIND26                                                         
         NI    ACMINDS4,FF-(ACMICLOY+ACMICLON)                                  
                                                                                
         LA    R1,RICLOTAB         R1=A(CLOSED OVERRIDE PROG TABLE)             
         USING CLOTABD,R1                                                       
REQIND22 CLC   CLOTPROG,SPACES     TEST DEFAULT ENTRY REACHED                   
         BE    REQIND24                                                         
         CLC   CLOTPROG,QPROG      MATCH REQUEST TO TABLE                       
         BE    REQIND24                                                         
         AHI   R1,CLOTABL                                                       
         BCT   R0,REQIND22                                                      
         DC    H'0'                                                             
                                                                                
REQIND24 ST    R1,ACMACLOT         SAVE A(RICLOTAB ENTRY)                       
         TM    CLOTINDS,CLOTRQST   TEST REQUEST CARD OVERRIDE                   
         BZ    REQIND26                                                         
                                                                                
         CLI   QCLOFILT,QCLOBOTH   BOTH CLOSED AND OPEN JOBS                    
         BNE   *+12                                                             
         OI    ACMINDS4,ACMICLOY+ACMICLON                                       
         B     REQIND26                                                         
         CLI   QCLOFILT,QCLONONE   OPEN JOBS ONLY                               
         BNE   *+12                                                             
         OI    ACMINDS4,ACMICLON                                                
         B     REQIND26                                                         
         CLI   QCLOFILT,QCLOONLY   CLOSED JOBS ONLY                             
         BNE   REQIND26                                                         
         OI    ACMINDS4,ACMICLOY                                                
                                                                                
REQIND26 OI    ACMINDS4,ACMILOKY+ACMILOKN                                       
         XC    ACMALOKT,ACMALOKT   CLEAR LOCKED TABLE ENTRY ADCON               
         LHI   R0,RILOKNUM                                                      
         LTR   R0,R0               NO DRAFT TABLE ENTRIES                       
         BZ    REQIND32                                                         
         NI    ACMINDS4,FF-(ACMILOKY+ACMILOKN)                                  
                                                                                
         LA    R1,RILOKTAB         R1=A(LOCKED OVERRIDE PROG TABLE)             
         USING LOKTABD,R1                                                       
REQIND28 CLC   LOKTPROG,SPACES     TEST DEFAULT ENTRY REACHED                   
         BE    REQIND30                                                         
         CLC   LOKTPROG,QPROG      MATCH REQUEST TO TABLE                       
         BE    REQIND30                                                         
         AHI   R1,LOKTABL                                                       
         BCT   R0,REQIND28                                                      
         DC    H'0'                                                             
                                                                                
REQIND30 ST    R1,ACMALOKT         SAVE A(RILOKTAB ENTRY)                       
         TM    LOKTINDS,LOKTRQST   TEST REQUEST CARD OVERRIDE                   
         BZ    REQIND32                                                         
                                                                                
         CLI   QLOKFILT,QLOKBOTH   BOTH LOCKED AND UNLOCKED ACCOUNTS            
         BNE   *+12                                                             
         OI    ACMINDS4,ACMILOKY+ACMILOKN                                       
         B     REQIND32                                                         
         CLI   QLOKFILT,QLOKNONE   UNLOCKED ACCOUNTS ONLY                       
         BNE   *+12                                                             
         OI    ACMINDS4,ACMILOKN                                                
         B     REQIND32                                                         
         CLI   QLOKFILT,QLOKONLY   LOCKED ACCOUNTS ONLY                         
         BNE   REQIND32                                                         
         OI    ACMINDS4,ACMILOKY                                                
                                                                                
REQIND32 NI    ACMINDS5,ACMIJMSE                                                
         OI    ACMINDS5,ACMIEXCN+ACMIEXCY                                       
         XC    ACMAEXCT,ACMAEXCT   CLEAR EXCLUDE TABLE ENTRY ADCON              
         LHI   R0,RIEXCNUM                                                      
         LTR   R0,R0               NO DRAFT TABLE ENTRIES                       
         BZ    REQIND38                                                         
         NI    ACMINDS5,FF-(ACMIEXCN+ACMIEXCY)                                  
                                                                                
         LA    R1,RIEXCTAB         R1=A(EXCLUDE OVERRIDE PROG TABLE)            
         USING EXCTABD,R1                                                       
REQIND34 CLC   EXCTPROG,SPACES     TEST DEFAULT ENTRY REACHED                   
         BE    REQIND36                                                         
         CLC   EXCTPROG,QPROG      MATCH REQUEST TO TABLE                       
         BE    REQIND36                                                         
         AHI   R1,EXCTABL                                                       
         BCT   R0,REQIND34                                                      
         DC    H'0'                                                             
                                                                                
REQIND36 ST    R1,ACMAEXCT         SAVE A(RIEXCTAB ENTRY)                       
         MVC   ACMWRK(1),EXCTINDS                                               
         NI    ACMWRK,ACMIEXCY+ACMIEXCN                                         
         OC    ACMINDS5,ACMWRK                                                  
         TM    EXCTINDS,EXCTRQST   TEST REQUEST CARD OVERRIDE                   
         BZ    REQIND38                                                         
         L     RF,ADQSTACK                                                      
         AHI   RF,ACQEXCL-ACQD                                                  
         CLI   0(RF),C' '          TEST OVERRIDE IN REQUEST CARD                
         BE    REQIND38                                                         
         NI    ACMINDS5,FF-(ACMIEXCY+ACMIEXCN)                                  
                                                                                
         CLI   0(RF),ACQEBOTH      BOTH INCLUDED AND EXCLUDED TRANS             
         BNE   *+12                                                             
         OI    ACMINDS5,ACMIEXCY+ACMIEXCN                                       
         B     REQIND38                                                         
         CLI   0(RF),ACQENONE      NON-EXCLUDED TRANSACTIONS ONLY               
         BNE   *+12                                                             
         OI    ACMINDS5,ACMIEXCN                                                
         B     REQIND38                                                         
         CLI   0(RF),ACQEONLY      EXCLUDED TRANSACTIONS ONLY                   
         BNE   *+12                                                             
         OI    ACMINDS5,ACMIEXCY                                                
         B     REQIND38                                                         
         DC    H'0'                                                             
*&&                                                                             
REQIND38 DS    0H                                                               
*&&UK                                                                           
         L     RE,ADQSTACK         TEST RUNNING IN 2ND CURRENCY                 
         CLI   ACQCURR-ACQD(RE),ACQC2ND                                         
         BNE   *+8                                                              
         OI    ACMINDS5,ACMI2NDC                                                
*&&                                                                             
         MVI   ACMINDS6,0                                                       
*&&UK                                                                           
         CLI   QWCTYPE,ACQWCALL    TEST NO SPECIAL FILTER                       
         BE    REQIND40                                                         
         CLI   QWCTYPE,ACQWCTFC    TEST TIME FOLLOWED BY COST                   
         BNE   *+12                                                             
         MVI   ACMINDS6,ACMITORD+ACMITTIM+ACMITCST+ACMITBIL                     
         B     REQIND40                                                         
         CLI   QWCTYPE,ACQWCTIM    TEST TIME                                    
         BNE   *+12                                                             
         MVI   ACMINDS6,ACMITORD+ACMITTIM+ACMITBIL                              
         B     REQIND40                                                         
         CLI   QWCTYPE,ACQWCCST    TEST TIME                                    
         BNE   *+12                                                             
         MVI   ACMINDS6,ACMITORD+ACMITCST+ACMITBIL                              
         B     REQIND40                                                         
         CLI   QWCTYPE,ACQWCORB    TEST ORDERS & BILLS                          
         BNE   *+12                                                             
         MVI   ACMINDS6,ACMITORD+ACMITBIL                                       
         B     REQIND40                                                         
         CLI   QWCTYPE,ACQWCORD    TEST ORDERS                                  
         BNE   *+12                                                             
         MVI   ACMINDS6,ACMITORD                                                
         B     REQIND40                                                         
         CLI   QWCTYPE,ACQWCBIL    TEST BILLS                                   
         BNE   *+12                                                             
         MVI   ACMINDS6,ACMITBIL                                                
         B     REQIND40                                                         
         DC    H'0'                                                             
*&&                                                                             
REQIND40 DS    0H                                                               
                                                                                
REQINDX  J     EXIT                                                             
         DROP  R1,RB                                                            
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO SET LEDGER LEVEL OPTION INDICATORS FOR REVERSES, DRAFTS  *         
* AND XJOBS                                                           *         
***********************************************************************         
                                                                                
LDGIND   NTR1  BASE=*,LABEL=*                                                   
         TM    ACMINDS,ACMITRVN+ACMITRVY                                        
         BNZ   LDGIND04                                                         
         ICM   R1,15,ACMAREVT                                                   
         BNZ   *+6                                                              
         DC    H'0'                                                             
         USING REVTABD,R1                                                       
         MVC   ACMWRK(1),REVTINDS  EXTRACT DEFAULT FROM TABLE                   
         NI    ACMWRK,REVTREVN+REVTREVY                                         
         SR    RE,RE                                                            
         ICM   RE,1,REVTPROF                                                    
         BZ    LDGIND02                                                         
         A     RE,APROFILE                                                      
         AHI   RE,ACPPFLDG-1-ACPROFSD                                           
         CLI   0(RE),QREVBOTH      REVERSED & NON-REVERSED                      
         BNE   *+12                                                             
         OI    ACMINDS,ACMITRVY+ACMITRVN                                        
         B     LDGIND04                                                         
         CLI   0(RE),QREVNONE      NON-REVERSED ONLY                            
         BNE   *+12                                                             
         OI    ACMINDS,ACMITRVN                                                 
         B     LDGIND04                                                         
         CLI   0(RE),QREVONLY      REVERSED ONLY                                
         BNE   *+12                                                             
         OI    ACMINDS,ACMITRVY                                                 
         B     LDGIND04                                                         
         DROP  R1                                                               
                                                                                
LDGIND02 OC    ACMINDS,ACMWRK      SET DEFAULT FROM TABLE ENTRY                 
         TM    ACMINDS,ACMITRVY+ACMITRVN                                        
         BNZ   LDGIND04                                                         
         OI    ACMINDS,ACMITRVY+ACMITRVN                                        
                                                                                
LDGIND04 TM    ACMINDS2,ACMIDRAY+ACMIDRAN TEST USER INPUT DRAFT OPTION          
         BNZ   LDGIND08            YES                                          
                                                                                
         ICM   R1,15,ACMADRAT      GET DRAFT TABLE ENTRY                        
         BNZ   *+6                                                              
         DC    H'0'                                                             
         USING DRATABD,R1                                                       
         MVC   ACMWRK(1),DRATINDS                                               
         NI    ACMWRK,ACMIDRAY+ACMIDRAN ISOLATE DRAFT OPTION BITS               
                                                                                
         SR    RE,RE                                                            
         ICM   RE,1,DRATPROF       GET POSITION IN REPORT PROFILE               
         BZ    LDGIND06            PROFILE DOES NOT HAVE DRAFT OPTION           
         A     RE,APROFILE                                                      
         AHI   RE,ACPPFLDG-1-ACPROFSD                                           
                                                                                
         CLI   0(RE),QDRABOTH      TEST PROFILE=LIVE+DRAFT                      
         BNE   *+12                                                             
         OI    ACMINDS2,ACMIDRAY+ACMIDRAN                                       
         B     LDGIND08                                                         
                                                                                
         CLI   0(RE),QDRANONE      TEST PROFILE=NON-DRAFT(LIVE)                 
         BNE   *+12                                                             
         OI    ACMINDS2,ACMIDRAN                                                
         B     LDGIND08                                                         
                                                                                
         CLI   0(RE),QDRAONLY      TEST PROFILE=ONLY DRAFT                      
         BNE   *+12                                                             
         OI    ACMINDS2,ACMIDRAY                                                
         B     LDGIND08                                                         
                                                                                
LDGIND06 OC    ACMINDS2,ACMWRK     SET DRAFT TABLE BITS                         
         TM    ACMINDS2,ACMIDRAY+ACMIDRAN TEST ANY BITS SET                     
         BNZ   LDGIND08                                                         
         OI    ACMINDS2,ACMIDRAN   SET DEFAULT=NON-DRAFT                        
                                                                                
LDGIND08 TM    ACMINDS2,ACMIXJBY+ACMIXJBN TEST USER INPUT XJOB OPTION           
         BNZ   LDGIND12            YES                                          
                                                                                
         ICM   R1,15,ACMAXJBT      GET XJOB TABLE ENTRY                         
         BNZ   *+6                                                              
         DC    H'0'                                                             
         USING XJBTABD,R1                                                       
         MVC   ACMWRK(1),XJBTINDS                                               
         NI    ACMWRK,ACMIXJBY+ACMIXJBN ISOLATE XJOB OPTION BITS                
                                                                                
         SR    RE,RE                                                            
         ICM   RE,1,XJBTPROF       GET POSITION IN REPORT PROFILE               
         BZ    LDGIND10            PROFILE DOES NOT HAVE DRAFT OPTION           
         A     RE,APROFILE                                                      
         AHI   RE,ACPPFLDG-1-ACPROFSD                                           
                                                                                
         CLI   0(RE),QXJBOTH       TEST PROFILE=XJOB+NON-XJOB                   
         BNE   *+12                                                             
         OI    ACMINDS2,ACMIXJBY+ACMIXJBN                                       
         B     LDGIND12                                                         
                                                                                
         CLI   0(RE),QXJNONE       TEST PROFILE=NON-XJOB                        
         BNE   *+12                                                             
         OI    ACMINDS2,ACMIXJBN                                                
         B     LDGIND12                                                         
                                                                                
         CLI   0(RE),QXJONLY       TEST PROFILE=ONLY XJOB                       
         BNE   *+12                                                             
         OI    ACMINDS2,ACMIXJBY                                                
         B     LDGIND12                                                         
                                                                                
LDGIND10 OC    ACMINDS2,ACMWRK     SET XJOB TABLE BITS                          
         TM    ACMINDS2,ACMIXJBY+ACMIXJBN TEST ANY BITS SET                     
         BNZ   LDGIND12                                                         
         OI    ACMINDS2,ACMIXJBN   SET DEFAULT=NON-XJOB                         
         DROP  R1                                                               
                                                                                
*&&US                                                                           
LDGIND12 TM    ACMINDS5,ACMIDJBY+ACMIDJBN TEST USER INPUT DJOB OPTION           
         BNZ   LDGIND20            YES                                          
                                                                                
         ICM   R1,15,ACMADJBT      GET DJOB TABLE ENTRY                         
         BNZ   *+6                                                              
         DC    H'0'                                                             
         USING DJBTABD,R1                                                       
         MVC   ACMWRK(1),DJBTINDS                                               
         NI    ACMWRK,ACMIDJBY+ACMIDJBN ISOLATE DJOB OPTION BITS                
                                                                                
         SR    RE,RE                                                            
         ICM   RE,1,DJBTPROF       GET POSITION IN REPORT PROFILE               
         BZ    LDGIND14            PROFILE DOES NOT HAVE DRAFT OPTION           
         A     RE,APROFILE                                                      
         AHI   RE,ACPPFLDG-1-ACPROFSD                                           
                                                                                
         CLI   0(RE),QXJBOTH       TEST PROFILE=DJOB+NON-DJOB                   
         BNE   *+12                                                             
         OI    ACMINDS5,ACMIDJBY+ACMIDJBN                                       
         B     LDGIND20                                                         
                                                                                
         CLI   0(RE),QXJNONE       TEST PROFILE=NON-DJOB                        
         BNE   *+12                                                             
         OI    ACMINDS5,ACMIDJBN                                                
         B     LDGIND20                                                         
                                                                                
         CLI   0(RE),QXJONLY       TEST PROFILE=ONLY DJOB                       
         BNE   *+12                                                             
         OI    ACMINDS5,ACMIDJBY                                                
         B     LDGIND20                                                         
                                                                                
LDGIND14 OC    ACMINDS5,ACMWRK     SET DJOB TABLE BITS                          
         TM    ACMINDS5,ACMIDJBY+ACMIDJBN TEST ANY BITS SET                     
         BNZ   LDGIND20                                                         
         OI    ACMINDS5,ACMIDJBN   SET DEFAULT=NON-DJOB                         
         DROP  R1                                                               
*&&                                                                             
*&&UK                                                                           
LDGIND12 TM    ACMINDS4,ACMICLOY+ACMICLON TEST USER INPUT CLOSED FILT           
         BNZ   LDGIND16            YES                                          
                                                                                
         ICM   R1,15,ACMACLOT      GET CLOSED TABLE ENTRY                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         USING CLOTABD,R1                                                       
         MVC   ACMWRK(1),CLOTINDS                                               
         NI    ACMWRK,ACMICLOY+ACMICLON ISOLATE OPTION BITS                     
                                                                                
         SR    RE,RE                                                            
         ICM   RE,1,CLOTPROF       GET POSITION IN REPORT PROFILE               
         BZ    LDGIND14            PROFILE DOES NOT HAVE DRAFT OPTION           
         A     RE,APROFILE                                                      
         AHI   RE,ACPPFLDG-1-ACPROFSD   )                                       
                                                                                
         CLI   0(RE),QCLOBOTH      TEST PROFILE=OPEN+CLOSED                     
         BNE   *+12                                                             
         OI    ACMINDS4,ACMICLOY+ACMICLON                                       
         B     LDGIND16                                                         
                                                                                
         CLI   0(RE),QCLONONE      TEST PROFILE=OPEN ONLY                       
         BNE   *+12                                                             
         OI    ACMINDS4,ACMICLON                                                
         B     LDGIND16                                                         
                                                                                
         CLI   0(RE),QCLOONLY      TEST PROFILE=CLOSED ONLY                     
         BNE   *+12                                                             
         OI    ACMINDS4,ACMICLOY                                                
         B     LDGIND16                                                         
                                                                                
LDGIND14 OC    ACMINDS4,ACMWRK     SET CLOSED TABLE BITS                        
         TM    ACMINDS4,ACMICLOY+ACMICLON TEST ANY BITS SET                     
         BNZ   LDGIND16                                                         
         OI    ACMINDS4,ACMICLOY+ACMICLON                                       
         DROP  R1                                                               
                                                                                
LDGIND16 TM    ACMINDS4,ACMILOKY+ACMILOKN TEST USER INPUT LOCKED FILT           
         BNZ   LDGIND20            YES                                          
                                                                                
         ICM   R1,15,ACMALOKT      GET LOCKED TABLE ENTRY                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         USING LOKTABD,R1                                                       
         MVC   ACMWRK(1),LOKTINDS                                               
         NI    ACMWRK,ACMILOKY+ACMILOKN ISOLATE OPTION BITS                     
                                                                                
         SR    RE,RE                                                            
         ICM   RE,1,LOKTPROF       GET POSITION IN REPORT PROFILE               
         BZ    LDGIND18            PROFILE DOES NOT HAVE DRAFT OPTION           
         A     RE,APROFILE                                                      
         AHI   RE,ACPPFLDG-1-ACPROFSD                                           
                                                                                
         CLI   0(RE),QLOKBOTH      TEST PROFILE=LOCKED+UNLOCKED                 
         BNE   *+12                                                             
         OI    ACMINDS4,ACMILOKY+ACMILOKN                                       
         B     LDGIND20                                                         
                                                                                
         CLI   0(RE),QLOKNONE      TEST PROFILE=UNLOCKED ONLY                   
         BNE   *+12                                                             
         OI    ACMINDS4,ACMILOKN                                                
         B     LDGIND20                                                         
                                                                                
         CLI   0(RE),QLOKONLY      TEST PROFILE=LOCKED ONLY                     
         BNE   *+12                                                             
         OI    ACMINDS4,ACMILOKY                                                
         B     LDGIND20                                                         
                                                                                
LDGIND18 OC    ACMINDS4,ACMWRK     SET LOCKED TABLE BITS                        
         TM    ACMINDS4,ACMILOKY+ACMILOKN TEST ANY BITS SET                     
         BNZ   LDGIND20                                                         
         OI    ACMINDS4,ACMILOKY+ACMILOKN                                       
         DROP  R1                                                               
*&&                                                                             
LDGIND20 TM    ACMINDS,ACMINEWO    TEST NEW OFFICE SYSTEM IN USE                
         BNZ   LDGIND22                                                         
         L     RF,ADCMPEL          TEST COMPANY USES OFFICES                    
         TM    CPYSTAT1-CPYELD(RF),CPYSOROE                                     
         BZ    LDGIND22                                                         
         CLI   ACMOFPOS,1          TEST OFFPOS=1 FOR REQUESTED LEDGER           
         BNE   LDGIND22                                                         
         CLC   QACCOUNT,SPACES     TEST SPECIFIC ACCOUNT REQUESTED              
         BNE   LDGIND22                                                         
         CLI   MCC1ACCS,C'*'       TEST LIMIT ACCESS LOGON                      
         BNE   LDGIND22                                                         
         MVC   QACCOUNT(1),MCC1ACCS+1                                           
         OI    ACMINDS3,ACMIAUTO   SET AUTO ACCOUNT SET                         
                                                                                
LDGIND22 NI    ACMINDS,FF-ACMIPBUK TURNOFF PASS TRANS. TO ACBUCKET              
         TM    ACMINDS,ACMINEWO    TEST NEW OFFICE SYSTEM IN USE                
         BNO   LDGIND24            NO, DOES NOT APPLY                           
         L     RF,ADLEDGER                                                      
         CLI   LDGKUNT-LDGKEY(RF),C'G'                                          
         BE    LDGIND24            UNITS G & S ALREADY HAVE BUCKETS             
         CLI   LDGKUNT-LDGKEY(RF),C'S'                                          
         BE    LDGIND24                                                         
         CLC   =C'1C',LDGKUNT-LDGKEY(RF)                                        
         BE    LDGIND24                                                         
         OI    ACMINDS,ACMIPBUK    PASS TRANSACTIONS TO ACBUCKET                
                                                                                
LDGIND24 DS    0H                                                               
                                                                                
LDGINDX  J     EXIT                                                             
         DROP  RB                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO ESTABLISH SYSTEM AND PROGRAM PROFILES                    *         
* NTRY P1=A(ADDRESS OF RECORD)                                        *         
***********************************************************************         
                                                                                
PROFILES NTR1  BASE=*,LABEL=*                                                   
         L     R2,0(R1)            R2=A(RECORD)                                 
         L     R4,APROFILE                                                      
         USING ACPROFSD,R4         R4=A(PROFILE BLOCK)                          
         XC    ACMPFKEY,ACMPFKEY                                                
         XC    ACMPACC,ACMPACC                                                  
         MVI   ACMPFSYS,C'A'                                                    
         MVC   ACMPFPGM,=C'0000'                                                
         MVC   ACMPFAGY,ALPHAID                                                 
         LA    R3,ACPPFCPY         POINT TO COMPANY PROFILE BLOCK               
         CLI   1(R2),C' '          TEST COMPANY RECORD PASSED                   
         BE    PROF08                                                           
         LA    R3,ACPPFLDG         POINT TO LEDGER PROFILE BLOCK                
         MVC   ACMPFUNL,1(R2)      UNIT/LEDGER                                  
         CLI   3(R2),C' '          TEST LEDGER RECORD PASSED                    
         BE    PROF08                                                           
         LA    R3,ACPPFACC         POINT TO ACCOUNT PROFILE BLOCK               
         MVC   ACMPFACC,ACTKACT-ACTRECD(R2)                                     
                                                                                
         CLI   ACMSYS,ACMSEUR                                                   
         BNE   PROF02                                                           
         CLC   6(2,R2),SPACES      DO WE HAVE A 5 CHAR LEVEL A                  
         BE    PROF02                                                           
         MVC   ACMWRK(4),=C'0000'  IF SO AND THE LAST 4 ARE NUMERIC             
         MVZ   ACMWRK(4),4(R2)                                                  
         CLC   ACMWRK(4),=C'0000'                                               
         BNE   PROF02                                                           
         PACK  ACMDUB,4(4,R2)      CONVERT THE LAST 4 TO BINARY                 
         CVB   R0,ACMDUB                                                        
         STCM  R0,3,ACMPFACC+1     AND STICK IN THE PROFILE KEY                 
                                                                                
PROF02   TM    ACMINDS,ACMILDGP    DID I GET LEDGER PROFILES                    
         BZ    PROFX               IF NOT DON'T LOOK AT ACCOUNT                 
         LR    R1,R2                                                            
         AH    R1,DATADISP                                                      
         SR    R0,R0                                                            
         USING PPRELD,R1                                                        
PROF04   CLI   PPREL,0                                                          
         BE    PROF08                                                           
         CLI   PPREL,PPRELQ                                                     
         BE    *+14                                                             
         IC    R0,PPRLN                                                         
         AR    R1,R0                                                            
         B     PROF04                                                           
                                                                                
         TM    ACMINDS,ACMINEWO                                                 
         BZ    PROF06                                                           
         CLI   PPRGAOFF,X'41'                                                   
         BL    PROF08                                                           
         MVI   ACMPFIND,ACMPFNEW   SET NEW OFFICE INDICATOR                     
         MVC   ACMPFOF2,PPRGAOFF   SET 2 CHARACTER OFFICE CODE                  
         LA    R3,ACPPFOFF                                                      
         B     PROF08                                                           
                                                                                
PROF06   CLI   PPRUFORA,X'41'                                                   
         BL    PROF08                                                           
         MVI   ACMPFIND,ACMPFOLD   SET OLD OFFICE INDICATOR                     
         MVC   ACMPFOFF,PPRGAOFF   SET 1 CHARACTER OFFICE CODE                  
         CLI   ACMPFOFF,X'41'                                                   
         BNL   *+10                                                             
         MVC   ACMPFOFF,PPRUFORA                                                
         LA    R3,ACPPFOFF                                                      
         DROP  R1                                                               
                                                                                
PROF08   DS    0H                  GET SYSPROF AT ANY LEVEL                     
         GOTOR GETPROF,ACMPARA,ACMPFKEY,ACPSYSP1,(X'FF',DATAMGR),      *        
               (10,ACMAPRF)                                                     
         MVC   SYSPROF,ACPSYSPF                                                 
                                                                                
         NI    ACMPFSYS,X'BF'      GET SYSPROF SECOND PROFILE SET               
         MVC   ACMPFPGM(2),ACMPFPGM+1                                           
         MVI   ACMPFPGM+2,C'A'                                                  
         GOTOR (RF),(R1),,ACPSYSP2                                              
                                                                                
         MVI   ACMPFSYS,C'A'                                                    
         MVI   ACMPFPGM,C'0'                                                    
         MVC   ACMPFPGM+1(2),RCPROGGP ESTABLISH OLD PROGPROF                    
         GOTOR (RF),(R1),,PROGPROF                                              
                                                                                
         MVI   ACMPFSYS,C'A'                                                    
         MVI   ACMPFPGM,C'0'                                                    
         MVC   ACMPFPGM+1(2),RCPROGGP                                           
         CLI   ACMPFIND,ACMPFNEW   TEST OFFICE PROFILES REQUESTED               
         BE    *+12                                                             
         CLI   ACMPFIND,ACMPFOLD                                                
         BNE   PROF10                                                           
         MVC   ACMPACC,ACMPFACC    YES - SAVE ACCOUNT KEY VALUE                 
         XC    ACMPFACC,ACMPFACC                                                
                                                                                
PROF10   GOTOR PROFGET             GET CPY/UNIT/LEDGER/OFFICE PROFILE           
         OC    ACMPACC,ACMPACC     TEST SAVED ACCOUNT SET                       
         BZ    PROFX                                                            
                                                                                
         MVI   ACMPFSYS,C'A'                                                    
         MVI   ACMPFPGM,C'0'                                                    
         MVC   ACMPFPGM+1(2),RCPROGGP                                           
         MVC   ACMPFACC,ACMPACC                                                 
         LA    R3,ACPPFACC                                                      
         GOTOR PROFGET             GET ACCOUNT PROFILES                         
         B     PROFX                                                            
                                                                                
PROFX    J     EXIT                                                             
                                                                                
         USING ACPPFACC,R3                                                      
PROFGET  LR    R0,RE               GET THREE SETS OF PROFILES                   
         GOTOR GETPROF,ACMPARA,,ACPPFAC1                                        
         MVC   ACMPFPGM(2),ACMPFPGM+1                                           
         MVI   ACMPFPGM+2,C'A'                                                  
         NI    ACMPFSYS,X'BF'                                                   
         GOTOR (RF),(R1),,ACPPFAC2                                              
         MVI   ACMPFPGM+2,C'B'                                                  
         GOTOR (RF),(R1),,ACPPFAC3                                              
PROFGETX LR    RE,R0                                                            
         BR    RE                                                               
         DROP  R3,R4,RB                                                         
         EJECT                                                                  
***********************************************************************         
* GET OFFICE NAMES FOR A NEW OFFICES COMPANY                          *         
***********************************************************************         
                                                                                
GETOFN   NTR1  BASE=*,LABEL=*                                                   
         L     R2,ACMAOFNB         R2=A(OFFICE NAME ENTRY)                      
         L     R3,ACMABUF                                                       
         USING OFFRECD,R3                                                       
         SR    R4,R4               R4=NUMBER OF ENTRIES IN BUFFER               
         MVC   OFFKEY,SPACES                                                    
         MVI   OFFKTYP,OFFKTYPQ                                                 
         MVC   OFFKCPY,RCCOMPFL                                                 
         MVC   ACMKEY(L'OFFKEY),OFFKEY SAVE ORIGINAL KEY                        
         GOTOR DATAMGR,DMCB,DMRDHI,ACMACFIL,OFFRECD,OFFRECD                     
         B     GETOFN04                                                         
                                                                                
GETOFN02 GOTOR DATAMGR,DMCB,DMRSEQ,ACMACFIL,OFFRECD,OFFRECD                     
                                                                                
GETOFN04 BE    *+14                                                             
         TM    8(R1),X'80'         TEST FOR EOF                                 
         BNZ   GETOFN10                                                         
         DC    H'0'                                                             
         CLC   OFFKEY(OFFKOFF-OFFKEY),ACMKEY  TEST SAME COMPANY                 
         BNE   GETOFN10                                                         
                                                                                
         MVC   0(L'OFFKOFF,R2),OFFKOFF  EXTRACT THE OFFICE CODE                 
         MVC   L'OFFKOFF(L'NAMEREC,R2),SPACES                                   
                                                                                
         LA    R1,OFFRECD                                                       
         AH    R1,DATADISP         NOW GET THE NAME                             
         SR    R0,R0                                                            
         USING NAMELD,R1                                                        
GETOFN06 CLI   NAMEL,0             TEST FOR EOR                                 
         BE    GETOFN08            YES                                          
         CLI   NAMEL,NAMELQ                                                     
         BE    *+14                                                             
         IC    R0,NAMLN                                                         
         AR    R1,R0                                                            
         B     GETOFN06                                                         
         SR    RE,RE               EXTRACT NAME INTO BUFFER                     
         IC    RE,NAMLN                                                         
         SHI   RE,NAMLN1Q+1                                                     
         EX    RE,*+8                                                           
         B     GETOFN08                                                         
         MVC   L'OFFKOFF(0,R2),NAMEREC                                          
                                                                                
GETOFN08 AHI   R2,ACMOFNL                                                       
         AHI   R4,1                                                             
         B     GETOFN02                                                         
                                                                                
GETOFN10 ST    R4,ACMNOFNB         SAVE NUMBER OF TABLE ENTRIES                 
         XC    ACMKEY,ACMKEY       TIDY UP AFTERWARDS                           
                                                                                
GETOFNX  J     EXIT                                                             
         DROP  R1,R3,RB                                                         
         EJECT                                                                  
***********************************************************************         
* BUILD A LIST OF OFFICES USING LIMIT ACCESS OF ORIGIN ID             *         
***********************************************************************         
                                                                                
SETOFF   NTR1  BASE=*,LABEL=*                                                   
         L     R2,ACMAOFL          INITIALISE OFFAL BLOCK                       
         ST    R2,ADOFFALD         SET A(OFFAL BLOCK FOR APPLICATIONS)          
         USING OFFALD,R2           R2=A(OFFAL CONTROL BLOCK)                    
         LA    R0,ACMROFL                                                       
         ST    R0,ADOFFLST         SET A(OFFLICE LIST IN ACWORKD)               
         LA    R0,ACMOFCN                                                       
         ST    R0,OFFAREQL                                                      
         MVC   OFFAALPH,ALPHAID                                                 
         MVC   OFFACPY,RCCOMPFL                                                 
         MVC   OFFACOMF,ADCOMFAC                                                
         ICM   R1,15,ADCMPEL                                                    
         JNZ   *+6                                                              
         DC    H'0'                                                             
         MVC   OFFACST1,CPYSTAT1-CPYELD(R1)                                     
         MVC   OFFACST2,CPYSTAT2-CPYELD(R1)                                     
         MVC   OFFACST3,CPYSTAT3-CPYELD(R1)                                     
         MVC   OFFACST4,CPYSTAT4-CPYELD(R1)                                     
         MVC   OFFALIMA,MCC1ACCS                                                
         MVI   OFFAACT,OFFAINI                                                  
         GOTOR ADOFFAL,OFFALD      CALL OFFAL TO INITIALISE                     
         JE    SETOFF20                                                         
         TM    OFFAERR,OFFAESEC                                                 
         JO    *+6                                                              
         DC    H'00'                                                            
         TM    ACMINDS3,ACMIMULT   TEST MULTI REQUEST                           
         JO    EXITLO              NO OFFICES FOUND                             
         DC    H'0'                                                             
                                                                                
SETOFF20 TM    ACMINDS,ACMINEWO    TEST NEW OFFICES                             
         JNZ   EXITEQ                                                           
         MVC   ACMOFCL(ACMOFCLL),OFFAWORK                                       
         J     EXITEQ                                                           
         DROP  R2,RB                                                            
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO SET OFFICE (2D ACCOUNT NAME) IN COMPANY RECORD IF        *         
* REQUEST IS FOR A SINGLE OFFICE ELSE COMPANY NAME IS USED            *         
***********************************************************************         
                                                                                
SETOFN   NTR1  BASE=*,LABEL=*                                                   
         LA    R3,SVCPYNAM                                                      
         CLI   QOFFICE,C' '                                                     
         BE    SETOFN04                                                         
         TM    QOFFICE,X'40'       CAN'T BE 'NOT'                               
         BZ    SETOFN04                                                         
         LA    R2,ACMKEY           OFFICE REQUEST - READ OFFICE RECORD          
         USING ACTRECD,R2          AND USE ITS NAME INSTEAD                     
         MVC   ACTKEY,SPACES                                                    
         MVC   ACTKCPY,RCCOMPFL                                                 
         MVI   ACTKUNT,C'2'                                                     
         MVI   ACTKLDG,C'D'                                                     
         MVC   ACTKACT,QOFFICE                                                  
         GOTOR DATAMGR,DMCB,DMRDHI,ACMACDIR,ACTRECD,ACTRECD                     
         BNE   SETOFN04                                                         
         MVC   ACMDA,ACTKDA                                                     
         L     R2,ACMABUF                                                       
         GOTOR DATAMGR,DMCB,ACMDMGET,ACMACMST,ACMDA,ACTRECD,ACMWRK              
         BNE   SETOFN04                                                         
                                                                                
         LA    R1,ACTRFST                                                       
         USING NAMELD,R1                                                        
         SR    R0,R0                                                            
SETOFN02 CLI   NAMEL,0                                                          
         BE    SETOFN04                                                         
         CLI   NAMEL,NAMELQ                                                     
         BE    *+14                                                             
         IC    R0,NAMLN                                                         
         AR    R1,R0                                                            
         B     SETOFN02                                                         
         LA    R3,NAMELD                                                        
                                                                                
SETOFN04 L     R1,ADCMPNAM                                                      
         SR    RE,RE                                                            
         IC    RE,NAMLN                                                         
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         CLC   NAMELD(0),0(R3)     TEST NAME NEEDS CHANGING                     
         BE    SETOFNX                                                          
                                                                                
         MVI   NAMEL,X'FF'                                                      
         GOTOR ACMVHELO,ACMPARA,(C'D',ACMACSYS),(X'FF',ADCOMP),0                
         GOTOR (RF),(R1),(C'P',ACMACSYS),ADCOMP,(R3),0                          
         GOTOR SETCPY                                                           
                                                                                
SETOFNX  J     EXIT                                                             
         DROP  R1,R2,RB                                                         
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO INITIALISE ACCOUNT GROUP BUFFER                          *         
***********************************************************************         
                                                                                
SETGRP   NTR1  BASE=*,LABEL=*                                                   
         OC    ACMAGRPA,ACMAGRPA   TEST BUFFER ACQUIRED & INITIALISED           
         BNZ   SETGRP18                                                         
         L     R0,=A(GRPTABL*GRPTMAXN+1)                                        
         GETMAIN RU,LV=(0),LOC=(ANY,ANY),BNDRY=PAGE                             
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
         LR    R3,R1                                                            
         ST    R3,ACMAGRPA         SAVE A(ACCOUNT GROUP BUFFER)                 
         USING GRPTABD,R3                                                       
         SR    R4,R4                                                            
                                                                                
         LA    R2,ACMKEY                                                        
         USING AGRRECD,R2                                                       
         XC    AGRKEY,AGRKEY                                                    
         MVI   AGRKTYP,AGRKTYPQ                                                 
         MVC   AGRKCPY,RCCOMPFL                                                 
         MVC   ACMDUB(AGRKGTYP-AGRRECD),AGRRECD                                 
         GOTOR DATAMGR,DMCB,DMRDHI,ACMACDIR,AGRRECD,AGRRECD                     
         B     SETGRP04                                                         
                                                                                
SETGRP02 GOTOR DATAMGR,DMCB,DMRSEQ,ACMACDIR,AGRRECD,AGRRECD                     
                                                                                
SETGRP04 BNE   SETGRP16                                                         
         CLC   AGRRECD(AGRKGTYP-AGRRECD),ACMDUB                                 
         BNE   SETGRP16                                                         
         MVC   ACMDA,AGRKDA                                                     
         GOTOR DATAMGR,DMCB,ACMDMGET,ACMACMST,ACMDA,ACMABUF,ACMWRK              
         BE    *+6                                                              
         DC    H'0'                                                             
                                                                                
         SAM31                     ENTER XA MODE                                
         CHI   R4,GRPTMAXN         TEST GROUP BUFFER IS FULL                    
         BL    *+6                                                              
         DC    H'0'                YES - CAN'T DO MUCH ABOUT THAT               
         AHI   R4,1                INCREMENT N'ENTRIES IN BUFFER                
         XC    GRPTABD(GRPTABL),GRPTABD                                         
         MVC   GRPTTYP,AGRKGTYP    SET GROUP TYPE AND CODE                      
         MVC   GRPTCOD,AGRKAGR                                                  
                                                                                
         L     R1,ACMABUF                                                       
         AHI   R1,AGRRFST-AGRRECD                                               
         SR    R0,R0                                                            
SETGRP06 CLI   0(R1),0             TEST END OF RECORD                           
         BE    SETGRP14                                                         
         CLI   0(R1),NAMELQ        TEST NAME ELEMENT                            
         BE    SETGRP10                                                         
         CLI   0(R1),RSTELQ        TEST STATUS ELEMENT                          
         BE    SETGRP12                                                         
                                                                                
SETGRP08 IC    R0,1(R1)            BUMP TO NEXT ELEMENT                         
         AR    R1,R0                                                            
         B     SETGRP06                                                         
                                                                                
         USING NAMELD,R1                                                        
SETGRP10 SR    RE,RE               EXTRACT GROUP NAME                           
         IC    RE,NAMLN                                                         
         SHI   RE,NAMLN1Q+1                                                     
         MVC   GRPTNAM,SPACES                                                   
         EX    RE,*+8                                                           
         B     SETGRP08                                                         
         MVC   GRPTNAM(0),NAMEREC                                               
                                                                                
         USING RSTELD,R1                                                        
SETGRP12 MVC   GRPTOFF,RSTOFFC     EXTRACT GROUP OFFICE CODE                    
         B     SETGRP08                                                         
                                                                                
SETGRP14 MVC   BYTE,AGRKGTYP                                                    
         NI    BYTE,X'0F'                                                       
         SR    RE,RE                                                            
         IC    RE,BYTE                                                          
         SLL   RE,2                                                             
         LA    RE,ACMAGRP1-4(RE)                                                
         OC    0(4,RE),0(RE)                                                    
         BNZ   *+8                                                              
         ST    R3,0(RE)                                                         
         AHI   R3,GRPTABL          BUMP TO NEXT OUTPUT TABLE ENTRY              
         MVI   GRPTTYP,GRPTEOTQ    SET END OF BUFFER                            
         SAM24                     EXIT XA MODE                                 
         B     SETGRP02            GET NEXT RECORD                              
                                                                                
SETGRP16 LTR   R4,R4               TEST ANY ENTRIES IN BUFFER                   
         BZ    SETGRPX                                                          
                                                                                
SETGRP18 XC    ACMAGRPN,ACMAGRPN   CLEAR NUMBER OF ACCOUNT GROUPS               
         MVC   BYTE,QACCOUNT+1     LOCATE ENTRY FOR GROUP TYPE                  
         NI    BYTE,X'0F'                                                       
         SR    R1,R1                                                            
         IC    R1,BYTE                                                          
         SLL   R1,2                                                             
         LA    R1,ACMAGRP1-4(R1)                                                
         SAM31                     ENTER XA MODE                                
         ICM   R3,15,0(R1)         GET A(GROUP BUFFER)                          
         BZ    SETGRPX                                                          
         CLI   QACCOUNT+3,C'&&'    TEST ALL GROUP REQUEST                       
         BNE   SETGRP20                                                         
         SR    RE,RE                                                            
         ST    R3,ACMAGRPS         SAVE A(FIRST/NEXT GROUP ENTRY)               
         AHI   R3,GRPTABL          BUMP TO NEXT ENTRY                           
         AHI   RE,1                AND INCREMENT NUMBER OF ENTRIES              
         CLC   GRPTTYP,QACCOUNT+1                                               
         BE    *-14                                                             
         STH   RE,ACMAGRPN         SAVE N'ENTRIES TO PROCESS                    
         B     SETGRPX                                                          
                                                                                
SETGRP20 CLC   GRPTTYP,QACCOUNT+1  LOCATE BUFFER ENTRY FOR GROUP                
         BNE   SETGRPX                                                          
         CLC   GRPTCOD,QACCOUNT+3  (MATCH ON TYPE AND CODE)                     
         BE    *+12                                                             
         AHI   R3,GRPTABL          BUMP TO NEXT ENTRY IF NO MATCH               
         B     SETGRP20                                                         
         ST    R3,ACMAGRPS         SAVE A(GROUP ENTRY)                          
         MVC   ACMAGRPN,=H'1'      SET ONE ENTRY IN BUFFER                      
         B     SETGRPX                                                          
                                                                                
SETGRPX  SAM24                     EXIT XA MODE & RETURN WITH CC SET            
         OC    ACMAGRPN,ACMAGRPN   TEST ANY ENTRIES IN BUFFER                   
         J     EXIT                                                             
         DROP  R2,R3,RB                                                         
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO INITIALISE LIST OF ACCOUNTS FOR AN ACCOUNT GROUP         *         
***********************************************************************         
                                                                                
SETACT   NTR1  BASE=*,LABEL=*                                                   
         OC    ACMAACTA,ACMAACTA   TEST BUFFER ACQUIRED & INITIALISED           
         BNZ   SETACT02                                                         
         L     R0,=A(L'ACTKACT*ACMAACTM)                                        
         GETMAIN RU,LV=(0),LOC=(ANY,ANY),BNDRY=PAGE                             
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
         ST    R1,ACMAACTA         SAVE A(ACCOUNT GROUP BUFFER)                 
                                                                                
SETACT02 XC    ACMAACTN,ACMAACTN   CLEAR N'ENTRIES IN BUFFER                    
         L     R3,ACMAACTA         R3=A(ACCOUNT CODE BUFFER)                    
         ST    R3,ACMAACTS         SET A(START OF BUFFER)                       
         SR    R4,R4               R4=N'ENTRIES IN BUFFER                       
         LA    R2,ACMKEY                                                        
         USING AGPRECD,R2          BUILD PASSIVE POINTER KEY                    
         XC    AGPKEY,AGPKEY                                                    
         MVI   AGPKTYP,AGPKTYPQ                                                 
         MVC   AGPKCPY,RCCOMPFL                                                 
         MVC   AGPKGTYP,QACCOUNT+1                                              
         MVC   AGPKAGR,QACCOUNT+3                                               
         MVC   AGPKUNT,QUNIT                                                    
         MVC   AGPKLDG,QLEDGER                                                  
         MVC   ACMDUB(AGPKACT-AGPRECD),AGPRECD                                  
         GOTOR DATAMGR,DMCB,DMRDHI,ACMACDIR,AGPRECD,AGPRECD                     
         B     SETACT06                                                         
                                                                                
SETACT04 GOTOR DATAMGR,DMCB,DMRSEQ,ACMACDIR,AGPRECD,AGPRECD                     
                                                                                
SETACT06 BNE   SETACT12                                                         
         CLC   AGPRECD(AGPKACT-AGPRECD),ACMDUB                                  
         BNE   SETACT12                                                         
         SAM31                     ENTER XA MODE                                
         LTR   R4,R4                                                            
         BZ    SETACT08                                                         
         CLC   AGPKACT,0(R3)       TEST FOR DUPLICATE ACCOUNT KEY               
         BE    SETACT10                                                         
         AHI   R3,L'ACTKACT                                                     
         CHI   R4,ACMAACTM         TEST ACCOUNT BUFFER IS FULL                  
         BL    *+6                                                              
         DC    H'0'                                                             
                                                                                
SETACT08 AHI   R4,1                INCREMENT N'ENTRIES IN BUFFER                
         MVC   0(L'ACTKACT,R3),AGPKACT                                          
                                                                                
SETACT10 SAM24                     EXIT XA MODE                                 
         B     SETACT04                                                         
                                                                                
SETACT12 LTR   R4,R4               TEST ANY ACCOUNTS IN BUFFER                  
         BZ    SETACTX                                                          
         STH   R4,ACMAACTN         SET N'ACCOUNTS TO PROCESS                    
                                                                                
         SAM31                     ENTER XA MODE                                
         XC    L'ACTKACT(L'ACTKACT,R3),L'ACTKACT(R3)                            
         L     R3,ACMAACTS                                                      
         LA    RE,ACMLVA           SET DEPTH OF ACCOUNTS IN BUFFER              
         LHI   R1,1                                                             
         SR    R0,R0                                                            
         IC    R0,ACMDPTH                                                       
         SHI   R0,1                                                             
         BZ    SETACT16                                                         
                                                                                
SETACT14 MVC   ACMWRK(L'ACTKACT),0(R3)                                          
         LH    RF,0(RE)                                                         
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   ACMWRK(0),SPACES                                                 
         LH    RF,2(RE)                                                         
         EX    RF,*+8                                                           
         B     *+10                                                             
         CLC   ACMWRK(0),SPACES                                                 
         BE    SETACT16                                                         
         AHI   RE,L'ACMLVA                                                      
         AHI   R1,1                                                             
         BCT   R0,SETACT14                                                      
                                                                                
SETACT16 STC   R1,ACMAACTD                                                      
                                                                                
SETACTX  SAM24                     EXIT XA MODE & RETURN WITH CC SET            
         OC    ACMAACTN,ACMAACTN   TEST ANY ENTRIES TO PROCESS                  
         J     EXIT                                                             
         DROP  R2,RB                                                            
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO INITIALISE LIST OF ACCOUNTS WITH CURRENT ACTIVITY        *         
***********************************************************************         
                                                                                
CURACT   NTR1  BASE=*,LABEL=*                                                   
         OC    ACMAACTA,ACMAACTA   TEST BUFFER ACQUIRED & INITIALISED           
         BNZ   CURACT02                                                         
         L     R0,=A(L'ACTKACT*ACMAACTM)                                        
         GETMAIN RU,LV=(0),LOC=(ANY,ANY),BNDRY=PAGE                             
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
         ST    R1,ACMAACTA         SAVE A(ACCOUNT BUFFER)                       
                                                                                
CURACT02 XC    ACMAACTN,ACMAACTN   CLEAR N'ENTRIES IN BUFFER                    
         L     R3,ACMAACTA         R3=A(ACCOUNT CODE BUFFER)                    
         ST    R3,ACMAACTS         SET A(START OF BUFFER)                       
         SR    R4,R4               R4=N'ENTRIES IN BUFFER                       
                                                                                
         USING MOSPASD,R2          BUILD PASSIVE POINTER KEY                    
         LA    R2,ACMKEY                                                        
         XC    MOSPKEY,MOSPKEY                                                  
         MVI   MOSPTYP,MOSPTYPQ                                                 
         MVC   MOSPCPY,RCCOMPFL                                                 
         MVC   MOSPMOS,ACMMSTR     MONTH                                        
         L     RF,ADLEDGER                                                      
         MVC   MOSPUNT(L'MOSPUNT+L'MOSPLDG),LDGKUNT-LDGRECD(RF)                 
         XC    ACMDUB,ACMDUB                                                    
         MVC   ACMDUB(MOSPACT-MOSPASD),MOSPASD                                  
                                                                                
CURACT04 GOTOR DATAMGR,DMCB,DMRDHI,ACMACDIR,MOSPASD,MOSPASD                     
         BNE   CURACT08                                                         
         CLC   MOSPASD(MOSPACT-MOSPASD),ACMDUB                                  
         BNE   CURACT08                                                         
         CLC   MOSPACT,SPACES                                                   
         BNE   CURACT05                                                         
         GOTOR HEXOUT,DMCB,MOSPCPY,MSGBDCPY,1,0                                 
         GOTOR HEXOUT,DMCB,MOSPMOS,MSGBDMOS,2,0                                 
         GOTOR HEXOUT,DMCB,MOSPCCPY,MSGBDCCP,1,0                                
         MVC   MSGBDULA,MOSPULA                                                 
         MVC   MSGBDWKC,MOSPWORK                                                
         MVC   MSGBDULC,MOSPULC                                                 
         GOTOR DATAMGR,DMCB,=C'OPMSG',('MSGBDLNQ',MSGBADKY)                     
         B     CURACT07            SKIP IT                                      
                                                                                
CURACT05 SAM31                     ENTER XA MODE                                
         LTR   R4,R4                                                            
         BZ    CURACT06                                                         
         AHI   R3,L'ACTKACT                                                     
         CHI   R4,ACMAACTM         TEST ACCOUNT BUFFER IS FULL                  
         BL    *+6                                                              
         DC    H'0'                                                             
                                                                                
CURACT06 AHI   R4,1                INCREMENT N'ENTRIES IN BUFFER                
         MVC   0(L'ACTKACT,R3),MOSPACT                                          
         SAM24                     EXIT XA MODE                                 
                                                                                
CURACT07 LLC   R0,MOSPACT+L'MOSPACT-1                                           
         AHI   R0,1                                                             
         STC   R0,MOSPACT+L'MOSPACT-1                                           
         XC    MOSPOFF(MOSPKSTA-MOSPOFF),MOSPOFF                                
         B     CURACT04                                                         
                                                                                
CURACT08 LTR   R4,R4               TEST ANY ACCOUNTS IN BUFFER                  
         BZ    CURACTX                                                          
         STH   R4,ACMAACTN         SET N'ACCOUNTS TO PROCESS                    
                                                                                
         SAM31                     ENTER XA MODE                                
         XC    L'ACTKACT(L'ACTKACT,R3),L'ACTKACT(R3)                            
         L     R3,ACMAACTS                                                      
         LA    RE,ACMLVA           SET DEPTH OF ACCOUNTS IN BUFFER              
         LHI   R1,1                                                             
         SR    R0,R0                                                            
         IC    R0,ACMDPTH                                                       
         SHI   R0,1                                                             
         BZ    CURACT12                                                         
                                                                                
CURACT10 MVC   ACMWRK(L'ACTKACT),0(R3)                                          
         LH    RF,0(RE)                                                         
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   ACMWRK(0),SPACES                                                 
         LH    RF,2(RE)                                                         
         EX    RF,*+8                                                           
         B     *+10                                                             
         CLC   ACMWRK(0),SPACES                                                 
         BE    CURACT12                                                         
         AHI   RE,L'ACMLVA                                                      
         AHI   R1,1                                                             
         BCT   R0,CURACT10                                                      
                                                                                
CURACT12 STC   R1,ACMAACTD                                                      
                                                                                
CURACTX  SAM24                     EXIT XA MODE & RETURN WITH CC SET            
         OC    ACMAACTN,ACMAACTN   TEST ANY ENTRIES TO PROCESS                  
         J     EXIT                                                             
                                                                                
MSGBADKY DC    C'AUTONOTE*'                                                     
         DC    C'JSHA,RDES,RGUP,DCUR,JHIG :'                                    
MSGDESC  DC    C'BAD PASSIVE=K,'                                                
MSGBDTYP DC    CL2'15'             MOSPTYPQ                                     
MSGBDCPY DC    CL2' '              COMPANY                                      
MSGBDMOS DC    CL4' '              MOS                                          
         DC    C'('                                                             
MSGBDULA DC    CL14' '             UNIT/LEDGER/ACCOUNT                          
MSGBDWKC DC    CL2' '              WORKCODE/OFFICE                              
         DC    C')'                                                             
MSGBDCCP DC    CL2' '              CONTRA COMPANY                               
         DC    C'('                                                             
MSGBDULC DC    CL14' '             CONTRA UNIT/LEDGER/ACCOUNT                   
         DC    C')'                                                             
MSGBDLNQ EQU   *-MSGBADKY                                                       
         DROP  R2,RB                                                            
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO INITIALISE LIST OF ACCOUNTS FOR A PERSON                 *         
***********************************************************************         
                                                                                
SETPRS   NTR1  BASE=*,LABEL=*                                                   
         OC    ACMAACTA,ACMAACTA   TEST BUFFER ACQUIRED & INITIALISED           
         BNZ   SETPRS02                                                         
         L     R0,=A(L'ACTKACT*ACMAACTM)                                        
         GETMAIN RU,LV=(0),LOC=(ANY,ANY),BNDRY=PAGE                             
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
         ST    R1,ACMAACTA         SAVE A(ACCOUNT GROUP BUFFER)                 
                                                                                
SETPRS02 XC    ACMAACTN,ACMAACTN   CLEAR N'ENTRIES IN BUFFER                    
         L     R2,ACMAACTA         R3=A(ACCOUNT CODE BUFFER)                    
         ST    R2,ACMAACTS         SET A(START OF BUFFER)                       
         SR    R4,R4               R4=N'ENTRIES IN BUFFER                       
         L     R3,ACMABUF                                                       
         USING PERRECD,R3          GET PERSON RECORD                            
         MVC   PERKEY,SPACES                                                    
         MVI   PERKTYP,PERKTYPQ    X'0F'                                        
         MVC   PERKCPY,RCCOMPFL                                                 
         MVC   PERKCODE,ACMPRSN                                                 
         GOTOR DATAMGR,DMCB,DMREAD,ACMACFIL,(R3),(R3)                           
         BNE   SETPRSX                                                          
         LH    R1,ACMLVA                                                        
         LH    RF,ACMLVB                                                        
         SR    RF,R1                                                            
         BCTR  RF,0                                                             
         STH   RF,ACMDUB           LEVEL TWO LENGTH - 1                         
         LH    R1,ACMLVB                                                        
         LH    RF,ACMLVC                                                        
         SR    RF,R1                                                            
         BCTR  RF,0                                                             
         STH   RF,ACMDUB+2         LEVEL THREE LENGTH - 1                       
         LH    R1,ACMLVC                                                        
         LH    RF,ACMLVD                                                        
         SR    RF,R1                                                            
         BCTR  RF,0                                                             
         STH   RF,ACMDUB+4         LEVEL FOUR LENGTH - 1                        
                                                                                
         USING LOCELD,R6                                                        
         LR    R6,R3                                                            
         AH    R6,DATADISP                                                      
SETPRS04 CLI   0(R6),0             END OF RECORD                                
         BE    SETPRS12                                                         
         CLI   0(R6),LOCELQ        X'83'                                        
         BNE   SETPRS10                                                         
         MVC   ACMWRK,SPACES                                                    
         LA    RE,ACMWRK                                                        
         LH    RF,ACMLVA           FILL IN OFFICE                               
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,RE),LOCOFF                                                   
         LA    RE,1(RF,RE)         BUMP UP IN ACMWRK                            
         LH    RF,ACMDUB           FILL IN DEPARTMENT                           
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,RE),LOCDEPT                                                  
         LA    RE,1(RF,RE)         BUMP UP IN ACMWRK                            
         LH    RF,ACMDUB+2         FILL IN SUB DEPARTMENT                       
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,RE),LOCSUB      MOVE IN SUB DEPARTMENT                       
         LA    RE,1(RF,RE)         BUMP UP IN ACMWRK                            
         LH    RF,ACMDUB+4         FILL IN PERSON CODE                          
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,RE),ACMPRSN                                                  
                                                                                
         SAM31                     ENTER XA MODE                                
         L     R2,ACMAACTS                  START OF BUFFER                     
         LTR   R4,R4                                                            
         BZ    SETPRS08                                                         
         LR    R0,R4                                                            
SETPRS06 CLC   ACMWRK(L'ACTKACT),0(R2)      TEST FOR DUPLICATE                  
         BE    SETPRS10                     DUPLICATE FOUND                     
         AHI   R2,L'ACTKACT                                                     
         BCT   R0,SETPRS06                                                      
         CHI   R4,ACMAACTM         TEST ACCOUNT BUFFER IS FULL                  
         BL    *+6                                                              
         DC    H'0'                                                             
SETPRS08 MVC   0(L'ACTKACT,R2),ACMWRK       SAVE IT                             
         XC    L'ACTKACT(L'ACTKACT,R2),L'ACTKACT(R2)                            
         AHI   R4,1                INCREMENT N'ENTRIES IN BUFFER                
         SAM24                     EXIT XA MODE                                 
                                                                                
SETPRS10 SR    R1,R1                                                            
         IC    R1,1(R6)                                                         
         AR    R6,R1                                                            
         B     SETPRS04            NEXT ELEMENT                                 
                                                                                
SETPRS12 LTR   R4,R4               TEST ANY ACCOUNTS IN BUFFER                  
         BZ    SETPRSX                                                          
         STH   R4,ACMAACTN         SET N'ACCOUNTS TO PROCESS                    
         MVI   ACMAACTD,4                                                       
         GOTOR ACMVQSRT,ACMPARA,ACMAACTS,(R4),L'ACTKACT,L'ACTKACT,0             
         ORG   *-2                                                              
         O     RF,ACMXAON                                                       
         BASSM RE,RF               ENTER XA MODE                                
         SAM24                     EXIT XA MODE                                 
                                                                                
SETPRSX  OC    ACMAACTN,ACMAACTN   TEST ANY ENTRIES TO PROCESS                  
         J     EXIT                                                             
         DROP  R3,R6,RB                                                         
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO APPLY ACCOUNT FILTERS                                    *         
***********************************************************************         
                                                                                
FLTACT   NTR1  BASE=*,LABEL=*                                                   
         MVC   ACMFLTS(ACMFLTX),SPACES                                          
*&&UK*&& NI    ACMINDS4,FF-(ACMICLOA+ACMILOKA)                                  
         L     R1,ADLVASTA         HIGH LEVEL FILTERING                         
         GOTOR SETFLT                                                           
         CLI   MODE,PROCLEVA                                                    
         BE    FLTACT04                                                         
         L     R1,ADLVBSTA                                                      
         GOTOR SETFLT                                                           
         CLI   MODE,PROCLEVB                                                    
         BE    FLTACT04                                                         
         L     R1,ADLVCSTA                                                      
         GOTOR SETFLT                                                           
         CLI   MODE,PROCLEVC                                                    
         BE    FLTACT04                                                         
         L     R1,ADACCSTA                                                      
         GOTOR SETFLT                                                           
                                                                                
         ZAP   ACMABAL,ACMPZERO    ZERO THE ADJUSTED BALANCE                    
         CLI   ACMATYP,ACMATPL     TEST PROFIT/LOSS                             
         BE    FLTACT04                                                         
         TM    ACMINDS,ACMINEWO    TEST NEW OFFICES                             
         BNO   FLTACT02                                                         
         L     R1,ACMAOFL                                                       
         USING OFFALD,R1                                                        
         OC    OFFANEWA,OFFANEWA   TEST FOR LIMITED ACCESS                      
         BNZ   FLTACT04            YES                                          
         CLC   OFFAOFFC,SPACES     TEST FOR A REQUESTED OFFICE FILTER           
         BNE   FLTACT04            NO-ALLOW THE TRANSACTION                     
         CLI   FCSEQ,FCSEQACC                                                   
         BNE   FLTACT04                                                         
                                                                                
FLTACT02 ICM   R1,15,ADACCBAL      GET BALANCE RECORD ADDRESS                   
         BZ    FLTACT04                                                         
         ZAP   ACMABAL,ABLFRWD-ABLELD(L'ABLFRWD,R1)                             
                                                                                
FLTACT04 DS    0H                                                               
*&&UK                                                                           
         TM    ACMINDS4,ACMILOKA   TEST ACCOUNT IS LOCKED                       
         BZ    *+16                                                             
         TM    ACMINDS4,ACMILOKY   YES - TEST WANT LOCKED ACCOUNTS              
         BZ    FLTACTN                                                          
         B     *+12                                                             
         TM    ACMINDS4,ACMILOKN   TEST WANT UNLOCKED ACCOUNTS                  
         BZ    FLTACTN                                                          
*&&                                                                             
         LA    R2,QFILTER1         FILTER 1                                     
         LA    R1,ACMFLT1                                                       
         GOTOR TSTFLT                                                           
         LA    R2,QFILTER2         FILTER 2                                     
         LA    R1,ACMFLT2                                                       
         GOTOR TSTFLT                                                           
         LA    R2,QFILTER3         FILTER 3                                     
         LA    R1,ACMFLT3                                                       
         GOTOR TSTFLT                                                           
         LA    R2,QFILTER4         FILTER 4                                     
         LA    R1,ACMFLT4                                                       
         GOTOR TSTFLT                                                           
         LA    R2,QFILTER5         FILTER 5                                     
         LA    R1,ACMFLT5                                                       
         GOTOR TSTFLT                                                           
         L     R1,ADLEDGER                                                      
         CLC   ACMPROD,LDGKUNT-LDGRECD(R1)                                      
         BNE   FLTACT22                                                         
*&&UK                                                                           
         TM    ACMINDS4,ACMICLOA   TEST ACCOUNT IS CLOSED                       
         BZ    *+16                                                             
         TM    ACMINDS4,ACMICLOY   YES - TEST WANT CLOSED JOBS                  
         BZ    FLTACTN                                                          
         B     *+12                                                             
         TM    ACMINDS4,ACMICLON   TEST WANT OPEN JOBS                          
         BZ    FLTACTN                                                          
*&&                                                                             
FLTACT06 L     R3,ADPROFIL         OFFICE FILTERING                             
         USING PPRELD,R3                                                        
         CLI   FCSUPOFC,YES        TEST TO SUPPRESS OFFICE FILTERING            
         BE    FLTACT14                                                         
         TM    ACMINDS,ACMINEWO    TEST NEW OFFICES                             
         BZ    FLTACT10                                                         
                                                                                
         SR    R0,R0                                                            
         ICM   R0,3,ACMOFCN        MATCH OFFICE TO REQUESTED LIST               
         BNZ   *+6                                                              
         DC    H'0'                                                             
         LA    R1,ACMROFL                                                       
FLTACT08 CLC   PPRGAOFF,0(R1)      MATCH PRODUCTION OFFICE TO LIST              
         BE    FLTACT14                                                         
         AHI   R1,L'PPRGAOFF       BUMP TO NEXT LIST ENTRY                      
         BCT   R0,FLTACT08                                                      
         B     FLTACTN                                                          
                                                                                
FLTACT10 LA    R2,QOFFICE          PRODUCTION OFFICE FILTERING                  
         LA    R1,PPRGAOFF                                                      
         CLI   0(R1),X'41'                                                      
         BNL   *+8                                                              
         LA    R1,PPRUFORA                                                      
         CLI   ACMSYS,ACMSUSA                                                   
         BNE   FLTACT12                                                         
         CLI   ACMOFPOS,0          IF NO OFFICE POSITION DEFINED                
         BNE   FLTACT12                                                         
         MVI   ACMOFPOS,LDGOPROF   THEN ASSUME CLIENT                           
                                                                                
FLTACT12 CLI   ACMOFPOS,LDGOTRAN   IF OFFICE IS AT TRANSACTION LEVEL            
         BE    FLTACT14            THEN DON'T FILTER AT ACCOUNT LEVEL           
         GOTOR TSTFLT                                                           
                                                                                
FLTACT14 LA    R2,QBILTYPE                                                      
         LA    R1,PPRBTYPE                                                      
         GOTOR TSTFLT                                                           
                                                                                
         CLI   MODE,PROCLEVC       TEST PRODUCTION JOB LEVEL                    
         BNE   FLTACT20                                                         
         CLI   ACMSYS,ACMSUSA                                                   
         BNE   FLTACT18                                                         
         L     R1,ADHEIRC                                                       
         AH    R1,DATADISP                                                      
         USING JOBELD,R1                                                        
         SR    R0,R0                                                            
FLTACT16 CLI   JOBEL,0             TEST FOR EOR                                 
         BE    FLTACT18            YES                                          
         CLI   JOBEL,JOBELQ        TEST FOR JOB ELEMENT                         
         BE    *+14                YES                                          
         IC    R0,JOBLN                                                         
         AR    R1,R0                                                            
         B     FLTACT16                                                         
                                                                                
         TM    JOBSTA1,JOBSXJOB    TEST FOR X-JOB                               
         BO    *+16                YES                                          
         TM    ACMINDS2,ACMIXJBN   TEST TO INCLUDE NON-XJOBS                    
         BO    FLTACT18            YES                                          
         B     FLTACTN             NO-SKIP ACCOUNT                              
                                                                                
         TM    ACMINDS2,ACMIXJBY   TEST TO INCLUDE X-JOBS                       
         BZ    FLTACTN             NO-DROP ACCOUNT                              
         DROP  R1                                                               
                                                                                
FLTACT18 CLI   QMGROUP,C' '        TEST FOR MEDIA GROUP REQUEST                 
         BE    FLTACT20                                                         
         GOTOR SETMGR              EXTRACT MEDIA GROUP                          
         LA    R2,QMGROUP                                                       
         LA    R1,ACMMEGRP                                                      
         GOTOR TSTFLT              MEDIA GROUP FILTER                           
                                                                                
FLTACT20 LA    R2,QMEDIA           MEDIA FILTER FOR PRODUCTION                  
         LA    R1,FCRULMED                                                      
         GOTOR TSTFLT                                                           
         GOTOR FLTBGP,PPRELD                                                    
         BNE   FLTACTN                                                          
                                                                                
FLTACT22 MVI   ACMFLAG,ACMFLAC     TELL ROUTINE IT'S ACCOUNT LEVEL              
         NI    ACMPESEC,FF-(ACMPEEMP+ACMPECLI)                                  
         GOTOR OFFLMT              CHECK OFFICE LIMIT ACCESS                    
         BNE   FLTACTN                                                          
*                                                                               
FLTACT24 L     R1,ADACC                                                         
         TM    ACTRSTA-ACTRECD(R1),ACTSDRFT                                     
         BO    *+16                DRAFT JOB                                    
         TM    ACMINDS5,ACMIDJBN   INCLUDE NON-DRAFT JOBS?                      
         BO    FLTACTY             YES                                          
         B     FLTACTN             NO, SKIP ACCOUNT                             
                                                                                
         TM    ACMINDS5,ACMIDJBY   INCLUDE DRAFT JOBS                           
         BO    FLTACTY             YES                                          
                                                                                
FLTACTN  CLI   *,0                 SET CONDITIONS FOR EXIT                      
         B     FLTACTX                                                          
FLTACTY  CLI   *+1,0               I GUESS IT'S OK                              
                                                                                
FLTACTX  J     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO APPLY REQUEST FILTER TO ACCOUNT FILTER VALUE             *         
***********************************************************************         
                                                                                
TSTFLT   CLI   0(R2),C' '          SPACE=NO FILTER                              
         BER   RE                                                               
         MVC   ACMWRK+0(1),0(R2)                                                
         MVC   ACMWRK+1(1),0(R1)                                                
         LHI   R1,X'80'            SET MASK TO EQUAL                            
         TM    ACMWRK,X'40'        TEST POSITIVE FILTER                         
         JNZ   *+12                                                             
         LHI   R1,X'70'            SET MASK TO NOT EQUAL                        
         OI    ACMWRK,X'40'                                                     
         CLI   ACMWRK,C'.'         CONVERT FULL STOP TO SPACE                   
         JNE   *+8                                                              
         MVI   ACMWRK,C' '                                                      
         CLC   ACMWRK(1),ACMWRK+1  MATCH REQUEST FILTER TO VALUE                
         BASR  RF,0                                                             
         EX    R1,8(RF)                                                         
         J     FLTACTN                                                          
         NOPR  RE                                                               
         EJECT                                                                  
***********************************************************************         
* FILTER A TRANSACTION RECORD AGAINST REQUEST                         *         
***********************************************************************         
                                                                                
FLTTRN   NTR1  BASE=*,LABEL=*                                                   
         L     R2,ADTRANS                                                       
         USING TRNELD,R2           R2=A(TRANSACTION ELEMENT)                    
         CLI   TRNEL,TRNELQ        TEST IF A TRANSACTION ELEMENT                
         BNE   FLTTRNY                                                          
         L     R3,ACMATRN          R3=A(TRANSACTION RECORD)                     
         USING TRNRECD,R3                                                       
                                                                                
         XC    ACMTELS(ACMTELSL),ACMTELS                                        
         LR    R1,R2               SET ADDRESS OF TRANSACTION ELEMENTS          
         SR    R0,R0                                                            
FLTTRN02 CLI   0(R1),TRNELQ                                                     
         BNE   *+8                                                              
         ST    R1,ACMTATRN                                                      
         CLI   0(R1),TRSELQ                                                     
         BNE   *+8                                                              
         ST    R1,ACMTATRS                                                      
         CLI   0(R1),DUEELQ                                                     
         BNE   *+8                                                              
         ST    R1,ACMTADUE                                                      
         CLI   0(R1),BNDELQ                                                     
         BNE   *+18                                                             
         OC    ACMTABND,ACMTABND                                                
         BNZ   *+8                                                              
         ST    R1,ACMTABND                                                      
         CLI   0(R1),PTAELQ                                                     
         BNE   *+18                                                             
         OC    ACMTAPTA,ACMTAPTA                                                
         BNZ   *+8                                                              
         ST    R1,ACMTAPTA                                                      
*&&UK                                                                           
         CLI   0(R1),GDAELQ                                                     
         BNE   *+16                                                             
         CLI   GDATYPE-GDAELD(R1),GDATERPD                                      
         BNE   *+8                                                              
         ST    R1,ACMTAEPD                                                      
*&&                                                                             
         IC    R0,1(R1)                                                         
         AR    R1,R0                                                            
         CLI   0(R1),0                                                          
         BNE   FLTTRN02                                                         
                                                                                
         TM    ACMHIST,ACMHNOT                                                  
         BZ    FLTTRN04                                                         
         LR    RF,R2                                                            
         SH    RF,DATADISP                                                      
         XC    ACCOPEEL(ACCOPLEN,RF),ACCOPEEL(RF)                               
                                                                                
FLTTRN04 TM    ACMHIST,ACMHPRC     TEST HISTORY                                 
         BNZ   FLTTRN16                                                         
         TM    ACMINDS,ACMIEMUD    TEST EMULATED FILE                           
         BNZ   FLTTRN16                                                         
         MVI   ACMFLAG,ACMFLTN     TELL ROUTINE WE'RE AT TRANS LEVEL            
         GOTOR OFFLMT              CHECK OFFICE LIMIT ACCESS                    
         BNE   FLTTRNN             DID NOT PASS LIMIT ACCESS TEST               
                                                                                
         CLI   FCDRAOVR,NO         TEST APPLICATION OVERRIDE                    
         BE    FLTTRN06            NO                                           
         NI    ACMINDS2,FF-(ACMIDRAY+ACMIDRAN)                                  
         NI    FCDRAOVR,ACMIDRAY+ACMIDRAN ISOLATE DRAFT TRANS. BITS             
         OC    ACMINDS2,FCDRAOVR   SET OVERRIDE                                 
         MVI   FCDRAOVR,NO         RESTORE SWITCH                               
                                                                                
FLTTRN06 CLI   FCREVOVR,NO         TEST APPLIC SET REVERSAL OVERRIDE            
         BE    FLTTRN08                                                         
         NI    ACMINDS,FF-(ACMITRVN+ACMITRVY)                                   
         NI    FCREVOVR,ACMITRVY+ACMITRVN                                       
         OC    ACMINDS,FCREVOVR    SET VALUES IN MONACC INDICATOR BYTE          
         MVI   FCREVOVR,NO                                                      
                                                                                
FLTTRN08 TM    ACMINDS,ACMIEMUD    TEST NEW FILE                                
         BZ    FLTTRN14            NO                                           
                                                                                
         TM    TRNRSTAT,TRNSDRFT   TEST DRAFT TRANSACTION                       
         BO    FLTTRN10            YES                                          
                                                                                
         TM    ACMINDS2,ACMIDRAN   TEST NON-DRAFT(LIVE) REQUESTED               
         BO    FLTTRN14            YES                                          
         B     FLTTRNN             NO                                           
                                                                                
FLTTRN10 CLI   FCADVEST,YES        TEST ADVANCE/EST. BILL RECS REQUIRED         
         BNE   FLTTRN12                                                         
         CLI   TRNRSTYP,99         YES - ALWAYS PASS TYPES 99 & 199             
         BE    FLTTRN14                                                         
         CLI   TRNRSTYP,199                                                     
         BE    FLTTRN14                                                         
                                                                                
FLTTRN12 TM    ACMINDS2,ACMIDRAY   TEST DRAFT TRANSACTIONS REQUESTED            
         BZ    FLTTRNN             NO                                           
                                                                                
FLTTRN14 TM    TRNSTAT,TRNSREV     TEST THIS IS A REVERSAL                      
         BNZ   *+12                                                             
         TM    ACMINDS,ACMITRVN    NO - DOES APPLIC WANT NON-REVERSALS          
         BZ    FLTTRNN                                                          
                                                                                
FLTTRN16 GOTOR GETMOS              ESTABLISH MOS OF TRANSACTION                 
         BNE   FLTTRNN                                                          
         TM    TRNSTAT,TRNSREV     TEST REVERSED TRANSACTION                    
         BZ    FLTTRN20                                                         
         CLC   ACMMDTE,ACMMSTR     YES - DOES IT FALL WITHIN REQ PERIOD         
         BL    FLTTRN20                                                         
         CLC   ACMMDTE,ACMMEND                                                  
         BH    FLTTRN20                                                         
                                                                                
         ICM   R1,15,ACMTATRS      YES - LOCATE TRANS STATUS ELEMENT            
         BZ    FLTTRN18                                                         
         USING TRSELD,R1                                                        
         CLI   TRSLN,TRSRMOS+L'TRSRMOS-TRSELD                                   
         BL    FLTTRN18                                                         
         OC    TRSRMOS,TRSRMOS     TEST REVERSAL MONTH OF SERVICE SET           
         BZ    FLTTRN18                                                         
         CLC   TRSRMOS,ACMMSTR     INCLUDE THIS REVERSED TRANSACTION IF         
         BL    FLTTRN20            THE OTHER TRANSACTION WILL BE FILT-          
         CLC   TRSRMOS,ACMMEND     ERED OUT BECAUSE OF REQUESTED MOS            
         BH    FLTTRN20                                                         
FLTTRN18 TM    ACMINDS,ACMITRVY    TEST APPLIC WANTS REVERSALS                  
         BZ    FLTTRNN                                                          
         DROP  R1                                                               
                                                                                
FLTTRN20 DS    0H                                                               
*&&US                                                                           
         TM    ACMINDS,ACMIEMUD    TEST NEW FILE                                
         BZ    FLTTRN22            NO                                           
         L     RF,ADACC                                                         
         CLC   ACMPROD,1(RF)       TEST PRODUCTION LEDGER                       
         BNE   FLTTRN22                                                         
         CLI   FCRNTIME,YES        PASS 'R' AND 'N' TIME                        
         BE    FLTTRN22                                                         
                                                                                
* H+K HAS SOME CORRUPTED RATE ELEMENTS ON THE FILE--THE 'B'                     
* TIME TEST WILL BE RESTRICTED TO THE O+M AGENCIES ON THE NEW FILE              
                                                                                
         SR    R0,R0                                                            
         LA    R1,TRNELD           R1=ELEMENT POINTER                           
         USING PRTELD,R1                                                        
         IC    R0,PRTLN                                                         
         AR    R1,R0               BUMP TO NEXT ELEMENT                         
         CLI   PRTEL,0             TEST FOR EOR                                 
         BE    FLTTRN22            YES                                          
         CLI   PRTEL,PRTELQ        TEST FOR PERSONNEL RATE ELEM                 
         BNE   *-18                NO                                           
                                                                                
         CLI   PRTLN,PRTSTAT-PRTELD TEST FOR OLD(SMALL) ELEMENT                 
         BNH   FLTTRN22            YES                                          
         TM    PRTSTAT,PRTSBILQ    TEST FOR 'B' TIME                            
         BZ    FLTTRNN             NO-SKIP THE ITEM                             
         DROP  R1                                                               
*&&                                                                             
FLTTRN22 TM    ACMHIST,ACMHPRC     TEST HISTORY FILE                            
         BNZ   *+12                                                             
         TM    ACMINDS,ACMIEMUD    TEST NEW FILE                                
         BNZ   FLTTRN24            YES                                          
         GOTOR OFFFLT                                                           
         BNE   FLTTRNN                                                          
                                                                                
FLTTRN24 TM    ACMHIST,ACMHPRC     PROCESSING HISTORY FILE REC?                 
         BZ    FLTTRN28                                                         
         CLI   ACMATYP,ACMATPL     HANDLE PROFIT/LOSS DIFFERENTLY               
         BE    FLTTRN28                                                         
         TM    TRNSTAT,TRNSDR      A DEBIT ?                                    
         BZ    FLTTRN26            NO, GO MUST BE CREDIT                        
         SP    ACMABAL,TRNAMNT     TAKE OUT/ALREADY INCLUDED FROM X'32'         
         SP    ACMOBAL,TRNAMNT                                                  
         B     FLTTRN28                                                         
FLTTRN26 AP    ACMABAL,TRNAMNT     TAKE OUT/ALREADY INCLUDED FROM X'32'         
         AP    ACMOBAL,TRNAMNT                                                  
                                                                                
FLTTRN28 CLI   ACMATYP,ACMATPL     HANDLE PROFIT/LOSS DIFFERENTLY               
         BNE   FLTTRN32                                                         
         CLC   ACMMDTE,ACMFDTE     DROP IF BEFORE FISCAL START                  
         BL    FLTTRN30                                                         
         CLC   ACMMDTE,ACMMSTR     IF  LOWER THAN M.O.S. START                  
         BL    FLTTRN34            ADD TO ACCTBAL                               
                                                                                
FLTTRN30 LA    RF,ACMMSTR          USE MOS START IF PRESENT                     
         OC    ACMMSTR,ACMMSTR                                                  
         BNZ   *+8                                                              
         LA    RF,ACMFDTE          ELSE USE FISCAL START                        
         CLC   ACMMDTE,0(RF)       TEST BEFORE START                            
         BL    FLTTRNN                                                          
         CLC   ACMMDTE,ACMMEND     JUST GET CORRECT TRANSACTIONS                
         BH    FLTTRNN                                                          
         B     FLTTRN38                                                         
                                                                                
FLTTRN32 CLI   ACMATYP,ACMATBS     HANDLE BALANCE DIFFERENTLY ALSO              
         BNE   FLTTRN38                                                         
         CLC   ACMMDTE,ACMMSTR                                                  
         BL    FLTTRN34            IF LOWER ADD TO OPENING BALANCE              
         CLC   ACMMDTE,ACMMEND                                                  
         BH    FLTTRNN             IF HIGHER, DROP IT                           
         B     FLTTRN38            ELSE, CONTINUE FILTERING                     
                                                                                
FLTTRN34 LR    RF,R2                                                            
         SH    RF,DATADISP                                                      
         OC    ACCOPEEL(ACCOPLEN,RF),ACCOPEEL(RF)                               
         BNZ   FLTTRNN             DON'T ADD/SUB PEELED ITEMS                   
         TM    TRNSTAT,TRNSDR      IS THIS A DEBIT ?                            
         BZ    FLTTRN36            NO, GO MUST BE CREDIT                        
         AP    ACMABAL,TRNAMNT     YES, ADD IT THEN                             
         AP    ACMOBAL,TRNAMNT                                                  
         B     FLTTRNN             DROP ITEM                                    
FLTTRN36 SP    ACMABAL,TRNAMNT     CREDIT COMES HERE                            
         SP    ACMOBAL,TRNAMNT                                                  
         B     FLTTRNN             NOW DROP IT                                  
                                                                                
FLTTRN38 TM    ACMINDS3,ACMIACTF   IS THERE ACTIVITY DATE OPTION                
         BZ    FLTTRN42                                                         
         ICM   R1,15,ACMTATRS                                                   
         BZ    FLTTRN42                                                         
         USING TRSELD,R1                                                        
         CLC   TRSDATE,ACMASTR                                                  
         BL    FLTTRNN             BEFORE ACTIVITY START                        
         CLC   TRSDATE,ACMAEND                                                  
         BH    FLTTRNN             AFTER ACTIVITY END                           
         DROP  R1                                                               
                                                                                
FLTTRN42 MVC   ACMWRK(L'TRNSTAT),TRNSTAT                                        
         NC    ACMWRK(L'TRNSTAT),ACMTSINC                                       
         CLC   ACMWRK(L'TRNSTAT),ACMTSINC                                       
         BNE   FLTTRNN                                                          
         MVC   ACMWRK(L'TRNSTAT),TRNSTAT                                        
         NC    ACMWRK(L'TRNSTAT),ACMTSEXC                                       
         BNZ   FLTTRNN                                                          
                                                                                
FLTTRN46 ICM   R1,1,ACMTMSK        TEST OPTION TO FILTER AN INPUT TYPE          
         BZ    FLTTRN50                                                         
         CLI   ACMTFLT,0           TEST MANUAL ONLY                             
         BNE   FLTTRN48                                                         
                                                                                
         CLI   TRNTYPE,6                                                        
         EX    R1,FLTTRNEX                                                      
         CLC   TRNNARR(6),=C'MANUAL'                                            
         EX    R1,FLTTRNEX                                                      
         B     FLTTRN50                                                         
                                                                                
FLTTRN48 CLC   TRNTYPE,ACMTFLT     MATCH ON REQUEST TYPE                        
         EX    R1,FLTTRNEX                                                      
                                                                                
FLTTRN50 TM    ACMINDS3,ACMIDUEF   TEST DUE DATE FILTER PRESENT                 
         BZ    FLTTRN54                                                         
         ICM   R1,15,ACMTADUE      POINT TO DUE DATE ELEMENT                    
         BZ    FLTTRN52                                                         
         USING DUEELD,R1                                                        
         CLC   DUEDATE,ACMDSTR     APPLY DUE DATE FILTER                        
         BL    FLTTRNN                                                          
         CLC   DUEDATE,ACMDEND                                                  
         BH    FLTTRNN                                                          
         B     FLTTRN54                                                         
                                                                                
FLTTRN52 CLC   TRNDATE,ACMDSTR1    NO DUE DATE - USE TRANSACTION DATE           
         BL    FLTTRNN                                                          
         CLC   TRNDATE,ACMDEND1                                                 
         BH    FLTTRNN                                                          
                                                                                
FLTTRN54 DS    0H                                                               
*&&UK                                                                           
         TM    ACMINDS3,ACMIEPDF   TEST EARLY PAYMENT DATE FILTER               
         BZ    FLTTRN56                                                         
         ICM   R1,15,ACMTAEPD      POINT TO DUE DATE ELEMENT                    
         BZ    FLTTRNN                                                          
         USING GDAELD,R1                                                        
         CLC   GDADATE,ACMEPDST    APPLY EARLY PAYMENT DATE FILTER              
         BL    FLTTRNN                                                          
         CLC   GDADATE,ACMEPDND                                                 
         BH    FLTTRNN                                                          
*&&                                                                             
FLTTRN56 DS    0H                                                               
         B     FLTTRNY                                                          
                                                                                
FLTTRNEX NOP   FLTTRNN                                                          
                                                                                
FLTTRNY  CLI   FCPRCFIL,YES        TEST APPLICATION WANTS FILE RECORD           
         BNE   FLTTRNYX            RECORD PASSED FOR FILTERING                  
         MVI   MODE,PROCTRNF                                                    
         GOTOR ACAPPLIC,ACMPARA,ACWORKD,TRNKEY                                  
         BNE   FLTTRNN             CC=NOT EQUAL - DISCARD RECORD                
                                                                                
         CLI   FCTSTACK,YES        DOES APPLICATION WANT A STACK BUILT          
         BNE   FLTTRNYX                                                         
         LH    R1,ACMTSACT         R1=ACTUAL #ENTRIES IN STACK                  
         LR    RF,R1                                                            
         SLL   RF,2                                                             
         A     RF,ACMTSADR         RF=A(NEXT ENTRY IN STACK)                    
         CLC   ACMOFCAC,TRNKOFF    TEST CHANGE OF OFFICE/CONTRA                 
         BE    FLTTRNY2                                                         
         AHI   R1,1                SET D/A OF CONTRA IN STACK                   
         CHI   R1,ACMTSNUM                                                      
         BNH   *+6                                                              
         DC    H'0'                                                             
                                                                                
         MVC   0(L'ACMCACDA,RF),ACMCACDA                                        
         AHI   RF,L'ACMCACDA                                                    
         MVC   ACMOFCAC,TRNKOFF                                                 
FLTTRNY2 AHI   R1,1                SET D/A OF TRANSACTION IN STACK              
         CHI   R1,ACMTSNUM                                                      
         BNH   *+6                                                              
         DC    H'0'                                                             
                                                                                
         TM    ACMTSTA1,TRNSARCH   CAN'T DEAL WITH ARCHIVED RECORDS YET         
         BZ    *+6                                                              
         DC    H'0'                                                             
                                                                                
         MVC   0(L'ACMTRNDA,RF),ACMTRNDA                                        
         STH   R1,ACMTSACT                                                      
                                                                                
FLTTRNYX CLI   *+1,0                                                            
         B     FLTTRNX                                                          
                                                                                
FLTTRNN  CLI   *,0                                                              
         B     FLTTRNX                                                          
                                                                                
FLTTRNX  J     EXIT                                                             
         DROP  R2,R3                                                            
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO ESTABLISH MONTH OF SERVICE FOR A TRANSACTION             *         
***********************************************************************         
                                                                                
         USING TRNELD,R2                                                        
GETMOS   TM    ACMHIST,ACMHPRC     HISTORY RECORD                               
         BO    GETMOS02                                                         
         TM    ACMINDS,ACMIEMUD    TEST EMULATED FILE                           
         BZ    GETMOS02                                                         
         OC    ACMTTMOS,ACMTTMOS   TEST TRANSACTION MONTH SET                   
         BZ    GETMOS02                                                         
         MVC   ACMMDTE,ACMTTMOS    SET MOS FROM TRANSACTION STATUS AREA         
         TM    RCAPIND1,RCAPIUBM   TEST USING BILLING MONTH FOR MOS             
         BZR   RE                  NO - EXIT WITH MOS SET & CC EQUAL!           
                                                                                
GETMOS02 NTR1  ,                                                                
         TM    RCAPIND1,RCAPIUBM   TEST USING BILLING MONTH FOR MOS             
         BNZ   GETMOS04                                                         
         GOTOR VCONVMOS,ACMPARA,(X'FE',TRNELD),ACMMDTE                          
         B     GETMOSY                                                          
                                                                                
GETMOS04 SR    R0,R0                                                            
         ICM   R2,15,ACMTAPTA      R2=A(FIRST PTAEL)                            
         BNZ   GETMOS12                                                         
GETMOS06 ICM   R2,15,ACMTABND      R2=A(FIRST BNDEL)                            
         BZ    GETMOSN                                                          
                                                                                
         USING BNDELD,R2                                                        
GETMOS08 DS    0H                                                               
*&&UK*&& CLC   BNDBNO,SPACES                                                    
*&&US*&& OC    BNDBNO,BNDBNO                                                    
         BE    GETMOS10                                                         
         GOTOR DATCON,ACMPARA,(2,BNDDTE),(1,ACMWRK)                             
         MVC   ACMMDTE,ACMWRK                                                   
         B     GETMOSY                                                          
                                                                                
GETMOS10 IC    R0,BNDLN            BUMP TO NEXT ELEMENT                         
         AR    R2,R0                                                            
         CLI   BNDEL,0             TEST EOR                                     
         BE    GETMOSN                                                          
         CLI   BNDEL,BNDELQ        TEST BILLING ELEMENT                         
         BE    GETMOS08                                                         
         B     GETMOS10                                                         
                                                                                
         USING PTAELD,R2                                                        
GETMOS12 TM    PTASTAT1,PTASPEND   TEST FOR LIVE BILLING                        
         BNZ   GETMOS14                                                         
         CLI   PTATYPE,PTATRAL                                                  
         BNE   GETMOS14                                                         
         MVC   ACMMDTE,PTAMOA      YES - SET BILLING MONTH                      
         B     GETMOSY                                                          
                                                                                
GETMOS14 IC    R0,PTALN            BUMP TO NEXT ELEMENT                         
         AR    R2,R0                                                            
         CLI   PTAEL,0             TEST END OF RECORD                           
         BE    GETMOS06            TRY BNDEL ELEMENTS NEXT                      
         CLI   PTAEL,PTAELQ        TEST FOR PTA ELEMENT                         
         BE    GETMOS12            GOT ONE                                      
         B     GETMOS14                                                         
                                                                                
GETMOSN  CLI   *,0                                                              
         B     GETMOSX                                                          
GETMOSY  CLI   *+1,0                                                            
                                                                                
GETMOSX  B     FLTTRNX                                                          
         DROP  R2,RB                                                            
         EJECT                                                                  
***********************************************************************         
* FILTER A TIME RECORD AGAINST REQUEST AND APPLICATION SET FILTERS    *         
***********************************************************************         
                                                                                
FLTTIM   NTR1  BASE=*,LABEL=*                                                   
         L     R2,ADTRANS                                                       
         USING TIMELD,R2           R2=A(TIME ELEMENT)                           
         CLI   TIMEL,TIMELQ        TEST IF A TRANSACTION ELEMENT                
         BNE   FLTTIMN                                                          
         CLI   TIMETYP,TIMEINP     TEST TIME DETAIL                             
         BNE   FLTTIMN                                                          
                                                                                
         CLC   TIMMOA,ACMMSTR      APPLY MONTH OF ACTIVITY FILTERS              
         BL    FLTTIMN                                                          
         CLC   TIMMOA,ACMMEND                                                   
         BH    FLTTIMN                                                          
                                                                                
         TM    ACMINDS3,ACMIACTF   IS THERE ACTIVITY DATE OPTION                
         BZ    FLTTIM10                                                         
         GOTOR DATCON,ACMPARA,(1,TIMADAT),(2,ACMWRK)                            
         CLC   ACMWRK(2),ACMASTR   APPLY ACTIVITY DATE FILTERS                  
         BL    FLTTIMN                                                          
         CLC   ACMWRK(2),ACMAEND                                                
         BH    FLTTIMN                                                          
                                                                                
FLTTIM10 CLI   ACMOFPOS,LDGOTRAN   FILTERING BY TRANSACTION OFFICE              
         BNE   FLTTIMY             NO                                           
         TM    ACMINDS,ACMINEWO    NEW OFFICE (2 BYTE)                          
         BZ    FLTTIM20            NO                                           
         MVC   ACMWRK(2),TIMOFF                                                 
         MVI   ACMFLAG,ACMFLTO                                                  
         GOTOR OFFLMT                                                           
         BE    FLTTIMY                                                          
         B     FLTTIMN                                                          
                                                                                
FLTTIM20 CLI   QOFFICE,C' '        APPLY OFFICE REQUEST FILTER                  
         BE    FLTTIMY                                                          
         CLC   QOFFICE(1),TIMOFF                                                
         BE    FLTTIMY                                                          
         TM    QOFFICE,X'40'       TEST ALL EXCEPT REQUEST                      
         BO    FLTTIMN                                                          
         MVC   ACMWRK(1),QOFFICE                                                
         OI    ACMWRK,X'40'                                                     
         CLC   ACMWRK(1),TIMOFF                                                 
         BE    FLTTIMN                                                          
                                                                                
FLTTIMY  J     EXITEQ                                                           
                                                                                
FLTTIMN  J     EXITHI                                                           
         DROP  R2,RB                                                            
         EJECT                                                                  
***********************************************************************         
* SUB-ROUTINE TO FILTER ON A TRANSACTION RECORD DIRECTORY KEY         *         
*                                                                     *         
* AT ENTRY, R3=A(DIRECTORY KEY)                                       *         
* ON EXIT, CC=EQ IF OK, CC=NEQ TO SKIP DIRECTORY ENTRY                *         
***********************************************************************         
                                                                                
FLTKEY   NTR1  BASE=*,LABEL=*                                                   
         USING TRNKEY,R3                                                        
         MVI   ACMREC,ACMRTRNS     SET TRANSACTION RECORD                       
         MVC   ACMTKOFF,TRNKOFF    SET TRANSACTION OFFICE/WORK CODE             
         MVC   ACMTKSTA,TRNKSTAT   SET TRANSACTION STATUS VALUES                
         MVC   ACMTRNDA,TRNKDA     SET TRANSACTION DISK ADDRESS                 
         DROP  R3                                                               
                                                                                
         USING TIMRECD,R3                                                       
         TM    TIMKRI2,TIMKRI2Q    IS THIS A TIME RECORD?                       
         BNO   FLTKEY02            NO                                           
         CLI   TIMKRI3,TIMKRI3Q                                                 
         BNE   FLTKEY02                                                         
         CLI   TIMKRI4,TIMKRI4Q                                                 
         BNE   FLTKEY02                                                         
         MVI   ACMREC,ACMRTIME     SET TIME RECORD                              
         DROP  R3                                                               
                                                                                
FLTKEY02 CLI   ACMREC,ACMRTRNS     TEST TRANSACTION RECORD                      
         BE    FLTKEY04                                                         
         CLI   FCRDTIME,YES        TEST APPLICATION WANTS TIME RECORDS          
         BNE   FLTKEYN                                                          
         B     FLTKEY14                                                         
                                                                                
         USING TRNKEY,R3                                                        
FLTKEY04 TM    ACMTSTA2,TRNSUSED   TEST THIS TRANSACTION IS USED                
         BZ    *+12                                                             
         CLI   FCFLTUSD,YES        TEST FILTERING USED TRANSACTIONS             
         BE    FLTKEYN                                                          
*&&UK                                                                           
         CLC   =C'UP',QPROG        UNLESS REQUEST IS UNPEEL                     
         BE    *+12                                                             
         TM    ACMTSTA2,TRNSPEEL   DROP PEELED TRANSACTIONS                     
         BNZ   FLTKEYN                                                          
         TM    TRNKSTA2,TRNSEXCL   TEST TRANSACTION IS EXCLUDED                 
         BZ    *+16                                                             
         TM    ACMINDS5,ACMIEXCY   YES - TEST WANT EXCLUDED ONES                
         BZ    FLTKEYN                                                          
         B     *+12                                                             
         TM    ACMINDS5,ACMIEXCN   TEST WANT NON-EXCLUDED ONES                  
         BZ    FLTKEYN                                                          
                                                                                
         TM    ACMINDS3,ACMIEPDF   TEST EARLY PAYMENT DATE FILTER               
         BZ    *+12                                                             
         TM    TRNKSTA2,TRNSUSED   YES - ONLY WANT UNPAID TRANSACTIONS          
         BNZ   FLTKEYN                                                          
*&&                                                                             
         MVI   ACMFLAG,ACMFLTD     SET DIRECTORY LEVEL OFFICE FILTERING         
         GOTOR OFFLMT              APPLY LIMIT ACCESS TESTS                     
         BNE   FLTKEYN                                                          
         GOTOR OFFFLT              APPLY OFFICE FILTERS                         
         BNE   FLTKEYN                                                          
         CLI   FCDRAOVR,NO         TEST DRAFT TRANSACTION OVERRIDE              
         BE    FLTKEY06            NO                                           
                                                                                
         NI    ACMINDS2,FF-(ACMIDRAY+ACMIDRAN)                                  
         NI    FCDRAOVR,ACMIDRAY+ACMIDRAN                                       
         OC    ACMINDS2,FCDRAOVR                                                
         MVI   FCDRAOVR,NO                                                      
                                                                                
FLTKEY06 CLI   FCREVOVR,NO         TEST APPLIC SET REVERSAL OVERRIDE            
         BE    FLTKEY08                                                         
                                                                                
         NI    ACMINDS,FF-(ACMITRVN+ACMITRVY)                                   
         NI    FCREVOVR,ACMITRVY+ACMITRVN                                       
         OC    ACMINDS,FCREVOVR    SET VALUES IN MONACC INDICATOR BYTE          
         MVI   FCREVOVR,NO                                                      
                                                                                
FLTKEY08 TM    TRNKSTAT,TRNSDRFT   TEST DRAFT TRANSACTION                       
         BO    FLTKEY10            YES                                          
         TM    ACMINDS2,ACMIDRAN   TEST LIVE TRANSACTIONS WANTED                
         BO    FLTKEY14            YES                                          
         B     FLTKEYN                                                          
                                                                                
FLTKEY10 CLI   FCADVEST,YES        TEST ADVANCE/EST. BILL RECS REQUIRED         
         BNE   FLTKEY12                                                         
         CLI   TRNKSTYP,99         YES - ALWAYS PASS TYPES 99 & 199             
         BE    FLTKEY14                                                         
         CLI   TRNKSTYP,199                                                     
         BE    FLTKEY14                                                         
                                                                                
FLTKEY12 TM    ACMINDS2,ACMIDRAY   TEST DRAFTS REQUESTED                        
         BZ    FLTKEYN                                                          
                                                                                
FLTKEY14 CLC   TRNKDATE,ACMTSTR    TRANSACTION DATE FILTER                      
         BL    FLTKEYN                                                          
         CLC   TRNKDATE,ACMTEND                                                 
         BH    FLTKEYN                                                          
                                                                                
         CLI   ACMREC,ACMRTRNS     TEST TRANSACTION RECORD                      
         BNE   FLTKEY26                                                         
         TM    TRNKSTAT,TRNSREVS   TEST TRANSACTIONS IS A REVERSAL              
         BO    FLTKEY16            YES                                          
         TM    ACMINDS,ACMITRVN    TEST NON-REVERSALS REQUESTED                 
         BZ    FLTKEYN             NO                                           
                                                                                
FLTKEY16 TM    RCAPIND1,RCAPIUBM   TEST USING BILLING MONTH FOR MOS             
         BNZ   FLTKEY20                                                         
         CLI   ACMATYP,ACMATBS     TEST BALANCE SHEET ACCOUNT                   
         BNE   FLTKEY18                                                         
         CLC   TRNKSMOS,ACMMEND    TEST PAST REQUEST END MOS                    
         BH    FLTKEYN                                                          
         CLI   FCSETBAL,YES        TEST BBF REQUIRED                            
         BE    FLTKEY20                                                         
         CLC   TRNKSMOS,ACMMSTR    TEST BEFORE MOS START                        
         BL    FLTKEYN             YES, SKIP IT                                 
         B     FLTKEY20                                                         
                                                                                
FLTKEY18 CLI   ACMATYP,ACMATPL     TEST PROFIT AND LOSS ACCOUNT                 
         BNE   FLTKEY20            NO                                           
         CLC   TRNKSMOS,ACMFDTE    IS IT BEFORE FISCAL START                    
         BL    *+14                                                             
         CLC   TRNKSMOS,ACMMSTR    IS IT BEFORE START DATE                      
         BL    FLTKEY20                                                         
         LA    RF,ACMMSTR                                                       
         OC    ACMMSTR,ACMMSTR     IF START MOS WAS INPUT THEN USE IT           
         BNZ   *+8                                                              
         LA    RF,ACMFDTE          OTHERWISE USE FISCAL START TO FILTER         
         CLC   TRNKSMOS,0(RF)                                                   
         BL    FLTKEYN             YES                                          
                                                                                
FLTKEY20 DS    0H                  DUE DATE NOT KEY FILLED IN US                
*&&UK                                                                           
         TM    ACMINDS3,ACMIDUEF   TEST DUE DATE FILTER SET                     
         BZ    FLTKEY24                                                         
         TM    TRNKSTA2,TRNSUSED   TEST USED (DUE DATE OVERWRITTEN)             
         BNZ   FLTKEY24                                                         
         OC    TRNKSDUE,TRNKSDUE   TEST DUE DATE SET                            
         BZ    FLTKEY22                                                         
         CLC   TRNKSDUE,ACMDSTR    TEST WITHIN REQUEST LIMITS                   
         BL    FLTKEYN                                                          
         CLC   TRNKSDUE,ACMDEND                                                 
         BH    FLTKEYN                                                          
         B     FLTKEY24                                                         
                                                                                
FLTKEY22 CLC   TRNKDATE,ACMDSTR1   NO DUE DATE - USE TRANSACTION DATE           
         BL    FLTKEYN                                                          
         CLC   TRNKDATE,ACMDEND1                                                
         BH    FLTKEYN                                                          
*&&                                                                             
FLTKEY24 DS    0H                  OTHER TRANSACTION FILTERING HERE             
         B     FLTKEYY                                                          
         DROP  R3                                                               
                                                                                
         USING TIMRECD,R3                                                       
FLTKEY26 DS    0H                                                               
         DROP  R3                                                               
                                                                                
FLTKEYY  CLI   FCPRCDIR,YES        TEST APPLICATION WANTS DIRECTORY             
         BNE   FLTKEYYX            RECORD PASSED FOR FILTERING                  
         MVI   MODE,PROCTRND                                                    
         GOTOR ACAPPLIC,ACMPARA,ACWORKD,(R3)                                    
         BNE   FLTKEYN             CC=NOT EQUAL - DISCARD RECORD                
FLTKEYYX J     EXITEQ                                                           
                                                                                
FLTKEYN  J     EXITHI              SET CC=NEQ                                   
         DROP  RB                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO CALL GETOPT TO RESOLVE PRODUCTION OPTIONS                *         
***********************************************************************         
                                                                                
GETPOP   NTR1  BASE=*,LABEL=*                                                   
         L     R2,ADGOBLOC         R2=A(GOBLOCK)                                
         USING GOBLOCKD,R2                                                      
         GOTOR GETOPT,ACMPARA,GOBLOCKD                                          
         L     R1,ADPROFIL         A(COMPOSITE PROFILE)                         
         USING PPRELD,R1                                                        
         MVC   PPRBTYPE,GOBILTYP   REPLACE THE BILL TYPE                        
         MVC   PPRBLAMT,GOBILAM1   AND BILL AMOUNT                              
GETPOPX  J     EXIT                                                             
         DROP  R1,R2,RB                                                         
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO GET OPTION BLOCK FOR PRODUCTION LEDGER ACCOUNTS          *         
* NTRY R1=A(ADDRESS OF RECORD)                                        *         
***********************************************************************         
                                                                                
PSTOPT   L     R1,0(R1)            R1=A(RECORD)                                 
         CLC   ACMPROD,1(R1)       TEST PRODUCTION LEDGER                       
         BNER  RE                                                               
PSTOPT02 NTR1  BASE=*,LABEL=*                                                   
         L     R2,ADGOBLOC         R2=A(GOBLOCK)                                
         USING GOBLOCKD,R2                                                      
         MVC   GOABUFF,ACMAGOB                                                  
         MVC   GOLBUFF,ACMLGOB                                                  
         MVC   GOADM,DATAMGR                                                    
         MVC   GOAKEY,LASTIO                                                    
         L     R1,ADLEDGER                                                      
         MVC   GOSELCUL,0(R1)      COMPANY/UNIT/LEDGER                          
         MVC   GOACOMP,ADCOMP      A(COMPANY RECORD)                            
         MVC   GOALEDG,ADLEDGER    A(LEDGER RECORD)                             
         MVC   GOCTRY,RCCTRY                                                    
         MVC   GOAEXT,ACMAGOX      A(EXTENDED GOBLOCK)                          
         MVC   GOABEXT,ACMAGOBB    A(BILLING EXTENSION GOBLOCK)                 
                                                                                
         XC    GOSELPRO,GOSELPRO                                                
         XC    GOAPRO,GOAPRO                                                    
         XC    GOSELJOB,GOSELJOB                                                
         XC    GOAJOB,GOAJOB                                                    
         XC    GOSELWC,GOSELWC                                                  
                                                                                
         MVC   GOSELCLI,SPACES                                                  
         L     R1,ADHEIRA                                                       
         MVC   GOSELCLI,3(R1)      CLIENT CODE                                  
         MVC   GOACLI,ADHEIRA      A(CLIENT RECORD)                             
         CLI   MODE,PROCLEVA       IF LEVEL A ONLY WANT CLIENT                  
         BE    PSTOPT04                                                         
                                                                                
         MVC   GOSELPRO,SPACES                                                  
         L     R1,ADHEIRB                                                       
         AH    R1,ACMLVA           LENGTH OF LEVEL A-1                          
         MVC   GOSELPRO,4(R1)      PRODUCT CODE TO GOBLOCK                      
         MVC   GOAPRO,ADHEIRB      A(PRODUCT RECORD)                            
         CLI   MODE,PROCLEVB       IF LEVEL B ONLY WANT CLIENT/PRO              
         BE    PSTOPT04                                                         
                                                                                
         MVC   GOSELJOB,SPACES                                                  
         L     R1,ADHEIRC                                                       
         AH    R1,ACMLVB           LENGTH OF LEVEL A+B-1                        
         MVC   GOSELJOB,4(R1)      JOB CODE TO GOBLOCK                          
         MVC   GOAJOB,ADHEIRC      A(JOB RECORD)                                
                                                                                
PSTOPT04 CLI   FCGETOPT,NO         TEST NO-OP GETOPT CALL                       
         BE    PSTOPTX                                                          
         CLI   FCGETOPT,FCGETOWC   TEST WORK CODE & JOB LEVELS ONLY             
         BE    *+12                                                             
         CLI   FCGETOPT,FCGETOJB   TEST JOB LEVEL GETOPT ONLY                   
         BNE   *+12                                                             
         CLI   MODE,PROCLEVC       TEST JOB LEVEL MODE                          
         BNE   PSTOPTX                                                          
         GOTOR GETPOP                                                           
                                                                                
PSTOPTX  J     EXIT                                                             
         DROP  R2,RB                                                            
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO APPLY OFFICE/ANALYSIS FILTERS TO TRANSACTION RECORDS     *         
***********************************************************************         
                                                                                
         USING TRNELD,R2                                                        
OFFFLT   STM   RE,R1,12(RD)                                                     
         L     RF,ADACC                                                         
         CLC   ACMPROD,ACTKUNT-ACTRECD(RF)                                      
         JE    OFFFLT10                                                         
         CLI   FCSUPOFC,YES        TEST TO SUPPRESS OFFICE FILTERING            
         JE    OFFFLTY             YES                                          
         TM    ACMINDS,ACMINEWO    TEST NEW OFFICES IN USE                      
         JZ    OFFFLT06                                                         
         L     R1,ACMAOFL                                                       
         USING OFFALD,R1                                                        
         OC    OFFANEWA,OFFANEWA   TEST FOR LIMITED ACCESS                      
         JNZ   *+14                YES                                          
         CLC   OFFAOFFC,SPACES     TEST FOR A REQUESTED OFFICE FILTER           
         JE    OFFFLTY             NO-ALLOW THE TRANSACTION                     
                                                                                
         CLI   ACMOFPOS,LDGONKLO   TEST FOR OFFICE IN KEY LEDGER                
         JL    OFFFLT02            NO                                           
         CLI   ACMOFPOS,LDGONKHI                                                
         JNH   OFFFLTY             SKIP TRANS OFFICE FILLTERING                 
                                                                                
OFFFLT02 SR    R0,R0                                                            
         ICM   R0,3,ACMOFCN                                                     
         JNZ   *+6                                                              
         DC    H'0'                                                             
         LA    R1,ACMROFL                                                       
         LA    RF,ACMTKOFF                                                      
         TM    ACMHIST,ACMHPRC     HISTORY RECORD                               
         JO    *+12                                                             
         TM    ACMINDS,ACMIEMUD                                                 
         JNZ   OFFFLT04                                                         
         LA    RF,TRNOFFC                                                       
                                                                                
OFFFLT04 CLC   0(L'TRNOFFC,RF),0(R1) MATCH OFFICE TO REQUESTED LIST             
         JE    OFFFLTY                                                          
         AHI   R1,L'TRNOFFC        BUMP TO NEXT LIST ENTRY                      
         BRCT  R0,OFFFLT04                                                      
         J     OFFFLTN                                                          
                                                                                
OFFFLT06 CLI   ACMOFPOS,LDGOTRAN   IS OFFICE AT TRANSACTION LEVEL               
         JNE   *+12                                                             
         TM    ACMSTAT,ACMFTRN     DO THEY ALWAYS WANT TO FILTER                
         JO    *+12                                                             
         CLI   QOFFICE,C' '        OPTION TO FILTER ANALYSIS CODE               
         JE    OFFFLTY                                                          
         LA    RF,ACMTOFFC                                                      
         TM    ACMHIST,ACMHPRC     HISTORY RECORD                               
         JO    *+12                                                             
         TM    ACMINDS,ACMIEMUD                                                 
         JNZ   *+8                                                              
         LA    RF,TRNOFFC                                                       
         CLC   QOFFICE(1),0(RF)                                                 
         JE    OFFFLTY                                                          
         TM    QOFFICE,X'40'       TEST ALL EXCEPT REQUEST                      
         JO    OFFFLTN                                                          
         MVC   ACMWRK(1),QOFFICE                                                
         OI    ACMWRK,X'40'                                                     
         CLC   ACMWRK(1),0(RF)                                                  
         JE    OFFFLTN                                                          
         J     OFFFLTY                                                          
                                                                                
OFFFLT10 CLC   QTRNSFLT,SPACES     WORKCODE FILTER?                             
         JE    OFFFLTY                                                          
         LA    RF,ACMTKOFF                                                      
         TM    ACMHIST,ACMHPRC     HISTORY RECORD                               
         JO    *+12                                                             
         TM    ACMINDS,ACMIEMUD    TEST NEW FILE                                
         JNZ   *+8                                                              
         LA    RF,TRNANAL                                                       
         CLC   QTRNSFLT,0(RF)      MATCH WORK CODE FILTER                       
         JE    OFFFLTY                                                          
         TM    QTRNSFLT,X'40'      TEST ALL EXCEPT REQUEST                      
         JO    OFFFLTN                                                          
         MVC   ACMWRK(L'QTRNSFLT),QTRNSFLT                                      
         OI    ACMWRK,X'40'                                                     
         CLC   ACMWRK(L'QTRNSFLT),0(RF)                                         
         JE    OFFFLTN                                                          
                                                                                
OFFFLTY  CR    RE,RE                                                            
         J     OFFFLTX                                                          
OFFFLTN  LTR   RE,RE                                                            
                                                                                
OFFFLTX  LM    RE,R1,12(RD)                                                     
         BR    RE                                                               
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO POST ELEMENT ADDRESSES AND FIX BALANCE ELEMENT           *         
***********************************************************************         
                                                                                
PSTELS   XC    ADACCELS(ADACCELX-ADACCELS),ADACCELS                             
         XC    ACMAXPRO,ACMAXPRO                                                
         L     R1,0(R1)                                                         
         ST    R1,ADACC                                                         
         AH    R1,DATADISP                                                      
         SR    R0,R0                                                            
PSTELS02 CLI   0(R1),0                                                          
         BER   RE                                                               
         CLI   0(R1),NAMELQ                                                     
         JNE   *+8                                                              
         ST    R1,ADACCNAM                                                      
         CLI   0(R1),ADRELQ                                                     
         JNE   *+8                                                              
         ST    R1,ADACCADD                                                      
         CLI   0(R1),PPRELQ                                                     
         JNE   *+8                                                              
         ST    R1,ADACCJOB                                                      
         CLI   0(R1),RSTELQ                                                     
         JNE   *+8                                                              
         ST    R1,ADACCSTA                                                      
         CLI   0(R1),XPRELQ                                                     
         JNE   *+8                                                              
         ST    R1,ACMAXPRO                                                      
         CLI   0(R1),ABLELQ                                                     
         JNE   PSTELS04                                                         
         ST    R1,ADACCBAL                                                      
PSTELS04 IC    R0,1(R1)                                                         
         AR    R1,R0                                                            
         J     PSTELS02                                                         
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO FILTER ACCOUNTS OR TRANSACTIONS AGAINST THE LIMIT ACCESS *         
* OFFICE LIST                                                         *         
***********************************************************************         
                                                                                
OFFLMT   BASE  RF                                                               
         STM   RE,R3,12(RD)                                                     
         CLI   FCSUPOFC,YES        TEST TO SUPPRESS OFFICE FILTERING            
         BE    OFFLMTY                                                          
         TM    ACMINDS,ACMINEWO    TEST NEW OFFICES IN USE                      
         BNZ   OFFNEW                                                           
         MVC   ACMBYTE,ACMFLAG     SAVE FLAG SETTING                            
         L     R1,ADOFFLST         POINT TO OFFICE LIST                         
         CLI   0(R1),0                                                          
         BE    OFFLMT16            NO LIMIT ACCESS                              
         LHI   R0,32               R0=NUMBER OF OFFICES IN LIST                 
         CLI   ACMFLAG,ACMFLAC                                                  
         BE    OFFLMT04                                                         
         CLI   ACMFLAG,ACMFLTO     TEST 'OTHER' TYPES OF OFFICE                 
         BNE   *+14                                                             
         MVC   ACMFLAG,ACMWRK      YES - OFFICE PASSED IN ACMWRK                
         B     OFFLMT10                                                         
         CLI   ACMOFPOS,LDGOTRAN   FOR TRANSACTIONS                             
         BE    OFFLMT02                                                         
         CLI   ACMSYS,ACMSUSA                                                   
         BNE   OFFLMTY                                                          
         CLI   ACMOFPOS,0          OR IF NOT SPECIFIED (US ONLY)                
         BE    OFFLMT02                                                         
         B     OFFLMTY                                                          
                                                                                
OFFLMT02 LA    RE,TRNANAL-TRNELD(R2)                                            
         CLI   ACMFLAG,ACMFLTD     TEST DIRECTORY LEVEL FILTERING               
         BNE   *+8                                                              
         LA    RE,ACMTOFFC                                                      
         MVC   ACMFLAG,0(RE)       SET OFFICE CODE                              
         B     OFFLMT10                                                         
                                                                                
OFFLMT04 CLI   ACMOFPOS,LDGOTRAN   IF OFFICE ON TRANSACTIONS IT'S OK            
         BE    OFFLMTY                                                          
         CLI   ACMOFPOS,0          TEST OFFICE IN ACCOUNT KEY                   
         BE    OFFLMTY                                                          
         CLI   ACMOFPOS,LDGOKEY                                                 
         BH    OFFLMT06                                                         
         SR    R3,R3                                                            
         IC    R3,ACMOFPOS         POSITIONAL OFFICE CODE                       
         A     R3,ADACC                                                         
         AHI   R3,2                POINT R3 TO OFFICE CODE IN ACCOUNT           
         MVC   ACMFLAG,0(R3)                                                    
         CLI   ACMFLAG,C' '                                                     
         BE    OFFLMTY             IF HIGH LEVEL IT'S OK                        
         B     OFFLMT10                                                         
                                                                                
OFFLMT06 CLI   ACMOFPOS,LDGOFLT1   TEST OFFICE IN ACCOUNT FILTERS               
         BL    OFFLMT08                                                         
         MVC   ACMFLAG,ACMOFPOS    ADJUST OFFPOS                                
         NI    ACMFLAG,X'07'                                                    
         SR    R3,R3                                                            
         IC    R3,ACMFLAG                                                       
         IC    R3,ACMFLTS-1(R3)                                                 
         STC   R3,ACMFLAG          SET FILTER VALUE                             
         B     OFFLMT10                                                         
                                                                                
OFFLMT08 CLI   ACMOFPOS,LDGOPROF   TEST CLIENT OFFICE (PPRUFORA)                
         BNE   OFFLMTY                                                          
         L     R3,ADPROFIL                                                      
         MVC   ACMFLAG,PPRGAOFF-PPRELD(R3)                                      
                                                                                
OFFLMT10 DS    0H                                                               
*&&US*&& CLI   0(R1),C'0'          SKIP OFFICE ZERO                             
*&&UK*&& CLI   0(R1),C'*'          SKIP OFFICE ASTERISK                         
         BE    OFFLMT12                                                         
         CLI   0(R1),0             SKIP ZERO ENTRIES                            
         BE    OFFLMT12                                                         
         CLC   ACMFLAG,0(R1)       MATCH OFFICE TO LIST                         
         BE    OFFLMT16                                                         
OFFLMT12 AHI   R1,1                BUMP TO NEXT LIST ENTRY                      
         BCT   R0,OFFLMT10                                                      
                                                                                
         L     RE,ADACC            OFFICE NOT FOUND-EXCLUDE RECORD              
         CLC   =C'1R',1(RE)        TEST PERSONNEL LEDGER                        
         BNE   OFFLMTN             NO-OFFICE NOT FOUND-EXCLUDE RECORD           
         CLI   FCPERSEC,FCPERCLI   TEST CLIENT ORIENTED SECURITY                
         BE    *+12                                                             
         CLI   FCPERSEC,FCPERBTH   CHECK 1R AND 1C OFFICE?                      
         BNE   OFFLMTN             NO-                                          
         CLI   ACMBYTE,ACMFLAC     ACCOUNT LEVEL CALL?                          
         BNE   OFFLMTN             NO                                           
         B     OFFLMTY             PASS BACK ALL ACCOUNTS                       
                                                                                
OFFLMT16 L     RE,ADACC            OFFICE  FOUND-INCLUDE RECORD                 
         CLC   =C'1R',1(RE)        TEST PERSONNEL LEDGER                        
         BNE   OFFLMTY             NO- EXIT OK                                  
         CLI   ACMBYTE,ACMFLAC     ACCOUNT LEVEL CALL?                          
         BNE   OFFLMTY             NO- EXIT OK                                  
         OI    ACMPESEC,ACMPEEMP   SHOW THAT 1R OFFICE PASSED SECURITY          
         B     OFFLMTY             EXIT OK                                      
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO FILTER ACCOUNTS OR TRANSACTIONS FOR NEW OFFICE SYSTEM    *         
* AGAINST THE LIMIT ACCESS OFFICE LIST                                *         
***********************************************************************         
                                                                                
OFFNEW   SR    R0,R0                                                            
         ICM   R0,3,ACMOFCN        R0=N'ENTRIES IN OFFICE LIST                  
         BNZ   *+6                                                              
         DC    H'0'                                                             
         L     R1,ACMAOFL                                                       
         USING OFFALD,R1                                                        
         OC    OFFANEWA,OFFANEWA   TEST FOR LIMITED ACCESS                      
         BNZ   *+14                YES                                          
         CLC   OFFAOFFC,SPACES     TEST FOR A REQUESTED OFFICE FILTER           
         BE    OFFNEW16            NO-ALLOW ACCESS                              
         LA    R1,ACMROFL                                                       
         CLI   ACMFLAG,ACMFLTO     TEST 'OTHER' OFFICE PASSED                   
         BNE   OFFNEW01                                                         
         LA    R3,ACMWRK           YES - VALUE IS PASSED IN ACMWRK              
         B     OFFNEW14                                                         
                                                                                
OFFNEW01 CLI   MODE,PROCSBOF       TEST FOR ACCT/OFF/CONTRA BUCKET              
         BE    OFFNEW04                                                         
         CLI   ACMFLAG,ACMFLAC     TEST FOR ACCOUNT LEVEL CALL                  
         BE    OFFNEW08                                                         
         CLI   ACMFLAG,ACMFLTD     TEST DIRECTORY LEVEL FILTER                  
         BE    OFFNEW04                                                         
                                                                                
***********************************************************************         
* MONFLAG = 'T'  TRANSACTION LEVEL CALL OR 'O' OFFICE ACCOUNT LEVEL   *         
***********************************************************************         
                                                                                
OFFNEW02 L     R3,ACMAOFA                                                       
         CLI   ACMFLAG,ACMFLOA     TEST OFFICE ACCOUNT                          
         BE    *+10                YES                                          
         LR    R3,R2               HANDLE TRANSACTIONS                          
         SH    R3,DATADISP                                                      
                                                                                
OFFNEW04 CLC   ACMPROD,1(R3)       TEST PRODUCTION LEDGER                       
         BE    OFFLMTY                                                          
                                                                                
         CLI   ACMOFPOS,LDGONKLO   TEST OFFICE IN KEY LEDGER                    
         BL    OFFNEW06            NO                                           
         CLI   ACMOFPOS,LDGONKHI                                                
         BH    OFFNEW06            NO                                           
         CLC   =C'1R',1(R3)        TEST PERSONNEL LEDGER                        
         BNE   OFFLMTY             NO-ACCEPT TRANSACTION                        
                                                                                
         CLI   FCPERSEC,FCPERCLI   TEST CLIENT ORIENTED SECURITY                
         BE    OFFNEW06            YES- CLT OFFICE FILTERING                    
         CLI   FCPERSEC,FCPERBTH   TEST CHECK 1R AND 1C OFFICE                  
         BNE   OFFLMTY             NO-SKIP CLT OFFICE FILTERING                 
         TM    ACMPESEC,ACMPEEMP   DID 1R OFFICE ALREADY PASS SECURITY?         
         BO    OFFLMTY             YES- SKIP CLT OFFICE FILTERING               
                                                                                
OFFNEW06 CLI   ACMFLAG,ACMFLTN     TEST TRANSACTION LEVEL CALL                  
         BE    *+12                YES                                          
         AHI   R3,TRNKOFF-TRNKEY   NO-BUMP TO OFFICE                            
         B     *+8                                                              
         LA    R3,TRNOFFC-TRNELD(R2)                                            
         B     OFFNEW14                                                         
                                                                                
***********************************************************************         
* MONFLAG = 'A'  ACCOUNT LEVEL CALL                                   *         
***********************************************************************         
                                                                                
OFFNEW08 L     R3,ADACC            HANDLE PRODUCTION ACCOUNTS                   
         CLC   ACMPROD,1(R3)       TEST PRODUCTION LEDGER                       
         BNE   OFFNEW10            NO                                           
         L     R3,ADPROFIL         YES-POINT R3 AT OFFICE                       
         AHI   R3,PPRGAOFF-PPRELD                                               
         B     OFFNEW14                                                         
                                                                                
OFFNEW10 CLI   ACMOFPOS,LDGONKLO   TEST OFFICE IN KEY LEDGER                    
         BL    OFFLMTY             NO-SECURITY IS AT TRANSACTION LEVEL          
         CLI   ACMOFPOS,LDGONKHI                                                
         BH    OFFLMTY                                                          
                                                                                
OFFNEW12 IC    R3,ACMOFPOS                                                      
         N     R3,=F'15'                                                        
         A     R3,ADACC                                                         
         AHI   R3,2                ADD IN UNIT AND LEDGER                       
         CLC   0(2,R3),SPACES      TEST FOR HIGH LEVEL ACCOUNT                  
         BE    OFFLMTY             YES                                          
                                                                                
***********************************************************************         
* SECURITY TEST LOOP                                                  *         
***********************************************************************         
                                                                                
OFFNEW14 CLC   0(L'PPRGAOFF,R1),0(R3)                                           
         BE    OFFNEW16            OFFICE FOUND - INCLUDE RECORD                
         AHI   R1,L'PPRGAOFF                                                    
         BCT   R0,OFFNEW14                                                      
                                                                                
         L     RE,ADACC            OFFICE NOT FOUND-EXCLUDE RECORD              
         CLC   =C'1R',1(RE)        TEST PERSONNEL LEDGER                        
         BNE   OFFLMTN             NO-OFFICE NOT FOUND-EXCLUDE RECORD           
         CLI   FCPERSEC,FCPERCLI   TEST CLIENT ORIENTED SECURITY                
         BE    *+12                                                             
         CLI   FCPERSEC,FCPERBTH   SPEC SET TO CHECK 1R AND 1C OFFICE?          
         BNE   OFFLMTN             NO-                                          
         CLI   ACMFLAG,ACMFLAC     ACCOUNT LEVEL CALL?                          
         BNE   OFFLMTN             NO                                           
         B     OFFLMTY             PASS BACK ALL ACCOUNTS                       
                                                                                
OFFNEW16 L     RE,ADACC            OFFICE NOT FOUND-EXCLUDE RECORD              
         CLC   =C'1R',1(RE)        TEST PERSONNEL LEDGER                        
         BNE   OFFLMTY             NO-OFFICE FOUND-INCLUDE RECORD               
         CLI   ACMFLAG,ACMFLAC     ACCOUNT LEVEL CALL?                          
         BNE   OFFLMTY             NO- EXIT OK                                  
         OI    ACMPESEC,ACMPEEMP   SHOW THAT 1R OFFICE PASSED SECURITY          
         B     OFFLMTY             EXIT OK                                      
                                                                                
OFFLMTN  CLI   *,0                 NO MATCH SET CC=NE                           
         B     OFFLMTX                                                          
OFFLMTY  CLI   *+1,0               MATCHED SET CC=EQ                            
                                                                                
OFFLMTX  LM    RE,R3,12(RD)                                                     
         BR    RE                                                               
         DROP  RF                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO APPLY QBILGRUP FILTER TO PRODUCTION ACCOUNT PROFILE      *         
***********************************************************************         
                                                                                
SETRQF   XC    ACMTTYP,ACMTTYP     SET TRANSACTION TYPE FILTER                  
*&&US                                                                           
         CLC   QSELECT(3),=C'TYPE'                                              
         JNE   SETRQF02                                                         
         LHI   R1,1                                                             
         MVC   ACMWRK(2),QSELECT+4 SAVE TYPE FIELD                              
         CLI   QSELECT+3,C'E'                                                   
         JE    *+14                                                             
         LHI   R1,2                                                             
         MVC   ACMWRK(3),QSELECT+3                                              
         OI    ACMWRK,X'40'                                                     
         MVI   ACMTMSK,X'70'       SET CONDITION MASK VALUE                     
         TM    QSELECT+4,X'40'                                                  
         JNZ   *+8                                                              
         MVI   ACMTMSK,X'80'                                                    
                                                                                
         CLI   ACMWRK,C'M'         HANDLE MANUAL TYPE FILTER                    
         JE    SETRQF04                                                         
         BASR  RF,0                                                             
         EX    R1,8(RF)            HANDLE NUMERIC TRANSACTION TYPE              
         J     *+10                                                             
         PACK  ACMDUB,ACMWRK(0)                                                 
         CVB   R1,ACMDUB                                                        
         STC   R1,ACMTFLT          SET TRNTYP FILTER VALUE                      
         J     SETRQF04                                                         
SETRQF02 DS    0H                                                               
*&&                                                                             
         CLC   QTRNSTYP,SPACES                                                  
         JE    SETRQF04                                                         
         MVI   ACMTMSK,X'70'       SET CONDITION MASK VALUE                     
         TM    QTRNSTYP,X'40'                                                   
         JNZ   *+8                                                              
         MVI   ACMTMSK,X'80'                                                    
         MVC   ACMWRK(L'QTRNSTYP),QTRNSTYP                                      
         OI    ACMWRK,X'40'                                                     
*&&US*&& CLI   ACMWRK,C'M'         HANDLE MANUAL TYPE FILTER                    
*&&US*&& JE    SETRQF04                                                         
         PACK  ACMDUB,ACMWRK(L'QTRNSTYP)                                        
         CVB   R1,ACMDUB                                                        
         STC   R1,ACMTFLT                                                       
                                                                                
SETRQF04 MVI   ACMTSINC,0                                                       
         MVI   ACMTSEXC,0                                                       
         CLI   QSTATUS,C' '                                                     
         JE    SETRQF06                                                         
         LA    R1,ACMTSINC                                                      
         TM    QSTATUS,X'40'                                                    
         JNZ   *+8                                                              
         LA    R1,ACMTSEXC                                                      
         MVC   ACMWRK,QSTATUS                                                   
         OI    ACMWRK,X'40'                                                     
         CLI   ACMWRK,C'A'         TEST APPROVED FILTER                         
         JNE   *+8                                                              
         OI    0(R1),TRNSAPPR                                                   
         CLI   ACMWRK,C'H'         TEST HOLD FILTER                             
         JNE   SETRQF06                                                         
         OI    0(R1),TRNSHOLD                                                   
                                                                                
SETRQF06 DS    0H                                                               
                                                                                
SETRQFX  BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO APPLY QBILGRUP FILTER TO PRODUCTION ACCOUNT PROFILE      *         
***********************************************************************         
                                                                                
         USING PPRELD,R1                                                        
FLTBGP   CLC   QBILGRUP,SPACES                                                  
         JE    FLTBGPY                                                          
         CLC   PPRGRUP,QBILGRUP                                                 
         JE    FLTBGPY                                                          
         TM    QBILGRUP,X'40'                                                   
         JNZ   FLTBGPN                                                          
         MVC   ACMWRK(L'QBILGRUP),QBILGRUP                                      
         OI    ACMWRK,X'40'                                                     
         CLC   PPRGRUP,ACMWRK                                                   
         JNE   FLTBGPY                                                          
                                                                                
FLTBGPN  LTR   RE,RE                                                            
         BR    RE                                                               
FLTBGPY  CR    RE,RE                                                            
         BR    RE                                                               
         DROP  R1                                                               
         EJECT                                                                  
***********************************************************************         
* SUB-ROUTINE TO SET COMPANY ELEMENT ADDRESSES, ON EXIT R1=A(EOR)     *         
***********************************************************************         
                                                                                
SETCPY   L     R1,ADCOMP           SET COMPANY ELEMENT ADDRESSES                
         AH    R1,DATADISP                                                      
         SR    R0,R0                                                            
         XC    ADCMPELS(ADCMPELX-ADCMPELS),ADCMPELS                             
SETCPY02 CLI   0(R1),0                                                          
         BER   RE                                                               
         CLI   0(R1),CPYELQ                                                     
         JNE   *+8                                                              
         ST    R1,ADCMPEL                                                       
         CLI   0(R1),NAMELQ                                                     
         JNE   *+8                                                              
         ST    R1,ADCMPNAM                                                      
         CLI   0(R1),ADRELQ                                                     
         JNE   *+8                                                              
         ST    R1,ADCMPADD                                                      
         IC    R0,1(R1)                                                         
         AR    R1,R0                                                            
         J     SETCPY02                                                         
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO ESTABLISH COMPOSITE PRODUCTION PROFILE                   *         
***********************************************************************         
                                                                                
         USING PPRELD,R1                                                        
SETPPR   L     RF,ADPROFIL                                                      
         CLI   PPRLN,PPRNARRP-PPRELD                                            
         JE    *+10                                                             
         MVC   PPRLN-PPRELD(L'PPRLN,RF),PPRLN                                   
         OC    PPRRECV,PPRRECV                                                  
         JZ    *+10                                                             
         MVC   PPRRECV-PPRELD(L'PPRRECV,RF),PPRRECV                             
         OC    PPRCOST,PPRCOST                                                  
         JZ    *+10                                                             
         MVC   PPRCOST-PPRELD(L'PPRCOST,RF),PPRCOST                             
         OC    PPRBTYPE,PPRBTYPE                                                
         JZ    *+10                                                             
         MVC   PPRBTYPE-PPRELD(L'PPRBTYPE,RF),PPRBTYPE                          
         OC    PPRBLAMT,PPRBLAMT                                                
         JZ    *+10                                                             
         MVC   PPRBLAMT-PPRELD(L'PPRBLAMT,RF),PPRBLAMT                          
         OC    PPRUFORA,PPRUFORA                                                
         JZ    *+10                                                             
         MVC   PPRUFORA-PPRELD(L'PPRUFORA,RF),PPRUFORA                          
         CLC   PPRGAOFF,SPACES                                                  
         JNH   *+10                                                             
         MVC   PPRGAOFF-PPRELD(L'PPRGAOFF,RF),PPRGAOFF                          
         OC    PPRUWRK(PPRUWRKN*L'PPRUWRK),PPRUWRK                              
         JZ    *+10                                                             
         MVC   PPRUWRK-PPRELD(PPRUWRKN*L'PPRUWRK,RF),PPRUWRK                    
         CLC   PPRBILLP,SPACES                                                  
         JNH   *+10                                                             
         MVC   PPRBILLP-PPRELD(L'PPRBILLP,RF),PPRBILLP                          
         CLI   PPRLN,PPRNARRP-PPRELD                                            
         JE    *+10                                                             
         MVC   PPRNARRP-PPRELD(L'PPRNARRP,RF),PPRNARRP                          
SETPPRX  BR    RE                                                               
         DROP  R1                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO ESTABLISH COMPOSITE PRODUCTION EXTRA PROFILE             *         
***********************************************************************         
                                                                                
         USING XPRELD,R1                                                        
SETXPR   LA    RF,XPROFDEF                                                      
         CP    XPRDUE,(XPRDUE-XPRELD)(L'XPRDUE,RF)                              
         JE    *+10                                                             
         MVC   ACMXPRO+(XPRDUE-XPRELD)(L'XPRDUE),XPRDUE                         
         CP    XPROVER,(XPROVER-XPRELD)(L'XPROVER,RF)                           
         JE    *+10                                                             
         MVC   ACMXPRO+(XPROVER-XPRELD)(L'XPROVER),XPROVER                      
         CP    XPRLOW,(XPRLOW-XPRELD)(L'XPRLOW,RF)                              
         JE    *+10                                                             
         MVC   ACMXPRO+(XPRLOW-XPRELD)(L'XPRLOW),XPRLOW                         
         CLC   XPRSUM,(XPRSUM-XPRELD)(RF)                                       
         JE    *+10                                                             
         MVC   ACMXPRO+(XPRSUM-XPRELD)(L'XPRSUM),XPRSUM                         
         CLC   XPRNET,(XPRNET-XPRELD)(RF)                                       
         JE    *+10                                                             
         MVC   ACMXPRO+(XPRNET-XPRELD)(L'XPRNET),XPRNET                         
         CLC   XPRDET,(XPRDET-XPRELD)(RF)                                       
         JE    *+10                                                             
         MVC   ACMXPRO+(XPRDET-XPRELD)(L'XPRDET),XPRDET                         
         CLC   XPREDET,(XPREDET-XPRELD)(RF)                                     
         JE    *+10                                                             
         MVC   ACMXPRO+(XPREDET-XPRELD)(L'XPREDET),XPREDET                      
         CLC   XPRCD,(XPRCD-XPRELD)(RF)                                         
         JE    *+10                                                             
         MVC   ACMXPRO+(XPRCD-XPRELD)(L'XPRCD),XPRCD                            
         CLI   XPRLN,XPRLN2Q                                                    
         JL    SETXPRX                                                          
         CLC   XPREST,(XPREST-XPRELD)(RF)                                       
         JE    *+10                                                             
         MVC   ACMXPRO+(XPREST-XPRELD)(L'XPREST),XPREST                         
         OC    XPRSTAT1,XPRSTAT1                                                
         JZ    *+10                                                             
         MVC   ACMXPRO+(XPRSTAT1-XPRELD)(L'XPRSTAT1),XPRSTAT1                   
         OC    XPRSTAT2,XPRSTAT2                                                
         JZ    *+10                                                             
         MVC   ACMXPRO+(XPRSTAT2-XPRELD)(L'XPRSTAT2),XPRSTAT2                   
         OC    XPRBILL,XPRBILL                                                  
         JZ    *+10                                                             
         MVC   ACMXPRO+(XPRBILL-XPRELD)(L'XPRBILL),XPRBILL                      
         OC    XPRINTC,XPRINTC                                                  
         JZ    *+10                                                             
         MVC   ACMXPRO+(XPRINTC-XPRELD)(L'XPRINTC),XPRINTC                      
SETXPRX  BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO SET FILTER VALUES FROM ACCOUNT STATUS ELEMENT            *         
***********************************************************************         
                                                                                
SETFLT   LTR   R1,R1                                                            
         BZR   RE                                                               
         USING RSTELD,R1                                                        
*&&UK                                                                           
         TM    RSTSTAT1,RSTSACIC   TEST/SET ACCOUNT IS CLOSED                   
         JZ    *+8                                                              
         OI    ACMINDS4,ACMICLOA                                                
         TM    RSTSTAT1,RSTSACIL   TEST/SET ACCOUNT IS LOCKED                   
         JZ    *+8                                                              
         OI    ACMINDS4,ACMILOKA                                                
*&&                                                                             
         CLI   RSTFILT1,X'41'                                                   
         JL    *+10                                                             
         MVC   ACMFLT1,RSTFILT1                                                 
         CLI   RSTFILT2,X'41'                                                   
         JL    *+10                                                             
         MVC   ACMFLT2,RSTFILT2                                                 
         CLI   RSTFILT3,X'41'                                                   
         JL    *+10                                                             
         MVC   ACMFLT3,RSTFILT3                                                 
         CLI   RSTFILT4,X'41'                                                   
         JL    *+10                                                             
         MVC   ACMFLT4,RSTFILT4                                                 
         CLI   RSTLN,RSTLN2Q       TEST FILTER5 PRESENT                         
         JL    SETFLT02                                                         
         CLI   RSTFILT5,X'41'                                                   
         JL    *+10                                                             
         MVC   ACMFLT5,RSTFILT5                                                 
SETFLT02 TM    ACMINDS,ACMIMOSR    DON'T SET ACCTYPE IF NOT CONVERTED           
         BZR   RE                                                               
         TM    RSTSTAT3,RSTSLAPL+RSTSLABS                                       
         BZR   RE                  NOT OVERRIDDEN IN ACCOUNT                    
         MVI   ACMATYP,ACMATPL                                                  
         TM    RSTSTAT3,RSTSLAPL                                                
         BOR   RE                                                               
         MVI   ACMATYP,ACMATBS                                                  
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO SET MEDIA GROUP FOR MEDIA                                *         
* NTRY - FCRULMED=MEDIA CODE                                          *         
***********************************************************************         
                                                                                
SETMGR   MVI   ACMMEGRP,C' '                                                    
         L     R1,ADCOMP                                                        
         AH    R1,DATADISP         R1=A(COMPANY FIRST ELEMENT)                  
         SR    R0,R0                                                            
         USING PMDELD,R1                                                        
SETMGR02 CLI   PMDEL,0             TEST FOR END OF BUFFER                       
         BER   RE                                                               
         CLI   PMDEL,PMDELQ        TEST FOR MEDIA ELEMENT                       
         JNE   *+14                                                             
         CLC   FCRULMED,PMDCODE    MATCH ON MEDIA CODE                          
         JE    *+14                                                             
         IC    R0,PMDLN                                                         
         AR    R1,R0                                                            
         J     SETMGR02                                                         
         MVC   ACMMEGRP,PMDGRP     EXTRACT GROUP VALUE                          
         OC    ACMMEGRP,SPACES     SPACE PAD THE GROUP                          
         BR    RE                                                               
         DROP  R1                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO APPLY MEDIA OR MEDIA GROUP FILTER FOR ORDER RECORDS      *         
* RETURNS TO 0(RE) TO EXCLUDE, 4(RE) TO INCLUDE                       *         
***********************************************************************         
                                                                                
TSTMED   CLI   0(R2),C' '          SPACE=NO FILTER                              
         JE    TSTMEDY                                                          
         MVC   ACMWRK+0(1),0(R2)                                                
         MVC   ACMWRK+1(1),0(R1)                                                
         LHI   R1,X'80'            SET MASK TO EQUAL                            
         TM    ACMWRK,X'40'        TEST POSITIVE FILTER                         
         JNZ   *+12                                                             
         LHI   R1,X'70'            SET MASK TO NOT EQUAL                        
         OI    ACMWRK,X'40'                                                     
         CLC   ACMWRK(1),ACMWRK+1  MATCH REQUEST FILTER TO VALUE                
         BASR  RF,0                                                             
         EX    R1,8(RF)                                                         
         J     TSTMEDN                                                          
         JNOP  TSTMEDY                                                          
                                                                                
TSTMEDN  LTR   RE,RE                                                            
         J     TSTMEDX                                                          
TSTMEDY  CR    RE,RE                                                            
TSTMEDX  BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO ESTABLISH WORK-CODE TYPE                                 *         
* NTRY - R1=A(WORK-CODE CODE)                                         *         
***********************************************************************         
                                                                                
GETWCT   BASE  RF                                                               
         STM   RE,R1,12(RD)                                                     
         MVI   ACMWCTYP,ACMWTORD                                                
         CLC   =C'**',0(R1)        TEST FOR ORDER WORK-CODE                     
         BE    GETWCTX                                                          
         MVI   ACMWCTYP,ACMWTBIL                                                
         CLC   =C'99',0(R1)        TEST FOR BILLING WORK-CODE                   
         BE    GETWCTX                                                          
         MVI   ACMWCTYP,ACMWTTIM                                                
         L     RE,ACMAWCTL                                                      
         ICM   R0,15,0(RE)         R0=NUMBER OF WORK CODES                      
         BZ    GETWCT02                                                         
         AHI   RE,4                TEST FOR TIME WORK-CODE                      
         CLC   0(L'ACMWRKC,R1),0(RE)                                            
         BE    GETWCTX                                                          
         AHI   RE,L'ACMWRKC                                                     
         BCT   R0,*-14                                                          
                                                                                
GETWCT02 MVI   ACMWCTYP,ACMWTCST   SET COST WORK CODE                           
                                                                                
GETWCTX  LM    RE,R1,12(RD)                                                     
         BR    RE                                                               
         DROP  RF                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO TEST CURRENT WORK-CODE TYPE AND SET NEXT IF NECESSARY    *         
* NTRY - R1=A(WORK-CODE CODE)                                         *         
* EXIT - CC=EQUAL IF CORRECT WORK-CODE TYPE                           *         
*        CC=LOW IF INCORRECT - ACMWRK(2) CONTAINS NEXT WORK CODE      *         
*        CC=HIGH IF NO MORE WORK-CODES FOR THIS TYPE TO PROCESS       *         
***********************************************************************         
                                                                                
NXTWCT   BASE  RF                                                               
         STM   RE,R1,12(RD)                                                     
         TM    ACMWTYPC,ACMWTORD+ACMWTBIL                                       
         BZ    NXTWCT02                                                         
         MVI   ACMBYTE,ACMWTORD                                                 
         CLC   =C'**',0(R1)        TEST FOR ORDER WORK-CODE                     
         BE    NXTWCT06                                                         
         MVI   ACMBYTE,ACMWTBIL                                                 
         CLC   =C'99',0(R1)        TEST FOR BILLING WORK-CODE                   
         BE    NXTWCT06                                                         
         B     NXTWCTHI                                                         
                                                                                
NXTWCT02 L     RE,ACMAWCTL                                                      
         CLI   ACMWTYPC,ACMWTTIM                                                
         BE    *+8                                                              
         L     RE,ACMAWCCL                                                      
         ICM   R0,15,0(RE)         R0=NUMBER OF WORK CODES IN LIST              
         BZ    NXTWCTHI                                                         
         AHI   RE,4                LOOK FOR WORK-CODE IN LIST                   
                                                                                
NXTWCT04 CLC   0(L'ACMWRKC,R1),0(RE)                                            
         BL    NXTWCTLO            LOW - SET NEXT TO PROCESS                    
         BE    NXTWCTEQ            EQUAL - PROCESS THIS                         
         AHI   RE,L'ACMWRKC        HIGH - LOOK AT NEXT IN LIST                  
         BCT   R0,NXTWCT04                                                      
         B     NXTWCTHI            NO MORE IN LIST TO PROCESS                   
                                                                                
NXTWCT06 CLC   ACMBYTE,ACMWTYPC    TEST THIS SPECIAL ONE WANTED                 
         BE    NXTWCTEQ                                                         
         B     NXTWCTHI                                                         
                                                                                
NXTWCTLO MVI   ACMBYTE,0           SET CC=LOW (DON'T PROCESS THIS)              
         MVC   ACMNXWC,0(RE)       AND NEXT WORK-CODE TO PROCESS                
         B     NXTWCTX                                                          
                                                                                
NXTWCTEQ MVI   ACMBYTE,1           SET CC=EQUAL (PROCESS THIS)                  
         B     NXTWCTX                                                          
                                                                                
NXTWCTHI MVI   ACMBYTE,2           SET CC=HIGH (NO MORE TO PROCESS)             
                                                                                
NXTWCTX  CLI   ACMBYTE,1           SET CONDITION CODE FOR CALLER                
         LM    RE,R1,12(RD)                                                     
         BR    RE                                                               
         DROP  RF                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO SET NEXT WORK-CODE TYPE FILTER                           *         
* EXIT WITH CC=NOT EQUAL IF NO MORE TYPES TO PROCESS                  *         
***********************************************************************         
                                                                                
SETWCT   BASE  RF                                                               
         TM    ACMWTYPS,ACMWTORD                                                
         BZ    SETWCT02                                                         
         MVI   ACMWTYPC,ACMWTORD                                                
         NI    ACMWTYPS,FF-ACMWTORD                                             
         MVC   ACMLOWC,=C'**'                                                   
         B     SETWCTY                                                          
                                                                                
SETWCT02 TM    ACMWTYPS,ACMWTTIM                                                
         BZ    SETWCT04                                                         
         MVI   ACMWTYPC,ACMWTTIM                                                
         NI    ACMWTYPS,FF-ACMWTTIM                                             
         L     R1,ACMAWCTL                                                      
         OC    0(4,R1),0(R1)                                                    
         BZ    SETWCT04                                                         
         MVC   ACMLOWC,4(R1)                                                    
         B     SETWCTY                                                          
                                                                                
SETWCT04 TM    ACMWTYPS,ACMWTCST                                                
         BZ    SETWCT06                                                         
         MVI   ACMWTYPC,ACMWTCST                                                
         NI    ACMWTYPS,FF-ACMWTCST                                             
         L     R1,ACMAWCCL                                                      
         OC    0(4,R1),0(R1)                                                    
         BZ    SETWCT06                                                         
         MVC   ACMLOWC,4(R1)                                                    
         B     SETWCTY                                                          
                                                                                
SETWCT06 TM    ACMWTYPS,ACMWTBIL                                                
         BZ    SETWCTN                                                          
         MVI   ACMWTYPC,ACMWTBIL                                                
         NI    ACMWTYPS,FF-ACMWTBIL                                             
         MVC   ACMLOWC,=C'99'                                                   
                                                                                
SETWCTY  CLI   *+1,0                                                            
         BR    RE                                                               
SETWCTN  CLI   *+0,0                                                            
         BR    RE                                                               
         DROP  RF                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO MERGE TRANSACTIONS WITH PEEL HISTORY                     *         
***********************************************************************         
                                                                                
HSTTRN   NTR1  BASE=*,LABEL=*                                                   
         L     RF,ACMATRN          RESET FILE ADDRESSES                         
         AH    RF,DATADISP                                                      
         ST    RF,ADTRANS                                                       
         LR    R2,R1               SAVE CALLERS PARM LIST                       
         OC    ACMHDCB,ACMHDCB     IS HISTORY OPEN?                             
         BNZ   HSTTRN02                                                         
         XC    ACMHPRM(24),ACMHPRM                                              
         GOTOR DATAMGR,ACMHPRM,ACMDMDTF,ACMACHST                                
         MVC   ACMHDCB,12(R1)      SAVE ADDRESS OF HISTORY DCB                  
         MVI   ACMWRK,NO                                                        
         MVC   ACMWRK+1(7),ACMACHST                                             
         MVI   ACMWRK+8,C'X'                                                    
         GOTOR DATAMGR,ACMHPRM,ACMDMOPN,ACMACSYS,ACMWRK,ACFILEC                 
                                                                                
HSTTRN02 CLI   3(R2),ISREAD        IS IT READ?                                  
         BE    HSTTRN04                                                         
         CLI   3(R2),ISRDSEQ       IS IT SEQUENTIAL?                            
         BE    *+6                                                              
         DC    H'0'                INVALID COMMAND                              
         TM    ACMHIST,ACMHFST     IS FIRST TIME SET?                           
         BZ    HSTTRN04                                                         
         TM    ACMHIST,ACMHPRC     WAS HISTORY FILE PROCESSED LAST?             
         BNO   HSTTRN08            NO, READ NEXT FILE RECORD                    
         GOTOR ACMISDDS,ACMHPRM,ISRDSEQ,ACMHIO,0,ACMHDCB,ACMHIO,0               
         OC    8(2,R1),8(R1)       TEST ERRORS                                  
         BZ    HSTTRN10                                                         
         TM    9(R1),X'04'         TEST EOF                                     
         BO    *+6                                                              
         DC    H'0'                I/O ERROR                                    
         OI    ACMHIST,ACMHEOF     SET HISTORY EOF                              
         B     HSTTRN10                                                         
                                                                                
HSTTRN04 NI    ACMHIST,FF-(ACMHFEOF+ACMHEOF) TURNOFF EOF INDICATORS             
         OI    ACMHIST,ACMHFST     SET FIRST TIME                               
         L     R4,ACMHIO           SAVE KEY IN HISTORY FILE IO AREA             
         L     R3,16(R2)           A(KEYARG)                                    
         MVC   ACMWRK(L'TRNKEY),0(R3) SAVE INPUT KEY                            
                                                                                
         GOTOR ACMISDDS,ACMHPRM,ISREAD,(R4),0,ACMHDCB,ACMWRK,0                  
         OC    8(2,R1),8(R1)       TEST ERRORS                                  
         BZ    HSTTRN06                                                         
         TM    9(R1),X'04'         TEST EOF                                     
         BO    *+6                                                              
         DC    H'0'                I/O ERROR                                    
         OI    ACMHIST,ACMHEOF     SET HISTORY EOF                              
         B     HSTTRN08                                                         
                                                                                
HSTTRN06 CLI   3(R2),ISRDSEQ       IS IT SEQUENTIAL?                            
         BE    HSTTRN08                                                         
         LA    R4,ACMWRK           R4=INPUT KEY                                 
         LHI   R1,TRNKCULC-TRNKEY  SAME ACC/OFF                                 
         CLC   TRNKCULC-TRNKEY(L'TRNKCULC,R4),SPACES                            
         BNH   *+8                                                              
         LHI   R1,TRNKDATE-TRNKEY  SAME ACC/OFF/CONTRA                          
         L     R4,ACMHIO                                                        
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         CLC   ACMWRK(0),0(R4)                                                  
         BE    HSTTRN08                                                         
         OI    ACMHIST,ACMHEOF     SET HISTORY EOF                              
                                                                                
HSTTRN08 LR    R1,R2               SET USERS R1                                 
         GOTOR ACMISDDS            ORIGINAL READ                                
         OC    8(2,R1),8(R1)       TEST ERRORS                                  
         BZ    HSTTRN10                                                         
         TM    9(R1),X'04'         TEST EOF                                     
         BO    *+6                                                              
         DC    H'0'                I/O ERROR                                    
         OI    ACMHIST,ACMHFEOF    SET FILE EOF                                 
                                                                                
HSTTRN10 TM    ACMHIST,ACMHEOF+ACMHFEOF EOF ON BOTH?                            
         BO    HSTTRNEF            BOTH EOF - RETURN EOF                        
         TM    ACMHIST,ACMHFEOF    EOF ON ACCOUNT FILE?                         
         BO    HSTTRNHX            PROCESS HISTORY                              
         TM    ACMHIST,ACMHEOF     EOF ON HISTORY?                              
         BO    HSTTRNFX            PROCESS FILE                                 
         L     R3,4(R2)            FILE IO AREA                                 
         L     R4,ACMHIO                                                        
         CLC   0(L'TRNKEY,R3),0(R4)                                             
         BNH   HSTTRNFX            PROCESS FILE                                 
                                                                                
HSTTRNHX OI    ACMHIST,ACMHPRC     SET PROCESS HISTORY                          
         L     R4,ACMHIO           HISTORY IO AREA                              
         LR    RF,R4                                                            
         AH    RF,DATADISP                                                      
         ST    RF,ADTRANS                                                       
         TM    ACMHIST,ACMHNOT     TREAT AS NOT PEELED                          
         BNO   *+10                                                             
         XC    ACCOPEEL(ACCOPLEN,R4),ACCOPEEL(R4)                               
         B     *+8                                                              
                                                                                
HSTTRNFX NI    ACMHIST,FF-ACMHPRC  TURNOFF PROCESS HISTORY                      
         XC    8(2,R2),8(R2)                                                    
         B     HSTTRNX                                                          
                                                                                
HSTTRNEF NI    ACMHIST,FF-ACMHPRC  TURNOFF PROCESS HISTORY                      
         MVI   8(R2),0                                                          
         MVI   9(R2),X'04'         SET EOF FOR USER                             
                                                                                
HSTTRNX  J     EXIT                                                             
         DROP  RB                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO INITIALISE TABLE OF CURRENCY CODES                       *         
***********************************************************************         
                                                                                
SETCUR   NTR1  BASE=*,LABEL=*                                                   
         TM    RCFLAG1,RCFBCURT    TEST CURRENCY TABLE REQUIRED                 
         BZ    SETCURX                                                          
         OC    ACMACURT,ACMACURT   TEST CURRENCY TABLE INITIALISED              
         BNZ   SETCUR02                                                         
         LHI   R0,CURTABLN                                                      
         SLL   R0,8                MAXIMUM OF 256 TABLE ENTRIES                 
         GETMAIN R,LV=(0)                                                       
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                CAN'T ALLOCATE CURRENCY TABLE                
         ST    R1,ACMACURT         SAVE A(BUFFER)                               
         LA    RF,CURTABLN(R1)                                                  
         GOTOR ACMBLDCR,DMCB,0,(X'40',(RF)),ADCOMFAC                            
                                                                                
SETCUR02 DS    0H                                                               
*&&UK                                                                           
         ICM   RF,15,ADCMPEL                                                    
         BZ    SETCUR06                                                         
         USING CPYELD,RF                                                        
         CLI   CPYLN,CPYCURR-CPYELD                                             
         BL    SETCUR06                                                         
         LA    RE,CPYCURR                                                       
         TM    ACMINDS5,ACMI2NDC   TEST 2ND CURRENCY REQUESTED                  
         BZ    *+8                                                              
         LA    RE,CPYCURRS                                                      
         CLI   0(RE),C' '          TEST CURRENCY SET                            
         BNH   SETCUR06                                                         
         MVC   RCCURR,0(RE)        SET CURRENCY (FIRST OR SECOND)               
         B     SETCUR12                                                         
*&&                                                                             
SETCUR06 OC    RCCURR,RCCURR       TEST AGENCY CURRENCY RESOLVED                
         BNZ   SETCUR12                                                         
                                                                                
SETCUR08 LA    R1,CURTAB                                                        
SETCUR10 CLI   0(R1),0             ESTABLISH DEFAULT CURRENCY CODE              
         BNE   *+6                                                              
         DC    H'0'                                                             
         CLC   RCCTRY,0(R1)        MATCH ON COUNTRY CODE                        
         BE    *+12                                                             
         AHI   R1,L'CURTAB                                                      
         B     SETCUR10                                                         
         MVC   RCCURR,1(R1)        SET DEFAULT CURRENCY CODE                    
                                                                                
SETCUR12 L     RF,ACMACURT         SAVE A(BUFFER)                               
         LA    R1,CURTABLN(RF)                                                  
         USING CURTABD,R1                                                       
SETCUR14 CLI   CURTABD,0           TEST END OF CURRENCY TABLE                   
         BNE   *+6                                                              
         DC    H'0'                                                             
         CLC   CURTCUR,RCCURR      MATCH ON AGENCY CURRENCY                     
         BE    *+12                                                             
         AHI   R1,CURTABLN                                                      
         B     SETCUR14                                                         
         MVC   0(CURTABLN,RF),CURTABD                                           
                                                                                
SETCURX  J     EXIT                                                             
         DROP  R1                                                               
         DROP  RB                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO INITIALIZE AND FILTER BASED ON CONTRA ACCOUNT OR LIST    *         
***********************************************************************         
                                                                                
FLTCON   NTR1  BASE=*,LABEL=*                                                   
         TM    ACMCFIND,ACMCFINT   INITIALIZE?                                  
         BZ    FLTCON18                                                         
         MVI   ACMCFIND,0          CLEAR INDICATOR                              
         CLI   FCCONFLT,C'Y'                                                    
         BNE   *+8                                                              
         OI    ACMCFIND,ACMCCFLT   WANT CONTRA FILTERS PASSED                   
                                                                                
         BASR  RF,0                                                             
         AHI   RF,CONFLT-*                                                      
         STCM  RF,15,ACMCFLTR                                                   
         XC    ACMCFNTY,ACMCFNTY                                                
         XC    ACMCFMAX,ACMCFMAX                                                
         MVC   ACMCFKEY,SPACES                                                  
                                                                                
         CLC   =C'1R',QUNIT        FOR PERSONNEL REPORTS ONLY                   
         BNE   FLTCON06                                                         
         OC    ACMPCPOS,ACMPCPOS   ALREADY HAVE 1C OFF POSITION?                
         BNZ   FLTCON06            YES- SKIP 1C LEDGER LOOKUP                   
         L     R2,ACMABUF                                                       
         MVC   0(L'LDGKEY,R2),SPACES                                            
         MVC   0(1,R2),RCCOMPFL                                                 
         MVC   1(2,R2),=C'1C'                                                   
         GOTOR DATAMGR,DMCB,DMREAD,ACMACFIL,(R2),(R2)                           
         BNE   FLTCON06                                                         
                                                                                
         USING LDGELD,R1                                                        
         L     R1,ACMABUF                                                       
         AH    R1,DATADISP                                                      
FLTCON02 CLI   0(R1),0             TEST EOR                                     
         BE    FLTCON06                                                         
         CLI   0(R1),LDGELQ                                                     
         BE    FLTCON04                                                         
         SR    R0,R0               LOCATE LEDGER ELEMENT                        
         IC    R0,1(R1)                                                         
         AR    R1,R0                                                            
         B     FLTCON02                                                         
                                                                                
FLTCON04 MVI   ACMPCPOS,1          DEFAULT TO FIRST LEVEL IN KEY                
         TM    ACMINDS,ACMINEWO    NEW TWO CHARACTER OFFICE ?                   
         BZ    *+8                 NO                                           
         OI    ACMPCPOS,LDGOKEY2   SET TO 2 CHARACTER OFFICE                    
         CLI   LDGOPOS,LDGONKHI    OFFICE IN KEY ?                              
         BH    FLTCON06            NO                                           
         MVC   ACMPCPOS,LDGOPOS    YES, SAVE OFFPOS                             
         DROP  R1                                                               
                                                                                
FLTCON06 CLI   MCRQNUM,3                                                        
         BL    FLTCON10                                                         
                                                                                
         USING ACQD,R3                                                          
         L     R3,ADQSTACK                                                      
         CLC   ACQCFLT1(L'ACMCFLTS),SPACES  ANY CONTRA FILTERS?                 
         BE    *+8                         NO                                   
         OI    ACMCFIND,ACMCCFLT           SET TO YES                           
         DROP  R3                                                               
                                                                                
         LA    R2,RQFLTTAB         REQUEST ACCOUNT FILTERS                      
FLTCON08 SR    R3,R3                                                            
         ICM   R3,3,0(R2)                                                       
         BZ    FLTCON10            NO CONTRA FILTER ENTRY                       
         A     R3,ADQSTACK                                                      
         CLI   0(R3),ACQCNTR       CONTRA ACCOUNT FILTER?                       
         BE    FLTCON12                                                         
         AHI   R2,RQFLTQ           BUMP UP IN TABLE                             
         B     FLTCON08                                                         
                                                                                
FLTCON10 CLI   MCRQNUM,2                                                        
         BL    FLTCONOK                                                         
         CLC   QCUL(L'QCUL+L'QCACCT),SPACES                                     
         BE    FLTCONOK                                                         
*&&UK                                                                           
         CLC   =C'55',QPROG                                                     
         BE    FLTCONOK                                                         
         CLC   =C'5D',QPROG                                                     
         BE    FLTCONOK                                                         
         CLC   =C'5P',QPROG                                                     
         BE    FLTCONOK                                                         
         CLC   =C'5T',QPROG                                                     
         BE    FLTCONOK                                                         
         CLC   =C'38',QPROG                                                     
         BE    FLTCONOK                                                         
*&&                                                                             
         L     R1,ACMCFLST                                                      
         LA    RE,QCUL+L'QCUL+L'QCACCT-1                                        
         CLI   0(RE),C' '                                                       
         BH    *+8                                                              
         BCT   RE,*-8                                                           
         LA    RF,QCUL-1                                                        
         SR    RE,RF                                                            
         STC   RE,0(R1)                                                         
         MVC   1(L'QCUL+L'QCACCT,R1),QCUL                                       
         MVC   ACMCFMAX,=H'1'      SET NUMBER OF ENTRIES IN LIST                
         MVC   ACMCFNTY,=H'1'      SET CURRENT ENTRY                            
         OI    ACMCFIND,ACMCFACT   CONTRA FILTER PRESENT                        
         TM    1(R1),X'40'         TEST EXCLUDE CONTRA                          
         BNZ   FLTCONOK                                                         
         OI    1(R1),X'40'         SET UPPER CASE                               
         OI    ACMCFIND,ACMCFXCL   SET TO EXCLUDE LIST TYPE                     
         B     FLTCONOK                                                         
                                                                                
FLTCON12 CLI   1(R3),C'+'          INCLUDE LIST                                 
         BE    FLTCON16            NO, IT IS INCLUDE LIST                       
         CLI   1(R3),C'-'          EXCLUDE LIST                                 
         BE    FLTCON14            YES SO SET TI EXCLUDE                        
         TM    1(R3),X'40'         TEST SINGLE ACCT FOR EXCLUDE                 
         BO    FLTCON16                                                         
FLTCON14 OI    ACMCFIND,ACMCFXCL   SET TO EXCLUDE TYPE LIST                     
                                                                                
FLTCON16 AHI   R3,1                POINT PAST "C" IN REQ CARD                   
         GOTOR ACMVALST,DMCB,(C'R',ACMABUF),(RCCOMPFL,(R3)),DATAMGR             
         ICM   R4,15,DMCB          POINTS TO ACMABUF                            
         BZ    FLTCONOK            BAD LIST                                     
         GOTOR ACMVALST,DMCB,(C'B',(R4)),ACMCFLST,A(ACMCFLN)                    
         ICM   R2,15,DMCB+8        ANY ENTRIES?                                 
         BZ    FLTCONOK            NO                                           
         STH   R2,ACMCFMAX         SAVE NUMBER OF ENTRIES IN LIST               
         MVC   ACMCFNTY,=H'1'      SET CURRENT ENTRY                            
         OI    ACMCFIND,ACMCFACT   CONTRA FILTER PRESENT                        
         B     FLTCONOK                                                         
                                                                                
         USING CHDRECD,R3                                                       
FLTCON18 TM    ACMCFIND,ACMCFACT   CONTRA FILTER PRESENT                        
         BZ    FLTCON44            NO-CONTINUE                                  
         CLC   ACMCFKEY(CHDKCULC-CHDRECD),CHDKEY                                
         BE    *+10                SAME ACCOUNT/OFFICE                          
         MVC   ACMCFNTY,=H'1'      SET TO START AT BEGINING                     
         MVC   ACMCFKEY,CHDKEY     SAVE CURRENT CONTRA KEY                      
         LH    R6,ACMCFNTY         CURRENT POSITION IN TABLE                    
         LR    R4,R6               R4=A(CURRENT ENTRY)                          
         BCTR  R4,0                                                             
         MHI   R4,15               BUMP INTO TABLE                              
         A     R4,ACMCFLST         BASE OF TABLE                                
                                                                                
FLTCON20 CLC   ACMCFNTY,ACMCFMAX   END OF TABLE?                                
         BH    FLTCON42            YES                                          
FLTCON22 SR    R1,R1                                                            
         IC    R1,0(R4)            GET LENGTH OF COMPARE                        
         BCTR  R1,0                ONE FOR EXCUTE                               
         CLC   1(2,R4),SPACES      IS  UNIT/LEDGER BLANK                        
         BNH   FLTCON24            YES THEN NO COMPANY CODE                     
         CLC   CHDKCCPY,RCCOMPFL                                                
         BL    FLTCON26            LOWER  THAN TABLE GET NEXT CONTRA            
         BH    FLTCON30            HIGHER THAN TABLE BUMP UP IN TABLE           
                                                                                
FLTCON24 EX    R1,*+12             FILTER CONTRA COMPARE                        
         BL    FLTCON26            LOWER THAN TABLE GET NEXT CONTRA             
         BH    FLTCON30            HIGHER THAN TABLE BUMP UP IN TABLE           
         CLC   CHDKULC(0),1(R4)                                                 
         TM    ACMCFIND,ACMCFXCL   EXCLUDE TYPE LIST?                           
         BZ    FLTCON44            PASS BACK THIS ONE                           
         MVI   CHDKSPCS,X'FF'      READ NEXT CONTRA                             
         B     FLTCON28                                                         
                                                                                
FLTCON26 TM    ACMCFIND,ACMCFXCL   EXCLUDE TYPE LIST?                           
         BO    FLTCON44            PASS BACK THIS ONE                           
         MVC   CHDKCULC,SPACES     CLEAR                                        
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   CHDKULC(0),1(R4)    READ POSSIBLE CONTRA FOR ACCT/OFFICE         
         CLC   1(2,R4),SPACES      IS UNIT/LEDGER BLANK                         
         BNH   *+10                IF YES THEN NO COMPANY CODE                  
         MVC   CHDKCCPY,RCCOMPFL                                                
         CLC   =C'29',CHDKULA      29 HAS FUNNY CONTRAS                         
         BNE   *+8                                                              
         MVI   CHDKCCPY,C'*'                                                    
         MVC   CHDKSPCS,SPACES                                                  
         MVI   CHDKBTYP,C' '                                                    
         XC    CHDKNULL,CHDKNULL                                                
                                                                                
FLTCON28 GOTOR ACMISDDS,DMCB,ISREAD,(R3),0,ACMADDIR,(R3),0                      
         CLC   ACMCFKEY(CHDKCULC-CHDRECD),CHDKEY                                
         BE    FLTCON22            GO TRY AGAIN                                 
         B     FLTCONNO            EXIT CC=NO                                   
                                                                                
FLTCON30 AHI   R4,15               BUMP UP IN TABLE                             
         AHI   R6,1                BUMP UP BY ONE                               
         STH   R6,ACMCFNTY         SAVE CURRENT ENTRY                           
         B     FLTCON20                                                         
                                                                                
FLTCON42 TM    ACMCFIND,ACMCFXCL   EXCLUDE TYPE LIST?                           
         BO    FLTCON44            PASS BACK THIS ONE                           
         MVI   CHDKCCPY,X'FF'      READ FOR NEXT ACCOUNT/OFFICE                 
         B     FLTCON52            THEN EXIT                                    
                                                                                
FLTCON44 TM    ACMCFIND,ACMCCFLT   CONTRA FILTERS 1 - 5?                        
         BZ    FLTCON46            NO                                           
         GOTOR CONFLT,DMCB,CHDKULC,ACMCFLTS,ACWORKD                             
         BNE   FLTCON50            READ NEXT CONTRA                             
                                                                                
FLTCON46 TM    ACMCFIND,ACMCFOFF   FILTER CONTRA OFFICE?                        
         BZ    FLTCONOK            PASS BACK THIS ONE                           
         CLI   FCPERSEC,FCPERCLI   TEST CLIENT ORIENTED SECURITY                
         BE    FLTCON48            YES- CLT OFFICE FILTERING                    
         CLI   FCPERSEC,FCPERBTH   TEST CHECK 1R AND 1C OFFICE                  
         BNE   FLTCONOK            NO-SKIP CLT OFFICE FILTERING                 
         TM    ACMPESEC,ACMPEEMP   DID 1R OFFICE ALREADY PASS SECURITY?         
         BO    FLTCONOK            YES- SKIP CLT OFFICE FILTERING               
FLTCON48 CLC   =C'1C',CHDKULC      ONLY TESTING FOR 1C                          
         BNE   FLTCON50                                                         
         OC    ACMPCPOS,ACMPCPOS   OFFICE POSITION SPECIFIED?                   
         BZ    FLTCONOK                                                         
         TM    ACMPCPOS,LDGONKHI   CHECK FOR OFFICE POSTION                     
         BH    FLTCONOK            CAN'T BE ANYWHERE BUT THE KEY                
         MVC   ACMWRK,SPACES                                                    
         LA    R2,CHDKCACT         POINT AT CONTRA ACCOUNT                      
         SR    R1,R1                                                            
         IC    R1,ACMPCPOS         ISOLATE DISP INTO KEY                        
         N     R1,=F'15'                                                        
         SHI   R1,1                                                             
         AR    R2,R1                                                            
         LHI   R1,1                ONE BYTE OFFICE                              
         TM    ACMINDS,ACMINEWO    CHECK FOR NEW OFFICES                        
         BZ    *+8                 NOT ON NEW OFFICES                           
         LHI   R1,2                TWO BYTE OFFICE                              
         SHI   R1,1                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   ACMWRK(0),0(R2)     CONTRA OFFICE                                
         MVI   ACMFLAG,ACMFLTO     APPLY OFFICE LIMIT ACCESS                    
         GOTOR OFFLMT                                                           
         BE    FLTCONOK                                                         
                                                                                
FLTCON50 MVI   CHDKSPCS,X'FF'      READ FOR NEXT CONTRA                         
FLTCON52 GOTOR ACMISDDS,DMCB,ISREAD,(R3),0,ACMADDIR,(R3),0                      
         OC    DMCB+8(2),DMCB                                                   
         BZ    FLTCONNO                                                         
         TM    DMCB+9,X'04'        EOF?                                         
         BO    FLTCONNO                                                         
         DC    H'0'                HARDWARE ERROR                               
                                                                                
FLTCONOK J     EXITEQ                                                           
FLTCONNO J     EXITHI                                                           
         DROP  RB                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO APPLY CONTRA ACCOUNT FILTERS 1 - 5                       *         
*                                                                     *         
* NTRY - R1=A(CONTRA UNIT/LEDGER/ACCOUNT)                             *         
* EXIT - CC=EQUAL IF RECORD PASSES THE FILTERS ELSE NOT EQUAL         *         
***********************************************************************         
                                                                                
         USING CFWORKD,R8                                                       
CONFLT   NTR1  BASE=*,LABEL=*,WORK=(R8,CFWORKX-CFWORKD)                         
         L     R3,0(,R1)           CONTRA ACCOUNT TO FIND FILTERS ON            
         L     R7,4(,R1)           WHERE TO PUT RESULT OF 5 FILTERS             
         L     RA,8(,R1)           ACWORKD                                      
         L     RC,AMONACC          MONACC STORAGE                               
                                                                                
         USING ACTRECD,R2                                                       
CONFLT02 LA    R2,CFAKEY           BUILD ACCOUNT KEY                            
         MVC   ACTKEY,SPACES                                                    
         MVC   ACTKCPY,RCCOMPFL                                                 
         MVC   ACTKULA,0(R3)       BUILD KEY OF ACCOUNT RECORD                  
         CLC   ACTKULA(L'LDGUL),SPACES                                          
         BE    CONFLTY                                                          
                                                                                
CONFLT04 OC    ACMALDGT,ACMALDGT   HAVE A(LEDGER TABLE)                         
         BNZ   CONFLT06            YES - ALREADY SET                            
         LHI   R0,LDGTABQ*LDGTABN  NO  - ACQUIRE STORAGE FOR IT                 
         GETMAIN R,LV=(0)                                                       
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
                                                                                
         ST    R1,ACMALDGT         SET ADDRESS OF ACQUIRED STORAGE              
         LTR   R0,R1                                                            
         BNZ   *+6                                                              
         DC    H'0'                                                             
                                                                                
         LHI   R1,LDGTABQ*LDGTABN  CLEAR STORAGE AREA                           
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
                                                                                
         USING LDGTABD,R4                                                       
CONFLT06 ICM   R4,15,ACMALDG       POINT TO CURRENT ENTRY                       
         BZ    CONFLT08            NONE SET                                     
         CLC   LDGUL,ACTKUNT       MATCH CURRENT UNIT/LEDGER                    
         BE    CONFLT32            FOUND                                        
                                                                                
CONFLT08 ICM   R4,15,ACMALDGT      LOAD START OF TABLE                          
         LHI   R0,LDGTABN          MAX ENTRIES                                  
                                                                                
CONFLT10 CLC   LDGUL,ACTKUNT       TEST ENTRY FOUND                             
         BNE   CONFLT12                                                         
         ST    R4,ACMALDG          YES - SET AS CURRENT ENTRY                   
         B     CONFLT32                                                         
                                                                                
CONFLT12 CLI   LDGTABD,0           NO - TEST FREE ENTRY                         
         BE    CONFLT14                                                         
         AHI   R4,LDGTABQ          BUMP TO NEXT TABLE ENTRY                     
         BCT   R0,CONFLT10                                                      
         DC    H'0'                INCREASE N'ENTRIES IN LDGTAB                 
                                                                                
CONFLT14 ST    R4,ACMALDG          SET AS CURRENT ENTRY                         
         MVC   LDGUL,ACTKUNT       SET DEFAULT LEDGER VALUES                    
         MVI   LDGLV1LN,L'ACTKACT                                               
         GOTOR DATAMGR,DMCB,DMKEY,ACMACDIR,CFKEYLST,CFKEYLST                    
                                                                                
         USING LDGRECD,RF                                                       
         LA    RF,CFKEY                                                         
         MVC   LDGKEY,SPACES                                                    
         MVC   LDGKEY(LDGKEND),ACTKEY   COMPANY/UNIT/LEDGER                     
         MVC   CFKEYSAV,LDGKEY                                                  
         GOTOR DATAMGR,DMCB,DMREAD,ACMACDIR,LDGKEY,LDGKEY                       
         BE    *+6                                                              
         DC    H'00'                                                            
                                                                                
         LA    RF,CFKEY                                                         
         GOTOR DATAMGR,DMCB,ACMDMGET,ACMACMST,LDGKDA,CFIO,CFWORK                
         BE    *+6                                                              
         DC    H'00'                                                            
         DROP  RF                                                               
                                                                                
         USING ACLELD,R1           LOCATE HEIRARCHY ELEMENT                     
         SR    R0,R0                                                            
         LA    R1,CFIO+(LDGRFST-LDGRECD)                                        
                                                                                
CONFLT16 CLI   ACLEL,0             TEST END OF RECORD                           
         BE    CONFLT20                                                         
         CLI   ACLEL,ACLELQ        TEST ELEMENT FOUND                           
         BE    CONFLT18                                                         
         IC    R0,ACLLN                                                         
         AR    R1,R0                                                            
         B     CONFLT16                                                         
                                                                                
CONFLT18 MVI   LDGMAXLV,1                                                       
         MVC   LDGLV1LN,ACLVLEN                                                 
         CLI   LDGLV1LN,L'ACTKACT                    MAX LEVEL ?                
         BE    CONFLT20                                                         
         MVI   LDGMAXLV,2                                                       
         MVC   LDGLV2LN,ACLVLEN+(L'ACLVALS)                                     
         CLI   LDGLV2LN,L'ACTKACT                    MAX LEVEL ?                
         BE    CONFLT20                                                         
         MVI   LDGMAXLV,3                                                       
         MVC   LDGLV3LN,ACLVLEN+(L'ACLVALS*2)                                   
         CLI   LDGLV3LN,L'ACTKACT                    MAX LEVEL ?                
         BE    CONFLT20                                                         
         MVI   LDGMAXLV,4                                                       
         MVC   LDGLV4LN,ACLVLEN+(L'ACLVALS*3)                                   
         DROP  R1                                                               
*                                  ACQUIRE STORAGE                              
CONFLT20 LHI   R0,10000            NUMBER OF ENTIRES FOR 1C CONTRA              
         CLC   =C'1C',LDGUL                                                     
         BE    CONFLT22                                                         
         LHI   R0,1000             NUMBER OF ENTIRES FOR 1N CONTRA              
         CLC   =C'1N',LDGUL                                                     
         BE    CONFLT22                                                         
         LHI   R0,FLTTABN          DEFAULT NUMBER OF ENTIRES                    
                                                                                
CONFLT22 ST    R0,LDGMAX#          SAVE MAX NUMBER OF ENTIRES                   
         MHI   R0,FLTTABQ          CALCULATE SIZE OF TABLE                      
         ST    R0,LDGTABSZ         SAVE VALUE                                   
         GETMAIN R,LV=(0)                                                       
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
                                                                                
         ST    R1,LDGAFLT          SAVE A(FILTER TABLE)                         
         LTR   R0,R1                                                            
         BNZ   *+6                                                              
         DC    H'0'                                                             
                                                                                
         XC    LDGFLT#,LDGFLT#           SET ENTRIES TO ZERO                    
         L     R1,LDGTABSZ               CLEAR STORAGE AREA                     
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
                                                                                
***********************************************************************         
*        BUILD TABLE OF ACCOUNTS / FILTERS FOR CONTRA LEDGER          *         
***********************************************************************         
                                                                                
         USING ACTRECD,R2                                                       
         LA    R2,CFKEY                                                         
         MVC   ACTKEY,SPACES                                                    
         MVC   ACTKCPY(3),CFKEYSAV                                              
         MVI   ACTKACT,X'41'       READ FOR 1ST ACCOUNT                         
         L     R6,LDGAFLT          A(TABLE OF ACCOUNT/FILTERS)                  
                                                                                
CONFLT24 GOTOR DATAMGR,DMCB,DMRDHI,ACMACDIR,ACTRECD,ACTRECD                     
         BE    *+6                                                              
         DC    H'00'                                                            
                                                                                
         OC    ACTKSAF1(L'CFFLTS),SPACES     REMOVE NULL TYPE FILTERS           
         CLC   ACTKEY(3),CFKEYSAV  SAME LEDGER ?                                
         BNE   CONFLT30            FINISHED                                     
         MVI   CFCTLLV,X'FF'       CONTROL LEVEL TO READ                        
         GOTOR CFSETLVL                                                         
         CLC   CFCURLV,LDGMAXLV    IF MAX LEVEL OF LEDGER THEN                  
         BE    CONFLT28                                                         
         MVI   CFCTLLV,X'41'       READ NEXT ACCOUNT LEVEL                      
                                                                                
CONFLT28 GOTOR CFPUTFLT            R6=LOCATION TO PLACE RECORD                  
         SR    RF,RF                                                            
         IC    RF,CFCURLVN         GET LENGTH OF LEVEL                          
         LA    RE,ACTKACT(RF)                                                   
         MVC   0(1,RE),CFCTLLV     SET FOR NEXT READ                            
         LA    R1,ACTKEY                                                        
         AHI   RE,1                POINT PAST CONTROL LEVEL CODE                
         LR    RF,RE                                                            
         SR    RF,R1               WHAT IS LEFT TO CLEAR                        
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     CONFLT24                                                         
         MVC   0(0,RE),SPACES                                                   
                                                                                
CONFLT30 GOTOR DATAMGR,DMCB,DMKEY,ACMACDIR,DMWORK,DMWORK     RESET KEY          
         CLC   CFKEYLST(L'ACCKEY),DMWORK                     SAME  KEY          
         BE    CONFLT32                                      YES                
         GOTOR ACMISDDS,ACMPARA,ISREAD,ACMABUF2,0,ACMADDIR,CFKEYLST,0           
         OC    8(2,R1),8(R1)       TEST FOR ERRORS                              
         BZ    CONFLT32                                                         
         TM    9(R1),X'04'         TEST FOR EOF                                 
         BO    *+6                 YES                                          
         DC    H'0'                DUMP ON DISK ERROR                           
                                                                                
CONFLT32 LA    R2,CFAKEY           R2 = ACC TO FIND IN BINTAB                   
         GOTOR CFSETLVL            GET LEVEL OF ACCOUNT PASSED                  
         SR    R3,R3                                                            
         IC    R3,CFCURLV          CURRENT LEVEL                                
         BCTR  R3,0                LESS ONE TO CORRECT DISPLACEMENT             
         MVC   0(L'ACMCFLTS,R7),SPACES      PLACE RESULT HERE                   
                                                                                
CONFLT34 GOTOR BINSRCH,DMCB,(X'00',ACTKUNT),LDGAFLT,LDGFLT#,FLTTABQ,   *        
               14,LDGMAX#                                                       
         CLI   0(R1),0             TEST IF ANYTHING FOUND                       
         BE    CONFLT36                                                         
         SHI   R3,1                CURRENT LEVEL PROCESSED                      
         BM    CONFLT38            NO FILTERS FOR ACCOUNT                       
         LA    RE,LDGLVLS(R3)      POINT TO PRIOR LEVEL LENGTH                  
         SR    RF,RF                                                            
         IC    RF,0(,RE)           GET LENGTH                                   
         LA    RE,ACTKACT(RF)      POINT TO END OF LEVEL                        
         LHI   R1,L'ACTKACT                                                     
         SR    R1,RF               LENGTH TO CLEAR OUT ACCOUNT FOR              
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     CONFLT34                                                         
         MVC   0(0,RE),SPACES                                                   
                                                                                
         USING FLTTABD,R6                                                       
CONFLT36 L     R6,0(,R1)           A(ENTRY)                                     
         MVC   0(L'ACMCFLTS,R7),FLTCFLT                                         
         DROP  R6                                                               
                                                                                
         USING ACQD,R1                                                          
CONFLT38 L     R1,ADQSTACK                                                      
         CLC   ACQCFLT1(L'ACMCFLTS),SPACES   ANY CONTRA FILTERS?                
         BE    CONFLTY                       NO REQUEST TO FILTER               
         LA    R1,ACQCFLT1                                                      
         LA    RE,ACMCFLTS                                                      
         LHI   R0,L'ACMCFLTS                                                    
         MVI   CFBYTE,0                                                         
         DROP  R1                                                               
                                                                                
CONFLT40 CLI   0(R1),C' '          TEST ANY FILTER VALUE SET                    
         BE    CONFLT44            NO, DON'T CARE THEN                          
         CLC   0(1,R1),0(RE)       TEST FILTER MATCHES                          
         BE    CONFLT44            THIS ONE OK SO FAR                           
                                                                                
CONFLT42 TM    0(R1),X'40'         TEST 'NOT' FILTER VALUE                      
         BO    CONFLTN             FILTER OUT THIS ONE                          
         MVC   BYTE,0(R1)                                                       
         OI    BYTE,X'40'                                                       
         CLC   BYTE,0(RE)          EXCLUDE ?                                    
         BE    CONFLTN             YES, EXCLUDE THIS ONE                        
                                                                                
CONFLT44 AHI   R1,L'ACTKSAF1                                                    
         AHI   RE,L'ACTKSAF1                                                    
         BCT   R0,CONFLT40                                                      
                                                                                
CONFLTY  MVI   CFBYTE,0                                                         
         B     CONFLTX                                                          
CONFLTN  MVI   CFBYTE,1                                                         
                                                                                
CONFLTX  CLI   CFBYTE,0                                                         
         J     EXIT                                                             
         DROP  R2,R4                                                            
         EJECT                                                                  
***********************************************************************         
* FIGURE OUT WHAT LEVEL OF THE ACCOUNT WE ARE PROCESSING              *         
***********************************************************************         
                                                                                
         USING ACTRECD,R2                                                       
         USING LDGTABD,R4                                                       
CFSETLVL STM   RE,R5,CFREGS                                                     
         SR    R1,R1                                                            
         IC    R1,LDGMAXLV         INITIALIZE                                   
         MVI   CFCURLVN,L'ACTKACT  DEFAULT LENGTH OF LOWEST LEVEL = 12          
                                                                                
CFSETLV2 STC   R1,CFCURLV                                                       
         SHI   R1,1                                                             
         BNP   CFSETLV4            FINISHED                                     
         LA    RE,LDGLVLS(R1)                                                   
         SR    RF,RF                                                            
         IC    RF,0(,RE)           LENGTH OF LEVEL                              
         BCTR  RE,0                LOOK AT PRIOR LEVEL                          
         SR    R3,R3                                                            
         IC    R3,0(,RE)           LENGTH OF PRIOR LEVEL                        
         LA    RE,ACTKACT(R3)      POINT  TO PERTINENT PART                     
         SR    RF,R3               LENGTH OF PERTINENT PART                     
         BCTR  RF,0                                                             
         EXCLC RF,0(RE),SPACES                                                  
         BNE   CFSETLV4            LEVEL FOUND & SET IN CFCURLV                 
         STC   R3,CFCURLVN         SAVE CURRENT LEVEL LENGTH                    
         B     CFSETLV2            LEVEL NOT FOUND                              
                                                                                
CFSETLV4 LM    RE,R5,CFREGS                                                     
         BR    RE                                                               
         DROP  R2,R4                                                            
         EJECT                                                                  
***********************************************************************         
* PUT ONLY ACCOUNTS/FILTERS WITH VALUES TO TABLE - COMPOSIT           *         
* R4 = A(LEDGER ENTRY)                                                *         
* R6 = A(NEXT AVAILABLE ENTRY IN TABLE)                               *         
***********************************************************************         
                                                                                
         USING ACTRECD,R2                                                       
         USING LDGTABD,R4                                                       
CFPUTFLT STM   RE,R5,CFREGS                                                     
         LA    R2,CFKEY                                                         
         SR    R1,R1                                                            
         IC    R1,CFCURLV          GET CURRENT LEVEL OF ACCOUNT                 
         BCTR  R1,0                                                             
         MHI   R1,L'CFFLTS                                                      
         LA    RE,CFFLTS(R1)                 POINT TO LEVEL                     
         MVC   0(L'CFFLTS,RE),ACTKSAF1       MOVE  IN DATA                      
         CLC   ACTKSAF1(L'CFFLTS),SPACES     ANY FILTER SET IN DIR ?            
         BNH   CFPUTFL8                      NO, SO DON'T PUT TO TABLE          
         CLI   CFCURLV,1                     FIRST LEVEL ?                      
         BE    CFPUTFL4                                                         
         LR    R3,RE               START  OF FILTERS                            
         SHI   R3,L'CFFLTS         POINT  TO PREVIOUS LEVEL                     
         LR    R5,RE               START  OF FILTERS                            
         LHI   RF,L'CFFLTS         NUMBER OF FILTERS                            
                                                                                
CFPUTFL2 CLI   0(R5),C' '          ANY FILTER VALUE ?                           
         BH    *+10                YES, SO DON'T REPLACE                        
         MVC   0(1,R5),0(R3)       MERGE LEVELS                                 
         AHI   R3,1                NEXT FILTER                                  
         AHI   R5,1                NEXT FILTER                                  
         BCT   RF,CFPUTFL2                                                      
                                                                                
         USING FLTTABD,R6                                                       
CFPUTFL4 MVC   FLTCULA,ACTKUNT     CONTRA U/L/ACCOUNT                           
         MVC   FLTCFLT,0(RE)       CONTRA COMPOSIT FILTER VALUES                
         CLI   RCTRACE,YES                                                      
         BNE   CFPUTFL6                                                         
         L     RF,VBIGPRNT         COPY TO XP IN CASE OF WIDE PRINTING          
         MVC   P,SPACES                                                         
         MVC   P+1(14),FLTCULA                                                  
         MVC   P+18(5),FLTCFLT                                                  
         MVC   XP+1-BIGPRNTD(L'P,RF),P                                          
         MVI   SKIPSPEC,YES                                                     
         GOTOR ACREPORT                                                         
                                                                                
CFPUTFL6 AHI   R6,FLTTABQ          NEXT LOCATION                                
         L     R1,LDGFLT#                                                       
         AHI   R1,1                                                             
         C     R1,LDGMAX#          MAX NUMBER OF ENTRIES                        
         BNH   *+6                                                              
         DC    H'0'                TABLE FULL                                   
                                                                                
         ST    R1,LDGFLT#          NUMBER OF ENTRIES IN TABLE                   
                                                                                
CFPUTFL8 LM    RE,R5,CFREGS                                                     
         BR    RE                                                               
         DROP  R2,R4,R6,RB                                                      
                                                                                
CFWORKD  DSECT                     ** LOCAL WORKING STORAGE **                  
CFREGS   DS    8A                  USED FOR STM                                 
CFBYTE   DS    X                   FLAG BYTE                                    
CFAKEY   DS    XL(ACCKLEN)         PASSED ACCOUNT KEY                           
CFKEY    DS    XL(ACCKLEN)         GENERAL KEY                                  
CFKEYSAV DS    XL(ACCKLEN)         GENERAL KEY SAVE                             
CFKEYLST DS    XL64                LAST    KEY READ BY DATAMGR                  
                                                                                
CFFLTS   DS    0CL5                                                             
CFFLT1   DS    CL5                 LEVEL 1 FILTERS                              
CFFLT2   DS    CL5                 LEVEL 2 FILTERS COMPOSIT                     
CFFLT3   DS    CL5                 LEVEL 3 FILTERS COMPOSIT                     
CFFLT4   DS    CL5                 LEVEL 4 FILTERS COMPOSIT                     
                                                                                
CFCTLLV  DS    XL1                 CONTROL CODE FOR READ AHEAD                  
CFCURLV  DS    AL1                 CURRENT LEVEL OF ACCOUNT                     
CFCURLVN DS    AL1                 CURRENT LENGTH OF LEVEL                      
                                                                                
CFWORK   DS    XL64                                                             
CFIO     DS    XL2000                                                           
CFWORKX  EQU   *                                                                
ACMASTER CSECT                                                                  
         EJECT                                                                  
***********************************************************************         
* CONTROL READING OF SYSTEM RECOVERY FILE                             *         
***********************************************************************         
                                                                                
RCVGET   NTR1  BASE=*,LABEL=*                                                   
         XC    ACMRCVDA,ACMRCVDA   SET TO FIRST RECORD                          
         XC    ACMRCVSQ,ACMRCVSQ   INITIALISE SEQUENCE NUMBER                   
         L     R3,ACMABUF                                                       
         ST    R3,ADTRANS                                                       
         CLI   RCINPUT,RCITAPE     TEST INPUT=TAPE                              
         BE    RCVGET04                                                         
                                                                                
         USING RCVRECD,R3                                                       
RCVGET02 SR    R0,R0                                                            
         CLI   FCRDRCVR,FCRDRCVD   TEST CALLER WANTS DELETED RECORDS            
         BNE   *+8                                                              
         LHI   R0,X'29'                                                         
         GOTOR DATAMGR,DMCB,((R0),DMRSEQ),ACMACRCV,ACMRCVDA,RCVHDR,0            
         TM    8(R1),X'80'         TEST EOF                                     
         BNZ   RCVGETX                                                          
         CLI   8(R1),0             TEST ERRORS (IGNORE IF SET)                  
         BNE   RCVGET02                                                         
         LH    R1,18(R1)           PICK UP RECORD LENGTH                        
         AHI   R1,4                                                             
         XC    RCVLEN(4),RCVLEN    CLEAR 1ST 4 BYTES                            
         STH   R1,RCVLEN           SET TOTAL RECORD LENGTH                      
         GOTOR RCVGO                                                            
         B     RCVGET02                                                         
                                                                                
RCVGET04 L     R2,ADRCVT                                                        
         OPEN  ((R2),(INPUT))      INPUT IS RECOVERY TAPE                       
                                                                                
RCVGET06 GET   (R2),RCVRECD                                                     
         GOTOR RCVGO                                                            
         B     RCVGET06                                                         
                                                                                
RCVGET08 CLOSE ((R2))                                                           
                                                                                
RCVGETX  J     EXIT                                                             
                                                                                
RCVGO    CLC   RCVDATE,ACMDAYB     KEEP TODAY OR HIGHER                         
         BLR   RE                                                               
         CLI   FCRDRCVR,FCRDRCVD   TEST CALLER WANTS DELETED RECORDS            
         BE    RCVGO10             YES                                          
         CLI   RCVTSKID,X'FF'      NO - EXIT IF A DELETED RECORD                
         BER   RE                                                               
*                                                                               
RCVGO10  CLI   RCVFILTY,X'FF'      JOB START RECORDS                            
         BER   RE                                                               
         CLC   RCVSEQNO,=F'1'      OFF-LINE                                     
         BE    RCVGO20                                                          
*        CLI   RCVTSKID,X'FF'                                                   
*        BE    RCVGO20             JUST PASS IT ALONG                           
         TM    RCVTIME,RCVXFIL     HAS EXTENTION TRAILER?                       
         BZ    RCVGO12             NO, MUST BE OFF-LINE                         
         LR    R0,RE               SAVE RE                                      
         LH    R1,RCVLEN                                                        
         LA    RE,RCVLEN(R1)       POINT TO TRAILER LENGTH                      
         LLC   RF,-1(RE)                                                        
         SR    RE,RF                                                            
         USING RECVEXT,RE                                                       
         CLI   RSYSIX,0            ADV AOR OR TOR NUMBER                        
         LR    RE,R0               RESTORE RE                                   
         BNE   RCVGO20             MUST BE ON-LINE                              
         DROP  RE                                                               
*                                                                               
RCVGO12  MVC   RCVSEQNO,=F'1'      SET TO OFF-LINE FOR APP.                     
*                                                                               
RCVGO20  L     R1,ACMRCVSQ         INCREMENT SEQUENCE NUMBER                    
         AHI   R1,1                                                             
         ST    R1,ACMRCVSQ                                                      
*                                                                               
         CLI   FCRDRCVR,FCRDRCVD   TEST CALLER WANTS DELETED RECORDS            
         BE    *+10                                                             
         MVC   RCVSEQNO,ACMRCVSQ   SET RECORD SEQUENCE NUMBER                   
         MVI   MODE,PROCRCVR       PASS RECORD TO APPLICATION                   
         LR    R0,RE                                                            
         GOTOR GO                                                               
         LR    RE,R0                                                            
         BR    RE                                                               
         DROP  R3,RB                                                            
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO READ ORDER RECORDS                                       *         
***********************************************************************         
                                                                                
ORDGET   NTR1  BASE=*,LABEL=*                                                   
         LA    R1,KEY                                                           
         USING ORDRECD,R1                                                       
         XC    ORDKEY,ORDKEY                                                    
         MVI   ORDKTYP,ORDKTYPQ                                                 
         MVC   ORDKCPY,RCSVCOMP                                                 
                                                                                
         GOTOR HIGH                GET FIRST ORDER RECORD                       
         B     ORDGET04                                                         
ORDGET02 GOTOR SEQ                 GET NEXT ORDER RECORD                        
ORDGET04 CLC   KEY(ORDKCPY+L'ORDKCPY-ORDRECD),KEYSAVE                           
         BNE   ORDGETX                                                          
         L     R1,ACMABUF                                                       
         ST    R1,ADACC            SET A(ORDER RECORD)                          
                                                                                
         AH    R1,DATADISP                                                      
         USING ORDELD,R1                                                        
         SR    R0,R0               LOCATE ORDER ELEMENT                         
ORDGET06 CLI   ORDEL,0             TEST EOR                                     
         BE    ORDGET02                                                         
         CLI   ORDEL,ORDELQ        TEST ORDER ELEMENT                           
         BE    *+14                                                             
         IC    R0,ORDLN                                                         
         AR    R1,R0                                                            
         B     ORDGET06                                                         
         CLI   QUNIT,C' '          MATCH ON REQUESTED UNIT                      
         BE    ORDGET08                                                         
         CLC   ORDACCU,QUNIT                                                    
         BNE   ORDGET02                                                         
         CLI   QLEDGER,C' '        MATCH ON REQUESTED LEDGER                    
         BE    ORDGET08                                                         
         CLC   ORDACCL,QLEDGER                                                  
         BNE   ORDGET02                                                         
         SR    RE,RE                                                            
         ICM   RE,1,ACMRQDPT       MATCH ON REQUESTED ACCOUNT                   
         BZ    ORDGET08                                                         
         SLL   RE,1                                                             
         LH    RE,ACMLVA-2(RE)                                                  
         EX    RE,*+8                                                           
         B     *+10                                                             
         CLC   ORDACCA(0),QACCOUNT                                              
         BNE   ORDGET02                                                         
                                                                                
ORDGET08 CLC   ACMPROD,QUNIT       TEST PRODUCTION LEDGER REQUEST               
         BNE   ORDGET12                                                         
         LH    RE,ACMLVB                                                        
         LA    RE,ORDACCA+1(RE)    POINT TO FIRST BYTE OF JOB CODE              
         MVC   FCRULMED,0(RE)      AND EXTRACT MEDIA CODE                       
         DROP  R1                                                               
                                                                                
         LA    R2,QMEDIA                                                        
         LA    R1,FCRULMED                                                      
         GOTOR TSTMED              FILTER MEDIA CODE                            
         BNE   ORDGET02                                                         
                                                                                
ORDGET10 CLI   QMGROUP,C' '        TEST MEDIA GROUP FILTER PRESENT              
         BE    ORDGET12                                                         
         GOTOR SETMGR              SET MEDIA GROUP CODE                         
         LA    R2,QMGROUP                                                       
         LA    R1,ACMMEGRP                                                      
         GOTOR TSTMED              FILTER MEDIA GROUP                           
         BNE   ORDGET02                                                         
                                                                                
ORDGET12 L     R1,ACMABUF                                                       
         AH    R1,DATADISP                                                      
         SR    R0,R0                                                            
         USING FFTELD,R1                                                        
ORDGET14 CLI   FFTEL,0             TEST OFFICE SECURITY                         
         BE    ORDGET16                                                         
         CLI   FFTEL,FFTELQ                                                     
         BNE   *+12                                                             
         CLI   FFTTYPE,FFTTOFFC                                                 
         BE    *+14                                                             
         IC    R0,FFTLN                                                         
         AR    R1,R0                                                            
         B     ORDGET14                                                         
                                                                                
         L     RF,ACMAOFL                                                       
         USING OFFALD,RF                                                        
         MVC   ACMWRK(L'QOFFICE),SPACES                                         
         SR    RE,RE                                                            
         IC    RE,FFTLN                                                         
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   ACMWRK(0),FFTDATA                                                
         MVC   OFFAOFFC,ACMWRK                                                  
         MVI   OFFAACT,OFFAVAL     VALIDATE OFFICE                              
         GOTOR ADOFFAL,OFFALD                                                   
         BNE   ORDGET02                                                         
                                                                                
         CLC   QOFFICE,SPACES      SPACE=NO FILTER                              
         BE    ORDGET16                                                         
         MVC   ACMWRK+L'QOFFICE(L'QOFFICE),QOFFICE                              
         LHI   R1,X'70'            SET MASK TO NOT EQUAL                        
         TM    ACMWRK+L'QOFFICE,X'40'                                           
         BNZ   *+12                                                             
         OI    ACMWRK+L'QOFFICE,X'40'                                           
         LHI   R1,X'80'            SET MASK TO EQUAL                            
         CLI   ACMWRK+L'QOFFICE,C'.'                                            
         BNE   *+8                                                              
         MVI   ACMWRK+L'QOFFICE,C' '                                            
         CLC   ACMWRK(L'QOFFICE),ACMWRK+L'QOFFICE                               
         EX    R1,*+8                                                           
         B     ORDGET16                                                         
         NOP   ORDGET02                                                         
         DROP  RF,R1                                                            
                                                                                
ORDGET16 MVI   MODE,PROCORD        PASS RECORD TO APPLICATION                   
         GOTOR GO                                                               
         B     ORDGET02                                                         
                                                                                
ORDGETX  J     EXIT                                                             
         DROP  RB                                                               
         EJECT                                                                  
***********************************************************************         
* READ WORK CODE RECORDS                                              *         
***********************************************************************         
                                                                                
WCOGET   NTR1  BASE=*,LABEL=*                                                   
         LA    R1,KEY                                                           
         USING WCORECD,R1                                                       
         XC    WCOKEY,WCOKEY                                                    
         MVI   WCOKTYP,WCOKTYPQ                                                 
         MVC   WCOKCPY,RCCOMPFL                                                 
         GOTOR HIGH                                                             
         B     WCOGET04                                                         
WCOGET02 GOTOR SEQ                                                              
WCOGET04 CLC   KEY(WCOKUNT-WCORECD),KEYSAVE                                     
         BNE   WCOGETX                                                          
         GOTOR GETUNTR                                                          
         MVC   ADACC,ADUNIT                                                     
         MVI   MODE,PROCWORK                                                    
         GOTOR GO                                                               
         B     WCOGET02                                                         
WCOGETX  J     EXIT                                                             
         DROP  RB                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO READ COMMENT RECORDS                                     *         
***********************************************************************         
                                                                                
SCMGET   NTR1  BASE=*,LABEL=*                                                   
         LA    R1,KEY                                                           
         USING SCMRECD,R1                                                       
         XC    SCMKEY,SCMKEY                                                    
         MVI   SCMKTYP,SCMKTYPQ                                                 
         MVC   SCMKCPY,RCSVCOMP                                                 
         GOTOR HIGH                                                             
         B     SCMGET04                                                         
SCMGET02 GOTOR SEQ                                                              
SCMGET04 CLC   KEY(SCMKCODE-SCMRECD),KEYSAVE                                    
         BNE   SCMGETX                                                          
         GOTOR GETUNTR                                                          
         MVC   ADACC,ADUNIT                                                     
         MVI   MODE,PROCCOMM                                                    
         GOTOR GO                                                               
         B     SCMGET02                                                         
SCMGETX  J     EXIT                                                             
         DROP  RB                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO READ LIST RECORDS                                        *         
***********************************************************************         
                                                                                
LSTGET   NTR1  BASE=*,LABEL=*                                                   
         LA    R1,KEY                                                           
         USING LSTRECD,R1                                                       
         XC    LSTKEY,LSTKEY                                                    
         MVI   LSTKTYP,LSTKTYPQ                                                 
         MVC   LSTKCPY,RCCOMPFL                                                 
         GOTOR HIGH                                                             
         B     LSTGET04                                                         
LSTGET02 GOTOR SEQ                                                              
LSTGET04 CLC   KEY(LSTKLST-LSTRECD),KEYSAVE                                     
         BNE   LSTGETX                                                          
         CLC   QACCOUNT,SPACES                                                  
         BE    *+14                                                             
         CLC   KEY(16),KEYSAVE                                                  
         BNE   LSTGETX                                                          
         GOTOR GETUNTR                                                          
         MVC   ADACC,ADUNIT                                                     
         MVI   MODE,PROCACL                                                     
         GOTOR GO                                                               
         B     LSTGET02                                                         
LSTGETX  J     EXIT                                                             
         DROP  RB                                                               
         EJECT                                                                  
***********************************************************************         
*                                                                     *         
***********************************************************************         
                                                                                
SRTGET   NTR1  BASE=*,LABEL=*                                                   
         L     R2,ADIN                                                          
         L     R3,ADIO                                                          
SRTGET02 GET   (R2),(R3)                                                        
         CLC   4(3,R3),RCRQTOT     CHECK REQUEST NO IN RECORD                   
         BH    SRTGETX             AGAINST PRESENT REQUEST NO                   
         BL    SRTGET02                                                         
         CLC   4(8,R3),ACMEFFS                                                  
         BE    SRTGETX                                                          
         MVI   MODE,PROCSORT                                                    
         GOTOR GO                                                               
         B     SRTGET02                                                         
SRTGETX  J     EXIT                                                             
         DROP  RB                                                               
         EJECT                                                                  
***********************************************************************         
*                                                                     *         
***********************************************************************         
                                                                                
ULAGET   NTR1  BASE=*,LABEL=*                                                   
                                                                                
GETUNT   MVI   ACMRQDPT,0          SET LEVEL 0 REQUEST (NO ACCOUNT)             
         CLI   QUNIT,C' '                                                       
         BNE   GETUNT08                                                         
         MVI   RCSVUNIT,C' '                                                    
         CLI   FCRDACC,YES                                                      
         BNE   ULAGETX                                                          
                                                                                
GETUNT02 SR    R1,R1                                                            
         IC    R1,RCSVUNIT                                                      
         AHI   R1,1                                                             
         MVC   KEY,SPACES                                                       
         MVC   KEY(L'UNTKCPY),RCSVCOMP                                          
         STC   R1,KEY+(UNTKUNT-UNTRECD)                                         
                                                                                
GETUNT04 GOTOR HIGH                FIRST OR NEXT                                
         MVC   RCSVUNIT,KEY+(UNTKUNT-UNTRECD)                                   
         CLC   KEYSAVE(UNTKUNT-UNTRECD),KEY                                     
         BE    GETUNT10                                                         
                                                                                
GETUNT06 MVI   MODE,COMPLAST                                                    
         GOTOR GO                                                               
         J     EXITLO                                                           
                                                                                
GETUNT08 MVC   RCSVUNIT,QUNIT      REQUESTED UNIT                               
         MVC   KEY,SPACES                                                       
         MVC   KEY(UNTKEND),RCSVCOMP                                            
         GOTOR READRTRN                                                         
         CLI   DMCB+8,0                                                         
         BNE   GETUNT06                                                         
                                                                                
GETUNT10 CLC   KEY(L'EOFR),EOFR    TEST  EOF RECORD                             
         BH    GETUNT06                                                         
         MVC   ACMKEY,KEY          BUILD WORK CODE BLOCK FOR UNIT               
         L     R0,ACMACBK                                                       
         L     R1,=A(ACMWCBL*ACMWCBN)                                           
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
                                                                                
         SR    R3,R3               R3=A(W/C LONG NAME BUFFER)                   
         SR    R4,R4               R4=N'ENTRIES LEFT IN LONG BUFFER             
         CLI   FCWCNAME,C'Y'       TEST APPLICATION WANTS LONG NAMES            
         BNE   GETUNT14                                                         
         ICM   R0,15,ACMWCNMA                                                   
         BNZ   GETUNT12                                                         
         L     R0,=A(WCNTABL*ACMWCBN)                                           
         GETMAIN R,LV=(0)                                                       
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
         ST    R1,ACMWCNMA         SAVE A(BUFFER)                               
         LR    R0,R1                                                            
                                                                                
GETUNT12 L     R1,=A(WCNTABL*ACMWCBN)                                           
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
         L     R3,ACMWCNMA         R3=A(W/C LONG NAME BUFFER)                   
         LA    R4,ACMWCBN          R4=N'ENTRIES IN BUFFER                       
                                                                                
         USING WCNTABD,R3                                                       
         USING ACMWCBK,R2                                                       
         USING WCORECD,R1                                                       
GETUNT14 L     R2,ACMACBK          BUILD A BLOCK OF WORK CODES                  
         LHI   R0,ACMWCBN                                                       
         LA    R1,KEY                                                           
         XC    WCOKEY,WCOKEY                                                    
         MVI   WCOKTYP,WCOKTYPQ    X'0A'                                        
         MVC   WCOKCPY,RCCOMPFL                                                 
         MVC   WCOKUNT,RCSVUNIT                                                 
         MVC   WCOKLDG,QLEDGER                                                  
         CLC   FCWCULOR,SPACES     ANY WORK CODE U/L OVER-RIDE ?                
         BNH   *+10                NO                                           
         MVC   WCOKUNT(2),FCWCULOR YES, GET WORK CODE FROM THIS U/L             
         GOTOR HIGH                                                             
         B     GETUNT18                                                         
                                                                                
GETUNT16 GOTOR SEQ                                                              
GETUNT18 CLC   KEY(WCOKLDG-WCORECD),KEYSAVE                                     
         BNE   GETUNT28                                                         
         CLI   QLEDGER,C' '                                                     
         BE    GETUNT20                                                         
         LA    RE,QLEDGER                                                       
         CLC   FCWCULOR,SPACES                                                  
         BNH   *+8                                                              
         LA    RE,FCWCULOR+1       POINT TO LEDGER                              
         CLC   0(L'QLEDGER,RE),KEY+(WCOKLDG-WCORECD)                            
         BNE   GETUNT16                                                         
                                                                                
GETUNT20 GOTOR GETUNTR             (USE UNIT AREA FOR READING)                  
         L     R1,ADUNIT                                                        
         MVC   ACMWCUL,WCOKUNT-WCORECD(R1)                                      
         AH    R1,DATADISP                                                      
         MVC   ACMWCEL,0(R1)                                                    
         CLI   ACMWCEL+1,WCOLNQ    DEAL WITH OLD STYLE ELEMENTS                 
         MVI   ACMWCEL+1,WCOLNQ                                                 
         BE    *+10                                                             
         XC    ACMWCEL+19(5),ACMWCEL+19                                         
         AHI   R2,ACMWCBL          BUMP TO NEXT W/C BUFFER ENTRY                
         DROP  R2                                                               
                                                                                
         LTR   R3,R3               TEST LONG NAME BUFFER TO BE BUILT            
         BZ    GETUNT26                                                         
         BCT   R4,*+6              TEST SPACE IN BUFFER FOR NAME                
         DC    H'0'                                                             
         MVC   WCNCODE,WCOCODE-WCOELD(R1)                                       
         MVC   WCNNAME,SPACES                                                   
         MVC   WCNNAME(L'WCODESC),WCODESC-WCOELD(R1)                            
         SR    RE,RE                                                            
GETUNT22 IC    RE,WCOLN-WCOELD(R1)                                              
         AR    R1,RE                                                            
         CLI   0(R1),0                                                          
         BE    GETUNT24                                                         
         CLI   0(R1),NAMELQ                                                     
         BNE   GETUNT22                                                         
         MVC   WCNNAME,SPACES                                                   
         IC    RE,NAMLN-NAMELD(R1)                                              
         SHI   RE,NAMLN1Q+1                                                     
         EX    RE,*+8                                                           
         B     GETUNT24                                                         
         MVC   WCNNAME(0),NAMEREC-NAMELD(R1)                                    
GETUNT24 AHI   R3,WCNTABL                                                       
         DROP  R3                                                               
                                                                                
GETUNT26 BCT   R0,GETUNT16                                                      
         DC    H'0'                                                             
                                                                                
GETUNT28 LTR   R3,R3               TEST LONG NAME BUFFER BUILT                  
         BZ    *+14                                                             
         LHI   R0,ACMWCBN                                                       
         SR    R0,R4                                                            
         STCM  R0,3,ACMWCNMN       SET N'ENTRIES IN LONG NAME BUFFER            
         MVC   KEY,ACMKEY          RESTORE SAVED UNIT KEY                       
         GOTOR READ                                                             
         GOTOR GETUNTR                                                          
                                                                                
         L     R1,ADUNIT           EXTRACT UNIT RECORD VALUES                   
         AH    R1,DATADISP                                                      
         SR    R0,R0                                                            
         XC    ADUNTELS(ADUNTELX-ADUNTELS),ADUNTELS                             
GETUNT30 CLI   0(R1),0                                                          
         BE    GETUNT32                                                         
         CLI   0(R1),NAMELQ                                                     
         BNE   *+8                                                              
         ST    R1,ADUNTNAM                                                      
         IC    R0,1(R1)                                                         
         AR    R1,R0                                                            
         B     GETUNT30                                                         
                                                                                
GETUNT32 MVI   MODE,UNITFRST                                                    
         GOTOR GO                                                               
GETUNTX  DS    0H                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO HANDLE LEDGER RECORDS                                    *         
***********************************************************************         
                                                                                
GETLDG   CLI   QLEDGER,C' '                                                     
         BNE   GETLDG08                                                         
         MVI   RCSVLEDG,C' '                                                    
         CLI   FCRDACC,YES                                                      
         BNE   ULAGETX                                                          
                                                                                
GETLDG02 SR    R1,R1                                                            
         IC    R1,RCSVLEDG                                                      
         AHI   R1,1                                                             
         MVC   KEY,SPACES                                                       
         MVC   KEY(LDGKLDG-LDGRECD),RCSVCOMP                                    
         STC   R1,KEY+(LDGKLDG-LDGRECD)                                         
                                                                                
GETLDG04 GOTOR HIGH                FIRST OR NEXT                                
         MVC   RCSVLEDG,KEY+(LDGKLDG-LDGRECD)                                   
         CLC   KEYSAVE(LDGKLDG-LDGRECD),KEY                                     
         BE    GETLDG10                                                         
                                                                                
GETLDG06 MVI   MODE,UNITLAST                                                    
         GOTOR GO                                                               
         CLI   QUNIT,C' '                                                       
         BE    GETUNT02                                                         
         MVI   MODE,COMPLAST                                                    
         GOTOR GO                                                               
         B     ULAGETX                                                          
                                                                                
GETLDG08 MVC   RCSVLEDG,QLEDGER    REQUESTED LEDGER                             
         MVC   KEY,SPACES                                                       
         MVC   KEY(LDGKEND),RCSVCOMP                                            
         GOTOR READRTRN                                                         
         CLI   DMCB+8,0                                                         
         BNE   GETLDG06                                                         
                                                                                
GETLDG10 GOTOR GETLDGR                                                          
         L     R1,ADLEDGER         EXTRACT LEDGER RECORD VALUES                 
         AH    R1,DATADISP                                                      
         SR    R0,R0                                                            
         XC    ADLDGELS(ADLDGELX-ADLDGELS),ADLDGELS                             
         XC    ACMALGST,ACMALGST                                                
GETLDG12 CLI   0(R1),0                                                          
         BE    GETLDG14                                                         
         CLI   0(R1),LDGELQ                                                     
         BNE   *+8                                                              
         ST    R1,ADLDGEL                                                       
         CLI   0(R1),ACLELQ                                                     
         BNE   *+8                                                              
         ST    R1,ADLDGHIR                                                      
         CLI   0(R1),NAMELQ                                                     
         BNE   *+8                                                              
         ST    R1,ADLDGNAM                                                      
         CLI   0(R1),RSTELQ                                                     
         BNE   *+8                                                              
         ST    R1,ACMALGST                                                      
         IC    R0,1(R1)                                                         
         AR    R1,R0                                                            
         B     GETLDG12                                                         
                                                                                
GETLDG14 L     R2,ADLDGHIR         SET ACCOUNT LEVEL LENGTHS                    
         USING ACLELD,R2                                                        
         XC    ACMLVS(ACMLVX-ACMLVS),ACMLVS                                     
         SR    R1,R1                                                            
                                                                                
         ICM   R1,1,ACLVLEN        R1=LEVEL A LENGTH                            
         BNZ   *+6                                                              
         DC    H'0'                                                             
         BCTR  R1,0                                                             
         STH   R1,ACMLVA                                                        
         MVI   ACMDPTH,1                                                        
                                                                                
         ICM   R1,1,ACLVLEN+(L'ACLVALS)                                         
         BZ    GETLDG16                                                         
         BCTR  R1,0                                                             
         STH   R1,ACMLVB                                                        
         MVI   ACMDPTH,2                                                        
                                                                                
         ICM   R1,1,ACLVLEN+(L'ACLVALS*2)                                       
         BZ    GETLDG16                                                         
         BCTR  R1,0                                                             
         STH   R1,ACMLVC                                                        
         MVI   ACMDPTH,3                                                        
                                                                                
         ICM   R1,1,ACLVLEN+(L'ACLVALS*3)                                       
         BZ    GETLDG16                                                         
         BCTR  R1,0                                                             
         STH   R1,ACMLVD                                                        
         MVI   ACMDPTH,4                                                        
                                                                                
GETLDG16 NI    ACMSTAT,FF-ACMFTRN TURN OFF FILTER BIT                           
         L     R1,ADLDGEL                                                       
         MVC   ACMOFPOS,LDGOPOS-LDGELD(R1)                                      
                                                                                
         GOTOR PROFILES,DMCB,ADLEDGER  GET LEDGER PROFILES                      
         OI    ACMINDS,ACMILDGP    ASSUME PROFILES FOR LEDGER                   
         OC    PROGPROF,PROGPROF   DID I GET PROFILES FOR LEDGER                
         BNZ   *+8                 IF YES LEAVE ALONE - ELSE TURN OFF           
         NI    ACMINDS,FF-ACMILDGP                                              
                                                                                
         GOTOR LDGIND              SET LEDGER INDICATORS                        
                                                                                
         L     RE,VFLTBUF                                                       
         XC    0(4,RE),0(RE)                                                    
         XC    ADPROFIL,ADPROFIL                                                
         MVI   ACMLTYP,C' '        SET UP FOR NO FILTERS                        
         CLC   ACMMEND,ACMEFFS     TEST MONTH OF SERVICE USED                   
         BE    GETLDG22            NO, LEAVE AS BLANK                           
         TM    ACMINDS,ACMIMOSR    WAS MOS FILTER REQUESTED                     
         BZ    GETLDG22            NO, LEAVE AS BLANK                           
                                                                                
         MVI   ACMLTYP,ACMATPL     FILTERING, SET UP FOR PROFIT & LOSS          
         LA    R1,LDGTAB           GET UNIT/LEDGER TABLE                        
         LHI   R0,LDGNUM           R0=NUMBER OF ENTRIES                         
         L     RF,ADLEDGER                                                      
GETLDG18 CLC   0(2,R1),1(RF)                                                    
         BE    GETLDG20            FOUND IT, LEAVE TYPE ALONE                   
         AHI   R1,2                KEEP LOOKING                                 
         BCT   R0,GETLDG18                                                      
         MVI   ACMLTYP,ACMATBS     NOT THERE, CHANGE TO BALANCE                 
                                                                                
GETLDG20 ICM   R1,15,ACMALGST      TEST IF TYPE OVERRIDDEN IN LEDGER            
         BZ    GETLDG22                                                         
         USING RSTELD,R1                                                        
         TM    RSTSTAT3,RSTSLAPL+RSTSLABS                                       
         BZ    GETLDG22            NOT OVERRIDDEN IN LEDGER                     
         MVI   ACMLTYP,ACMATPL                                                  
         TM    RSTSTAT3,RSTSLAPL                                                
         BO    GETLDG22                                                         
         MVI   ACMLTYP,ACMATBS                                                  
         DROP  R1                                                               
                                                                                
GETLDG22 MVI   MODE,LEDGFRST                                                    
         GOTOR GO                                                               
         XC    ACMPRMDE,ACMPRMDE   CLEAR ALL ACCOUNT MODES                      
         XC    ACMLFMDE,ACMLFMDE                                                
         XC    ACMLLMDE,ACMLLMDE                                                
                                                                                
GETLDGX  DS    0H                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO CONTROL REQUEST ACCOUNT READING                          *         
***********************************************************************         
                                                                                
REQACT   TM    ACMINDS4,ACMIUACL   USER ACCOUNT LIST SETUP ?                    
         BO    REQACT06            YES, SET ON ACMIAGRP                         
         TM    ACMINDS3,ACMIAGRP   TEST ACCOUNT GROUP REQUEST                   
         BZ    REQACT02                                                         
         GOTOR SETACT              BUILD LIST OF ACCOUNTS FOR GROUP             
         JZ    EXITHI                                                           
         B     REQACT04                                                         
                                                                                
REQACT02 CLI   FCRDMOSP,YES                                                     
         BNE   REQACT04                                                         
         GOTOR CURACT              BUILD LIST OF ACTIVE ACCOUNTS                
         BZ    NXTACT28                                                         
         OI    ACMINDS3,ACMIAGRP   SET ACCOUNT REQUEST                          
                                                                                
REQACT04 TM    ACMINDS3,ACMIAPRS                                                
         BZ    REQACT08                                                         
         GOTOR SETPRS              BUILD LIST OF PERSON ACCOUNTS                
         BZ    NXTACT28                                                         
                                                                                
REQACT06 OI    ACMINDS3,ACMIAGRP   SET ACCOUNT REQUEST                          
                                                                                
REQACT08 TM    ACMINDS,ACMIEMUD+ACMINEWO  TEST NEW FILE/NEW OFFICES             
         BNO   *+12                NO                                           
         CLI   FCSEQ,FCSEQOFF      TEST READ FILE IN OFFICE SEQUENCE            
         BE    OFFSEQ                                                           
                                                                                
         MVC   KEY,SPACES          SET UP KEY WITH CPY/UNIT/LDGR                
         MVC   KEY(ACTKACT-ACTRECD),RCSVCOMP                                    
         MVI   KEY+(ACTKACT-ACTRECD),X'41'                                      
                                                                                
         TM    ACMINDS3,ACMIAGRP   TEST ACCOUNT GROUP REQUEST                   
         BNZ   NXTACT                                                           
         MVC   ACMWRK(L'QACCOUNT),QACCOUNT                                      
         LH    R1,ACMLVA                                                        
         EX    R1,SPACOMP                                                       
         BNE   REQACT10                                                         
         CLI   FCRDACC,YES                                                      
         BNE   ULAGETX                                                          
         GOTOR HIGH                START WITH A READ HIGH                       
         B     NXTACT14                                                         
                                                                                
REQACT10 MVI   ACMRQDPT,1          SET LEVEL 1 ACCOUNT REQUESTED                
         EX    R1,KEYMOVE                                                       
         TM    ACMINDS3,ACMIAUTO   TEST AUTO ACCOUNT LEVEL 1                    
         BZ    REQACT12                                                         
         LTR   R1,R1               TEST ONE CHARACTER LEVEL 1 ACCOUNT           
         BZ    REQACT12                                                         
         GOTOR HIGH                NO - READ HIGH AND SET FIRST ACCOUNT         
         CLC   KEY(ACTKACT+1-ACTRECD),KEYSAVE                                   
         BNE   NXTACT28                                                         
         B     REQACT14                                                         
                                                                                
REQACT12 GOTOR READRTRN                                                         
         CLI   DMCB+8,0            TEST RECORD FOUND                            
         BNE   NXTACT28                                                         
                                                                                
REQACT14 GOTOR GETLVAR                                                          
         GOTOR PSTLVA                                                           
         MVC   ACMWRK(L'QACCOUNT),QACCOUNT                                      
         LH    R1,ACMLVA                                                        
         EX    R1,SPAMOVE                                                       
         LH    R1,ACMLVB                                                        
         EX    R1,SPACOMP                                                       
         BNE   REQACT16                                                         
         CLC   ACMDPTH,ACMRQDPT                                                 
         BNE   NXTACT                                                           
         B     REQACT22                                                         
                                                                                
REQACT16 MVI   ACMRQDPT,2          SET LEVEL 2 ACCOUNT REQUESTED                
         EX    R1,KEYMOVE                                                       
         GOTOR READ                                                             
         GOTOR GETLVBR                                                          
         GOTOR PSTLVB                                                           
         MVC   ACMWRK(L'QACCOUNT),QACCOUNT                                      
         LH    R1,ACMLVB                                                        
         EX    R1,SPAMOVE                                                       
         LH    R1,ACMLVC                                                        
         EX    R1,SPACOMP                                                       
         BNE   REQACT18                                                         
         CLC   ACMDPTH,ACMRQDPT                                                 
         BNE   NXTACT                                                           
         B     REQACT22                                                         
                                                                                
REQACT18 MVI   ACMRQDPT,3          SET LEVEL 3 ACCOUNT REQUESTED                
         EX    R1,KEYMOVE                                                       
         GOTOR READ                                                             
         GOTOR GETLVCR                                                          
         GOTOR PSTLVC                                                           
         MVC   ACMWRK(L'QACCOUNT),QACCOUNT                                      
         LH    R1,ACMLVC                                                        
         EX    R1,SPAMOVE                                                       
         LH    R1,ACMLVD                                                        
         EX    R1,SPACOMP                                                       
         BNE   REQACT20                                                         
         CLC   ACMDPTH,ACMRQDPT                                                 
         BNE   NXTACT                                                           
         B     REQACT22                                                         
                                                                                
REQACT20 MVI   ACMRQDPT,4          SET LEVEL 4 ACCOUNT REQUESTED                
         EX    R1,KEYMOVE                                                       
         GOTOR READ                                                             
         GOTOR GETACTR                                                          
         GOTOR PSTLVD                                                           
                                                                                
REQACT22 CLI   FCRDACC,YES                                                      
         BNE   ULAGETX                                                          
         GOTOR GETACT              PROCESS SINGLE ACCOUNT                       
         B     NXTACT28                                                         
                                                                                
SPAMOVE  MVC   ACMWRK(0),SPACES                                                 
SPACOMP  CLC   ACMWRK(0),SPACES                                                 
KEYMOVE  MVC   KEY+(ACTKACT-ACTRECD)(0),QACCOUNT                                
KEYCOMP  CLC   KEY+(ACTKACT-ACTRECD)(0),KEYSAVE+(ACTKACT-ACTRECD)               
REQCOMP  CLC   KEY+(ACTKACT-ACTRECD)(0),QACCOUNT                                
GRPCOMP  CLC   KEY+(ACTKACT-ACTRECD)(0),0(R3)                                   
GRPMOVE  MVC   KEY+(ACTKACT-ACTRECD)(0),0(R3)                                   
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO READ ACCOUNTS FOR A REQUEST                              *         
***********************************************************************         
                                                                                
NXTACT   CLI   FCRDACC,YES                                                      
         BNE   NXTACTX                                                          
                                                                                
NXTACT02 TM    ACMINDS3,ACMIAGRP                                                
         BNZ   *+12                                                             
         GOTOR SEQ                 GET NEXT RECORD                              
         B     NXTACT14                                                         
         SAM31                     ENTER XA MODE                                
         L     R3,ACMAACTS         R3=A(CURRENT A/C BUFFER ENTRY)               
                                                                                
NXTACT04 SR    R0,R0                                                            
         IC    R0,ACMAACTD         R0=DEPTH OF ACCTS IN BUFFER                  
         LA    R1,ACMLVA           R1=A(LEVEL LENGTHS)                          
                                                                                
NXTACT06 LH    RE,0(R1)            RE=LENGTH OF CURRENT LEVEL                   
         EX    RE,GRPCOMP          KEY VS. ACCOUNT IN BUFFER                    
         BL    NXTACT08                                                         
         BH    NXTACT10                                                         
         AHI   R1,L'ACMLVA         R1=A(NEXT LEVEL LENGTH)                      
         BCT   R0,NXTACT06                                                      
         CLC   ACMAACTD,ACMDPTH    DEPTH OF REQ. VS. DEPTH OF LEDGER            
         BNL   NXTACT10                                                         
         SAM24                     EXIT XA MODE & RETURN WITH CC SET            
         GOTOR SEQ                                                              
         B     NXTACT14                                                         
                                                                                
NXTACT08 MVC   KEYSAVE,KEY                                                      
         MVC   KEY+(ACTKACT-ACTRECD)(L'ACTKACT),SPACES                          
         EX    RE,GRPMOVE          ACCOUNT IN BUFFER TO KEY                     
         SAM24                     EXIT XA MODE                                 
         GOTOR READRTRN                                                         
         CLI   DMCB+8,0                                                         
         BE    NXTACT14                                                         
         SAM31                     ENTER XA MODE AGAIN                          
                                                                                
NXTACT10 LH    R0,ACMAACTN         R0=NUMBER IN BUFFER                          
         SHI   R0,1                                                             
         BZ    NXTACT12            END OF BUFFER                                
         STH   R0,ACMAACTN         SAVE NUMBER REMAINING IN BUFFER              
         AHI   R3,L'ACTKACT        R3=A(NEXT ITEM IN BUFFER)                    
         ST    R3,ACMAACTS                                                      
         B     NXTACT04                                                         
                                                                                
NXTACT12 SAM24                     EXIT XA MODE                                 
         B     NXTACT28                                                         
                                                                                
NXTACT14 CLC   KEY(ACTKACT-ACTRECD),KEYSAVE       TEST C/U/L                    
         BNE   NXTACT28                                                         
         CLC   KEY+ACTKEND(L'KEY-ACTKEND),SPACES  TEST ACCOUNT                  
         BNE   NXTACT02                                                         
                                                                                
         LA    R4,ACMLVA           R3=A(LEVEL LENGTHS)                          
         LHI   R2,1                R2=LEVEL NUMBER                              
         XC    ACMLDISP,ACMLDISP   CLEAR LAST LEVEL LENGTH                      
                                                                                
NXTACT16 LH    RE,0(R4)            RE=LEVEL LENGTH-1                            
         CLM   R2,1,ACMRQDPT       TEST LEVEL LE REQUEST LEVEL                  
         BH    NXTACT20                                                         
         TM    ACMINDS3,ACMIAUTO   TEST AUTO LEVEL 1 ACCOUNT                    
         BZ    NXTACT18                                                         
         CLC   KEY+(ACTKACT-ACTRECD)(1),QACCOUNT                                
         BNE   NXTACT28                                                         
         B     NXTACT20                                                         
                                                                                
NXTACT18 EX    RE,REQCOMP          YES - TEST KEY AGAINST REQUEST               
         BNE   NXTACT28                                                         
                                                                                
NXTACT20 EX    RE,KEYCOMP          TEST CHANGE AT LEVEL N                       
         BE    NXTACT26                                                         
         TM    ACMINDS3,ACMIAGRP   TEST ACCOUNT GROUPS ACTIVE                   
         BZ    NXTACT24                                                         
         CLM   R2,1,ACMAACTD       DEPTH OF ACCOUNT VS. DEPTH BUFFER            
         BH    NXTACT24                                                         
         SAM31                     SWITCH TO XA MODE                            
         EX    RE,GRPCOMP          KEY VS. ACCOUNT IN BUFFER                    
         BE    NXTACT22                                                         
         LA    RE,KEY+3            POINT TO ACCOUNT                             
         AH    RE,ACMLDISP         RE=LENGTH OF LAST LEVEL(-1)                  
         LHI   RF,L'KEY-4          RF=LENGTH TO END OF KEY                      
         SH    RF,ACMLDISP                                                      
         EX    RF,*+8                                                           
         B     NXTACT10                                                         
         XC    0(0,RE),0(RE)       CLEAR END OF KEY                             
                                                                                
NXTACT22 SAM24                     EXIT XA MODE                                 
                                                                                
NXTACT24 CLM   R2,1,ACMRQDPT       TEST LEVEL GR REQUEST LEVEL                  
         BNH   *+16                                                             
         GOTOR ACTGET              YES - GET AND POST ACCOUNT                   
         GOTOR PSTACT                                                           
         BNE   NXTACT14            CC=NEQ MEANS PROCESS NEXT RECORD             
         CLM   R2,1,ACMDPTH        TEST LEVEL IS MAXIMUM DEPTH                  
         BNE   NXTACT02                                                         
         CLI   FCRDACC,YES         ARE WE STILL READING ACCOUNT?                
         BNE   NXTACTX             NO                                           
         GOTOR GETACT              YES - READ TRANSACTIONS                      
         B     NXTACT02                                                         
                                                                                
NXTACT26 XC    ACMLDISP,ACMLDISP   LEVEL 1 DISPLACEMENT IS ZERO                 
         CHI   R2,1                IS IT FIRST LEVEL ?                          
         BNH   NXTACT27            YES                                          
         LH    RF,0(,R4)           R4=LAST LEVEL LENGTH-1                       
         AHI   RF,1                ADD BACK 1 TO EX LENGTH                      
         STH   RF,ACMLDISP         SAVE DISPLACEMENT TO LAST                    
                                                                                
NXTACT27 AHI   R4,L'ACMLVA         BUMP TO NEXT LEVEL LENGTH                    
         AHI   R2,1                BUMP TO NEXT LEVEL                           
         CLM   R2,1,ACMDPTH        TEST LEVEL GR MAXIMUM DEPTH                  
         BNH   NXTACT16                                                         
         DC    H'0'                INVALID RECORD                               
                                                                                
NXTACT28 LA    R1,ACMLLMDE                                                      
         LHI   R0,3                                                             
         GOTOR GOSAVE              PASS ANY SAVED LAST MODES                    
         MVI   MODE,LEDGLAST                                                    
         GOTOR GO                                                               
         TM    ACMINDS3,ACMIAUTO   TEST AUTO ACCOUNT SET                        
         BZ    *+16                                                             
         MVI   QACCOUNT,C' '       YES - CLEAR IT                               
         NI    ACMINDS3,FF-(ACMIAUTO)                                           
         MVI   ACMRQDPT,0                                                       
         CLI   FCRDMOSP,YES                                                     
         BNE   *+8                                                              
         NI    ACMINDS3,FF-(ACMIAGRP)                                           
         CLI   QLEDGER,C' '                                                     
         BE    GETLDG02                                                         
         MVI   MODE,UNITLAST                                                    
         GOTOR GO                                                               
         CLI   QUNIT,C' '                                                       
         BE    GETUNT02                                                         
         MVI   MODE,COMPLAST                                                    
         GOTOR GO                                                               
NXTACTX  B     ULAGETX                                                          
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO ACCOUNTS/TRANSACTIONS IN OFFICE SEQUENCE                 *         
* USES OFFICE/ACCOUNT PASSIVE POINTERS                                *         
***********************************************************************         
                                                                                
OFFSEQ   CLI   FCRDACC,YES                                                      
         BNE   ULAGETX                                                          
         MVI   ACMRQDPT,0                                                       
         MVC   ACMWRK(L'QACCOUNT),QACCOUNT                                      
         CLC   QACCOUNT,SPACES     TEST FOR REQUESTED ACCOUNT                   
         BE    OFFSEQ04            NO                                           
                                                                                
* FIND LEVEL OF REQUESTED ACCOUNT                                               
                                                                                
         LHI   R0,3                                                             
         LA    R2,ACMLVA           R2=A(LEVEL LENGTHS)                          
         LHI   R3,1                R3=LEVEL NUMBER                              
                                                                                
OFFSEQ02 STC   R3,ACMRQDPT         SET REQUESTED ACCOUNT DEPTH                  
         MVC   ACMWRK(L'QACCOUNT),QACCOUNT                                      
         LH    R1,0(R2)            LENGTH OF PRIOR LEVEL(S)                     
         EX    R1,SPAMOVE          CLEAR LEVELS BEFORE THIS ONE                 
         AHI   R2,2                BUMP TO THIS LEVEL                           
         LH    R1,0(R2)                                                         
         EX    R1,SPACOMP          TEST FOR SPACES                              
         BE    OFFSEQ04                                                         
         AHI   R3,1                INCREMENT LEVEL NUMBER                       
         BCT   R0,OFFSEQ02                                                      
         STC   R3,ACMRQDPT                                                      
                                                                                
* INITIALIZE OFFICE/ACCOUNT PASSIVE POINTER                                     
                                                                                
OFFSEQ04 XC    ACMLOAP,ACMLOAP     CLEAR LAST OA PASSIVE POINTER                
         MVC   ACMCURR,SPACES      INITIALIZE LAST ACCOUNT                      
         LA    R3,ACMLOAP                                                       
         USING OAPRECD,R3          BUILD INITIAL OAP KEY                        
         MVI   OAPKTYP,OAPKTYPQ                                                 
         MVC   OAPKCUL,RCSVCOMP    COMPANY/UNIT/LEDGER                          
         MVC   OAPKOFF,SPACES                                                   
                                                                                
* READ FOR NEXT OFFICE                                                          
                                                                                
OFFSEQ06 L     R3,ACMABUF                                                       
         MVC   OAPKEY,ACMLOAP      INITIALIZE WITH LAST OAP KEY                 
         MVI   OAPKACT,FF          SET TO BUMP PAST LAST ACCOUNT                
         MVC   ACMTRACE,OAPKEY                                                  
         GOTOR ACMISDDS,DMCB,ISREAD,(R3),0,ACMADDIR,(R3),0                      
         OC    8(2,R1),8(R1)                                                    
         BZ    OFFSEQ08                                                         
         TM    9(R1),X'04'         TEST FOR EOF                                 
         BO    OFFSEQ40            YES-WRAP IT UP                               
         DC    H'0'                                                             
                                                                                
OFFSEQ08 CLC   OAPKEY(OAPKOFF-OAPKEY),ACMLOAP TEST SAME CUL                     
         BNE   OFFSEQ40            NO-WRAP UP                                   
         MVC   ACMLOAP(L'OAPKEY),OAPKEY SAVE LAST KEY                           
         CLI   RCTRACE,YES         TEST FOR TRACE OPTION                        
         BNE   OFFSEQ10                                                         
         GOTOR DMTRACE             YES-PRINT THE KEYS                           
                                                                                
OFFSEQ10 GOTOR DALINK              GET THE RECORD                               
                                                                                
OFFSEQ12 LA    R2,DMCB                                                          
         ICM   R2,8,ACMEFFS                                                     
         GOTOR VACCEMU,DMCB,DMNEWO,,(R3),(R3)                                   
                                                                                
         USING OFARECD,R3                                                       
         L     R0,ACMAOFA          SAVE THE OFFICE-ACCOUNT RECORD               
         LA    R1,ACMMAXRL                                                      
         LA    RE,OFARECD                                                       
         SR    RF,RF                                                            
         ICM   RF,3,OFARLEN                                                     
         MVCL  R0,RE                                                            
         MVI   ACMFLAG,ACMFLOA     PERFORM OFFICE SECURITY                      
         GOTOR OFFLMT                                                           
         BNE   OFFSEQ06            NO-TRY NEXT OFFICE                           
                                                                                
         LHI   R0,ACMLMOFA-ACMLMODE                                             
         LA    R1,ACMLMODE                                                      
         OC    0(ACMLMOFA-ACMLMODE,R1),0(R1)                                    
         BZ    *+8                                                              
         GOTOR GOSAVE                                                           
                                                                                
         LHI   R0,3                                                             
         LA    R1,ACMLLMDE         NOW DEAL WITH LEVCLAST->LEVALAST             
         GOTOR GOSAVE                                                           
                                                                                
         LHI   R0,1                                                             
         LA    R1,ACMLMOFA         FINISH UP WITH OFFLAST                       
         GOTOR GOSAVE                                                           
                                                                                
         MVC   ACMCURR,SPACES      CLEAR OUT ACCOUNT CONTROL                    
         XC    ACMFMODE,ACMFMODE                                                
         XC    ACMLMODE,ACMLMODE                                                
         MVI   ACMFMOFA,OFFIRST    SET OFFICE FIRST MODE                        
         MVI   ACMLMOFA,OFFLAST                                                 
                                                                                
         ZAP   ACMOBAL,ACMPZERO                                                 
         MVC   ACMOFNAM,SPACES     GET OFFICE NAME                              
         ICM   R0,15,ACMNOFNB      R0=N'OFFICES                                 
         BZ    OFFSEQ14            NONE                                         
         L     R2,ACMAOFNB         R2=A(OFFICE NAME BUFFER)                     
         CLC   OFAKOFF,0(R2)       MATCH ON OFFICE CODE                         
         BE    *+16                                                             
         AHI   R2,ACMOFNL          NEXT ENTRY                                   
         BCT   R0,*-14                                                          
         B     OFFSEQ14            NOT IN BUFFER                                
                                                                                
         MVC   ACMOFNAM,L'OFAKOFF(R2)                                           
                                                                                
* HAVE AN OFFICE-NOW READ AHEAD FOR REQUESTED ACCOUNT IF ANY                    
                                                                                
OFFSEQ14 CLC   QACCOUNT,SPACES     TEST FOR REQUESTED ACCOUNT                   
         BE    OFFSEQ22                                                         
                                                                                
         USING OAPRECD,R3                                                       
         MVC   OAPKEY,ACMLOAP      SET LAST KEY                                 
         MVC   OAPKACT,QACCOUNT    SET REQUESTED ACCOUNT                        
         MVC   ACMTRACE,OAPKEY                                                  
         GOTOR ACMISDDS,DMCB,ISREAD,(R3),0,ACMADDIR,(R3),0                      
         OC    8(2,R1),8(R1)                                                    
         BZ    OFFSEQ16                                                         
         TM    9(R1),X'04'                                                      
         BO    OFFSEQ40                                                         
         DC    H'0'                                                             
                                                                                
OFFSEQ16 CLI   RCTRACE,YES                                                      
         BNE   *+8                                                              
         GOTOR DMTRACE                                                          
         CLC   OAPKEY(OAPKACT-OAPKEY),ACMLOAP TEST SAME OFFICE                  
         BE    OFFSEQ18                                                         
                                                                                
         CLI   ACMRQDPT,0          TEST FOR REQUESTED ACCOUNT                   
         BE    OFFSEQ06            NO-NEXT OFFICE RIGHT AWAY                    
         CLI   ACMFMOFA,OFFIRST    TEST FOR PENDING OFFIRST                     
         BNE   *+16                                                             
         XC    ACMFMODE,ACMFMODE   YES-CLEAR MODES-NO ACCOUNTS                  
         XC    ACMLMODE,ACMLMODE   FOR OFFICE-AVOID OFFLAST                     
         B     OFFSEQ06                                                         
                                                                                
OFFSEQ18 SR    R0,R0                                                            
         ICM   R0,1,ACMRQDPT                                                    
         BZ    OFFSEQ20            NO REQUESTED ACCOUNT                         
         LA    RE,ACMLVA-2         GET LENGTH OF REQUESTED KEY                  
         AHI   RE,2                                                             
         BCT   R0,*-4                                                           
                                                                                
         LH    R1,0(RE)                                                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         CLC   OAPKACT(0),QACCOUNT  TEST VS. REQUESTED A/C                      
         BE    OFFSEQ20            YES                                          
                                                                                
         CLI   ACMFMOFA,OFFIRST    TEST FOR PENDING OFFIRST                     
         BNE   *+16                NO                                           
         XC    ACMFMODE,ACMFMODE   CLEAR MODE STACK--NO ACCOUNTS                
         XC    ACMLMODE,ACMLMODE   FOR OFFICE-AVOID OFFLAST                     
         B     OFFSEQ06            TRY NEXT OFFICE                              
                                                                                
OFFSEQ20 MVC   ACMLOAP(L'OAPKEY),OAPKEY                                         
         GOTOR DALINK                                                           
                                                                                
         LA    R2,DMCB                                                          
         ICM   R2,8,ACMEFFS                                                     
         GOTOR VACCEMU,DMCB,DMNEWO,,(R3),(R3)                                   
         L     R0,ACMAOFA                                                       
         LHI   R1,ACMMAXRL                                                      
         USING OFARECD,R3                                                       
         LA    RE,OFARECD                                                       
         SR    RF,RF                                                            
         ICM   RF,3,OFARLEN                                                     
         MVCL  R0,RE                                                            
                                                                                
* PASS OFFIRST MODE TO APPLICATION DIRECTLY                                     
                                                                                
OFFSEQ22 CLI   ACMFMOFA,OFFIRST    TEST FOR PENDING OFFIRST                     
         BNE   OFFSEQ24            NO                                           
                                                                                
         MVI   MODE,OFFIRST                                                     
         GOTOR GO                                                               
         MVI   ACMFMOFA,0                                                       
                                                                                
* READ ANY HIGHER LEVEL ACCOUNT RECORDS ON CHANGE AT A LEVEL                    
                                                                                
OFFSEQ24 LHI   R2,1                R2=LEVEL NUMBER                              
         L     R3,ACMAOFA          R3=A(ACCOUNT/OFFICE KEY)                     
         LA    R4,ACMLVA           R4=A(LEVEL'S LENGTH)                         
         SR    R0,R0                                                            
         IC    R0,ACMDPTH                                                       
         SH    R0,ACMHONE          R0=LOOP COUNTER OF HIGHER LEVELS             
         BZ    OFFSEQ32                                                         
                                                                                
OFFSEQ26 STC   R0,ACMODPTH         SET COUNTER                                  
         LH    R1,0(R4)            TEST FOR CHANGE AT THIS LEVEL                
         EX    R1,*+8                                                           
         B     *+10                                                             
         CLC   OFAKACT(0),ACMCURR+(ACTKACT-ACTKEY)                              
         BE    OFFSEQ30            NO                                           
                                                                                
         MVC   KEY,SPACES                                                       
         AHI   R1,ACTKACT-ACTKEY                                                
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   KEY(0),OFAKCULA     READ ACCOUNT AT HIGHER LEVEL                 
         GOTOR READ                                                             
         GOTOR ACTGET                                                           
         GOTOR PSTACT                                                           
         BE    OFFSEQ30                                                         
                                                                                
OFFSEQ28 L     R3,ACMABUF                                                       
         USING OAPRECD,R3                                                       
         MVC   OAPKEY,ACMLOAP      GET LAST KEY                                 
         LH    R1,0(R4)            GET LENGTH OF THIS LEVEL                     
         LA    RE,OAPKACT+1(R1)                                                 
         MVI   0(RE),FF            BUMP TO NEXT ACCOUNT                         
         MVC   ACMTRACE,OAPKEY                                                  
         GOTOR ACMISDDS,DMCB,ISREAD,(R3),0,ACMADDIR,(R3),0                      
         OC    8(2,R1),8(R1)                                                    
         BZ    OFFSEQ16                                                         
         TM    9(R1),X'04'                                                      
         BO    OFFSEQ40                                                         
         DC    H'0'                                                             
                                                                                
OFFSEQ30 AHI   R2,1                NEXT LEVEL                                   
         AHI   R4,2                                                             
         SR    R0,R0                                                            
         IC    R0,ACMODPTH         GET COUNTER                                  
         BCT   R0,OFFSEQ26                                                      
                                                                                
* UPDATE THE OFFICE BALANCE BROUGHT FORWARD FOR THE NEW OFFICE/ACCOUNT          
                                                                                
OFFSEQ32 L     R3,ACMAOFA                                                       
         USING OFARECD,R3                                                       
         ZAP   ACMOBAL,ACMPZERO                                                 
         CLI   ACMATYP,ACMATPL     TEST PROFIT/LOSS LEDGER                      
         BE    OFFSEQ36            YES-LET FLTTRN DEVELOP BALANCE BF            
         LA    R2,OFARECD                                                       
         AH    R2,DATADISP                                                      
         USING ABLELD,R2                                                        
         SR    R0,R0                                                            
                                                                                
OFFSEQ34 CLI   ABLEL,0             TEST FOR EOR                                 
         BE    OFFSEQ36            YES                                          
         CLI   ABLEL,ABLELQ        TEST FOR BALANCE ELEMENT                     
         BNE   *+14                NO                                           
         ZAP   ACMOBAL,ABLFRWD     ADD IN BALANCE BROUGHT FORWARD               
         B     OFFSEQ36                                                         
         IC    R0,ABLLN                                                         
         AR    R2,R0                                                            
         B     OFFSEQ34                                                         
         DROP  R2                                                               
                                                                                
* READ THE ACCOUNT AND GET TRANSACTIONS FOR THE OFFICE ACCOUNT                  
                                                                                
OFFSEQ36 SR    R2,R2                                                            
         IC    R2,ACMDPTH                                                       
         MVC   KEY,SPACES                                                       
         MVC   KEY(L'OFAKCULA),OFAKCULA READ LOWEST LEVEL ACCOUNT               
         MVC   ACMCURR,KEY                                                      
         GOTOR READ                                                             
         GOTOR ACTGET                                                           
         GOTOR PSTACT                                                           
         BNE   OFFSEQ38                                                         
         GOTOR GETACT                                                           
         DROP  R3                                                               
                                                                                
* READ NEXT OFFICE/ACCOUNT PASSIVE POINTER                                      
                                                                                
OFFSEQ38 L     R3,ACMABUF          R3=A(OFFICE ACCOUNT PASSIVE POINTER)         
         USING OAPRECD,R3                                                       
         MVC   OAPKEY,ACMLOAP                                                   
         MVI   OAPKEND(R3),FF                                                   
         MVC   ACMTRACE,OAPKEY                                                  
         GOTOR ACMISDDS,DMCB,ISREAD,(R3),0,ACMADDIR,(R3),0                      
         OC    8(2,R1),8(R1)                                                    
         BZ    OFFSEQ16                                                         
         TM    9(R1),X'04'                                                      
         BO    OFFSEQ40                                                         
         DC    H'0'                                                             
                                                                                
OFFSEQ40 XC    ACMLOAP,ACMLOAP                                                  
         XC    ACMCURR,ACMCURR                                                  
                                                                                
         LHI   R0,ACMLMOFA-ACMLMODE                                             
         LA    R1,ACMLMODE                                                      
         OC    0(ACMLMOFA-ACMLMODE,R1),0(R1)                                    
         BZ    *+8                                                              
         GOTOR GOSAVE                                                           
                                                                                
         LHI   R0,3                                                             
         LA    R1,ACMLLMDE         NOW DEAL WITH LEVCLAST->LEVALAST             
         GOTOR GOSAVE                                                           
                                                                                
         LHI   R0,1                                                             
         LA    R1,ACMLMOFA         FINISH UP WITH OFFLAST                       
         GOTOR GOSAVE                                                           
                                                                                
         LA    R1,ACMLLMDE                                                      
         LHI   R0,3                                                             
         GOTOR GOSAVE              PASS ANY SAVED LAST MODES                    
         MVI   MODE,LEDGLAST                                                    
         GOTOR GO                                                               
         CLI   QLEDGER,C' '                                                     
         BE    GETLDG02                                                         
         MVI   MODE,UNITLAST                                                    
         GOTOR GO                                                               
         CLI   QUNIT,C' '                                                       
         BE    GETUNT02                                                         
         MVI   MODE,COMPLAST                                                    
         GOTOR GO                                                               
                                                                                
ULAGETX  J     EXITEQ                                                           
         DROP  R3,RB                                                            
         EJECT                                                                  
***********************************************************************         
* CONTROL OF READING TRANSACTIONS AND HISTORIES FOR AN ACCOUNT        *         
***********************************************************************         
                                                                                
GETACT   NTR1  BASE=*,LABEL=*                                                   
         MVC   ACMATYP,ACMLTYP    RESET THE TYPE                                
         L     R1,ADACC                                                         
         TM    ACCOSTAT(R1),ACTSDELT TEST DELETED RECORD                        
         BO    GETTRNX                                                          
         GOTOR FLTACT                                                           
         BNE   GETTRNX            RECORD PASSED FILTER TESTS                    
                                                                                
         CLI   ACMDPTH,4          LEVEL MODES ARE ONE LESS THAN DEPTH           
         BE    GETACT02                                                         
         MVI   ACMLFMDE+2,0                                                     
         CLI   ACMDPTH,3                                                        
         BE    GETACT02                                                         
         MVI   ACMLFMDE+1,0                                                     
         CLI   ACMDPTH,2                                                        
         BE    GETACT02                                                         
         MVI   ACMLFMDE,0                                                       
                                                                                
GETACT02 LA    R1,ACMLFMDE         PASS MODES LEVAFRST B AND C                  
         LHI   R0,3                IF PRESENT                                   
         GOTOR GOSAVE                                                           
         CLI   FCSEQ,FCSEQOFF      TEST READING IN OFFICE SEQUENCE              
         BE    GETACT04            YES                                          
         XC    ACMFMODE,ACMFMODE   INITIALIZE FIRST                             
         XC    ACMLMODE,ACMLMODE   AND LAST MODES  FOR EST/TRANS/SUB            
                                                                                
GETACT04 CLI   FCGTFILT,YES        TEST FILTER NAMES REQUIRED                   
         BNE   GETACT06                                                         
         L     R1,ADACC                                                         
         MVC   WORK(3),0(R1)                                                    
         MVI   WORK+3,0                                                         
         GOTOR GETFILT,ACMPARA,(C'A',WORK),(5,ACMFLTS),FILTBLOC,       *        
               (X'80',VFLTBUF),DATAMGR                                          
                                                                                
GETACT06 MVI   MODE,PROCACC                                                     
         GOTOR GO                                                               
GETACTX  DS    0H                                                               
         EJECT                                                                  
***********************************************************************         
* CONTROL READING OF ESTIMATES                                        *         
***********************************************************************         
                                                                                
GETEST   CLI   FCRDEST,YES         DOES APPLICATION WANT ESTIMATES              
         BNE   GETESTX                                                          
         L     RF,ACMATRN                                                       
         AHI   RF,ACMMAXRL                                                      
         ST    RF,ADTRANS                                                       
         L     R2,ADACC                                                         
         AH    R2,DATADISP                                                      
         SR    R0,R0                                                            
                                                                                
         USING BUDELD,R2                                                        
GETEST02 CLI   BUDEL,0             TEST END OF RECORD                           
         BNE   *+16                                                             
         MVI   MODE,LASTEST                                                     
         GOTOR GO                                                               
         B     GETESTX                                                          
         CLI   BUDEL,BUDELQ        OLD JOB ESTIMATE ELEMENT                     
         BNE   *+12                                                             
         LA    R1,BUDWORK          POINT TO BUDGET WORK CODE                    
         B     GETEST04                                                         
         USING ESTELD,R2                                                        
         CLI   ESTEL,ESTELQ        NEW JOB ESTIMATE ELEMENT                     
         BNE   GETEST08                                                         
         LA    R1,ESTWORK          POINT TO ESTIMATE WORK CODE                  
                                                                                
GETEST04 L     RF,ADTRANS                                                       
         MVC   0(100,RF),0(R2)                                                  
         CLI   FCGETOPT,FCGETOWC   TEST GETOPT AT WORK CODE LEVEL               
         BNE   GETEST06                                                         
         L     RF,ADGOBLOC                                                      
         USING GOBLOCKD,RF         RF=A(GOBLOCK)                                
         MVC   GOSELWC,0(R1)                                                    
         MVC   GOAKEY,LASTIO                                                    
         DROP  RF                                                               
         GOTOR GETPOP              RESOLVE WORK CODE LEVEL OPTIONS              
                                                                                
GETEST06 MVI   MODE,PROCEST                                                     
         GOTOR GO                                                               
         MVI   ACMLMACC,ACCLAST    EXPECTS ACCLAST AFTER PROCEST                
                                                                                
GETEST08 IC    R0,ESTLN            BUMP TO NEXT RECORD ELEMENT                  
         AR    R2,R0                                                            
         B     GETEST02                                                         
GETESTX  DS    0H                                                               
         EJECT                                                                  
***********************************************************************         
* CONTROL READING OF OFFICE-ACCOUNT RECORDS FOR AN ACCOUNT            *         
*                                                                     *         
* EACH RECORD IS PASSED WITH A PROCOFA CALL TO APPLICATION.           *         
* AN OFALAST CALL IS MADE TO THE APPLICATION FOR EACH ACCOUNT.        *         
***********************************************************************         
                                                                                
GETOFA   CLI   FCRDOFA,YES         TEST TO READ OFFICE-ACCOUNT RECS             
         BNE   GETOFAX                                                          
         TM    ACMINDS,ACMIEMUD+ACMINEWO TEST NEW FILE/NEW OFFICES              
         BNO   GETOFAX             NO                                           
                                                                                
         L     R2,ADACC            R2=A(ACCOUNT KEY)                            
         USING ACTRECD,R2                                                       
         CLC   ACMPROD,ACTKUNT     TEST PRODUCTION                              
         BE    GETOFAX             YES-CANNOT HAVE OFARECS                      
         CLC   =C'1J',ACTKUNT      TEST PROJECT CONTROL                         
         BE    GETOFAX             YES-ALSO CANNOT HAVE OFARECS                 
                                                                                
GETOFA02 L     R3,ACMABUF                                                       
         USING OFARECD,R3                                                       
         MVC   OFAKEY,ACTKEY       INITIALIZE OFFICE-ACCOUNT KEY                
                                                                                
GETOFA04 SR    R1,R1                                                            
         IC    R1,OFAKOFF+1        INCREMENT THE OFFICE CODE                    
         AHI   R1,1                TO BUMP TO NEXT OFAREC                       
         STC   R1,OFAKOFF+1                                                     
         MVC   ACMTRACE,0(R3)      SET TRACE KEY                                
                                                                                
         GOTOR DATAMGR,DMCB,DMRDHI,ACMACFIL,(R3),(R3)                           
         BE    GETOFA06                                                         
         TM    8(R1),X'80'         TEST FOR EOF                                 
         BO    GETOFA10            YES                                          
         DC    H'0'                                                             
                                                                                
GETOFA06 TM    OFARSTAT,ACTSDELT   TEST FOR DELETED RECORD                      
         BO    GETOFA04            YES                                          
         CLI   RCTRACE,YES         TEST FOR TRACE OPTION                        
         BNE   *+8                                                              
         GOTOR DMTRACE             YES                                          
         CLC   ACTKEY(ACTKEND),OFAKEY TEST SAME ACCOUNT                         
         BNE   GETOFA10            NO-DONE                                      
                                                                                
GETOFA08 L     R0,ACMAOFA          R0=A(OFFICE-ACCOUNT RECORD BUFFER)           
         LHI   R1,ACMMAXRL         R1=MAXIMUM RECORD LENGTH                     
         LA    RE,OFARECD                                                       
         SR    RF,RF                                                            
         ICM   RF,3,OFARLEN                                                     
         MVCL  R0,RE               MOVE RECORD TO ITS BUFFER                    
                                                                                
         MVI   ACMFLAG,ACMFLOA     CHECK OFFICE SECURITY                        
         GOTOR OFFLMT                                                           
         BNE   GETOFA04                                                         
                                                                                
         MVI   MODE,PROCOFA                                                     
         GOTOR GO                                                               
         B     GETOFA04                                                         
                                                                                
GETOFA10 MVC   OFAKEY,ACTKEY       RESTORE ACCOUNT KEY                          
         GOTOR DATAMGR,DMCB,DMREAD,ACMACFIL,(R3),(R3)                           
         BE    *+6                                                              
         DC    H'0'                                                             
                                                                                
         MVI   MODE,OFALAST        LAST TIME HOOK FOR ACCOUNT                   
         GOTOR GO                                                               
                                                                                
GETOFAX  DS    0H                                                               
         DROP  R2,R3                                                            
         EJECT                                                                  
***********************************************************************         
* CONTROL READING OF TRANSACTIONS AND HISTORIES FOR AN ACCOUNT        *         
***********************************************************************         
                                                                                
GETTRN   XC    ACMTSACT,ACMTSACT   CLEAR NUMBER OF ENTRIES IN STACK             
         CLI   FCRDTIME,YES        TEST APPLICATION WANTS TIME RECORDS          
         BE    GETTRN02                                                         
         CLI   FCRDTRNS,YES        TEST APPLICATION WANTS TRANSACTIONS          
         BE    GETTRN02                                                         
         CLI   FCRDHIST,YES        TEST APPLICATION WANTS HISTORIES             
         BNE   GETTRN76                                                         
                                                                                
GETTRN02 L     RF,ACMACAC                                                       
         AH    RF,DATADISP                                                      
         ST    RF,ADSUBAC          SET ADDRESSES OF FIRST ELEMENTS              
         L     RF,ACMATRN                                                       
         AH    RF,DATADISP                                                      
         ST    RF,ADTRANS                                                       
         L     RF,ADACC                                                         
         MVC   ACMCURR,0(RF)       SAVE ACCOUNT CODE                            
         CLI   ACMATYP,ACMATPL     TEST PROFIT/LOSS LEDGER                      
         BNE   *+10                YES-LET FLTTRN DEVELOP BALANCE BF            
         ZAP   ACMOBAL,ACMPZERO    CLEAR OFFICE BALANCE                         
         CLI   FCSEQ,FCSEQOFF      TEST READING FILE IN OFFICE SEQUENCE         
         BE    *+10                YES                                          
         ZAP   ACMOBAL,ACMPZERO    CLEAR OFFICE BALANCE                         
         XC    ACMOFCAC,ACMOFCAC   CLEAR CONTRA-ACCOUNT CODE                    
         NI    ACMHIST,FF-ACMHFST  TURNOFF FIRST                                
                                                                                
* FOR NEW FILE/NEW OFFICE COMPANY, USE A DIFFERENT ROUTINE TO                   
* READ RECORDS BY CONTRA-ACCOUNT FOR NON-PRODUCTION LEDGERS                     
                                                                                
         CLI   FCRDMOSP,YES        CURRENT TRANSACTIONS                         
         BNE   *+12                                                             
         GOTOR MOSTRN                                                           
         J     EXIT                                                             
         CLI   FCSEQ,FCSEQNEW      NEW FILE SEQUENCE REQUESTED                  
         BE    GETTRN04                                                         
         TM    ACMINDS,ACMIEMUD+ACMINEWO  TEST NEW FILE/NEW OFFICES             
         BNO   GETTRN04            NO                                           
         CLC   ACMPROD,ACTKUNT-ACTKEY(RF)  TEST PRODUCTION                      
         BE    GETTRN04            YES                                          
         CLC   =C'1J',ACTKUNT-ACTKEY(RF)   TEST PROJECT CONTROL                 
         BE    GETTRN04            YES                                          
         CLI   FCSEQ,FCSEQOFF      TEST FOR OFFICE SEQUENCE READ                
         BNE   *+12                YES                                          
         GOTOR OFFTRN                                                           
         J     EXIT                                                             
         CLI   FCGENBUK,NO         TEST IF GENERATING BUCKETS                   
         BNE   *+12                                                             
         GOTOR NEWTRN                                                           
         J     EXIT                                                             
         GOTOR NEWFIL              YES-READ NEW FILE                            
         J     EXIT                                                             
                                                                                
GETTRN04 MVI   ACMFMACC,ACCFRST    GET READY FOR FIRST                          
         MVI   ACMLMACC,ACCLAST    EXPECTS ACCLAST ALL THE TIME                 
         MVC   ACMWTYPS,ACMINDS6   EXTRACT WORK-CODE TYPE FILTER BITS           
         NI    ACMWTYPS,ACMITALL                                                
                                                                                
         USING TRNRECD,R3                                                       
GETTRN06 L     R3,ACMABUF                                                       
         L     RF,ADACC                                                         
         MVC   TRNKEY,0(RF)                                                     
         XC    ACMWRKC,ACMWRKC     LEDGER MAY REQUIRE ANALYSIS MODES            
         XC    ACMLCAC,ACMLCAC                                                  
         TM    ACMWTYPS,ACMITALL                                                
         BZ    GETTRN10                                                         
         GOTOR SETWCT              SET FIRST/NEXT WORK-CODE TYPE FILTER         
         BNE   GETTRN76                                                         
         MVC   TRNKWORK,ACMLOWC                                                 
         MVI   ACMFMWCT,WCTFRST    SET FIRST FOR WORK-CODE TYPE                 
                                                                                
GETTRN08 MVC   ACMTRACE,0(R3)                                                   
         L     R3,ACMABUF                                                       
         ST    R3,ACMALTN                                                       
         GOTOR ACMISDDS,DMCB,ISREAD,(R3),0,ACMADDIR,(R3),0                      
         OC    8(2,R1),8(R1)                                                    
         BZ    GETTRN12            NO ERRORS                                    
         TM    9(R1),X'04'         TEST FOR EOF                                 
         BO    GETTRN76            YES                                          
         DC    H'0'                                                             
                                                                                
GETTRN10 MVC   ACMTRACE,0(R3)                                                   
         L     R3,ACMABUF                                                       
         ST    R3,ACMALTN                                                       
         TM    ACMINDS,ACMIEMUD    TEST ACCOUNT FILE EMULATED                   
         BZ    GETTRN28                                                         
         GOTOR ACMISDDS,DMCB,ISRDSEQ,(R3),0,ACMADDIR,(R3),0                     
         ORG   *-2                                                              
         TM    ACMHIST,ACMHTRN     TEST HISTORIES TO BE INCLUDED                
         BZ    *+8                                                              
         L     RF,AHSTTRN          GET HISTORICAL TRANSACTIONS                  
         BASR  RE,RF                                                            
         OC    8(2,R1),8(R1)                                                    
         BZ    GETTRN12            NO ERRORS                                    
         TM    9(R1),X'04'         TEST FOR EOF                                 
         BO    GETTRN76            YES                                          
         DC    H'0'                                                             
                                                                                
GETTRN12 TM    ACMHIST,ACMHPRC     TEST PROCESS HISTORY                         
         BNO   GETTRN14                                                         
         L     R3,ACMHIO           R3=HISTORY RECORD                            
         B     GETTRN32                                                         
                                                                                
GETTRN14 TM    TRNKSTAT,TRNSDELT   TEST FOR DELETED RECORD                      
         BO    GETTRN10            YES-SKIP IT                                  
         CLI   RCTRACE,YES                                                      
         BNE   *+8                                                              
         GOTOR DMTRACE                                                          
         CLC   ACMCURR,TRNKCULA    TEST SAME ACCOUNT                            
         BNE   GETTRN76                                                         
                                                                                
         CLC   TRNKCULC,SPACES     TEST OFFICE-ACCOUNT RECORD                   
         BE    GETTRN22                                                         
                                                                                
         USING CHDKEY,R3                                                        
         CLC   CHDKSPCS,SPACES     TEST CAC RECORD                              
         BNE   GETTRN16            MUST BE TRANACTION                           
         MVC   ACMCACDA,CHDKDA     YES - SAVE DISK ADDRESS                      
         TM    ACMCFIND,ACMCFACT+ACMCFOFF+ACMCCFLT                              
         BZ    GETTRN22            NO                                           
         GOTOR FLTCON                                                           
         BNE   GETTRN14                                                         
         B     GETTRN22                                                         
                                                                                
         USING TRNRECD,R3                                                       
GETTRN16 TM    ACMINDS,ACMINEWO    TEST NEW OFFICES                             
         BZ    GETTRN18                                                         
         CLI   FCRDTIME,YES        TEST APPLICATION WANTS TIME RECORDS          
         BE    GETTRN18                                                         
         CLI   FCRDTRNS,YES        TEST APPLICATION WANTS TRANSACTIONS          
         BE    GETTRN18                                                         
         LLC   RF,TRNKDATE-1       GET NEXT OFFICE                              
         AHI   RF,1                                                             
         STC   RF,TRNKDATE-1                                                    
         XC    TRNKDATE(TRNKSTA-TRNKDATE),TRNKDATE                              
         B     GETTRN08                                                         
                                                                                
GETTRN18 GOTOR FLTKEY                                                           
         BNE   GETTRN10            GET THE NEXT TRANSACTION                     
                                                                                
         L     R3,ACMATRN                                                       
         SHI   R3,ADJLEN                                                        
         MVC   TRNKSTA,ACMTKSTA    SET TRANSACTION STATUS                       
         MVC   TRNKDA,ACMTRNDA     SET D/A OF TRANSACTION RECORD                
         GOTOR DALINK              READ TRANSACTION RECORD                      
         MVC   WORK(TRNRSMOS-TRNRECD),TRNRECD                                   
         AHI   R3,ADJLEN                                                        
         MVC   TRNRECD(TRNRSTA2-TRNRECD),WORK                                   
         XC    TRNRECD+ACCOUSED(ACCOULEN+ACCOPLEN),TRNRECD+ACCOUSED             
         SR    RE,RE                                                            
         ICM   RE,3,TRNRLEN        ADJUST RECORD LENGTH                         
         SHI   RE,ADJLEN                                                        
         STCM  RE,3,TRNRLEN                                                     
         L     R3,ACMABUF                                                       
                                                                                
         CLI   ACMREC,ACMRTRNS     TEST TRANSACTION RECORD                      
         BNE   GETTRN62                                                         
         TM    WORK+(TRNRSTA2-TRNRECD),TRNSUSED+TRNSPEEL                        
         BZ    GETTRN62                                                         
         L     RF,ACMATRN          SET OLD FILE USED AND PEELED DATES           
         LA    RE,ACCORFST(RF)                                                  
         SR    R0,R0                                                            
         USING TRSELD,RE                                                        
GETTRN20 IC    R0,TRSLN            LOCATE TRANSACTION STATUS ELEMENT            
         AR    RE,R0                                                            
         CLI   TRSEL,0                                                          
         BE    GETTRN62                                                         
         CLI   TRSEL,TRSELQ                                                     
         BNE   GETTRN20                                                         
         MVC   ACCOUSED(ACCOULEN+ACCOPLEN,RF),TRSUDAT                           
         B     GETTRN62                                                         
         DROP  RE                                                               
                                                                                
GETTRN22 GOTOR DALINK                                                           
*&&UK                                                                           
         TM    ACMINDS5,ACMI2NDC   TEST SECOND CURRENCY REQUIRED                
         BZ    GETTRN24                                                         
         CLI   CACRFST-CACRECD(R3),CACELQ                                       
         BNE   GETTRN24                                                         
         LA    R1,CACRFST-CACRECD(R3)                                           
         USING OCAELD,R1                                                        
         SR    RF,RF                                                            
         IC    RF,OCALN            BUMP TO EOR                                  
         AR    R1,RF                                                            
         CLI   OCAEL,0             TEST EOR                                     
         BNE   *-10                                                             
         SR    R1,RF               BACK TO PREVIOUS ELEMENT                     
         CLI   OCAEL,OCAELQ        TEST OCAEL                                   
         BNE   GETTRN24                                                         
         CLI   OCALN,OCALN1Q       OF MINIMUM SIZE                              
         BNE   GETTRN24                                                         
         MVI   OCAEL,0             SET EOR TO LOSE OCAEL                        
         DROP  R1                                                               
         ICM   RF,3,CACRLEN-CACRECD(R3)                                         
         SHI   RF,OCALN1Q          CORRECT RECORD LENGTH                        
         STCM  RF,3,CACRLEN-CACRECD(R3)                                         
*&&                                                                             
GETTRN24 LA    R2,DMCB             R2=A(DATAMGR PARMS)                          
         ICM   R2,8,ACMEFFS                                                     
         GOTOR VACCEMU,DMCB,DMNEWO,,(R3),(R3)                                   
*&&UK                                                                           
         TM    ACMINDS5,ACMI2NDC   TEST SECOND CURRENCY REQUIRED                
         BZ    GETTRN26                                                         
         CLI   ACCORFST(R3),CACELQ TEST EMULATED OLD BUCKET RECORD              
         BNE   GETTRN26                                                         
         L     RE,ADCMPEL          CALL TOBACCO                                 
         MVC   WORK(L'CPYCURR),CPYCURR-CPYELD(RE)                               
         MVC   WORK+L'CPYCURR(L'CPYCURRS),CPYCURRS-CPYELD(RE)                   
         LA    R0,TOBAAOUT                                                      
         TM    CPYSTAT7-CPYELD(RE),CPYSSCNV TEST CONVERSION RUN                 
         BZ    *+8                                                              
         LA    R0,TOBAACVS                                                      
         GOTOR VTOBACCO,DMCB,((R0),WORK),('TOBAIOLD',(R3)),ADCOMFAC,0           
*&&                                                                             
GETTRN26 B     GETTRN34                                                         
                                                                                
GETTRN28 L     R3,ACMABUF                                                       
         GOTOR ACMISDDS,DMCB,ISRDSEQ,(R3),0,ADACCFIL,(R3),0                     
         ORG   *-2                                                              
         TM    ACMHIST,ACMHTRN     TEST HISTORIES TO BE INCLUDED                
         BZ    *+8                                                              
         L     RF,AHSTTRN          GET HISTORICAL TRANSACTIONS                  
         BASR  RE,RF                                                            
         OC    8(2,R1),8(R1)       TEST FOR ERRORS                              
         BZ    GETTRN30                                                         
         TM    9(R1),X'04'         TEST EOF                                     
         BO    GETTRN76                                                         
         DC    H'0'                I/O ERROR                                    
                                                                                
GETTRN30 TM    ACMHIST,ACMHPRC     TEST PROCESS HISTORY                         
         BZ    GETTRN32                                                         
         L     R3,ACMHIO           R3=HISTORY RECORD                            
                                                                                
GETTRN32 TM    TRNRSTAT,TRNSDELT   DELETED  -  SKIP IT                          
         BO    GETTRN10                                                         
         CLI   RCTRACE,YES                                                      
         BNE   *+8                                                              
         GOTOR DMTRACE             TRACE I/O AND MODES                          
         CLC   ACMCURR,0(R3)                                                    
         BNE   GETTRN76            SAME ACCOUNT                                 
                                                                                
GETTRN34 TM    ACMINDS,ACMIEMUD+ACMINEWO INTERCEPT OFFICE/ACC RECORDS           
         BNO   GETTRN36            MUST BE ON NEW FILE/NEW OFFICES              
         CLC   TRNKOFF,SPACES      TEST FOR AN OFFICE                           
         BE    GETTRN36            NO                                           
         CLC   OFAKEND(L'OFAKEY-OFAKEND,R3),SPACES  REST OF KEY=SPACES          
         BE    GETTRN10            GET NEXT RECORD                              
                                                                                
GETTRN36 CLI   TRNRECD+ACCORFST,0  TEST ANY ELEMENTS ON RECORD                  
         BE    GETTRN10                                                         
         CLI   TRNRECD+ACCORFST,OCAELQ   IS THERE DUMMY OCAEL                   
         BNE   GETTRN38                  NO                                     
         USING OCAELD,R1                                                        
         LA    R1,TRNRECD+ACCORFST                                              
         CLI   OCALN,OCALN1Q             CHECK LENGTH IS 3 BYTES                
         BNE   GETTRN60                                                         
         CLI   OCAEL+OCALN1Q,0           CHECK THERE ARE NO FURTHER ELS         
         BE    GETTRN10                  NO FURTHER ELS GET NEXT RECORD         
         B     GETTRN60                  FURTHER ELS GO OFF AS BELOW            
         DROP  R1                                                               
GETTRN38 CLI   TRNRECD+ACCORFST,CACELQ                                          
         BNE   GETTRN60                                                         
         OC    ACMLMODE(ACMLMWCT-ACMLMODE),ACMLMODE                             
         BZ    GETTRN42            NO SAVE MODES                                
         LA    R1,ACMLMODE         SET FOR SBACLAST/ANALLAST                    
         LHI   R0,ACMLMWCT-ACMLMODE                                             
         OC    ACMWRKC,ACMWRKC                                                  
         BZ    *+14                                                             
         CLC   TRNKOFF,ACMWRKC                                                  
         BNE   GETTRN40            IF WORK CODE CB SBACLAST/ANALLAST            
         LHI   R0,1                                                             
         LA    R2,TRNRECD+ACCORFST                                              
         CLC   ACMLCAC,TRNKCULC                                                 
         BE    GETTRN42            SAME SUBACC NOTHING TO PASS                  
                                                                                
GETTRN40 XC    ACMLCAC,ACMLCAC                                                  
         GOTOR GOSAVE                                                           
                                                                                
GETTRN42 MVC   BUCKTYPE,TRNKREF    SAVE BUCKET TYPE                             
         CLI   TRNKUNT,C'1'        TEST UNIT 1                                  
         BNE   GETTRN44                                                         
         CLI   TRNKREF+1,C' '      IS THIS A NEW COST RECORD                    
         BNH   GETTRN44            NO, PROCESS RECORD                           
         CLI   ACMCAM,C'A'         PASS ALL METHODS                             
         BE    GETTRN44            YES                                          
         CLC   BUCKTYPE,ACMCAM     YES - MUST MATCH METHOD                      
         BNE   GETTRN10                                                         
                                                                                
GETTRN44 L     R0,ACMACAC                                                       
         SR    R1,R1                                                            
         ICM   R1,3,TRNRLEN                                                     
         LA    RE,TRNRECD                                                       
         LR    RF,R1                                                            
         MVCL  R0,RE               MOVE RECORD TO SUB-ACCOUNT BLOCK             
                                                                                
         CLC   ACMPROD,TRNKUNT     TEST PRODUCTION LIKE LEDGER                  
         BE    *+14                                                             
         CLC   =C'1J',TRNKUNT                                                   
         BNE   GETTRN49                                                         
         TM    ACMINDS4,ACMIWCFL   SINGLE WORK CODE FILTER                      
         BZ    GETTRN46                                                         
         SR    R1,R1                                                            
         MVC   ACMWRK(2),QWRKLIST                                               
         OI    ACMWRK,X'40'        TURN ON FOR EXCLUDE                          
         CLC   TRNKWORK,ACMWRK                                                  
         BNE   *+16                                                             
         TM    QWRKLIST,X'40'      SEE IF SET FOR INCLUDE                       
         BZ    GETTRN48            DON'T KEEP                                   
         B     GETTRN49            YES KEEP                                     
                                                                                
         TM    QWRKLIST,X'40'      SEE IF SET FOR EXCLUDE                       
         BO    GETTRN48            DON'T KEEP                                   
         B     GETTRN49            YES KEEP                                     
                                                                                
GETTRN46 TM    ACMINDS,ACMIWLST    TEST WORK CODE LIST PRESENT                  
         BZ    GETTRN49                                                         
         TM    ADLSTREC,X'80'      TEST FOR VALID LIST RECORD                   
         BNZ   GETTRN49                                                         
         GOTOR ACMVALST,ACMPARA,ADLSTREC,TRNKOFF                                
         CLI   0(R1),0                                                          
         BNE   *+6                                                              
         DC    H'0'                DIE IF BAD LIST RECORD                       
         CLI   0(R1),C'E'          TEST EXCLUDE                                 
         BNE   GETTRN49                                                         
GETTRN48 IC    R1,TRNKOFF+1        YES - SET NEXT WORK CODE KEY                 
         AHI   R1,1                                                             
         STC   R1,TRNKOFF+1                                                     
         XC    TRNKCULC(TRNKSTA-TRNKCULC),TRNKCULC                              
         B     GETTRN72                                                         
                                                                                
         USING CHDKEY,R3                                                        
GETTRN49 L     R2,ADSUBAC          FOR PROCSUBAC - ADSUBAC                      
         MVC   ADSUBAC,ACMABUF     POINTS TO START OF RECORD                    
         MVI   MODE,PROCSBAC       GO TO APPLICATION                            
         CLC   CHDKOFF,SPACES                                                   
         BE    GETTRN51                                                         
         CLI   CHDKUNT,C'G'                                                     
         JE    GETTRN50                                                         
         CLI   CHDKUNT,C'S'        ONLY DO FOR UNIT S OR G                      
         JNE   GETTRN51                                                         
         CLI   CHDKLDG,C'J'        PRODUCTION?                                  
         JE    GETTRN51            NO OFFICE IT'S WORKCODE, SO SKIP             
                                                                                
GETTRN50 MVI   MODE,PROCSBOF       ACCT/OFF/CONTRA BUCKET                       
         MVI   ACMFLAG,ACMFLOA     PERFORM OFFICE SECURITY                      
         GOTOR OFFLMT                                                           
         BE    GETTRN51            THIS OFFICE IS OKAY                          
         ST    R2,ADSUBAC          RESTORE TO POINT TO 43 ELEM                  
         B     GETTRN16            NO-TRY NEXT OFFICE                           
         DROP  R3                                                               
                                                                                
         USING TRNRECD,R3                                                       
GETTRN51 GOTOR GO                                                               
         ST    R2,ADSUBAC          NOW ADSUBAC TO 43 ELEMENT                    
         CLC   ACMLCAC,TRNKCULC                                                 
         BE    *+8                                                              
         MVI   ACMFMSBA,SBACFRST   SAVE MODE TILL I GET TRANSACTION             
         MVC   ACMLCAC,TRNKCULC                                                 
         CLC   ACMPROD,TRNKUNT                                                  
         BE    *+14                PROD LEDGER ALWAYS HAS ANALYSIS              
         CLC   =C'1J',TRNKUNT      PROJECT CONTROL ALWAYS ANALYSIS              
         BNE   GETTRN54                                                         
         CLC   TRNKOFF,ACMWRKC                                                  
         BE    GETTRN54            AND NO CHANGE                                
         TM    ACMINDS6,ACMITALL   TEST WORK-CODE TYPE FILTER ACTIVE            
         BZ    GETTRN52                                                         
         GOTOR NXTWCT,TRNKWORK     SET & TEST WORK-CODE TYPE                    
         BE    GETTRN52                                                         
         BH    GETTRN76                                                         
         MVC   TRNKWORK,ACMNXWC    SET NEXT WORK-CODE TO PROCESS                
         XC    TRNKCULC(TRNKEND-(TRNKCULC-TRNRECD)),TRNKCULC                    
         B     GETTRN08                                                         
                                                                                
GETTRN52 MVC   ACMWRKC,TRNKOFF     SAVE NEW CODE                                
         MVI   ACMFMANA,ANALFRST   AND MODE                                     
         MVI   ACMFMSBA,SBACFRST   IF ANALFRST MUST HAVE SBACCFRST              
                                                                                
GETTRN54 LA    R2,TRNRECD+ACCORFST                                              
         USING BUKELD,R2                                                        
GETTRN56 SR    R1,R1                                                            
         IC    R1,BUKLN            BUMP OVER 43 ELEMENT                         
         AR    R2,R1                                                            
         CLI   BUKEL,0                                                          
         BE    GETTRN10            END OF RECORD                                
         CLI   BUKEL,PBKELQ        TEST PRIOR BUCKET ELEMENT                    
         BE    *+8                                                              
         CLI   BUKEL,BUKELQ                                                     
         BNE   GETTRN56                                                         
         CLI   FCRDHIST,YES        DO WE NEED HISTORIES ?                       
         BNE   GETTRN10            NO, SKIP THE RECORD                          
         CLI   ACMATYP,ACMATPL     DO ONLY FOR PROFIT/LOSS TYPE                 
         BNE   GETTRN58            NO, SKIP OVER ACCTBAL UPDATE                 
                                                                                
         CLI   BUCKTYPE,C' '                                                    
         BNE   GETTRN58            DON'T ADD SPECIAL BUCKETS TO BALANCE         
         CLI   FCRDTRNS,YES        READING TRANSACTIONS ?                       
         BE    GETTRN58            YES - THEN DONT BUILD FROM BUCKETS           
         TM    ACMINDS,ACMINEWO    TEST NEW OFFICE IN USE                       
         BO    GETTRN58            CAN'T GET BALANCE FRWD FROM BUCKETS          
         CLC   BUKMOS,ACMFDTE      DROP IT IF BEFORE FISCAL START               
         BL    GETTRN58                                                         
         CLC   BUKMOS,ACMMSTR      OR IF NOT LOWER THAN M.O.S. START            
         BNL   GETTRN58                                                         
         CLI   BUKEL,PBKELQ                                                     
         BNE   *+20                                                             
         AP    ACMABAL,BUKEL+(PBKDR-PBKELD)(L'PBKDR)                            
         SP    ACMABAL,BUKEL+(PBKCR-PBKELD)(L'PBKCR)                            
         B     GETTRN56                                                         
         AP    ACMABAL,BUKDR       OTHERWISE, SUM THE DEBITS                    
         SP    ACMABAL,BUKCR       AND THE CREDITS                              
         B     GETTRN56                                                         
                                                                                
GETTRN58 CLI   FCRDHIST,YES                                                     
         BNE   GETTRN56            DON'T PASS HISTORY USELESS THEY ASK          
         CLC   BUKMOS,ACMHSTR      ONLY PASS HISTORY WITHIN                     
         BL    GETTRN56            START AND END DATES                          
         CLC   BUKMOS,ACMHEND                                                   
         BH    GETTRN56                                                         
         MVC   ACMSVATN,ADTRANS    SAVE ADTRANS                                 
         ST    R2,ADTRANS          ADTRANS MUST POINT TO 45                     
         MVI   ACMFMDET,PROCHIST                                                
         GOTOR TRNMODE             PASS SAVED UP MODES                          
         MVC   ADTRANS,ACMSVATN    RESTORE ADTRANS                              
         B     GETTRN56                                                         
                                                                                
GETTRN60 LA    R2,TRNRECD+ACCORFST FIRST ELEMENT ON OLD FILE                    
         B     *+8                                                              
GETTRN62 L     R2,ADTRANS                                                       
         USING TRNELD,R2                                                        
         CLI   TRNEL,TRNELQ        TEST FOR TRANSACTION RECORD                  
         BNE   GETTRN90                                                         
         CLI   FCRDTRNS,YES                                                     
         BE    GETTRN63                                                         
         CLI   FCRDTIME,YES                                                     
         BE    GETTRN10                                                         
         TM    ACMINDS,ACMINEWO    NEW OFFICE                                   
         BZ    GETTRN70            NO                                           
         LLC   RF,TRNKDATE-1       LAST CHAR OF CONTRA                          
         AHI   RF,1                                                             
         STC   RF,TRNKDATE-1                                                    
         XC    TRNKDATE(TRNKSTA-TRNKDATE),TRNKDATE                              
         B     GETTRN08                                                         
                                                                                
GETTRN63 TM    ACMINDS,ACMIEMUD    TEST EMULATED FILE                           
         BNZ   GETTRN64                                                         
         CLC   TRNDATE,ACMTSTR     PASS ONLY TRANSACTION WITHIN                 
         BL    GETTRN10            START AND END DATES                          
         CLC   TRNDATE,ACMTEND                                                  
         BH    GETTRN10                                                         
         L     R0,ACMATRN                                                       
         SR    R1,R1                                                            
         ICM   R1,3,TRNRLEN                                                     
         LA    RE,TRNRECD                                                       
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
                                                                                
GETTRN64 GOTOR FLTTRN              FILTER IT                                    
         BNE   GETTRN10            DON'T WANT, SO GET NEXT                      
         TM    ACMINDS6,ACMITTIM+ACMITCST TEST TIME/COST W/C TYPES              
         BZ    GETTRN68                                                         
         CLC   =C'**',TRNKWORK     TEST ORDER TRANSACTION RECORD                
         BNE   GETTRN68                                                         
         LA    R1,TRNELD                                                        
         SR    R0,R0                                                            
         USING OAMELD,R1           LOCATE ORDER AMOUNT ELEMENT                  
GETTRN66 IC    R0,OAMLN                                                         
         AR    R1,R0                                                            
         CLI   OAMEL,0                                                          
         BE    GETTRN68                                                         
         CLI   OAMEL,OAMELQ                                                     
         BNE   GETTRN66                                                         
         GOTOR GETWCT,OAMWORK                                                   
         TM    ACMINDS6,ACMITTIM+ACMITCST TEST TIME AND COST REQUIRED           
         BO    GETTRN68                                                         
         CLI   ACMWCTYP,ACMWTCST   TEST COST WORK CODE                          
         BNE   *+16                                                             
         TM    ACMINDS6,ACMITCST   TEST COST WORK-CODES REQUIRED                
         BZ    GETTRN10                                                         
         B     GETTRN68                                                         
         CLI   ACMWCTYP,ACMWTTIM   TEST TIME WORK CODE                          
         BE    *+6                                                              
         DC    H'0'                                                             
         TM    ACMINDS6,ACMITTIM   TEST TIME WORK-CODES REQUIRED                
         BZ    GETTRN10                                                         
         DROP  R1                                                               
                                                                                
GETTRN68 MVI   ACMFMDET,PROCTRNS                                                
         GOTOR TRNMODE             PASS IT TO APPLICATION                       
         B     GETTRN10            GET NEXT                                     
                                                                                
GETTRN70 CLI   TRNKDATE,X'40'                                                   
         BL    GETTRN10            BAD TRANSACTION DATE GET NEXT                
         XC    TRNKDATE(TRNKSTA-TRNKDATE),TRNKDATE                              
         MVI   TRNKDATE,X'FF'      OR HIGH FOR HISTORY                          
                                                                                
GETTRN72 MVC   ACMTRACE,0(R3)                                                   
         ST    R3,ACMALTN                                                       
         TM    ACMINDS,ACMIEMUD    TEST ACCOUNT FILE EMULATED                   
         BZ    GETTRN74                                                         
                                                                                
         GOTOR ACMISDDS,DMCB,ISREAD,(R3),0,ACMADDIR,(R3),0                      
         OC    8(2,R1),8(R1)                                                    
         BZ    GETTRN14                                                         
         TM    9(R1),X'04'         TEST FOR EOF                                 
         BO    GETTRN76                                                         
         DC    H'0'                                                             
                                                                                
GETTRN74 GOTOR ACMISDDS,DMCB,ISREAD,(R3),0,ADACCFIL,(R3),0                      
         OC    8(2,R1),8(R1)       TEST FOR ERRORS                              
         BZ    GETTRN32                                                         
         TM    9(R1),X'04'         TEST E-O-F                                   
         BO    GETTRN76                                                         
         DC    H'0'                I/O ERROR                                    
                                                                                
GETTRN76 TM    ACMINDS6,ACMITALL   TEST WORK-CODE TYPE FILTER PRESENT           
         BZ    GETTRN78                                                         
         LA    R1,ACMLMODE         PASS SAVED LAST TIME MODES                   
         LHI   R0,ACMLMAOF-ACMLMODE                                             
         GOTOR GOSAVE                                                           
         TM    ACMWTYPS,ACMITALL   TEST ANY WORK-CODE TYPE FILTERS LEFT         
         BNZ   GETTRN06                                                         
                                                                                
GETTRN78 LA    R1,ACMLMODE                                                      
         LHI   R0,ACMLMOFA-ACMLMODE                                             
         OC    0(ACMLMOFA-ACMLMODE,R1),0(R1)                                    
         BZ    *+8                                                              
         GOTOR GOSAVE              PASS SBACLAST/ANALLAST/ACCLAST               
                                                                                
         CLI   MODE,ACCLAST        TEST MODE OF ACCLAST JUST PASSED             
         BNE   GETTRN98            (CAN BE RESET BY THE APPLICATION)            
         CLI   FCTSTACK,YES        TEST TRANSACTION STACK REQUIRED              
         BNE   GETTRN98                                                         
         OC    ACMTSACT,ACMTSACT   TEST ANYTHING IN TRANSACTION STACK           
         BZ    GETTRN98                                                         
         XC    ACMOFCAC,ACMOFCAC   CLEAR CONTRA-ACCOUNT CODE                    
                                                                                
         L     R4,ACMTSADR         R4=A(D/A STACK)                              
                                                                                
GETTRN80 MVC   TRNKDA,0(R4)        SET DISK ADDRESS OF RECORD                   
         MVI   TRNKSTAT,0                                                       
         GOTOR DALINK              GET D/A RECORD INTO BUFFER                   
*&&UK                                                                           
         TM    ACMINDS5,ACMI2NDC   TEST SECOND CURRENCY REQUIRED                
         BZ    GETTRN82                                                         
         CLI   CACRFST-CACRECD(R3),CACELQ                                       
         BNE   GETTRN82                                                         
         LA    R1,CACRFST-CACRECD(R3)                                           
         USING OCAELD,R1                                                        
         SR    RF,RF                                                            
         IC    RF,OCALN            BUMP TO EOR                                  
         AR    R1,RF                                                            
         CLI   OCAEL,0             TEST EOR                                     
         BNE   *-10                                                             
         SR    R1,RF               BACK TO PREVIOUS ELEMENT                     
         CLI   OCAEL,OCAELQ        TEST OCAEL                                   
         BNE   GETTRN82                                                         
         CLI   OCALN,OCALN1Q       OF MINIMUM SIZE                              
         BNE   GETTRN82                                                         
         MVI   OCAEL,0             SET EOR TO LOSE OCAEL                        
         DROP  R1                                                               
         ICM   RF,3,CACRLEN-CACRECD(R3)                                         
         SHI   RF,OCALN1Q          CORRECT RECORD LENGTH                        
         STCM  RF,3,CACRLEN-CACRECD(R3)                                         
*&&                                                                             
GETTRN82 LA    R2,DMCB             CONVERT TO OLD FILE FORMAT                   
         ICM   R2,8,ACMEFFS                                                     
         GOTOR VACCEMU,DMCB,DMNEWO,,(R3),(R3)                                   
*&&UK                                                                           
         TM    ACMINDS5,ACMI2NDC   TEST SECOND CURRENCY REQUIRED                
         BZ    GETTRN84                                                         
         CLI   ACCORFST(R3),CACELQ TEST EMULATED OLD BUCKET RECORD              
         BNE   GETTRN84                                                         
         L     RE,ADCMPEL          CALL TOBACCO WITH MERGED CHD/CACREC          
         MVC   WORK(L'CPYCURR),CPYCURR-CPYELD(RE)                               
         MVC   WORK+L'CPYCURR(L'CPYCURRS),CPYCURRS-CPYELD(RE)                   
         LA    R0,TOBAAOUT                                                      
         TM    CPYSTAT7-CPYELD(RE),CPYSSCNV TEST CONVERSION RUN                 
         BZ    *+8                                                              
         LA    R0,TOBAACVS                                                      
         GOTOR VTOBACCO,DMCB,((R0),WORK),('TOBAIOLD',(R3)),ADCOMFAC,0           
*&&                                                                             
         USING CHDRECD,R3                                                       
GETTRN84 CLC   CHDKSPCS,SPACES     TEST CONTRA-HEADER                           
         BNE   GETTRN86                                                         
         OC    ACMOFCAC,ACMOFCAC   TEST FIRST CONTRA-ACCOUNT                    
         BZ    *+12                                                             
         MVI   MODE,SCACLAST       GIVE CONTRA LAST FOR PREVIOUS                
         GOTOR GO                                                               
                                                                                
         MVC   ACMOFCAC,CHDKOFF                                                 
         L     R0,ACMACAC                                                       
         SR    R1,R1                                                            
         ICM   R1,3,CHDRLEN                                                     
         LA    RE,CHDRECD                                                       
         LR    RF,R1                                                            
         MVCL  R0,RE               MOVE CONTRA-HEADER TO BUFFER                 
         MVI   MODE,SCACFRST                                                    
         GOTOR GO                                                               
         B     GETTRN88                                                         
                                                                                
         USING TRNRECD,R3                                                       
GETTRN86 L     R0,ACMATRN                                                       
         SR    R1,R1                                                            
         ICM   R1,3,TRNRLEN                                                     
         LA    RE,TRNRECD                                                       
         LR    RF,R1                                                            
         MVCL  R0,RE               MOVE TRANSACTION TO BUFFER                   
         MVI   MODE,SPRCTRNS                                                    
         GOTOR GO                                                               
                                                                                
GETTRN88 AHI   R4,L'ACMCACDA       BUMP TO NEXT DISK ADDRESS                    
         LH    R1,ACMTSACT         DECREMENT NUMBER OF ENTRIES                  
         SH    R1,ACMHONE                                                       
         STH   R1,ACMTSACT                                                      
         BP    GETTRN80                                                         
         MVI   MODE,SCACLAST       GIVE CONTRA LAST FOR PREVIOUS                
         GOTOR GO                                                               
         MVI   MODE,SACTLAST       GIVE ACCOUNT LAST                            
         GOTOR GO                                                               
         B     GETTRN98                                                         
         DROP  R2,R3                                                            
                                                                                
         USING TIMRECD,R3                                                       
         USING TIMELD,R2                                                        
GETTRN90 CLI   TIMEL,TIMELQ        TEST TIME RECORD                             
         BE    GETTRN92                                                         
         SR    R0,R0               BUMP TO NEXT ELEMENT                         
         IC    R0,TIMLN                                                         
         AR    R2,R0                                                            
         CLI   TIMEL,0             TEST EOR                                     
         BE    GETTRN70                                                         
         B     GETTRN90                                                         
                                                                                
GETTRN92 CLI   FCRDTIME,YES        TEST APPLICATION WANTS TIME RECORDS          
         BNE   GETTRN70                                                         
         MVC   ACMSVATN,ADTRANS    SAVE ADTRANS ADDRESS                         
                                                                                
GETTRN94 ST    R2,ADTRANS          SET A(TIME ELEMENT) FOR APPLICATION          
         GOTOR FLTTIM              FILTER TIME ELEMENT                          
         BNE   GETTRN96                                                         
         MVI   ACMFMDET,PROCTIME                                                
         GOTOR TRNMODE                                                          
                                                                                
GETTRN96 SR    R0,R0               BUMP TO NEXT ELEMENT                         
         IC    R0,TIMLN                                                         
         AR    R2,R0                                                            
         CLI   TIMEL,TIMELQ        TEST TIME ELEMENT                            
         BE    GETTRN94                                                         
         CLI   TIMEL,0             TEST END OF RECORD                           
         BNE   GETTRN96                                                         
         MVC   ADTRANS,ACMSVATN    RESTORE ADTRANS VALUE                        
         B     GETTRN10                                                         
                                                                                
GETTRN98 L     R1,ACMACAC          CLEAR SAVE AREAS TO BINARY ZEROES            
         XC    0(256,R1),0(R1)                                                  
         L     R1,ACMATRN                                                       
         XC    0(256,R1),0(R1)                                                  
         L     R1,ACMAANL                                                       
         XC    0(256,R1),0(R1)                                                  
         L     R1,ACMABUF                                                       
         XC    0(256,R1),0(R1)                                                  
                                                                                
GETTRNX  J     EXIT                                                             
         DROP  RB                                                               
         EJECT                                                                  
***********************************************************************         
* READ HISTORIES AND TRANSACTIONS FOR AN ACCOUNT FOR A NEW FILE/NEW   *         
* NEW OFFICES COMPANY.  ROUTINE WILL READ THE FILE BY CONTRA-ACCOUNT, *         
* RETURNING TRANSACTIONS BY CONTRA-ACCOUNT/OFFICE/DATE/REFERENCE.     *         
***********************************************************************         
                                                                                
NEWTRN   NTR1  BASE=*,LABEL=*                                                   
         MVI   ACMFMACC,ACCFRST    SET PENDING ACCFRST/ACCLAST MODES            
         MVI   ACMLMACC,ACCLAST                                                 
         MVC   ACMLCAC,SPACES      INITIALIZE LAST CONTRA-ACCOUNT               
         GOTOR BLDBUF              BUILD CONTRA/OFFICE BUFFER                   
         L     R3,ACMABUF                                                       
         USING CACRECD,R3                                                       
                                                                                
* READ THE NEXT CONTRA-ACCOUNT RECORD                                           
                                                                                
NEWTRN02 MVC   CACKEY,SPACES                                                    
         MVC   CACKCULA,ACMCURR                                                 
         MVC   CACKCULC,ACMLCAC                                                 
         MVI   CACKSPAC,X'FF'      BUMP TO NEXT CONTRA-ACCOUNT                  
                                                                                
NEWTRN04 MVC   ACMTRACE,0(R3)                                                   
         ST    R3,ACMALTN                                                       
         GOTOR ACMISDDS,DMCB,ISREAD,(R3),0,ACMADDIR,(R3),0                      
         OC    8(2,R1),8(R1)                                                    
         BZ    NEWTRN06            NO ERRORS                                    
         TM    9(R1),X'04'         TEST FOR EOF                                 
         BO    NEWTRN60            YES-PREPARE FOR NEXT ACCOUNT                 
         DC    H'0'                                                             
                                                                                
NEWTRN06 MVC   ACMLCAC,CACKCULC    EXTRACT NEXT CONTRA-ACCOUNT                  
         TM    CACKSTAT,ACTSDELT   TEST FOR DELETED RECORD                      
         BO    NEWTRN10            YES                                          
         CLI   RCTRACE,YES         TEST FOR TRACE OPTION                        
         BNE   NEWTRN10                                                         
         GOTOR DMTRACE                                                          
                                                                                
NEWTRN10 CLC   CACKCULA,ACMCURR    TEST SAME ACCOUNT                            
         BNE   NEWTRN60            NO-READY FOR NEXT ACCOUNT                    
         CLC   CACKOFF,SPACES      TEST OFFICE=SPACES                           
         BNE   NEWTRN60            NO-HAVE HIT ACC/OFFICE RECORDS               
         TM    CACKSTAT,ACTSDELT                                                
         BO    NEWTRN02            TRY NEXT CONTRA-ACCOUNT IF DELETED           
         TM    ACMCFIND,ACMCFACT+ACMCFOFF+ACMCCFLT                              
         BZ    NEWTRN12            NO                                           
         GOTOR FLTCON                                                           
         BNE   NEWTRN06                                                         
         MVC   ACMLCAC,CACKCULC    SAVE CONTRA-ACCOUNT INCASE CHANGED           
                                                                                
NEWTRN12 MVI   ACMFMSBA,SBACFRST   SET PENDING SBACFRST MODE                    
         MVC   ACMKEY(L'CACKEY),CACKEY EXTRACT CONTRA-ACCOUNT KEY               
         USING CACRECD,R3                                                       
         GOTOR DALINK                                                           
*&&UK                                                                           
         TM    ACMINDS5,ACMI2NDC   TEST SECOND CURRENCY REQUIRED                
         BZ    NEWTRN14                                                         
         CLI   CACRFST-CACRECD(R3),CACELQ                                       
         BNE   NEWTRN14                                                         
         LA    R1,CACRFST-CACRECD(R3)                                           
         USING OCAELD,R1           IF CONTRA HEADER                             
         SR    RF,RF                                                            
         IC    RF,OCALN            BUMP TO EOR                                  
         AR    R1,RF                                                            
         CLI   OCAEL,0             TEST EOR                                     
         BNE   *-10                                                             
         SR    R1,RF               BACK TO PREVIOUS ELEMENT                     
         CLI   OCAEL,OCAELQ        TEST OCAEL                                   
         BNE   NEWTRN14                                                         
         CLI   OCALN,OCALN1Q       OF MINIMUM SIZE                              
         BNE   NEWTRN14                                                         
         MVI   OCAEL,0             SET EOR TO LOSE OCAEL                        
         DROP  R1                                                               
         ICM   RF,3,CACRLEN-CACRECD(R3)                                         
         SHI   RF,OCALN1Q          CORRECT RECORD LENGTH                        
         STCM  RF,3,CACRLEN-CACRECD(R3)                                         
*&&                                                                             
NEWTRN14 LA    R2,DMCB                                                          
         ICM   R2,8,ACMEFFS                                                     
         GOTOR VACCEMU,DMCB,DMNEWO,,(R3),(R3)                                   
*&&UK                                                                           
         TM    ACMINDS5,ACMI2NDC   TEST SECOND CURRENCY REQUIRED                
         BZ    NEWTRN16                                                         
         CLI   ACCORFST(R3),CACELQ TEST EMULATED OLD BUCKET RECORD              
         BNE   NEWTRN16                                                         
         L     RE,ADCMPEL          CALL TOBACCO WITH MERGED CHD/CACREC          
         MVC   WORK(L'CPYCURR),CPYCURR-CPYELD(RE)                               
         MVC   WORK+L'CPYCURR(L'CPYCURRS),CPYCURRS-CPYELD(RE)                   
         LA    R0,TOBAAOUT                                                      
         TM    CPYSTAT7-CPYELD(RE),CPYSSCNV TEST CONVERSION RUN                 
         BZ    *+8                                                              
         LA    R0,TOBAACVS                                                      
         GOTOR VTOBACCO,DMCB,((R0),WORK),('TOBAIOLD',(R3)),ADCOMFAC,0           
*&&                                                                             
                                                                                
* PROCESS CONTRA-ACCOUNT BUCKET RECORD (PROCSBAC)                               
                                                                                
NEWTRN16 L     R0,ACMACAC                                                       
         LHI   R1,ACMMAXRL                                                      
         LA    RE,CACRECD                                                       
         SR    RF,RF                                                            
         ICM   RF,3,CACRLEN                                                     
         MVCL  R0,RE               SLOT RECORD                                  
                                                                                
         CLI   CACKBTYP+1,C' '     IS THIS A NEW COST RECORD                    
         BE    NEWTRN18            NO, OK TO PROCESS                            
         CLI   ACMCAM,C'A'         PASS ALL METHODS                             
         BE    NEWTRN18                                                         
         CLC   ACMCAM,CACKBTYP     MUST MATCH METHOD                            
         BNE   NEWTRN26                                                         
                                                                                
NEWTRN18 MVC   BUCKTYPE,CACKBTYP   EXTRACT BUCKET TYPE                          
         L     R2,ADSUBAC          SAVE SUB-ACCOUNT ELEM POINTER                
         MVC   ADSUBAC,ACMABUF     GIVE CALLER C/A KEY                          
         MVI   MODE,PROCSBAC                                                    
         GOTOR GO                                                               
         ST    R2,ADSUBAC          RESTORE ELEMENT POINTER                      
                                                                                
* PASS APPLICATION BUCKETS (PROCHIST)                                           
                                                                                
NEWTRN20 CLI   FCRDHIST,YES        TEST APPLICATION WANTS HISTORY               
         BNE   NEWTRN26            NO                                           
                                                                                
         LA    R2,CACRECD                                                       
         AH    R2,DATADISP                                                      
         USING BUKELD,R2                                                        
                                                                                
NEWTRN22 SR    R0,R0                                                            
         IC    R0,BUKLN                                                         
         AR    R2,R0                                                            
         CLI   BUKEL,0             TEST FOR EOR                                 
         BE    NEWTRN26            YES                                          
         CLI   BUKEL,PBKELQ        TEST PRIOR BUCKET ELEMENT                    
         BE    *+8                                                              
         CLI   BUKEL,BUKELQ        TEST FOR BUCKET ELEMENT                      
         BNE   NEWTRN22            NO                                           
                                                                                
NEWTRN24 CLC   BUKMOS,ACMHSTR                                                   
         BL    NEWTRN22                                                         
         CLC   BUKMOS,ACMHEND                                                   
         BH    NEWTRN22                                                         
         MVC   ACMSVATN,ADTRANS    SAVE TRANSACTION ELEMENT POINTER             
         ST    R2,ADTRANS          GIVE APPLICATION A(BUCKET ELEMENT)           
         MVI   ACMFMDET,PROCHIST                                                
         GOTOR TRNMODE                                                          
         MVC   ADTRANS,ACMSVATN    RESTORE TRANSACTION ELEM POINTER             
         B     NEWTRN22            NEXT BUCKET                                  
                                                                                
* READ FOR ADDITIONAL BUCKET RECORDS FOR THE CONTRA-ACCOUNT                     
                                                                                
NEWTRN26 MVC   ACMTRACE,0(R3)                                                   
         GOTOR ACMISDDS,DMCB,ISRDSEQ,(R3),0,ACMADDIR,(R3),0                     
         OC    8(2,R1),8(R1)                                                    
         BZ    NEWTRN28                                                         
         TM    9(R1),X'04'         TEST FOR EOF                                 
         BO    NEWTRN30            YES-TRY THE TRANSACTIONS NOW                 
         DC    H'0'                                                             
                                                                                
NEWTRN28 TM    CACKSTAT,ACTSDELT   TEST FOR DELETED RECORD                      
         BO    NEWTRN26            YES-SKIP RECORD                              
         CLI   RCTRACE,YES                                                      
         BNE   *+8                                                              
         GOTOR DMTRACE                                                          
         USING CHDRECD,R3                                                       
         CLC   ACMKEY(CHDKSPCS-CHDKEY),CHDKEY  TEST SAME CONTRA-ACCOUNT         
         BNE   NEWTRN30            NO-TRY TRANSACTIONS                          
         CLC   CHDKSPCS,SPACES     TEST FOR CONTRA ACCOUNT RECORD               
         BNE   NEWTRN30            NO                                           
         GOTOR DALINK                                                           
         LA    R2,DMCB                                                          
         ICM   R2,8,ACMEFFS                                                     
         GOTOR VACCEMU,DMCB,DMNEWO,,(R3),(R3)                                   
         B     NEWTRN16                                                         
                                                                                
* READ THE TRANSACTIONS FOR THE CONTRA-ACCOUNT                                  
                                                                                
NEWTRN30 CLI   FCRDTIME,YES        TEST APPLICATION WANTS TIME RECORDS          
         BE    *+12                                                             
         CLI   FCRDTRNS,YES        TEST APPLICATION WANTS TRANSACTIONS          
         BNE   NEWTRN56            NO-NEXT CONTRA-ACCOUNT                       
                                                                                
         OC    ACMNCOB,ACMNCOB     TEST FOR ANY ENTRIES FOR ACCOUNT             
         BZ    NEWTRN56            NO-NEXT CONTRA-ACCOUNT                       
         XC    ACMWRK,ACMWRK       BUILD BINSRCH KEY                            
         MVC   ACMWRK(L'ACMLCAC),ACMLCAC                                        
         GOTOR BINSRCH,DMCB,(X'02',ACMWRK),ACMACOB,ACMNCOB,ACMCOBNL,   *        
               ACMCOBNL,ACMNCOB                                                 
         CLI   0(R1),0             TEST IF ANYTHING FOUND                       
         BNE   NEWTRN56            NO-NEXT CONTRA-ACCOUNT                       
         L     R4,0(R1)            GET A(ENTRY)                                 
         CLC   ACMLCAC,0(R4)       TEST IF CONTRA-ACCOUNT FOUND                 
         BNE   NEWTRN56                                                         
         L     R3,ACMABUF          INITIALIZE TRANSACTION KEY                   
         USING TRNRECD,R3                                                       
         B     NEWTRN34                                                         
                                                                                
* LOOK FOR ANOTHER OFFICE FOR THE CONTRA-ACCOUNT                                
                                                                                
NEWTRN32 AHI   R4,ACMCOBNL                                                      
         C     R4,ACMACOBX         TEST IF PAST BUFFER END                      
         BNL   NEWTRN56            YES-TRY NEXT CONTRA-ACCOUNT                  
         CLC   ACMLCAC,0(R4)       TEST SAME CONTRA-ACCOUNT                     
         BNE   NEWTRN56            NO                                           
                                                                                
* READ FOR THE TRANSACTIONS FOR THE OFFICE/CONTRA-ACCOUNT                       
                                                                                
NEWTRN34 MVC   TRNKEY,SPACES                                                    
         MVC   TRNKCULA,ACMCURR    SET THE ACCOUNT                              
         MVC   TRNKOFF,L'TRNKCULC(R4)  EXTRACT OFFICE                           
         MVC   TRNKCULC,ACMLCAC    SET THE CONTRA-ACCOUNT                       
         ST    R3,ACMALTN                                                       
         MVC   ACMTRACE,0(R3)                                                   
         MVC   ACMKEY(L'TRNKEY),0(R3)                                           
         L     R3,ACMABUF                                                       
         GOTOR ACMISDDS,DMCB,ISREAD,(R3),0,ACMADDIR,(R3),0                      
         ORG   *-2                                                              
         TM    ACMHIST,ACMHTRN     TEST HISTORIES TO BE INCLUDED                
         BZ    *+8                                                              
         L     RF,AHSTTRN          GET HISTORICAL TRANSACTIONS                  
         BASR  RE,RF                                                            
         OC    8(2,R1),8(R1)                                                    
         BZ    NEWTRN36                                                         
         TM    9(R1),X'04'         TEST FOR EOF                                 
         BO    NEWTRN56            YES-NEXT CONTRA-ACCOUNT                      
         DC    H'0'                                                             
                                                                                
NEWTRN36 TM    ACMHIST,ACMHPRC     PROCESS HISTORY                              
         BZ    NEWTRN38                                                         
         L     R3,ACMHIO           R3=HISTORY RECORD                            
         B     NEWTRN42                                                         
                                                                                
* PROCESS A TRANSACTION - (PROCTRNS)                                            
                                                                                
NEWTRN38 TM    TRNKSTAT,TRNSDELT   TEST FOR DELETED RECORD                      
         BO    NEWTRN52            YES-SKIP TO NEXT RECORD                      
         CLI   RCTRACE,YES                                                      
         BNE   *+8                                                              
         GOTOR DMTRACE                                                          
         CLC   TRNKEY(TRNKDATE-TRNKEY),ACMKEY  TEST SAME ACC/OFF/CAC            
         BNE   NEWTRN32            NO-NEXT ACCOUNT/OFFICE                       
         CLC   CHDKSPCS-CHDKEY(L'CHDKSPCS,R3),SPACES TEST CAC RECORD            
         BNE   NEWTRN40                                                         
         CLI   ACMOCBF,YES                                                      
         BNE   NEWTRN52                                                         
         GOTOR DALINK              GET THE RECORD                               
         LA    R2,DMCB                                                          
         ICM   R2,8,ACMEFFS                                                     
         GOTOR VACCEMU,DMCB,DMNEWO,,(R3),(R3)                                   
         L     R3,ADSUBAC          SAVE SUB-ACCOUNT ELEM POINTER                
         MVC   ADSUBAC,ACMABUF     GIVE CALLER C/A KEY                          
         MVI   MODE,PROCSBAC                                                    
         GOTOR GO                                                               
         ST    R3,ADSUBAC                                                       
         B     NEWTRN52            NEXT RECORD                                  
                                                                                
NEWTRN40 GOTOR FLTKEY              FILTER ON TRANSACTION KEY                    
         BNE   NEWTRN52            SKIP RECORD                                  
         GOTOR DALINK              GET THE RECORD                               
         LA    R2,DMCB                                                          
         ICM   R2,8,ACMEFFS                                                     
         GOTOR VACCEMU,DMCB,DMNEWO,,(R3),(R3)                                   
                                                                                
NEWTRN42 LA    R2,TRNRECD          R2=A(TRANSACTION)                            
         AH    R2,DATADISP                                                      
                                                                                
         USING TRNELD,R2                                                        
         CLI   TRNEL,TRNELQ        TEST FOR TRANSACTION RECORD                  
         BNE   NEWTRN44                                                         
         CLI   FCRDTRNS,YES        TEST APPLICATION WANTS TRANSACTIONS          
         BNE   NEWTRN52                                                         
         L     R0,ACMATRN                                                       
         LHI   R1,ACMMAXRL                                                      
         LA    RE,TRNRECD                                                       
         SR    RF,RF                                                            
         ICM   RF,3,TRNRLEN                                                     
         MVCL  R0,RE                                                            
                                                                                
         GOTOR FLTTRN              APPLY TRANSACTION FILTERS                    
         BNE   NEWTRN52            REJECT THE ITEM                              
                                                                                
         MVI   ACMFMDET,PROCTRNS                                                
         GOTOR TRNMODE                                                          
         B     NEWTRN52                                                         
         DROP  R2,R3                                                            
                                                                                
* PROCESS TIME RECORDS - (PROCTIME)                                             
                                                                                
         USING TIMRECD,R3                                                       
         USING TIMELD,R2                                                        
NEWTRN44 CLI   TIMEL,TIMELQ        TEST FOR TIME RECORD                         
         BE    NEWTRN46                                                         
         SR    R0,R0               BUMP TO NEXT ELEMENT                         
         IC    R0,TIMLN                                                         
         AR    R2,R0                                                            
         CLI   TIMEL,0             TEST END OF RECORD                           
         BE    NEWTRN52                                                         
         B     NEWTRN44                                                         
                                                                                
NEWTRN46 CLI   FCRDTIME,YES        TEST APPLICATION WANTS TIME RECORDS          
         BNE   NEWTRN52                                                         
         MVC   ACMSVATN,ADTRANS    SAVE ADTRANS ADDRESS                         
                                                                                
NEWTRN48 ST    R2,ADTRANS          SET A(TIME ELEMENT) FOR APPLICATION          
         GOTOR FLTTIM              FILTER TIME ELEMENT                          
         BNE   NEWTRN50                                                         
         MVI   ACMFMDET,PROCTIME                                                
         GOTOR TRNMODE                                                          
                                                                                
NEWTRN50 SR    R0,R0               BUMP TO NEXT ELEMENT                         
         IC    R0,TIMLN                                                         
         AR    R2,R0                                                            
         CLI   TIMEL,TIMELQ        TEST TIME ELEMENT                            
         BE    NEWTRN48                                                         
         CLI   TIMEL,0             TEST END OF RECORD                           
         BNE   NEWTRN50                                                         
         MVC   ADTRANS,ACMSVATN    RESTORE ADTRANS VALUE                        
         DROP  R2,R3                                                            
                                                                                
* READ SEQUENTIAL FOR MORE TRANSACTIONS FOR SAME OFFICE/CAC                     
                                                                                
NEWTRN52 MVC   ACMTRACE,0(R3)                                                   
         L     R3,ACMABUF                                                       
         GOTOR ACMISDDS,DMCB,ISRDSEQ,(R3),0,ACMADDIR,(R3),0                     
         ORG   *-2                                                              
         TM    ACMHIST,ACMHTRN     TEST HISTORIES TO BE INCLUDED                
         BZ    *+8                                                              
         L     RF,AHSTTRN          GET HISTORICAL TRANSACTIONS                  
         BASR  RE,RF                                                            
         OC    8(2,R1),8(R1)                                                    
         BZ    NEWTRN54                                                         
         TM    9(R1),X'04'         TEST FOR EOF                                 
         BO    NEWTRN56            YES-NEXT CONTRA-ACCOUNT                      
         DC    H'0'                                                             
                                                                                
         USING TRNRECD,R3                                                       
NEWTRN54 TM    ACMHIST,ACMHPRC     PROCESS HISTORY?                             
         BZ    NEWTRN38                                                         
         L     R3,ACMHIO           R3=HISTORY RECORD                            
         CLC   TRNKEY(TRNKDATE-TRNKEY),ACMKEY  TEST SAME ACC/OFF/CAC            
         BNE   NEWTRN32            NO-NEXT ACCOUNT/OFFICE                       
         B     NEWTRN42                                                         
                                                                                
* PREPARE FOR NEXT CONTRA-ACCOUNT                                               
                                                                                
NEWTRN56 LHI   R0,1                ONLY CONSIDER SBACLAST                       
         LA    R1,ACMLMSBA         PASS LAST TIME MODES                         
         GOTOR GOSAVE              PASS ANY PENDING SBACLAST MODE               
         B     NEWTRN02                                                         
                                                                                
* PREPARE FOR NEXT ACCOUNT                                                      
                                                                                
NEWTRN60 LA    R1,ACMLMODE                                                      
         LHI   R0,ACMLMOFA-ACMLMODE                                             
         OC    0(ACMLMOFA-ACMLMODE,R1),0(R1)                                    
         BZ    *+8                                                              
         GOTOR GOSAVE                                                           
                                                                                
         XC    ACMKEY,ACMKEY       CLEAR OUT SAVE AREAS                         
         XC    ACMLCAC,ACMLCAC                                                  
                                                                                
NEWTRNX  J     EXIT                                                             
         DROP  RB                                                               
         EJECT                                                                  
***********************************************************************         
* READ THE NEW FILE IN OFFICE SEQUENCE AND GENERATE BUCKETS           *         
***********************************************************************         
                                                                                
NEWFIL   NTR1  BASE=*,LABEL=*                                                   
         MVI   ACMFMACC,ACCFRST                                                 
         MVI   ACMLMACC,ACCLAST                                                 
         NI    ACMINDS2,FF-ACMIOFAP-ACMIOFAA                                    
         L     R4,ACMABUCK         R4=A(BUCKET BLOCK)                           
         USING BUCKBLKD,R4                                                      
         XC    BUCKBLKD(BUCKBLKL),BUCKBLKD                                      
         MVC   BUCKLEV,FCGENBUK                                                 
         MVC   BUCKCTRY,RCCTRY                                                  
         MVC   BUCKACOM,ADCOMFAC                                                
         MVC   BUCKATRN,ACMATRN                                                 
         MVC   BUCKABUK,ACMABUK                                                 
         MVC   BUCKABUF,ACMABUKB                                                
         LA    R0,NEWFIL50         PASS A(HOOK ROUTINE)                         
         ST    R0,BUCKHOOK                                                      
                                                                                
         L     R3,ACMABUF                                                       
         USING TRNRECD,R3                                                       
         MVC   TRNKEY,SPACES                                                    
         MVC   TRNKCULA,ACMCURR    INITIALIZE KEY                               
                                                                                
NEWFIL02 SR    R1,R1                                                            
         IC    R1,TRNKOFF+1                                                     
         AHI   R1,1                BUMP TO NEXT OFFICE                          
         STC   R1,TRNKOFF+1                                                     
         MVC   TRNKCULC(TRNKSTAT-TRNKCULC),SPACES                               
         ST    R3,ACMALTN                                                       
         MVC   ACMTRACE,TRNKEY                                                  
         GOTOR ACMISDDS,DMCB,ISREAD,(R3),0,ACMADDIR,(R3),0                      
         B     NEWFIL06                                                         
                                                                                
NEWFIL04 MVC   ACMTRACE,TRNKEY                                                  
         ST    R3,ACMALTN                                                       
         GOTOR ACMISDDS,DMCB,ISRDSEQ,(R3),0,ACMADDIR,(R3),0                     
                                                                                
NEWFIL06 OC    8(2,R1),8(R1)       TEST FOR ERROR                               
         BZ    NEWFIL08            NO                                           
         TM    9(R1),X'04'         TEST FOR EOF                                 
         BO    NEWFIL38            END OF ACCOUNT                               
         DC    H'0'                                                             
                                                                                
NEWFIL08 TM    TRNKSTAT,TRNSDELT   TEST FOR DELETED RECORD                      
         BO    NEWFIL04            YES-SKIP IT                                  
         MVC   ACMKEY,TRNKEY                                                    
         CLI   RCTRACE,YES                                                      
         BNE   *+8                                                              
         GOTOR DMTRACE                                                          
         CLC   ACMCURR,TRNKCULA    TEST SAME ACCOUNT                            
         BNE   NEWFIL38            NO-WRAP UP FOR ACCOUNT                       
         CLC   TRNKCULC,SPACES     TEST OFFICE/ACCOUNT RECORD                   
         BE    NEWFIL12            YES                                          
         USING CHDKEY,R3                                                        
         CLC   CHDKSPCS,SPACES     TEST FOR CAC RECORD                          
         BNE   NEWFIL10            YES                                          
         TM    ACMCFIND,ACMCFACT+ACMCFOFF+ACMCCFLT                              
         BZ    NEWFIL12            NO                                           
         GOTOR FLTCON                                                           
         BNE   NEWFIL08                                                         
         B     NEWFIL12                                                         
                                                                                
NEWFIL10 GOTOR FLTKEY                                                           
         BE    NEWFIL12            TAKE THE TRANSACTION                         
         B     NEWFIL04                                                         
                                                                                
         USING TRNRECD,R3                                                       
NEWFIL12 GOTOR DALINK                                                           
         LA    R2,DMCB             R2=A(DATAMGR PARMS)                          
         ICM   R2,8,ACMEFFS                                                     
         GOTOR VACCEMU,DMCB,DMNEWO,,(R3),(R3)                                   
         CLC   TRNKCULC,SPACES     TEST CAC=SPACES                              
         BNE   NEWFIL24            NO-CANNOT BE OFFICE-ACCOUNT                  
                                                                                
* PROCESS AN OFFICE-ACCOUNT RECORD                                              
                                                                                
         MVI   BUCKACT,BUCKALST    LAST TIME FOR OFFICE                         
         GOTOR ACMVACBU,BUCKBLKD                                                
         LHI   R0,ACMLMACC-ACMLMODE                                             
         LA    R1,ACMLMODE         LAST TIME MODES                              
         GOTOR GOSAVE                                                           
                                                                                
         TM    ACMINDS2,ACMIOFAP   TEST PENDING OFACLAST HOOK                   
         BZ    *+16                NO                                           
         MVI   MODE,OFACLAST                                                    
         GOTOR GO                                                               
         NI    ACMINDS2,FF-ACMIOFAP                                             
                                                                                
         L     R0,ACMAOFA                                                       
         LHI   R1,ACMMAXRL                                                      
         LA    RE,TRNRECD                                                       
         SR    RF,RF                                                            
         ICM   RF,3,TRNRLEN                                                     
         MVCL  R0,RE                                                            
         MVI   ACMFLAG,ACMFLOA     OFFICE SECURITY ON OFARECD                   
         GOTOR OFFLMT                                                           
         BNE   NEWFIL02            TRY NEXT OFFICE                              
                                                                                
         NI    ACMINDS2,FF-ACMIOFAA                                             
         MVI   ACMFMAOF,OFFIRST    SET PENDING OFFICE FIRST MODE                
         OI    ACMINDS2,ACMIOFAP                                                
                                                                                
         MVC   ACMOFNAM,SPACES     FIND THE OFFICE NAME                         
         ICM   R0,15,ACMNOFNB      GET N'OFFICES                                
         BZ    NEWFIL14                                                         
         L     R2,ACMAOFNB                                                      
                                                                                
         CLC   TRNKOFF,0(R2)       MATCH ON OFFICE CODE                         
         BE    *+16                                                             
         AHI   R2,ACMOFNL                                                       
         BCT   R0,*-14                                                          
         B     NEWFIL14            NOT IN LIST                                  
                                                                                
         MVC   ACMOFNAM,L'OFFKOFF(R2) EXTRACT NAME                              
                                                                                
NEWFIL14 ZAP   ACMOBAL,ACMPZERO    CLEAR OFFICE BAL BROUGHT FORWARD             
         CLI   ACMATYP,ACMATPL     TEST PROFIT AND LOSS LEDGER                  
         BE    NEWFIL22            YES-DEVELOP BALANCE IN FLTTRN                
                                                                                
         LA    R2,TRNRECD                                                       
         AH    R2,DATADISP         FIND BALANCE ON RECORD                       
         USING ABLELD,R2                                                        
         SR    R0,R0                                                            
NEWFIL16 CLI   ABLEL,0             TEST FOR EOR                                 
         BE    NEWFIL22            YES-GET NEXT RECORD                          
         CLI   ABLEL,ABLELQ        TEST FOR BALANCE ELEMENT                     
         BNE   NEWFIL20            NO                                           
         ZAP   ACMOBAL,ABLFRWD     EXTRACT BALANCE                              
         L     R1,ACMAOFL                                                       
         USING OFFALD,R1                                                        
         OC    OFFANEWA,OFFANEWA   TEST FOR LIMITED ACCESS                      
         BNZ   NEWFIL18            YES                                          
         CLC   OFFAOFFC,SPACES     TEST FOR A REQUESTED OFFICE FILTER           
         BNE   NEWFIL18            NO-ALLOW THE TRANSACTION                     
         CLI   FCSEQ,FCSEQACC                                                   
         BE    NEWFIL22                                                         
                                                                                
NEWFIL18 AP    ACMABAL,ABLFRWD     AND BALANCE FOR ACROSS OFFICE                
         B     NEWFIL22            ALL DONE-GET NEXT RECORD                     
                                                                                
NEWFIL20 IC    R0,ABLLN                                                         
         AR    R2,R0                                                            
         B     NEWFIL16                                                         
         DROP  R2                                                               
                                                                                
NEWFIL22 MVI   MODE,OFACFRST                                                    
         GOTOR GO                                                               
         B     NEWFIL04                                                         
                                                                                
NEWFIL24 LA    R2,TRNRECD                                                       
         AH    R2,DATADISP         POINT AT FIRST ELEMENT                       
         USING TRNELD,R2                                                        
         CLI   TRNEL,TRNELQ        TEST FOR TRANSACTION                         
         BE    NEWFIL28            YES                                          
         CLI   TRNEL,CACELQ        TEST FOR CONTRA-ACCOUNT ELEMENT              
         BNE   NEWFIL32                                                         
                                                                                
* PROCESS A CONTRA-ACCOUNT RECORD                                               
                                                                                
         CLI   FCGENBUK,FCGENCON   TEST USER WANTS CONTRA LEVEL BUCKETS         
         BNE   NEWFIL26            NO                                           
         MVI   BUCKACT,BUCKALST    LAST TIME CALL                               
         GOTOR ACMVACBU,BUCKBLKD                                                
         ST    R3,BUCKACHD         POST NEW BUCKET RECORD                       
         MVI   BUCKACT,BUCKAPBK                                                 
         GOTOR ACMVACBU,BUCKBLKD                                                
         XC    BUCKACHD,BUCKACHD                                                
                                                                                
NEWFIL26 LHI   R0,1                                                             
         LA    R1,ACMLMSBA                                                      
         GOTOR GOSAVE                                                           
         MVI   ACMFMSBA,SBACFRST                                                
         L     R0,ACMACAC                                                       
         LHI   R1,ACMMAXRL                                                      
         LA    RE,TRNRECD                                                       
         SR    RF,RF                                                            
         ICM   RF,3,TRNRLEN                                                     
         MVCL  R0,RE                                                            
         B     NEWFIL04            GET NEXT RECORD                              
                                                                                
* PROCESS A TRANSACTION                                                         
                                                                                
NEWFIL28 L     R0,ACMATRN                                                       
         LHI   R1,ACMMAXRL                                                      
         LA    RE,TRNRECD                                                       
         SR    RF,RF                                                            
         ICM   RF,3,TRNRLEN                                                     
         MVCL  R0,RE                                                            
                                                                                
         GOTOR FLTTRN                                                           
         BNE   NEWFIL04            BOUNCE THE TRANSACTION                       
                                                                                
         OI    ACMINDS2,ACMIOFAA   SET ACTIVITY FLAG                            
         TM    ACMINDS,ACMIPBUK    TEST PASS TRANSACTION TO ACBUCKET            
         BNO   NEWFIL30            DON'T POST TRANSACTIONS                      
         MVI   BUCKACT,BUCKAPST    POST THE TRANSACTION                         
         GOTOR ACMVACBU,BUCKBLKD                                                
                                                                                
NEWFIL30 CLI   FCRDTRNS,YES        TEST APPLICATION WANTS TRANSACTION           
         BNE   *+12                NO                                           
         MVI   ACMFMDET,PROCTRNS                                                
         GOTOR TRNMODE                                                          
         B     NEWFIL04                                                         
         DROP  R2,R3                                                            
                                                                                
* PROCESS A TIME RECORD                                                         
                                                                                
         USING TIMRECD,R3                                                       
         USING TIMELD,R2                                                        
NEWFIL32 CLI   TIMEL,TIMELQ        TEST FOR TIME RECORD                         
         BNE   NEWFIL04                                                         
         CLI   FCRDTIME,YES        TEST APPLICATION WANTS TIME RECORDS          
         BNE   NEWFIL04                                                         
         L     R0,ACMATRN                                                       
         LHI   R1,ACMMAXRL                                                      
         LA    RE,TIMRECD                                                       
         SR    RF,RF                                                            
         ICM   RF,3,TIMRLEN                                                     
         MVCL  R0,RE                                                            
         LA    R2,TIMRECD                                                       
         AH    R2,DATADISP         POINT AT FIRST ELEMENT                       
                                                                                
         MVC   ACMSVATN,ADTRANS    SAVE ADTRANS ADDRESS                         
                                                                                
NEWFIL34 ST    R2,ADTRANS          SET A(TIME ELEMENT) FOR APPLICATION          
         GOTOR FLTTIM              FILTER TIME ELEMENT                          
         BNE   NEWFIL36                                                         
         MVI   ACMFMDET,PROCTIME                                                
         GOTOR TRNMODE                                                          
                                                                                
NEWFIL36 SR    R0,R0               BUMP TO NEXT ELEMENT                         
         IC    R0,TIMLN                                                         
         AR    R2,R0                                                            
         CLI   TIMEL,TIMELQ        TEST TIME ELEMENT                            
         BE    NEWFIL34                                                         
         CLI   TIMEL,0             TEST END OF RECORD                           
         BNE   NEWFIL36                                                         
         MVC   ADTRANS,ACMSVATN    RESTORE ADTRANS VALUE                        
         B     NEWFIL04                                                         
         DROP  R2,R3                                                            
                                                                                
* WRAP UP FOR ACCOUNT                                                           
                                                                                
NEWFIL38 MVI   BUCKACT,BUCKALST                                                 
         GOTOR ACMVACBU,BUCKBLKD                                                
         LA    R1,ACMLMODE         PASS SBACLAST,OFFLAST                        
         LHI   R0,ACMLMACC-ACMLMODE                                             
         OC    0(ACMLMACC-ACMLMODE,R1),0(R1)                                    
         BZ    *+8                 NO                                           
         GOTOR GOSAVE                                                           
         TM    ACMINDS2,ACMIOFAP   TEST PENDING OFACLAST HOOK                   
         BZ    *+12                                                             
         MVI   MODE,OFACLAST                                                    
         GOTOR GO                                                               
                                                                                
         LA    R1,ACMLMACC         PASS ACCLAST MODE                            
         LHI   R0,1                                                             
         GOTOR GOSAVE                                                           
                                                                                
         XC    ACMKEY,ACMKEY                                                    
                                                                                
         J     EXIT                                                             
                                                                                
* HOOK ROUTINE FOR ACBUCKET                                                     
                                                                                
NEWFIL50 NTR1  ,                                                                
         MVI   BYTE,PROCCBUK       SET FOR CONTRA-BUCKETS                       
         CLI   FCGENBUK,FCGENCON                                                
         BE    *+8                                                              
         MVI   BYTE,PROCOBUK                                                    
         CLI   FCRDTRNS,YES        TEST PASSING TRANSACTIONS                    
         BE    NEWFIL52            YES                                          
                                                                                
         MVC   ACMFMDET,BYTE       NO-HANDLE PASSING SAVED MODES                
         GOTOR TRNMODE                                                          
         B     NEWFIL54                                                         
                                                                                
NEWFIL52 MVC   MODE,BYTE                                                        
         GOTOR GO                                                               
                                                                                
NEWFIL54 J     EXIT                                                             
         DROP  R4,RB                                                            
         EJECT                                                                  
***********************************************************************         
* READ TRANSACTIONS FOR AN OFFICE/ACCOUNT                             *         
***********************************************************************         
                                                                                
OFFTRN   NTR1  BASE=*,LABEL=*                                                   
         MVI   ACMFMACC,ACCFRST                                                 
         NI    ACMINDS2,FF-ACMIOFAA TURN OFF ACTIVITY BIT                       
                                                                                
         L     R4,ACMABUCK         INITIALIZE BUCKET BLOCK                      
         USING BUCKBLKD,R4                                                      
         XC    BUCKBLKD(BUCKBLKL),BUCKBLKD                                      
         MVC   BUCKLEV,FCGENBUK                                                 
         MVC   BUCKCTRY,RCCTRY                                                  
         MVC   BUCKACOM,ADCOMFAC                                                
         MVC   BUCKATRN,ACMATRN                                                 
         MVC   BUCKACHD,ACMACAC                                                 
         MVC   BUCKABUK,ACMABUK                                                 
         MVC   BUCKABUF,ACMABUKB                                                
         LA    R0,OFFTRN50         HOOK FOR BUCKET ROUTINE                      
         ST    R0,BUCKHOOK                                                      
                                                                                
         MVI   MODE,OFACFRST       PASS OFACFRST                                
         GOTOR GO                                                               
                                                                                
* READ FOR FIRST TRANSACTION FOR OFFICE/ACCOUNT                                 
                                                                                
OFFTRN02 L     R3,ACMABUF                                                       
         USING TRNRECD,R3                                                       
         L     R2,ACMAOFA                                                       
         USING OFARECD,R2                                                       
         MVC   TRNKEY,SPACES                                                    
         MVC   TRNKCULA,OFAKCULA                                                
         MVC   TRNKOFF,OFAKOFF                                                  
         MVI   TRNKSBR,FF                                                       
         ST    R3,ACMALTN                                                       
         MVC   ACMTRACE,TRNKEY                                                  
         GOTOR ACMISDDS,DMCB,ISREAD,(R3),0,ACMADDIR,(R3),0                      
         ORG   *-2                                                              
         TM    ACMHIST,ACMHTRN     TEST HISTORIES TO BE INCLUDED                
         BZ    *+8                                                              
         L     RF,AHSTTRN          GET HISTORICAL TRANSACTIONS                  
         BASR  RE,RF                                                            
         B     OFFTRN06                                                         
                                                                                
OFFTRN04 L     R2,ACMAOFA                                                       
         MVC   ACMTRACE,TRNKEY                                                  
         L     R3,ACMABUF                                                       
         GOTOR ACMISDDS,DMCB,ISRDSEQ,(R3),0,ACMADDIR,(R3),0                     
         ORG   *-2                                                              
         TM    ACMHIST,ACMHTRN     TEST HISTORIES TO BE INCLUDED                
         BZ    *+8                                                              
         L     RF,AHSTTRN          GET HISTORICAL TRANSACTIONS                  
         BASR  RE,RF                                                            
                                                                                
OFFTRN06 OC    8(2,R1),8(R1)                                                    
         BZ    OFFTRN08                                                         
         TM    9(R1),X'04'                                                      
         BO    OFFTRN32                                                         
         DC    H'0'                                                             
                                                                                
OFFTRN08 TM    ACMHIST,ACMHPRC     PROCESS THE HISTORY RECORD                   
         BZ    OFFTRN10                                                         
         L     R3,ACMHIO                                                        
         CLC   TRNKEY(TRNKCULC-TRNKEY),OFAKEY  TEST SAME OFF/ACC                
         BNE   OFFTRN32                                                         
         B     OFFTRN18                                                         
                                                                                
OFFTRN10 TM    TRNKSTAT,TRNSDELT   TEST FOR DELETED RECORD                      
         BO    OFFTRN04            YES-SKIP IT                                  
         CLI   RCTRACE,YES                                                      
         BNE   *+8                                                              
         GOTOR DMTRACE                                                          
         CLC   TRNKEY(TRNKCULC-TRNKEY),OFAKEY  TEST SAME OFF/ACC                
         BNE   OFFTRN32                                                         
                                                                                
         USING CHDKEY,R3                                                        
         CLC   CHDKSPCS,SPACES     TEST CAC RECORD                              
         BNE   OFFTRN12            NO                                           
         TM    ACMCFIND,ACMCFACT+ACMCFOFF+ACMCCFLT                              
         BZ    OFFTRN14            NO                                           
         GOTOR FLTCON                                                           
         BNE   OFFTRN10                                                         
         B     OFFTRN14                                                         
                                                                                
         USING TRNRECD,R3                                                       
OFFTRN12 GOTOR FLTKEY              FILTER ON TRANSACTION DIRECTORY KEY          
         BNE   OFFTRN04                                                         
                                                                                
OFFTRN14 GOTOR DALINK                                                           
                                                                                
OFFTRN16 LA    R2,DMCB                                                          
         ICM   R2,8,ACMEFFS                                                     
         GOTOR VACCEMU,DMCB,DMNEWO,,(R3),(R3)                                   
                                                                                
OFFTRN18 LA    R2,TRNRECD                                                       
         AH    R2,DATADISP                                                      
         USING TRNELD,R2                                                        
         CLI   TRNEL,TRNELQ                                                     
         BE    OFFTRN22                                                         
         CLI   TRNEL,BUKELQ                                                     
         BE    OFFTRN20                                                         
         CLI   TRNEL,CACELQ                                                     
         BNE   OFFTRN26                                                         
         DROP  R2                                                               
                                                                                
* PROCESS A CONTRA-ACCOUNT RECORD                                               
                                                                                
         CLI   FCGENBUK,FCGENCON                                                
         BNE   OFFTRN20                                                         
         MVI   BUCKACT,BUCKALST                                                 
         GOTOR ACMVACBU,BUCKBLKD                                                
                                                                                
OFFTRN20 LHI   R0,1                                                             
         LA    R1,ACMLMSBA                                                      
         GOTOR GOSAVE                                                           
         MVI   ACMFMSBA,SBACFRST   SET FIRST FOR CAC                            
         L     R0,ACMACAC                                                       
         LHI   R1,ACMMAXRL                                                      
         LA    RE,TRNRECD                                                       
         SR    RF,RF                                                            
         ICM   RF,3,TRNRLEN                                                     
         MVCL  R0,RE                                                            
         CLI   FCGENBUK,FCGENCON                                                
         BNE   OFFTRN04                                                         
         SR    R0,R0               TEST BUCKETS FOR PRIOR MONTHS                
         IC    R0,1(R2)                                                         
         AR    R2,R0                                                            
         CLI   0(R2),BUKELQ                                                     
         BNE   OFFTRN04                                                         
         OI    ACMINDS2,ACMIOFAA   SET ACTIVITY FLAG                            
         MVI   BUCKACT,BUCKAPBK                                                 
         GOTOR ACMVACBU,BUCKBLKD                                                
         B     OFFTRN04                                                         
                                                                                
* PROCESS A TRANSACTION                                                         
                                                                                
OFFTRN22 L     R0,ACMATRN                                                       
         LHI   R1,ACMMAXRL                                                      
         LA    RE,TRNRECD                                                       
         SR    RF,RF                                                            
         ICM   RF,3,TRNRLEN                                                     
         MVCL  R0,RE               SAVE THE TRANSACTION                         
                                                                                
         OI    ACMINDS2,ACMIOFAA   SET ACTIVITY FLAG                            
         TM    ACMINDS,ACMIPBUK    TEST PASS TRANSACTIONS TO ACBUCKET           
         BNO   OFFTRN24            DON'T POST TRANSACTIONS                      
         MVI   BUCKACT,BUCKAPST                                                 
         GOTOR ACMVACBU,BUCKBLKD                                                
                                                                                
OFFTRN24 GOTOR FLTTRN                                                           
         BNE   OFFTRN04            GET NEXT RECORD                              
                                                                                
         CLI   FCRDTRNS,YES                                                     
         BNE   *+12                                                             
         MVI   ACMFMDET,PROCTRNS                                                
         GOTOR TRNMODE                                                          
         B     OFFTRN04                                                         
         DROP  R3                                                               
                                                                                
* PROCESS A TIME RECORD                                                         
                                                                                
         USING TIMRECD,R3                                                       
         USING TIMELD,R2                                                        
OFFTRN26 CLI   TIMEL,TIMELQ        TEST TIME ELEMENT                            
         BE    *+6                                                              
         DC    H'0'                                                             
         CLI   FCRDTIME,YES                                                     
         BNE   OFFTRN04                                                         
         L     R0,ACMATRN                                                       
         LHI   R1,ACMMAXRL                                                      
         LA    RE,TIMRECD                                                       
         SR    RF,RF                                                            
         ICM   RF,3,TIMRLEN                                                     
         MVCL  R0,RE               SAVE THE TRANSACTION                         
         LA    R2,TIMRECD                                                       
         AH    R2,DATADISP         POINT AT FIRST ELEMENT                       
         MVC   ACMSVATN,ADTRANS    SAVE ADTRANS ADDRESS                         
                                                                                
OFFTRN28 ST    R2,ADTRANS          SET A(TIME ELEMENT) FOR APPLICATION          
         GOTOR FLTTIM              FILTER TIME ELEMENT                          
         BNE   OFFTRN30                                                         
         MVI   ACMFMDET,PROCTIME                                                
         GOTOR TRNMODE                                                          
                                                                                
OFFTRN30 SR    R0,R0               BUMP TO NEXT ELEMENT                         
         IC    R0,TIMLN                                                         
         AR    R2,R0                                                            
         CLI   TIMEL,TIMELQ        TEST TIME ELEMENT                            
         BE    OFFTRN28                                                         
         CLI   TIMEL,0             TEST END OF RECORD                           
         BNE   OFFTRN30                                                         
         MVC   ADTRANS,ACMSVATN    RESTORE ADTRANS VALUE                        
         B     OFFTRN04                                                         
         DROP  R2,R3                                                            
                                                                                
OFFTRN32 MVI   BUCKACT,BUCKALST                                                 
         GOTOR ACMVACBU,BUCKBLKD                                                
         LA    R1,ACMLMODE                                                      
         LHI   R0,ACMLMAOF-ACMLMODE                                             
         OC    0(ACMLMAOF-ACMLMODE,R1),0(R1)                                    
         BZ    *+8                                                              
         GOTOR GOSAVE                                                           
         MVI   MODE,OFACLAST                                                    
         GOTOR GO                                                               
         J     EXIT                                                             
                                                                                
OFFTRN50 NTR1  ,                                                                
         MVI   BYTE,PROCCBUK       SET FOR CONTRA-BUCKETS                       
         CLI   FCGENBUK,FCGENCON                                                
         BE    *+8                                                              
         MVI   BYTE,PROCOBUK                                                    
         CLI   FCRDTRNS,YES        TEST PASSING TRANSACTIONS                    
         BE    OFFTRN52            YES                                          
                                                                                
         MVC   ACMFMDET,BYTE       NO-HANDLE PASSING SAVED MODES                
         GOTOR TRNMODE                                                          
         B     OFFTRN54                                                         
                                                                                
OFFTRN52 MVC   MODE,BYTE                                                        
         GOTOR GO                                                               
                                                                                
OFFTRN54 J     EXIT                                                             
         DROP  R4,RB                                                            
         EJECT                                                                  
***********************************************************************         
* READ TRANSACTIONS IN MONTH SEQUENCE                                 *         
***********************************************************************         
                                                                                
MOSTRN   NTR1  BASE=*,LABEL=*                                                   
         MVI   ACMFMACC,ACCFRST                                                 
         MVI   ACMLMACC,ACCLAST                                                 
         XC    ACMWRKC,ACMWRKC                                                  
         XC    ACMLCAC,ACMLCAC                                                  
         L     R4,ADACC                                                         
         USING ACTRECD,R4                                                       
         LA    R3,ACMKMOS                                                       
         USING MOSPASD,R3                                                       
         XC    MOSPKEY,MOSPKEY                                                  
         MVI   MOSPTYP,MOSPTYPQ                                                 
         MVC   MOSPCPY,ACTKCPY     COMPANY                                      
         MVC   MOSPMOS,ACMMSTR     MOA                                          
         MVC   MOSPULA,ACTKULA     U/L/A                                        
         MVC   ACMTRACE,0(R3)                                                   
         L     R3,ACMABUF                                                       
         ST    R3,ACMALTN                                                       
         GOTOR ACMISDDS,DMCB,ISREAD,(R3),0,ACMADDIR,ACMKMOS,0                   
                                                                                
MOSTRN02 OC    8(2,R1),8(R1)                                                    
         BZ    *+6                 NO ERRORS                                    
         DC    H'0'                                                             
         CLI   RCTRACE,YES                                                      
         BNE   *+8                                                              
         GOTOR DMTRACE                                                          
         CLC   MOSPKEY(MOSPOFF-MOSPKEY),ACMKMOS  TEST SAME ACCOUNT              
         BNE   MOSTRN10                                                         
         MVC   ACMKMOS(L'TRNKEY),MOSPKEY                                        
         MVC   ACMTKOFF,MOSPOFF    SET TRANSACTION OFFICE/WORK CODE             
         MVC   ACMTKSTA,MOSPKSTA   SET TRANSACTION STATUS VALUES                
         MVC   ACMTRNDA,MOSPKDA    SET TRANSACTION DISK ADDRESS                 
                                                                                
         L     R3,ACMATRN                                                       
         USING TRNRECD,R3                                                       
         SHI   R3,ADJLEN                                                        
         MVC   TRNKSTA,ACMTKSTA                                                 
         MVC   TRNKDA,ACMTRNDA                                                  
         GOTOR DALINK              GET TRANSACTION RECORD                       
         MVC   ACMKEY(TRNKSTA-TRNKEY),TRNRECD BUILD TRANSACTION KEY             
         MVC   ACMKEY+(TRNKSTA-TRNKEY)(L'TRNKSTA),TRNRSTA                       
         MVC   ACMKEY+(TRNKDA-TRNKEY)(L'TRNKDA),ACMTRNDA                        
         LR    R0,R3                                                            
         LA    R3,ACMKEY                                                        
         GOTOR FLTKEY                                                           
         BNE   MOSTRN08            GET THE NEXT TRANSACTION                     
         LR    R3,R0                                                            
         MVC   WORK(TRNRSMOS-TRNRECD),TRNRECD   SAVE STATUS BYTES               
         AHI   R3,ADJLEN                                                        
         MVC   TRNRECD(TRNRSTA2-TRNRECD),WORK                                   
         XC    TRNRECD+ACCOUSED(ACCOULEN+ACCOPLEN),TRNRECD+ACCOUSED             
         SR    RE,RE                                                            
         ICM   RE,3,TRNRLEN        ADJUST RECORD LENGTH                         
         SHI   RE,ADJLEN                                                        
         STCM  RE,3,TRNRLEN                                                     
         TM    TRNRSTAT,TRNSDELT   TEST RECORD DELETED                          
         BO    MOSTRN08                                                         
         TM    WORK+(TRNRSTA2-TRNRECD),TRNSUSED+TRNSPEEL                        
         BZ    MOSTRN06                                                         
         L     RF,ACMATRN          SET OLD FILE USED AND PEELED DATES           
         LA    RE,ACCORFST(RF)                                                  
         SR    R0,R0                                                            
                                                                                
         USING TRSELD,RE                                                        
MOSTRN04 IC    R0,TRSLN            LOCATE TRANSACTION STATUS ELEMENT            
         AR    RE,R0                                                            
         CLI   TRSEL,0                                                          
         BE    MOSTRN06                                                         
         CLI   TRSEL,TRSELQ                                                     
         BNE   MOSTRN04                                                         
         MVC   ACCOUSED(ACCOULEN+ACCOPLEN,RF),TRSUDAT                           
         DROP  RE                                                               
                                                                                
MOSTRN06 L     R2,ADTRANS                                                       
         USING TRNELD,R2                                                        
         CLI   TRNEL,TRNELQ        TEST FOR TRANSACTION RECORD                  
         BE    *+6                                                              
         DC    H'0'                                                             
         GOTOR FLTTRN              FILTER TRANSACTION RECORD                    
         BNE   MOSTRN08                                                         
         MVI   ACMFMDET,PROCTRNS                                                
         GOTOR TRNMODE             PASS TO APPLICATION                          
                                                                                
MOSTRN08 L     R3,ACMABUF                                                       
         GOTOR ACMISDDS,DMCB,ISRDSEQ,(R3),0,ACMADDIR,(R3),0                     
         B     MOSTRN02                                                         
                                                                                
MOSTRN10 LA    R1,ACMLMODE                                                      
         LHI   R0,ACMLMOFA-ACMLMODE                                             
         OC    0(ACMLMOFA-ACMLMODE,R1),0(R1)                                    
         BZ    MOSTRNX                                                          
         GOTOR GOSAVE              PASS SBACLAST/ANALLAST/ACCLAST               
                                                                                
MOSTRNX  J     EXIT                                                             
         DROP  R2,R3,R4,RB                                                      
         EJECT                                                                  
***********************************************************************         
* BUILD A LIST OF CONTRA-ACCOUNTS/OFFICES FOR THE ACCOUNT             *         
***********************************************************************         
                                                                                
BLDBUF   NTR1  BASE=*,LABEL=*                                                   
         XC    ACMNCOB,ACMNCOB     CLEAR N'ENTRIES                              
         XC    ACMACOBX,ACMACOBX   CLEAR A(END OF ACCOUNT BUFFER)               
         OC    ACMACOB,ACMACOB     TEST IF BUFFER WAS ACQUIRED                  
         BNZ   BLDBUF02            YES                                          
                                                                                
         L     R0,ACMLCOB          GET LENGTH OF BUFFER                         
         GETMAIN R,LV=(0)                                                       
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
         ST    R1,ACMACOB          SAVE A(BUFFER)                               
         L     R1,ACMLCOB                                                       
         SR    R0,R0               COMPUTE MAXIMUM NUMBER OF ENTRIES            
         D     R0,=A(ACMCOBNL)                                                  
         ST    R1,ACMMXCOB                                                      
                                                                                
BLDBUF02 L     R2,ACMACOB          R2=A(CONTRA-OFFICE ENTRY)                    
         XC    ACMKEY,ACMKEY                                                    
         LA    R3,ACMKEY                                                        
         USING TRNKEY,R3                                                        
         MVC   TRNKEY,SPACES                                                    
         MVC   TRNKCULA,ACMCURR    SET ACCOUNT                                  
         MVI   TRNKOFF+1,X'41'     SET TO READ FIRST OFFICE/ACCOUNT             
         B     BLDBUF06                                                         
                                                                                
BLDBUF04 MVI   TRNKDATE,FF         SET TO BUMP TO NEXT CONTRA-ACCOUNT           
                                                                                
BLDBUF06 MVC   ACMTRACE,TRNKEY                                                  
         GOTOR ACMISDDS,DMCB,ISREAD,ACMKEY,0,ACMADDIR,ACMKEY,0                  
         OC    8(2,R1),8(R1)                                                    
         BZ    BLDBUF08            NO ERRORS                                    
         TM    9(R1),X'04'         TEST FOR EOF                                 
         BO    BLDBUF08                                                         
         B     BLDBUF14            END OF ACCOUNT                               
                                                                                
BLDBUF08 CLI   RCTRACE,YES                                                      
         BNE   *+8                                                              
         GOTOR DMTRACE                                                          
         CLC   ACMCURR,TRNKEY      TEST SAME ACCOUNT                            
         BNE   BLDBUF14            NO-ALL DONE                                  
         CLC   TRNKCULC,SPACES     TEST FOR OFFICE-ACCOUNT RECORD               
         BE    BLDBUF10            YES                                          
         TM    ACMCFIND,ACMCFACT+ACMCFOFF+ACMCCFLT                              
         BZ    BLDBUF12            NO, SO CONTRA OK                             
         GOTOR FLTCON                                                           
         BNE   BLDBUF08                                                         
         B     BLDBUF12            CONTRA OK                                    
                                                                                
BLDBUF10 L     RE,ACMAOFA          MOVE KEY TO OFFICE/ACCOUNT BUFFER            
         MVC   0(ACCKLEN,RE),TRNKEY                                             
         MVI   ACMFLAG,ACMFLOA                                                  
         GOTOR OFFLMT              TEST SECURITY ON OFFICE                      
         BE    BLDBUF04            YES-PROCEED WITH NEXT RECORD                 
         SR    R1,R1                                                            
         IC    R1,TRNKOFF+1        NO-BUMP TO NEXT OFFICE                       
         AHI   R1,1                                                             
         STC   R1,TRNKOFF+1                                                     
         B     BLDBUF06                                                         
                                                                                
BLDBUF12 L     R1,ACMNCOB          INCREMENT ENTRY COUNT                        
         AHI   R1,1                                                             
         C     R1,ACMMXCOB         TEST AGAINST MAXIMUM BUFFER SIZE             
         BNH   *+6                                                              
         DC    H'0'                                                             
         ST    R1,ACMNCOB                                                       
         MVC   0(L'TRNKCULC,R2),TRNKCULC                                        
         MVC   L'TRNKCULC(L'TRNKOFF,R2),TRNKOFF                                 
         AHI   R2,ACMCOBNL         ADVANCE TO NEXT ENTRY                        
         B     BLDBUF04                                                         
                                                                                
BLDBUF14 CLC   ACMNCOB,=F'1'       TEST MORE THAN 1 ENTRY                       
         BNH   BLDBUFX             NO                                           
         GOTOR ACMVQSRT,DMCB,ACMACOB,ACMNCOB,ACMCOBNL,ACMCOBNL,0                
                                                                                
BLDBUFX  ICM   R1,15,ACMNCOB       GET NUMBER OF ENTRIES                        
         BZ    *+16                NONE                                         
         MHI   R1,ACMCOBNL                                                      
         A     R1,ACMACOB                                                       
         ST    R1,ACMACOBX                                                      
         J     EXIT                                                             
         DROP  R3,RB                                                            
         EJECT                                                                  
***********************************************************************         
* POST VALUES FOR ANY LEVEL OF ACCOUNT (R2 IS ACCOUNT LEVEL NUMBER)   *         
***********************************************************************         
                                                                                
ACTGET   CHI   R2,1                                                             
         JE    GETLVAR                                                          
         CHI   R2,2                                                             
         JE    GETLVBR                                                          
         CHI   R2,3                                                             
         JE    GETLVCR                                                          
         CHI   R2,4                                                             
         JE    GETLVDR                                                          
         DC    H'0'                                                             
                                                                                
***********************************************************************         
* MOVE RECORD TO RECORD BUFFER                                        *         
***********************************************************************         
                                                                                
GETCPYR  MVC   ADCOMP,ACFILEC                                                   
         LA    RF,ADCOMP                                                        
         J     GETRECD                                                          
                                                                                
GETUNTR  LA    RF,ADUNIT                                                        
         J     GETRECD                                                          
                                                                                
GETLDGR  LA    RF,ADLEDGER                                                      
         J     GETRECD                                                          
                                                                                
GETLVAR  ST    RE,ACMSVRE2                                                      
         LA    R1,ACMLLMDE                                                      
         LHI   R0,3                PASS LAST MODES                              
         GOTOR GOSAVE              LEVCLAST B, A                                
         L     RE,ACMSVRE2                                                      
         LA    RF,ADHEIRA                                                       
         J     GETRECD                                                          
                                                                                
GETLVBR  ST    RE,ACMSVRE2                                                      
         LA    R1,ACMLLMDE                                                      
         LHI   R0,2                PASS LAST MODES                              
         GOTOR GOSAVE              LEVCLAST B                                   
         L     RE,ACMSVRE2                                                      
         LA    RF,ADHEIRB                                                       
         J     GETRECD                                                          
                                                                                
GETLVCR  ST    RE,ACMSVRE2                                                      
         LA    R1,ACMLLMDE                                                      
         LHI   R0,1                PASS LAST MODES                              
         GOTOR GOSAVE              LEVCLAST                                     
         L     RE,ACMSVRE2                                                      
         LA    RF,ADHEIRC                                                       
         J     GETRECD                                                          
                                                                                
GETLVDR  DS    0H                                                               
GETACTR  LA    RF,ADHEIRD                                                       
         J     GETRECD                                                          
         EJECT                                                                  
GETRECD  NTR1  ,                                                                
         LA    R3,4(RF)            R3=A(NEXT RECORD ADDRESS)                    
         L     R0,0(RF)                                                         
         ST    R0,LASTIO                                                        
         L     RE,ACMABUF                                                       
         SR    RF,RF                                                            
         ICM   RF,3,ACCRLEN-ACCRECD(RE)                                         
         STCM  RF,3,LASTLEN                                                     
         LR    R1,RF                                                            
         MVCL  R0,RE                                                            
                                                                                
         CLC   ADHEIRA,LASTIO                                                   
         JNE   *+10                                                             
         MVC   LEVAIO(6),LASTIO                                                 
         CLC   ADHEIRB,LASTIO                                                   
         JNE   *+10                                                             
         MVC   LEVBIO(6),LASTIO                                                 
         CLC   ADHEIRC,LASTIO                                                   
         JNE   *+10                                                             
         MVC   LEVCIO(6),LASTIO                                                 
                                                                                
         L     RE,LASTIO                                                        
         LR    R1,RE                                                            
         AH    R1,DATADISP                                                      
         SR    R0,R0                                                            
GETREC02 CLI   0(R1),0             FIND THE END OF RECORD                       
         JE    *+14                                                             
         IC    R0,1(R1)                                                         
         AR    R1,R0                                                            
         J     GETREC02                                                         
                                                                                
         CLM   RE,15,ADLEDGER      IF WE HAVE JUST READ A LEDGER THEN           
         JNE   GETREC14            WE'LL ADD ON WORK CODE ELEMENTS              
         MVC   ACMWRK(L'WCOCODE),LDGKUNT-LDGRECD(RE)                            
         L     R2,ACMACBK                                                       
         LA    R0,ACMWCBN                                                       
*&&UK                                                                           
         L     RE,ACMAWCTL                                                      
         XC    0(4,RE),0(RE)                                                    
         L     RE,ACMAWCCL                                                      
         XC    0(4,RE),0(RE)                                                    
*&&                                                                             
         USING ACMWCBK,R2                                                       
         USING WCOELD,R1                                                        
GETREC04 CLI   0(R2),0             TEST END OF BLOCK                            
         JE    GETREC14                                                         
         CLC   ACMWCUL,ACMWRK      TEST MATCH ON UNIT/LEDGER                    
         JNE   GETREC12                                                         
         MVC   WCOELD(L'ACMWCEL),ACMWCEL                                        
*&&UK                                                                           
         CLC   =C'**',WCOCODE      IGNORE ORDER WORK-CODE                       
         JE    GETREC10                                                         
         CLC   =C'99',WCOCODE      IGNORE BILL WORK-CODE                        
         JE    GETREC10                                                         
                                                                                
         CLI   RCCTRY,CTRYGER      TEST GERMANY                                 
         JNE   GETREC06                                                         
         L     RE,ACMAWCTL                                                      
         CLC   WCOCODE,=C'00'      TIME WORK-CODES < C'00'                      
         JNH   GETREC08                                                         
         L     RE,ACMAWCCL         COST WORK-CODES > C'00'                      
         J     GETREC08                                                         
                                                                                
GETREC06 L     RE,ACMAWCTL                                                      
         TM    WCOSTAT,WCOSHCOE    TEST TIME WORK-CODE                          
         JNZ   GETREC08                                                         
         L     RE,ACMAWCCL                                                      
                                                                                
GETREC08 L     RF,0(RE)                                                         
         AHI   RF,1                                                             
         ST    RF,0(RE)                                                         
         SLL   RF,1                                                             
         LA    RF,2(RE,RF)                                                      
         MVC   0(L'WCOCODE,RF),WCOCODE                                          
         XC    L'WCOCODE(L'WCOCODE,RF),L'WCOCODE(RF)                            
*&&                                                                             
GETREC10 AHI   R1,L'ACMWCEL                                                     
GETREC12 AHI   R2,ACMWCBL                                                       
         JCT   R0,GETREC04                                                      
         DROP  R1,R2                                                            
                                                                                
GETREC14 XC    0(2,R1),0(R1)                                                    
         AHI   R1,200              ALLOW 200 BYTES GROWTH                       
         ST    R1,0(R3)            SET ADDRESS OF NEXT RECORD                   
         J     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* POST VALUES FOR ANY LEVEL OF ACCOUNT (R2 IS ACCOUNT LEVEL NUMBER)   *         
***********************************************************************         
                                                                                
PSTACT   CHI   R2,1                                                             
         JE    PSTLVA                                                           
         CHI   R2,2                                                             
         JE    PSTLVB                                                           
         CHI   R2,3                                                             
         JE    PSTLVC                                                           
         CHI   R2,4                                                             
         JE    PSTLVD                                                           
         DC    H'0'                                                             
                                                                                
***********************************************************************         
* POST VALUES FOR LEVEL 1 ACCOUNT RECORD                              *         
***********************************************************************         
                                                                                
PSTLVA   NTR1  ,                                                                
         GOTOR PROFILES,DMCB,ADHEIRA  GET ACCOUNT PROFILES                      
         XC    ADLVBELS(ADLVDELX-ADLVBELS),ADLVBELS                             
         XC    ACMABXP(8),ACMABXP                                               
         XC    ACMLLMDE,ACMLLMDE                                                
         XC    ACMLFMDE,ACMLFMDE                                                
         XC    ACMPRMDE,ACMPRMDE                                                
         MVI   MODE,PROCLEVA                                                    
         MVI   ACMPRMDE,PROCLEVA                                                
         MVI   ACMLFMDE,LEVAFRST                                                
         MVC   FCRULMED,SPACES                                                  
         GOTOR PSTELS,ADHEIRA      POST ELEMENT ADDRESSES                       
         MVC   ADLVANAM(ADLVAELX-ADLVAELS),ADACCELS                             
         MVC   ACMAAXP,ACMAXPRO                                                 
         ICM   RE,15,ADLVASUP                                                   
         JZ    PSTALLX                                                          
         L     RF,ACMAPRO                                                       
         ST    RF,ADPROFIL                                                      
         MVC   0(256,RF),0(RE)                                                  
         MVC   ACMXPRO,XPROFDEF    SET UP STANDARD PROFILE                      
         LA    R0,ACMXPRO                                                       
         ST    R0,ADXTPROF                                                      
         ICM   R1,15,ACMAAXP                                                    
         JZ    *+8                                                              
         GOTOR SETXPR                                                           
         GOTOR PSTOPT,ADHEIRA                                                   
         CLI   ACMDPTH,1           TEST ONE LEVEL LEDGER                        
         JE    PSTALLX                                                          
         ICM   R1,15,ADLVASUP                                                   
         JZ    PSTALLX                                                          
         GOTOR FLTBGP              APPLY QBILGRUP FILTER                        
         JE    PSTALLX                                                          
         LH    RF,ACMLVA                                                        
         IC    R1,KEY+3(RF)        INCREMENT LAST BYTE OF KEY                   
         AHI   R1,1                                                             
         STC   R1,KEY+3(RF)                                                     
         MVC   ACMKEY,KEYSAVE                                                   
         GOTOR HIGH                                                             
         MVC   KEYSAVE,ACMKEY                                                   
         J     EXITHI                                                           
         EJECT                                                                  
***********************************************************************         
* POST VALUES FOR LEVEL 2 ACCOUNT RECORD                              *         
***********************************************************************         
                                                                                
PSTLVB   NTR1  ,                                                                
         CLI   ACMSYS,ACMSUSA                                                   
         JNE   PSTLVB02                                                         
         L     R1,ADHEIRB                                                       
         CLC   =C'1R',ACTKUNT-ACTRECD(R1)                                       
         JNE   PSTLVB02                                                         
         GOTOR PROFILES,DMCB,ADHEIRB                                            
                                                                                
PSTLVB02 XC    ADLVCELS(ADLVDELX-ADLVDELS),ADLVCELS                             
         XC    ACMACXP,ACMACXP                                                  
         MVI   MODE,PROCLEVB                                                    
         MVI   ACMPRMDE+1,PROCLEVB                                              
         XC    ACMPRMDE+2(2),ACMPRMDE+2                                         
         MVI   ACMLFMDE+1,LEVBFRST                                              
         MVI   ACMLFMDE+2,0                                                     
         MVC   FCRULMED,SPACES                                                  
         GOTOR PSTELS,ADHEIRB      POST ELEMENT ADDRESSES                       
         MVC   ADLVBELS(ADLVBELX-ADLVBELS),ADACCELS                             
         MVC   ACMABXP,ACMAXPRO                                                 
         ICM   RE,15,ADLVASUP      RE=A(CLIENT PROFILE)                         
         JZ    PSTALLX             NONE-CANNOT HAVE OVERRIDES                   
         L     RF,ACMAPRO                                                       
         ST    RF,ADPROFIL                                                      
         MVC   0(256,RF),0(RE)     MOVE CLIENT PROFILE TO COMPOSITE             
         ICM   R1,15,ADLVBSUP                                                   
         JZ    *+8                                                              
         GOTOR SETPPR                                                           
         ICM   R1,15,ACMABXP                                                    
         JZ    *+8                                                              
         GOTOR SETXPR                                                           
         GOTOR PSTOPT,ADHEIRB                                                   
         J     PSTALLX                                                          
         EJECT                                                                  
***********************************************************************         
* POST VALUES FOR LEVEL 3 ACCOUNT RECORD                              *         
***********************************************************************         
                                                                                
PSTLVC   NTR1  ,                                                                
         XC    ADLVDELS(ADLVDELX-ADLVDELS),ADLVDELS                             
         MVI   MODE,PROCLEVC                                                    
         MVI   ACMPRMDE+2,PROCLEVC                                              
         MVI   ACMPRMDE+3,0                                                     
         MVI   ACMLFMDE+2,LEVCFRST                                              
         GOTOR PSTELS,ADHEIRC      POST ELEMENT ADDRESSES                       
         MVC   ADLVCELS(ADLVCELX-ADLVCELS),ADACCELS                             
         MVC   ACMACXP,ACMAXPRO                                                 
         LH    RE,ACMLVB                                                        
         L     R1,ADHEIRC                                                       
         LA    R1,ACTKACT+1-ACTRECD(R1,RE)                                      
         MVC   FCRULMED,0(R1)                                                   
         ICM   RE,15,ADLVASUP                                                   
         JZ    PSTALLX                                                          
         L     RF,ACMAPRO                                                       
         ST    RF,ADPROFIL                                                      
         MVC   0(256,RF),0(RE)                                                  
         ICM   R1,15,ADLVBSUP                                                   
         JZ    *+8                                                              
         GOTOR SETPPR                                                           
         ICM   R1,15,ADLVCSUP                                                   
         JZ    *+8                                                              
         GOTOR SETPPR                                                           
         MVC   ACMXPRO,XPROFDEF                                                 
         ICM   R1,15,ACMAAXP                                                    
         JZ    *+8                                                              
         GOTOR SETXPR                                                           
         ICM   R1,15,ACMABXP                                                    
         JZ    *+8                                                              
         GOTOR SETXPR                                                           
         ICM   R1,15,ACMACXP                                                    
         JZ    *+8                                                              
         GOTOR SETXPR                                                           
         GOTOR PSTOPT,ADHEIRC                                                   
         J     PSTALLX                                                          
         EJECT                                                                  
***********************************************************************         
* POST VALUES FOR LEVEL 4 ACCOUNT RECORD                              *         
***********************************************************************         
                                                                                
PSTLVD   NTR1  ,                                                                
         MVI   MODE,PROCLEVD                                                    
         MVI   ACMPRMDE+3,PROCLEVD                                              
         GOTOR PSTELS,ADHEIRD      POST ELEMENT ADDRESSES                       
         MVC   ADLVDELS(ADLVDELX-ADLVDELS),ADACCELS                             
         J     PSTALLX                                                          
                                                                                
***********************************************************************         
* EXIT FROM ACCOUNT POST ROUTINES                                     *         
***********************************************************************         
                                                                                
PSTALLX  GOTOR FLTACT              FILTER UP TO PRESENT LEVEL                   
         JNE   PSTXIT              IF DIDN'T PASS TEST DON'T PASS ANY           
         LA    R1,ACMPRMDE                                                      
         LHI   R0,4                                                             
         GOTOR GOSAVE              ELSE PASS ALL SAVED PROCMODES                
PSTXIT   J     EXITEQ              SET CC=EQ FOR CALLER                         
         EJECT                                                                  
***********************************************************************         
* PASS TRANSACTION MODES TO APPLICATION PROGRAM                       *         
***********************************************************************         
                                                                                
TRNMODE  ST    RE,ACMSVRE          WHEN I GET PROCTRNS OR PROCHIST              
         LA    R1,ACMFMODE         PASS ALL SAVED FIRST TIME MODES              
         LHI   R0,ACMFMDET+1-ACMFMODE                                           
                                                                                
TRNMOD02 MVC   MODE,0(R1)                                                       
         CLI   FCSEQ,FCSEQOFF                                                   
         JE    TRNMOD04                                                         
         CLI   MODE,ACCFRST                                                     
         JNE   *+8                                                              
         MVI   ACMLMACC,ACCLAST    IF I SENT FIRST MUST SEND LAST               
         CLI   MODE,OFFIRST                                                     
         JNE   *+8                                                              
         MVI   ACMLMAOF,OFFLAST                                                 
         J     TRNMOD06                                                         
                                                                                
TRNMOD04 CLI   MODE,OFFIRST        ADJUST MODES FOR OFFICE SEQUENCE             
         JNE   *+8                                                              
         MVI   ACMLMOFA,OFFLAST                                                 
         CLI   MODE,ACCFRST                                                     
         JNE   *+8                                                              
         MVI   ACMLMACC,ACCLAST                                                 
                                                                                
TRNMOD06 CLI   MODE,SBACFRST                                                    
         JNE   *+8                                                              
         MVI   ACMLMSBA,SBACLAST                                                
         CLI   MODE,WCTFRST                                                     
         JNE   *+8                                                              
         MVI   ACMLMWCT,WCTLAST                                                 
         CLI   MODE,ANALFRST                                                    
         JNE   TRNMOD10                                                         
                                                                                
         MVC   ACMSVAT2,ADTRANS    SAVE ADTRANS                                 
         L     RF,ACMAANL          SET UP DUMMY 42 TO GET RATE                  
         ST    RF,ADTRANS          AND PASS ANALYSIS CODE                       
         USING TRNELD,RF                                                        
         MVI   TRNEL,PRUELQ                                                     
         MVI   TRNLN,4                                                          
         MVC   TRNDATE(L'ACMWRKC),ACMWRKC                                       
         MVC   TRNANAL,ACMWRKC                                                  
         DROP  RF                                                               
                                                                                
         CLI   FCGETOPT,FCGETOWC   TEST GETOPT AT ANALFRST                      
         JNE   TRNMOD08                                                         
         CLI   MODE,ANALFRST       TEST FIRST FOR WORK CODE                     
         JNE   TRNMOD08                                                         
         L     RF,ADGOBLOC                                                      
         USING GOBLOCKD,RF         RF=A(GOBLOCK)                                
         MVC   GOSELWC,ACMWRKC                                                  
         MVC   GOAKEY,ACMALTN                                                   
         DROP  RF                                                               
         GOTOR GETPOP              RESOLVE WORK CODE LEVEL OPTIONS              
                                                                                
TRNMOD08 MVI   ACMLMANA,ANALLAST                                                
         GOTOR GO                  PASS ANALFRST                                
         MVC   ADTRANS,ACMSVAT2    RESTORE ADTRANS                              
         L     R3,ACMABUF          RESTORE ADDRESS OF BUFFER                    
         J     TRNMOD16                                                         
                                                                                
TRNMOD10 STM   R0,R1,ACMDUB        PROTECT R0/R1 FROM GOTOR CALLS               
         CLI   MODE,PROCTRNS       TEST PROCTRNS                                
         JNE   TRNMOD14                                                         
         CLC   ACMPROD,QUNIT       TEST PRODUCTION LEDGER                       
         JNE   TRNMOD14                                                         
         L     RF,ADTRANS                                                       
         SH    RF,DATADISP                                                      
         TM    ACMHIST,ACMHPRC     TEST PROCESSING HISTORY RECORD               
         JZ    *+8                                                              
         L     RF,ACMHIO           YES - TRANSACTION IS SOMEWHERE ELSE          
                                                                                
         CLI   FCPRORAT,NO         TEST PRORATA CALLS REQUIRED                  
         JE    TRNMOD12                                                         
         GOTOR ACMAPRAT,ACMPARA,(X'80',(RF)),ADGOBLOC,ADCOMFAC,0,      *        
               ACMAPROB,ACMAPRO2                                                
         ORG   *-2                                                              
         CLI   FCPRORAT,C'B'       TEST BILLING INFO BLOCK REQUIRED             
         JNE   *+8                                                              
         OI    0(R1),X'40'         YES - SET INDICATOR IN PRORATA PLIST         
         BASR  RE,RF                                                            
         J     TRNMOD14                                                         
                                                                                
TRNMOD12 DS    0H                                                               
*&&US                                                                           
         CLI   FCPTACNV,YES        CONVERT PTA'S?                               
         JNE   TRNMOD14            NO                                           
         GOTOR ACMAPTAC,ACMPARA,(X'80',(RF)),ADCOMFAC,0                         
*&&                                                                             
TRNMOD14 LM    R0,R1,ACMDUB        RESTORE SAVED REGISTERS                      
         CLI   MODE,0                                                           
         JE    TRNMOD16                                                         
         GOTOR GO                                                               
                                                                                
TRNMOD16 MVI   0(R1),0             CLEAR LAST MODE                              
         AHI   R1,1                NEXT MODE                                    
         BRCT  R0,TRNMOD02                                                      
                                                                                
TRNMODEX L     RE,ACMSVRE                                                       
         BR    RE                                                               
         DROP  R8                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO PUT RECORDS TO SEQUENTIAL RECOVERY OUTPUT FILE           *         
*                                                                     *         
* NTRY - R1=A(RECOVERY RECORD)                                        *         
***********************************************************************         
                                                                                
PUTRCV   NTR1  BASE=*,LABEL=*                                                   
         BASR  R9,0                                                             
         AHI   R9,GLOBALS-*                                                     
         USING GLOBALS,R9          R9=A(GLOBAL LITERALS)                        
         L     RA,VACWORKC                                                      
         USING ACWORKD,RA          RA=A(GLOBAL WORKING STORAGE)                 
         L     RC,AMONACC                                                       
         USING ACMD,RC             RC=A(MONACC WORKING STORAGE)                 
         L     R5,ADMASTC                                                       
         USING MASTD,R5            R5=A(MASTER WORKING STORAGE)                 
         LR    R3,R1               R3=A(RECOVERY RECORD)                        
         L     R2,ADRCVT                                                        
         PUT   (R2),(R3)                                                        
         CLI   MCRECOVR,C'2'       TEST TWO OUTPUT FILES REQUESTED              
         BNE   PUTRCVX                                                          
         L     R2,ADRCVC                                                        
         PUT   (R2),(R3)                                                        
PUTRCVX  J     EXIT                                                             
         EJECT                                                                  
GLOBALS  DS    0D                  ** GLOBAL LITERALS **                        
         LTORG                                                                  
                                                                                
VCONVMOS DC    V(CONVMOS)                                                       
VPRINTF  DC    V(PRINTF)                                                        
VCOMC    DC    V(COMC)                                                          
VPPGPRT  DC    V(PPGPRINT)                                                      
*&&UK                                                                           
VTOBACCO DC    V(TOBACCO)                                                       
*&&                                                                             
AHSTTRN  DC    A(HSTTRN)                                                        
VACWORKC DC    V(ACWORKC)                                                       
VFLTBUF  DC    V(FILTBUFF)                                                      
VSHMUSS  DC    V(DMSHMUSS)                                                      
VISGENQ  DC    V(DMISGENQ)                                                      
                                                                                
SVCPY    DC    X'40'                                                            
T00A     DC    C'R',X'000A'                                                     
                                                                                
RQFLTTAB DS    0AL2                                                             
         DC    AL2(ACQTYP1-ACQD)                                                
RQFLTQ   EQU   *-RQFLTTAB          LENGTH OF ENTRY                              
         DC    AL2(ACQTYP2-ACQD)                                                
         DC    AL2(ACQTYP3-ACQD)                                                
         DC    AL2(ACQTYP4-ACQD)                                                
         DC    AL2(ACQTYP5-ACQD)                                                
         DC    AL2(ACQTYP6-ACQD)                                                
         DC    AL2(ACQTYP7-ACQD)                                                
         DC    AL2(ACQTYP8-ACQD)                                                
         DC    AL2(0)              END OF TABLE                                 
                                                                                
CURTAB   DS    0XL4                ** DEFAULT CURRENCY CODES **                 
       ++INCLUDE ACCURTAB                                                       
CURTABX  DC    AL1(0)                                                           
                                                                                
RIREVTAB DS    0XL4                ** REVERSAL TABLE (SEE REVTABD) **           
*&&UK*&& DC    C'10',AL1(00,REVTRQST+REVTREVN+REVTREVY)                         
*&&UK*&& DC    C'31',AL1(20,REVTRQST+REVTREVN)                                  
*&&UK*&& DC    C'37',AL1(00,REVTRQST+REVTREVN+REVTREVY)                         
*&&UK*&& DC    C'38',AL1(00,REVTRQST+REVTREVN)                                  
*&&UK*&& DC    C'PB',AL1(17,REVTRQST+REVTREVN)                                  
*&&UK*&& DC    C'21',AL1(00,REVTRQST+REVTREVN)                                  
*&&UK*&& DC    C'22',AL1(00,REVTRQST+REVTREVN)                                  
*&&UK*&& DC    C'P6',AL1(05,REVTRQST+REVTREVN)                                  
*&&UK*&& DC    C'63',AL1(18,REVTRQST+REVTREVN)                                  
*&&UK*&& DC    C'25',AL1(00,REVTRQST+REVTREVN+REVTREVY)                         
*&&UK*&& DC    C'81',AL1(00,REVTRQST+REVTREVN+REVTREVY)                         
*&&UK*&& DC    C'D5',AL1(00,REVTRQST+REVTREVN+REVTREVY)                         
*&&UK*&& DC    C'98',AL1(00,REVTRQST+REVTREVN+REVTREVY)                         
*&&UK*&& DC    C'UP',AL1(00,REVTRQST+REVTREVN+REVTREVY)                         
*&&UK*&& DC    C'BU',AL1(00,REVTREVN+REVTREVY)                                  
*&&UK*&& DC    C'50',AL1(00,REVTREVN+REVTREVY)                                  
*&&UK*&& DC    C'57',AL1(03,REVTREVN)                                           
                                                                                
*&&US*&& DC    C'31',AL1(17,REVTRQST+REVTREVN+REVTREVY)                         
*&&US*&& DC    C'36',AL1(01,REVTRQST+REVTREVN+REVTREVY)                         
*&&US*&& DC    C'38',AL1(04,REVTRQST+REVTREVN+REVTREVY)                         
*&&US*&& DC    C'3D',AL1(16,REVTRQST+REVTREVN+REVTREVY)                         
*&&US*&& DC    C'61',AL1(11,REVTRQST+REVTREVN+REVTREVY)                         
*&&US*&& DC    C'67',AL1(00,REVTRQST+REVTREVN+REVTREVY)                         
*&&US*&& DC    C'98',AL1(08,REVTRQST+REVTREVN+REVTREVY)                         
*&&US*&& DC    C'C1',AL1(05,REVTRQST+REVTREVN)                                  
*&&US*&& DC    C'CF',AL1(03,REVTRQST+REVTREVN+REVTREVY)                         
*&&US*&& DC    C'GT',AL1(03,REVTRQST+REVTREVN+REVTREVY)                         
*&&US*&& DC    C'P1',AL1(02,REVTRQST+REVTREVN+REVTREVY)                         
*&&US*&& DC    C'XC',AL1(00,REVTRQST+REVTREVN+REVTREVY)                         
                                                                                
         DC    C'RL',AL1(00,REVTRQST+REVTREVN+REVTREVY)                         
         DC    C'RP',AL1(00,REVTRQST+REVTREVN+REVTREVY)                         
         DC    C'IL',AL1(00,REVTRQST+REVTREVN+REVTREVY)                         
         DC    C'IP',AL1(00,REVTRQST+REVTREVN+REVTREVY)                         
         DC    C'PL',AL1(00,REVTRQST+REVTREVN+REVTREVY)                         
         DC    C'PP',AL1(00,REVTRQST+REVTREVN+REVTREVY)                         
         DC    C'XL',AL1(00,REVTRQST+REVTREVN+REVTREVY)                         
         DC    C'XP',AL1(00,REVTRQST+REVTREVN+REVTREVY)                         
         DC    C'XS',AL1(00,REVTRQST+REVTREVN+REVTREVY)                         
         DC    C'VL',AL1(00,REVTRQST+REVTREVN+REVTREVY)                         
         DC    C'VP',AL1(00,REVTRQST+REVTREVN+REVTREVY)                         
*&&UK*&& DC    C'  ',AL1(00,REVTRQST+REVTREVN)                                  
*&&US*&& DC    C'  ',AL1(00,REVTREVN+REVTREVY)                                  
RIREVNUM EQU   (*-RIREVTAB)/L'RIREVTAB                                          
                                                                                
RIDRATAB DS    0XL(DRATABL)        ** DRAFT TABLE (SEE DRATABD) **              
*&&US*&& DC    C'LA',AL1(00,DRATRQST)                                           
         DC    C'RL',AL1(00,DRATRQST+DRATDRAN+DRATDRAY)                         
         DC    C'RP',AL1(00,DRATRQST+DRATDRAN+DRATDRAY)                         
         DC    C'IL',AL1(00,DRATRQST+DRATDRAN+DRATDRAY)                         
         DC    C'IP',AL1(00,DRATRQST+DRATDRAN+DRATDRAY)                         
         DC    C'PL',AL1(00,DRATRQST+DRATDRAN+DRATDRAY)                         
         DC    C'PP',AL1(00,DRATRQST+DRATDRAN+DRATDRAY)                         
         DC    C'XL',AL1(00,DRATRQST+DRATDRAN+DRATDRAY)                         
         DC    C'XP',AL1(00,DRATRQST+DRATDRAN+DRATDRAY)                         
         DC    C'VL',AL1(00,DRATRQST+DRATDRAN+DRATDRAY)                         
         DC    C'VP',AL1(00,DRATRQST+DRATDRAN+DRATDRAY)                         
         DC    C'  ',AL1(00,DRATDRAN)  DEFAULT = LIVE ONLY                      
RIDRANUM EQU   (*-RIDRATAB)/L'RIDRATAB                                          
                                                                                
RIXJBTAB DS    0XL(XJBTABL)        ** XJOB TABLE (SEE XJBTABD) **               
*&&US*&& DC    C'12',AL1(00,XJBTXJBN+XJBTXJBY)                                  
*&&US*&& DC    C'59',AL1(00,XJBTRQST+XJBTXJBN)                                  
*&&US*&& DC    C'61',AL1(00,XJBTRQST+XJBTXJBN)                                  
*&&US*&& DC    C'67',AL1(29,XJBTRQST+XJBTXJBN)                                  
*&&US*&& DC    C'73',AL1(00,XJBTRQST+XJBTXJBN)                                  
*&&US*&& DC    C'BU',AL1(00,XJBTRQST+XJBTXJBN)                                  
*&&US*&& DC    C'DT',AL1(00,XJBTXJBN+XJBTXJBY)                                  
*&&US*&& DC    C'P1',AL1(00,XJBTRQST+XJBTXJBN)                                  
*&&US*&& DC    C'P2',AL1(00,XJBTRQST+XJBTXJBN)                                  
*&&US*&& DC    C'P3',AL1(00,XJBTRQST+XJBTXJBN)                                  
*&&US*&& DC    C'P4',AL1(00,XJBTRQST+XJBTXJBN)                                  
*&&US*&& DC    C'VL',AL1(00,XJBTRQST+XJBTXJBN+XJBTXJBY)                         
*&&US*&& DC    C'VP',AL1(00,XJBTRQST+XJBTXJBN+XJBTXJBY)                         
*&&US*&& DC    C'XH',AL1(00,XJBTRQST+XJBTXJBN+XJBTXJBY)                         
*&&US*&& DC    C'XW',AL1(00,XJBTRQST+XJBTXJBN+XJBTXJBY)                         
*&&US*&& DC    C'X9',AL1(00,XJBTRQST+XJBTXJBN+XJBTXJBY)                         
*&&US*&& DC    C'ZT',AL1(00,XJBTRQST+XJBTXJBN+XJBTXJBY)                         
*MN                                                                             
*&&US*&& DC    C'XB',AL1(00,XJBTRQST+XJBTXJBN+XJBTXJBY)                         
*&&US*&& DC    C'JS',AL1(00,XJBTXJBN+XJBTXJBY)                                  
*MN                                                                             
         DC    C'  ',AL1(00,XJBTXJBN)  DEFAULT = NON-XJOB ONLY                  
RIXJBNUM EQU   (*-RIXJBTAB)/L'RIXJBTAB                                          
                                                                                
RIDJBTAB DS    0XL(DJBTABL)        ** DRAFT JOB TABLE (SEE DJXTABD) **          
*&&US*&& DC    C'73',AL1(00,DJBTRQST+DJBTDJBN)                                  
*&&US*&& DC    C'VL',AL1(00,DJBTRQST+DJBTDJBN)                                  
*&&US*&& DC    C'VP',AL1(00,DJBTRQST+DJBTDJBN)                                  
         DC    C'  ',AL1(00,DJBTDJBN)  DEFAULT = NON-DRAFT JOBS ONLY            
RIDJBNUM EQU   (*-RIDJBTAB)/L'RIDJBTAB                                          
                                                                                
RICLOTAB DS    0XL(CLOTABL)        ** CLOSED TABLE (SEE CLOTABD) **             
*&&UK*&& DC    C'62',AL1(00,CLOTRQST+CLOTCLON)                                  
*&&UK*&& DC    C'77',AL1(00,CLOTRQST+CLOTCLON)                                  
*&&UK*&& DC    C'VL',AL1(00,CLOTRQST+CLOTCLOY+CLOTCLON)                         
         DC    C'  ',AL1(00,CLOTRQST+CLOTCLOY+CLOTCLON)                         
RICLONUM EQU   (*-RICLOTAB)/L'RICLOTAB                                          
                                                                                
RILOKTAB DS    0XL(LOKTABL)        ** LOCKED TABLE (SEE LOKTABD) **             
*&&UK*&& DC    C'77',AL1(00,LOKTRQST+LOKTLOKN)                                  
*&&UK*&& DC    C'C2',AL1(00,LOKTRQST+LOKTLOKN)                                  
*&&UK*&& DC    C'C3',AL1(00,LOKTRQST+LOKTLOKN)                                  
*&&UK*&& DC    C'C4',AL1(00,LOKTRQST+LOKTLOKN)                                  
*&&UK*&& DC    C'IL',AL1(00,LOKTRQST+LOKTLOKY+LOKTLOKN)                         
*&&UK*&& DC    C'PL',AL1(00,LOKTRQST+LOKTLOKY+LOKTLOKN)                         
*&&UK*&& DC    C'RL',AL1(00,LOKTRQST+LOKTLOKY+LOKTLOKN)                         
*&&UK*&& DC    C'RL',AL1(00,LOKTRQST+LOKTLOKY+LOKTLOKN)                         
*&&UK*&& DC    C'VL',AL1(00,LOKTRQST+LOKTLOKY+LOKTLOKN)                         
*&&UK*&& DC    C'XL',AL1(00,LOKTRQST+LOKTLOKY+LOKTLOKN)                         
         DC    C'  ',AL1(00,LOKTRQST+LOKTLOKY+LOKTLOKN)                         
RILOKNUM EQU   (*-RILOKTAB)/L'RILOKTAB                                          
                                                                                
RIEXCTAB DS    0XL(EXCTABL)        ** EXCLUDE TABLE (SEE EXCTABD) **            
         DC    C'P7',AL1(EXCTRQST+EXCTEXCN)                                     
         DC    C'DB',AL1(EXCTRQST+EXCTEXCN)                                     
         DC    C'EB',AL1(EXCTRQST+EXCTEXCN)                                     
         DC    C'  ',AL1(EXCTRQST+EXCTEXCN+EXCTEXCY)                            
RIEXCNUM EQU   (*-RIEXCTAB)/L'RIEXCTAB                                          
                                                                                
BC01     DC    C'BC01'                                                          
BL01     DC    C'BL01'                                                          
BL09     DC    C'BL09'                                                          
BL12     DC    C'BL12'                                                          
                                                                                
CFPCLOSE DC    C'CLOSE'                                                         
CFPCLERR DC    C'CLO/ERR'                                                       
CFPKEEP  DC    C'KEEP'                                                          
CFFACWRK DC    C'FACWRK '                                                       
CFEOFREC DC    X'00400000FFFFFFFFFFFFFFFF'                                      
                                                                                
QDATETAB DC    AL2(ACQTYP1-ACQD)                                                
         DC    AL2(ACQTYP2-ACQD)                                                
         DC    AL2(ACQTYP3-ACQD)                                                
         DC    AL2(ACQTYP4-ACQD)                                                
         DC    AL2(ACQTYP5-ACQD)                                                
         DC    AL2(ACQTYP6-ACQD)                                                
         DC    AL2(ACQTYP7-ACQD)                                                
         DC    AL2(ACQTYP8-ACQD)                                                
QDATETQ  EQU   (*-QDATETAB)/2                                                   
                                                                                
DATTAB   DS    0XL(DATTABL)        ** DATE DISPLACEMENTS/TYPES **               
         DC    AL2(ACQSTART-ACQD),AL1(0)                                        
         DC    AL2(ACQEND-ACQD),AL1(0)                                          
         DC    AL2(ACQMOSPD-ACQD),AL1(DATITWO+DATIYMO)                          
         DC    AL2(ACQACTST-ACQD),AL1(DATITWO)                                  
         DC    AL2(ACQDUEST-ACQD),AL1(DATITWO+DATIDUE)                          
*&&UK*&& DC    AL2(ACQEPDST-ACQD),AL1(DATITWO)                                  
         DC    AL2(ACQDTSTR-ACQD),AL1(DATITWO)                                  
         DC    AL2(ACQDTEND-ACQD),AL1(DATITWO)                                  
         DC    AL2(ACQTYP1-ACQD),AL1(DATIFLT+DATITWO)                           
         DC    AL2(ACQTYP2-ACQD),AL1(DATIFLT+DATITWO)                           
         DC    AL2(ACQTYP3-ACQD),AL1(DATIFLT+DATITWO)                           
         DC    AL2(ACQTYP4-ACQD),AL1(DATIFLT+DATITWO)                           
         DC    AL2(ACQTYP5-ACQD),AL1(DATIFLT+DATITWO)                           
         DC    AL2(ACQTYP6-ACQD),AL1(DATIFLT+DATITWO)                           
         DC    AL2(ACQTYP7-ACQD),AL1(DATIFLT+DATITWO)                           
         DC    AL2(ACQTYP8-ACQD),AL1(DATIFLT+DATITWO)                           
DATTABX  DC    AL2(0)                                                           
                                                                                
DATTABD  DSECT                     ** DSECT TO COVER DATE TABLE **              
DATDDSP  DS    AL2                 DISPLACEMENT TO DATE                         
DATINDS  DS    X                   INDICATORS                                   
DATIFLT  EQU   X'80'               SCRIBE-LIKE DATE FILTER                      
DATITWO  EQU   X'40'               PERIOD EXPRESSION                            
DATIDUE  EQU   X'20'               CHECK APFLAG                                 
DATIYMO  EQU   X'10'               YYMM ONLY                                    
DATTABL  EQU   *-DATTABD           L'DATE TABLE                                 
ACMASTER CSECT                                                                  
                                                                                
PHASE01  DS    0CL8                                                             
         DC    C'AC'                                                            
PROG01   DC    C'XX'                                                            
         DC    C'01'                                                            
TEST01   DC    C' '                                                             
         DC    C' '                                                             
                                                                                
PHASE02  DS    0CL8                                                             
         DC    C'AC'                                                            
PROG02   DC    C'XX'                                                            
         DC    C'02'                                                            
TEST02   DC    C' '                                                             
         DC    C' '                                                             
                                                                                
SRADJLST DS    0CL6                ** TABLE OF PROG CODES TO ADJUST **          
*&&US*&& DC    C'Z41414'                                                        
*&&US*&& DC    C'Z51515'                                                        
*&&US*&& DC    C'Z22222'                                                        
*&&US*&& DC    C'Z36363'                                                        
*&&US*&& DC    C'Z76767'                                                        
*&&US*&& DC    C'DU2525'                                                        
*&&US*&& DC    C'DP9898'                                                        
*&&UK*&& DC    C'D0D0DC'                                                        
*&&UK*&& DC    C'DQDQDC'                                                        
*&&UK*&& DC    C'DHDHDC'                                                        
*&&UK*&& DC    C'5D5D55'                                                        
*&&UK*&& DC    C'5T5T5P'                                                        
*&&UK*&& DC    C'HDHDHP'                                                        
*&&UK*&& DC    C'6P6P63'                                                        
SRADJNUM EQU   (*-SRADJLST)/L'SRADJLST                                          
                                                                                
EOFR     DC    X'FEFE'                                                          
                                                                                
LDGTAB   DS    0CL2                ** TABLE OF P&L LEDGERS **                   
         DC    C'GP'                                                            
*&&UK*&& DC    C'SD'                                                            
         DC    C'SE'                                                            
         DC    C'SI'                                                            
LDGNUM   EQU   (*-LDGTAB)/L'LDGTAB                                              
                                                                                
HEXCODE  DC    C'0123456789ABCDEF'                                              
                                                                                
XPROFDEF DC    AL1(XPRELQ,XPRLN2Q)                                              
         DC    PL2'10',PL3'10000',PL4'5000',C'YNYN  ',7X'00'                    
                                                                                
SVCPYNAM DS    CL(NAMLN1Q+L'NAMEREC)                                            
                                                                                
DMERR1   DC    CL28'*** DATA MANAGER ERROR  ***'                                
DMERR2   DC    CL28'*** ILLEGAL FILE UPDATE ***'                                
DMERR3   DC    CL28'*** REQUEST NUMBER NNNN ***'                                
         EJECT                                                                  
         DS    0D                                                               
         DC    CL16'*UTL**UTL**UTL**'                                           
UTL      DC    (TUTLXALN)X'00'                                                  
         ORG   UTL+(TSYS-UTLD)                                                  
         DC    X'0A'               SYSTEM 10 (CONTROL)                          
         ORG   UTL+(TUTLXADR-UTLD)                                              
         DC    A(XAUTL)                                                         
         ORG                                                                    
UTLL     EQU   *-UTL                                                            
*                                                                               
         DS    0D                                                               
         DC    CL16'****XAUTL******'                                            
XAUTL    DC    (XASWTABL)X'00'                                                  
         ORG   XAUTL                                                            
         DC    AL4(XAUTLLNT)                                                    
         ORG                                                                    
                                                                                
         DS    0D                                                               
         DC    CL16'*SSB**SSB**SSB**'                                           
SSB      DC    XL(SSOOFFX-SSOOFF)'00'                                           
         ORG   SSB+(SSOXTND-SSBD)                                               
         DC    X'FF'               SET EXTENDED OFFLINE SSB                     
         ORG   SSB+(SSOSTAT2-SSBD)                                              
         DC    AL1(SSOSNRCV)       SET RECOVERY OFF                             
         ORG   SSB+(SSOFWNDX-SSBD)                                              
         DC    V(FACINDX)          FACWRK INDEX                                 
         ORG   SSB+(SSOFWBUF-SSBD)                                              
         DC    V(FACBUFF)          FACWRK BUFFER                                
         ORG   SSB+(SSOFLAG3-SSBD)                                              
         DC    AL1(SSO3XUTL)       OFFLINE EXTENDED UTL                         
         ORG                                                                    
SSBL     EQU   *-SSB                                                            
                                                                                
NO       EQU   C'N'                                                             
YES      EQU   C'Y'                                                             
FF       EQU   X'FF'                                                            
ADJLEN   EQU   (TRNRFST-TRNRECD)-ACCORFST                                       
                                                                                
CORELIST DC    1200X'00'                                                        
         EJECT                                                                  
LDGTABD  DSECT                                                                  
LDGUL    DS    CL2                 UNIT LEDGER CODE                             
LDGLVLS  DS    0XL4                                                             
LDGLV1LN DS    XL1                 LEVEL 1             LENGTH                   
LDGLV2LN DS    XL1                 LEVEL 1 + 2         LENGTH                   
LDGLV3LN DS    XL1                 LEVEL 1 + 2 + 3     LENGTH                   
LDGLV4LN DS    XL1                 LEVEL 1 + 2 + 3 + 4 LENGTH                   
LDGMAXLV DS    XL1                 MAX LEVELS ON THIS U/L                       
LDGAFLT  DS    A                   A(FILTER TABLE)                              
LDGFLT#  DS    A                   NUMBER OF ENTRIES                            
LDGMAX#  DS    A                   MAX NUMBER OF ENTRIES                        
LDGTABSZ DS    A                   SIZE OF TABLE                                
LDGTABQ  EQU   *-LDGTABD                                                        
LDGTABN  EQU   32                                                               
                                                                                
FLTTABD  DSECT                     ** CONTRA-ACCOUNT FILTER TABLE **            
FLTCULA  DS    CL(L'ACTKULA)       UNIT, LEDGER AND ACCOUNT CODE                
FLTCFLT  DS    CL(L'ACMCFLTS)      ACCOUNT FILTERS                              
FLTTABQ  EQU   *-FLTTABD                                                        
FLTTABN  EQU   4000                                                             
         EJECT                                                                  
***********************************************************************         
* DCBS FOR RECOVERY TAPE AND WORK FILES                               *         
***********************************************************************         
                                                                                
ACMASTER CSECT                                                                  
                                                                                
DCBRCVT  DCB   DDNAME=RCVTAPE,DSORG=PS,RECFM=VB,BLKSIZE=27648,         *        
               LRECL=8200,MACRF=(GM,PM),EODAD=RCVGET08                          
                                                                                
DCBRCVC  DCB   DDNAME=RCVCOPY,DSORG=PS,RECFM=VB,BLKSIZE=27648,         *        
               LRECL=8200,MACRF=(GM,PM),EODAD=RCVGET08                          
                                                                                
DCBIN    DCB   DDNAME=IN,DSORG=PS,RECFM=VB,LRECL=1000,BLKSIZE=1000,    *        
               MACRF=GM,EODAD=DCBEOF                                            
DCBEOF   DC    H'0'                                                             
                                                                                
DCBOUT   DCB   DDNAME=OUT,DSORG=PS,RECFM=VB,LRECL=1000,BLKSIZE=1008,   *        
               MACRF=PM                                                         
                                                                                
DCBWORK  DCB   DDNAME=SORTWK01,DSORG=PS,RECFM=FB,LRECL=256,BLKSIZE=256,*        
               MACRF=(RP,WP)                                                    
         EJECT                                                                  
       ++INCLUDE ACMASTD                                                        
         EJECT                                                                  
REVTABD  DSECT                     ** REVERSAL TABLE **                         
REVTPROG DS    CL2                 PROGRAM CODE                                 
REVTPROF DS    XL1                 PROFILE VALUE                                
REVTINDS DS    XL1                 PROGRAM INDICATORS                           
REVTRQST EQU   X'80'               LOOK AT QREVERSE ON REQUEST CARD             
REVTREVY EQU   ACMITRVY            INCLUDE REVERSED TRANSACTIONS                
REVTREVN EQU   ACMITRVN            INCLUDE NON-REVERSED TRANSACTIONS            
REVTABL  EQU   *-REVTABD           LENGTH OF TABLE ENTRY                        
                                                                                
DRATABD  DSECT                     ** DRAFT TABLE **                            
DRATPROG DS    CL2                 PROGRAM CODE                                 
DRATPROF DS    XL1                 PROFILE VALUE                                
DRATINDS DS    XL1                 PROGRAM INDICATORS                           
DRATDRAY EQU   ACMIDRAY            INCLUDE DRAFT TRANSACTIONS                   
DRATDRAN EQU   ACMIDRAN            INCLUDE NON-DRAFT TRANSACTIONS(LIVE)         
DRATRQST EQU   X'20'               LOOK AT QDRAFT ON REQUEST CARD               
DRATABL  EQU   *-DRATABD           LENGTH OF TABLE ENTRY                        
                                                                                
XJBTABD  DSECT                     ** XJOB TABLE **                             
XJBTPROG DS    CL2                 PROGRAM CODE                                 
XJBTPROF DS    XL1                 PROFILE VALUE                                
XJBTINDS DS    XL1                 PROGRAM INDICATORS                           
XJBTXJBY EQU   ACMIXJBY            INCLUDE X-JOBS                               
XJBTXJBN EQU   ACMIXJBN            INCLUDE NON-XJOBS                            
XJBTRQST EQU   X'80'               LOOK AT QXJOB ON REQUEST CARD                
XJBTABL  EQU   *-XJBTABD           LENGTH OF TABLE ENTRY                        
                                                                                
DJBTABD  DSECT                     ** DJOB TABLE **                             
DJBTPROG DS    CL2                 PROGRAM CODE                                 
DJBTPROF DS    XL1                 PROFILE VALUE                                
DJBTINDS DS    XL1                 PROGRAM INDICATORS                           
DJBTDJBY EQU   ACMIDJBY            INCLUDE DRAFT JOBS                           
DJBTDJBN EQU   ACMIDJBN            INCLUDE NON-DRAFT JOBS                       
DJBTRQST EQU   X'80'               LOOK AT QDJOB ON REQUEST CARD                
DJBTABL  EQU   *-DJBTABD           LENGTH OF TABLE ENTRY                        
                                                                                
CLOTABD  DSECT                     ** CLOSED TABLE **                           
CLOTPROG DS    CL2                 PROGRAM CODE                                 
CLOTPROF DS    XL1                 PROFILE VALUE                                
CLOTINDS DS    XL1                 PROGRAM INDICATORS                           
CLOTCLOY EQU   ACMICLOY            INCLUDE CLOSED JOBS                          
CLOTCLON EQU   ACMICLON            INCLUDE OPEN JOBS                            
CLOTRQST EQU   X'01'               LOOK AT QCLOFILT ON REQUEST CARD             
CLOTABL  EQU   *-CLOTABD           LENGTH OF TABLE ENTRY                        
                                                                                
LOKTABD  DSECT                     ** LOCKED TABLE **                           
LOKTPROG DS    CL2                 PROGRAM CODE                                 
LOKTPROF DS    XL1                 PROFILE VALUE                                
LOKTINDS DS    XL1                 PROGRAM INDICATORS                           
LOKTLOKY EQU   ACMILOKY            INCLUDE LOCKED ACCOUNTS                      
LOKTLOKN EQU   ACMILOKN            INCLUDE NOT ACCOUNTS                         
LOKTRQST EQU   X'01'               LOOK AT QLOKFILT ON REQUEST CARD             
LOKTABL  EQU   *-LOKTABD           LENGTH OF TABLE ENTRY                        
                                                                                
EXCTABD  DSECT                     ** EXCLUDE TABLE **                          
EXCTPROG DS    CL2                 PROGRAM CODE                                 
EXCTINDS DS    XL1                 PROGRAM INDICATORS                           
EXCTEXCY EQU   ACMIEXCY            INCLUDE EXCLUDED TRANSACTIONS                
EXCTEXCN EQU   ACMIEXCN            INCLUDE NOT EXCLUDED TRANSACTIONS            
EXCTRQST EQU   X'01'               LOOK AT ACQEXCL ON REQUEST CARD              
EXCTABL  EQU   *-EXCTABD           LENGTH OF TABLE ENTRY                        
                                                                                
GRPTABD  DSECT                     ** ACCOUNT GROUP TABLE **                    
GRPTTYP  DS    CL(L'AGRKGTYP)      GROUP TYPE (SEE AGRKGTYP)                    
GRPTEOTQ EQU   X'FF'               END OF TABLE INDICATOR                       
GRPTCOD  DS    CL(L'AGRKAGR)       GROUP CODE                                   
GRPTNAM  DS    CL(L'NAMEREC)       GROUP NAME                                   
GRPTOFF  DS    CL(L'RSTOFFC)       OFFICE CODE                                  
GRPTABL  EQU   *-GRPTABD                                                        
GRPTMAXN EQU   1024                MAXIMUM NUMBER OF ACCOUNT GROUPS             
                                                                                
* ACQD                                                                          
         PRINT OFF                                                              
       ++INCLUDE ACQD                                                           
         PRINT ON                                                               
                                                                                
* ACRCVRECD                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACRCVRECD                                                      
         PRINT ON                                                               
                                                                                
* DMRCVREXT                                                                     
         PRINT OFF                                                              
       ++INCLUDE DMRCVREXT                                                      
         PRINT ON                                                               
                                                                                
* ACGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
         PRINT ON                                                               
                                                                                
* ACGENMODES                                                                    
         PRINT OFF                                                              
       ++INCLUDE ACGENMODES                                                     
         PRINT ON                                                               
                                                                                
* ACREPWORKD                                                                    
         PRINT OFF                                                              
       ++INCLUDE ACREPWORKD                                                     
         PRINT ON                                                               
                                                                                
* ACREPPROFD                                                                    
         PRINT OFF                                                              
       ++INCLUDE ACREPPROFD                                                     
         PRINT ON                                                               
                                                                                
* ACBUCKETD                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACBUCKETD                                                      
         PRINT ON                                                               
                                                                                
* ACGOBLOCK                                                                     
         PRINT OFF                                                              
GOBLOCKD DSECT                                                                  
       ++INCLUDE ACGOBLOCK                                                      
         PRINT ON                                                               
                                                                                
* ACJOBBLOCK                                                                    
         PRINT OFF                                                              
JBLOCKD  DSECT                                                                  
       ++INCLUDE ACJOBBLOCK                                                     
         PRINT ON                                                               
                                                                                
* ACBIGPRNTD                                                                    
         PRINT OFF                                                              
       ++INCLUDE ACBIGPRNTD                                                     
         PRINT ON                                                               
                                                                                
* CTGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE CTGENFILE                                                      
         PRINT ON                                                               
                                                                                
* DDLOGOD                                                                       
         PRINT OFF                                                              
       ++INCLUDE DDLOGOD                                                        
         PRINT ON                                                               
                                                                                
* DDBIGBOX                                                                      
         PRINT OFF                                                              
       ++INCLUDE DDBIGBOX                                                       
         PRINT ON                                                               
                                                                                
* DDCOMFACS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
                                                                                
* DDCOMCTRLD                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDCOMCTRLD                                                     
         PRINT ON                                                               
                                                                                
* DDREMOTED                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDREMOTED                                                      
         PRINT ON                                                               
                                                                                
* DDREPXTRAD                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDREPXTRAD                                                     
         PRINT ON                                                               
                                                                                
* DDMASTD                                                                       
         PRINT OFF                                                              
       ++INCLUDE DDMASTD                                                        
         PRINT ON                                                               
                                                                                
* DDCTRYEQUS                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDCTRYEQUS                                                     
         PRINT ON                                                               
                                                                                
* DDSOFDATD                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDSOFDATD                                                      
         PRINT ON                                                               
                                                                                
* DDTSARD                                                                       
         PRINT OFF                                                              
       ++INCLUDE DDTSARD                                                        
         PRINT ON                                                               
                                                                                
* DMWRKRK                                                                       
         PRINT OFF                                                              
       ++INCLUDE DMWRKRK                                                        
         PRINT ON                                                               
                                                                                
* DMGREQUS                                                                      
       ++INCLUDE DMGREQUS                                                       
                                                                                
* ACOFFALD                                                                      
         PRINT OFF                                                              
       ++INCLUDE ACOFFALD                                                       
         PRINT ON                                                               
                                                                                
* DDCURTABD                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCURTABD                                                      
         ORG   CURTLONG+L'CURTLONG                                              
CURTABLN EQU   *-CURTABD                                                        
         PRINT ON                                                               
                                                                                
* ACWCNTABD                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACWCNTABD                                                      
         PRINT ON                                                               
*&&UK                                                                           
* ACTOBACCOD                                                                    
         PRINT OFF                                                              
       ++INCLUDE ACTOBACCOD                                                     
         PRINT ON                                                               
*&&                                                                             
* DDLINKD                                                                       
         PRINT OFF                                                              
       ++INCLUDE DDLINKD                                                        
         PRINT ON                                                               
* DDRUNNERD                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDRUNNERD                                                      
         PRINT ON                                                               
* DDCOREQUS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOREQUS                                                      
         PRINT ON                                                               
* FASSB                                                                         
         PRINT OFF                                                              
       ++INCLUDE FASSB                                                          
         ORG   SSBD                                                             
       ++INCLUDE FASSBOFF                                                       
         PRINT ON                                                               
* FAUTL                                                                         
         PRINT OFF                                                              
       ++INCLUDE FAUTL                                                          
         PRINT ON                                                               
                                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'118ACMASTER  07/31/20'                                      
         END                                                                    
