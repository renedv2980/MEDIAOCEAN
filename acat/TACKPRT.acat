*          DATA SET TACKPRT    AT LEVEL 210 AS OF 06/23/16                      
*CATALP TACKPRT                                                                 
         TITLE 'TACKPRT - TALENT PRINT CHECK ROUTINE'                           
TACKPRT  CSECT                                                                  
         ENTRY SSB                                                              
         PRINT NOGEN                                                            
         NMOD1 0,TACKPRT,RA                                                     
         L     RC,0(R1)            RC = A(GENCON WORKING STORAGE)               
         USING GEND,RC                                                          
         L     R8,ASPOOLD          R8 = A(SPOOL DSECT)                          
         USING SPOOLD,R8                                                        
         L     R7,ASUBSYSD         R7 = A(CONTROLLER WORKING STORAGE)           
         USING SUBSYSD,R7                                                       
         LA    R6,BUFF             R6 = A(LOCAL WORKING STORAGE)                
         LA    R6,8(R6)                                                         
         USING CHECKD,R6                                                        
         EJECT                                                                  
* CHECKS PASS2 - READ BACK THE SORT RECORDS AND PRINT A CHECK FOR EACH          
* ONE.  ADDITIONALLY WRITE BACK THE CHECK RECORD FOUND IN EACH SORT             
* RECORD TO THE FILE.                                                           
*                                                                               
MAIN     DS    0H                                                               
         MVC   AIO,AIO1                                                         
*                                  CALL TAPPGUAR TO CLEAR GUAR BUFFER           
         GOTO1 CALLTAP,DMCB,(RC),(X'18',0),,SYSCOMM                             
*                                                                               
         L     RE,ADUETAB          CLEAR DUE COMPANY TABLE                      
         L     RF,=A(MAXDUES*DUELEN+1)                                          
         SR    R1,R1                                                            
         MVCL  RE,R0                                                            
*                                                                               
         L     RE,ALINTAB          CLEAR LIEN TABLE                             
         L     RF,=A(MAXLINS*LINLEN+1)                                          
         SR    R1,R1                                                            
         MVCL  RE,R0                                                            
*                                                                               
         L     RE,ATRSTAB          CLEAR TRUSTEE TABLE                          
         L     RF,=A(MAXW4TS*TRSTLEN+1)                                         
         SR    R1,R1                                                            
         MVCL  RE,R0                                                            
*                                                                               
         L     RF,NOSRTCKS                                                      
         CLI   RECNUM,CK           ONLY FOR TALENT CHECKS                       
         BNE   M05                                                              
         CLC   NOSRTCKS,=F'100'    MIN OF 100 CHECKS BEFORE SPLIT               
         BNL   M03                                                              
         LA    RF,99                                                            
         B     M05                                                              
*                                                                               
M03      SR    RE,RE                                                            
         D     RE,=F'2'            DIVIDE # OF CHECKS BY 2                      
         LTR   RE,RE                                                            
         BZ    *+8                                                              
         AHI   RF,1                                                             
M05      ST    RF,NOSRTCKS                                                      
         C     RF,=F'3000'                                                      
         BL    M06                                                              
         LA    RF,3000                                                          
         ST    RF,NOSRTCKS                                                      
*                                                                               
M06      BAS   RE,INITDWN          INITIALIZE DOWNLOAD                          
         BRAS  RE,FILHEAD          FILE HEADER                                  
*                                                                               
         XC    WORKID,WORKID       BUILD WORKER FILE ID                         
         LA    RF,WORKID                                                        
         USING UKRECD,RF                                                        
         MVC   UKUSRID,ORIGID                                                   
         MVI   UKSYSPRG,C'T'                                                    
         MVC   UKSYSPRG+1(2),QCRDCODE                                           
         MVC   UKDAY,TGTODAY1+2                                                 
         MVI   UKCLASS,C'P'                                                     
         CLI   REQURG,C'U'                                                      
         BNE   M10                                                              
         MVI   UKSYSPRG+2,C'U'     CHECKS WILL BE 'CU', TOM SAID                
         CLI   RECNUM,CK                                                        
         BE    M09                                                              
         MVI   UKSYSPRG+2,C'1'     PRINT CHECKS WILL BE 'P1'                    
         CLI   RECNUM,PC                                                        
         BE    M09                                                              
         MVI   UKSYSPRG+2,C'2'     P+ CHECKS WILL BE 'P2'                       
*                                  CANADA DOESN'T DO URGENT CHECKS              
M09      MVI   UKSUBPRG,C'U'                                                    
         MVI   UKFLAG,1            ALLOW MULTIPLE WORKER FILES                  
*                                                                               
         DROP  RF                                                               
*                                  GET NEXT SORT RECORD                         
M10      GOTO1 SORTER,DMCB,=C'GET'                                              
         OC    DMCB+4(4),DMCB+4                                                 
         BZ    M20                 DONE IF NO RETURN ADDRESS                    
*                                                                               
         L     R0,DMCB+4           MOVE SORT RECORD TO SRTREC                   
         LHI   R1,SRTRECL                                                       
         LA    RE,SRTREC                                                        
         LR    RF,R1                                                            
         MVCL  RE,R0                                                            
*                                                                               
         CLI   SRTTPOF,C' '        IF DON'T HAVE GOOD OFFICE                    
         BH    M15                                                              
         MVI   SRTTPOF,C'1'        DEFAULT TO 1                                 
         CLI   RECNUM,PC           IF PRINT CHECKS                              
         BE    *+8                                                              
         CLI   RECNUM,EC                                                        
         BNE   M15                                                              
         MVI   SRTTPOF,C'A'        DEFAULT TO A                                 
*                                                                               
M15      L     R4,ASRTCHK          R4 = A(CHECK DETAILS ELEMENT)                
         MVI   ELCODE,TACDELQ                                                   
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING TACDD,R4                                                         
*                                                                               
         TM    TACDSTAT,TACDSTRS   GENERATED BY TRUST?                          
         BZ    *+8                                                              
         OI    SRTTRBTS,SRTTTRST                                                
*                                                                               
         TM    TACDSTAT,TACDSLIN   GENERATED BY LIEN?                           
         BZ    *+8                                                              
         OI    SRTBITS,SRTBLIN                                                  
         NI    SRTBITS2,X'FF'-SRTBEFTL                                          
         TM    TACDSTA2,TACDSELN   EFT LIEN?                                    
         BZ    *+8                                                              
         OI    SRTBITS2,SRTBEFTL                                                
*        MVI   LINE,X'99'                                                       
         MVC   BLOCK(14),=CL14' '                                               
         MVC   BLOCK(8),=C'SORT GET'                                            
         ICM   R2,7,TACDSEQ+1                                                   
         EDIT  (R2),(5,BLOCK+9),FILL=0                                          
         GOTO1 DOTRACE,DMCB,(C'G',SRTREC),SRTCHECK-SRTREC,(14,BLOCK)            
         DROP  R4                                                               
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'G',ASRTCHK),0,=C'SRTCHECK'                       
         BAS   RE,PROCCHK          PROCESS CHECK                                
*        B     M10                 REPEAT UNTIL NO MORE CHECKS                  
*                                                                               
         CLI   RECNUM,CK           ONLY FOR TALENT CHECKS                       
         BNE   M10                                                              
         TM    WHEN,X'20'          IF RUNNING SOON, DON'T SPLIT                 
         BO    M10                                                              
         CLC   TOTCNT,NOSRTCKS     DO WE NEED TO SPLIT TO 2 REPORTS?            
         BL    M10                                                              
         BRAS  RE,FILTRLR          FILE TRAILER                                 
*                                                                               
         L     RE,AERITAB          CLEAR ERROR INVOICE TABLE                    
         L     RF,=A(MAXERIS*ERILEN+1)                                          
         SR    R1,R1                                                            
         MVCL  RE,R0                                                            
*                                                                               
         L     RE,AWARTAB          CLEAR WARNING CHECK TABLE                    
         L     RF,=A(MAXWARS*WARLEN+1)                                          
         SR    R1,R1                                                            
         MVCL  RE,R0                                                            
*                                                                               
         MVC   NEXTERI,AERITAB     SET NEXT ERITAB ENTRY TO FIRST               
         MVC   NEXTWAR,AWARTAB     SET NEXT WARTAB ENTRY TO FIRST               
         L     RF,NEXTERI          MARK END OF ERROR INVOICE TABLE              
         MVI   0(RF),X'FF'                                                      
*                                                                               
         BRAS  RE,CLSEDWN          FINISH UP DOWNLOAD                           
         BRAS  RE,NEWPRTQ                                                       
         BRAS  RE,FILHEAD          FILE HEADER                                  
         XC    TOTEARN,TOTEARN     RESET TOTALS                                 
         XC    TOTNTAX,TOTNTAX                                                  
         XC    TOTCVERN,TOTCVERN                                                
         XC    TOTNET,TOTNET                                                    
         XC    TOTCNT,TOTCNT                                                    
         XC    TOTPST,TOTPST                                                    
         XC    TOTSPST,TOTSPST                                                  
         L     RF,NOSRTCKS                                                      
         AHI   RF,1                                                             
         ST    RF,NOSRTCKS                                                      
         B     M10                 REPEAT UNTIL NO MORE CHECKS                  
*                                                                               
*=====================================================================          
M20      BRAS  RE,FILTRLR          FILE TRAILER                                 
         BRAS  RE,CLSEDWN          FINISH UP DOWNLOAD                           
*                                                                               
         TM    WHEN,X'20'          IF RUNNING SOON                              
         BZ    M24                                                              
         GOTO1 CALLTAP,DMCB,(RC),(X'01',0),,SYSCOMM                             
M24      BAS   RE,FLUDUC           FLUSH DUE COMPANY TABLE                      
         BAS   RE,FLULIN           FLUSH LIEN TABLE                             
         BAS   RE,FLUTRST          FLUSH TRUSTEE TABLE                          
         BAS   RE,PROCEND          PRINT TOTALS AND POST ACC ACCOUNTS           
*                                                                               
*                                  READ SYSTEM CONTROL RECORD                   
         GOTO1 RECVAL,DMCB,TLSYCDQ,(X'30',0)                                    
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         L     R4,AIO              R4 = A(SYSTEM CONTROL ELEMENT)               
         MVI   ELCODE,TASYELQ                                                   
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING TASYD,R4                                                         
         LA    R2,TASYUSST         R2 = A(STARTING CHECK NUMBER)                
         CLI   RECNUM,CN                                                        
         BNE   *+8                                                              
         LA    R2,TASYCNST                                                      
         CLI   RECNUM,EC                                                        
         BNE   *+8                                                              
         LA    R2,TASYEUST                                                      
         CLI   RECNUM,DC                                                        
         BNE   *+8                                                              
         LA    R2,TASYDMST                                                      
         CLI   RECNUM,PC                                                        
         BNE   *+8                                                              
         LA    R2,TASYPCST                                                      
         CLI   RECNUM,PK                                                        
         BNE   *+8                                                              
         LA    R2,TASYLCST                                                      
*                                  SAVE NEXT CHECK NUMBER IN ELEMENT            
         EDIT  (4,CHKNBIN),(8,(R2)),FILL=0                                      
*                                                                               
         CLI   REQURG,C'U'         IF THIS IS URGENT CHECK RUN                  
         BNE   M40                                                              
         TIME  DEC                                                              
         CLI   RECNUM,PC                                                        
         BE    M35                                                              
         CLI   RECNUM,PK                                                        
         BE    M38                                                              
         STCM  R0,14,TASYLKEN      SET LOCKOUT END TIME                         
         B     M40                                                              
M35      STCM  R0,14,TASYPLEN      SET PRINT LOCKOUT END TIME                   
         B     M40                                                              
M38      STCM  R0,14,TASYLLEN      SET P+ LOCKOUT END TIME                      
*                                                                               
M40      GOTO1 PUTREC              WRITE BACK SYSTEM RECORD                     
*        BRAS  RE,COMMIT                                                        
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'R',AIO),0,=C'SYSTEM REC'                         
*                                                                               
M90      B     XIT                                                              
         EJECT                                                                  
* THIS ROUTINE PROCESSES THE CHECK READ INTO THE SORT RECORD BY MAIN.           
*                                                                               
PROCCHK  NTR1                                                                   
         L     R5,AERITAB          R5 = A(ERROR INVOICE TABLE)                  
         USING ERITABD,R5                                                       
*                                                                               
PRC10    CLI   0(R5),X'FF'         IF END OF TABLE THEN GO AHEAD                
         BE    PRC50                                                            
         TM    ERITERR,X'80'       IGNORE 'WARNINGS'                            
         BO    PRC20                                                            
*                                                                               
         OC    SRTAAGY,SRTAAGY     IF THIS IS AN ADJUSTMENT INVOICE             
         BZ    PRC15                                                            
         CLC   ERIAGY,SRTAAGY      THEN TEST ERROR FILTER AGAINST               
         BNE   PRC20                   ADJUSTMENT AGY/INV                       
         CLC   ERIINV,SRTAINV                                                   
         BE    PRCX                RETURN IF MATCH FOUND                        
*                                                                               
PRC15    CLC   ERIAGY,SRTAGY       ELSE TEST FILTER AGAINST NORMAL              
         BNE   PRC20                   AGY/INV                                  
         CLC   ERIINV,SRTINV                                                    
         BE    PRCX                RETURN IF MATCH FOUND                        
*                                                                               
PRC20    LA    R5,ERILEN(R5)       BUMP TO NEXT ERROR INVOICE                   
         B     PRC10                                                            
*                                                                               
PRC50    L     RF,TOTEARN          ADD TAXABLE EARNINGS TO TOTAL                
         A     RF,SRTEARN              TAXABLE EARNINGS                         
         ST    RF,TOTEARN                                                       
*                                                                               
         L     RF,TOTNTAX          ADD NON-TAXABLE EARNINGS TO TOTAL            
         A     RF,SRTNTAX              NON-TAXABLE EARNINGS                     
         ST    RF,TOTNTAX                                                       
*                                                                               
         L     RF,TOTNET           ADD NET CHECK AMOUNT TO TOTAL                
         A     RF,SRTNET               NET CHECK AMOUNTS                        
*                                                                               
PRC60    ST    RF,TOTNET                                                        
*                                                                               
         L     RF,TOTMDED          TO TOTAL                                     
         A     RF,SRTMDED          ADD MISC. DEDUCTIONS                         
         A     RF,SRTDUES          AND UNION DUES                               
         ST    RF,TOTMDED                                                       
*                                                                               
         L     RF,TOTGSTPD         ADD GST PAID TO TOTAL                        
         A     RF,SRTGSTPD             GST PAID                                 
         ST    RF,TOTGSTPD                                                      
*                                                                               
         L     RF,TOTPST                                                        
         A     RF,SRTPST           ADD PST TO TOTAL                             
         ST    RF,TOTPST                                                        
*                                                                               
         L     RF,TOTDIRCT         ADD ELECTRONIC TRANSFERS TO TOTAL            
         A     RF,SRTDIRCT             DIRECT DEPOSIT                           
         A     RF,SRTWIRE              WIRE TRANSFER                            
         CLI   RECNUM,EC           EURO CHECKS?                                 
         BNE   *+8                                                              
         A     RF,SRTTRST              TRUSTS ARE WIRED FOR EURO CHECKS         
         ST    RF,TOTDIRCT                                                      
*                                  GET USE TABLE ENTRY FOR THIS USE,TYP         
         GOTO1 USEVAL,DMCB,SRTUSE,SRTUTYP                                       
*                                                                               
         GOTO1 =A(ADDACC),DMCB,(0,(RC))  ADD AMOUNTS TO ACCTAB ENTRIES          
*                                                                               
         GOTO1 NOPRNT              IF WE DON'T WANT TO PRINT THIS CHECK         
         BE    PRC80               THEN DON'T PRINT IT                          
*                                                                               
*                                  CONVERT BINARY CHECK NUMBER TO CHAR          
         EDIT  (4,CHKNBIN),(8,CHKNCHR),FILL=0                                   
*                                                                               
         L     R2,CHKNBIN          INCREMENT BINARY CHECK NUMBER                
         AHI   R2,1                                                             
         ST    R2,CHKNBIN                                                       
*                                                                               
         L     R2,TOTCNT           INCREMENT CHECK COUNTER                      
         AHI   R2,1                                                             
         ST    R2,TOTCNT                                                        
*                                  GET CATEGORY ENTRY FOR THIS CATEGORY         
         GOTO1 CATVAL,DMCB,SRTCAT                                               
*                                  GET MEDIA TABLE ENTRY FOR THIS MEDIA         
         GOTO1 MEDVAL,DMCB,SRTMED                                               
*                                                                               
         CLI   TGUSEQU,UFGR        IF FGR USE                                   
         BNE   PRC70                                                            
         TM    TGUSTYST,MAJORS     AND IS NEW TYPE WITH MAJORS/AREAS            
         BZ    PRC70                                                            
         GOTO1 MAJVAL,DMCB,(X'80',SRTMAJ)  GET AREA EXPRESSION                  
*                                                                               
PRC70    BRAS  RE,DWNCHK                                                        
*                                                                               
         CLI   SRTWARN,0           IF CHECK HAS A WARNING                       
         BE    PRC75                                                            
*                                                                               
         L     RF,AWARTAB          DIE IF NO MORE ROOM IN WARTAB                
         A     RF,=A(MAXWARS*WARLEN)                                            
         C     RF,NEXTWAR                                                       
         BH    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         L     R5,NEXTWAR          R5 = A(NEXT WARTAB ENTRY TO ADD)             
         USING WARTABD,R5                                                       
*                                                                               
         MVC   WARCHK,CHKNCHR      SAVE CHECK NUMBER IN ENTRY                   
         MVC   WARTERR,SRTWARN     SAVE INVOICE ERROR NUMBER IN ENTRY           
*                                                                               
         LA    R5,WARLEN(R5)       SAVE ADDRESS OF NEXT ENTRY                   
         ST    R5,NEXTWAR                                                       
*                                                                               
PRC75    CLI   REQTRACE,C'K'       IF WE ARE TRACING CHECKS                     
         BNE   PRC80                                                            
*                                                                               
         L     R4,AMASTD           SAVE AND CLEAR PRTQUE REMOTE KEY             
         USING MASTD,R4                                                         
         L     R4,MCVREMOT                                                      
         USING REMOTED,R4                                                       
         MVC   SVREMOT,REMOTKEY                                                 
         XC    REMOTKEY,REMOTKEY                                                
*                                                                               
         OC    SVREMOT,SVREMOT     IF WE ARE PRINTING REMOTE                    
         BZ    PRC80                                                            
         BRAS  RE,DWNCHK                                                        
*                                                                               
PRC80    TM    SRTBITS2,SRTBPPLA   SET TO WRITE BACK NOW                        
         BZ    PRC82                                                            
         GOTO1 =A(SENDMAIL),DMCB,(X'80',0)                                      
*                                                                               
PRC82    TM    SRTBITS2,SRTPKCAN   SET TO WRITE BACK NOW                        
         BZ    PRC84                                                            
         GOTO1 =A(SENDMAIL),DMCB,(X'40',0)                                      
*                                                                               
PRC84    TM    SRTBITS2,SRTADREC   SET TO WRITE BACK NOW                        
         BZ    PRC86                                                            
         GOTO1 =A(SENDMAIL),DMCB,(X'20',0)                                      
*                                                                               
         USING TAPDD,R4                                                         
PRC86    L     R4,ASRTCHK                                                       
         MVI   ELCODE,TAPDELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   PRC90                                                            
         TM    TAPDADJS,TAPDADVD   VOID?                                        
         BO    PRC90                                                            
         OC    TAPDGUAR,TAPDGUAR                                                
         BZ    PRC90                                                            
         XC    FULL,FULL                                                        
         XC    TGFULL,TGFULL                                                    
*                                                                               
         ICM   RF,15,TAPDGRS       PAID GROSS                                   
         ICM   RE,15,TAPDGUAR      + PAID GUARANTEE                             
         AR    RF,RE                                                            
         ICM   RE,15,TAPDREXP      + PAID REIMBURSEMENTS                        
         AR    RF,RE                                                            
         ST    RF,FULL                                                          
*                                                                               
         USING TACXD,R4                                                         
         L     R4,ASRTCHK                                                       
         MVI   ELCODE,TACXELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   PRC88                                                            
         ICM   RE,15,TACXGST       + GST                                        
         ICM   RF,15,TACXPST       + PST                                        
         AR    RE,RF                                                            
         L     RF,FULL                                                          
         AR    RE,RF                                                            
         ST    RE,FULL             = A                                          
*                                                                               
         USING TACDD,R4                                                         
PRC88    L     R4,ASRTCHK                                                       
         MVI   ELCODE,TACDELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   *+10                                                             
         MVC   TGFULL,TACDNET      NET                                          
*                                                                               
         USING TAODD,R4                                                         
         MVC   AIO,ASRTCHK                                                      
         MVI   ELCODE,TAODELQ                                                   
         GOTO1 GETL,DMCB,(1,=AL1(TAODTYPD))                                     
         BNE   PRC88A                                                           
         L     R4,TGELEM                                                        
         L     RF,TGFULL                                                        
         ICM   RE,15,TAODAMT       + DIRECT DEPOSIT                             
         AR    RF,RE                                                            
         ST    RF,TGFULL                                                        
*                                                                               
PRC88A   GOTO1 GETL,DMCB,(1,=AL1(TAODTYPW))                                     
         BNE   PRC88B                                                           
         L     R4,TGELEM                                                        
         L     RF,TGFULL                                                        
         ICM   RE,15,TAODAMT       + WIRE TRANSFER                              
         AR    RF,RE                                                            
         ST    RF,TGFULL                                                        
*                                                                               
PRC88B   GOTO1 GETL,DMCB,(1,=AL1(TAODTYPT))                                     
         BNE   PRC88C                                                           
         L     R4,TGELEM                                                        
         L     RF,TGFULL                                                        
         ICM   RE,15,TAODAMT       + TRUSTEE DEDUCTIONS                         
         AR    RF,RE                                                            
         ST    RF,TGFULL                                                        
*                                                                               
         USING TADWD,R4                                                         
PRC88C   L     R4,ASRTCHK                                                       
         MVI   ELCODE,TADWELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   PRC88D                                                           
         L     RF,TGFULL                                                        
         ICM   RE,15,TADWREC       + DUE COMPANY                                
         AR    RF,RE                                                            
         ST    RF,TGFULL                                                        
*                                                                               
         USING TALWD,R4                                                         
PRC88D   L     R4,ASRTCHK                                                       
         MVI   ELCODE,TALWELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   PRC88E                                                           
         L     RF,TGFULL                                                        
         ICM   RE,15,TALWREC       + LIEN                                       
         AR    RF,RE                                                            
         ST    RF,TGFULL           = B                                          
*                                                                               
PRC88E   L     RF,TGFULL                                                        
         L     RE,FULL                                                          
         CR    RF,RE               B <= A?                                      
         BNH   PRC90                                                            
         GOTO1 =A(SENDMAIL),DMCB,(X'10',0)  NO - WARNING                        
*                                                                               
PRC90    L     R4,AMASTD                                                        
         USING MASTD,R4                                                         
         L     R4,MCVREMOT                                                      
         USING REMOTED,R4                                                       
*                                  CALL TAPPGUAR TO UPDATE GUAR RECORD          
         LA    RF,X'04'            SET TO WRITE BACK NOW                        
         TM    WHEN,X'20'          IF RUNNING SOON                              
         BZ    *+8                                                              
         LA    RF,X'06'            SET TO WRITE TO BUFFER                       
         MVC   DMFILE,SYSDIR                                                    
         XC    FILENAME,FILENAME                                                
         GOTO1 CALLTAP,DMCB,(RC),((RF),ASRTCHK),,SYSCOMM                        
*                                                                               
         CLI   REQTRACE,C'K'       IF WE ARE TRACING CHECKS                     
         BNE   *+10                                                             
         MVC   REMOTKEY,SVREMOT    RESTORE PRTQUE REMOTE KEY                    
         DROP  R4                                                               
*                                                                               
         MVC   AIO,ASRTCHK         FROM CHECK RECORD                            
         MVI   ELCODE,TACMELQ                                                   
         MVI   BYTE,TACMTYPG       REMOVE INVOICE (GENERAL) COMMENT             
         GOTO1 DELL,DMCB,(1,BYTE)                                               
         MVC   AIO,AIO1                                                         
*                                                                               
         BAS   RE,UPDCHK           UPDATE CHECK RECORD                          
*                                                                               
PRCX     B     XIT                                                              
         EJECT                                                                  
*----------------------------------------------------------------------         
* THIS ROUTINE FINISHES UP THE CHECK RUN.  IT PRINTS A TOTAL PAGE               
* AND POSTS THE AMOUNTS FOUND IN ACCTAB TO THE WORKER FILE.                     
*----------------------------------------------------------------------         
PROCEND  NTR1                                                                   
         CLI   RECNUM,CN           IF THIS IS CAN$ CHECKS                       
         BE    PE00                                                             
         CLI   RECNUM,EC           OR EURO CHECKS                               
         BNE   PE90                                                             
PE00     GOTO1 =A(ADDACC),DMCB,(1,(RC))  ADD CONV. DIFF. ACCTAB ENTRIES         
*                                                                               
PE90     GOTO1 =A(POSTALL),DMCB,(RC) POST ACCTAB ENTRIES TO WORKER FILE         
*                                                                               
         B     XIT                                                              
         DROP  R5                  DROP PREVIOUS USING ON WARTABD               
         EJECT                                                                  
* THIS ROUTINE PRINTS THE TOTALS PAGE WHICH INCLUDES A REPORT OF THE            
* TOTAL INDIVIDUAL GROSS EARNINGS, THE TOTAL NON-TAXABLE EARNINGS,              
* AND A LIST OF THE ERROR INVOICES WITH ERROR DESCRIPTIONS.                     
*                                                                               
TOTPAGE  NTR1                                                                   
*        MVI   LINE,X'99'          FORCE PAGE EJECT                             
*                                                                               
         MVC   P(8),=C'RUN DATE'                                                
         MVC   P+9(8),TGTODAY8                                                  
         MVC   P+27(10),=C'CHECK DATE'                                          
         GOTO1 DATCON,DMCB,(1,CHKDATE1),(8,P+38)                                
         BAS   RE,CALLSPL                                                       
         BAS   RE,CALLSPL                                                       
*                                                                               
         MVC   P(22),=C'TOTAL TAXABLE EARNINGS'                                 
         EDIT  (4,TOTEARN),(15,P+32),2,COMMAS=YES,FLOAT=$,MINUS=YES             
         BAS   RE,CALLSPL                                                       
         BAS   RE,CALLSPL                                                       
*                                                                               
         MVC   P(26),=C'TOTAL NON-TAXABLE EARNINGS'                             
         EDIT  (4,TOTNTAX),(15,P+32),2,COMMAS=YES,FLOAT=$,MINUS=YES             
         BAS   RE,CALLSPL                                                       
         BAS   RE,CALLSPL                                                       
*                                                                               
         OC    TOTCVERN,TOTCVERN   IF CONVERTED EARNINGS EXIST                  
         BZ    TP05                                                             
         MVC   P(24),=C'TOTAL CONVERTED EARNINGS'                               
         EDIT  (4,TOTCVERN),(15,P+32),2,COMMAS=YES,FLOAT=$,MINUS=YES            
         BAS   RE,CALLSPL                                                       
         BAS   RE,CALLSPL                                                       
*                                                                               
TP05     MVC   P(9),=C'TOTAL NET'                                               
         EDIT  (4,TOTNET),(15,P+32),2,COMMAS=YES,FLOAT=$,MINUS=YES              
         BAS   RE,CALLSPL                                                       
         BAS   RE,CALLSPL                                                       
*                                                                               
         MVC   P(30),=C'TOTAL NUMBER OF CHECKS WRITTEN'                         
         EDIT  (4,TOTCNT),(15,P+31),COMMAS=YES,ZERO=NOBLANK                     
         BAS   RE,CALLSPL                                                       
         BAS   RE,CALLSPL                                                       
*                                                                               
         L     R2,AERITAB          R2 = A(ERROR INVOICE TABLE)                  
         USING ERITABD,R2                                                       
*                                                                               
TP10     CLI   0(R2),X'FF'         WHILE NOT END OF TABLE                       
         BE    TP20                                                             
*                                                                               
         MVC   P(6),ERIAGY         PRINT AGY/INV/ERROR DESCRIPTION              
         GOTO1 TINVCON,DMCB,ERIINV,P+10,DATCON                                  
         ZIC   R3,ERITERR                                                       
         GOTO1 ERROUT,DMCB,((R3),P+20)                                          
         TM    ERITERR,X'80'       IF THIS IS ACTUALLY A WARNING                
         BZ    *+10                                                             
         MVC   P+58(13),=C'** WARNING **'  DISPLAY ADD'L LITERAL                
         BAS   RE,CALLSPL                                                       
*                                                                               
         LA    R2,ERILEN(R2)       BUMP R2 TO NEXT ERROR INVOICE                
         B     TP10                AND LOOP BACK                                
*                                                                               
TP20     L     R2,AWARTAB          R2 = A(ERROR INVOICE TABLE)                  
         USING WARTABD,R2                                                       
*                                                                               
TP30     CLI   0(R2),X'FF'         WHILE NOT END OF TABLE                       
         BE    TP50                                                             
*                                                                               
         MVC   P(8),WARCHK         PRINT CHECK NUMBER/ERROR DESCRIPTION         
         ZIC   R3,WARTERR                                                       
         GOTO1 ERROUT,DMCB,((R3),P+20)                                          
         BAS   RE,CALLSPL                                                       
*                                                                               
         LA    R2,WARLEN(R2)       BUMP R2 TO NEXT ERROR INVOICE                
         B     TP30                AND LOOP BACK                                
*                                                                               
TP50     DS    0H                                                               
*                                                                               
TPX      B     XIT                                                              
         DROP  R2                                                               
         EJECT                                                                  
* UPDATE THE CHECK RECORD WITH THE CHECK DETAILS AND WRITE IT BACK TO           
* THE FILE.  THEN BUILD PASSIVE POINTER BLOCK AND ADD ALL PASSIVE               
* POINTERS FOR THE CHECK RECORD.                                                
*                                                                               
UPDCHK   NTR1                                                                   
         LA    R3,KEY              R3 = A(KEY)                                  
         USING TLDRD,R3                                                         
*                                                                               
         TM    SRTBITS2,SRTBPPLA   P+ AGY COMM CHANGED?                         
         BZ    UC3                                                              
         MVC   AIO,ASRTCHK                                                      
         MVI   ELCODE,TANUELQ                                                   
         GOTO1 GETL,DMCB,(1,=AL1(TANUTPAC))                                     
         BE    *+6                 DIE, IF NONE FOUND                           
         DC    H'0'                                                             
                                                                                
         L     R4,TGELEM                                                        
         USING TANUD,R4                                                         
         MVC   TANUOVAM,SRTPPLA    REPLACE IT IN THE CHECK                      
         MVC   AIO,AIO1                                                         
*                                                                               
UC3      L     R4,ASRTCHK          R4 = A(PAYMENT DETAILS ELEMENT)              
         MVI   ELCODE,TAPDELQ                                                   
         BAS   RE,GETEL                                                         
         USING TAPDD,R4                                                         
         MVC   TAPDMDED,SRTMDED    SAVE MISC DEDUCTIONS (MAY HAVE CHGD)         
         MVC   TAPDDUES,SRTDUES    SAVE UNION DUES      (MAY HAVE CHGD)         
*                                                                               
         L     R4,ASRTCHK          R4 = A(CHECK DETAILS ELEMENT)                
         MVI   ELCODE,TACDELQ                                                   
         BAS   RE,GETEL                                                         
         USING TACDD,R4                                                         
*                                                                               
         GOTO1 NOPRNT              IF CHECK WAS PRINTED                         
         BE    UC5                                                              
         MVC   TACDCHK,CHKNCHR     THEN SAVE CHECK NUMBER IN ELEMENT            
                                                                                
         CLC   SRTSSN,=C'000112843'   IF AOS CHECK                              
         BE    *+14                                                             
         CLC   SRTSSN,=C'000117358'   IF AOS CHECK                              
         BNE   UC5                                                              
         MVC   TACDNET,SRTNET      SRTNET NOW HAS PST IN IT                     
*                                                                               
UC5      TM    SRTBITS,SRTBLIN     IF CHECK WAS GENERATED BY A LIEN             
         BZ    UC8                                                              
         OI    TACDSTAT,TACDSLIN   THEN SET GENERATED BY LIEN BIT               
         BAS   RE,NEWCHK           ADD NEW CHECK RECORD                         
         B     UC50                                                             
*                                                                               
UC8      TM    SRTTRBTS,SRTTTRST   IF CHECK WAS GENERATED BY W4 TRUSTEE         
         BZ    UC10                                                             
         OI    TACDSTAT,TACDSTRS   THEN SET GENERATED BY W4 TRUSTEE             
         BAS   RE,NEWCHK           ADD NEW CHECK RECORD                         
         B     UCX                                                              
*                                                                               
UC10     MVC   TLDRDA,SRTDSKA      ELSE READ OLD CHECK RECORD INTO VOID         
         MVC   FILENAME,CHKFIL                                                  
*        MVI   RDUPDATE,C'Y'                                                    
         GOTO1 GETREC                                                           
*                                                                               
         GOTO1 ASAVPTRS,DMCB,ASPBLK  SAVE OLD PASSIVE POINTERS                  
*                                                                               
         MVC   AIO,ASRTCHK         WRITE NEW CHECK RECORD BACK                  
         GOTO1 PUTREC                                                           
*        BRAS  RE,COMMIT                                                        
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'C',ASRTCHK),0,=C'UPDATED CHECK REC'              
         GOTO1 DOTRACE,DMCB,(C'U',ASRTCHK),0,=C'UPDATED CHECK REC'              
*                                                                               
         MVC   FILENAME,CHKDIR     SET FILE NAME TO CHKDIR                      
*                                                                               
         GOTO1 ADDPTRS             ADD NEW POINTERS TO FILE                     
*                                                                               
         XC    FILENAME,FILENAME   RESTORE FILE NAME AND AIO                    
         MVC   AIO,AIO1                                                         
*                                                                               
UC50     BAS   RE,UPDWITH          UPDATE DUECOMP AND LIEN RECORDS              
         BAS   RE,UPDW4            UPDATE W4 RECORDS                            
*                                                                               
UCX      B     XIT                                                              
         DROP  R3,R4                                                            
         EJECT                                                                  
* THIS ROUTINE HANDLES THE ADDING OF NEW CHECK RECORDS GENERATED BY             
* A LIEN OR A W4 TRUSTEE                                                        
*                                                                               
NEWCHK   NTR1                                                                   
         LA    R3,KEY              IF CHECK RECORD ALREADY EXISTS               
         USING TLDRD,R3                (DELETED OR NOT)                         
         L     RF,ASRTCHK                                                       
         MVC   TLDRKEY,0(RF)                                                    
         OI    DMINBTS,X'08'                                                    
         MVC   FILENAME,CHKDIR                                                  
         GOTO1 HIGH                                                             
         XC    FILENAME,FILENAME                                                
         NI    DMINBTS,X'F7'                                                    
         CLC   TLDRKEY,KEYSAVE                                                  
         BNE   NC50                                                             
*                                                                               
         OI    DMINBTS,X'08'       READ OLD CHECK RECORD INTO VOID              
         MVC   FILENAME,CHKFIL                                                  
*        MVI   RDUPDATE,C'Y'                                                    
         GOTO1 GETREC                                                           
         NI    DMINBTS,X'F7'                                                    
*                                                                               
         GOTO1 ASAVPTRS,DMCB,ASPBLK  SAVE OLD PASSIVE POINTERS                  
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'L',AIO),0,=C'OLD LIEN/TRST CHK'                  
*                                                                               
         MVC   AIO,ASRTCHK                                                      
         BAS   RE,DELSTPL                                                       
         GOTO1 PUTREC              WRITE NEW CHECK RECORD BACK                  
*        BRAS  RE,COMMIT                                                        
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'L',ASRTCHK),0,=C'UPDATED LIEN/TRST CHK'          
*                                                                               
         XC    FILENAME,FILENAME                                                
         MVC   AIO,AIO1                                                         
*                                                                               
         TM    TLDRSTAT,X'80'      IF KEY WAS MARKED FOR DELETION               
         BZ    NCX                                                              
         NI    TLDRSTAT,X'7F'      THEN UNDELETE KEY AND WRITE IT BACK          
         MVC   FILENAME,CHKDIR                                                  
         GOTO1 WRITE                                                            
         XC    FILENAME,FILENAME                                                
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'L',KEY),38,=C'UNDELETED ACTIVE KEY'              
         B     NC100                                                            
*                                                                               
NC50     MVC   AIO,ASRTCHK         ELSE ADD NEW CHECK RECORD                    
         MVC   FILENAME,CHKFIL                                                  
         BAS   RE,DELSTPL                                                       
         GOTO1 ADDREC                                                           
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'L',ASRTCHK),0,=C'NEW LIEN/TRST CHK'              
*                                                                               
         XC    FILENAME,FILENAME                                                
         MVC   AIO,AIO1                                                         
*                                                                               
         L     RE,ASPBLK           CLEAR OLD PASSIVE POINTERS                   
         L     RF,=A(PPBLOCKL)                                                  
         SR    R1,R1                                                            
         MVCL  RE,R0                                                            
*                                                                               
NC100    MVC   AIO,ASRTCHK         ADD NEW PASSIVE POINTERS                     
         MVC   FILENAME,CHKDIR                                                  
         GOTO1 ADDPTRS                                                          
         XC    FILENAME,FILENAME                                                
         MVC   AIO,AIO1                                                         
*                                                                               
NCX      B     XIT                                                              
         EJECT                                                                  
* THIS ROUTINE DELETES THE CHECK PULL/STOP ELEMENTS FROM NEW LIEN               
* CHECKS ... ON ENTRY, AIO=ASRTCHK                                              
* ALSO THE BREAKOUT ELEMENT FOR LIEN AND TRUSTS                                 
*                                                                               
DELSTPL  NTR1                                                                   
         USING TACDD,R4                                                         
         L     R4,AIO             IF CHECK IS GENERATED BY A LIEN               
         MVI   ELCODE,TACDELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   DSPX                                                             
         TM    TACDSTAT,TACDSLIN  LIEN                                          
         BNO   DSPX                                                             
         DROP  R4                                                               
*                                                                               
         MVI   ELCODE,TACNELQ     DELETE PULL/STOP CANCEL ELEMENT               
         GOTO1 REMELEM                                                          
         MVI   ELCODE,TAKPELQ     DELETE STOP ELEMENT                           
         GOTO1 REMELEM                                                          
         MVI   ELCODE,TAKLELQ     DELETE PULL ELEMENT                           
         GOTO1 REMELEM                                                          
DSPX     B     XIT                                                              
         EJECT                                                                  
* THIS ROUTINE LOOPS THROUGH THE DUE COMPANY AND LIEN WITHHOLDING               
* ELEMENTS AND UPDATES THE CORRESPONDING RECORDS WITH THE AMOUNT                
* RECOVERED FOR EACH.  IF THE CHECK IS A CREDIT PAYMENT CHECK, THEN             
* THE ROUTINE ABORTS AND NO UPDATES OCCUR.                                      
*                                                                               
UPDWITH  NTR1                                                                   
         TM    SRTPST1,TAPDPCRD    IF CHECK IS A CREDIT PAYMENT                 
         BO    UWX                 THEN RETURN                                  
*                                                                               
         L     R4,ASRTCHK          R4 = A(FIRST DW ELEMENT)                     
         MVI   ELCODE,TADWELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   UW50                IF NONE FOUND THEN SKIP TO LIEN              
*                                                                               
UW10     BAS   RE,SAVDUC           SAVE DETAILS IN DUE COMPANY TABLE            
*                                                                               
         MVI   ELCODE,TADWELQ                                                   
         BAS   RE,NEXTEL                                                        
         BE    UW10                REPEAT UNTIL NO MORE DW ELEMENTS             
*                                                                               
UW50     L     R4,ASRTCHK          R4 = A(FIRST LW ELEMENT)                     
         MVI   ELCODE,TALWELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   UWX                 IF NONE FOUND THEN DONE                      
         USING TALWD,R4                                                         
*                                                                               
         TM    TALWREC,X'80'       IF LIEN PAYEE CHECK THEN DONE                
         BO    UWX                                                              
*                                                                               
         BAS   RE,SAVLIN           SAVE DETAILS IN LIEN TABLE                   
*                                                                               
UWX      B     XIT                                                              
         EJECT                                                                  
* THIS ROUTINE SAVES THE DETAILS OF THE DUE COMPANY WITHHOLDING ELEMENT         
* PASSED IN R4 IN THE DUE COMPANY TABLE FOR UPDATE AT END OF RUN.               
*                                                                               
         USING TADWD,R4                                                         
SAVDUC   NTR1                                                                   
         LA    RE,MAXDUES          RE=MAXIMUM NUMBER OF TABLE ENTRIES           
         L     R5,ADUETAB          R5 = A(DUE COMPANY TABLE)                    
         USING SDUED,R5                                                         
*                                                                               
SDUC10   OC    SDUED(SDUELEN),SDUED  TEST LOGICAL END OF TABLE                  
         BZ    SDUC20                                                           
         CLC   SRTSSN,SDUESSN                  ELSE MATCH ON SSN                
         BNE   *+14                                                             
         CLC   TADWDUC,SDUEDWEL+TADWDUC-TADWD  AND DUE COMPANY CODE             
         BE    SDUC15                                                           
         LA    R5,SDUELEN(R5)      NO MATCH - TRY NEXT ENTRY IN TABLE           
         BCT   RE,SDUC10                                                        
         DC    H'0'                NEED TO MAKE TABLE BIGGER (TACKDUE)          
*                                            * FOUND A MATCH IN TABLE           
SDUC15   ICM   R1,15,SDUEDWEL+TADWREC-TADWD  ADD TO TOTAL COLLECTED             
         A     R1,TADWREC                                                       
         STCM  R1,15,SDUEDWEL+TADWREC-TADWD                                     
         B     SDUCX                                                            
*                                                                               
*                                  * ADD NEW ENTRY TO TABLE                     
SDUC20   MVC   SDUESSN,SRTSSN      SAVE SOCIAL SECURITY NUMBER                  
         MVC   SDUEDWEL,TADWEL     AND ENTIRE DUE COMP WITHHOLDING EL.          
*                                                                               
SDUCX    B     XIT                                                              
         EJECT                                                                  
* THIS ROUTINE LOOPS THROUGH THE ENTRIES IN THE DUE COMPANY TABLE               
* AND CALLS UPDDUC TO UPDATE THEIR CORRESPONDING DUE COMPANY RECORDS.           
*                                                                               
FLUDUC   NTR1                                                                   
         L     R5,ADUETAB          R5 = A(DUE COMPANY TABLE)                    
         USING SDUED,R5                                                         
*                                                                               
FDUC10   OC    SDUED(SDUELEN),SDUED  TEST LOGICAL END OF TABLE                  
         BZ    FDUCX                                                            
         MVC   SRTSSN,SDUESSN      SET SOCIAL SECURITY NUMBER                   
         LA    R4,SDUEDWEL         SET R4=A(DUE COMP WITHHOLDING EL.)           
*                                                                               
         BAS   RE,UPDDUC           UPDATE DUE COMPANY RECORD                    
*                                                                               
         LA    R5,SDUELEN(R5)      BUMP TO NEXT ENTRY IN TABLE                  
         B     FDUC10              AND CONTINUE                                 
*                                                                               
FDUCX    B     XIT                                                              
         EJECT                                                                  
* THIS ROUTINE UPDATES THE DUE COMPANY RECORD BASED ON THE DUE COMPANY          
* WITHHOLDING ELEMENT PASSED IN R4.  ROUTINE ASSUMES SRTSSN IS SET.             
*                                                                               
         USING TADWD,R4                                                         
UPDDUC   NTR1                                                                   
         LA    R3,KEY              BUILD KEY OF DUE COMPANY RECORD              
         USING TLDUD,R3                                                         
         XC    TLDUKEY,TLDUKEY                                                  
         MVI   TLDUCD,TLDUCDQ                                                   
         MVC   TLDUSSN,SRTSSN                                                   
         MVC   TLDUDUC,TADWDUC                                                  
*                                                                               
         GOTO1 HIGH                READ KEY                                     
         CLC   TLDUKEY,KEYSAVE                                                  
         BE    *+6                                                              
         DC    H'0'                                                             
*        MVI   RDUPDATE,C'Y'                                                    
         GOTO1 GETREC              READ RECORD FOR UPDATE                       
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'D',AIO),0,=C'OLD DUECOMP REC'                    
*                                                                               
         MVC   DUECOL,TADWREC      SAVE COLLECTED AMOUNT IN DUECOL              
*                                                                               
         L     R4,AIO              R4 = A(DUE COMPANY DETAILS ELEMENT)          
         MVI   ELCODE,TADUELQ                                                   
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING TADUD,R4                                                         
*                                                                               
         ICM   RF,15,TADUCOL       ADD AMOUNT COLLECTED                         
         A     RF,DUECOL                                                        
         STCM  RF,15,TADUCOL                                                    
*                                                                               
         GOTO1 PUTREC              WRITE RECORD BACK                            
*        BRAS  RE,COMMIT                                                        
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'D',AIO),0,=C'UPDATED DUECOMP REC'                
*                                                                               
         B     XIT                                                              
         EJECT                                                                  
* THIS ROUTINE SAVES THE DETAILS OF THE LIEN WITHHOLDING ELEMENT                
* PASSED IN R4 IN THE LIEN TABLE FOR UPDATE AT END OF RUN.                      
*                                                                               
         USING TALWD,R4                                                         
SAVLIN   NTR1                                                                   
         LA    RE,MAXLINS          RE=MAXIMUM NUMBER OF TABLE ENTRIES           
         L     R5,ALINTAB          R5 = A(LIEN TABLE)                           
         USING SLIND,R5                                                         
*                                                                               
SLIN10   OC    SLIND(SLINLEN),SLIND  TEST LOGICAL END OF TABLE                  
         BZ    SLIN20                                                           
         CLC   SRTSSN,SLINSSN                  ELSE MATCH ON SSN                
         BNE   *+14                                                             
         CLC   TALWLIN,SLINLWEL+TALWLIN-TALWD  AND LIEN CODE                    
         BE    SLIN15                                                           
         LA    R5,SLINLEN(R5)      NO MATCH - TRY NEXT ENTRY IN TABLE           
         BCT   RE,SLIN10                                                        
         DC    H'0'                NEED TO MAKE TABLE BIGGER (TACKLIN)          
*                                            * FOUND A MATCH IN TABLE           
SLIN15   ICM   R1,15,SLINLWEL+TALWREC-TALWD  ADD TO TOTAL COLLECTED             
         A     R1,TALWREC                                                       
         STCM  R1,15,SLINLWEL+TALWREC-TALWD                                     
         B     SLINX                                                            
*                                                                               
*                                  * ADD NEW ENTRY TO TABLE                     
SLIN20   MVC   SLINSSN,SRTSSN      SAVE SOCIAL SECURITY NUMBER                  
         MVC   SLINLWEL,TALWEL     AND ENTIRE LIEN WITHHOLDING EL.              
*                                                                               
SLINX    B     XIT                                                              
         EJECT                                                                  
* THIS ROUTINE LOOPS THROUGH THE ENTRIES IN THE LIEN TABLE                      
* AND CALLS UPDLIN TO UPDATE THEIR CORRESPONDING LIEN RECORDS.                  
*                                                                               
FLULIN   NTR1                                                                   
         L     R5,ALINTAB          R5 = A(LIEN TABLE)                           
         USING SLIND,R5                                                         
*                                                                               
FLIN10   OC    SLIND(SLINLEN),SLIND  TEST LOGICAL END OF TABLE                  
         BZ    FLINX                                                            
         MVC   SRTSSN,SLINSSN      SET SOCIAL SECURITY NUMBER                   
         LA    R4,SLINLWEL         SET R4=A(LIEN WITHHOLDING EL.)               
*                                                                               
         BAS   RE,UPDLIN           UPDATE LIEN RECORD                           
*                                                                               
         LA    R5,SLINLEN(R5)      BUMP TO NEXT ENTRY IN TABLE                  
         B     FLIN10              AND CONTINUE                                 
*                                                                               
FLINX    B     XIT                                                              
         EJECT                                                                  
* THIS ROUTINE UPDATES THE LIEN RECORD BASED ON THE LIEN                        
* WITHHOLDING ELEMENT PASSED IN R4.  ROUTINE ASSUMES SRTSSN IS SET.             
*                                                                               
         USING TALWD,R4                                                         
UPDLIN   NTR1                                                                   
         LA    R3,KEY              BUILD KEY OF LIEN RECORD                     
         USING TLLND,R3                                                         
         XC    TLLNKEY,TLLNKEY                                                  
         MVI   TLLNCD,TLLNCDQ                                                   
         MVC   TLLNSSN,SRTSSN                                                   
         MVC   TLLNLIN,TALWLIN                                                  
*                                                                               
         GOTO1 HIGH                READ KEY                                     
         CLC   TLLNKEY,KEYSAVE                                                  
         BE    *+6                                                              
         DC    H'0'                                                             
*        MVI   RDUPDATE,C'Y'                                                    
         GOTO1 GETREC              READ RECORD FOR UPDATE                       
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'L',AIO),0,=C'OLD LIEN REC'                       
*                                                                               
         MVC   LIENCOL,TALWREC     SAVE COLLECTED AMOUNT IN LIENCOL             
*                                                                               
         L     R4,AIO              R4 = A(LIEN DETAILS ELEMENT)                 
         MVI   ELCODE,TALNELQ                                                   
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING TALND,R4                                                         
*                                                                               
         ICM   RF,15,TALNCOL       ADD AMOUNT COLLECTED                         
         A     RF,LIENCOL                                                       
         STCM  RF,15,TALNCOL                                                    
*                                                                               
         GOTO1 PUTREC              WRITE RECORD BACK                            
*        BRAS  RE,COMMIT                                                        
         GOTO1 DOTRACE,DMCB,(C'L',AIO),0,=C'UPDATED LIEN REC'                   
         B     XIT                                                              
         EJECT                                                                  
* THIS ROUTINE LOOKS FOR A W4 TRUSTEE DEDUCTION ELEMENT AND IF IT               
* FINDS ONE ADDS IT TO THE TRUSTEE TABLE FOR LATER UPDATE                       
* IF THE CHECK IS A CREDIT PAYMENT CHECK NO UPDATES OCURR                       
*                                                                               
         USING TACDD,R4                                                         
UPDW4    NTR1                                                                   
         TM    SRTPST1,TAPDPCRD    IF CHECK IS A CREDIT PAYMENT                 
         BO    UPDW4X              THEN RETURN                                  
         TM    TACDSTAT,TACDSTRS   IF TRUSTEE CHECK                             
         BO    UPDW4X              THEN RETURN                                  
*                                                                               
         TM    SRTADJS,TAPDADTR    IF REFUND CHECK                              
         BZ    UPDW42                                                           
         MVC   AIO,ASRTCHK                                                      
         MVI   ELCODE,TAODELQ                                                   
         GOTO1 GETL,DMCB,(1,=AL1(TAODTYPT))                                     
         MVC   AIO,AIO1                                                         
         BNE   UPDW4X                                                           
         L     R4,TGELEM           GET AMOUNT OF REFUND                         
         USING TAODD,R4                                                         
         ICM   R2,15,TAODAMT                                                    
         B     UPDW48                                                           
*                                                                               
UPDW42   OC    SRTTRST,SRTTRST     IF AMOUNT ZERO                               
         BZ    UPDW4X              EXIT                                         
         L     R2,SRTTRST                                                       
*                                                                               
         MVC   AIO,ASRTCHK         AND AMOUNT NOT SENT TO A TRUSTEE             
         MVI   ELCODE,TANUELQ                                                   
         GOTO1 GETL,DMCB,(1,=AL1(TANUTRST))                                     
         MVC   AIO,AIO1                                                         
         BE    UPDW4X                                                           
*                                                                               
UPDW48   BAS   RE,SAVTRST          SAVE DETAILS IN TABLE                        
UPDW4X   B     XIT                                                              
         SPACE 2                                                                
* THIS ROUTINE PASSES THE AMOUNT OF THE W4 TRUSTEE DEDUCTION IN R2              
* WHICH GETS ADDED TO THE TRUSTEE TABLE FOR UPDATE AT END OF RUN.               
*                                                                               
SAVTRST  NTR1                                                                   
         LA    RE,MAXW4TS          RE=MAXIMUM NUMBER OF TABLE ENTRIES           
         L     R5,ATRSTAB          R5 = A(TABLE)                                
         USING STRSTD,R5                                                        
*                                                                               
STRST10  OC    STRSTD(STRSTLEN),STRSTD TEST END OF TABLE                        
         BZ    STRST20                                                          
         CLC   SRTSSN,STRSTSSN     MATCH ON SSN                                 
         BE    STRST15                                                          
         LA    R5,STRSTLEN(R5)     TRY NEXT ENTRY IN TABLE                      
         BCT   RE,STRST10                                                       
         DC    H'0'                MAKE TABLE BIGGER (TACKTRST)                 
*                                                                               
STRST15  CLC   SRTEMP,=C'TP '      ADD TO TOTAL COLLECTED                       
         BNE   STRST16                                                          
         ICM   R1,15,STRSTCTP                                                   
         AR    R1,R2                                                            
         STCM  R1,15,STRSTCTP                                                   
         B     STRSTX                                                           
STRST16  CLC   SRTEMP,=C'P+ '                                                   
         BNE   STRST17                                                          
         ICM   R1,15,STRSTCPK                                                   
         AR    R1,R2                                                            
         STCM  R1,15,STRSTCPK                                                   
         B     STRSTX                                                           
STRST17  ICM   R1,15,STRSTCPP                                                   
         AR    R1,R2                                                            
         STCM  R1,15,STRSTCPP                                                   
         B     STRSTX                                                           
*                                  * ADD NEW ENTRY TO TABLE                     
STRST20  MVC   STRSTSSN,SRTSSN     SAVE SOCIAL SECURITY NUMBER                  
         LR    R1,R2               AND ENTIRE AMOUNT DEDUCTED                   
         CLC   SRTEMP,=C'TP '                                                   
         BNE   *+12                                                             
         STCM  R1,15,STRSTCTP                                                   
         B     STRSTX                                                           
         CLC   SRTEMP,=C'P+ '                                                   
         BNE   *+12                                                             
         STCM  R1,15,STRSTCPK                                                   
         B     STRSTX                                                           
         STCM  R1,15,STRSTCPP                                                   
*                                                                               
STRSTX   OC    SRTW4DOB,SRTW4DOB                                                
         BZ    STRSTX2                                                          
         ICM   R1,15,STRSTERN      ADDING TO MINORS EARNINGS                    
         A     R1,SRTGRS                                                        
         STCM  R1,15,STRSTERN                                                   
STRSTX2  GOTO1 DOTRACE,DMCB,(C'L',ATRSTAB),200,=C'SAVE TRSTAB'                  
         B     XIT                                                              
         EJECT                                                                  
* THIS ROUTINE LOOPS THROUGH TRUSTEE TABLE AND UPDATES THE W4 RECORDS           
*                                                                               
FLUTRST  NTR1                                                                   
         GOTO1 DOTRACE,DMCB,(C'L',ATRSTAB),200,=C'TRSTAB'                       
*                                                                               
         L     R5,ATRSTAB          R5 = A(TABLE)                                
         USING STRSTD,R5                                                        
*                                                                               
FLTRST10 OC    STRSTD(STRSTLEN),STRSTD  TEST END OF TABLE                       
         BZ    XIT                                                              
*        MVI   RDUPDATE,C'Y'                                                    
         GOTO1 RECVAL,DMCB,TLW4CDQ,(X'A0',STRSTSSN)                             
         BE    *+6                                                              
         DC    H'0'                                                             
         L     R4,AIO                                                           
         USING TAWXD,R4                                                         
         MVI   ELCODE,TAWXELQ                                                   
         BAS   RE,GETEL                                                         
         BE    FLTRST12                                                         
*                                                                               
         XC    ELEMENT,ELEMENT     THIS MUST BE INDIVID. PAID AS CORP           
         LA    R4,ELEMENT          AND THEREFORE AMT IS HELD ON CORP            
         MVI   TAWXEL,TAWXELQ      BUT ELE MIGHT NOT EXIST IF 1ST TIME          
         MVI   TAWXLEN,TAWXLNQ                                                  
         MVC   TAWXHTP,STRSTCTP                                                 
         MVC   TAWXHPPL,STRSTCPK                                                
         MVC   TAWXHPP,STRSTCPP                                                 
         MVC   TAWXMERN,STRSTERN                                                
         GOTO1 ADDELEM                                                          
         B     FLTRST14                                                         
*                                                                               
FLTRST12 ICM   RF,15,TAWXHTP       ADD TO AMOUNT COLLECTED AND HELD             
         ICM   RE,15,STRSTCTP                                                   
         AR    RF,RE                                                            
         STCM  RF,15,TAWXHTP                                                    
         ICM   RF,15,TAWXHPPL                                                   
         ICM   RE,15,STRSTCPK                                                   
         AR    RF,RE                                                            
         STCM  RF,15,TAWXHPPL                                                   
         ICM   RF,15,TAWXHPP       ADD TO AMOUNT COLLECTED AND HELD             
         ICM   RE,15,STRSTCPP                                                   
         AR    RF,RE                                                            
         STCM  RF,15,TAWXHPP                                                    
         ICM   RF,15,TAWXMERN                                                   
         ICM   RE,15,STRSTERN                                                   
         AR    RF,RE                                                            
         STCM  RF,15,TAWXMERN                                                   
*                                                                               
FLTRST14 GOTO1 PUTREC                                                           
*        BRAS  RE,COMMIT                                                        
         GOTO1 DOTRACE,DMCB,(C'L',AIO),0,=C'UPDATED W4 REC'                     
*                                                                               
FLTRST15 LA    R5,STRSTLEN(R5)     BUMP TO NEXT ENTRY IN TABLE                  
         B     FLTRST10             AND CONTINUE                                
         DROP  R4                                                               
         EJECT                                                                  
*---------------------------------------------------------------------          
*        INITDWN - SETUP DOWNLOAD PARAMETERS                                    
*---------------------------------------------------------------------          
         USING DLCBD,R1                                                         
INITDWN  NTR1                                                                   
         LA    R1,DLCB             DOWNLOAD CONTROL BLOCK                       
*                                                                               
         XC    DLCBD(DLCBXLX),DLCBD                                             
         LA    RF,CALLSPL          A(HOOK ROUTINE FOR PRINTING)                 
         ST    RF,DLCBAPR                                                       
         LA    RF,P                A(PRINT LINE)                                
         MVC   P,SPACES                                                         
         ST    RF,DLCBAPL                                                       
         MVI   DLCBACT,DLCBSOR     START OF REPORT                              
         MVC   DLCBAED,EDITOR                                                   
         OI    DLCBFLG1,DLCBFXFL+DLCBFXTN  EXTENDED TEXT FIELD USED             
         MVC   DLCXMAXL,=Y(L'P)    MAXIMUM LENGTH OF PRINT LINE                 
         MVI   DLCXDELC,C','       DELIMITER                                    
         MVI   DLCXEOTC,C'"'       TEXT DELIMITER                               
         MVI   DLCXEOTA,C''''      TEXT DELIMITER ALTERNATE                     
         MVI   DLCXEOLC,X'5E'      SEMI-COLON FOR END OF LINE                   
         MVI   DLCXEORC,C':'       END OF REPORT                                
         GOTO1 =V(DLFLD),DLCBD                                                  
*                                                                               
         B     XIT                                                              
         EJECT                                                                  
*---------------------------------------------------------------------          
*        CALLSPL - USER SUPPLIED PRINT LINE ROUTINE                             
*---------------------------------------------------------------------          
CALLSPL  NTR1                                                                   
         MVI   FORCEHED,C'N'       NEVER HEAD UP                                
         MVI   SPACING,1                                                        
         MVI   LINE,1              PREVENT PAGE BREAK                           
         GOTO1 SPOOL,DMCB,(R8)                                                  
*                                                                               
         B     XIT                                                              
         DROP  R1                                                               
         EJECT                                                                  
* MISCELLANEOUS CODE                                                            
*                                                                               
*                                                                               
CALLTAP  NTR1                                                                   
         CLI   4(R1),X'18'         IF CALLING TO CLEAR GUAR BUFFER              
         BE    CT10                                                             
         CLI   4(R1),X'01'         OR CALLING TO FLUSH GUAR BUFFER              
         BE    CT10                SKIP INVOICE RELATED CODE                    
*                                                                               
         MVC   TGCID,SRTCID        SAVE CID IN GLOBAL FOR TAPPGUAR CALL         
         MVC   TGSSN,SRTSSN             SSN                                     
         MVC   TGAGY,SRTAGY             AGENCY                                  
         MVC   TGCLI,SRTCLI             CLIENT                                  
*                                                                               
         TM    SRTINVS,TAINSBIL    IF INVOICED BILLED                           
         BO    YES                                                              
         TM    SRTINVS2,TAINSHLP   OR PUR PRINTED                               
         BO    YES                                                              
         CLI   SRTADJS,0           OR THIS IS ADJUSTMENT INVOICE                
         BNE   YES                                                              
*                                                                               
CT10     CLI   REQTAPP,C'Y'        OR OPTION TO SUPPRESS TAPPGUAR CALLS         
         BE    YES                 THEN RETURN                                  
*                                                                               
         CLI   TAPPTRAC,C'Y'       IF TRACING TAPPGUAR CALLS                    
         BNE   *+8                                                              
         OI    4(R1),X'40'         THEN SET TRACE BIT IN PARAMETER 2            
*                                                                               
         GOTO1 =V(TAPPGUAR)        CALL TAPPGUAR - PARMS ALREADY SET            
         MVC   TGXARG,0(R1)                                                     
         GOTO1 DOTRACE,DMCB,(C'T',TGXARG),1,=C'GUAR RETURN'                     
         B     XIT                                                              
*                                                                               
         GETEL R4,DATADISP,ELCODE                                               
*                                                                               
YES      SR    RC,RC                                                            
NO       LTR   RC,RC                                                            
XIT      XIT1                                                                   
*                                                                               
CHKDIR   DC    CL8'CHKDIR'                                                      
CHKFIL   DC    CL8'CHKFIL'                                                      
*                                                                               
*                                                                               
XS       DC    82C'X'                                                           
         EJECT                                                                  
         LTORG                                                                  
         SPACE 3                                                                
         DROP  RA                  DROP SECONDARY BASE REGISTER                 
         EJECT                                                                  
*---------------------------------------------------------------------          
*              ROUTINE TO SET UP SECOND REPORT ON QUEUE                         
*---------------------------------------------------------------------          
NEWPRTQ  NTR1  BASE=*,LABEL=*                                                   
         L     R2,ALOGOC                                                        
         USING LOGOD,R2                                                         
         MVI   LOGOTYPE,C'E'                                                    
         GOTO1 ALOGO,DMCB,(R2)     END REPORT LOGOS                             
*                                                                               
         TM    WHEN,X'20'          SOON?                                        
         BO    NPQ010                                                           
         GOTO1 VPRINT,DMCB,=C'CLOSE'                                            
*--------------------------------  COMMENT OUT CUZ IT DIES OVERNIGHT            
*        CLI   DMCB+8,0                                                         
*        BE    *+6                                                              
*        DC    H'0'                                                             
*                                                                               
NPQ010   XC    SPECS,SPECS         CLEAR SPECS                                  
         XC    HEADHOOK,HEADHOOK   AND HEADLINE HOOK                            
*                                                                               
         L     R2,ABOX                                                          
         USING BOXD,R2                                                          
         LA    RE,132                                                           
         ST    RE,BOXWIDTH         SET LENGTH OF PRINT LINE                     
*                                                                               
         L     RF,AMASTD                                                        
         USING MASTD,RF                                                         
         L     R2,AREMOT                                                        
         USING REMOTED,R2                                                       
         TM    WHEN,X'20'          SOON?                                        
         BZ    NPQ050                                                           
         XC    MCREMPQK,MCREMPQK                                                
         B     NPQ060                                                           
*                                                                               
NPQ050   MVC   REMOTABF,MCVPQBUF   OVERNIGHT                                    
         MVC   REMOTADM,MCVDMGR                                                 
         MVC   REMOTAOP,MCVPQOPN                                                
         MVC   REMOTDST,MCDESTID                                                
         XC    MCALTREF,MCALTREF                                                
         L     RF,MCVLOGO          DOWN/SQL - PATCH LOGOS NOT TO PRINT          
         MVC   0(2,RF),=X'07FE'                                                 
         LA    RF,MCREMOTE                                                      
*                                                                               
         MVI   REMOTCPY,C'1'                                                    
         MVI   REMOTCLS,C'E'                                                    
         MVC   REMOTJID,=C'TCK'                                                 
NPQ060   MVC   REMOTKEY(11),SPACES                                              
         MVC   REMOTSYS(6),=C'CHECK2'                                           
         MVC   REMOTFRM(4),=C'TAL$'                                             
*                                                                               
         XIT1                                                                   
         LTORG                                                                  
         DROP  R2                                                               
         EJECT                                                                  
*---------------------------------------------------------------------          
*        COMMIT CALL TO DATAMGR                                                 
*---------------------------------------------------------------------          
COMMIT   NTR1  BASE=*,LABEL=*                                                   
         GOTO1 DATAMGR,DMCB,=C'COMMIT'  COMMIT                                  
COMMITX  XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
*---------------------------------------------------------------------          
*        GETEL FOR ROUTINES, USES GETEL9                                        
*---------------------------------------------------------------------          
GTEL     NTR1  BASE=*,LABEL=*                                                   
         BAS   RE,GETEL9                                                        
         XIT1  REGS=(R4)                                                        
*                                                                               
         GETELN (R4),DATADISP,ELCODE,9                                          
         LTORG                                                                  
         EJECT                                                                  
*---------------------------------------------------------------------          
*        CLOSE DOWNLOAD                                                         
*---------------------------------------------------------------------          
         USING DLCBD,R4                                                         
CLSEDWN  NTR1  BASE=*,LABEL=*                                                   
         LA    R4,DLCB                                                          
         MVI   DLCBACT,DLCBEOR      END OF REPORT                               
         GOTO1 =V(DLFLD),DLCBD      LAST FOR REPORT                             
*                                                                               
         XIT1                                                                   
         DROP  R4                                                               
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
*---------------------------------------------------------------------          
*        END-OF-LINE DOWNLOAD                                                   
*---------------------------------------------------------------------          
         USING DLCBD,R4                                                         
EOLDOWN  NTR1  BASE=*,LABEL=*                                                   
         LA    R4,DLCB                                                          
         MVI   DLCBACT,DLCBEOL      END OF LINE                                 
         GOTO1 =V(DLFLD),DLCBD      (DON'T USE RF, BASR RE,RF BELOW)            
*                                                                               
         XIT1                                                                   
*                                                                               
         DROP  R4                                                               
         LTORG                                                                  
         EJECT                                                                  
*---------------------------------------------------------------------          
*        FILE HEADER                                                            
*---------------------------------------------------------------------          
FILHEAD  NTR1  BASE=*,LABEL=*                                                   
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C'FH'),2      FILE HEADER               
*                                                                               
         LA    RF,=C'P+'            EMPLOYER=PRODUCTIONS PLUS                   
         CLI   RECNUM,PK            EVENT CHECKS?                               
         BE    FILHEAD5                                                         
         LA    RF,=C'TP'            EMPLOYER=TP                                 
         CLI   RECNUM,PC            PRINT CHECKS?                               
         BE    *+8                                                              
         CLI   RECNUM,EC            EURO CHECKS?                                
         BNE   *+8                                                              
         LA    RF,=C'PP'            EMPLOYER=PP                                 
FILHEAD5 GOTO1 =A(SENDDOWN),DMCB,(C'T',(RF)),2                                  
*                                                                               
         LA    RF,=C'US'            COUNTRY=US                                  
         CLI   RECNUM,CN            CANADIAN CHECKS?                            
         BNE   *+8                                                              
         LA    RF,=C'CN'            COUNTRY=CANADIAN                            
         CLI   RECNUM,EC            EURO CHECKS?                                
         BNE   *+8                                                              
         LA    RF,=C'EU'            COUNTRY=EURO                                
         GOTO1 =A(SENDDOWN),DMCB,(C'T',(RF)),2                                  
*                                                                               
         BRAS  RE,EOLDOWN                                                       
         XIT1                                                                   
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*---------------------------------------------------------------------          
*        CHECK HEADER                                                           
*---------------------------------------------------------------------          
CHKHEAD  NTR1  BASE=*,LABEL=*                                                   
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C'CH'),2      CHECK HEADER              
*                                                                               
         CLI   RECNUM,PC            PRINT CHECKS?                               
         BE    CKHEAD10             YES, DIFFERENT STUFF                        
         CLI   RECNUM,EC            EURO CHECKS?                                
         BE    CKHEAD10             YES, DIFFERENT STUFF                        
*                                                                               
         CLI   RECNUM,PK            P+ CHECKS?                                  
         BE    CKHEAD03             YES, DIFFERENT STUFF                        
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C'TP'),2      TALENT PARTNERS           
         B     CKHEAD05                                                         
                                                                                
CKHEAD03 GOTO1 =A(SENDDOWN),DMCB,(C'T',=C'P+'),2      PAYROLL+                  
CKHEAD05 MVC   COMPFONE,=CL38'Talent Partners   312-923-7900'                   
         LA    RF,COMPFONE+18                                                   
         BAS   RE,OFFPHONE                                                      
         GOTO1 =A(SENDDOWN),DMCB,(C'T',COMPFONE),L'COMPFONE                     
         LA    RF,=C'Talent Partners'                                           
         GOTO1 =A(SENDDOWN),DMCB,(C'T',(RF)),15                                 
         LA    RF,=CL24'Commercial Services, LLC'                               
         CLI   RECNUM,PK                                                        
         BNE   *+8                                                              
         LA    RF,=CL24'Payroll Services'                                       
         GOTO1 =A(SENDDOWN),DMCB,(C'T',(RF)),24                                 
         B     CKHEAD20                                                         
*                                                                               
CKHEAD10 GOTO1 =A(SENDDOWN),DMCB,(C'T',=C'PP'),2      PRINT PAYROLL             
         MVC   COMPFONE,=CL38'Print Payroll Services    212-727-1800'           
         LA    RF,COMPFONE+26                                                   
         BAS   RE,OFFPHONE                                                      
         GOTO1 =A(SENDDOWN),DMCB,(C'T',COMPFONE),L'COMPFONE                     
         LA    RF,=C'Print Payroll Services'                                    
         GOTO1 =A(SENDDOWN),DMCB,(C'T',(RF)),22                                 
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C' '),1                                 
*                                                                               
CKHEAD20 OC    SRTSUC,SPACES                                                    
         GOTO1 =A(SENDDOWN),DMCB,(C'L',SRTSUC),L'SRTSUC    STATE UC             
         GOTO1 =A(SENDDOWN),DMCB,(C'T',CHKNCHR),L'CHKNCHR  CHK NUMBER           
         GOTO1 =A(SENDDOWN),DMCB,(C'D',SRTCDTE),6          CHK DATE             
         GOTO1 =A(SENDDOWN),DMCB,(C'D',SRTCDTE),6          PAY ENDING           
         GOTO1 =A(SENDDOWN),DMCB,(C'T',SRTAGY),L'SRTAGY    AGENCY               
         GOTO1 =A(SENDDOWN),DMCB,(C'I',SRTINV),6           INVOICE              
*                                                                               
         BRAS  RE,EOLDOWN                                                       
CKHEAD99 XIT1                                                                   
*                                                                               
         LTORG                                                                  
COMPFONE DS    CL38                                                             
         EJECT                                                                  
*                                                                               
OFFPHONE NTR1                                                                   
         LA    R2,OFFTAB                                                        
         USING OFFD,R2             R2=A(OFFICE TABLE)                           
         LA    R0,MAXOFFS          R0=MAX N'OFFICES                             
*                                                                               
OPHONE3  CLC   SRTTPOF,OFFCODE     MATCH ON OFFICE CODE                         
         BE    OPHONE5                                                          
         LA    R2,OFFNEXT                                                       
         BCT   R0,OPHONE3                                                       
         B     CKHEAD99            NOT FOUND - SKIP                             
*                                                                               
OPHONE5  DS    0H                                                               
         MVC   0(3,RF),OFFTEL      OFFICE PHONE NUMBER                          
         MVC   4(3,RF),OFFTEL+3                                                 
         MVC   8(4,RF),OFFTEL+6                                                 
         B     CKHEAD99                                                         
         EJECT                                                                  
*---------------------------------------------------------------------          
*        EMPLOYEE DETAIL                                                        
*---------------------------------------------------------------------          
EMPDETL  NTR1  BASE=*,LABEL=*                                                   
         BAS   RE,EDGETW4                                                       
*                                                                               
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C'ED'),2      EMPLOYEE DETAIL           
         GOTO1 =A(SENDDOWN),DMCB,(C'L',SRTNAME),L'SRTNAME   NAME                
*                                                                               
         CLC   SRTPNAM,SPACES      ADDRESS CHANGED BECAUSE OF AGENT             
         BNH   EMDETL03                                                         
         GOTO1 =A(EDWADD),DMCB,BLOCK                  ADDRESS                   
         B     EMDETL04                                                         
*                                                                               
EMDETL03 GOTO1 =A(EDWADD),DMCB,SRTADD1                ADDRESS                   
*                                                                               
EMDETL04 DS    0H                                                               
*                                                                               
         MVC   BLOCK(20),SPACES                                                 
         CLI   SRTW4TY,TAW4TYCO    IF PERFORMER IS NOT CORPORATION              
         BE    EMDETL05                                                         
         CLI   SRTW4TY,TAW4TYCA    OR CANADIAN                                  
         BE    EMDETL05                                                         
         CLI   SRTW4TY,TAW4TYTR    OR TRUSTEE                                   
         BE    EMDETL05                                                         
*                                                                               
         MVC   BLOCK(9),SRTSSN    THEN PRINT SSN IN SSN FIELD                   
         B     EMDETL08                                                         
*                                                                               
EMDETL05 MVC   BLOCK+10(9),SRTSSN    ELSE PRINT SSN IN FEDERAL ID FIELD         
*                                                                               
EMDETL08 OC    SRTFSO,SRTFSO       IF FSO EXISTS                                
         BZ    *+10                                                             
         MVC   BLOCK(9),SRTFSO    THEN PRINT FSO IN SSN FIELD                   
*                                                                               
         MVC   WORK,SPACES                                                      
         CLC   BLOCK(9),SPACES                                                  
         BNH   EMDETL10                                                         
         MVC   WORK(7),=C'XXX-XX-'                                              
*                                                                               
         BAS   RE,CANUNION         IF CANADIAN, SHOW ALL OF IT                  
         BNE   EMDETL09                                                         
         MVC   WORK(3),BLOCK                                                    
         MVC   WORK+4(2),BLOCK+3                                                
*                                                                               
EMDETL09 MVC   WORK+7(4),BLOCK+5                                                
EMDETL10 GOTO1 =A(SENDDOWN),DMCB,(C'T',WORK),11             SS#                 
         GOTO1 =A(SENDDOWN),DMCB,(C'L',SRTFSON),L'SRTFSON   CORP FSO            
*                                                                               
EMDETL30 MVC   WORK,SPACES                                                      
         CLC   BLOCK+10(9),SPACES                                               
         BNH   EMDETL40                                                         
         MVC   WORK(6),=C'XX-XXX'                                               
*                                                                               
         BAS   RE,CANUNION         IF CANADIAN, SHOW ALL OF IT                  
         BNE   EMDETL35                                                         
         MVC   WORK(2),BLOCK+10                                                 
         MVC   WORK+3(3),BLOCK+12                                               
*                                                                               
EMDETL35 MVC   WORK+6(4),BLOCK+15                                               
EMDETL40 GOTO1 =A(SENDDOWN),DMCB,(C'T',WORK),10             FEDERAL ID          
*                                                                               
         GOTO1 =A(SENDDOWN),DMCB,(C'T',SRTGST#),L'SRTGST#   GST #               
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C' '),1             PST                 
* WITHOLDING TAX UNITS                                                          
         BAS   RE,EDGETWH          GET WITHHOLDING DATA                         
*                                                                               
EMDETL60 CLI   SRTW4TY,TAW4TYTR    TRUSTEE?                                     
         BE    EMDETL61            YES, DON'T SHOW WORK STATE & CITY            
         GOTO1 =A(SENDDOWN),DMCB,(C'T',SRTSOW),L'SRTSOW    STATE WORK           
         GOTO1 =A(SENDDOWN),DMCB,(C'T',SRTCOW),L'SRTCOW    CITY WORK            
         B     EMDETL63                                                         
*                                                                               
EMDETL61 GOTO1 =A(SENDDOWN),DMCB,(C'T',=C' '),1             BLANK               
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C' '),1             BLANK               
*                                                                               
EMDETL63 CLC   SRTPNAM,SPACES                                                   
         BNH   EMDETL70                                                         
         MVC   BLOCK(L'SRTPNAM),SRTPNAM                                         
         LA    R3,L'SRTPNAM                                                     
         LA    RF,BLOCK+L'SRTPNAM-1                                             
EMDETL65 CLI   0(RF),C' '                                                       
         BH    EMDETL68                                                         
         BCTR  RF,0                                                             
         BCT   R3,EMDETL65                                                      
*                                                                               
EMDETL68 MVC   1(2,RF),=C', '                                                   
         LA    R3,2(R3)                                                         
         LA    RF,3(RF)                                                         
         MVC   0(4,RF),=4C'00'                                                  
         GOTO1 TRNSAGT,DMCB,(X'40',SRTNCDE),0(RF)                               
         LA    R3,4(R3)                                                         
*                                                                               
         GOTO1 =A(SENDDOWN),DMCB,(C'L',BLOCK),(R3)       AG NAME,CODE           
         GOTO1 =A(EDWADD),DMCB,SRTADD1                      ADDRESS             
         B     EMDETL80                                                         
*                                                                               
EMDETL70 LA    R3,5                5 BLANK FIELDS                               
EMDETL71 GOTO1 =A(SENDDOWN),DMCB,(C'T',=C' '),1             BLANK               
         BCT   R3,EMDETL71                                                      
*                                                                               
EMDETL80 DS    0H                                                               
*                                                                               
EMDETL99 BRAS  RE,EOLDOWN                                                       
EMDETLX  XIT1                                                                   
         EJECT                                                                  
*---------------------------------------------------------------------          
*        CANADIAN UNION                                                         
*---------------------------------------------------------------------          
CANUNION NTR1                                                                   
         CLC   SRTUN,=C'ACT'                                                    
         BE    CANUNYES                                                         
         CLC   SRTUN,=C'UDA'                                                    
         BE    CANUNYES                                                         
         CLC   SRTUN,=C'NON'                                                    
         BNE   CANUNNO                                                          
         CLC   SRTLOCL,=C'ACT'                                                  
         BE    CANUNYES                                                         
         CLC   SRTLOCL,=C'UDA'                                                  
         BNE   CANUNNO                                                          
*                                                                               
CANUNYES SR    RC,RC                                                            
CANUNNO  LTR   RC,RC                                                            
         B     EMDETLX                                                          
*---------------------------------------------------------------------          
*        GET W4 FOR ADDRESS AND EXEMPTIONS                                      
*---------------------------------------------------------------------          
EDWADD   NTR1                                                                   
         LA    R2,4                                                             
         LA    R4,4                                                             
         L     R3,0(R1)            POINT TO ADDRESS LINES                       
*                                                                               
EDWADD10 DS    0H                                                               
*        GOTO1 =A(MIXDCASE),DMCB,(R3),30                                        
         LA    RE,L'SRTADD1                                                     
         CHI   R2,1                LAST LINE?                                   
         BNE   *+8                                                              
         LA    RE,L'SRTADD4        IS LONGER                                    
         ST    RE,DMCB+4                                                        
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         CLC   0(0,R3),SPACES                                                   
         BNH   EDWADD30                                                         
         GOTO1 =A(SENDDOWN),DMCB,(C'T',(R3))             ADDRESS                
         BCTR  R4,0                DECREASE # OF LINES LEFT                     
EDWADD30 LA    R3,L'SRTADD1(R3)    BUMP TO NEXT LINE                            
         BCT   R2,EDWADD10         UNTIL DONE                                   
*                                                                               
         LTR   R4,R4               NONE LEFT?                                   
         BZ    EDWADDX             QUIT                                         
EDWADD40 GOTO1 =A(SENDDOWN),DMCB,(C'T',=C' '),1     ELSE FILL W/ BLANK          
         BCT   R4,EDWADD40                                                      
*                                                                               
EDWADDX  B     EMDETLX                                                          
*---------------------------------------------------------------------          
*        GET W4 FOR ADDRESS AND EXEMPTIONS                                      
*---------------------------------------------------------------------          
EDGETW4  NTR1                                                                   
         L     R5,AIO                                                           
         MVC   WORK,SPACES                                                      
         MVC   BLOCK,SPACES                                                     
         MVC   WORK(9),SRTSSN                                                   
         OC    SRTFSO,SRTFSO                                                    
         BZ    *+10                                                             
         MVC   WORK(9),SRTFSO                                                   
*                                                                               
         MVC   AIO,AW4IO                                                        
         GOTO1 RECVAL,DMCB,TLW4CDQ,(X'A0',WORK)       READ W4 RECORD            
         ST    R5,AIO                                                           
*                                                                               
         L     R4,AW4IO                                                         
         MVI   ELCODE,TAA2ELQ      LOOK FOR NEW STYLE ADDRESS                   
         BRAS  RE,GETEL                                                         
         BE    EDGW30                                                           
         L     R4,AW4IO                                                         
         MVI   ELCODE,TAADELQ      ELSE LOOK FOR OLD STYLE ADDRESS              
         BRAS  RE,GETEL                                                         
         BNE   EDGWXIT                                                          
*                                                                               
         USING TAADD,R4                                                         
EDGW25   ZIC   RF,TAADLEN          SAVE ADDRESS IN SORT RECORD                  
         SH    RF,=H'4'                                                         
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   BLOCK(0),TAADADD                                                 
         AHI   RF,1                                                             
         GOTO1 =A(MIXDCASE),DMCB,BLOCK,(RF)                                     
         B     EDGWXIT                                                          
*                                                                               
         USING TAA2D,R4                                                         
EDGW30   CLI   TAA2LEN,TAA2LNQ        FOR OLD STYLE ADDRESSES                   
         BL    *+14                                                             
         CLC   TAA2CTRY,=C'US'        AND NEW STYLE US ADDRESSES                
         BNE   EDGW40                                                           
         MVC   BLOCK(3*30),TAA2ADDR   SAVE FIRST 3 ADDRESS LINES                
         GOTO1 =A(MIXDCASE),DMCB,BLOCK,90                                       
         MVC   WORK(25),TAA2CITY      CITY                                      
         GOTO1 =A(MIXDCASE),DMCB,WORK,25                                        
         MVC   WORK+26(2),TAA2ST      STATE                                     
         MVC   WORK+29(10),TAA2ZIP    ZIP                                       
         GOTO1 SQUASHER,DMCB,WORK,39  SQUASH IT                                 
         MVC   BLOCK+90(39),WORK      SAVE IT                                   
         B     EDGWXIT                                                          
*                                                                               
EDGW40   MVC   BLOCK(2*30),TAA2ADDR   ELSE SAVE FIRST 2 ADDRESS LINES           
         GOTO1 =A(MIXDCASE),DMCB,BLOCK,60                                       
         MVC   WORK(25),TAA2CITY      CITY                                      
         GOTO1 =A(MIXDCASE),DMCB,WORK,25                                        
         MVC   WORK+26(2),TAA2ST      STATE                                     
         MVC   WORK+29(10),TAA2ZIP    ZIP                                       
         GOTO1 SQUASHER,DMCB,WORK,39  SQUASH IT                                 
         MVC   BLOCK+60(39),WORK      SAVE IT                                   
         GOTO1 VALCTRY,DMCB,(X'80',TAA2CTRY)                                    
         BNE   EDGWXIT                                                          
         USING CTRYTABD,R1                                                      
         L     R1,TGACTRY                                                       
         ZIC   RE,CTRYDSP                                                       
         LTR   RE,RE                                                            
         BNZ   *+8                                                              
         IC    RE,CTRYLEN                                                       
         SHI   RE,CTRYDESC-CTRYTABD+1                                           
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   BLOCK+90(0),CTRYDESC                                             
EDGWXIT  B     EMDETLX                                                          
         DROP  R1,R4                                                            
         EJECT                                                                  
*---------------------------------------------------------------------          
*        GET WH ELEMENTS FOR EXEMPTIONS                                         
*---------------------------------------------------------------------          
EDGETWH  NTR1                                                                   
         MVI   BYTE,3              3 WITHHOLDINGS TO LOOK UP                    
*                                                                               
         USING TAWHD,R4                                                         
EDGWH05  L     R4,AW4IO                                                         
         MVI   ELCODE,TAWHELQ      LOOK FOR NEW STYLE ADDRESS                   
         BRAS  RE,GETEL                                                         
         B     *+8                                                              
EDGWH10  BRAS  RE,NEXTEL                                                        
         BNE   EDGWH30                                                          
         CLI   BYTE,3              FEDERAL                                      
         BNE   EDGWH13                                                          
         CLC   TAWHUNIT,=C'FD '    FEDERAL TAX UNIT                             
         BE    EDGWH20                                                          
         B     EDGWH10                                                          
*                                                                               
EDGWH13  CLI   BYTE,2              STATE                                        
         BNE   EDGWH15                                                          
         CLC   TAWHUNIT,=C'FD '    NOT FD                                       
         BE    EDGWH10                                                          
         CLI   TAWHUNIT+2,C' '     STATE DOESN'T HAVE A 3RD CHAR                
         BE    EDGWH20                                                          
         B     EDGWH10                                                          
*                                                                               
EDGWH15  CLI   BYTE,1              LOCAL                                        
         BNE   EDGWH30                                                          
         CLI   TAWHUNIT+2,C' '     LOCAL HAVE A 3RD CHAR                        
         BNE   EDGWH20                                                          
         B     EDGWH10                                                          
*                                                                               
EDGWH20  GOTO1 =A(SENDDOWN),DMCB,(C'T',TAWHUNIT),L'TAWHUNIT   FED UNIT          
         GOTO1 =A(SENDDOWN),DMCB,(C'T',TAWHSTAT),L'TAWHSTAT   MAR STAT          
         SR    R1,R1                                                            
         IC    R1,TAWHEXS                                                       
         ST    R1,WORK                                                          
         GOTO1 =A(SENDDOWN),DMCB,(C'N',WORK),4                                  
         GOTO1 =A(SENDDOWN),DMCB,(C'A',TAWHFLAT),L'TAWHFLAT                     
         B     EDGWH50                                                          
*                                                                               
EDGWH30  GOTO1 =A(SENDDOWN),DMCB,(C'T',=C' '),1                                 
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C' '),1                                 
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C' '),1                                 
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C' '),1                                 
*                                                                               
EDGWH50  SR    RF,RF                                                            
         IC    RF,BYTE                                                          
         AHI   RF,-1                                                            
         STC   RF,BYTE                                                          
         BNZ   EDGWH05                                                          
*                                                                               
EDGWHXIT B     EMDETLX                                                          
*                                                                               
         LTORG                                                                  
         DROP  R4                                                               
         EJECT                                                                  
*---------------------------------------------------------------------          
*        COMMERCIAL DETAIL                                                      
*---------------------------------------------------------------------          
COMDETL  NTR1  BASE=*,LABEL=*                                                   
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C'CD'),2      COMML DETAIL              
*                                                                               
         LA    RF,=C'TP'            EMPLOYER=TP                                 
         CLI   RECNUM,PK                                                        
         BNE   *+8                                                              
         LA    RF,=C'P+'            EMPLOYER=P+                                 
*                                                                               
         CLI   RECNUM,PC            PRINT CHECKS?                               
         BE    *+8                                                              
         CLI   RECNUM,EC                                                        
         BNE   *+8                                                              
         LA    RF,=C'PP'            EMPLOYER=PP                                 
CMDTL100 GOTO1 =A(SENDDOWN),DMCB,(C'T',(RF)),2                                  
*                                                                               
         GOTO1 =A(SENDDOWN),DMCB,(C'L',SRTAGYN),L'SRTAGYN  AGENCY NAME          
         GOTO1 =A(SENDDOWN),DMCB,(C'T',SRTAGY),L'SRTAGY    AGENCY               
         GOTO1 =A(SENDDOWN),DMCB,(C'T',SRTCID),L'SRTCID    COMMERCIAL           
*                                                                               
         CLI   RECNUM,PC           PRINT CHECKS?                                
         BE    CMDTL500                                                         
         CLI   RECNUM,EC                                                        
         BE    CMDTL500                                                         
*                                                                               
         GOTO1 =A(SENDDOWN),DMCB,(C'T',TGMENAME),L'TGMENAME   MEDIA             
         GOTO1 =A(SENDDOWN),DMCB,(C'D',SRTFRST),6          DT 1ST SERV          
*                                                                               
*        TM    TGCAUNI,AFM         IF PERFORMER IS NOT A MUSICIAN               
         GOTO1 UNITEST,DMCB,TGCAUNIS,AFM,0,0,0                                  
         BO    CMDTL105                                                         
         MVC   WORK(3),SRTCFCY     FIRST FIXED CYCLE COMES FROM THE             
         OC    SRTCFCY,SRTCFCY     COMMERCIAL UNLESS OVERRIDDEN BY              
         BNZ   *+10                THE CAST RECORD                              
         MVC   WORK(3),SRTFCYC                                                  
         GOTO1 =A(SENDDOWN),DMCB,(C'D',WORK),6             1ST FIXD CYC         
         B     CMDTL110                                                         
*                                                                               
CMDTL105 GOTO1 =A(SENDDOWN),DMCB,(C'T',=C' '),1                                 
CMDTL110 GOTO1 =A(SENDDOWN),DMCB,(C'D',SRTAIR),6           1ST AIR              
         GOTO1 =A(SENDDOWN),DMCB,(C'A',SRTOV1),4           OV1 %                
         GOTO1 =A(SENDDOWN),DMCB,(C'A',SRTOV2),4           OV2 %                
         GOTO1 =A(SENDDOWN),DMCB,(C'L',SRTCLIN),L'SRTCLIN  ADVERTISER           
         GOTO1 =A(SENDDOWN),DMCB,(C'T',SRTORIG),L'SRTORIG  ORIGINAL             
         GOTO1 =A(SENDDOWN),DMCB,(C'T',SRTEDIT),L'SRTEDIT  EDIT                 
         GOTO1 =A(SENDDOWN),DMCB,(C'T',SRTUN),L'SRTUN      UNION                
         GOTO1 =A(SENDDOWN),DMCB,(C'T',SRTLOCL),L'SRTLOCL  LOCAL                
         GOTO1 =A(SENDDOWN),DMCB,(C'T',SRTCAT),L'SRTCAT    CATEGORY             
*                                                                               
         BAS   RE,RCTNAME          RETRIEVE CATEGORY NAME                       
         GOTO1 =A(SENDDOWN),DMCB,(C'L',BLOCK),32           CTGRY NAME           
*                                                                               
         CLI   SRTDBL,C' '                                                      
         BE    CMDTL120                                                         
         MVC   BLOCK(1),SRTDBL                                                  
         MVC   BLOCK+1(4),=C' DBL'                                              
         GOTO1 =A(SENDDOWN),DMCB,(C'T',BLOCK),5            DOUBLES              
         B     CMDTL130                                                         
*                                                                               
CMDTL120 GOTO1 =A(SENDDOWN),DMCB,(C'T',=C' '),1            NO DOUBLES           
CMDTL130 GOTO1 =A(SENDDOWN),DMCB,(C'T',SRTONOF),L'SRTONOF  ON/OFF               
         GOTO1 =A(SENDDOWN),DMCB,(C'T',SRTYEAR),L'SRTYEAR  CONTRACT YR          
         GOTO1 =A(SENDDOWN),DMCB,(C'L',SRTPRDN),L'SRTPRDN  PRODUCT NAME         
         GOTO1 =A(SENDDOWN),DMCB,(C'L',SRTTITL),L'SRTTITL  COMML TITLE          
*                                                                               
         MVC   WORK(3),SRTCEXP     EXPIRATION DATE COMES FROM THE               
         OC    SRTCEXP,SRTCEXP     COMMERCIAL UNLESS OVERRIDDED BY              
         BNZ   *+10                THE CAST RECORD                              
         MVC   WORK(3),SRTEXP                                                   
         GOTO1 =A(SENDDOWN),DMCB,(C'D',WORK),6             EXPR DATE            
*                                                                               
         LA    RF,=C'Yes'                                                       
         CLI   SRTMED,C'C'         MADE FOR CABLE?                              
         BE    *+8                                                              
         LA    RF,=C'No '                                                       
         GOTO1 =A(SENDDOWN),DMCB,(C'T',(RF)),3             CABLE COMML          
*                                                                               
         LA    RF,=C'Yes'                                                       
         CLI   SRTCTYP,CTYSPAN     MADE FOR SPANISH?                            
         BE    *+8                                                              
         LA    RF,=C'No '                                                       
         GOTO1 =A(SENDDOWN),DMCB,(C'T',(RF)),3             SPAN COMML           
*        TM    TGCAUNI,AFM         IF PERFORMER IS A MUSICIAN                   
         GOTO1 UNITEST,DMCB,TGCAUNIS,AFM,0,0,0                                  
         BZ    CMDTL200                                                         
         GOTO1 =A(SENDDOWN),DMCB,(C'T',SRTCON),12          AFM1                 
         GOTO1 =A(SENDDOWN),DMCB,(C'T',SRTCON+12),12       AFM2                 
         GOTO1 =A(SENDDOWN),DMCB,(C'T',SRTCON34),12        AFM3                 
         GOTO1 =A(SENDDOWN),DMCB,(C'T',SRTCON34+12),12     AFM4                 
         GOTO1 =A(SENDDOWN),DMCB,(C'L',SRTLEAD),L'SRTLEAD  LEADER               
         GOTO1 =A(SENDDOWN),DMCB,(C'D',SRTDUB),6           DUB DATE             
         B     CMDTL999            EOL AND LEAVE                                
*                                                                               
CMDTL200 LA    R3,6                                                             
CMDTL210 GOTO1 =A(SENDDOWN),DMCB,(C'T',=C' '),1                                 
         BCT   R3,CMDTL210                                                      
         B     CMDTL999            EOL AND LEAVE                                
*                                                                               
CMDTL500 DS    0H                  PRINT CHECK COMMERCIAL DETAIL                
         GOTO1 =A(SENDDOWN),DMCB,(C'L',SRTSVCNM),L'SRTSVCNM  SVC NAME           
         GOTO1 =A(SENDDOWN),DMCB,(C'L',SRTPHONM),L'SRTPHONM  PHOTOGR            
*                                                                               
         MVC   WORK(3),SRTSHOOT                                                 
         L     R4,ASRTCHK          R4=A(CHECK RECORD)                           
         MVI   ELCODE,TACAELQ                                                   
         BRAS  RE,GETEL            IF CHECK HAS SHOOT DATE                      
         BNE   CMDTL502                                                         
         USING TACAD,R4                                                         
         OC    TACASDTE,TACASDTE   DON'T USE IF ZERO                            
         BZ    CMDTL502                                                         
         MVC   WORK(3),TACASDTE    USE IT IF NOT ZERO                           
*                                                                               
CMDTL502 GOTO1 =A(SENDDOWN),DMCB,(C'D',WORK),6               SHOOT DATE         
         GOTO1 =A(SENDDOWN),DMCB,(C'T',SRTSTUD),L'SRTSTUD    SHOOT STUD         
         GOTO1 =A(SENDDOWN),DMCB,(C'T',SRTCITY),L'SRTCITY    SHOOT CITY         
         GOTO1 =A(SENDDOWN),DMCB,(C'L',SRTCLIN),L'SRTCLIN  ADVERTISER           
         GOTO1 =A(SENDDOWN),DMCB,(C'T',SRTCAT),L'SRTCAT    CATEGORY             
*                                                                               
         CLC   SRTCST,=6X'FF'      IF AGENT CHECK                               
         BNE   CMDTL503                                                         
         CLC   SRTCAT,=C'ZZZ'                                                   
         BNE   CMDTL503                                                         
         CLC   SRTCAT,=C'ZZ '                                                   
         BNE   CMDTL503                                                         
         MVC   BLOCK(32),=CL32'Agent'  PRINT AGENT FOR CATEGORY                 
         B     CMDTL505                                                         
*                                                                               
CMDTL503 BAS   RE,RCTNAME          RETRIEVE CATEGORY NAME                       
CMDTL505 GOTO1 =A(SENDDOWN),DMCB,(C'L',BLOCK),32           CTGRY NAME           
*                                                                               
         L     R4,ASRTCHK          R4=A(CHECK RECORD)                           
         MVI   ELCODE,TADLELQ      LOOK FOR DEAL ELEMENT                        
         BRAS  RE,GETEL                                                         
         BNE   CMDTL508                                                         
         USING TADLD,R4                                                         
         GOTO1 =A(SENDDOWN),DMCB,(C'D',TADLPUB),6                               
         GOTO1 =A(SENDDOWN),DMCB,(C'D',TADLEXP),6                               
*                                                                               
         MVC   BLOCK(2),=C'UN'     FILL BLOCK FOR LATER, UNLIMITED              
         CLI   TADLTERM,TADLTUNL   IF SPECIAL TERM                              
         BE    CMDTL510                                                         
         MVC   BLOCK(2),=C'1X'     ONE TIME                                     
         CLI   TADLTERM,TADLT1X    IF ONE TIME TERM                             
         BE    CMDTL510                                                         
         EDIT  TADLTERM,(2,BLOCK),ALIGN=LEFT  MONTHS                            
         B     CMDTL510                                                         
*                                                                               
CMDTL508 LA    R3,4                                                             
CMDTL509 GOTO1 =A(SENDDOWN),DMCB,(C'T',=C' '),1             4 BLANKS            
         BCT   R3,CMDTL509                                                      
         B     CMDTL520                                                         
*                                                                               
CMDTL510 LA    RF,SPACES                                                        
         CLC   SRTCST,=6X'FF'      SKIP IF AGENT CHECK                          
         BNE   CMDTL513                                                         
         CLC   SRTCAT,=C'ZZZ'                                                   
         BE    CMDTL515                                                         
         CLC   SRTCAT,=C'ZZ '                                                   
         BE    CMDTL515                                                         
*                                                                               
CMDTL513 L     R4,ASRTCHK          R4=A(CHECK RECORD)                           
         MVI   ELCODE,TACAELQ      LOOK FOR CAST ELEMENT                        
         BRAS  RE,GETEL                                                         
         BNE   CMDTL515                                                         
         USING TACAD,R4                                                         
         CLI   TACAEBAS,TACAEPUB                                                
         BNE   *+12                                                             
         LA    RF,=CL16'Publication Date'                                       
         B     *+8                                                              
         LA    RF,=CL16'Shoot Date'                                             
CMDTL515 GOTO1 =A(SENDDOWN),DMCB,(C'T',(RF)),16            BASED ON             
*                                                                               
         GOTO1 =A(SENDDOWN),DMCB,(C'T',BLOCK),2            MONTHS               
CMDTL520 GOTO1 =A(SENDDOWN),DMCB,(C'L',SRTPRDN),L'SRTPRDN  PRODUCT NAME         
         GOTO1 =A(SENDDOWN),DMCB,(C'L',SRTTITL),L'SRTTITL  COMML TITLE          
         L     R4,ASRTCHK          R4=A(CHECK RECORD)                           
         MVI   ELCODE,TADLELQ      LOOK FOR DEAL ELEMENT                        
         BRAS  RE,GETEL                                                         
         BNE   CMDTL590                                                         
         USING TADLD,R4                                                         
         MVC   BLOCK(L'TGNAME),SPACES                                           
         MVC   AIO,AIO1                                                         
         GOTO1 RECVAL,DMCB,TLUSCDQ,(X'AC',TADLUSE),0                            
         BNE   *+10                                                             
         MVC   BLOCK(L'TGNAME),TGNAME                                           
         GOTO1 =A(SENDDOWN),DMCB,(C'T',BLOCK),L'TGNAME     USE                  
*                                                                               
         MVC   BLOCK(L'TGNAME),SPACES                                           
         GOTO1 RECVAL,DMCB,TLARCDQ,(X'AC',TADLAREA),0                           
         BNE   *+10                                                             
         MVC   BLOCK(L'TGNAME),TGNAME                                           
         GOTO1 =A(SENDDOWN),DMCB,(C'L',BLOCK),L'TGNAME     AREA                 
         MVC   AIO,ASRTCHK                                                      
         B     CMDTL600                                                         
*                                                                               
CMDTL590 GOTO1 =A(SENDDOWN),DMCB,(C'T',=C' '),1            2 BLANKS             
         BASR  RE,RF                                                            
CMDTL600 GOTO1 =A(SENDDOWN),DMCB,(C'T',=C' '),1            PAYEE                
*                                                                               
CMDTL999 BRAS  RE,EOLDOWN                                                       
CMDTLXIT XIT1                                                                   
*                                                                               
         EJECT                                                                  
*---------------------------------------------------------------------          
*        RETRIEVE CATEGORY NAME                                                 
*            SAVE IT IN BLOCK                                                   
*---------------------------------------------------------------------          
RCTNAME  NTR1                                                                   
         MVC   BLOCK(L'SPACES),SPACES                                           
         L     RF,TGACATS                                                       
         USING CATTABD,RF                                                       
RCTNAME1 CLI   0(RF),X'FF'         TEST END OF TABLE                            
         BE    CMDTLXIT                                                         
         CLC   CATCDE,SRTCAT       FIND CATEGORY                                
         BE    RCTNAME5                                                         
         ZIC   R1,CATLEN                                                        
         AR    RF,R1                                                            
         B     RCTNAME1                                                         
RCTNAME5 ZIC   RE,CATLEN           SET CATEGORY NAME                            
         SH    RE,=Y(CATNAME-CATTABD+1)                                         
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   BLOCK(0),CATNAME    SAVE IT IN BLOCK                             
         B     CMDTLXIT                                                         
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*---------------------------------------------------------------------          
*        USE INFO                                                               
*---------------------------------------------------------------------          
USEINFO  NTR1  BASE=*,LABEL=*                                                   
         CLI   RECNUM,PC           SKIP IF PRINT CHECKS                         
*        BE    UINFOXIT                                                         
*                                                                               
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C'UI'),2           USE INFO             
*                                                                               
         LA    RF,=C'US'            COUNTRY=US                                  
         CLI   RECNUM,CN            CANADIAN CHECKS?                            
         BNE   *+8                                                              
         LA    RF,=C'CN'            COUNTRY=CANADIAN                            
         CLI   RECNUM,EC            EUROPEAN CHECKS?                            
         BNE   *+8                                                              
         LA    RF,=C'EC'            COUNTRY=EUROPEAN                            
         GOTO1 =A(SENDDOWN),DMCB,(C'T',(RF)),2                                  
*                                                                               
         LA    RF,=C'Session'                                                   
         TM    TGUSSTAT,SESSION    THEN PUT 'SESSION' IN PAY TYPE FLD           
         BO    *+8                                                              
         LA    RF,=C'Reuse  '                                                   
         CLI   RECNUM,PK                                                        
         BNE   *+8                                                              
         LA    RF,=C'Event  '                                                   
         GOTO1 =A(SENDDOWN),DMCB,(C'T',(RF)),7                                  
*                                                                               
         CLI   TGUSEQU,UDEM        DON'T PRINT CYCLE DATES                      
         BE    UINFO10              . . . FOR DEMO PAYMENTS                     
         CLI   TGUSEQU,UCDM                                                     
         BE    UINFO10                                                          
         GOTO1 =A(SENDDOWN),DMCB,(C'D',SRTCYCS),6          CYC ST DATE          
         GOTO1 =A(SENDDOWN),DMCB,(C'D',SRTCYCE),6          CYC EN DATE          
         B     UINFO20                                                          
*                                                                               
UINFO10  GOTO1 =A(SENDDOWN),DMCB,(C'T',=C' '),1            BLANK                
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C' '),1                                 
*                                                                               
UINFO20  GOTO1 =A(SENDDOWN),DMCB,(C'T',SRTUSE),L'SRTUSE    USE                  
         MVC   BLOCK(L'SPACES),SPACES                                           
         CLI   RECNUM,PK                                                        
         BE    *+10                                                             
         MVC   BLOCK(L'TGUSNAME),TGUSNAME                                       
         GOTO1 =A(MIXDCASE),DMCB,BLOCK,L'TGUSNAME                               
*                                                                               
         LA    RF,BLOCK+L'TGUSNAME                                              
         CLI   0(RF),C' '                                                       
         BH    *+8                                                              
         BCT   RF,*-8                                                           
         LA    RF,2(RF)                                                         
*                                                                               
         CLC   SRTUSE,=C'RTK'      IF USE IS INDUSTRIAL RETAKE                  
         BNE   UINFO30                                                          
         CLI   SRTIST,0            ADD TYPE TO DESCRIPTION                      
         BE    UINFO70                                                          
         MVC   0(L'RTKES,RF),RTKES                                              
         TM    SRTIST,TASDIENQ                                                  
         BO    UINFO70                                                          
         MVC   0(L'RTK60,RF),RTK60                                              
         TM    SRTIST,TASDI60Q                                                  
         BO    UINFO70                                                          
         MVC   0(L'RTKPS,RF),RTKPS                                              
         B     UINFO70                                                          
                                                                                
UINFO30  OC    SRTUMAJ(3),SRTUMAJ  IF UPGRADE EXISTS                            
         BZ    UINFO50                                                          
         MVC   BYTE,SRTUMAJ        SAVE SRTUMAJ IN BYTE                         
         MVC   HALF,SRTUUNT        SAVE SRTUUNT IN HALF                         
         CLI   SRTVERS,0           IF HAVE VERSION CODE                         
         BE    UINFO53                                                          
         MVC   BYTE,SRTVUMAJ        USE SRTVUMAJ                                
         MVC   HALF,SRTVUUNT        AND SRTVUUNT INSTEAD                        
         B     UINFO53                                                          
*                                                                               
UINFO50  OC    SRTMAJ(3),SRTMAJ    IF MAJORS/UNITS EXISTS                       
         BZ    UINFO59X                                                         
         CLI   TGUSEQU,UFGR        SKIP IF FGR USE                              
         BE    UINFO59X                                                         
         CLI   TGUSEQU,ULCB        SKIP IF LCB USE                              
         BE    UINFO59X                                                         
         MVC   BYTE,SRTMAJ         SAVE SRTMAJ IN BYTE                          
         MVC   HALF,SRTUNT         SAVE SRTUNT IN HALF                          
         CLI   SRTVERS,0           IF HAVE VERSION CODE                         
         BE    UINFO53                                                          
         MVC   BYTE,SRTVMAJ        USE SRTVMAJ                                  
         MVC   HALF,SRTVUNT        AND SRTVUNT INSTEAD                          
*                                                                               
UINFO53  BAS   RE,UINFOPM          PRINT THE MARKETS                            
*                                                                               
*                                                                               
UINFO55  EDIT  (2,HALF),(4,(RF)),ALIGN=LEFT,ZERO=NOBLANK                        
*                                                                               
UINFO58  LA    RF,BLOCK+L'TGUSNAME                                              
         CLI   0(RF),C' '                                                       
         BH    *+8                                                              
         BCT   RF,*-8                                                           
         LA    RF,2(RF)                                                         
*                                                                               
UINFO59X TM    TGUSTYST,MAJORS       AND IS NEW TYPE WITH MAJORS/AREAS          
         BZ    UINFO60                                                          
         CLI   TGUSEQU,UFGR          IF FGR USE                                 
         BNE   UINFO60                                                          
         MVC   2(L'TGMACHAR,RF),TGMACHAR  DISPLAY AREA EXPRESSION               
*                                                                               
UINFO60  LA    RF,1(RF)                                                         
         XR    R1,R1                                                            
         CLI   SRTITAG,0             IF TAGS EXISTS                             
         BZ    UINFO63                                                          
         ZIC   R1,SRTITAG                                                       
UINFO63  CLI   SRTDEMO,0             IF DEMO EXISTS                             
         BZ    UINFO65                                                          
         ZIC   R1,SRTDEMO                                                       
UINFO65  OC    SRTUSES,SRTUSES       IF USES EXISTS                             
         BZ    UINFO69                                                          
         LH    R1,SRTUSES                                                       
*                                                                               
UINFO69  LTR   R1,R1                                                            
         BZ    UINFO70                                                          
         STH   R1,HALF               SHOW TAGS                                  
*                                                                               
         OC    SRTSTUS,SRTSTUS     USE RANGE                                    
         BZ    UINFO69A                                                         
*                                                                               
         SR    R2,R2               R2 = STARTING USE NUMBER                     
         ICM   R2,3,SRTSTUS                                                     
         SR    R3,R3               R3 = ENDING USE NUMBER                       
         ICM   R3,3,SRTUSES                                                     
         AR    R3,R2                                                            
         BCTR  R3,0                                                             
         STH   R2,HALF                                                          
*                                                                               
         CR    R2,R3               IF R2 = R3, 1 USE                            
         BE    UINFO69A            NOT =, USE RANGE                             
         EDIT  (R2),(4,(RF)),ALIGN=LEFT,ZERO=NOBLANK                            
         AR    RF,R0                                                            
         MVC   0(3,RF),=C' - '                                                  
         AHI   RF,3                                                             
         EDIT  (R3),(4,(RF)),ALIGN=LEFT,ZERO=NOBLANK                            
         B     UINFO69X                                                         
*                                                                               
UINFO69A EDIT  (2,HALF),(4,(RF)),ALIGN=LEFT,ZERO=NOBLANK                        
*                                                                               
UINFO69X LA    RF,BLOCK+L'TGUSNAME                                              
         CLI   0(RF),C' '                                                       
         BH    *+8                                                              
         BCT   RF,*-8                                                           
         LA    RF,2(RF)                                                         
*                                                                               
UINFO70  GOTO1 =A(SENDDOWN),DMCB,(C'T',BLOCK),L'SPACES                          
*                                                                               
         LA    RF,NOUWKTAB         DON'T PRINT USE WEEKS FOR THESE USES         
UINFO70A CLI   0(RF),X'FF'         EOT                                          
         BE    UINFO70B                                                         
         CLC   TGUSEQU,0(RF)                                                    
         BE    UINFO80                                                          
         AHI   RF,1                                                             
         B     UINFO70A                                                         
*                                                                               
UINFO70B CLI   TGUSEQU,UDOR         . . . FOR DOR SHOW 26W1                     
         BNE   UINFO71                                                          
         MVC   BLOCK(10),=CL10'26W1'                                            
         B     UINFO73                                                          
UINFO71  TM    TGUSWKS,X'C0'       IF X'80' OR X'40' SET IN THE NO.             
         BZ    UINFO75                 OF WEEKS IN THE CYCLE                    
         MVC   BLOCK(10),SPACES                                                 
         MVC   BYTE,TGUSWKS                                                     
         NI    BYTE,X'3F'          THEN CLEAR THEM OUT                          
         EDIT  (1,BYTE),(2,BLOCK),ALIGN=LEFT   DISP # OF MONTHS                 
         LA    RE,BLOCK                                                         
         AR    RE,R0                                                            
         MVI   0(RE),C'M'                                                       
         TM    TGUSWKS,X'40'       IF X'40' BIT IS SET                          
         BZ    *+8                                                              
         MVI   0(RE),C'D'          THEN DISPLAYING DAYS                         
UINFO73  GOTO1 =A(SENDDOWN),DMCB,(C'T',BLOCK),4                                 
         B     UINFO90                                                          
*                                                                               
UINFO75  SR    RF,RF                                                            
         IC    RF,TGUSWKS                                                       
         LTR   RF,RF                                                            
         BZ    UINFO80                                                          
         ST    RF,WORK                                                          
         GOTO1 =A(SENDDOWN),DMCB,(C'N',WORK),4             USE WKS              
         B     UINFO90                                                          
*                                                                               
UINFO80  GOTO1 =A(SENDDOWN),DMCB,(C'T',=C' '),1            BLANK                
*                                                                               
UINFO90  MVI   WORK,C'N'                                                        
*                                                                               
         USING TACDD,R4                                                         
         L     R4,ASRTCHK                                                       
         MVI   ELCODE,TACDELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   UINFO100                                                         
         TM    TACDSTAT,TACDHLDY                                                
         BZ    UINFO100                                                         
         MVI   WORK,C'Y'                                                        
         DROP  R4                                                               
*                                                                               
UINFO100 CLI   RECNUM,PK                                                        
         BNE   *+8                                                              
         MVI   WORK,C' '                                                        
         GOTO1 =A(SENDDOWN),DMCB,(C'T',WORK),1             HOLIDAY              
*                                                                               
         BRAS  RE,EOLDOWN                                                       
UINFOXIT XIT1                                                                   
*                                                                               
UINFOPM  DS    0H                                                               
         TM    BYTE,NY             PRINT THE MAJOR MARKETS                      
         BZ    UINFOPM3                                                         
         MVC   0(2,RF),=C'NY'                                                   
         LA    RF,2(RF)                                                         
         TM    BYTE,LA+CHI                                                      
         BZ    UINFOPM3                                                         
         MVI   0(RF),C'+'                                                       
         LA    RF,1(RF)                                                         
UINFOPM3 TM    BYTE,LA                                                          
         BZ    UINFOPM5                                                         
         MVC   0(2,RF),=C'LA'                                                   
         LA    RF,2(RF)                                                         
         TM    BYTE,CHI                                                         
         BZ    UINFOPM5                                                         
         MVI   0(RF),C'+'                                                       
         LA    RF,1(RF)                                                         
UINFOPM5 LA    RF,1(RF)                                                         
         TM    BYTE,CHI                                                         
         BZR   RE                                                               
         SHI   RF,1                                                             
         MVC   0(3,RF),=C'CHI'                                                  
         LA    RF,4(RF)                                                         
         BR    RE                                                               
*                                                                               
NOUWKTAB DC    AL1(UDEM)           USES THAT DOESN'T HAVE USE WEEKS             
         DC    AL1(UCDM)                                                        
         DC    AL1(USIN)                                                        
         DC    AL1(URGM)                                                        
         DC    AL1(UDWN)                                                        
         DC    AL1(UAUD)                                                        
         DC    AL1(UPPF)                                                        
         DC    AL1(UISS)                                                        
         DC    AL1(UINA)                                                        
         DC    AL1(UCNL)                                                        
         DC    AL1(UOTH)                                                        
         DC    AL1(UPEN)                                                        
         DC    AL1(UFGS)                                                        
         DC    X'FF'                                                            
*                                                                               
RTKES    DC    C'Entire  '                                                      
RTKPS    DC    C'Part <60'                                                      
RTK60    DC    C'Part >60'                                                      
                                                                                
         LTORG                                                                  
         EJECT                                                                  
*---------------------------------------------------------------------          
*        USE DETAIL                                                             
*---------------------------------------------------------------------          
USEDETL  NTR1  BASE=*,LABEL=*                                                   
         XC    CHKCALC,CHKCALC                                                  
*                                                                               
         CLI   RECNUM,PK           P+ CHECKS                                    
         BE    UDTL700                                                          
*                                                                               
         CLI   RECNUM,CK           IF REGULAR CHECKS                            
         BE    *+12                                                             
         CLI   RECNUM,CN           OR CANADIAN CHECKS                           
         BNE   *+14                                                             
         CLC   SRTUN,=C'NON'       AND NON UNION, JUST PRINT TOTALS             
         BE    UDTL07                                                           
*                                                                               
         L     R4,ASRTCHK          R4 = A(PAYMENT DETAILS)                      
         MVI   ELCODE,TAPDELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   UDTL08                                                           
*                                                                               
         USING TAPDD,R4                                                         
         TM    TAPDSTA2,TAPDSPRI   PER CYCLE PMT ON GUAR PRI COMML?             
         BZ    UDTL08                                                           
*                                                                               
UDTL07   GOTO1 =A(SENDDOWN),DMCB,(C'T',=C'UD'),2           USE DETAIL           
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C'Total'),5        TOTAL                
         B     UDTL900W                                                         
*                                                                               
UDTL08   L     R4,ASRTCHK          R4 = A(BREAKOUT ELEMENT)                     
         TM    SRTBITS,SRTBLIN     LIEN AND TRUST CHECKS                        
         BO    UDTL09              DON'T SHOW BREAKOUT                          
         CLI   SRTKSTA,SRTKSW4T                                                 
         BE    UDTL09                                                           
*                                                                               
         MVI   ELCODE,TABKELQ                                                   
         BRAS  RE,GETEL                                                         
         BE    UDTL10                                                           
*                                                                               
UDTL09   MVC   CHKCALC,SRTPAY      IF BREAKOUT NOT FOUND,                       
         B     UDTL490             JUST SHOW GROSS                              
*                                                                               
         USING TABKD,R4                                                         
UDTL10   ZIC   R5,TABKNSUB         NUMBER OF DETAILS                            
         LTR   R5,R5                                                            
         BZ    UDTL490                                                          
*                                                                               
UDTL100  DS    0H                                                               
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C'UD'),2           USE DETAIL           
         CLI   TABKCODE,0          REGULAR REUSE BREAKOUT                       
         BE    UDTL200                                                          
         LA    RF,BRKTABL                                                       
UDTL130  CLI   0(RF),0             NO MATCHING DESCRIPTION                      
         BE    UDTL400                                                          
         CLC   TABKCODE,0(RF)                                                   
         BE    UDTL150                                                          
         AHI   RF,L'BRKTABL                                                     
         B     UDTL130                                                          
*                                                                               
UDTL150  MVC   BLOCK(25),1(RF)     MOVE DESCRIPTION                             
         CLI   TABKCODE,4          APPLIED                                      
         BNE   UDTL190                                                          
         MVC   BLOCK,SPACES                                                     
         OC    SRTACDE,SRTACDE     APPLIED CODE COMMENTS                        
         BZ    UDTL190                                                          
         MVC   BLOCK(25),=CL25'Unknown Applied Code'                            
         GOTO1 APPLVAL,DMCB,SRTACDE                                             
         BNE   UDTL190                                                          
         MVC   BLOCK(7),=C'Applied'                                             
         MVC   BLOCK+8(12),TGAPTEXT                                             
*                                                                               
         CLC   =C'CABLE',TGAPTEXT                                               
         BNE   UDTL170                                                          
         CLC   SRTUSE,=C'LCB'                                                   
         BE    UDTL180                                                          
         GOTO1 =A(MIXDCASE),DMCB,BLOCK+8,5                                      
         B     UDTL171                                                          
*                                                                               
UDTL170  CLC   =C'WS UPGRADE',TGAPTEXT                                          
         BNE   UDTL180                                                          
         GOTO1 =A(MIXDCASE),DMCB,BLOCK+11,7                                     
*                                                                               
UDTL171  MVC   BYTE,SRTMAJ         SAVE SRTMAJ IN BYTE                          
         MVC   HALF,SRTUNT         SAVE SRTUNT IN HALF                          
         CLI   SRTVERS,0           IF HAVE VERSION CODE                         
         BE    UDTL172                                                          
         MVC   BYTE,SRTVMAJ        USE SRTVMAJ                                  
         MVC   HALF,SRTVUNT        AND SRTVUNT INSTEAD                          
*                                                                               
UDTL172  LA    RF,BLOCK+14                                                      
         CLC   =C'CABLE',TGAPTEXT                                               
         BE    UDTL175                                                          
*                                                                               
         LA    RF,BLOCK+19                                                      
         TM    BYTE,NY             PRINT THE MAJOR MARKETS                      
         BZ    UDTL173                                                          
         MVC   0(2,RF),=C'NY'                                                   
         LA    RF,2(RF)                                                         
         TM    BYTE,LA+CHI                                                      
         BZ    UDTL173                                                          
         MVI   0(RF),C'+'                                                       
         LA    RF,1(RF)                                                         
UDTL173  TM    BYTE,LA                                                          
         BZ    UDTL174                                                          
         MVC   0(2,RF),=C'LA'                                                   
         LA    RF,2(RF)                                                         
         TM    BYTE,CHI                                                         
         BZ    UDTL174                                                          
         MVI   0(RF),C'+'                                                       
         LA    RF,1(RF)                                                         
UDTL174  LA    RF,1(RF)                                                         
         TM    BYTE,CHI                                                         
         BZ    UDTL175                                                          
         SHI   RF,1                                                             
         MVC   0(3,RF),=C'CHI'                                                  
         LA    RF,4(RF)                                                         
UDTL175  EDIT  (2,HALF),(4,(RF)),ALIGN=LEFT,ZERO=NOBLANK                        
         GOTO1 =A(SENDDOWN),DMCB,(C'T',BLOCK),38                                
         B     UDTL400                                                          
*                                                                               
UDTL180  CLI   TGMEEQU,RADIO       FOR RADIO INVOICES                           
         BNE   UDTL190                                                          
         CLI   SRTACDE,APPLHLD     WHOSE APPLIED CODE IS HLD                    
         BNE   UDTL190                                                          
         MVC   BLOCK+8(12),=C'Cycle GRT   '    CHANGE TEXT                      
*                                                                               
UDTL190  CLI   TABKCODE,51                                                      
         BE    *+8                                                              
         CLI   TABKCODE,52                                                      
         BNE   UDTL300                                                          
         LA    RF,BLOCK+11                                                      
         EDIT  TABKUN2,(2,(RF)),0,ZERO=NOBLANK,ALIGN=LEFT                       
         GOTO1 =A(SENDDOWN),DMCB,(C'T',BLOCK),25           DESCRIPTION          
         B     UDTL400                                                          
*                                                                               
UDTL200  CLI   TGUSEQU,UWSP        IF CHECK FOR WILDSPOT USE                    
         BNE   UDTL210                                                          
         OC    TABKUN2,TABKUN2     AND THIS ENTRY IS FOR MAJORS                 
         BNZ   UDTL210                                                          
         MVC   BLOCK(13),=C'Major Markets'                                      
         GOTO1 =A(SENDDOWN),DMCB,(C'T',BLOCK),13                                
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C' '),1                                 
         B     UDTL408                                                          
*                                                                               
UDTL210  CLI   TGUSEQU,UCBL        IF CHECK FOR CABLE                           
         BE    *+12                                                             
         CLI   TGUSEQU,USCB        OR SPANISH CABLE                             
         BNE   UDTL220                                                          
         CLC   TABKUN2,=F'1'       AND USE RANGE IS 1-1                         
         BNE   UDTL230             PRINT MINIMUM                                
         MVC   BLOCK(7),=C'Minimum'                                             
         GOTO1 =A(SENDDOWN),DMCB,(C'T',BLOCK),7                                 
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C' '),1                                 
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C' '),1                                 
         B     UDTL410                                                          
*                                                                               
UDTL220  CLI   TGUSEQU,UCLA        IF CLASS A                                   
         BNE   UDTL230                                                          
         OC    TABKUN2,TABKUN2     WAS DISCOUNTED, DO NOT DISPLAY RNG           
         BNZ   UDTL230                                                          
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C' '),1                                 
         B     UDTL400                                                          
*                                                                               
UDTL230  MVC   BLOCK(L'SPACES),SPACES                                           
         EDIT  TABKUN1,(8,BLOCK),0,ZERO=NOBLANK,ALIGN=LEFT                      
         LA    RF,BLOCK                                                         
         AR    RF,R0               NUMBER OF SIGNIFICANT CHAR                   
         MVI   0(RF),C'-'                                                       
         AHI   RF,1                                                             
*                                                                               
UDTL240  CLI   TGUSEQU,UCBL        IF CHECK FOR CABLE                           
         BE    *+12                                                             
         CLI   TGUSEQU,USCB        OR SPANISH CABLE                             
         BNE   UDTL250                                                          
         CLC   TABKUN2,=F'2000'    AND USE RANGE IS X-200                       
         BNE   UDTL250             PRINT "-MAX"                                 
         MVC   0(3,RF),=C'Max'                                                  
         AHI   RF,3                                                             
         B     UDTL260                                                          
*                                                                               
UDTL250  EDIT  TABKUN2,(8,(RF)),0,ZERO=NOBLANK,ALIGN=LEFT                       
         AR    RF,R0                                                            
UDTL260  LA    RE,BLOCK                                                         
         SR    RF,RE                                                            
         GOTO1 =A(SENDDOWN),DMCB,(C'T',BLOCK),25           RANGE                
         B     UDTL400                                                          
*                                                                               
UDTL300  GOTO1 =A(SENDDOWN),DMCB,(C'L',BLOCK),25           DESCRIPTION          
         ZICM  RF,TABKNOUN,4                                                    
         CLI   TABKCODE,PVCPDW                                                  
         BE    UDTL310                                                          
         CLI   TABKCODE,PVCTRV                                                  
         BNE   UDTL400                                                          
         CLC   SRTPDTE,=X'B00110'                                               
         BL    UDTL310                                                          
         XR    RE,RE                                                            
         D     RE,=F'100'                                                       
UDTL310  STCM  RF,15,TGFULL                                                     
         GOTO1 =A(SENDDOWN),DMCB,(C'A',TGFULL),4           UNIT                 
         B     UDTL408                                                          
*                                                                               
UDTL400  CLI   TABKCODE,7                                  DOUBLES              
         BNE   UDTL401                                                          
         GOTO1 =A(SENDDOWN),DMCB,(C'T',SRTDBL),1                                
         B     UDTL404                                                          
*                                                                               
UDTL401  CLI   TABKCODE,1                                                       
         BE    UDTL403                                                          
         CLI   TABKCODE,2                                                       
         BE    UDTL403                                                          
         CLI   TABKCODE,3                                                       
         BE    UDTL403                                                          
         CLI   TABKCODE,63         FRI/SAT HRS HAS RATES                        
         BE    UDTL405                                                          
         CLI   TABKCODE,57                                                      
         BE    UDTL403H                                                         
         CLI   TABKCODE,60                                                      
         BE    UDTL407                                                          
         CLI   TABKCODE,67                                                      
         BE    UDTL403                                                          
         CLI   TABKCODE,58                                                      
         BL    UDTL402                                                          
         CLI   TABKCODE,62                                                      
         BNH   UDTL403                                                          
UDTL402  CLI   TABKCODE,4                                                       
         BL    UDTL405                                                          
         CLI   TABKCODE,50                                                      
         BH    UDTL405                                                          
UDTL403  GOTO1 =A(SENDDOWN),DMCB,(C'T',=C' '),1            WORK UNIT            
         B     UDTL404                                                          
*                                                                               
UDTL403H GOTO1 =A(SENDDOWN),DMCB,(C'N',TABKNOUN),4         UNIT                 
UDTL404  GOTO1 =A(SENDDOWN),DMCB,(C'T',=C' '),1            RATE                 
         ICM   RF,15,TABKRATE                                                   
         B     UDTL450                                                          
*                                                                               
UDTL405  CLI   TABKCODE,66                                                      
         BNE   UDTL407                                                          
         MVC   BLOCK(8),SPACES                                                  
         EDIT  TABKNOUN,(8,BLOCK),2,ZERO=NOBLANK,ALIGN=LEFT                     
         LA    RF,BLOCK                                                         
         AR    RF,R0               SIGNIFICANT CHAR                             
         AHI   RF,-3                                                            
         MVI   0(RF),C':'          CONVERT IT TO TIME H:MM                      
         GOTO1 =A(SENDDOWN),DMCB,(C'T',BLOCK),8                                 
         B     UDTL408                                                          
*                                                                               
UDTL407  GOTO1 =A(SENDDOWN),DMCB,(C'N',TABKNOUN),4         UNIT                 
UDTL408  GOTO1 =A(SENDDOWN),DMCB,(C'A',TABKRATE),6         RATE                 
*                                                                               
UDTL410  CLI   TABKCODE,66         MUSIC                                        
         BNE   UDTL415                                                          
         ICM   RF,15,TABKUN2       AMOUNT CALCULATED IN PAY                     
         B     UDTL450                                                          
*                                                                               
UDTL415  XR    RE,RE               CALCULATED AMOUNT                            
         ICM   RF,15,TABKRATE      RATE                                         
         ICM   R1,15,TABKNOUN      NUMBER OF UNITS                              
         MR    RE,R1                                                            
         CLI   TABKCODE,PVCPDW                                                  
         BE    UDTL420                                                          
         CLI   TABKCODE,PVCTRV                                                  
         BNE   UDTL450                                                          
         CLC   SRTPDTE,=X'B00110'                                               
         BL    UDTL420                                                          
         D     RE,=F'100'                                                       
         XR    RE,RE                                                            
UDTL420  D     RE,=F'50'                                                        
         LTR   RF,RF                                                            
         BM    *+8                                                              
         AHI   RF,1                                                             
         SRA   RF,1                                                             
*                                                                               
UDTL450  ST    RF,WORK                                                          
         A     RF,CHKCALC                                                       
         ST    RF,CHKCALC                                                       
         GOTO1 =A(SENDDOWN),DMCB,(C'A',WORK),4             AMOUNT               
*                                                                               
         BRAS  RE,EOLDOWN                                                       
*                                                                               
         LA    R4,TABKSLNQ(R4)     BUMP NEXT SUB ELEMENT                        
         BCT   R5,UDTL100          GET THE REST                                 
*                                                                               
UDTL490  OC    SRTGUAR,SRTGUAR                                                  
         BZ    UDTL900                                                          
         OC    SRTACDE,SRTACDE                                                  
         BZ    UDTL900                                                          
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C'UD'),2           USE DETAIL           
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C'Applied Guarantee'),17                
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C' '),1            WORK UNIT            
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C' '),1            WORK RATE            
         L     RF,SRTGUAR                                                       
         A     RF,CHKCALC                                                       
         ST    RF,CHKCALC                                                       
         GOTO1 =A(SENDDOWN),DMCB,(C'A',SRTGUAR),6          GRT                  
         BRAS  RE,EOLDOWN                                                       
         B     UDTL900                                                          
         EJECT                                                                  
*---------------------------------------------------------------------          
*        USE DETAIL FOR P+ CHECKS                                               
*---------------------------------------------------------------------          
UDTL700  XC    UDTLTOT,UDTLTOT                                                  
*                                                                               
         USING TLTMD,R3                                                         
         MVC   AIO,AIO2            GET TIMESHEET RECORD                         
         XC    KEY,KEY                                                          
         LA    R3,KEY                                                           
         MVI   TLTMCD,TLTMCDQ      X'B8'                                        
         MVI   TLTMSTA,TLTMSPPL    PAYROLL PLUS TIMESHEET                       
         MVC   TLTMCOM,SRTCOM                                                   
         MVC   TLTMINV,SRTINV                                                   
         XC    TLTMINV,=X'FFFFFFFFFFFF'                                         
         MVC   TLTMSSN,SRTFSO      USE FSO IF IT EXISTS                         
         OC    SRTFSO,SRTFSO                                                    
         BNZ   *+10                                                             
         MVC   TLTMSSN,SRTSSN                                                   
         MVC   TLTMSORT,SRTCST                                                  
         XC    TLTMSORT(4),TLTMSORT                                             
         GOTO1 HIGH                                                             
         CLC   KEY(TLTMCAT-TLTMD),KEYSAVE                                       
         BNE   UDTL800                                                          
         GOTO1 GETREC                                                           
                                                                                
         USING TATDD,R4                                                         
         L     R4,AIO                                                           
         MVI   ELCODE,TATDELQ      EVENT TIME TASK-PAY TYPE DETAILS             
         BRAS  RE,GETEL                                                         
         B     UDTL720                                                          
UDTL710  BRAS  RE,NEXTEL                                                        
UDTL720  BNE   UDTL800                                                          
*        TM    TATDSTAT,TATDSACO                                                
*        BZ    UDTL710                                                          
                                                                                
         USING UDTLD,R2                                                         
         MVC   UDTLDESC,SPACES                                                  
         LA    R2,UDTLDESC                                                      
         MVC   UDTLTASK,TATDTASK   TASK                                         
         MVC   UDTLPTYP(6),TATDPMTY PAY TYPE                                    
         BAS   RE,PMTYNAME                                                      
         GOTO1 =A(MIXDCASE),DMCB,(R2),L'UDTLDESC                                
         MVC   UDTLUNIT,TATDUNIT    TAX UNIT                                    
         MVI   UDTLUND,C'_'         USE UNDERSCORES TO HOLD SPACES              
         MVI   UDTLUND2,C'_'                                                    
         GOTO1 SQUASHER,DMCB,UDTLDESC,35                                        
         DROP  R2                                                               
                                                                                
         LA    R1,L'UDTLDESC                                                    
         LA    RE,UDTLDESC                                                      
UDTL730  CLI   0(RE),C'_'          IF UNDERSCORE                                
         BNE   UDTL735                                                          
         MVI   0(RE),C' '          CHANGE TO SPACE                              
         SHI   RE,1                GO BACK ONE                                  
         MVI   0(RE),C':'          SEPARATE WITH COLON                          
         B     UDTL740                                                          
UDTL735  LA    RE,1(RE)                                                         
         BCT   R1,UDTL730                                                       
                                                                                
UDTL740  LA    R1,L'UDTLDESC                                                    
         LA    RE,UDTLDESC                                                      
UDTL743  CLI   0(RE),C'_'          IF UNDERSCORE                                
         BNE   UDTL745                                                          
         MVI   0(RE),C' '          CHANGE TO SPACE                              
         SHI   RE,1                GO BACK ONE                                  
         MVI   0(RE),C','          SEPARATE WITH COMMA                          
         B     UDTL750                                                          
UDTL745  LA    RE,1(RE)                                                         
         BCT   R1,UDTL743                                                       
                                                                                
UDTL750  GOTO1 =A(SENDDOWN),DMCB,(C'T',=C'UD'),2             USE DETAIL         
         GOTO1 =A(SENDDOWN),DMCB,(C'T',UDTLDESC),25          DESCRPXN           
         GOTO1 =A(SENDDOWN),DMCB,(C'A',TATDUNTS),4           UNIT               
         GOTO1 =A(SENDDOWN),DMCB,(C'A',TATDRATE),4           RATE               
         GOTO1 =A(SENDDOWN),DMCB,(C'A',TATDAMNT),4           AMOUNT             
         BRAS  RE,EOLDOWN                                                       
                                                                                
         L     RF,UDTLTOT          ACCUM TOTAL AMOUNT                           
         ICM   RE,15,TATDAMNT                                                   
         AR    RF,RE                                                            
         ST    RF,UDTLTOT                                                       
                                                                                
         B     UDTL710                                                          
         DROP  R4                                                               
                                                                                
UDTL800  MVC   AIO,AIO1                                                         
         B     UDTL900             SHOW TOTAL                                   
                                                                                
UDTL900  GOTO1 =A(SENDDOWN),DMCB,(C'T',=C'UD'),2           USE DETAIL           
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C'Total'),5        TOTAL                
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C' '),1            WORK UNIT            
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C' '),1            WORK RATE            
         CLI   RECNUM,PK                                   P+ CHECKS            
         BE    UDTL900Y                                                         
         TM    TGUSSTA2,NORATES                                                 
         BO    UDTL900X                                                         
         GOTO1 =A(SENDDOWN),DMCB,(C'A',CHKCALC),6          CALC GROSS           
         BRAS  RE,EOLDOWN                                                       
         CLC   SRTPAY,CHKCALC                              OVERRIDE             
         BE    UDTLX                                                            
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C'UD'),2                                
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C'Manual Override'),15                  
UDTL900W GOTO1 =A(SENDDOWN),DMCB,(C'T',=C' '),1            WORK UNIT            
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C' '),1            WORK RATE            
UDTL900X GOTO1 =A(SENDDOWN),DMCB,(C'A',SRTPAY),6           GROSS                
         BRAS  RE,EOLDOWN                                                       
         B     UDTLX                                                            
*&&DO                                                                           
UDTL900Y L     RF,SRTPAY                                                        
         A     RF,SRTREXP                                                       
         ST    RF,WORK                                                          
         GOTO1 =A(SENDDOWN),DMCB,(C'A',WORK),6             GROSS                
         BRAS  RE,EOLDOWN                                                       
*&&                                                                             
                                                                                
UDTL900Y GOTO1 =A(SENDDOWN),DMCB,(C'A',UDTLTOT),6          GROSS                
         BRAS  RE,EOLDOWN                                                       
                                                                                
UDTLX    XIT1                                                                   
*                                                                               
UDTLDESC DS    CL35                                                             
UDTLTOT  DS    F                                                                
         EJECT                                                                  
                                                                                
         USING UDTLD,R2                                                         
PMTYNAME NTR1                                                                   
         MVC   AIO,AIO3                                                         
         MVC   BYTE,ELCODE                                                      
                                                                                
         GOTO1 RECVAL,DMCB,TLPMCDQ,(X'A0',UDTLPTYP),('TLPMSCDQ',0)              
         BNE   PMTYNXIT                                                         
                                                                                
         USING TANAD,R4                                                         
         L     R4,AIO                                                           
         MVI   ELCODE,TANAELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   PMTYNXIT                                                         
         MVC   WORK,SPACES                                                      
         ZIC   RF,TANALEN                                                       
         AHI   RF,-3                                                            
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   WORK(0),TANANAME                                                 
         MVC   UDTLPTYP,WORK                                                    
         OC    UDTLPTYP,SPACES                                                  
         DROP  R2,R4                                                            
                                                                                
PMTYNXIT MVC   AIO,AIO2                                                         
         MVC   ELCODE,BYTE                                                      
         B     UDTLX                                                            
         EJECT                                                                  
         LTORG                                                                  
       ++INCLUDE TAPAYBRKT                                                      
         EJECT                                                                  
*---------------------------------------------------------------------          
*        EARNINGS RECORD                                                        
*---------------------------------------------------------------------          
ERNRECD  NTR1  BASE=*,LABEL=*                                                   
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C'ER'),2           EARNINGS             
*                                                                               
         L     RF,SRTPAY                                                        
         ST    RF,BLOCK                                                         
         MVC   BLOCK+4(4),=F'0'                                                 
*                                                                               
         CLI   RECNUM,CN           IF CHECK IS NOT CAN$                         
         BE    ERNREC3                                                          
         CLI   RECNUM,EC           AND NOT EUROS                                
         BE    ERNREC3                                                          
         CLI   SRTW4TY,TAW4TYCO    AND PERFORMER NOT PAID AS CORP               
         BE    ERNREC3                                                          
         CLI   SRTW4TY,TAW4TYCA    AND NOT PAID AS CANADIAN                     
         BE    ERNREC3                                                          
         CLI   SRTW4TY,TAW4TYTR    AND NOT PAID AS TRUSTEE                      
         BE    ERNREC3                                                          
         CLI   SRTW4TY,TAW4TYFO    AND NOT PAID AS FOREIGNER                    
         BNE   ERNREC5                                                          
*                                                                               
ERNREC3  MVC   BLOCK(4),=F'0'                                                   
         ST    RF,BLOCK+4                                                       
*                                                                               
ERNREC5  GOTO1 =A(SENDDOWN),DMCB,(C'A',BLOCK),6            PAY                  
         L     RF,SRTYEARN                                 PAY YTD              
         A     RF,SRTYDUE                                + DUE COMP             
         A     RF,SRTYDUET                                                      
         A     RF,SRTYDUEN                                                      
         ST    RF,WORK                                                          
         GOTO1 =A(SENDDOWN),DMCB,(C'Y',WORK),6             PAY YTD              
*                                                                               
         GOTO1 =A(SENDDOWN),DMCB,(C'A',BLOCK+4),6          MISC PAY             
         L     RF,SRTYNTAX                                                      
         S     RF,SRTYREX                                                       
         ST    RF,WORK                                                          
         GOTO1 =A(SENDDOWN),DMCB,(C'Y',WORK),6             MISC PAY YTD         
*                                                                               
         GOTO1 =A(SENDDOWN),DMCB,(C'A',SRTREXP),6          REIM EXP             
         GOTO1 =A(SENDDOWN),DMCB,(C'Y',SRTYREX),6          REIM EXP YTD         
*                                                                               
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C' '),1            H&W                  
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C' '),1            H&W YTD              
*                                                                               
         L     RF,SRTGST                                                        
         LCR   RF,RF                                                            
         A     RF,SRTPST                                                        
         ST    RF,WORK                                                          
         GOTO1 =A(SENDDOWN),DMCB,(C'A',WORK),6             HST                  
         L     RF,SRTYGST                                                       
         LCR   RF,RF                                                            
         A     RF,SRTYPST                                                       
         ST    RF,WORK                                                          
         GOTO1 =A(SENDDOWN),DMCB,(C'Y',WORK),6             HST YTD              
*                                                                               
         XC    BLOCK(4),BLOCK                                                   
         GOTO1 =A(SENDDOWN),DMCB,(C'A',BLOCK),4            PST                  
         GOTO1 =A(SENDDOWN),DMCB,(C'A',BLOCK),4            PST YTD              
*                                                                               
         L     RF,SRTDUE                                                        
         A     RF,SRTDUETR                                                      
         A     RF,SRTDUENR                                                      
         LCR   RF,RF                                                            
         ST    RF,WORK                                                          
         GOTO1 =A(SENDDOWN),DMCB,(C'A',WORK),6      DUE COMP                    
         L     RF,SRTYDUE                                                       
         A     RF,SRTYDUET                                                      
         A     RF,SRTYDUEN                                                      
         LCR   RF,RF                                                            
         ST    RF,WORK                                                          
         GOTO1 =A(SENDDOWN),DMCB,(C'Y',WORK),6      DUE CMP YTD                 
*                                                                               
         L     RF,SRTPAY                                                        
         A     RF,SRTREXP          ALREADY AFTER DUE COMPANY                    
         S     RF,SRTGSTPD                                                      
         A     RF,SRTPST                                                        
         S     RF,SRTDUE                                                        
         ST    RF,BLOCK                                                         
*                                                                               
         L     RF,SRTYEARN                                                      
         A     RF,SRTYNTAX                                                      
         S     RF,SRTYGST                                                       
         A     RF,SRTYPST                                                       
*        S     RF,SRTYDUE                                                       
         ST    RF,BLOCK+4                                                       
*                                                                               
         GOTO1 =A(SENDDOWN),DMCB,(C'A',BLOCK),6            GROSS                
         GOTO1 =A(SENDDOWN),DMCB,(C'Y',BLOCK+4),6          GROSS YTD            
*                                                                               
         BRAS  RE,EOLDOWN                                                       
         XIT1                                                                   
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*---------------------------------------------------------------------          
*        COMMENT LINES                                                          
*---------------------------------------------------------------------          
CMTLINE  NTR1  BASE=*,LABEL=*                                                   
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C'CC'),2           COMMENTS             
         MVC   NUMLINES,=F'8'                                                   
*                                                                               
         CLI   RECNUM,PC           IF PRINT CHECKS                              
         BE    CMT100                                                           
         CLI   RECNUM,EC                                                        
         BE    CMT100                                                           
*                                                                               
         MVC   AIO,ASRTCHK                                                      
         GOTO1 CHAROUT,DMCB,TANUELQ,0,TANUTMEM  GET MEMBERSHIP NUMBER           
         MVC   AIO,AIO1                                                         
         BNE   CMT005                                                           
         MVC   BLOCK(19),=C'Membership Number: '                                
         MVC   BLOCK+20(L'TGNAME),TGNAME                                        
         GOTO1 =A(SENDDOWN),DMCB,(C'T',BLOCK),19+L'TGNAME                       
         L     RF,NUMLINES                                                      
         BCTR  RF,0                                                             
         ST    RF,NUMLINES                                                      
*                                                                               
CMT005   LA    R4,23               MAX 23                                       
         LA    R2,SRTCLA           USE DATES                                    
         OC    0(3,R2),0(R2)       IF NO DATES                                  
         BZ    CMT100              LEAVE                                        
CMT010   LA    R5,12                                                            
         LA    R3,BLOCK                                                         
         MVC   0(11,R3),=C'Use Dates: '                                         
         LA    R3,11(R3)                                                        
*                                                                               
CMT020   OC    0(3,R2),0(R2)       IF DATE IS ZERO                              
         BZ    CMT050                                                           
         GOTO1 DATCON,DMCB,(1,0(R2)),(12,0(R3))                                 
         LA    R3,5(R3)                                                         
         LA    R2,4(R2)                                                         
         OC    0(3,R2),0(R2)                                                    
         BZ    CMT030                                                           
         MVI   0(R3),C','                                                       
         LA    R3,1(R3)                                                         
CMT030   BCTR  R4,0                                                             
         BCT   R5,CMT020                                                        
*                                                                               
CMT050   LR    RF,R3                                                            
         LA    RE,BLOCK                                                         
         SR    RF,RE                                                            
         GOTO1 =A(SENDDOWN),DMCB,(C'L',BLOCK),(RF)                              
         L     RF,NUMLINES                                                      
         BCTR  RF,0                                                             
         ST    RF,NUMLINES                                                      
         OC    0(3,R2),0(R2)                                                    
         BNZ   CMT010                                                           
*                                                                               
CMT100   DS    0H                                                               
*                                                                               
CMT220   L     R4,ASRTCHK          R4 = A(FIRST DUECOMP WITHHLD ELEM)           
         MVI   ELCODE,TADWELQ                                                   
         BRAS  RE,GETEL            SKIP IF NONE FOUND                           
         BNE   CMT250                                                           
         USING TADWD,R4                                                         
*                                                                               
CMT230   ICM   RF,15,TADWREC       IF AMOUNT RECOVERED IS NOT NEGATIVE          
         BNP   CMT240                                                           
*                                                                               
         MVC   AIO,AIO1            DON'T CLOBBER ASRTCHK                        
         BAS   RE,DUECOMM          THEN PRINT DUECOMP COMMENT                   
         OC    NUMLINES,NUMLINES                                                
         BZ    CMT990                                                           
*                                                                               
CMT240   MVI   ELCODE,TADWELQ      GET NEXT DW ELEMENT                          
         BRAS  RE,NEXTEL                                                        
         BE    CMT230              REPEAT UNTIL NO MORE DW ELEMENTS             
*                                                                               
CMT250   L     R4,ASRTCHK          R4 = A(LIEN WITHHLD ELEM)                    
         MVI   ELCODE,TALWELQ                                                   
         BRAS  RE,GETEL            SKIP IF NONE FOUND                           
         BNE   CMT255                                                           
*                                                                               
         MVC   AIO,AIO1            DON'T CLOBBER ASRTCHK                        
         BAS   RE,LINCOMM          PRINT LIEN COMMENT                           
         OC    NUMLINES,NUMLINES                                                
         BZ    CMT990                                                           
*                                                                               
CMT255   MVC   AIO,ASRTCHK         SEARCH FOR W4 TRUSTEE DEDUCTION              
         MVI   ELCODE,TAODELQ                                                   
         GOTO1 GETL,DMCB,(1,=AL1(TAODTYPT))                                     
         MVC   AIO,AIO1                                                         
         BNE   CMT500              SKIP IF NONE FOUND                           
         L     R4,TGELEM                                                        
         BRAS  RE,TRSTCOMM         PRINT W4 TRUSTEE COMMENT                     
         OC    NUMLINES,NUMLINES                                                
         BZ    CMT990                                                           
*                                                                               
CMT500   MVC   AIO,ASRTCHK         SEARCH CHECK REC FOR CHECK COMMENT           
         MVI   ELCODE,TACMELQ                                                   
         XR    R2,R2               CLEAR L'CHECK COMMENT                        
         GOTO1 GETL,DMCB,(1,=AL1(TACMTYPC))                                     
         MVC   AIO,AIO1                                                         
         BNE   CMT530              SKIP IF NONE FOUND                           
*                                                                               
         L     R4,TGELEM           R4 = A(CHECK COMMENT ELEMENT)                
         USING TACMD,R4                                                         
         IC    R2,TACMLEN          R2 = LENGTH OF CHECK COMMENT                 
         SH    R2,=AL2(TACMLNQ)                                                 
         BZ    CMT530                                                           
         LA    R3,TACMCOMM         R3 = A(CHECK COMMENT)                        
*                                                                               
         CLI   0(R3),C'X'          IF CHECK COMMENT BEGINS WITH 'X'             
         BNE   CMT530                                                           
         BCTR  R2,0                SET TO PRINT ONE LESS CHARACTER              
         LA    R3,1(R3)            BUMP PAST 'X'                                
         B     CMT550              AND SKIP INVOICE COMMENT                     
*                                                                               
CMT530   OC    SRTINVC,SRTINVC     IF INVOICE COMMENTS EXIST                    
         BZ    CMT550              THEN PRINT THEM                              
         GOTO1 =A(SENDDOWN),DMCB,(C'L',SRTINVC),60                              
         L     RF,NUMLINES                                                      
         AHI   RF,-1                                                            
         ST    RF,NUMLINES                                                      
         OC    NUMLINES,NUMLINES                                                
         BZ    CMT990                                                           
*                                                                               
CMT550   LTR   R2,R2               IF WE FOUND CHECK COMMENT                    
         BZ    CMT600                                                           
         GOTO1 =A(SENDDOWN),DMCB,(C'L',(R3)),(R2)                               
         L     RF,NUMLINES                                                      
         AHI   RF,-1                                                            
         ST    RF,NUMLINES                                                      
         OC    NUMLINES,NUMLINES                                                
         BZ    CMT990                                                           
*                                                                               
CMT600   L     R4,ASRTCHK          R4 = A(SOAP EPISODES ELEMENT)                
         MVI   ELCODE,TASOELQ                                                   
         BRAS  RE,GETEL            SKIP IF NONE FOUND                           
         BNE   CMT700                                                           
*                                                                               
         BRAS  RE,EPICOMM          PRINT EPISODES COMMENT                       
         OC    NUMLINES,NUMLINES                                                
         BZ    CMT990                                                           
*                                                                               
CMT700   MVC   AIO,ASRTCHK         SET AIO=A(FREE FORM NUMBER ELEMENT)          
         MVI   ELCODE,TANUELQ                                                   
         GOTO1 GETL,DMCB,(1,=AL1(TANUTCST))                                     
         MVC   AIO,AIO1            RESET AIO                                    
         BNE   CMT750                                                           
*                                                                               
         BAS   RE,AGTSSN           PRINT NAMES OF CONTRIBUTING MODELS           
         OC    NUMLINES,NUMLINES                                                
         BZ    CMT990                                                           
*                                                                               
CMT750   L     R4,ASRTCHK          R4=A(CHECK RECORD)                           
         MVI   ELCODE,TACAELQ      LOOK FOR CAST DETAILS ELEMENT                
         BRAS  RE,GETEL                                                         
         BNE   CMT880                                                           
         USING TACAD,R4                                                         
         OC    TACALFTF,TACALFTF                                                
         BZ    CMT880                                                           
         DROP  R4                                                               
*                                                                               
         BAS   RE,LIFTCOMM                                                      
         OC    NUMLINES,NUMLINES                                                
         BZ    CMT990                                                           
*                                                                               
CMT880   L     R4,NUMLINES              REMAINING LINES = BLANKS                
CMT885   GOTO1 =A(SENDDOWN),DMCB,(C'T',=C' '),1                                 
         BCT   R4,CMT885                                                        
*                                                                               
CMT990   BRAS  RE,EOLDOWN                                                       
CMTXIT   XIT1                                                                   
*                                                                               
DECNLNS  DS    0H                                                               
         L     RF,NUMLINES         DECREMENT NUM OF LINES                       
         AHI   RF,-1                                                            
         ST    RF,NUMLINES                                                      
         BNZR  RE                  GOT SOME LINES LEFT                          
         B     CMTXIT              OTHERWISE QUIT                               
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*----------------------------------------------------------------------         
* THIS ROUTINE BUILDS AND PRINTS THE COMMENT FOR THE DUE COMPANY                
* WITHHOLDING ELEMENT POINTED BY R4.                                            
*----------------------------------------------------------------------         
DUECOMM  NTR1                                                                   
         USING TADWD,R4                                                         
         MVI   BLOCK,C' '          PRE-SET OUTPUT BLOCK TO SPACES               
         MVC   BLOCK+1(179),BLOCK                                               
*                                  MOVE AMOUNT RECOVERED TO COMMENT             
         EDIT  (4,TADWREC),(12,BLOCK),2,ALIGN=LEFT,ZERO=NOBLANK                 
*                                                                               
         OC    SRTDUETR,SRTDUETR   ANY TAXABLE REIMBURSEMENTS USED?             
         JNZ   DUE100                                                           
         OC    SRTDUENR,SRTDUENR   ANY NON TAX REIM USED?                       
         JNZ   DUE100                                                           
*                                  MOVE NARRATIVE TO COMMENT                    
         MVC   BLOCK+14(35),DEDCOMM                                             
         MVC   BLOCK+49(29),=C'BECAUSE OF AN OVERPAYMENT FOR'                   
*                                                                               
*                                  MOVE REFERENCE NUMBER TO COMMENT             
         MVC   BLOCK+120(8),=C'REF='                                            
         MVC   BLOCK+124(6),TADWDUC                                             
*                                                                               
         CLI   TADWDUC,X'FA'       IS IT A YEAR 2000 DATE?                      
         BL    DUEC2               NO                                           
*                                                                               
         MVC   WORK(4),TADWDUC     YES, CONVERT IT                              
         MVC   WORK+4(2),=C'01'                                                 
         GOTO1 DATCON,DMCB,(0,WORK),(X'20',WORK)                                
         MVC   BLOCK+124(4),WORK                                                
         MVC   BLOCK+128(2),TADWDUC+4                                           
*                                                                               
DUEC2    LA    RF,BLOCK+130                                                     
         BAS   RE,BACKRF                                                        
         MVI   1(RF),C'.'                                                       
*                                  MOVE REMAINING BALANCE TO COMMENT            
         MVC   BLOCK+140(8),=C'REM BAL='                                        
         LA    R2,BLOCK+148                                                     
         EDIT  (4,TADWBAL),(11,(R2)),2,ALIGN=LEFT,ZERO=NOBLANK                  
         LA    RF,BLOCK+159                                                     
         BAS   RE,BACKRF                                                        
         MVI   1(RF),C'.'                                                       
*                                                                               
         XC    COMMNAME,COMMNAME   CLEAR FAKE FIELD HEADER FOR NAME             
         MVI   COMMNAME,36+8                                                    
*                                                                               
         LA    R3,KEY              BUILD DUE COMPANY KEY                        
         USING TLDUD,R3                                                         
         XC    KEY,KEY                                                          
         MVI   TLDUCD,TLDUCDQ                                                   
         MVC   TLDUSSN,SRTSSN                                                   
         MVC   TLDUDUC,TADWDUC                                                  
*                                                                               
         GOTO1 HIGH                READ DUE COMPANY KEY                         
         CLC   TLDUKEY,KEYSAVE                                                  
         BNE   DC10                SKIP IF NOT FOUND                            
*                                                                               
         GOTO1 GETREC              GET RECORD                                   
*                                                                               
         L     R4,AIO              R4 = A(DUE COMPANY DETAILS ELEMENT)          
         MVI   ELCODE,TADUELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   DC10                                                             
         USING TADUD,R4                                                         
*                                                                               
         OC    TADUCINV,TADUCINV   MOVE CREDIT INV NUM INTO COMMENT             
         BZ    DC5                                                              
         MVC   BLOCK+161(7),=C'CR INV='                                         
         GOTO1 TINVCON,DMCB,TADUCINV,BLOCK+168,DATCON                           
         MVI   BLOCK+174,C'.'                                                   
*                                  READ AGENCY RECORD AND EXTRACT NAME          
DC5      GOTO1 RECVAL,DMCB,TLAYCDQ,(X'88',TADUAGY),COMMNAME                     
*                                                                               
*                                  MOVE NAME INTO COMMENT                       
DC10     MVC   BLOCK+79(36),COMMNAME+8                                          
         LA    RF,BLOCK+115                                                     
         BAS   RE,BACKRF                                                        
         MVI   1(RF),C'.'                                                       
*                                  SQUASH AND CHOP INTO 80 BYTE BLOCKS          
         GOTO1 SQUASHER,DMCB,BLOCK,180                                          
         GOTO1 CHOPPER,DMCB,(180,BLOCK),(90,AIO2),2                             
         L     R3,8(R1)                                                         
*        M     R2,=F'80'                                                        
*                                  PRINT COMMENT                                
         L     R4,AIO2                                                          
DC25     GOTO1 =A(SENDDOWN),DMCB,(C'L',(R4)),90                                 
         BAS   RE,DECNLNS                                                       
         LA    R4,90(R4)                                                        
         BCT   R3,DC25                                                          
         B     DCX                                                              
*                                                                               
         USING TADWD,R4                                                         
DUE100   MVC   SVREF,TADWDUC                                                    
         MVC   SVBAL,TADWBAL                                                    
         MVC   SVSTAT,TADWSTAT                                                  
*                                                                               
         XC    COMMNAME,COMMNAME   CLEAR FAKE FIELD HEADER FOR NAME             
         MVI   COMMNAME,36+8                                                    
*                                                                               
         LA    R3,KEY              BUILD DUE COMPANY KEY                        
         USING TLDUD,R3                                                         
         XC    KEY,KEY                                                          
         MVI   TLDUCD,TLDUCDQ                                                   
         MVC   TLDUSSN,SRTSSN                                                   
         MVC   TLDUDUC,SVREF                                                    
*                                                                               
         GOTO1 HIGH                READ DUE COMPANY KEY                         
         CLC   TLDUKEY,KEYSAVE                                                  
         BNE   DUE140              SKIP IF NOT FOUND                            
*                                                                               
         GOTO1 GETREC              GET RECORD                                   
*                                                                               
         L     R4,AIO              R4 = A(DUE COMPANY DETAILS ELEMENT)          
         MVI   ELCODE,TADUELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   DUE140                                                           
         USING TADUD,R4                                                         
*                                                                               
         MVC   SVCINV,TADUCINV                                                  
*                                  READ AGENCY RECORD AND EXTRACT NAME          
DUE130   GOTO1 RECVAL,DMCB,TLAYCDQ,(X'88',TADUAGY),COMMNAME                     
*                                                                               
*                                  MOVE NAME INTO COMMENT                       
*                                                                               
DUE140   TM    SVSTAT,TADWSTR+TADWSNR                                           
         JNZ   DUE150                                                           
         MVC   BLOCK+14(37),DEDCOMMT                                            
         MVC   BLOCK+51(29),=C'BECAUSE OF AN OVERPAYMENT FOR'                   
         MVC   BLOCK+81(36),COMMNAME+8                                          
         LA    RF,BLOCK+117                                                     
         BAS   RE,BACKRF                                                        
         MVI   1(RF),C'.'                                                       
         MVC   BLOCK+120(8),=C'REF='                                            
         MVC   BLOCK+124(6),SVREF                                               
         LA    RF,BLOCK+130                                                     
         BAS   RE,BACKRF                                                        
*                                  MOVE REMAINING BALANCE TO COMMENT            
         MVC   BLOCK+140(8),=C'REM BAL='                                        
         LA    R2,BLOCK+148                                                     
         EDIT  (4,SVBAL),(11,(R2)),2,ALIGN=LEFT,ZERO=NOBLANK                    
         LA    RF,BLOCK+159                                                     
         BAS   RE,BACKRF                                                        
         MVI   1(RF),C'.'                                                       
*                                                                               
         OC    SVCINV,SVCINV       MOVE CREDIT INV NUM INTO COMMENT             
         BZ    DUE150                                                           
         MVC   BLOCK+161(7),=C'CR INV='                                         
         GOTO1 TINVCON,DMCB,TADUCINV,BLOCK+168,DATCON                           
         MVI   BLOCK+174,C'.'                                                   
         B     DUE170                                                           
*                                                                               
DUE150   TM    SVSTAT,TADWSTR      TAXABLE REIMBURSEMENTS?                      
         JZ    DUE160                                                           
         MVC   BLOCK+14(46),DEDCOMMR                                            
         MVC   BLOCK+60(29),=C'BECAUSE OF AN OVERPAYMENT FOR'                   
         MVC   BLOCK+90(36),COMMNAME+8                                          
         LA    RF,BLOCK+126                                                     
         BAS   RE,BACKRF                                                        
         MVI   1(RF),C'.'                                                       
         MVC   BLOCK+127(8),=C'REF='                                            
         MVC   BLOCK+131(6),SVREF                                               
         LA    RF,BLOCK+137                                                     
         BAS   RE,BACKRF                                                        
*                                  MOVE REMAINING BALANCE TO COMMENT            
         MVC   BLOCK+140(8),=C'REM BAL='                                        
         LA    R2,BLOCK+148                                                     
         EDIT  (4,SVBAL),(11,(R2)),2,ALIGN=LEFT,ZERO=NOBLANK                    
         LA    RF,BLOCK+159                                                     
         BAS   RE,BACKRF                                                        
         MVI   1(RF),C'.'                                                       
*                                                                               
         OC    SVCINV,SVCINV       MOVE CREDIT INV NUM INTO COMMENT             
         BZ    DUE170                                                           
         MVC   BLOCK+161(7),=C'CR INV='                                         
         GOTO1 TINVCON,DMCB,TADUCINV,BLOCK+168,DATCON                           
         MVI   BLOCK+174,C'.'                                                   
         B     DUE170                                                           
*                                                                               
DUE160   TM    SVSTAT,TADWSNR      NON TAXABLE REIMBURSEMENTS?                  
         JZ    DUE170                                                           
         MVC   BLOCK+14(50),DEDCOMMN    NON-TAXABLE REIMBURSEMENTS              
         MVC   BLOCK+64(29),=C'BECAUSE OF AN OVERPAYMENT FOR'                   
         MVC   BLOCK+94(36),COMMNAME+8                                          
         LA    RF,BLOCK+130                                                     
         BAS   RE,BACKRF                                                        
         MVI   1(RF),C'.'                                                       
         MVC   BLOCK+131(8),=C'REF='                                            
         MVC   BLOCK+135(6),SVREF                                               
         LA    RF,BLOCK+141                                                     
         BAS   RE,BACKRF                                                        
*                                  MOVE REMAINING BALANCE TO COMMENT            
         MVC   BLOCK+144(8),=C'REM BAL='                                        
         LA    R2,BLOCK+152                                                     
         EDIT  (4,SVBAL),(11,(R2)),2,ALIGN=LEFT,ZERO=NOBLANK                    
         LA    RF,BLOCK+163                                                     
         BAS   RE,BACKRF                                                        
         MVI   1(RF),C'.'                                                       
*                                                                               
         OC    SVCINV,SVCINV       MOVE CREDIT INV NUM INTO COMMENT             
         BZ    DUE170                                                           
         MVC   BLOCK+165(7),=C'CR INV='                                         
         GOTO1 TINVCON,DMCB,TADUCINV,BLOCK+172,DATCON                           
         MVI   BLOCK+175,C'.'                                                   
*                                                                               
*                                  SQUASH AND CHOP INTO 80 BYTE BLOCKS          
DUE170   GOTO1 SQUASHER,DMCB,BLOCK,180                                          
         GOTO1 CHOPPER,DMCB,(180,BLOCK),(90,AIO2),2                             
         L     R3,8(R1)                                                         
*        M     R2,=F'80'                                                        
*                                  PRINT COMMENT                                
         L     R4,AIO2                                                          
DUE180   GOTO1 =A(SENDDOWN),DMCB,(C'L',(R4)),90                                 
         BAS   RE,DECNLNS                                                       
         LA    R4,90(R4)                                                        
         BCT   R3,DUE180                                                        
*                                                                               
DCX      B     CMTXIT                                                           
*                                                                               
DEDCOMM  DC    CL35'WAS DEDUCTED FROM YOUR GROSS WAGES'                         
DEDCOMMT DC    CL37'WAS DEDUCTED FROM YOUR TAXABLE WAGES'                       
DEDCOMMR DC    CL46'WAS DEDUCTED FROM YOUR TAXABLE REIMBURSEMENTS'              
DEDCOMMN DC    CL50'WAS DEDUCTED FROM YOUR NON-TAXABLE REIMBURSEMENTS'          
*                                                                               
SVREF    DS    CL(L'TADWDUC)                                                    
SVBAL    DS    CL(L'TADWBAL)                                                    
SVSTAT   DS    CL(L'TADWSTAT)                                                   
SVCINV   DS    CL(L'TADUCINV)                                                   
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
* THIS ROUTINE BUILDS AND PRINTS THE COMMENT FOR LIFTED FROM ON CAST            
*                                                                               
LIFTCOMM NTR1                                                                   
         USING TACAD,R4                                                         
         MVC   AIO,AIO2                                                         
         MVC   FULL,SRTCOM       SAVE INTERNAL COMM ID                          
         GOTO1 RECVAL,DMCB,TLCOCCDQ,(X'A4',TACALFTF)                            
         BNE   CMTXIT                                                           
         DROP  R4                                                               
         L     R4,AIO2                                                          
         MVI   ELCODE,TACOELQ      GET COMM DETAILS ELEMENT                     
         BRAS  RE,GETEL                                                         
         USING TACOD,R4                                                         
         XC    BLOCK(25),BLOCK     PRE-CLEAR COMMENT AREA                       
         MVC   BLOCK(13),=CL13'LIFTED FROM: '                                   
         MVC   BLOCK+13(L'TACOCID),TACOCID                                      
         DROP  R4                                                               
         GOTO1 RECVAL,DMCB,TLCOCCDQ,(X'A4',FULL) RESTORE INT. CID               
         BE    *+6                                                              
         DC    H'00'                                                            
         MVC   AIO,AIO1                                                         
*                                  PRINT COMMENT                                
         GOTO1 =A(SENDDOWN),DMCB,(C'L',BLOCK),25                                
         BAS   RE,DECNLNS                                                       
         B     CMTXIT                                                           
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
* THIS ROUTINE BUILDS AND PRINTS THE COMMENT FOR THE LIEN WITHHOLDING           
* ELEMENT POINTED BY R4.                                                        
*                                                                               
LINCOMM  NTR1                                                                   
         USING TALWD,R4                                                         
         MVI   BLOCK,C' '          PRE-SET OUTPUT BLOCK TO SPACES               
         MVC   BLOCK+1(194),BLOCK                                               
         XC    COMMNAME,COMMNAME   CLEAR FAKE FIELD HEADER FOR NAME             
         MVI   COMMNAME,36+8                                                    
*                                                                               
         ICM   RF,15,TALWREC       AMOUNT RECOVERED                             
         BNP   LC20                THIS IS THE LIEN PAYEE CHECK                 
*                                                                               
*                                  MOVE AMOUNT RECOVERED TO COMMENT             
         EDIT  (4,TALWREC),(12,BLOCK),2,ALIGN=LEFT,ZERO=NOBLANK                 
*                                                                               
*                                  MOVE NARRATIVE TO COMMENT                    
         MVC   BLOCK+14(35),DEDCOMM                                             
         MVC   BLOCK+49(22),=C'BECAUSE OF A LIEN FROM'                          
*                                                                               
*                                  MOVE REFERENCE NUMBER TO COMMENT             
         MVC   BLOCK+120(8),=C'REF='                                            
         MVC   BLOCK+124(6),TALWLIN                                             
         LA    RF,BLOCK+130                                                     
         BAS   RE,BACKRF                                                        
         MVI   1(RF),C'.'                                                       
*                                  MOVE REMAINING BALANCE TO COMMENT            
         MVC   BLOCK+140(8),=C'REM BAL='                                        
         LA    R2,BLOCK+148                                                     
         EDIT  (4,TALWBAL),(11,(R2)),2,ALIGN=LEFT,ZERO=NOBLANK                  
         LA    RF,BLOCK+159                                                     
         BAS   RE,BACKRF                                                        
         MVI   1(RF),C'.'                                                       
*                                                                               
         MVC   TGSSN,SRTSSN        READ LIEN RECORD                             
         GOTO1 RECVAL,DMCB,TLLNCDQ,(X'A0',TALWLIN)                              
         BNE   LC10                                                             
*                                                                               
         L     R4,AIO              R4 = A(LIEN DETAILS ELEMENT)                 
         MVI   ELCODE,TALNELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   LC10                                                             
         USING TALND,R4                                                         
         CLC   TALNTYPE,=AL2(TALNTYGT) IF GROSS TRUSTEE                         
         BNE   LC5                                                              
         MVC   BLOCK+140(20),SPACES   CLEAR REMAINING BALANCE                   
         MVC   BLOCK+49(22),=C'AND PAID TO TRUSTEE - '                          
*                                                                               
*                                  READ LIEN PAYEE W4 REC AND EXTR NAME         
LC5      GOTO1 RECVAL,DMCB,TLW4CDQ,(X'88',TALNPAYE),COMMNAME                    
*                                  MOVE NAME INTO COMMENT                       
LC10     MVC   BLOCK+72(36),COMMNAME+8                                          
         LA    RF,BLOCK+108                                                     
         BAS   RE,BACKRF                                                        
         MVI   1(RF),C'.'                                                       
         B     LC30                                                             
*                                                                               
*                                  MOVE NARRATIVE TO COMMENT                    
LC20     MVC   BLOCK(20),=C'LIEN AMOUNT PAID FOR'                               
         MVC   BLOCK+158(20),=C'LIEN ORDER ATTACHED.'                           
*                                                                               
         USING TALWD,R4                                                         
         LR    R0,R4               SAVE A(LIEN WITHHOLDING ELEMENT)             
         MVC   TGSSN,SRTLSSN       READ LIEN RECORD                             
         GOTO1 RECVAL,DMCB,TLLNCDQ,(X'A0',TALWLIN)                              
         BNE   LC25                                                             
*                                                                               
         L     R4,AIO              R4 = A(LIEN DETAILS ELEMENT)                 
         MVI   ELCODE,TALNELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   LC22                                                             
         USING TALND,R4                                                         
         CLC   TALNTYPE,=AL2(TALNTYGT) IF GROSS TRUSTEE                         
         BNE   LC22                                                             
         MVC   BLOCK+158(20),SPACES  CLEAR LIEN ORDER ATTACHED WORDING          
         MVC   BLOCK(20),=C'AMOUNT IN TRUST FOR '                               
*                                                                               
         USING TACMD,R4                                                         
LC22     MVI   ELCODE,TACMELQ                                                   
         GOTO1 GETL,DMCB,(1,=AL1(TACMTYPL))                                     
         BNE   LC25                                                             
         L     R4,TGELEM                                                        
         ZIC   RE,TACMLEN                                                       
         SHI   RE,TACMLNQ+1                                                     
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   BLOCK+180(0),TACMCOMM                                            
         DROP  R4                                                               
*                                  MOVE REFERENCE NUMBER TO COMMENT             
         USING TALWD,R4                                                         
LC25     LR    R4,R0               RESTORE A(LIEN WITHHOLDING ELEMENT)          
         MVC   BLOCK+120(8),=C'REF='                                            
         MVC   BLOCK+124(6),TALWLIN                                             
         LA    RF,BLOCK+130                                                     
         BAS   RE,BACKRF                                                        
         MVI   1(RF),C'.'                                                       
*                                                                               
*                                  MOVE SSN OF PAYER TO COMMENT                 
         L     R4,ASRTCHK                                                       
         USING TLCKD,R4                                                         
         MVC   BLOCK+100(3),=C'SS#'                                             
         MVC   BLOCK+104(9),SRTLSSN                                             
         LA    RF,BLOCK+115                                                     
         BAS   RE,BACKRF                                                        
         MVI   1(RF),C'.'                                                       
*                                                                               
*                                  READ LIEN PAYER W4 REC AND EXTR NAME         
         GOTO1 RECVAL,DMCB,TLW4CDQ,(X'88',SRTLSSN),COMMNAME                     
*                                                                               
*                                  MOVE NAME INTO COMMENT                       
         MVC   BLOCK+50(36),COMMNAME+8                                          
         LA    RF,BLOCK+86                                                      
         BAS   RE,BACKRF                                                        
         MVI   1(RF),C'.'                                                       
*                                                                               
*                                  SQUASH AND CHOP INTO 80 BYTE BLOCKS          
LC30     GOTO1 SQUASHER,DMCB,BLOCK,195                                          
         GOTO1 CHOPPER,DMCB,(195,BLOCK),(90,AIO2),3                             
         L     R3,8(R1)                                                         
*        M     R2,=F'80'                                                        
*                                  PRINT COMMENT                                
         L     R4,AIO2                                                          
LC45     GOTO1 =A(SENDDOWN),DMCB,(C'L',(R4)),90                                 
         BAS   RE,DECNLNS                                                       
         LA    R4,90(R4)                                                        
         BCT   R3,LC45                                                          
*                                                                               
LCX      B     CMTXIT                                                           
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
* THIS ROUTINE PRINTS THE NAMES OF MODELS WHOSE SSN IS IN TANU ELEMS.           
* THE FIRST ELEMENT IS AT TGELEM.  IT USES AIO2 FOR OUTPUT BLOCK                
*                                                                               
         USING TANUD,R4                                                         
AGTSSN   NTR1                                                                   
         L     R4,TGELEM           R4=A(TANU ELEMENT IN RECORD)                 
         L     RE,AIO2                                                          
         LA    RF,2000             PRE-CLEAR OUTPUT BLOCK                       
         XCEFL                                                                  
         L     R5,AIO2             R5 = A(OUTPUT BLOCK)                         
*                                                                               
         MVC   0(7,R5),=C'MODELS:' START COMMENT                                
         LA    R5,8(R5)                                                         
*                                                                               
AGTS5    GOTO1 RECVAL,DMCB,TLW4CDQ,(X'A0',TANUMBER)                             
         BNE   AGTS15                                                           
         GOTO1 CHAROUT,DMCB,TANAELQ,0                                           
         BNE   AGTS15                                                           
         LA    R0,L'TGNAME                                                      
         GOTO1 SQUASHER,DMCB,TGNAME,(R0)                                        
         L     R0,4(R1)               R0=LENGTH OF STRING W/O BLANKS            
         MVC   0(L'TGNAME,R5),TGNAME  SAVE NAME IN OUTPUT BLOCK                 
         AR    R5,R0                  BUMP R5 PAST END OF STRING                
         MVI   0(R5),C','             PUT COMMA BETWEEN NAMES                   
         LA    R5,2(R5)               SKIP A SPACE                              
*                                                                               
AGTS15   MVI   ELCODE,TANUELQ                                                   
         BRAS  RE,NEXTEL           GET NEXT TANU ELEMENT                        
         BNE   AGTS20                                                           
         CLI   TANUTYPE,TANUTCST   TEST CORRECT TYPE                            
         BE    AGTS5                                                            
*                                                                               
AGTS20   SH    R5,=H'2'                                                         
         MVI   0(R5),0                                                          
*                                  SQUASH AND CHOP INTO 80 BYTE BLOCKS          
         GOTO1 SQUASHER,DMCB,AIO2,2000                                          
         GOTO1 CHOPPER,DMCB,AIO2,(80,AIO1),25,C'LEN=',2000                      
         L     R3,8(R1)                                                         
*                                  PRINT COMMENT                                
AGTS30   L     R4,AIO1                                                          
AGTS35   GOTO1 =A(SENDDOWN),DMCB,(C'L',(R4)),80                                 
         BAS   RE,DECNLNS                                                       
         LA    R4,80(R4)                                                        
         BCT   R3,AGTS35                                                        
*                                                                               
         B     CMTXIT                                                           
         EJECT                                                                  
BACKRF   CLI   0(RF),C' '          BACK RF TO FIRST NON-SPACE                   
         BHR   RE                                                               
         BCT   RF,BACKRF                                                        
*                                                                               
YES2     SR    RC,RC                                                            
NO2      LTR   RC,RC                                                            
XIT2     XIT1                                                                   
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*---------------------------------------------------------------------          
* THIS ROUTINE BUILDS AND PRINTS THE COMMENT FOR W4 TRUSTEE DEDUCTION           
*---------------------------------------------------------------------          
         USING TAODD,R4                                                         
TRSTCOMM NTR1  BASE=*,LABEL=*                                                   
         MVI   BLOCK,C' '          PRE-SET OUTPUT BLOCK TO SPACES               
         MVC   BLOCK+1(179),BLOCK                                               
         XC    COMMNAME,COMMNAME   CLEAR FAKE FIELD HEADER FOR NAME             
         MVI   COMMNAME,36+8                                                    
*                                                                               
         ICM   RF,15,TAODAMT       AMOUNT RECOVERED                             
         LR    R5,RF                                                            
         BNP   TC20                THIS IS THE TRUSTEE CHECK                    
*                                                                               
         EDIT  (4,TAODAMT),(12,BLOCK),2,ALIGN=LEFT,ZERO=NOBLANK                 
         MVC   BLOCK+14(35),=CL35'WAS DEDUCTED FROM YOUR GROSS WAGES'           
*                                                                               
         TM    SRTTRBTS,SRTTCATR    CA TRUST                                    
         BZ    TC02                                                             
         MVC   BLOCK+49(25),=C'PER CALIFORNIA COOGAN LAW'                       
         B     TC09                                                             
*                                                                               
TC02     TM    SRTTRBTS,SRTTNYTR    NY TRUST                                    
         BZ    TC03                                                             
         MVC   BLOCK+49(25),=C'PER NY CHILD PERFORMER ED'                       
         MVC   BLOCK+74(29),=C'UCATION AND TRUST ACT OF 2003'                   
         CLC   SRTCAT,=C'PM '                                                   
         BNE   TC09                                                             
         MVC   BLOCK+103(23),=C' AND AMENDED AS OF 2013'                        
         B     TC09                                                             
                                                                                
TC03     TM    SRTTRBTS,SRTTLATR    LOUISIANA TRUST                             
         BZ    TC05                                                             
         MVC   BLOCK+49(25),=C'PER LOUISIANA CHILD PERFO'                       
         MVC   BLOCK+74(22),=C'RMER TRUST ACT OF 2005'                          
         B     TC09                                                             
*                                                                               
TC05     TM    SRTTRBTS,SRTTNMTR    NEW MEXICO TRUST                            
         BZ    TC09                                                             
         MVC   BLOCK+49(37),=C'PER NEW MEXICO CODE SECTION 11.1.4.13'           
*                                                                               
TC09     MVC   AIO,ASRTCHK                                                      
         MVI   ELCODE,TANUELQ                                                   
         GOTO1 GETL,DMCB,(1,=AL1(TANUTRST))                                     
         MVC   AIO,AIO1                                                         
         BNE   TC10                                                             
         XC    BLOCK+74(29),BLOCK+74                                            
         XC    BLOCK+103(23),BLOCK+103                                          
         MVC   BLOCK+49(25),=C'AND PAID TO TRUSTEE -    '                       
         L     R4,TGELEM                                                        
         USING TANUD,R4                                                         
         GOTO1 RECVAL,DMCB,TLW4CDQ,(X'88',TANUMBER),COMMNAME                    
         MVC   BLOCK+72(36),COMMNAME+8                                          
TC10     LA    RF,BLOCK+126                                                     
         BAS   RE,BACKRF2                                                       
         MVI   1(RF),C'.'                                                       
         B     TC30                                                             
*                                                                               
         USING TAODD,R4                                                         
TC20     TM    SRTADJS,TAPDADTR    IF NOT REFUND CHECK TO TRUSTEE               
         BZ    TC25                                                             
         MVC   AIO,ASRTCHK                                                      
         MVI   ELCODE,TANUELQ                                                   
         GOTO1 GETL,DMCB,(1,=AL1(TANUTRST))                                     
         MVC   AIO,AIO1                                                         
         BNE   TCX                                                              
         OC    SRTNET,SRTNET                                                    
         BZ    TCX                 XIT - MINOR CHECK                            
*                                  FOR TRUSTEE CHECK                            
TC25     MVC   BLOCK(20),=C'AMOUNT IN TRUST FOR '                               
         L     R4,ASRTCHK          MOVE MINOR PAYER SSN TO COMMENT              
         USING TLCKD,R4                                                         
         MVC   BLOCK+50(3),=C'SS#'                                              
         LA    RF,BLOCK+54                                                      
         CLC   SRTUN,=C'ACT'       ACTRA UNION                                  
         BNE   *+10                                                             
         MVC   BLOCK+50(4),=C'SIN#'                                             
         LA    RF,1(RF)                                                         
*                                                                               
         MVC   0(9,RF),SRTTSSN                                                  
         LA    RF,9(RF)                                                         
         BAS   RE,BACKRF2                                                       
         MVI   1(RF),C'.'                                                       
*                                  READ W4 REC AND EXTR NAME                    
         GOTO1 RECVAL,DMCB,TLW4CDQ,(X'88',SRTTSSN),COMMNAME                     
         MVC   BLOCK+70(36),COMMNAME+8                                          
         LA    RF,BLOCK+106                                                     
         BAS   RE,BACKRF2                                                       
         MVI   1(RF),C'.'                                                       
*                                  SQUASH AND CHOP INTO 80 BYTE BLOCKS          
TC30     GOTO1 SQUASHER,DMCB,BLOCK,180                                          
         GOTO1 CHOPPER,DMCB,(180,BLOCK),(90,AIO2),2                             
         L     R3,8(R1)                                                         
TC33     L     R4,AIO2                                                          
TC35     GOTO1 =A(SENDDOWN),DMCB,(C'L',(R4)),90                                 
         BAS   RE,DECNLNS2                                                      
         LA    R4,90(R4)                                                        
         BCT   R3,TC35                                                          
*                                                                               
         LTR   R5,R5               AMOUNT RECOVERED                             
         BP    TCX                 THIS IS THE TRUSTEE CHECK                    
*                                                                               
         L     R4,AIO                                                           
         MVI   ELCODE,TAA2ELQ      ADDRESS ELEMENT                              
         BRAS  RE,GETEL                                                         
         BNE   TCX                                                              
         LR    R3,R4                                                            
         USING TAA2D,R3                                                         
         LA    R2,TAA2ADD1                                                      
*                                                                               
         LHI   R4,3                   SEND DOWN 3 LINES OF ADDRESS              
         CLI   TAA2LEN,TAA2LNQ        FOR OLD STYLE ADDRESSES                   
         BL    TC40                                                             
         CLC   TAA2CTRY,=C'US'        AND NEW STYLE US ADDRESSES                
         BE    TC40                                                             
         LHI   R4,2                   ELSE SEND DOWN 2                          
TC40     CLC   0(30,R2),SPACES                                                  
         BNH   TC45                                                             
         MVC   ELEM,SPACES                                                      
         MVC   ELEM+20(30),0(R2)                                                
         GOTO1 =A(SENDDOWN),DMCB,(C'L',ELEM),52                                 
         BAS   RE,DECNLNS2                                                      
TC45     LA    R2,30(R2)                                                        
         BCT   R4,TC40                                                          
*                                                                               
         MVC   WORK,SPACES                                                      
         MVC   WORK(25),TAA2CITY      CITY                                      
         GOTO1 =A(MIXDCASE),DMCB,WORK,25                                        
         MVC   WORK+26(2),TAA2ST      STATE                                     
         MVC   WORK+29(10),TAA2ZIP    ZIP                                       
         GOTO1 SQUASHER,DMCB,WORK,39  SQUASH IT                                 
         MVC   ELEM,SPACES                                                      
         MVC   ELEM+20(39),WORK                                                 
         GOTO1 =A(SENDDOWN),DMCB,(C'T',ELEM),61                                 
         BAS   RE,DECNLNS2                                                      
                                                                                
         CLI   TAA2LEN,TAA2LNQ        FOR OLD STYLE ADDRESSES                   
         BL    TC45A                                                            
         CLC   TAA2CTRY,=C'US'        AND NEW STYLE US ADDRESSES                
         BE    TC45A                                                            
         GOTO1 VALCTRY,DMCB,(X'80',TAA2CTRY)                                    
         BNE   TC45A                                                            
         MVC   ELEM,SPACES                                                      
         USING CTRYTABD,R1                                                      
         L     R1,TGACTRY                                                       
         ZIC   RE,CTRYDSP                                                       
         LTR   RE,RE                                                            
         BNZ   *+8                                                              
         IC    RE,CTRYLEN                                                       
         SHI   RE,CTRYDESC-CTRYTABD+1                                           
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   ELEM(0),CTRYDESC                                                 
         GOTO1 =A(SENDDOWN),DMCB,(C'T',ELEM),61                                 
         BAS   RE,DECNLNS2                                                      
         DROP  R1                                                               
*                                                                               
TC45A    MVI   BLOCK,C' '          PRE-SET OUTPUT BLOCK TO SPACES               
         MVC   BLOCK+1(179),BLOCK                                               
         TM    SRTTRBTS,SRTTCATR   CALIFORNIA TRUSTEE?                          
         BZ    TC50                                                             
         MVC   BLOCK(41),=C'THESE FUNDS SUBJECT TO CALIFORNIA FAMILY '          
         MVC   BLOCK+41(18),=C'CODE SECTION 6753.'                              
         GOTO1 =A(SENDDOWN),DMCB,(C'L',BLOCK),59                                
         BAS   RE,DECNLNS2                                                      
*                                                                               
TC50     TM    SRTTRBTS,SRTTNYTR   NY TRUSTEE?                                  
         BZ    TC60                                                             
         MVC   BLOCK(32),=C'THESE FUNDS SUBJECT TO NEW YORK '                   
         MVC   BLOCK+32(36),=C'CHILD PERFORMER EDUCATION AND TRUST '            
         MVC   BLOCK+68(12),=C'ACT OF 2003.'                                    
         GOTO1 =A(SENDDOWN),DMCB,(C'L',BLOCK),80                                
         BAS   RE,DECNLNS2                                                      
*                                                                               
TC60     TM    SRTTRBTS,SRTTLATR   LOUISIANA TRUSTEE?                           
         BZ    TC70                                                             
         MVC   BLOCK(33),=C'THESE FUNDS SUBJECT TO LOUISIANA '                  
         MVC   BLOCK+33(34),=C'CHILD PERFORMER TRUST ACT OF 2005.'              
         GOTO1 =A(SENDDOWN),DMCB,(C'L',BLOCK),67                                
         BAS   RE,DECNLNS2                                                      
*                                                                               
TC70     TM    SRTTRBTS,SRTTNMTR   NEW MEXICO TRUSTEE?                          
         BZ    TCX                                                              
         MVC   BLOCK(34),=C'THESE FUNDS SUBJECT TO NEW MEXICO '                 
         MVC   BLOCK+34(22),=C'CODE SECTION 11.1.4.13'                          
         GOTO1 =A(SENDDOWN),DMCB,(C'L',BLOCK),56                                
         BAS   RE,DECNLNS2                                                      
*                                                                               
TCX      XIT1                                                                   
*                                                                               
BACKRF2  CLI   0(RF),C' '          BACK RF TO FIRST NON-SPACE                   
         BHR   RE                                                               
         BCT   RF,BACKRF2                                                       
         BR    RE                                                               
*                                                                               
DECNLNS2 L     RF,NUMLINES         DECREMENT NUM OF LINES                       
         AHI   RF,-1                                                            
         ST    RF,NUMLINES                                                      
         BNZR  RE                  GOT SOME LINES LEFT                          
         B     TCX                 OTHERWISE QUIT                               
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*---------------------------------------------------------------------          
* THIS ROUTINE BUILDS AND PRINTS THE COMMENT FOR THE SOAP EPISODE               
* ELEMENT POINTED BY R4.                                                        
*---------------------------------------------------------------------          
EPICOMM  NTR1  BASE=*,LABEL=*                                                   
         MVI   BLOCK,C' '          PRE-SET OUTPUT BLOCK TO SPACES               
         MVC   BLOCK+1(239),BLOCK                                               
         MVC   BLOCK+240(240),BLOCK+239                                         
         LA    R5,BLOCK            R5 = OUTPUT BLOCK                            
*                                                                               
         MVC   0(9,R5),=C'EPISODES:'  START COMMENT                             
         LA    R5,10(R5)                                                        
*                                                                               
         USING TASOD,R4                                                         
EC05     ZIC   R0,TASONUM          R0 = N'EPISODE SUB-ELEMENTS                  
         LTR   R0,R0                                                            
         BZ    EC90                                                             
*                                                                               
         LA    R2,TASOSEPI         R2 = A(EPISODE SUB-ELEMENT)                  
         USING TASOSEPI,R2                                                      
*                                                                               
EC10     LH    R1,TASOEPI          CONVERT EPISODE TO CHARACTER                 
         CVD   R1,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  WORK(5),DUB+5(3)                                                 
*                                                                               
         L     R3,AEPITAB          R3 = A(EPISODE TABLE)                        
         USING EPITABD,R3                                                       
         LA    R1,MAXEPIS          R1 = MAX NUMBER OF EPISODE ENTRIES           
*                                                                               
EC20     CLI   0(R3),0             TEST END OF TABLE                            
         BE    EC30                                                             
         CLC   EPIAGY,SRTAGY       MATCH ON AGENCY                              
         BNE   *+14                                                             
         CLC   EPINUM,WORK         AND ON EPISODE NUMBER                        
         BE    EC40                                                             
         LA    R3,EPINEXT          TRY NEXT TABLE ENTRY                         
         BCT   R1,EC20                                                          
*                                                                               
         L     R3,AEPITAB          END OF TABLE - SET TO REUSE BEG.             
         XC    0(10*EPILEN,R3),0(R3)                                            
*                                                                               
EC30     MVC   EPIAGY,SRTAGY       NOT IN TABLE - ADD NEW ENTRY                 
         MVC   EPINUM,WORK                                                      
*                                                                               
         BAS   RE,GETEPI           GET EPISODE RECORD AND FILL IN TABLE         
*                                                                               
EC40     MVC   0(5,R5),EPINUM      PRINT EPISODE NUMBER                         
         LA    R5,5(R5)                                                         
*                                                                               
         TM    TASOSTAT,TASOSH+TASOSB+TASOSS  IF WRITER ROLES DEFINED           
         BZ    EC45                                                             
         MVI   0(R5),C':'                     PRINT THEM                        
         LA    R5,1(R5)                                                         
         TM    TASOSTAT,TASOSH     TEST HEAD WRITER                             
         BZ    *+14                                                             
         MVC   0(5,R5),=C'HEAD+'                                                
         LA    R5,5(R5)                                                         
         TM    TASOSTAT,TASOSB     TEST BREAKDOWN WRITER                        
         BZ    *+14                                                             
         MVC   0(6,R5),=C'BKDWN+'                                               
         LA    R5,6(R5)                                                         
         TM    TASOSTAT,TASOSS     TEST SCRIPT WRITER                           
         BZ    *+14                                                             
         MVC   0(6,R5),=C'SCRPT+'                                               
         LA    R5,6(R5)                                                         
         BCTR  R5,0                ERASE TRAILING '+'                           
         MVI   0(R5),C' '                                                       
*                                                                               
EC45     MVC   TGTHREE,EPIADT      SET TO PRINT AIR DATE                        
         TM    SRTCOSTA,TACOSWDT   IF THIS COMML REQUIRES WORK DATE             
         BZ    *+10                                                             
         MVC   TGTHREE,EPIWDT      SET TO PRINT WORK DATE                       
*                                                                               
         OC    TGTHREE,TGTHREE     IF DATE DEFINED                              
         BZ    EC50                                                             
         MVI   0(R5),C'-'          PRINT IT                                     
         GOTO1 DATCON,DMCB,(1,TGTHREE),(10,1(R5))                               
         MVC   6(3,R5),SPACES      (DON'T PRINT YEAR)                           
         LA    R5,6(R5)                                                         
*                                                                               
EC50     LA    R2,L'TASOSEPI(R2)   BUMP TO NEXT EPISODE SUB-ELEMENT             
         MVI   0(R5),C','          SEPARATE PRINTED ENTRIES WITH COMMA          
         LA    R5,1(R5)                                                         
         BCT   R0,EC10             LOOP                                         
*                                                                               
EC90     MVI   ELCODE,TASOELQ      LOOK FOR ANOTHER SOAP ELEMENT                
         BRAS  RE,NEXTEL                                                        
         BE    EC05                GO PROCESS                                   
*                                                                               
         BCTR  R5,0                                                             
         MVI   0(R5),C' '                                                       
*                                  SQUASH AND CHOP INTO 80 BYTE BLOCKS          
         GOTO1 SQUASHER,DMCB,BLOCK,480                                          
         GOTO1 CHOPPER,DMCB,BLOCK,(80,AIO),6,C'LEN=',480                        
         L     R3,8(R1)                                                         
*                                  PRINT COMMENT                                
EC93     L     R4,AIO                                                           
EC95     GOTO1 =A(SENDDOWN),DMCB,(C'L',(R4)),80                                 
         L     RF,NUMLINES                                                      
         AHI   RF,-1                                                            
         ST    RF,NUMLINES                                                      
         LTR   RF,RF                                                            
         BZ    ECX                                                              
         LA    R4,80(R4)                                                        
         BCT   R3,EC95                                                          
*                                                                               
ECX      XIT1                                                                   
         EJECT                                                                  
*              ROUTINE TO GET EPISODE RECORD AND FILL IN TABLE                  
         SPACE 1                                                                
         USING EPITABD,R3          R3 = A(TABLE ENTRY)                          
GETEPI   NTR1                                                                   
         MVC   TGAGY,SRTAGY        SET TO READ RECORD                           
         GOTO1 RECVAL,DMCB,TLEPCDQ,(X'A0',EPINUM)                               
         BNE   EPIX                                                             
*                                                                               
         L     R4,AIO              R4 = A(EPISODE INFORMATION ELEMENT)          
         MVI   ELCODE,TAEIELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   EPIX                                                             
         USING TAEID,R4                                                         
         MVC   EPIWDT,TAEIWDT      SAVE WORK DATE IN EPISODE TABLE              
         MVC   EPIADT,TAEIADT      AND AIR DATE                                 
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'E',AEPITAB),A(MAXEPIS*EPILEN),=C'EPITAB'         
*                                                                               
EPIX     B     ECX                                                              
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*---------------------------------------------------------------------          
*        GET YTD FOR EXTRA ITEMS                                                
*---------------------------------------------------------------------          
*&&DO                                                                           
         USING TACXD,R4                                                         
GETCX    NTR1  BASE=*,LABEL=*                                                   
         XC    ELEMENT,ELEMENT     COPY TACX INTO ELEMENT                       
         MVI   ELCODE,TACXELQ      CHECK EXTRA DETAILS                          
*                                                                               
         MVC   AIO,ASRTCHK                                                      
         L     R4,ASRTCHK                                                       
         CLI   RERUN,C'Y'          IF RERUN, LOAD WHAT WAS CALCULATED           
         BNE   GETCX100                                                         
         BRAS  RE,GETEL                                                         
         BNE   GETCX400            NO, JUST ADD WHAT WE GOT                     
         CLI   TACXLEN,TACXLNQ                                                  
         BNL   GETCX500            NOT FROM OLD GST PD, JUST GRAB YTD           
*                                                                               
         SR    RF,RF               COPY TACX INTO ELEMENT                       
         IC    RF,TACXLEN                                                       
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   ELEMENT(0),TACXEL                                                
*                                                                               
GETCX100 GOTO1 REMELEM             REMOVE ANY TACX                              
*                                                                               
         CLI   RERUN,C'Y'                                                       
         BE    GETCX400                                                         
*                                                                               
GETCX110 MVC   AIO,AIO1            GET LAST CHECK FOR EMPLOYEE FOR YEAR         
         XC    KEY,KEY                                                          
         LA    R4,KEY                                                           
         USING TLCKPD,R4                                                        
         MVI   TLCKPCD,TLCKECDQ       EMPLOYEE CHECKS                           
         MVC   TLCKESSN,SRTSSN        SS#                                       
         MVI   TLCKECUR,C'U'          DEFAULT CURRENCY                          
         CLI   RECNUM,CN                                                        
         BNE   *+8                                                              
         MVI   TLCKECUR,C'C'                                                    
         CLI   RECNUM,EC                                                        
         BNE   *+8                                                              
         MVI   TLCKECUR,C'E'                                                    
         MVC   TLCKEEMP,TGEMP         EMPLOYER                                  
         MVC   TLCKEDTE,SRTCDTE       CHECK DATE                                
         XC    TLCKEDTE,=X'FFFFFF'    COMPLEMENT IT                             
         MVC   FILENAME,=CL8'CHKDIR'                                            
         GOTO1 HIGH                                                             
         B     GETCX150                                                         
*                                                                               
GETCX130 LA    R4,KEY                                                           
         USING TLCKPD,R4                                                        
         ICM   RF,15,TLCKESEQ      BUMP TO GET NEXT CHECK                       
         AHI   RF,1                                                             
         STCM  RF,15,TLCKESEQ                                                   
         MVC   FILENAME,=CL8'CHKDIR'                                            
         GOTO1 SEQ                                                              
*                                                                               
GETCX150 MVC   FILENAME,=CL8'CHKFIL'                                            
         CLC   KEY(TLCKEDTE-TLCKPD),KEYSAVE    SAME SS#, CUR AND EMP            
         BNE   GETCX400                                                         
         CLC   TLCKEDTE(1),KEYSAVE+(TLCKEDTE-TLCKPD)      SAME YEAR             
         BNE   GETCX400                                                         
         GOTO1 GETREC                                                           
*                                                                               
         USING TACXD,R4                                                         
GETCX200 L     R4,AIO              GET THE TACX ELEMENT, NONE? ADD ONE          
         MVI   ELCODE,TACXELQ      CHECK EXTRA DETAILS                          
         BRAS  RE,GETEL                                                         
         BE    GETCX300            YES, COULD BE FROM GST PD                    
         B     GETCX130            NO, GET PREVIOUS CHECK                       
*                                                                               
GETCX300 SR    RF,RF               COPY TACX INTO ELEMENT                       
         IC    RF,TACXLEN                                                       
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   ELEMENT(0),TACXEL                                                
*                                                                               
GETCX400 LA    R4,ELEMENT          UPDATE AMTS FOR TACX ELEMENT                 
         MVI   TACXEL,TACXELQ                                                   
         MVI   TACXLEN,TACXLNQ2                                                 
*                                                                               
         L     RF,SRTGST                                                        
         ST    RF,TACXGST          GST                                          
         L     RF,TACXGSTY         GST                                          
         A     RF,TACXGST                                                       
         ST    RF,TACXGSTY                                                      
         MVC   TACXPST,SRTPST      PST                                          
         L     RF,TACXPSTY         PST                                          
         A     RF,SRTPSTPD                                                      
         ST    RF,TACXPSTY                                                      
         L     RF,TACXMDDY         MISC DEDUCTIONS                              
         A     RF,SRTMDED                                                       
         ST    RF,TACXMDDY                                                      
         L     RF,TACXMTRY         MPTR FUND                                    
         A     RF,SRTMPT                                                        
         ST    RF,TACXMTRY                                                      
         L     RF,TACXDTPY         DUE COMP                                     
         A     RF,SRTDUE                                                        
         A     RF,SRTDUETR                                                      
         A     RF,SRTDUENR                                                      
         ST    RF,TACXDTPY                                                      
         L     RF,TACXPRCY         PERM CHAR                                    
         A     RF,SRTCHAR                                                       
         ST    RF,TACXPRCY                                                      
         L     RF,TACXLIEY         LIENS                                        
         A     RF,SRTLIEN                                                       
         ST    RF,TACXLIEY                                                      
         L     RF,TACXUNDY         UNION DUES                                   
         A     RF,SRTDUES                                                       
         ST    RF,TACXUNDY                                                      
         L     RF,TACXTRSY         TRUSTS                                       
         A     RF,SRTTRST                                                       
         ST    RF,TACXTRSY                                                      
         L     RF,TACXDRTY         DIRECT                                       
         A     RF,SRTDIRCT                                                      
         ST    RF,TACXDRTY                                                      
         L     RF,TACXWRDY         WIRED                                        
         A     RF,SRTWIRE                                                       
         ST    RF,TACXWRDY                                                      
         L     RF,TACXDTPT         DUE COMP TAX REIM                            
         A     RF,SRTDUETR                                                      
         ST    RF,TACXDTPT                                                      
         L     RF,TACXDTPN         DUE COMP NON TAX REIM                        
         A     RF,SRTDUENR                                                      
         ST    RF,TACXDTPN                                                      
*                                                                               
GETCX450 MVC   AIO,ASRTCHK                                                      
         GOTO1 ADDELEM             ADD ELEMENT TO SRTCHK                        
         MVC   AIO,AIO1                                                         
*                                                                               
GETCX500 MVC   SRTYGST,TACXGSTY    LOAD SRTYTD AMOUNTS FOR THIS ELEM            
         MVC   SRTYMDED,TACXMDDY                                                
         MVC   SRTYMPT,TACXMTRY                                                 
         MVC   SRTYDUE,TACXDTPY                                                 
         MVC   SRTYCHAR,TACXPRCY                                                
         MVC   SRTYLIEN,TACXLIEY                                                
         MVC   SRTYDUES,TACXUNDY                                                
         MVC   SRTYTRST,TACXTRSY                                                
         XC    FILENAME,FILENAME                                                
*                                                                               
         XIT1                                                                   
         LTORG                                                                  
*&&                                                                             
         EJECT                                                                  
*---------------------------------------------------------------------          
*        TAX AND DEDUCTIONS                                                     
*---------------------------------------------------------------------          
         USING TACXD,R4                                                         
TAXDEDS  NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         BAS   RE,TAXD1                                                         
         BRAS  RE,TAXD2                                                         
         BRAS  RE,TAXD4                                                         
         BAS   RE,TAXD3                                                         
*                                                                               
TXDDXIT  XIT1                                                                   
*---------------------------------------------------------------------          
TAXD1    NTR1                                                                   
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C'T1'),2           TAX 1                
*                                                                               
         GOTO1 =A(SENDDOWN),DMCB,(C'A',SRTFTAX),L'SRTFTAX   FED TAX             
         GOTO1 =A(SENDDOWN),DMCB,(C'Y',SRTYFED),L'SRTYFED   FED TAX YTD         
         GOTO1 =A(SENDDOWN),DMCB,(C'A',SRTFICA),L'SRTFICA   FICA TAX            
         GOTO1 =A(SENDDOWN),DMCB,(C'Y',SRTYFIC),L'SRTYFIC   FICA TX YTD         
*        GOTO1 =A(SENDDOWN),DMCB,(C'A',SRTMEDC),L'SRTMEDC   MED TX              
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C' '),1                                 
*        GOTO1 =A(SENDDOWN),DMCB,(C'A',SRTYMEDC),L'SRTYMEDC MED TX YTD          
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C' '),1                                 
*                                                                               
         BRAS  RE,EOLDOWN                                                       
         B     TXDDXIT                                                          
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*---------------------------------------------------------------------          
TAXD3    NTR1                                                                   
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C'T3'),2           TAX 3                
*                                                                               
         GOTO1 (RF),DMCB,(C'A',SRTMDED),L'SRTMDED   MIS DED                     
         GOTO1 (RF),DMCB,(C'Y',SRTYMDED),L'SRTYMDED MIS DED YTD                 
         GOTO1 (RF),DMCB,(C'T',=C' '),1             N/A                         
         GOTO1 (RF),DMCB,(C'T',=C' '),1             N/A                         
         GOTO1 (RF),DMCB,(C'T',=C' '),1             N/A                         
         GOTO1 (RF),DMCB,(C'T',=C' '),1             N/A                         
         GOTO1 (RF),DMCB,(C'T',=C' '),1             N/A                         
         GOTO1 (RF),DMCB,(C'T',=C' '),1             N/A                         
         GOTO1 (RF),DMCB,(C'A',SRTMPT),L'SRTMPT     MPTRP                       
         GOTO1 (RF),DMCB,(C'Y',SRTYMPT),L'SRTYMPT   MPTRP YTD                   
         GOTO1 (RF),DMCB,(C'A',SRTCHAR),L'SRTCHAR   PERM CHAR                   
         GOTO1 (RF),DMCB,(C'Y',SRTYCHAR),L'SRTYCHAR   YTD                       
         GOTO1 (RF),DMCB,(C'A',SRTLIEN),L'SRTLIEN   LIEN                        
         GOTO1 (RF),DMCB,(C'Y',SRTYLIEN),L'SRTYLIEN   YTD                       
                                                                                
         CLI   RECNUM,PK                                                        
         BE    TAXD3100                                                         
         GOTO1 (RF),DMCB,(C'A',SRTDUES),L'SRTDUES   UNION DUES                  
         GOTO1 (RF),DMCB,(C'Y',SRTYDUES),L'SRTYDUES   YTD                       
         B     TAXD3500                                                         
                                                                                
TAXD3100 L     R2,SRTPPLA                                                       
         LCR   R2,R2                                                            
         ST    R2,WORK                                                          
         GOTO1 (RF),DMCB,(C'A',WORK),4              P+ AGY COMM                 
         L     R2,SRTYPPLA                                                      
         LCR   R2,R2                                                            
         ST    R2,WORK                                                          
         GOTO1 (RF),DMCB,(C'Y',WORK),4                YTD                       
                                                                                
TAXD3500 GOTO1 (RF),DMCB,(C'A',SRTTRST),L'SRTTRST   TRUST                       
         GOTO1 (RF),DMCB,(C'A',SRTYTRST),L'SRTYTRST   YTD                       
*---------------                                                                
         GOTO1 (RF),DMCB,(C'A',SRTDIRCT),L'SRTDIRCT DIRECT                      
         GOTO1 (RF),DMCB,(C'A',SRTYDIRT),L'SRTYDIRT   YTD                       
         GOTO1 (RF),DMCB,(C'A',SRTWIRE),L'SRTWIRE   WIRE                        
         GOTO1 (RF),DMCB,(C'A',SRTYWIRE),L'SRTYWIRE   YTD                       
*=================================================================              
         L     R1,SRTTDED                                  DEDUCTIONS           
         A     R1,SRTLIEN                                  +LIEN                
         S     R1,SRTPPLA                                  -P+ AGY COMM         
         A     R1,SRTTRST                                  +TRUST               
         TM    SRTADJS,TAPDADTR+TAPDADTT                                        
         BZ    *+8                                                              
         A     R1,SRTWIRE                                                       
*                                                                               
         CLI   RECNUM,PK                                                        
         JNE   TAXD3510                                                         
         CLI   SRTW4TY,TAW4TYCA    OR CANADIAN                                  
         JNE   TAXD3510                                                         
         A     R1,SRTCTAX                                                       
*                                                                               
TAXD3510 ST    R1,WORK                                                          
         GOTO1 =A(SENDDOWN),DMCB,(C'A',WORK),6             DEDUCTIONS           
*                                                                               
         L     R1,SRTYTOTD                                 YTD DEDUCT           
         A     R1,SRTYFED                                                       
         A     R1,SRTYFIC                                                       
         A     R1,SRTYMDED                                                      
         A     R1,SRTYMPT                                                       
         A     R1,SRTYCHAR                                                      
         A     R1,SRTYLIEN                                                      
         S     R1,SRTYPPLA                                                      
         A     R1,SRTYDUES                                                      
         A     R1,SRTYTRST                                                      
         A     R1,SRTYDIRT                                                      
         A     R1,SRTYWIRE                                                      
*        S     R1,SRTYTRST                                                      
         ST    R1,SRTYTOTD                                                      
*                                                                               
         ST    R1,WORK                                                          
         GOTO1 =A(SENDDOWN),DMCB,(C'Y',WORK),6             YTD DEDUCT           
*                                                                               
         BRAS  RE,EOLDOWN                                                       
         B     TXDDXIT                                                          
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*---------------------------------------------------------------------          
* STATE OF WORK                                                                 
*---------------------------------------------------------------------          
         USING TACYD,R4                                                         
TAXD2    NTR1  BASE=*,LABEL=*                                                   
                                                                                
         XC    DUMTACW,DUMTACW                                                  
         XC    DUMTACY,DUMTACY                                                  
                                                                                
         CLI   RECNUM,PK                                                        
         BE    TAXD2800                                                         
                                                                                
         GOTO1 =A(TAXUNT),DMCB,ASRTCHK,SRTSOW                                   
         CLC   SRTSOW,=CL3'CN '    SKIP CANADA                                  
         BE    TAXD2200                                                         
*                                                                               
         LTR   R4,R4                                                            
         BZ    TAXD2200                                                         
         OC    SRTWTAX,SRTWTAX     ANY STATE OF WORK TAX?                       
         BNZ   TAXD2000                                                         
         OC    TACYTAX,TACYTAX     ANY STATE OF WORK TAX YTD?                   
         BNZ   TAXD2000                                                         
         OC    TACYSUI,TACYSUI     ANY STATE SUI?                               
         BNZ   TAXD2000                                                         
         OC    TACYSDI,TACYSDI     ANY STATE SDI?                               
         BNZ   TAXD2000                                                         
         OC    TACYSUI,TACYSUI     ANY STATE SUI YTD?                           
         BNZ   TAXD2000                                                         
         OC    TACYSDI,TACYSDI     ANY STATE SDI YTD?                           
         BZ    TAXD2200                                                         
*                                                                               
TAXD2000 MVI   BYTE,C'N'           DEFAULT NON-RESIDENT                         
         CLC   SRTSOW,SRTSOR       RESIDENT AND WORK STATE SAME?                
         BNE   *+8                                                              
         MVI   BYTE,C'Y'           YES, FLAG AS RESIDENT                        
*                                                                               
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C'T2'),2   TAX 2                        
         GOTO1 (RF),DMCB,(C'T',BYTE),1             RES Y/N                      
         GOTO1 (RF),DMCB,(C'T',=C'N'),1            STATE                        
         GOTO1 (RF),DMCB,(C'T',SRTSOW),L'SRTSOW    AREA                         
         GOTO1 (RF),DMCB,(C'A',SRTWTAX),L'SRTWTAX  AREA TAX                     
         GOTO1 (RF),DMCB,(C'Y',TACYTAX),L'TACYTAX  AREA TAX YTD                 
         GOTO1 (RF),DMCB,(C'A',SRTSUI),L'SRTSUI    SUI                          
         L     R1,SRTYTOTD                                                      
         A     R1,TACYTAX                                                       
         ST    R1,SRTYTOTD                                                      
*                                                                               
         LTR   R4,R4                                                            
         BZ    TAXD2110                                                         
         GOTO1 (RF),DMCB,(C'Y',TACYSUI),L'TACYSUI  SUI YTD                      
         L     R1,SRTYTOTD                                                      
         A     R1,TACYSUI                                                       
         ST    R1,SRTYTOTD                                                      
         B     TAXD2115                                                         
*                                                                               
TAXD2110 GOTO1 (RF),DMCB,(C'T',=C' '),1                                         
TAXD2115 L     R1,SRTSDI                                                        
         A     R1,SRTSFLI                                                       
         ST    R1,WORK                                                          
         GOTO1 (RF),DMCB,(C'A',WORK),L'SRTSDI    SDI+FLI                        
*                                                                               
         LTR   R4,R4                                                            
         BZ    TAXD2120                                                         
         L     R1,TACYSDI                                                       
         CLI   TACYLEN,TACYLN2Q                                                 
         BL    *+8                                                              
         A     R1,TACYSFLI                                                      
         ST    R1,WORK                                                          
         GOTO1 (RF),DMCB,(C'Y',WORK),L'TACYSDI  SDI+FLI YTD                     
         L     R1,SRTYTOTD                                                      
         A     R1,TACYSDI                                                       
         CLI   TACYLEN,TACYLN2Q                                                 
         BL    *+8                                                              
         A     R1,TACYSFLI                                                      
         ST    R1,SRTYTOTD                                                      
         B     TAXD2125                                                         
*                                                                               
TAXD2120 GOTO1 (RF),DMCB,(C'T',=C' '),1                                         
TAXD2125 BRAS  RE,EOLDOWN                                                       
         EJECT                                                                  
*------------------------------------------------------------                   
* CITY OF WORK                                                                  
*------------------------------------------------------------                   
TAXD2200 DS    0H                                                               
         GOTO1 =A(TAXUNT),DMCB,ASRTCHK,SRTCOW                                   
         LTR   R4,R4                                                            
         BZ    TAXD2300                                                         
         OC    SRTLTAX,SRTLTAX     ANY LOCAL TAX                                
         BNZ   TAXD2250                                                         
         OC    TACYTAX,TACYTAX     AND YTD LOCAL                                
         BZ    TAXD2300                                                         
*                                                                               
TAXD2250 GOTO1 =A(SENDDOWN),DMCB,(C'T',=C'T2'),2       TAX 2                    
         GOTO1 (RF),DMCB,(C'T',BYTE),1                 RESIDENT                 
         GOTO1 (RF),DMCB,(C'T',=C'Y'),1                CITY                     
         GOTO1 (RF),DMCB,(C'T',SRTCOW),L'SRTCOW        AREA                     
         GOTO1 (RF),DMCB,(C'A',SRTLTAX),L'SRTLTAX      AREA TAX                 
         GOTO1 (RF),DMCB,(C'Y',TACYTAX),L'TACYTAX      AREA TAX YTD             
         GOTO1 (RF),DMCB,(C'T',=C' '),1                                         
         GOTO1 (RF),DMCB,(C'T',=C' '),1                                         
         GOTO1 (RF),DMCB,(C'T',=C' '),1                                         
         GOTO1 (RF),DMCB,(C'T',=C' '),1                                         
*                                                                               
         BRAS  RE,EOLDOWN                                                       
                                                                                
         L     R1,SRTYTOTD                                                      
         A     R1,SRTYLOC                                                       
         ST    R1,SRTYTOTD                                                      
         EJECT                                                                  
*---------------------------------------------------------------------          
* STATE OF RESIDENCE                                                            
*---------------------------------------------------------------------          
TAXD2300 DS    0H                  STATE OF RESIDENCE                           
         CLC   SRTSOW,SRTSOR       WORK AND RESIDENT SAME                       
         BE    TAXD2400            SKIP, DONE ALREADY ABOVE                     
*                                                                               
         GOTO1 =A(TAXUNT),DMCB,ASRTCHK,SRTSOR                                   
         LTR   R3,R3               TACW FOUND?                                  
         BNZ   TAXD2350                                                         
         LTR   R4,R4               TACY FOUND?                                  
         BZ    TAXD2400            BOTH NOT FOUND, SKIP IT                      
         LA    R3,DUMTACW                                                       
*                                                                               
TAXD2350 LTR   R4,R4                                                            
         BNZ   TAXD2380                                                         
         LA    R4,DUMTACY                                                       
*                                                                               
TAXD2380 BAS   RE,TAXD2AMT         ANY NON-ZERO AMOUNTS?                        
         BZ    TAXD2400            NO, SKIP                                     
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C'T2'),2   TAX 2                        
         GOTO1 (RF),DMCB,(C'T',=C'Y'),1            RESIDENT                     
         GOTO1 (RF),DMCB,(C'T',=C'N'),1            STATE                        
         GOTO1 (RF),DMCB,(C'T',SRTSOR),L'SRTSOR    AREA                         
*                                                                               
         USING TACWD,R3                                                         
         GOTO1 (RF),DMCB,(C'A',TACWTAX),L'TACWTAX  AREA TAX                     
         GOTO1 (RF),DMCB,(C'Y',TACYTAX),L'TACYTAX  AREA TAX YTD                 
*                                                                               
         GOTO1 (RF),DMCB,(C'A',TACWSUI),L'TACWSUI  SUI                          
         GOTO1 (RF),DMCB,(C'Y',TACYSUI),L'TACYSUI  SUI YTD                      
*                                                                               
         GOTO1 (RF),DMCB,(C'A',TACWSDI),L'TACWSDI  SDI                          
         GOTO1 (RF),DMCB,(C'Y',TACYSDI),L'TACYSDI  SDI YTD                      
*                                                                               
         BRAS  RE,EOLDOWN                                                       
*                                                                               
         L     R1,SRTYTOTD                                                      
         A     R1,TACYTAX                                                       
         A     R1,TACYSUI                                                       
         A     R1,TACYSDI                                                       
         ST    R1,SRTYTOTD                                                      
         EJECT                                                                  
*---------------------------------------------------------------------          
* CITY OF RESIDENCE                                                             
*---------------------------------------------------------------------          
TAXD2400 CLC   SRTCOW,SRTCOR       WORK AND RESIDENT SAME                       
         BE    TAXD2500            SKIP, DONE ALREADY ABOVE                     
*                                                                               
         GOTO1 =A(TAXUNT),DMCB,ASRTCHK,SRTCOR                                   
         LTR   R3,R3               TACW FOUND?                                  
         BNZ   TAXD2450                                                         
         LTR   R4,R4               TACY FOUND?                                  
         BZ    TAXD2500            BOTH NOT FOUND, SKIP IT                      
         LA    R3,DUMTACW                                                       
*                                                                               
TAXD2450 LTR   R4,R4                                                            
         BNZ   TAXD2480                                                         
         LA    R4,DUMTACY                                                       
*                                                                               
TAXD2480 BAS   RE,TAXD2AMT         ANY NON-ZERO AMOUNTS?                        
         BZ    TAXD2500            NO, SKIP                                     
                                                                                
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C'T2'),2   TAX 2                        
         GOTO1 (RF),DMCB,(C'T',=C'Y'),1            RESIDENT                     
         GOTO1 (RF),DMCB,(C'T',=C'Y'),1            CITY                         
         GOTO1 (RF),DMCB,(C'T',SRTCOR),L'SRTCOR    AREA                         
*                                                                               
         GOTO1 (RF),DMCB,(C'A',TACWTAX),L'TACWTAX  AREA TAX                     
         GOTO1 (RF),DMCB,(C'Y',TACYTAX),L'TACYTAX  AREA TAX YTD                 
*                                                                               
         GOTO1 (RF),DMCB,(C'A',TACWSUI),L'TACWSUI  SUI                          
         GOTO1 (RF),DMCB,(C'Y',TACYSUI),L'TACYSUI  SUI YTD                      
*                                                                               
         GOTO1 (RF),DMCB,(C'A',TACWSDI),L'TACWSDI  SDI                          
         GOTO1 (RF),DMCB,(C'Y',TACYSDI),L'TACYSDI  SDI YTD                      
*                                                                               
         BRAS  RE,EOLDOWN                                                       
*                                                                               
         L     R1,SRTYTOTD                                                      
         A     R1,TACYTAX                                                       
         A     R1,TACYSUI                                                       
         A     R1,TACYSDI                                                       
         ST    R1,SRTYTOTD                                                      
         B     TAXD2500                                                         
         EJECT                                                                  
*----------------------------------------------------------------------         
* FIND THE OTHER TACY NOT MATCHING SRTSOR AND SRTCOR                            
*----------------------------------------------------------------------         
TAXD2500 XC    OTHAMTS(16),OTHAMTS    CLEAR TEMP AMOUNTS                        
         MVC   AIO,AIO1                                                         
         L     R4,ASRTCHK                                                       
         MVI   ELCODE,TACYELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   TAXD2570                                                         
*                                                                               
         USING TACYD,R4                                                         
TAXD2540 CLC   TACYUNIT,=C'FD '    EXCLUDE FEDERAL                              
         BE    TAXD2560                                                         
         CLC   TACYUNIT,=C'CN '    EXCLUDE FEDERAL                              
         BE    TAXD2560                                                         
         CLC   TACYUNIT,SRTSOW     STATE OF WORK                                
         BE    TAXD2560                                                         
         CLC   TACYUNIT,SRTCOW     CITY OF WORK                                 
         BE    TAXD2560                                                         
         CLC   TACYUNIT,SRTSOR     STATE OF RESIDENCE                           
         BE    TAXD2560                                                         
         CLC   TACYUNIT,SRTCOR     CITY OF RESIDENCE                            
         BE    TAXD2560                                                         
*                                                                               
         CLI   TACYUNIT+2,C' '     ONLY CITIES USE 3 BYTES                      
         BNE   TAXD2550                                                         
         L     RE,OTHAMTS          AREA YTD TAX (STATE)                         
         A     RE,TACYTAX                                                       
         ST    RE,OTHAMTS                                                       
         L     RE,OTHAMTS+4        SUI YTD TAX                                  
         A     RE,TACYSUI                                                       
         ST    RE,OTHAMTS+4                                                     
         L     RE,OTHAMTS+8        SDI YTD TAX                                  
         A     RE,TACYSDI                                                       
         ST    RE,OTHAMTS+8                                                     
         B     TAXD2560                                                         
*                                                                               
TAXD2550 L     RE,OTHAMTS+12       AREA YTD TAX (CITY)                          
         A     RE,TACYTAX                                                       
         ST    RE,OTHAMTS+12                                                    
         B     TAXD2560                                                         
*                                                                               
TAXD2560 SR    RF,RF               BUMP TO NEXT ELEMENT                         
         IC    RF,TACYLEN                                                       
         AR    R4,RF                                                            
         CLI   TACYEL,0                                                         
         BE    TAXD2570                                                         
         CLI   TACYEL,TACYELQ                                                   
         BNE   TAXD2570                                                         
         B     TAXD2540                                                         
*                                                                               
         EJECT                                                                  
*----------------------------------------------------------------------         
* OTHER AREAS                                                                   
*----------------------------------------------------------------------         
TAXD2570 OC    OTHAMTS(12),OTHAMTS   ANY OTHER STATE YTD?                       
         BZ    TAXD2580                                                         
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C'T2'),2           TAX 2                
         GOTO1 (RF),DMCB,(C'T',=C'N'),1                    RESIDENT             
         GOTO1 (RF),DMCB,(C'T',=C'N'),1                    STATE                
         GOTO1 (RF),DMCB,(C'T',=C'OTHER'),5                OTHER                
         GOTO1 (RF),DMCB,(C'T',=C'.00'),3                                       
         GOTO1 (RF),DMCB,(C'Y',OTHAMTS),4                  TAX YTD              
         GOTO1 (RF),DMCB,(C'T',=C'.00'),3                                       
         GOTO1 (RF),DMCB,(C'Y',OTHAMTS+4),4                SUI YTD              
         GOTO1 (RF),DMCB,(C'T',=C'.00'),3                                       
         GOTO1 (RF),DMCB,(C'Y',OTHAMTS+8),4                SDI YTD              
         BRAS  RE,EOLDOWN                                                       
         L     R1,SRTYTOTD                                                      
         A     R1,OTHAMTS                                                       
         A     R1,OTHAMTS+4                                                     
         A     R1,OTHAMTS+8                                                     
         ST    R1,SRTYTOTD                                                      
*                                                                               
TAXD2580 OC    OTHAMTS+12(4),OTHAMTS+12                                         
         BZ    TAXD2600                                                         
*                                                                               
TAXD2590 GOTO1 =A(SENDDOWN),DMCB,(C'T',=C'T2'),2           TAX 2                
         GOTO1 (RF),DMCB,(C'T',=C'N'),1                    RESIDENT             
         GOTO1 (RF),DMCB,(C'T',=C'Y'),1                    CITY                 
         GOTO1 (RF),DMCB,(C'T',=C'OTHER'),5                OTHER                
         GOTO1 (RF),DMCB,(C'T',=C'.00'),3                                       
         GOTO1 (RF),DMCB,(C'Y',OTHAMTS+12),4               TAX YTD              
         GOTO1 (RF),DMCB,(C'T',=C'.00'),3                                       
         GOTO1 (RF),DMCB,(C'T',=C'.00'),3                                       
         GOTO1 (RF),DMCB,(C'T',=C'.00'),3                                       
         GOTO1 (RF),DMCB,(C'T',=C'.00'),3                                       
         BRAS  RE,EOLDOWN                                                       
         L     R1,SRTYTOTD                                                      
         A     R1,OTHAMTS+12                                                    
         ST    R1,SRTYTOTD                                                      
*                                                                               
         EJECT                                                                  
*----------------------------------------------------------------------         
* Canadian Tax                                                                  
*----------------------------------------------------------------------         
         USING TACWD,R3                                                         
TAXD2600 GOTO1 =A(TAXUNT),DMCB,ASRTCHK,=C'CN '                                  
         LTR   R4,R4               NO CANADIAN YTD?                             
         BZ    TAXD2699            DONE                                         
         MVC   SRTYCAN,TACYTAX                                                  
         XC    SRTCTAX,SRTCTAX                                                  
         LTR   R3,R3                                                            
         BZ    TAXD2610                                                         
         MVC   SRTCTAX,TACWTAX                                                  
*                                                                               
TAXD2610 GOTO1 =A(SENDDOWN),DMCB,(C'T',=C'T2'),2           TAX 2                
         GOTO1 (RF),DMCB,(C'T',=C'N'),1                    RESIDENT             
         GOTO1 (RF),DMCB,(C'T',=C'N'),1                    CITY                 
         GOTO1 (RF),DMCB,(C'T',=C'CN'),2                   CANADA               
         GOTO1 (RF),DMCB,(C'A',SRTCTAX),L'SRTCTAX          CANADIAN TAX         
         GOTO1 (RF),DMCB,(C'Y',SRTYCAN),L'SRTYCAN          CAN TAX YTD          
         GOTO1 (RF),DMCB,(C'T',=C'.00'),3                                       
         GOTO1 (RF),DMCB,(C'T',=C'.00'),3                                       
         GOTO1 (RF),DMCB,(C'T',=C'.00'),3                                       
         GOTO1 (RF),DMCB,(C'T',=C'.00'),3                                       
         BRAS  RE,EOLDOWN                                                       
         L     R1,SRTYTOTD                                                      
         A     R1,SRTYCAN                                                       
         ST    R1,SRTYTOTD                                                      
*                                                                               
TAXD2699 J     TAXD2XIT                                                         
                                                                                
         DROP  R3                                                               
         EJECT                                                                  
*---------------------------------------------------------------------          
* P+ CHECKS COMBINES ALL STATE WITHHOLDINGS                                     
*---------------------------------------------------------------------          
TAXD2800 XC    WTHAMTS(16),WTHAMTS    CLEAR TEMP AMOUNTS                        
         XC    OTHAMTS(16),OTHAMTS                                              
                                                                                
         MVC   AIO,AIO1                                                         
         L     R4,ASRTCHK                                                       
         MVI   ELCODE,TACWELQ                                                   
         BRAS  RE,GETEL                                                         
         B     TAXD2805                                                         
TAXD2803 BRAS  RE,NEXTEL                                                        
TAXD2805 BNE   TAXD2820                                                         
*                                                                               
         USING TACWD,R4                                                         
         CLC   TACWUNIT,=C'FD '    EXCLUDE FEDERAL                              
         BE    TAXD2803            GET NEXT ONE                                 
         CLC   TACWUNIT,=C'CN '    EXCLUDE CANADA                               
         BE    TAXD2803            GET NEXT ONE                                 
         CLI   TACWUNIT+2,C' '     CITY?                                        
         BH    TAXD2808            YES, DIFFERENT BUCKET                        
*                                                                               
         TM    TGSYSTAT,TASYSFLT   NEW FLAT TAX RULES?                          
         JZ    TAXD2806                                                         
         CLI   SRTTYPE,TAW4TYCA                                                 
         JE    TAXD2806                                                         
         CLC   TACWUNIT,=C'QC '                                                 
         JE    TAXD2803                                                         
*                                                                               
TAXD2806 L     RE,WTHAMTS          AREA TAX                                     
         A     RE,TACWTAX                                                       
         ST    RE,WTHAMTS                                                       
         L     RE,WTHAMTS+4        SUI TAX                                      
         A     RE,TACWSUI                                                       
         ST    RE,WTHAMTS+4                                                     
         L     RE,WTHAMTS+8        SDI TAX                                      
         A     RE,TACWSDI                                                       
         CLI   TACWLEN,TACWLN2Q                                                 
         BL    *+8                                                              
         A     RE,TACWSFLI         + FLI TAX                                    
         ST    RE,WTHAMTS+8                                                     
         B     TAXD2803                                                         
*                                                                               
TAXD2808 L     RE,WTHAMTS+12       CITY TAX                                     
         A     RE,TACWTAX                                                       
         ST    RE,WTHAMTS+12                                                    
         B     TAXD2803                                                         
*                                                                               
TAXD2820 MVC   AIO,AIO1                                                         
         L     R4,ASRTCHK                                                       
         MVI   ELCODE,TACYELQ                                                   
         BRAS  RE,GETEL                                                         
         B     TAXD2825                                                         
TAXD2823 BRAS  RE,NEXTEL                                                        
TAXD2825 BNE   TAXD2840                                                         
*                                                                               
         USING TACYD,R4                                                         
         CLC   TACYUNIT,=C'FD '    EXCLUDE FEDERAL                              
         BE    TAXD2823            GET NEXT ONE                                 
         CLC   TACYUNIT,=C'CN '    EXCLUDE CANADA                               
         BE    TAXD2823            GET NEXT ONE                                 
         CLI   TACYUNIT+2,C' '     CITY?                                        
         BH    TAXD2828            YES, DIFFERENT BUCKET                        
*                                                                               
         TM    TGSYSTAT,TASYSFLT   NEW FLAT TAX RULES?                          
         JZ    TAXD2826                                                         
         CLI   SRTTYPE,TAW4TYCA                                                 
         JE    TAXD2826                                                         
         CLC   TACYUNIT,=C'QC '                                                 
         JE    TAXD2823                                                         
*                                                                               
TAXD2826 L     RE,OTHAMTS          AREA TAX YTD                                 
         A     RE,TACYTAX                                                       
         ST    RE,OTHAMTS                                                       
         L     RE,OTHAMTS+4        SUI TAX YTD                                  
         A     RE,TACYSUI                                                       
         ST    RE,OTHAMTS+4                                                     
         L     RE,OTHAMTS+8        SDI TAX YTD                                  
         A     RE,TACYSDI                                                       
         CLI   TACYLEN,TACYLN2Q                                                 
         BL    *+8                                                              
         A     RE,TACYSFLI         + FLI TAX                                    
         ST    RE,OTHAMTS+8                                                     
         B     TAXD2823                                                         
                                                                                
TAXD2828 L     RE,OTHAMTS+12       CITY TAX YTD                                 
         A     RE,TACYTAX                                                       
         ST    RE,OTHAMTS+12                                                    
         B     TAXD2823                                                         
*                                                                               
TAXD2840 OC    WTHAMTS(12),WTHAMTS     ANY STATE WITHHOLDINGS?                  
         BNZ   TAXD2843                                                         
         OC    OTHAMTS(12),OTHAMTS                                              
         BZ    TAXD2860                                                         
TAXD2843 GOTO1 =A(SENDDOWN),DMCB,(C'T',=C'T2'),2           TAX 2                
         GOTO1 (RF),DMCB,(C'T',=C'N'),1                    RESIDENT             
         GOTO1 (RF),DMCB,(C'T',=C'N'),1                    STATE                
         GOTO1 (RF),DMCB,(C'T',=C' '),1                                         
         GOTO1 (RF),DMCB,(C'A',WTHAMTS),4                  TAX                  
         GOTO1 (RF),DMCB,(C'Y',OTHAMTS),4                  TAX YTD              
         GOTO1 (RF),DMCB,(C'A',WTHAMTS+4),4                SUI                  
         GOTO1 (RF),DMCB,(C'Y',OTHAMTS+4),4                SUI YTD              
         GOTO1 (RF),DMCB,(C'A',WTHAMTS+8),4                SDI                  
         GOTO1 (RF),DMCB,(C'Y',OTHAMTS+8),4                SDI YTD              
         BRAS  RE,EOLDOWN                                                       
                                                                                
TAXD2860 OC    WTHAMTS+12(4),WTHAMTS+12  ANY CITY WITHHOLDINGS?                 
         BNZ   TAXD2863                                                         
         OC    OTHAMTS+12(4),OTHAMTS+12                                         
         BZ    TAXD2880                                                         
TAXD2863 GOTO1 =A(SENDDOWN),DMCB,(C'T',=C'T2'),2           TAX 2                
         GOTO1 (RF),DMCB,(C'T',=C'N'),1                    RESIDENT             
         GOTO1 (RF),DMCB,(C'T',=C'Y'),1                    CITY                 
         GOTO1 (RF),DMCB,(C'T',=C' '),1                                         
         GOTO1 (RF),DMCB,(C'A',WTHAMTS+12),4               TAX                  
         GOTO1 (RF),DMCB,(C'Y',OTHAMTS+12),4               TAX YTD              
         GOTO1 (RF),DMCB,(C'T',=C'.00'),3                                       
         GOTO1 (RF),DMCB,(C'T',=C'.00'),3                                       
         GOTO1 (RF),DMCB,(C'T',=C'.00'),3                                       
         GOTO1 (RF),DMCB,(C'T',=C'.00'),3                                       
         BRAS  RE,EOLDOWN                                                       
                                                                                
TAXD2880 L     R1,SRTYTOTD                                                      
         A     R1,OTHAMTS                                                       
         A     R1,OTHAMTS+4                                                     
         A     R1,OTHAMTS+8                                                     
         A     R1,OTHAMTS+12                                                    
         ST    R1,SRTYTOTD                                                      
         SPACE 3                                                                
TAXD2XIT XIT1                                                                   
*---------------------------------------------------------------------          
* CHECK IF THERE ARE ANY AMOUNTS TO PRINT                                       
*        CC = ZERO, NOTHING TO PRINT                                            
*        CC = NZ, SOMETHING                                                     
*---------------------------------------------------------------------          
         USING TACWD,R3                                                         
TAXD2AMT OC    TACWTAX,TACWTAX                                                  
         BNZR  RE                                                               
         OC    TACWSUI,TACWSUI                                                  
         BNZR  RE                                                               
         OC    TACWSDI,TACWSDI                                                  
         BNZR  RE                                                               
*                                                                               
         OC    TACYTAX,TACYTAX                                                  
         BNZR  RE                                                               
         OC    TACYSUI,TACYSUI                                                  
         BNZR  RE                                                               
         OC    TACYSDI,TACYSDI                                                  
         BNZR  RE                                                               
         CR    RE,RE                                                            
         BR    RE                                                               
         EJECT                                                                  
*                                                                               
WTHAMTS  DS    4F                  TAX, SUI, SDI+FLI, CITY                      
OTHAMTS  DS    4F                  YTD TAX, SUI, SDI+FLI, CITY                  
DUMTACW  DS    XL(TACWLN2Q)                                                     
DUMTACY  DS    XL(TACYLN2Q)                                                     
*                                                                               
         DROP  R2,R3,R4                                                         
         LTORG                                                                  
         EJECT                                                                  
*---------------------------------------------------------------------          
*        POINT R3 TO TACW ELEMENT AND R4 TO TACY ELEMENT                        
*              P1 = A(IO AREA)                                                  
*              P2 = A(TAX UNIT)                                                 
*---------------------------------------------------------------------          
TAXUNT   NTR1                                                                   
                                                                                
         XR    R3,R3                                                            
         XR    R4,R4                                                            
                                                                                
         L     R5,AIO              SAVE WHAT WAS IN AIO                         
         MVC   AIO,0(R1)           USE ONE IN PARM 1                            
         L     RF,4(R1)                                                         
         MVC   WORK(3),0(RF)       COPY UNIT INTO WORK                          
         XR    R3,R3                                                            
         XR    R4,R4                                                            
         MVI   ELCODE,TACWELQ                                                   
         GOTO1 GETL,DMCB,(3,WORK)                                               
         BNE   TAXU020                                                          
         L     R3,TGELEM           FOUND IT, SET R3                             
*                                                                               
TAXU020  MVI   ELCODE,TACYELQ                                                   
         GOTO1 GETL,DMCB,(3,WORK)                                               
         BNE   TAXUXIT                                                          
         L     R4,TGELEM           FOUND IT, SET R4                             
*                                                                               
TAXUXIT  ST    R5,AIO              RESTORE AIO                                  
         XIT1  REGS=(R3,R4)                                                     
         LTORG                                                                  
         EJECT                                                                  
*---------------------------------------------------------------------          
* CANADIAN TAXES                                                                
*---------------------------------------------------------------------          
TAXD4    NTR1  BASE=*,LABEL=*                                                   
         CLI   RECNUM,PK                                                        
         JNE   TAXD4X                                                           
*        CLI   SRTW4TY,TAW4TYCA    OR CANADIAN                                  
*        JNE   TAXD4X                                                           
                                                                                
         USING TACWD,R4                                                         
         MVC   AIO,ASRTCHK                                                      
         MVI   ELCODE,TACWELQ                                                   
         GOTO1 GETL,DMCB,(3,=C'CN ')                                            
         JNE   TAXD405                                                          
                                                                                
         L     R4,TGELEM                                                        
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C'T4'),2                                
         MVC   WORK(20),=C'Canadian Taxes      '                                
         GOTO1 =A(SENDDOWN),DMCB,(C'T',WORK),20            TAXES                
         GOTO1 =A(SENDDOWN),DMCB,(C'A',TACWTAX),L'TACWTAX                       
         BRAS  RE,EOLDOWN                                                       
                                                                                
         TM    TGSYSTAT,TASYSFLT   NEW FLAT TAX RULES?                          
         JZ    TAXD405                                                          
         MVC   AIO,ASRTCHK                                                      
         MVI   ELCODE,TACWELQ                                                   
         GOTO1 GETL,DMCB,(3,=C'QC ')                                            
         JNE   TAXD405                                                          
                                                                                
         L     R4,TGELEM                                                        
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C'T4'),2                                
         MVC   WORK(20),=C'Provincial Tax-     '                                
         MVC   WORK+15(2),TACWUNIT                                              
         GOTO1 =A(SENDDOWN),DMCB,(C'T',WORK),20            TAXES                
         GOTO1 =A(SENDDOWN),DMCB,(C'A',TACWTAX),L'TACWTAX                       
         BRAS  RE,EOLDOWN                                                       
                                                                                
         USING TAATD,R4                                                         
TAXD405  MVC   AIO,ASRTCHK                                                      
         MVI   ELCODE,TAATELQ                                                   
         GOTO1 GETL,DMCB,(3,=C'CN ')                                            
         JNE   TAXD410                                                          
                                                                                
         L     R4,TGELEM                                                        
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C'T4'),2                                
         MVC   WORK(20),=C'Canadian Federal Tax'                                
         GOTO1 =A(SENDDOWN),DMCB,(C'T',WORK),20            TAXES                
         GOTO1 =A(SENDDOWN),DMCB,(C'A',TAATTAX),L'TAATTAX                       
         BRAS  RE,EOLDOWN                                                       
                                                                                
TAXD410  L     R4,ASRTCHK                                                       
         MVI   ELCODE,TAATELQ                                                   
         BRAS  RE,GETEL                                                         
         J     *+8                                                              
TAXD415  BRAS  RE,NEXTEL                                                        
         JNE   TAXD4X                                                           
         CLC   TAATUNIT,=C'CN '                                                 
         JE    TAXD415                                                          
*                                                                               
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C'T4'),2                                
         MVC   WORK(20),=C'Provincial Tax-     '                                
         MVC   WORK+15(2),TAATUNIT                                              
TAXD420  GOTO1 =A(SENDDOWN),DMCB,(C'T',WORK),20            TAXES                
         GOTO1 =A(SENDDOWN),DMCB,(C'A',TAATTAX),L'TAATTAX                       
         BRAS  RE,EOLDOWN                                                       
*                                                                               
         CLC   TAATUNIT,=C'CN '                                                 
         JE    TAXD415                                                          
*                                                                               
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C'T4'),2                                
         MVC   DUB(4),=C'CPP-'                                                  
         MVC   DUB+4(2),TAATUNIT                                                
         GOTO1 =A(SENDDOWN),DMCB,(C'T',DUB),6              PENSION PLAN         
         GOTO1 =A(SENDDOWN),DMCB,(C'A',TAATPP),L'TAATPP                         
         BRAS  RE,EOLDOWN                                                       
*                                                                               
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C'T4'),2                                
         MVC   DUB(4),=C'EI- '                                                  
         MVC   DUB+3(2),TAATUNIT                                                
         MVI   DUB+5,C' '                                                       
         GOTO1 =A(SENDDOWN),DMCB,(C'T',DUB),6              EMP INS.             
         GOTO1 =A(SENDDOWN),DMCB,(C'A',TAATEI),L'TAATEI                         
         BRAS  RE,EOLDOWN                                                       
*                                                                               
         CLC   TAATUNIT,=C'QC '                                                 
         JNE   TAXD415                                                          
         MVC   DUB(5),=C'QPIP-'                                                 
         MVC   DUB+5(2),TAATUNIT                                                
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C'T4'),2                                
         GOTO1 =A(SENDDOWN),DMCB,(C'T',DUB),7              PARENTAL INS         
         GOTO1 =A(SENDDOWN),DMCB,(C'A',TAATPIP),L'TAATPIP                       
         BRAS  RE,EOLDOWN                                                       
         J     TAXD415                                                          
*                                                                               
TAXD4X   MVC   AIO,AIO1                                                         
         XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
*---------------------------------------------------------------------          
*        SEND BLANK                                                             
*---------------------------------------------------------------------          
SENDBLK  NTR1  BASE=*,LABEL=*                                                   
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C' '),1                                 
         XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
*---------------------------------------------------------------------          
*        CHECK TOTALS                                                           
*---------------------------------------------------------------------          
CHKTOTL  NTR1  BASE=*,LABEL=*                                                   
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C'CT'),2           CHK TOTALS           
*                                                                               
         L     RF,SRTPAY                                                        
         A     RF,SRTREXP          ALREADY AFTER DUE COMP                       
         S     RF,SRTGSTPD                                                      
         A     RF,SRTPST                                                        
         S     RF,SRTDUE                                                        
         ST    RF,WORK                                                          
         GOTO1 =A(SENDDOWN),DMCB,(C'A',WORK),6             GROSS                
         L     RF,SRTYEARN                                                      
         A     RF,SRTYNTAX                                                      
         S     RF,SRTYGST                                                       
         A     RF,SRTYPST                                                       
*        S     RF,SRTYDUE                                - DUE COMP             
         ST    RF,BLOCK                                                         
         GOTO1 =A(SENDDOWN),DMCB,(C'Y',BLOCK),6            GROSS YTD            
*                                                                               
         L     R1,SRTTDED                                  DEDUCTIONS           
         A     R1,SRTLIEN                                  +LIEN                
         S     R1,SRTPPLA                                  -P+ AGY COMM         
         A     R1,SRTTRST                                  +TRUST               
         TM    SRTADJS,TAPDADTR+TAPDADTT                                        
         BZ    *+8                                                              
         A     R1,SRTWIRE                                                       
*                                                                               
         CLI   RECNUM,PK                                                        
         JNE   CHKTOT5                                                          
         CLI   SRTW4TY,TAW4TYCA    OR CANADIAN                                  
         JNE   CHKTOT5                                                          
         A     R1,SRTCTAX                                                       
*                                                                               
CHKTOT5  LCR   R1,R1                                                            
         ST    R1,WORK                                                          
         GOTO1 =A(SENDDOWN),DMCB,(C'A',WORK),6             DEDUCTIONS           
         L     R1,SRTYTOTD                                                      
         LCR   R1,R1                                                            
         ST    R1,WORK                                                          
         GOTO1 =A(SENDDOWN),DMCB,(C'Y',WORK),6             DEDUCT YTD           
*                                                                               
         L     R1,SRTNET                                   DEDUCTIONS           
         ST    R1,WORK                                                          
         GOTO1 =A(SENDDOWN),DMCB,(C'A',WORK),6             NET                  
         L     R1,BLOCK                                                         
         S     R1,SRTYTOTD                                                      
         ST    R1,WORK                                                          
         GOTO1 =A(SENDDOWN),DMCB,(C'Y',WORK),6             NET YTD              
*                                                                               
         BRAS  RE,EOLDOWN                                                       
         XIT1                                                                   
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*---------------------------------------------------------------------          
*        PRINTED CHECK                                                          
*---------------------------------------------------------------------          
PRTDCHK  NTR1  BASE=*,LABEL=*                                                   
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C'CK'),2      PRINTED CHECK             
*                                                                               
         GOTO1 =A(SENDDOWN),DMCB,(C'T',CHKNCHR),L'CHKNCHR  CHK NUMBER           
         GOTO1 =A(SENDDOWN),DMCB,(C'd',SRTCDTE),6          CHK DATE             
*                                                                               
         L     R4,SRTNET                                   DEDUCTIONS           
         CLI   RERUN,C'Y'          IF RERUN OF CHECKS                           
         BE    PRTDCHK5                                                         
         TM    SRTADJS,TAPDADVD+TAPDADRI                                        
         BNZ   PRTDCHK5                                                         
         BO    PRTDCHK5                                                         
PRTDCHK5 ST    R4,WORK                                                          
         GOTO1 =A(SENDDOWN),DMCB,(C'A',WORK),6             CHECK NET            
*                                                                               
         ST    R4,WORK                                                          
         GOTO1 =V(NUMTOLET),DMCB,(0,WORK),BLOCK                                 
         L     R2,0(R1)                                 LENGTH RETURNED         
         LA    R1,BLOCK(R2)                                                     
         MVC   0(4,R1),=C'/100'                                                 
         LA    R2,4(R2)                                                         
         GOTO1 =A(SENDDOWN),DMCB,(C'T',BLOCK),(R2)         PAY EXACTLY          
*                                                                               
         BAS   RE,PRTDADDR                                 PRINTED ADDR         
         BRAS  RE,EOLDOWN                                                       
YES8     SR    RC,RC                                                            
NO8      LTR   RC,RC                                                            
PRTDCHKX XIT1                                                                   
         EJECT                                                                  
*---------------------------------------------------------------------          
*        PRINTED ADDRESS                                                        
*---------------------------------------------------------------------          
PRTDADDR NTR1                                                                   
         L     RE,AIO              CLEAR ADDRESS LINES BLOCK                    
         LA    RF,132*8                                                         
         SR    R1,R1                                                            
         MVCL  RE,R0                                                            
*                                                                               
         L     R5,AIO              R5 = A(ADDRESS LINES BLOCK)                  
         LA    R4,132*7(R5)        R4 = A(END OF 7 LINE PRINT BLOCK)            
*                                                                               
*                                  PUT W4 NAME IN BLOCK                         
         MVC   0(L'SRTNAME,R5),SRTNAME                                          
         LA    R5,132(R5)                                                       
*                                                                               
         OC    SRTTSSN,SRTTSSN     TRUSTEE?                                     
         BZ    PAD100                                                           
*                                                                               
         BAS   RE,CKLIEN           SKIP, IF THIS IS A LIEN                      
         BE    PAD100                                                           
*                                                                               
         MVC   0(13,R5),=CL13'IN TRUST FOR '                                    
         MVC   13(32,R5),COMMNAME+8                                             
         LA    R5,132(R5)                                                       
*                                                                               
PAD100   OC    SRTFSO,SRTFSO       IF FSO EXISTS                                
         BZ    PAD110                                                           
*                                                                               
         MVC   0(3,R5),=C'FSO'     THEN PUT FSO NAME IN BLOCK                   
         MVC   4(32,R5),SRTFSON                                                 
         LA    R5,132(R5)                                                       
*                                                                               
PAD110   LA    R2,SRTADD           R2 = A(FIRST W4 ADDRESS LINE)                
*                                                                               
         OC    SRTPNAM,SRTPNAM     IF PAYEE NAME GIVEN                          
         BZ    PAD140                                                           
         MVC   0(3,R5),=C'C/O'     THEN PUT 'C/O' PAYEE NAME IN BLOCK           
         MVC   4(32,R5),SRTPNAM                                                 
         LA    R5,132(R5)                                                       
*                                                                               
PAD140   LA    R0,3                LOOP TROUGH 3 ADDRESS LINES                  
*                                                                               
PAD150   CLC   0(30,R2),SPACES     IF ADDRESS LINE IS NOT CLEAR                 
         BNH   *+14                                                             
         MVC   0(30,R5),0(R2)      THEN PUT IT IN BLOCK                         
         LA    R5,132(R5)                                                       
         LA    R2,30(R2)           BUMP TO NEXT ADDRES LINE                     
         BCT   R0,PAD150           REPEAT UNTIL NO MORE ADDRESS LINES           
*                                                                               
         CLC   0(39,R2),SPACES     IF HAVE CITY, STATE, ZIP                     
         BNH   *+14                                                             
         MVC   0(39,R5),0(R2)      THEN PUT IT IN BLOCK                         
         LA    R5,132(R5)                                                       
*                                                                               
         CR    R5,R4               IF THERE ARE 7 LINES IN THE BLOCK            
         BNE   PAD160                                                           
         L     R5,AIO              THEN SQUASH THE FIRST 2 TOGETHER             
         GOTO1 SQUASHER,DMCB,(R5),132+32                                        
         LA    RE,132(R5)          AND MOVE THE NEXT 5 PLUS THE BLANK           
         LA    RF,132*6                LINE AT THE END UP ONE LINE              
         LA    R0,132(RE)                                                       
         LR    R1,RF                                                            
         MVCL  RE,R0                                                            
*                                                                               
PAD160   L     R5,AIO              R5 = A(FIRST PRINT LINE IN BLOCK)            
*                                                                               
PAD170   OC    0(132,R5),0(R5)     WHILE PRINT LINE IS NOT CLEAR                
         BZ    PAD190                                                           
         MVC   BLOCK(L'P),0(R5)    PRINT IT                                     
         OC    BLOCK(L'P),SPACES                                                
         GOTO1 =A(SENDDOWN),DMCB,(C'T',BLOCK),L'P                               
         LA    R5,132(R5)          BUMP TO NEXT PRINT LINE                      
         B     PAD170              LOOP BACK                                    
*                                                                               
PAD190   B     PRTDCHKX                                                         
         EJECT                                                                  
*---------------------------------------------------------------------          
* THIS ROUTINE RETURNS A POSITIVE CC IF A LIEN WITHHLD ELEMENT EXISTS           
*---------------------------------------------------------------------          
CKLIEN   NTR1                                                                   
         L     R4,ASRTCHK          FIND LIEN WITHHOLDING ELEMENT                
         MVI   ELCODE,TALWELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   NO8                                                              
         B     YES8                                                             
         EJECT                                                                  
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*---------------------------------------------------------------------          
*        FILE TRAILER                                                           
*---------------------------------------------------------------------          
FILTRLR  NTR1  BASE=*,LABEL=*                                                   
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C'FT'),2      FILE TRAILER              
         MVC   BLOCK(132),SPACES                                                
         MVC   BLOCK(8),=C'Run Date'                                            
         GOTO1 DATCON,DMCB,(1,TGTODAY1),(23,BLOCK+9)                            
         MVC   BLOCK+27(10),=C'Check Date'                                      
         GOTO1 DATCON,DMCB,(1,CHKDATE1),(23,BLOCK+38)                           
         GOTO1 =A(SENDDOWN),DMCB,(C'T',BLOCK),132                               
*                                                                               
         MVC   BLOCK(132),SPACES                                                
         MVC   BLOCK(22),=C'Total Taxable Earnings'                             
         EDIT  (4,TOTEARN),(15,BLOCK+32),2,COMMAS=YES,FLOAT=$,MINUS=YES         
         GOTO1 =A(SENDDOWN),DMCB,(C'T',BLOCK),132                               
*                                                                               
         MVC   BLOCK(132),SPACES                                                
         MVC   BLOCK(26),=C'Total Non-Taxable Earnings'                         
         EDIT  (4,TOTNTAX),(15,BLOCK+32),2,COMMAS=YES,FLOAT=$,MINUS=YES         
         GOTO1 =A(SENDDOWN),DMCB,(C'T',BLOCK),132                               
*                                                                               
         OC    TOTCVERN,TOTCVERN                                                
         BZ    FTR0100                                                          
         MVC   BLOCK(132),SPACES                                                
         MVC   BLOCK(24),=C'Total Converted Earnings'                           
         EDIT  (4,TOTCVERN),(15,BLOCK+32),2,COMMAS=YES,FLOAT=$,        X        
               MINUS=YES                                                        
         GOTO1 =A(SENDDOWN),DMCB,(C'T',BLOCK),132                               
*                                                                               
FTR0100  MVC   BLOCK(132),SPACES                                                
         MVC   BLOCK(9),=C'Total Net'                                           
         L     RF,TOTNET                                                        
         EDIT  (RF),(15,BLOCK+32),2,COMMAS=YES,FLOAT=$,MINUS=YES                
         GOTO1 =A(SENDDOWN),DMCB,(C'T',BLOCK),132                               
*                                                                               
         MVC   BLOCK(132),SPACES                                                
         MVC   BLOCK(30),=C'Total Number of Checks Written'                     
         EDIT  (4,TOTCNT),(15,BLOCK+31),COMMAS=YES,ZERO=NOBLANK                 
         GOTO1 =A(SENDDOWN),DMCB,(C'T',BLOCK),132                               
*                                                                               
         GOTO1 (RF),DMCB,(C'T',=C' '),1                                         
*                                                                               
         L     R2,AERITAB          R2 = A(ERROR INVOICE TABLE)                  
         USING ERITABD,R2                                                       
*                                                                               
FTR0110  CLI   0(R2),X'FF'         WHILE NOT END OF TABLE                       
         BE    FTR0120                                                          
*                                                                               
         MVC   BLOCK(132),SPACES                                                
         MVC   BLOCK(6),ERIAGY     PRINT AGY/INV/ERROR DESCRIPTION              
         GOTO1 TINVCON,DMCB,ERIINV,BLOCK+10,DATCON                              
         ZIC   R3,ERITERR                                                       
         GOTO1 ERROUT,DMCB,((R3),BLOCK+20)                                      
         TM    ERITERR,X'80'       IF THIS IS ACTUALLY A WARNING                
         BZ    *+10                                                             
         MVC   BLOCK+58(13),=C'** WARNING **' DISPLAY ADD'L LITERAL             
*                                                                               
         CLI   ERITERR,TAINECP     CHK - W4 MISSING PROVINCE CODE               
         BNE   FTR0115                                                          
         GOTO1 SSNPACK,DMCB,SRTSSN,TGFULL                                       
         MVC   BLOCK+51(4),=C'FOR '                                             
         MVC   BLOCK+55(6),TGFULL                                               
*                                                                               
FTR0115  GOTO1 =A(SENDDOWN),DMCB,(C'T',BLOCK),132                               
*                                                                               
         LA    R2,ERILEN(R2)       BUMP R2 TO NEXT ERROR INVOICE                
         B     FTR0110             AND LOOP BACK                                
*                                                                               
FTR0120  L     RF,NEXTWAR          SET END OF WARNING CHECK TABLE               
         MVI   0(RF),X'FF'                                                      
         L     R2,AWARTAB          R2 = A(ERROR INVOICE TABLE)                  
         USING WARTABD,R2                                                       
*                                                                               
FTR0130  CLI   0(R2),X'FF'         WHILE NOT END OF TABLE                       
         BE    FTR0150                                                          
*                                                                               
         MVC   BLOCK(132),SPACES                                                
         MVC   BLOCK(8),WARCHK     PRINT CHECK NUMBER/ERROR DESCRIPTION         
         ZIC   R3,WARTERR                                                       
         GOTO1 ERROUT,DMCB,((R3),BLOCK+20)                                      
         GOTO1 =A(SENDDOWN),DMCB,(C'T',BLOCK),132                               
*                                                                               
         LA    R2,WARLEN(R2)       BUMP R2 TO NEXT ERROR INVOICE                
         B     FTR0130             AND LOOP BACK                                
*                                                                               
FTR0150  DS    0H                                                               
*                                                                               
         BRAS  RE,EOLDOWN                                                       
         XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
*---------------------------------------------------------------------          
*        MIXED CASE TEXT                                                        
*              P1 = ADDRESS OF DATA                                             
*              P2 = LENGTH                                                      
*---------------------------------------------------------------------          
MIXDCASE NTR1  BASE=*,LABEL=*                                                   
         L     RF,0(R1)            ADDRESS OF TEXT                              
         L     R2,4(R1)            LENGTH OF TEXT                               
         MVI   BYTE,C'U'           UPPERCASE                                    
*                                                                               
MCASE10  CLI   BYTE,C'U'           UPPERCASE?                                   
         BNE   MCASE20             NO                                           
         TM    0(R1),X'80'         DON'T LOWERCASE 2 CHAR WORDS                 
         BZ    MCASE15                                                          
         CH    R2,=H'2'            CHECK ONLY IF MORE THAN 2 CHARS LEFT         
         BNH   MCASE15                                                          
         CLI   2(RF),C'a'          SWITCH TO UPPER CASE IF NOT ALPHA            
         BL    MCASE30                                                          
         CLI   0(RF),C'Z'                                                       
         BH    MCASE30                                                          
*                                                                               
MCASE15  CLI   0(RF),C'a'          SWITCH TO UPPER CASE IF NOT ALPHA            
         BL    MCASE90                                                          
         CLI   0(RF),C'Z'                                                       
         BH    MCASE90                                                          
         MVI   BYTE,C'L'           SWITCH TO LOWERCASE MODE                     
         B     MCASE90             AND BUMP TO NEXT CHAR                        
*                                                                               
MCASE20  CLI   0(RF),C'a'          LOWER THAN LOWER A, SWTICH TO UPPER          
         BL    MCASE30                                                          
         CLI   0(RF),C'A'          HAS TO BE UPPERCASE A-Z                      
         BL    MCASE90                                                          
         CLI   0(RF),C'Z'                                                       
         BH    MCASE30             HIGHER THAN UPPER Z, SWITCH TO UPPER         
         NI    0(RF),X'BF'         REMOVE UPPERCASE BIT                         
         B     MCASE90             AND BUMP TO NEXT CHAR                        
*                                                                               
MCASE30  MVI   BYTE,C'U'           SWITCH TO UPPERCASE MODE                     
*                                                                               
MCASE90  LA    RF,1(RF)            BUMP TO NEXT CHAR                            
         BCT   R2,MCASE10          UNTIL ALL CHARS ARE DONE                     
*                                                                               
MCASEX   XIT1                                                                   
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*---------------------------------------------------------------------          
*        SEND TO DOWNLOAD                                                       
*              P1, B0   =  TYPE TO PASS                                         
*                  B1-3 =  ADDRESS OF DATA                                      
*              P2       =  LENGTH                                               
*---------------------------------------------------------------------          
         USING DLCBD,R4                                                         
SENDDOWN NTR1  BASE=*,LABEL=*                                                   
         LA    R4,DLCB                                                          
*                                                                               
         MVI   DLCBFLX,C' '         FILL FIELD WITH SPACES                      
         MVC   DLCBFLX+1(L'DLCBFLX-1),DLCBFLX                                   
*                                                                               
         MVI   DLCBACT,DLCBPUT      PUT ITEM TO PRINT LINE                      
         MVI   DLCBTYP,DLCBTXT      DATA TYPE IS TEXT                           
         OI    DLCBFLG1,DLCBFXFL+DLCBFXTN                                       
*                                                                               
         L     RE,DMCB+4            POSSIBLE LENGTH                             
         STC   RE,DLCBLEN                                                       
         BCTR  RE,0                 FOR EX MVC                                  
*                                                                               
         L     RF,DMCB              GET A(DATA)                                 
         LA    RF,0(RF)                                                         
*-----------------------------------------------------------                    
         CLI   DMCB,C'T'           TEXT                                         
         BE    *+8                                                              
         CLI   DMCB,C'L'           TEXT, 1ST CHR WORD UP, ELSE LOWER            
         BE    *+8                                                              
         CLI   DMCB,C'Z'           TEXT, 1ST CHR WORD UP, ELSE LOWER            
         BNE   SDOWN10                                                          
         EX    RE,*+8                                                           
         B     *+10                                                             
         CLC   0(0,RF),SPACES       ANY DATA?                                   
         BH    SDOWN03                                                          
         MVI   DLCBLEN,1            NO, GET 1 SPACE THEN                        
         B     SDOWNX                                                           
*                                                                               
SDOWN03  EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   DLCBFLX(0),0(RF)     MOVE DATA                                   
*                                                                               
         LA    RF,DLCBFLX                                                       
         AR    RF,RE               LAST CHARACTER                               
         AHI   RE,1                                                             
SDOWN05  CLI   0(RF),C' '                                                       
         BH    SDOWN08                                                          
*                                                                               
         IC    R5,DLCBLEN          BACK UP 1 CHAR                               
         BCTR  R5,0                                                             
         STC   R5,DLCBLEN                                                       
         BCTR  RF,0                                                             
         BCT   RE,SDOWN05                                                       
         B     SDOWN09                                                          
*                                                                               
SDOWN08  CLI   0(RF),C'"'          CONVERT " TO '                               
         BNE   *+8                                                              
         MVI   0(RF),C''''                                                      
         BCTR  RF,0                                                             
         BCT   RE,SDOWN08                                                       
*                                                                               
         CLI   DMCB,C'L'                                                        
         BE    *+8                                                              
         CLI   DMCB,C'Z'                                                        
         BNE   SDOWNX                                                           
         MVI   WORK,C'U'           SIGNIFY WANT UPPERCASE                       
         LA    RF,DLCBFLX          START OF TEXT                                
         SR    RE,RE               GET LENGTH                                   
         IC    RE,DLCBLEN                                                       
*                                                                               
SDOWN08A CLI   WORK,C'U'           IN UPPERCASE MODE?                           
         BNE   SDOWN08D                                                         
         CLI   0(RF),C' '          SPACE, DON'T CHANGE MODE                     
         BE    SDOWN08B                                                         
*                                                                               
         CLI   DMCB,C'Z'                                                        
         BNE   *+12                                                             
         CLI   2(RF),C' '          COULD BE STATE                               
         BE    SDOWN08B                                                         
*                                                                               
         MVI   WORK,C'L'           OTHERWISE, LOWER CASE NEXT TIME              
SDOWN08B AHI   RF,1                BUMP TO NEXT CHAR                            
         BCT   RE,SDOWN08A                                                      
         B     SDOWNX                                                           
*                                                                               
SDOWN08D CLI   0(RF),C' '                                                       
         BNE   SDOWN08F                                                         
SDOWN08E MVI   WORK,C'U'           UPPER CASE NEXT TIME                         
         B     SDOWN08B                                                         
*                                                                               
SDOWN08F CLI   0(RF),C'a'          LOWER THAN 'a', UPPERCASE NEXT TIME          
         BL    SDOWN08E                                                         
         CLI   0(RF),C'A'          ONLY ALPHA CHANGE TO LOWER                   
         BL    SDOWN08B                                                         
         CLI   0(RF),C'Z'                                                       
         BH    SDOWN08B                                                         
         NI    0(RF),X'BF'         SWITCH TO LOWER                              
         B     SDOWN08B                                                         
*                                                                               
SDOWN09  B     SDOWNX                                                           
*-----------------------------------------------------------                    
SDOWN10  CLI   DMCB,C'N'            BINARY NUMBER                               
         BNE   SDOWN20                                                          
         EDIT  (4,(RF)),(15,DLCBFLX),ALIGN=LEFT,ZERO=NOBLANK                    
         STC   R0,DLCBLEN           SAVE LENGTH                                 
         B     SDOWNX                                                           
*-----------------------------------------------------------                    
SDOWN20  CLI   DMCB,C'A'            BINARY AMOUNT                               
         BE    SDOWN25                                                          
         CLI   DMCB,C'Y'            BINARY AMOUNT FOR YTD                       
         BNE   SDOWN30                                                          
         TM    SRTTRBTS,SRTTTRST   IF CHECK WAS GENERATED BY W4 TRUSTEE         
         BO    SDOWN28                                                          
         TM    SRTBITS,SRTBLIN     IF CHECK WAS GENERATED BY A LIEN             
         BO    SDOWN28                                                          
SDOWN25  EDIT  (4,(RF)),(15,DLCBFLX),2,ALIGN=LEFT,FLOAT=-,ZERO=NOBLANK          
         STC   R0,DLCBLEN           SAVE LENGTH                                 
         B     SDOWNX                                                           
*                                                                               
SDOWN28  MVC   DLCBFLX(3),=C'   '                                               
*        MVC   DLCBFLX(3),=C'.00'  JUST SHOW ZERO                               
         MVI   DLCBLEN,1                                                        
         B     SDOWNX                                                           
*-----------------------------------------------------------                    
SDOWN30  CLI   DMCB,C'D'           DATE                                         
         BNE   SDOWN33                                                          
         MVI   SDOWNCH,C'/'                                                     
         B     SDOWN35                                                          
SDOWN33  CLI   DMCB,C'd'           DATE NO SLASHES                              
         BNE   SDOWN40                                                          
         MVI   SDOWNCH,C' '                                                     
SDOWN35  MVI   DLCBLEN,1                                                        
         OC    0(3,RF),0(RF)       ANY DATE?                                    
         BZ    SDOWNX                                                           
         GOTO1 DATCON,DMCB,(1,(RF)),(23,WORK)                                   
         MVC   DLCBFLX(5),WORK+5                                                
         MVC   DLCBFLX+2(1),SDOWNCH                                             
         MVC   DLCBFLX+5(1),SDOWNCH                                             
         MVC   DLCBFLX+6(4),WORK                                                
         MVI   DLCBLEN,10                                                       
         B     SDOWNX                                                           
*-----------------------------------------------------------                    
SDOWN40  CLI   DMCB,C'I'           INVOICE                                      
         BNE   SDOWN50                                                          
         GOTO1 TINVCON,DMCB,(RF),DLCBFLX,DATCON                                 
         MVI   DLCBLEN,6                                                        
         B     SDOWNX                                                           
*-----------------------------------------------------------                    
SDOWN50  DS    0H                                                               
*                                                                               
SDOWNX   GOTO1 =V(DLFLD),DLCBD                                                  
*                                                                               
SDOWNXX  XIT1                                                                   
         DROP  R4                                                               
SDOWNCH  DS    C'/'                                                             
         LTORG                                                                  
         EJECT                                                                  
*---------------------------------------------------------------------          
*        DOWN CHECK                                                             
*---------------------------------------------------------------------          
DWNCHK   NTR1  BASE=*,LABEL=*                                                   
         TM    SRTBITS2,SRTBEFTL   EFT LIEN?                                    
         JO    XIT                 DON'T PRINT IT                               
         BRAS  RE,CHKHEAD          CHECK HEADER                                 
*                                                                               
         TM    SRTADJS,TAPDADTR+TAPDADTT     TAX REFUND OR TRANSFER             
         BNZ   DWNCHK5                       YES, BLANK OUT UP TO CMMT          
*                                                                               
         BRAS  RE,EMPDETL          EMPLOYEE DETAIL                              
         BRAS  RE,COMDETL          COMMERCIAL DETAIL                            
         BRAS  RE,USEINFO          USE INFORMATION                              
         BRAS  RE,USEDETL          USE DETAIL                                   
*        BRAS  RE,GETCX            GET YTD FROM TACX                            
         BRAS  RE,ERNRECD          EARNINGS RECORD                              
         B     DWNCHK8                                                          
*                                                                               
DWNCHK5  BRAS  RE,BIGBLANK         BLANK OUT ALL THE ABOVE SECTIONS             
*                                                                               
DWNCHK8  BRAS  RE,CMTLINE          COMMENT LINES                                
         BRAS  RE,TAXDEDS          TAX AND DEDUCTIONS                           
         BRAS  RE,CHKTOTL          CHECK TOTAL                                  
         BRAS  RE,PRTDCHK          PRINTED CHECK                                
         XIT1                                                                   
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*---------------------------------------------------------------------          
*        BIG BLANK - BLANK OUT UP TO COMMENTS                                   
*---------------------------------------------------------------------          
BIGBLANK NTR1  BASE=*,LABEL=*                                                   
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C'ED'),2      EMPLOYEE DETAIL           
         LA    R2,29                                                            
BBLNK10  BAS   RE,BLNKOUT                                                       
         BCT   R2,BBLNK10                                                       
         BRAS  RE,EOLDOWN                                                       
*                                                                               
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C'CD'),2      COMMERCIAL DETAIL         
         LA    R2,21                                                            
BBLNK20  BAS   RE,BLNKOUT                                                       
         BCT   R2,BBLNK20                                                       
         BRAS  RE,EOLDOWN                                                       
*                                                                               
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C'UI'),2      EMPLOYEE DETAIL           
         LA    R2,8                                                             
BBLNK30  BAS   RE,BLNKOUT                                                       
         BCT   R2,BBLNK30                                                       
         BRAS  RE,EOLDOWN                                                       
*                                                                               
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C'UD'),2      EMPLOYEE DETAIL           
         LA    R2,4                                                             
BBLNK40  BAS   RE,BLNKOUT                                                       
         BCT   R2,BBLNK40                                                       
         BRAS  RE,EOLDOWN                                                       
*                                                                               
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C'ER'),2      EMPLOYEE DETAIL           
         LA    R2,16                                                            
BBLNK50  BAS   RE,BLNKOUT                                                       
         BCT   R2,BBLNK50                                                       
         BRAS  RE,EOLDOWN                                                       
*                                                                               
BBLNKXIT XIT1                                                                   
         LTORG                                                                  
*                                                                               
BLNKOUT  NTR1                                                                   
         GOTO1 =A(SENDDOWN),DMCB,(C'T',=C' '),1             BLANK               
         B     BBLNKXIT                                                         
         EJECT                                                                  
*---------------------------------------------------------------------          
* THIS ROUTINE ADDS THE AMOUNTS IN THE CHECK TO THE APPROPRIATE ACCTAB          
* ENTRIES.  THE HOB OF PARAMETER ONE IS ZERO IF ADDING DETAIL ENTRIES.          
* IT IS ONE IF MAKING CONVERSION DIFFERENCE POSTINGS.                           
*---------------------------------------------------------------------          
         USING ACCTABD,R5          WILL USE THIS DSECT IN ALL ROUTINES          
         DS    0D                                                               
ADDACC   NMOD1 0,ADDACC                                                         
         L     RC,0(R1)                                                         
*                                                                               
         CLI   0(R1),0             TEST ADDING DETAIL ENTRIES                   
         BNE   A70                                                              
         BAS   RE,ADDEARN          ADD TAX EARN TO SE4200(1,2, OR 3)            
*                                                                               
         BAS   RE,ADDTAX           ADD TAXES TO SXTX(TAX UNIT/TAX TYPE)         
*                                                                               
         BAS   RE,ADDNET           ADD NET TO SC(BANK ACCOUNT)                  
*                                                                               
         BAS   RE,ADDPNH           ADD P&H TO SE42006 AND SXPW(UNION)           
*                                                                               
         BAS   RE,ADDMDED          ADD MISC DED & UNION DUES TO SE11209         
*                                                                               
         BAS   RE,ADDMPPC          ADD MPTRF AND PC TO TXPRD(MP/PC)             
*                                                                               
         BAS   RE,ADDTRST          ADD W4 TRUSTEE DEDUCTION AMT TO              
*                                  SE11212(TP),SE11213(PG),SE11212(PP)          
         BAS   RE,ADDGST           ADD GST TO SE42011 AND SYTXCN G              
*                                                                               
         BAS   RE,ADDAGYCM         ADD P+ AGY COMM TO SE21210                   
*                                                                               
         BAS   RE,ADDDIRCT         ADD DIRECT TO SE21204                        
*                                                                               
         BAS   RE,ADDWIRE          ADD WIRE TO SE21205                          
*                                                                               
         BAS   RE,ADDEFTLN         ADD NET TO SE21208                           
*                                                                               
         BRAS  RE,ADDCANP          ADD CANADIAN TAXES FOR P+                    
         B     A90                                                              
*                                                                               
A70      CLI   0(R1),1             TEST ADDING CONVERSION DIFF. ENTRIES         
         BE    *+6                                                              
         DC    H'0'                UNDEFINED CALL                               
*                                                                               
         BAS   RE,ADDCMDED         ADD CONV. DIFF. FOR MISC. DED & DUES         
*                                                                               
         BAS   RE,ADDCNET          ADD CONV. DIFF. FOR NET AMOUNT               
*                                                                               
A90      GOTO1 DOTRACE,DMCB,(C'Z',AACCTAB),512,=C'ACCTAB'                       
*                                                                               
AXIT     XIT1                                                                   
*                                                                               
*                                                                               
         EJECT                                                                  
* THIS ROUTINE ADDS THE EARNINGS TO THE APPROPRIATE EARNINGS                    
* ACCTAB ENTRY, SE4200(1,2,3 OR 9).                                             
*                                                                               
ADDEARN  NTR1                                                                   
         OC    SRTEARN,SRTEARN     IF TAXABLE EARNINGS NOT ZERO                 
         BZ    AE50                                                             
*                                                                               
         LA    R5,BLOCK            BUILD LOOKUP ENTRY FOR SE4200X               
         MVC   ACCENTRY,BLANKACC                                                
         MVC   ACCKEY(1),SRTTPOF                                                
*                                                                               
         MVI   ACCTYPE,C'9'                                                     
         CLI   RECNUM,CN           IF PROCESSING CAN$ CHECKS THEN SET           
         BE    AE10                    ACCOUNT TYPE TO '9' (SE42009)            
         MVI   ACCTYPE,C'K'                                                     
         CLI   RECNUM,EC           IF PROCESSING EUR$ CHECKS THEN SET           
         BE    AE10                    ACCOUNT TYPE TO 'K' (SE11148)            
*                                                                               
**NO-OP  MVI   ACCTYPE,C'2'        ACCOUNT TYPE '2' (SE42002)                   
**JAN13  CLC   SRTEMP,TGTPEMP      BUT IF EMPLOYER IS TP OR PP                  
**       BE    *+14                                                             
**       CLC   =C'PP ',SRTEMP                                                   
**NO-OP  BNE   AE10                                                             
         MVI   ACCTYPE,C'1'        SET ACCOUNT TYPE TO '1' (SE42001)            
*                                                                               
AE10     GOTO1 ADDAMNT,DMCB,(R5),SRTEARN                                        
*                                                                               
AE50     OC    SRTNTAX,SRTNTAX     IF NON-TAXABLE EARNINGS NOT ZERO             
         BNZ   AE60                                                             
         OC    SRTTXRE,SRTTXRE     TAXABLE REIMB NOT ZERO                       
         BNZ   AE60                                                             
         OC    SRTGST,SRTGST       THERE MAY BE GST                             
         BNZ   AE60                                                             
*        OC    SRTPST,SRTPST       OR THERE MAY BE PST                          
*        BZ    AEX                                                              
*                                                                               
AE60     LA    R5,BLOCK                                                         
         MVC   ACCENTRY,BLANKACC                                                
         MVC   ACCKEY(1),SRTTPOF                                                
*                                                                               
         L     R2,SRTNTAX                                                       
*        CLI   RECNUM,PK                                                        
*        BNE   *+12                                                             
*        CLI   SRTTYPE,TAW4TYCA    IF PERFORMER IS CANADIAN                     
*        BE    *+8                 THEN SRTNTAX ALREADY HAS SRTTXRE             
*        A     R2,SRTTXRE                                                       
         MVI   ACCTYPE,C'9'            ACCOUNT TYPE TO '9' (SE42009)            
         CLI   RECNUM,CN           IF PROCESSING CAN$ CHECKS                    
         BE    AE90                                                             
         CLI   RECNUM,EC           BUT IF EUR CHECKS                            
         BNE   AE80                                                             
         MVI   ACCTYPE,C'K'            ACCOUNT TYPE TO 'K' (SE11148)            
         B     AE90                                                             
*                                                                               
AE80     MVI   ACCTYPE,C'3'        ELSE, BUILD LOOKUP ENTRY FOR SE42003         
         S     R2,SRTGSTPD         ADD IN GST                                   
         A     R2,SRTPST           AND PST                                      
*                                                                               
AE90     ST    R2,FULL                                                          
         GOTO1 ADDAMNT,DMCB,(R5),FULL                                           
*                                                                               
         CLI   RECNUM,PK                                                        
         BNE   AEX                                                              
         OC    SRTTXRE,SRTTXRE                                                  
         BZ    AEX                                                              
*                                                                               
         LA    R5,BLOCK            BUILD LOOKUP ENTRY FOR SE4200X               
         MVC   ACCENTRY,BLANKACC                                                
         MVC   ACCKEY(1),SRTTPOF                                                
         MVI   ACCTYPE,C'2'                                                     
         MVC   FULL,SRTTXRE                                                     
         GOTO1 ADDAMNT,DMCB,(R5),FULL                                           
*                                                                               
AEX      B     AXIT                                                             
         EJECT                                                                  
* THIS ROUTINE ADDS THE TAXES TO THE APPROPRIATE TAX ACCTAB ENTRIES,            
* SXTX(TAX UNIT/TAX TYPE).                                                      
*                                                                               
ADDTAX   NTR1                                                                   
         L     R4,ASRTCHK          R4 = A(FIRST CHECK WITHHLD ELEMENT)          
         MVI   ELCODE,TACWELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   AT100               SKIP IF NONE FOUND                           
         USING TACWD,R4                                                         
*                                                                               
AT10     OC    TACWTAX,TACWTAX     IF TAX TAKEN                                 
         BZ    AT20                                                             
         LA    R5,BLOCK            BUILD LOOKUP ENTRY FOR TYPE T(AX)            
         MVC   ACCENTRY,BLANKACC                                                
         MVI   ACCTYPE,C'4'                                                     
         MVC   ACCKEY(3),TACWUNIT                                               
         MVI   ACCKEY+3,C'T'                                                    
                                                                                
         TM    TACWSTAT,TACWNCRP+TACWNFOR                                       
         BZ    AT15                                                             
         MVI   ACCTYPE,C'P'        NON-RESIDENT CORP OR FOREIGNER               
*                                                                               
         MVC   TGTHREE,SRTSOW                                                   
         CLI   RECNUM,PK                                                        
         BNE   *+10                                                             
         MVC   TGTHREE,TACWUNIT                                                 
*                                                                               
         CLC   =C'PR ',TGTHREE                                                  
         BNE   AT12                                                             
         CLI   RECNUM,PK                                                        
         BNE   *+10                                                             
         MVC   ACCKEY+4(2),=C'P+'                                               
         CLI   RECNUM,PC                                                        
         BNE   *+10                                                             
         MVC   ACCKEY+4(2),=C'PP'                                               
*                                                                               
AT12     CLI   RECNUM,PK                                                        
         BNE   *+20                                                             
         CLC   =C'CN ',TGTHREE                                                  
         BNE   *+10                                                             
         MVC   ACCKEY+4(2),=C'P+'                                               
         CLC   =C'CA ',TGTHREE                                                  
         BNE   AT13                                                             
         MVI   ACCKEY+3,C'C'       NON-RES CORP IN CA                           
         CLI   RECNUM,PK                                                        
         BNE   *+10                                                             
         MVC   ACCKEY+4(2),=C'P+'                                               
         B     AT14                                                             
*&&DO                                                   <---JUL08/13            
         CLC   =C'CA ',SRTSOW                                                   
         BNE   AT13                                                             
*        TM    TACWSTAT,TACWNCRP                                                
*        BZ    *+12                                                             
         MVI   ACCKEY+3,C'C'       NON-RES CORP IN CA                           
         B     AT14                                                             
*&&                                                     <---JUL08/13            
*        TM    TACWSTAT,TACWNFOR   TP wants Foreigner marked as Corp            
*        BZ    AT14                (May 15, 2012)                               
*        MVI   ACCKEY+3,C'F'       NON-RES FOREIGNER IN CA                      
*        B     AT14                                                             
                                                                                
*AT13     CLC   =C'NC ',SRTSOW      NORTH CAROLINA      <---JUL08/13            
AT13     CLC   =C'NC ',TGTHREE     NORTH CAROLINA                               
         BNE   AT18                                                             
         TM    TACWSTAT,TACWNCRP                                                
         BZ    AT18                                                             
         MVI   ACCKEY+3,C'C'       NON-RES CORP IN NC                           
         CLI   RECNUM,PK                                                        
         BNE   *+10                                                             
         MVC   ACCKEY+4(2),=C'P+'                                               
                                                                                
AT14     CLC   =C'PP ',SRTEMP                                                   
         BNE   AT18                                                             
         MVC   ACCKEY+4(2),=C'PP'  PRINT PAYROLL                                
         B     AT18                                                             
                                                                                
AT15     OC    SRTFSO,SRTFSO       TEST PAYING INDIV AS CORP                    
         BZ    AT18                                                             
         CLC   SRTSOW,=C'CA '      TEST WORKED IN CA                            
         BNE   AT18                                                             
         CLC   TACWUNIT,=C'CA '    TEST FOR UNIT CA (IN CASE CN TAX)            
         BNE   AT18                                                             
         MVI   ACCKEY+3,C'C'       SET SPECIAL ACCKEY                           
         CLI   RECNUM,PK                                                        
         BNE   *+10                                                             
         MVC   ACCKEY+4(2),=C'P+'                                               
*                                                                               
AT18     GOTO1 ADDAMNT,DMCB,(R5),TACWTAX                                        
*                                                                               
AT20     CLC   TACWUNIT,=C'CN '    FINISHED IF CANADA                           
         BE    AT90                (WILL POST GST SEPARATELY)                   
*                                                                               
         CLC   TACWUNIT,=C'FD '    ELSE IF FEDERAL                              
         BNE   AT30                                                             
         OC    TACWFICA,TACWFICA   THEN IF FICA TAKEN                           
         BZ    AT90                                                             
         LA    R5,BLOCK            BUILD LOOKUP ENTRY FOR TYPE F(ICA)           
         MVC   ACCENTRY,BLANKACC                                                
         MVI   ACCTYPE,C'4'                                                     
         MVC   ACCKEY(3),TACWUNIT                                               
         MVI   ACCKEY+3,C'F'                                                    
         GOTO1 ADDAMNT,DMCB,(R5),TACWFICA                                       
         B     AT90                FINISHED FEDERAL                             
*                                                                               
AT30     CLI   TACWUNIT+2,C' '     ELSE IF STATE                                
         BH    AT90                                                             
         OC    TACWSDI,TACWSDI     THEN IF SDI TAKEN                            
         BZ    AT35                                                             
         LA    R5,BLOCK            BUILD LOOKUP ENTRY FOR TYPE D(IS)            
         MVC   ACCENTRY,BLANKACC                                                
         MVI   ACCTYPE,C'4'                                                     
         MVC   ACCKEY(3),TACWUNIT                                               
         MVI   ACCKEY+3,C'D'                                                    
*                                                                               
         GOTO1 ADDAMNT,DMCB,(R5),TACWSDI                                        
*                                                                               
AT35     CLI   TACWLEN,TACWLN2Q                                                 
         BL    AT40                                                             
         OC    TACWSFLI,TACWSFLI   THEN IF FLI TAKEN                            
         BZ    AT40                                                             
         LA    R5,BLOCK            BUILD LOOKUP ENTRY FOR TYPE FLI              
         MVC   ACCENTRY,BLANKACC                                                
         MVI   ACCTYPE,C'4'        FAMILY LEAVE INSURANCE                       
         MVC   ACCKEY(3),TACWUNIT                                               
         MVI   ACCKEY+3,C'F'                                                    
*                                                                               
         GOTO1 ADDAMNT,DMCB,(R5),TACWSFLI                                       
*                                                                               
AT40     OC    TACWSUI,TACWSUI     IF SUI TAKEN                                 
         BZ    AT90                                                             
         LA    R5,BLOCK            BUILD LOOKUP ENTRY FOR TYPE U(NEMP)          
         MVC   ACCENTRY,BLANKACC                                                
         MVI   ACCTYPE,C'4'                                                     
         MVC   ACCKEY(3),TACWUNIT                                               
         CLC   ACCKEY(3),=C'MT '   IF MONTANA                                   
         BNE   *+12                                                             
         MVI   ACCKEY+3,C'T'       SET ACCTYPE TO TAX                           
         B     *+8                                                              
         MVI   ACCKEY+3,C'U'       ELSE, SET TO SUI                             
*                                                                               
         GOTO1 ADDAMNT,DMCB,(R5),TACWSUI                                        
*                                                                               
AT90     MVI   ELCODE,TACWELQ      GET NEXT WITHHLD ELEMENT                     
         BRAS  RE,NEXTEL                                                        
         BE    AT10                LOOP BACK                                    
*                                                                               
AT100    TM    TGSYSTAT,TASYSFLT   NEW FLAT TAX RULES?                          
         BZ    *+12                                                             
         CLI   SRTW4TY,TAW4TYCA    IF PERFORMER IS CANADIAN                     
         BE    *+12                                                             
         CLI   SRTW4TY,TAW4TYFO    IF PERFORMER IS FOREIGNER                    
         BNE   ATX                                                              
         OC    SRTFTAX,SRTFTAX     AND FEDERAL TAX WAS TAKEN                    
         BZ    ATX                                                              
*                                                                               
         MVC   AIO,ASRTCHK                                                      
         MVI   ELCODE,TACWELQ                                                   
         GOTO1 GETL,DMCB,(3,=C'FD ')                                            
         JE    ATX                 ALREADY DID IT ABOVE                         
*                                                                               
         LA    R5,BLOCK            BUILD LOOKUP ENTRY FOR TYPE T(AX)            
         MVC   ACCENTRY,BLANKACC                                                
         MVI   ACCTYPE,C'4'                                                     
         MVC   ACCKEY(3),=C'FD '                                                
         MVI   ACCKEY+3,C'T'                                                    
*                                                                               
         GOTO1 ADDAMNT,DMCB,(R5),SRTFTAX                                        
*                                                                               
ATX      B     AXIT                                                             
         EJECT                                                                  
* THIS ROUTINE ADDS THE NET CHECK AMOUNT TO THE BACK ACCOUNT ACCTAB             
* ENTRY, SC(BACK ACCOUNT).                                                      
*                                                                               
ADDNET   NTR1                                                                   
         TM    SRTBITS2,SRTBEFTL   IF EFT LIEN, DON'T ADD IT                    
         JO    AXIT                                                             
         L     R2,SRTNET                                                        
*                                                                               
         CLC   SRTSSN,=C'000112843'   IF AOS CHECK, ADD PST                     
         BE    *+14                                                             
         CLC   SRTSSN,=C'000117358'   IF AOS CHECK, ADD PST                     
         BNE   ADDNET02                                                         
         A     R2,SRTPST                                                        
         ST    R2,SRTNET                                                        
         L     RF,TOTNET                                                        
         A     RF,SRTPST                                                        
         ST    RF,TOTNET                                                        
*                                                                               
ADDNET02 LTR   R2,R2               IF NET CHECK AMOUNT NOT ZERO                 
         BZ    ANX                                                              
         ST    R2,FULL                                                          
*                                                                               
         LA    R5,BLOCK            BUILD LOOKUP ENTRY FOR BANK ACCOUNT          
         MVC   ACCENTRY,BLANKACC       SC(BANK ACCOUNT)                         
         MVI   ACCTYPE,C'5'                                                     
*                                                                               
         L     R4,ASRTCHK          R4 = A(CHECK DETAILS ELEMENT)                
         MVI   ELCODE,TACDELQ                                                   
         BRAS  RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING TACDD,R4                                                         
         CLC   TACDBNK,BANKCODE                                                 
         BE    *+8                                                              
         MVI   ACCTYPE,X'FA'       USE OLD BANKCODE                             
*                                                                               
         GOTO1 ADDAMNT,DMCB,(R5),FULL                                           
*                                                                               
ANX      B     AXIT                                                             
         SPACE 3                                                                
* THIS ROUTINE ADDS THE CONVERSION DIFFERENCE FROM THE NET AMOUNTS              
* TO SPECIAL SK2CDNPR ACCOUNT FOR CAN$ CHECKS.                                  
* OR TO SPECIAL SK2EURPR ACCOUNT FOR EURO CHECKS.                               
*                                                                               
ADDCNET  NTR1                                                                   
*                                  * BUILD APPLICABLE AMOUNT                    
         L     R2,TOTNET           TOTAL (UNCONVERTED) NET                      
         A     R2,TOTDIRCT         + TOTAL DIRECT&WIRE (UNCONVERT) NET          
         A     R2,TOTMDED          + TOTAL(UNCONVERTED) MISC DED & DUES         
**no-op  A     R2,TRUSTCOL         + TOTAL(UNCONVERTED) TRUST                   
         S     R2,CONVDIFF         - CONV DIFF OF MISC DED                      
         A     R2,TOTGSTPD         + TOTAL (UNCONVERTED) GST PAID               
         S     R2,TOTPST                                                        
         S     R2,TOTCVERN         - TOTAL CONVERTED EARNINGS                   
         BZ    ANCX                                                             
*                                                                               
         LA    R5,BLOCK            BUILD LOOKUP ENTRY FOR SK2CDNPR              
         MVC   ACCENTRY,BLANKACC                                                
         MVI   ACCTYPE,C'C'                                                     
*                                                                               
         CLI   RECNUM,EC           IF EURO POST TO SK2EURPR                     
         BNE   *+8                                                              
         MVI   ACCTYPE,C'N'                                                     
*                                                                               
         STCM  R2,15,ACCAMNT                                                    
*                                                                               
         GOTO1 ADDAMNT,DMCB,(R5),ACCAMNT                                        
*                                                                               
ANCX     B     AXIT                                                             
         EJECT                                                                  
* THIS ROUTINE ADDS THE P&H AMOUNT TO THE P&H ACCTAB ENTRIES, SE42005           
* AND SXPW(UNION).                                                              
*                                                                               
ADDPNH   NTR1                                                                   
         OC    SRTPNH,SRTPNH       IF P&H AMOUNT TAKEN                          
         BZ    APX                                                              
*                                                                               
         CLI   TGUSEQU,USDR        SKIP SOAP RES. DIRECTORS                     
         BE    APX                                                              
         CLI   TGUSEQU,USDH                                                     
         BE    APX                                                              
         CLI   TGUSEQU,USWR        SKIP SOAP RES. WRITERS                       
         BE    APX                                                              
         CLI   TGUSEQU,USWH                                                     
         BE    APX                                                              
*                                                                               
         LA    R5,BLOCK            BUILD LOOKUP ENTRY FOR SE42005               
         MVC   ACCENTRY,BLANKACC                                                
         MVI   ACCTYPE,C'6'                                                     
         MVC   ACCKEY(1),SRTTPOF                                                
*                                                                               
         GOTO1 ADDAMNT,DMCB,(R5),SRTPNH                                         
*                                                                               
         LA    R5,BLOCK            BUILD LOOKUP ENTRY FOR SXPW(UNION)           
         MVC   ACCENTRY,BLANKACC                                                
         MVI   ACCTYPE,C'7'                                                     
         MVC   ACCKEY(3),SRTUN                                                  
         MVC   ACCKEY+3(3),SRTLOCL                                              
*                                                                               
         GOTO1 ADDAMNT,DMCB,(R5),SRTPNH                                         
*                                                                               
APX      B     AXIT                                                             
         EJECT                                                                  
* THIS ROUTINE ADDS THE MISCELLANEOUS DEDUCTIONS AND UNION DUES                 
* TO THE SE11209 ACCOUNT.                                                       
*                                                                               
ADDMDED  NTR1                                                                   
         L     R2,SRTMDED          IF MISC DED AMOUNT                           
         A     R2,SRTDUES          OR UNION DUES NOT ZERO                       
         LTR   R2,R2                                                            
         BZ    AMX                                                              
*                                                                               
         LA    R5,BLOCK            BUILD LOOKUP ENTRY FOR SE11209               
         MVC   ACCENTRY,BLANKACC                                                
         MVI   ACCTYPE,C'8'                                                     
         MVC   ACCKEY(1),SRTTPOF                                                
         STCM  R2,15,ACCAMNT                                                    
*                                                                               
         GOTO1 ADDAMNT,DMCB,(R5),ACCAMNT                                        
*                                                                               
AMX      B     AXIT                                                             
         SPACE 3                                                                
* THIS ROUTINE ADDS THE CONVERSION DIFFERENCE FOR MISCELLANEOUS                 
* DEDUCTIONS AND UNION DUES TO SPECIAL SK2CDNM ACCOUNT FOR CAN$ CHECKS.         
*                                                                               
ADDCMDED NTR1                                                                   
         XC    CONVDIFF,CONVDIFF   CLEAR SAVED AMOUNT FOR ADDCNET               
*                                                                               
         OC    TOTMDED,TOTMDED     IF TOTAL MISC DED & DUES NOT ZERO            
         BZ    AMCX                                                             
*                                                                               
         LA    R5,BLOCK            BUILD LOOKUP ENTRY FOR SK2CDNM               
         MVC   ACCENTRY,BLANKACC                                                
         MVI   ACCTYPE,C'D'                                                     
*                                                                               
         GOTO1 ADDAMNT,DMCB,(R5),TOTMDED                                        
*                                                                               
         MVC   CONVDIFF,ACCAMNT    SAVE AMOUNT JUST POSTED FOR ADDCNET          
*                                                                               
AMCX     B     AXIT                                                             
         EJECT                                                                  
* THIS ROUTINE ADDS THE MPTRF AND PERMANENT CHARITIES DEDUCTIONS TO             
* THE TXPRD MP AND TXPRD PC ACCOUNTS, RESPECTIVELY.                             
*                                                                               
ADDMPPC  NTR1                                                                   
         OC    SRTMPT,SRTMPT       IF MPTRF AMOUNT NOT ZERO                     
         BZ    ADDMP10                                                          
*                                                                               
         LA    R5,BLOCK            BUILD LOOKUP ENTRY FOR BANK ACCOUNT          
         MVC   ACCENTRY,BLANKACC       TXPRD MP                                 
         MVI   ACCTYPE,C'4'                                                     
         MVC   ACCKEY(6),=C'PRD MP'                                             
*                                                                               
         GOTO1 ADDAMNT,DMCB,(R5),SRTMPT                                         
*                                                                               
ADDMP10  OC    SRTCHAR,SRTCHAR     IF PERMANENT CHARITIES AMT NON-ZERO          
         BZ    ADDMPX                                                           
*                                                                               
         LA    R5,BLOCK            BUILD LOOKUP ENTRY FOR BANK ACCOUNT          
         MVC   ACCENTRY,BLANKACC       TXPRD PC                                 
         MVI   ACCTYPE,C'4'                                                     
         MVC   ACCKEY(6),=C'PRD PC'                                             
*                                                                               
         GOTO1 ADDAMNT,DMCB,(R5),SRTCHAR                                        
*                                                                               
ADDMPX   B     AXIT                                                             
         SPACE 2                                                                
* THIS ROUTINE ADDS THE W4 TRUSTEE DEDUCTION AMT TO SE11212(TP) OR              
* SE11213(PG), OR SE11212 (PP) ACCOUNT                                          
*                                                                               
ADDTRST  NTR1                                                                   
         TM    SRTADJS,TAPDADTR    IF REFUND CHECK                              
         BZ    ADDTRST2                                                         
         OC    SRTNET,SRTNET       GOING TO TRUSTEE                             
         BNZ   ADDTRST1                                                         
         CLI   RECNUM,EC                                                        
         BNE   ADDTRSTX            XIT ON MINOR CHECK                           
ADDTRST1 MVC   AIO,ASRTCHK                                                      
         MVI   ELCODE,TAODELQ                                                   
         GOTO1 GETL,DMCB,(1,=AL1(TAODTYPT))                                     
         MVC   AIO,AIO1                                                         
         BNE   ADDTRSTX                                                         
         L     R4,TGELEM           GET AMOUNT OF REFUND                         
         USING TAODD,R4                                                         
         ICM   R2,15,TAODAMT                                                    
         B     ADDTRST8                                                         
*                                                                               
ADDTRST2 OC    SRTTRST,SRTTRST     IF AMOUNT ZERO                               
         BZ    ADDTRSTX            EXIT                                         
         L     R2,SRTTRST                                                       
*                                                                               
         MVC   AIO,ASRTCHK         AND AMOUNT NOT SENT TO A TRUSTEE             
         MVI   ELCODE,TANUELQ                                                   
         GOTO1 GETL,DMCB,(1,=AL1(TANUTRST))                                     
         MVC   AIO,AIO1                                                         
         BE    ADDTRSTX                                                         
*                                                                               
ADDTRST8 LA    R5,BLOCK            BUILD LOOKUP ENTRY FOR BANK ACCOUNT          
         MVC   ACCENTRY,BLANKACC                                                
**NO-OP  MVI   ACCTYPE,C'I'        ACCOUNT TYPE I SE11213                       
**JAN13  CLC   SRTEMP,TGTPEMP      BUT IF EMPLOYER IS TP OR PP                  
**       BE    *+14                                                             
**       CLC   =C'PP ',SRTEMP                                                   
**NO-OP  BNE   *+8                                                              
         MVI   ACCTYPE,C'J'        SET ACCOUNT TYPE TO J SE11212                
         CLI   RECNUM,EC                                                        
         BNE   *+8                                                              
         MVI   ACCTYPE,C'M'        SET ACCOUNT TYPE TO M SE11215                
         STCM  R2,15,ACCAMNT                                                    
         GOTO1 ADDAMNT,DMCB,(R5),ACCAMNT                                        
*                                                                               
ADDTRSTX B     AXIT                                                             
         EJECT                                                                  
* THIS ROUTINE ADDS THE GOODS AND SERVICES TAX AMOUNT TO THE US$ GST            
* PAID ACCOUNT WHENEVER IT IS DUE.  IN ADDITION, IT ADDS IT TO THE CAN$         
* EXPENSE PAYABLE ACCOUNT WHEN GST IS DUE BUT NOT PAID TO THE PERFORMER         
* BECAUSE THERE IS NO GST# IS ON THE FILE.                                      
*                                                                               
ADDGST   NTR1                                                                   
         CLI   RECNUM,CN           FOR CANADIAN CHECKS                          
         BNE   ADDGSX                                                           
         OC    SRTGST,SRTGST       IF GST AMOUNT PRESENT                        
         BNZ   ADDGS10                                                          
         OC    SRTPST,SRTPST       OR PST AMOUNT                                
         BZ    ADDGSX                                                           
ADDGS10  L     R2,SRTGST           SET TO POST COMPLEMENTED AMOUNT              
         LCR   R2,R2                                                            
         LA    R5,BLOCK            BUILD LOOKUP ENTRY FOR GST PAID              
         MVC   ACCENTRY,BLANKACC                                                
         MVI   ACCTYPE,C'B'                                                     
         CLC   SRTGST,SRTGSTPD     IF GST AMOUNT NOT PAID TO PERF.              
         BE    *+8                                                              
         MVI   ACCKEY,C'Y'         SET TO POST TO DIFFERENT C/A                 
         A     R2,SRTPST           ADD IN PST AMOUNT                            
         STCM  R2,15,ACCAMNT                                                    
         GOTO1 ADDAMNT,DMCB,(R5),ACCAMNT                                        
*                                                                               
         CLC   SRTGST,SRTGSTPD     IF GST AMOUNT NOT PAID TO PERF.              
         BE    ADDGSX                                                           
         LA    R5,BLOCK            BUILD LOOKUP ENTRY FOR TYPE G(ST)            
         MVC   ACCENTRY,BLANKACC   PAYABLE TO CANADIAN GOVERNMENT               
         MVI   ACCTYPE,C'4'                                                     
         MVC   ACCKEY(3),=C'CN '                                                
         MVI   ACCKEY+3,C'G'                                                    
         A     R2,SRTPST           ADD IN PST AMOUNT                            
         STCM  R2,15,ACCAMNT                                                    
         GOTO1 ADDAMNT,DMCB,(R5),ACCAMNT                                        
*                                                                               
ADDGSX   B     AXIT                                                             
         EJECT                                                                  
* THIS ROUTINE ADDS THE DIRECT DEPOSIT AMOUNT TO THE                            
* SE21204 ACCOUNT                                                               
*                                                                               
ADDDIRCT NTR1                                                                   
         OC    SRTDIRCT,SRTDIRCT   IF DIRECT DEPOSIT AMOUNT                     
         BZ    ADDDX                                                            
*                                                                               
         LA    R5,BLOCK            BUILD LOOKUP ENTRY FOR SE21204               
         MVC   ACCENTRY,BLANKACC                                                
         MVI   ACCTYPE,C'G'                                                     
*                                                                               
         GOTO1 ADDAMNT,DMCB,(R5),SRTDIRCT                                       
*                                                                               
ADDDX    B     AXIT                                                             
                                                                                
* THIS ROUTINE ADDS THE WIRE TRANSFER AMOUNT TO THE                             
* SE21205 OR SE21207 ACCOUNT                                                    
*                                                                               
ADDWIRE  NTR1                                                                   
         OC    SRTWIRE,SRTWIRE     IF WIRE TRANSFER AMOUNT                      
         JNZ   ADDW10                                                           
         TM    SRTADJS,TAPDADVD                                                 
         JZ    AXIT                                                             
         TM    SRTBITS,SRTBWIRE    IF PERF'S CHKS ARE WIRED                     
         JZ    AXIT                                                             
*                                                                               
ADDW10   LA    R5,BLOCK            BUILD LOOKUP ENTRY FOR SE21205               
         MVC   ACCENTRY,BLANKACC                                                
         MVI   ACCTYPE,C'H'                                                     
*                                                                               
         CLI   RECNUM,EC           POST TO SE21207 IF EURO CHECKS               
         BNE   *+8                                                              
         MVI   ACCTYPE,C'L'                                                     
*                                                                               
         GOTO1 ADDAMNT,DMCB,(R5),SRTWIRE                                        
*                                                                               
ADDWX    J     AXIT                                                             
                                                                                
* THIS ROUTINE ADDS THE EFT LIEN AMOUNT TO THE                                  
* SE21208 ACCOUNT                                                               
*                                                                               
ADDEFTLN NTR1                                                                   
         TM    SRTBITS2,SRTBEFTL   IF EFT LIEN                                  
         JZ    AXIT                                                             
*                                                                               
         LA    R5,BLOCK            BUILD LOOKUP ENTRY FOR SE21208               
         MVC   ACCENTRY,BLANKACC                                                
         MVI   ACCTYPE,C'O'                                                     
*                                                                               
         GOTO1 ADDAMNT,DMCB,(R5),SRTNET                                         
*                                                                               
         J     AXIT                                                             
         EJECT                                                                  
* THIS ROUTINE ADDS THE P+ AGY COMMISSION TO THE                                
* SE21210 ACCOUNT                                                               
*                                                                               
ADDAGYCM NTR1                                                                   
         OC    SRTPPLA,SRTPPLA     IF P+ AGY COMMISSION?                        
         JZ    AXIT                                                             
*                                                                               
         LA    R5,BLOCK            BUILD LOOKUP ENTRY FOR SE21208               
         MVC   ACCENTRY,BLANKACC                                                
         MVI   ACCTYPE,C'V'                                                     
*                                                                               
         ICM   RE,15,SRTPPLA                                                    
         LCR   RE,RE                                                            
         ST    RE,WORK                                                          
         GOTO1 ADDAMNT,DMCB,(R5),WORK                                           
*                                                                               
         J     AXIT                                                             
         EJECT                                                                  
*                                                                               
* THIS ROUTINE ADDS CANADIAN TAXES FOR P+                                       
*                                                                               
ADDCANP  NTR1                                                                   
         CLI   RECNUM,PK                                                        
         JNE   ADDCANPX                                                         
         CLI   SRTW4TY,TAW4TYCA    OR CANADIAN                                  
         JNE   ADDCANPX                                                         
*                                                                               
         USING TAATD,R4                                                         
         L     R4,ASRTCHK                                                       
         MVI   ELCODE,TAATELQ                                                   
         BRAS  RE,GETEL                                                         
         J     *+8                                                              
ACANP10  BRAS  RE,NEXTEL                                                        
         JNE   ADDCANPX                                                         
*                                                                               
         CLC   TAATUNIT,=C'CN '                                                 
         JNE   ACANP20                                                          
*                                                                               
         LA    R5,BLOCK            CANADA                                       
         MVC   ACCENTRY,BLANKACC                                                
         MVI   ACCTYPE,C'Q'                                                     
         MVC   ACCKEY(3),=C'FD '                                                
         MVC   ACCAMNT,TAATTAX     TAXES                                        
         GOTO1 ADDAMNT,DMCB,(R5),ACCAMNT                                        
         J     ACANP10                                                          
*                                                                               
ACANP20  LA    R5,BLOCK            PROVINCES                                    
         MVC   ACCENTRY,BLANKACC                                                
         MVI   ACCTYPE,C'R'                                                     
         MVC   ACCKEY(3),=C'PR '                                                
         CLC   TAATUNIT,=C'QC '                                                 
         JNE   *+10                                                             
         MVC   ACCKEY(3),TAATUNIT                                               
         MVC   ACCAMNT,TAATTAX     TAXES                                        
         GOTO1 ADDAMNT,DMCB,(R5),ACCAMNT                                        
*                                                                               
         LA    R5,BLOCK                                                         
         MVC   ACCENTRY,BLANKACC                                                
         MVI   ACCTYPE,C'S'                                                     
         MVC   ACCKEY(3),=C'PR '                                                
         CLC   TAATUNIT,=C'QC '                                                 
         JNE   *+10                                                             
         MVC   ACCKEY(3),TAATUNIT                                               
         MVC   ACCAMNT,TAATPP      PENSION PLAN                                 
         GOTO1 ADDAMNT,DMCB,(R5),ACCAMNT                                        
*                                                                               
         LA    R5,BLOCK                                                         
         MVC   ACCENTRY,BLANKACC                                                
         MVI   ACCTYPE,C'T'                                                     
         MVC   ACCKEY(3),=C'PR '                                                
         CLC   TAATUNIT,=C'QC '                                                 
         JNE   *+10                                                             
         MVC   ACCKEY(3),TAATUNIT                                               
         MVC   ACCAMNT,TAATEI      EMP INSURANCE PREMIUM                        
         GOTO1 ADDAMNT,DMCB,(R5),ACCAMNT                                        
*                                                                               
         CLC   TAATUNIT,=C'QC '    QUEBEC?                                      
         JNE   ACANP10                                                          
         LA    R5,BLOCK                                                         
         MVC   ACCENTRY,BLANKACC                                                
         MVI   ACCTYPE,C'U'                                                     
         MVC   ACCKEY(3),TAATUNIT                                               
         MVC   ACCAMNT,TAATPIP     PARENTIAL INSURANCE PLAN                     
         GOTO1 ADDAMNT,DMCB,(R5),ACCAMNT                                        
         J     ACANP10                                                          
*                                                                               
ADDCANPX J     AXIT                                                             
         EJECT                                                                  
* THIS ROUTINE LOOKS UP THE ACCTAB FOR THE ENTRY CORRESPONDING TO THE           
* KEY POINTED TO BY PARAMETER 1.  IF IT DOESN'T FIND IT, IT ADDS THE            
* NEW ENTRY TO ACCTAB.  IT THEN ADDS AMOUNT POINTED TO BY PARAMETER 2           
* TO THAT ENTRY.                                                                
*                                                                               
ADDAMNT  NTR1                                                                   
         L     R5,0(R1)            R5 = A(ACCTAB LOOKUP ENTRY)                  
         L     R2,4(R1)            R2 = A(AMOUNT TO POST)                       
         L     R2,0(R2)            R2 = AMOUNT TO POST                          
*                                                                               
AA10     CLI   ACCTYPE,C'4'        IF POSTING TAXES                             
         BNE   AA20                                                             
         CLC   SRTEMP,TGTPEMP      AND EMPLOYER IS NOT TALENT PARTNERS          
         BE    AA20                                                             
         CLC   =C'PP ',SRTEMP      AND EMPLOYER IS NOT PP                       
         BE    AA15                                                             
         CLC   =C'RR ',SRTEMP      AND EMPLOYER IS NOT RR                       
         BE    AA15                                                             
         CLC   =C'P+ ',SRTEMP      AND EMPLOYER IS NOT P+                       
         BE    AA15                                                             
         MVC   ACCKEY(3),SRTEMP    THEN FORCE UNIT TO BE EMPLOYER               
         MVI   ACCKEY+3,C'N'       AND TYPE IS ALWAYS THE SAME                  
         B     AA20                                                             
*                                                                               
AA15     MVC   ACCKEY+4(2),SRTEMP  SET EMPLOYER IN ACCKEY FOR PP                
*                                                                               
AA20     BAS   RE,CONVERT          COMPUTE CONV. DIFF. IF NECESSARY             
*                                                                               
         STCM  R2,15,ACCAMNT       STORE AMOUNT IN ACCTAB ENTRY                 
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'Z',(R5)),ACCLEN,=C'ACCTAB'                       
*                                                                               
         TM    SRTINVS2,TAINSFRC   IF FORCING LAST YEAR                         
         BZ    *+8                                                              
         NI    ACCTYPE,X'BF'       TURN OF X'40' BIT IN POSTING TYPE            
*                                                                               
         GOTO1 ADDBIN,DMCB,AACCTAB,(R5)   ADD ENTRY TO TABLE                    
*                                                                               
         OI    ACCTYPE,X'40'       INSURE X'40' BIT IS ON                       
*                                                                               
         OC    CONVDIFF,CONVDIFF   IS THERE A CONVERSION DIFF. TO POST          
         BZ    AAX                                                              
         MVC   HALF(1),ACCTYPE     SAVE TYPE                                    
         MVC   HALF+1(1),ACCKEY    AND FIRST BYTE OF KEY                        
         MVC   ACCENTRY,BLANKACC   BUILD NEW ENTRY                              
*                                                                               
         MVI   ACCTYPE,C'C'        SET CONVERSION TYPE C                        
         CLI   HALF,C'B'           IF THIS IS GST POSTING                       
         BNE   AA40                                                             
         CLI   HALF+1,C'Y'         AND POSTING GOING TO CANADA                  
         BNE   *+8                                                              
         MVI   ACCTYPE,C'F'        SET CONVERSION TYPE F                        
         B     AA60                                                             
*                                                                               
AA40     CLI   HALF,C'6'           IF THIS IS NOT P&H POSTING                   
         BE    *+6                                                              
         DC    H'0'                INTERNAL BUG                                 
*                                                                               
         MVI   ACCTYPE,C'E'        ELSE SET CONVERSION TYPE E                   
         MVC   ACCKEY(3),SRTUN     AND SET UNION                                
*                                                                               
AA60     L     R2,CONVDIFF         SET R2=CONVERSION DIFFERENCE                 
         B     AA10                GO BACK AND START AGAIN                      
*                                                                               
AAX      B     AXIT                                                             
         EJECT                                                                  
* THIS ROUTINE CONVERTS AMOUNT IN R2 TO US$ IF RUNNING CAN$ CHECKS AND          
* OPTIONALLY RETURNS CONVERTED AMOUNT IN R2 AND CONVERSION DIFFERENCE           
* IN CONVDIFF.                                                                  
*                                                                               
CONVERT  NTR1                                                                   
         XC    CONVDIFF,CONVDIFF   CLEAR CONVERSION DIFFERENCE                  
*                                                                               
         CLI   RECNUM,CN           IF THIS IS CANADIAN $ CHECK RUN              
         BE    CVT05                                                            
         CLI   RECNUM,EC           OR THIS IS EUROPEAN CHECK RUN                
         BNE   CVTX                                                             
*                                                                               
CVT05    CLI   ACCTYPE,C'9'        IF POSTING EARNINGS                          
         BE    CVT10                                                            
         CLI   ACCTYPE,C'B'        OR GST SE42 POSTING                          
         BE    CVT10                                                            
         CLI   ACCTYPE,C'6'        OR P&H SE POSTING                            
         BE    CVT10                                                            
         CLI   ACCTYPE,C'D'        OR MISC DED CONVERSION DIFFERENCE            
         BE    CVT10                                                            
         CLI   ACCTYPE,C'K'        OR EURO CASH ACCOUNT                         
         BNE   CVTX                                                             
*                                                                               
CVT10    CVD   R2,DUB              CONVERT AMOUNT TO PACKED                     
         MP    DUB,SRTCCVT         MULTIPLY BY CONVERSION RATE                  
         SRP   DUB,60,5            DIVIDE BY 10,000 AND ROUND                   
         CVB   R0,DUB              NOW HAVE CONVERTED AMOUNT                    
*                                                                               
         CLI   ACCTYPE,C'9'        IF THIS IS EARNINGS                          
         BE    CVT13                                                            
         CLI   ACCTYPE,C'K'                                                     
         BNE   CVT15                                                            
CVT13    L     R3,TOTCVERN         ADD TO TOTAL CONVERTED EARNINGS              
         AR    R3,R0                                                            
         ST    R3,TOTCVERN                                                      
         B     CVT20               GO RETURN CONVERTED AMOUNT                   
*                                                                               
CVT15    LR    R1,R2               ELSE COMPUTE CONVERSION DIFFERENCE           
         SR    R1,R0                                                            
*                                                                               
         CLI   ACCTYPE,C'D'        IF THIS IS CONV DIFF FOR MISC DED            
         BNE   *+10                                                             
         LR    R2,R1               RETURN DIFF. TO CALLING RTN. TO              
         B     CVTX                   POST NOW                                  
*                                                                               
         ST    R1,CONVDIFF         ELSE SAVE DIFF. FOR NEXT TIME IN             
*                                                                               
CVT20    LR    R2,R0               RETURN CONVERTED AMOUNT TO POST NOW          
*                                                                               
CVTX     XIT1  REGS=(R2)                                                        
         DROP  R5                                                               
         EJECT                                                                  
*              CONSTANTS, ETC. FOR ADDACC ROUTINES                              
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
* THIS ROUTINE LOOPS THROUGH ACCTAB AND POSTS A WORKER FILE RECORD FOR          
* FOR EACH ENTRY.                                                               
*                                                                               
         DS    0D                                                               
POSTALL  NMOD1 0,POSTALL                                                        
         L     RC,0(R1)                                                         
*                                                                               
         CLI   REQURG,C'U'         URGENT CHECK RUN?                            
         BNE   PA05                                                             
         XC    POSTREC,POSTREC                                                  
         GOTOR CALLWRK,DMCB,=C'OPEN    '                                        
*                                                                               
PA05     L     R2,AACCTAB          R2 = A(ACCTAB PARAMETER BLOCK)               
         USING BIND,R2                                                          
         OC    BININ,BININ         RETURN IF NO ACCTAB ENTRIES                  
         BZ    PAX                                                              
*                                                                               
         LA    R5,BINTABLE         R5 = A(FIRST ACCTAB ENTRY)                   
         USING ACCTABD,R5                                                       
         L     R2,BININ            R2 = NUMBER OF ACCTAB ENTRIES                
*                                                                               
PA10     MVC   POSTDATE,CHKDATE1   SET POSTING DATE TO REG. CHECK DATE          
         MVC   POSTREF,CHKDATEA+4  SET LAST 4 CHARS OF REFERENCE                
         MVC   POSTMOS,MOS         MONTH OF SERVICE                             
*                                                                               
         TM    ACCTYPE,X'40'       IF X'40' BIT IS OFF                          
         BO    PA14                                                             
         MVC   POSTDATE,CHKDATEL   SET POSTING DATE TO LAST YEAR                
         MVC   POSTREF,CHKDTEAL+4  SAME GOES FOR REFERENCE                      
         MVC   POSTMOS,MOSL        AND FOR MONTH OF SERVICE                     
         OI    ACCTYPE,X'40'       TURN ON X'40' BIT                            
*                                                                               
PA14     CLI   ACCTYPE,C'2'                                                     
         BNE   *+14                                                             
         MVC   ACCOUNT,=CL14'SE42013'                                           
         B     PA19                                                             
         CLI   ACCTYPE,C'K'        IF ACCOUNT TYPE K                            
         BE    PA17                                                             
         CLI   ACCTYPE,C'1'        IF ACCOUNT TYPE 1,2,3 OR 9                   
         BL    PA20                                                             
         CLI   ACCTYPE,C'9'                                                     
         BE    PA17                                                             
         CLI   ACCTYPE,C'3'                                                     
         BH    PA20                                                             
*                                  THEN DEBIT ACCOUNT SE4200(1,2,3,9)           
PA17     MVC   ACCOUNT,=CL14'SE4200'                                            
         CLI   RECNUM,EC                                                        
         BNE   PA18                                                             
         MVC   ACCOUNT,=CL14'SE42003'                                           
         B     *+10                                                             
*                                                                               
PA18     MVC   ACCOUNT+6(1),ACCTYPE                                             
PA19     MVC   CONTRA,=CL14'SX'                                                 
         MVC   CONTRA+2(1),ACCKEY                                               
         MVC   TPOFFICE,ACCKEY                                                  
         MVI   DRCRFLAG,C'D'                                                    
         B     PA999                                                            
*                                                                               
PA20     CLI   ACCTYPE,C'4'        ELSE IF ACCOUNT TYPE 4                       
         BE    PA21                                                             
         CLI   ACCTYPE,C'P'        ELSE IF ACCOUNT TYPE P                       
         BNE   PA30                                                             
*                                                                               
PA21     MVI   TPOFFICE,C' '                                                    
         MVI   DRCRFLAG,C'C'       THEN DEFAULT TO CREDIT ACCOUNT               
         MVC   ACCOUNT,=CL14'SXTX' ACCOUNT SXTX(UNIT/TYP)                       
         MVC   ACCOUNT+4(6),ACCKEY                                              
         MVC   CONTRA,=CL14'SE42004'                                            
*                                                                               
         CLI   RECNUM,PK           IF P+ CHECKS                                 
         BNE   PA22                                                             
*                                                                               
         L     RE,AACHTAB                                                       
PA21A    CLI   0(RE),X'FF'                                                      
         BE    PA30                                                             
         CLC   ACCKEY(4),0(RE)     MATCH ON UNIT/TYPE                           
         BNE   PA21B                                                            
         MVC   CONTRA,ACCOUNT      SET ACCOUNT TO CONTRA                        
         MVC   ACCOUNT,4(RE)       AND SET NEW POSTING ACCOUNT                  
         B     PA30                                                             
PA21B    AHI   RE,ACHLEN           ELSE, BUMP TO NEXT ENTRY IN TABLE            
         B     PA21A               LOOP FOR NEXT                                
*                                                                               
PA22     CLI   RECNUM,CN           IF CAN$ CHECKS                               
         BNE   PA23                                                             
         MVI   ACCOUNT+1,C'Y'      SET CAN$ EXPENSE LEDGER                      
         MVC   CONTRA,=CL14'SE42011'   AND DIFFERENT CONTRA ACCOUNT             
         B     PA999                                                            
*                                                                               
PA23     CLI   ACCKEY+3,C'N'       IF NOT TP/PP                                 
         BE    PA999               POST OLD STYLE                               
         L     RE,AACHTAB          ELSE, CHECK APPROPRIATE TABLE                
         CLC   ACCKEY+4(2),=C'PP'  TO SEE IF TAX UNIT GOES VIA WIRE             
         BE    PA24                                                             
         L     RE,AACHTAB                                                       
PA24     CLI   ACCTYPE,C'P'                                                     
         BNE   PA25                                                             
         LA    RE,NRESTAB          NRESTAB EMP DON'T MATTER                     
PA25     CLI   0(RE),X'FF'         TEST END OF TABLE                            
         BE    PA999               (POST OLD STYLE)                             
         CLC   ACCKEY(4),0(RE)     MATCH ON UNIT/TYPE                           
         BNE   PA27                                                             
         MVC   CONTRA,ACCOUNT      SET ACCOUNT TO CONTRA                        
         MVC   ACCOUNT,4(RE)       AND SET NEW POSTING ACCOUNT                  
         B     PA999                                                            
PA27     AHI   RE,ACHLEN           ELSE, BUMP TO NEXT ENTRY IN TABLE            
         B     PA25                LOOP FOR NEXT                                
*                                                                               
PA30     CLI   ACCTYPE,C'5'        ELSE IF ACCOUNT TYPE 5                       
         BE    *+8                                                              
         CLI   ACCTYPE,X'FA'       ELSE IF ACCOUNT TYPE FA                      
         BNE   PA40                                                             
*                                  THEN CREDIT ACCOUNT SC(BANK ACCOUNT)         
         MVC   ACCOUNT,=CL14'SC'                                                
         MVC   ACCOUNT+2(6),BANKCODE                                            
         CLI   ACCTYPE,C'5'                                                     
         BE    PA31                                                             
         MVC   ACCOUNT+2(6),=CL6'7004'   OLD BANK ACCTS                         
         CLI   RECNUM,CN           CAN$ CHECKS USE 1004                         
         BNE   PA31                                                             
         MVC   ACCOUNT+2(6),=CL6'1004'                                          
*                                                                               
PA31     MVC   CONTRA,=CL14'SE42'                                               
         MVI   TPOFFICE,C' '                                                    
         MVI   DRCRFLAG,C'C'                                                    
         B     PA999                                                            
*                                                                               
PA40     CLI   ACCTYPE,C'6'        ELSE IF ACCOUNT TYPE 6                       
         BNE   PA50                                                             
*                                  THEN DEBIT ACCOUNT SE42005                   
         MVC   ACCOUNT,=CL14'SE42005'                                           
         MVC   CONTRA,=CL14'SXPW'                                               
         CLI   RECNUM,CN                                                        
         BNE   *+8                                                              
         MVI   CONTRA+1,C'Y'                                                    
         MVC   TPOFFICE,ACCKEY                                                  
         MVI   DRCRFLAG,C'D'                                                    
         B     PA999                                                            
*                                                                               
PA50     CLI   ACCTYPE,C'7'        ELSE IF ACCOUNT TYPE 7                       
         BNE   PA60                                                             
*                                  THEN CREDIT ACCOUNT SXPW(UNION)              
         MVC   ACCOUNT,=CL14'SXPW'                                              
         CLI   RECNUM,CN                                                        
         BNE   *+8                                                              
         MVI   ACCOUNT+1,C'Y'                                                   
         MVC   ACCOUNT+4(3),ACCKEY                                              
         MVC   CONTRA,SPACES                                                    
         MVC   CONTRA+2(6),ACCKEY                                               
         MVC   CONTRA+9(3),=C'P&&H'                                             
         MVI   TPOFFICE,C' '                                                    
         MVI   DRCRFLAG,C'C'                                                    
         B     PA999                                                            
*                                                                               
PA60     CLI   ACCTYPE,C'8'        ELSE IF ACCOUNT TYPE 8                       
         BNE   PA65                                                             
*                                  THEN CREDIT ACCOUNT SE11209                  
         MVC   ACCOUNT,=CL14'SE11209'                                           
         MVC   CONTRA,=CL14'SE11209'                                            
         CLI   RECNUM,CN           DIFFERENT ACCOUNT FOR CAN$ CHECKS            
         BNE   *+16                                                             
         MVC   ACCOUNT,=CL14'SE11210'                                           
         MVC   CONTRA,=CL14'SE11210'                                            
         CLI   RECNUM,EC           DIFFERENT ACCOUNT FOR EURO CHECKS            
         BNE   *+16                                                             
         MVC   ACCOUNT,=CL14'SE11216'                                           
         MVC   CONTRA,=CL14'SE11216'                                            
         MVC   TPOFFICE,ACCKEY                                                  
         MVI   DRCRFLAG,C'C'                                                    
         B     PA999                                                            
*                                                                               
PA65     CLI   ACCTYPE,C'B'        ELSE IF ACCOUNT TYPE B                       
         BNE   PA70                                                             
*                                  THEN DEBIT ACCOUNT SE42011                   
         MVC   ACCOUNT,=CL14'SE42011'                                           
         MVC   CONTRA,=CL14'SC'        SET CONTRA TO CASH                       
         MVC   CONTRA+2(6),BANKCODE                                             
         CLI   ACCKEY,C'Y'             UNLESS WILL BE PAID TO CANADA            
         BNE   *+10                                                             
         MVC   CONTRA,=CL14'SYTXCN G'  SO CONTRA IS PAYABLE                     
         MVI   TPOFFICE,C' '                                                    
         MVI   DRCRFLAG,C'D'                                                    
         B     PA999                                                            
*                                                                               
PA70     CLI   ACCTYPE,C'C'        ELSE IF ACCOUNT TYPE C                       
         BE    PA73                                                             
         CLI   ACCTYPE,C'N'        OR IF ACCOUNT TYPE N                         
         BE    PA75                                                             
         BNE   PA80                                                             
*                                  THEN DEBIT ACCOUNT SK2CDNPR                  
PA73     MVC   ACCOUNT,=CL14'SK2CDNPR'                                          
         B     PA78                                                             
PA75     MVC   ACCOUNT,=CL14'SK2EURPR'                                          
PA78     MVC   CONTRA,=CL14'SC'                                                 
         MVC   CONTRA+2(6),BANKCODE                                             
PA79     MVI   TPOFFICE,C' '                                                    
         MVI   DRCRFLAG,C'D'                                                    
         B     PA999                                                            
*                                                                               
PA80     CLI   ACCTYPE,C'D'        ELSE IF ACCOUNT TYPE D                       
         BNE   PA90                                                             
*                                  THEN DEBIT ACCOUNT SK2CDNM                   
         MVC   ACCOUNT,=CL14'SK2CDNM'                                           
         MVC   CONTRA,=CL14'SE11210'                                            
         CLI   RECNUM,EC           DIFFERENT ACCOUNT FOR EURO CHECKS            
         BNE   *+16                                                             
         MVC   ACCOUNT,=CL14'SE11217'                                           
         MVC   CONTRA,=CL14'SE11217'                                            
         MVI   TPOFFICE,C' '                                                    
         MVI   DRCRFLAG,C'D'                                                    
         B     PA999                                                            
*                                                                               
PA90     CLI   ACCTYPE,C'E'        ELSE IF ACCOUNT TYPE E                       
         BNE   PA100                                                            
*                                  THEN DEBIT ACCOUNT SK2CDNPW                  
         MVC   ACCOUNT,=CL14'SK2CDNPW'                                          
         MVC   CONTRA,=CL14'SYPW'                                               
         MVC   CONTRA+4(3),ACCKEY                                               
         MVI   TPOFFICE,C' '                                                    
         MVI   DRCRFLAG,C'D'                                                    
         B     PA999                                                            
*                                                                               
PA100    CLI   ACCTYPE,C'F'        ELSE IF ACCOUNT TYPE F                       
         BNE   PA110                                                            
*                                  THEN DEBIT ACCOUNT SK2CDNGST                 
         MVC   ACCOUNT,=CL14'SK2CDNGST'                                         
         MVC   CONTRA,=CL14'SE42011'                                            
         MVI   TPOFFICE,C' '                                                    
         MVI   DRCRFLAG,C'D'                                                    
         B     PA999                                                            
*                                                                               
PA110    CLI   ACCTYPE,C'G'        ELSE IF ACCOUNT TYPE G                       
         BNE   PA120                                                            
*                                  THEN DEBIT ACCOUNT SE21204                   
         MVC   ACCOUNT,=CL14'SE21204'                                           
         MVC   CONTRA,=CL14'SE42002'                                            
         MVI   TPOFFICE,C' '                                                    
         MVI   DRCRFLAG,C'C'                                                    
         B     PA999                                                            
*                                                                               
PA120    CLI   ACCTYPE,C'H'        ELSE IF ACCOUNT TYPE H                       
         BNE   PA130                                                            
*                                  THEN DEBIT ACCOUNT SE21205                   
         MVC   ACCOUNT,=CL14'SE21205'                                           
         MVC   CONTRA,=CL14'SE42003'                                            
         MVI   TPOFFICE,C' '                                                    
         MVI   DRCRFLAG,C'C'                                                    
         B     PA999                                                            
*                                                                               
PA130    CLI   ACCTYPE,C'I'        ELSE IF ACCOUNT TYPE I                       
         BNE   PA140                                                            
         MVC   ACCOUNT,=CL14'SE11213'  THEN DEBIT ACCOUNT SE11213               
         MVC   CONTRA,=CL14'SE11213'                                            
         MVI   TPOFFICE,C' '                                                    
         MVI   DRCRFLAG,C'C'                                                    
         B     PA999                                                            
*                                                                               
PA140    CLI   ACCTYPE,C'J'        ELSE IF ACCOUNT TYPE J                       
         BNE   PA150                                                            
         MVC   ACCOUNT,=CL14'SE11212' THEN DEBIT ACCOUNT SE11212                
         MVC   CONTRA,=CL14'SE11212'                                            
         MVI   TPOFFICE,C' '                                                    
         MVI   DRCRFLAG,C'C'                                                    
         B     PA999                                                            
*                                                                               
PA150    CLI   ACCTYPE,C'K'        ELSE IF ACCOUNT TYPE K                       
         BNE   PA160                                                            
         MVC   ACCOUNT,=CL14'SE11212' THEN DEBIT ACCOUNT SE11212                
         MVC   CONTRA,=CL14'SE11212'                                            
         MVI   TPOFFICE,C' '                                                    
         MVI   DRCRFLAG,C'C'                                                    
         B     PA999                                                            
*                                                                               
PA160    CLI   ACCTYPE,C'L'        ELSE IF ACCOUNT TYPE L                       
         BNE   PA170                                                            
         MVC   ACCOUNT,=CL14'SE21207' THEN DEBIT ACCOUNT SE21207                
         MVC   CONTRA,=CL14'SE42003'                                            
         MVI   TPOFFICE,C' '                                                    
         MVI   DRCRFLAG,C'C'                                                    
         B     PA999                                                            
*                                                                               
PA170    CLI   ACCTYPE,C'M'        ELSE IF ACCOUNT TYPE M                       
         BNE   PA180                                                            
         MVC   ACCOUNT,=CL14'SE11215' THEN DEBIT ACCOUNT SE11215                
         MVC   CONTRA,=CL14'SE11215'                                            
         MVI   TPOFFICE,C' '                                                    
         MVI   DRCRFLAG,C'C'                                                    
         B     PA999                                                            
*                                                                               
PA180    CLI   ACCTYPE,C'O'        ELSE IF ACCOUNT TYPE O (EFT LIEN)            
         BNE   PA190                                                            
         MVC   ACCOUNT,=CL14'SE21208' THEN CREDIT ACCOUNT SE21208               
         MVC   CONTRA,=CL14'SE42001'                                            
         MVI   TPOFFICE,C' '                                                    
         MVI   DRCRFLAG,C'C'                                                    
         B     PA999                                                            
*                                                                               
PA190    CLI   ACCTYPE,C'V'        ELSE IF ACCOUNT TYPE V (P+ AGY COM)          
         BNE   PA200                                                            
         MVC   ACCOUNT,=CL14'SE21210' THEN CREDIT ACCOUNT SE21210               
         MVC   CONTRA,=CL14'SE42'                                               
         MVI   TPOFFICE,C' '                                                    
         MVI   DRCRFLAG,C'C'                                                    
         B     PA999                                                            
*                                                                               
PA200    CLI   ACCTYPE,C'Q'        ELSE IF ACCOUNT TYPE Q (CN TAXES)            
         BNE   PA210                                                            
         MVI   TPOFFICE,C' '                                                    
         MVC   ACCOUNT,=CL14'SXTXCN FD'                                         
         MVC   CONTRA,=CL14'SE42004'                                            
         B     PA999                                                            
*                                                                               
PA210    CLI   ACCTYPE,C'R'        ELSE IF ACCOUNT TYPE R (CN PROV TAX)         
         BNE   PA220                                                            
         MVI   TPOFFICE,C' '                                                    
         MVC   CONTRA,=CL14'SE42004'                                            
         MVC   ACCOUNT,=CL14'SXTXCN PR'                                         
         CLC   ACCKEY(3),=C'QC '                                                
         BNE   PA999                                                            
         MVC   ACCOUNT,=CL14'SXTXCN QU'                                         
         B     PA999                                                            
*                                                                               
PA220    CLI   ACCTYPE,C'S'        ELSE IF ACCOUNT TYPE S (CN PROV PP)          
         BNE   PA230                                                            
         MVI   TPOFFICE,C' '                                                    
         MVC   CONTRA,=CL14'SE42004'                                            
         MVC   ACCOUNT,=CL14'SXTXCN CPP'                                        
         CLC   ACCKEY(3),=C'QC '                                                
         BNE   PA999                                                            
         MVC   ACCOUNT,=CL14'SXTXCN QPP'                                        
         B     PA999                                                            
*                                                                               
PA230    CLI   ACCTYPE,C'T'        ELSE IF ACCOUNT TYPE T (CN PROV EI)          
         BNE   PA240                                                            
         MVI   TPOFFICE,C' '                                                    
         MVC   CONTRA,=CL14'SE42004'                                            
         MVC   ACCOUNT,=CL14'SXTXCN EI'                                         
         CLC   ACCKEY(3),=C'QC '                                                
         BNE   PA999                                                            
         MVC   ACCOUNT,=CL14'SXTXCN QEI'                                        
         B     PA999                                                            
*                                                                               
PA240    CLI   ACCTYPE,C'U'        ELSE IF ACCOUNT TYPE U (CN PROV PIP)         
         BNE   PA300                                                            
         MVI   TPOFFICE,C' '                                                    
         MVC   CONTRA,=CL14'SE42004'                                            
         MVC   ACCOUNT,=CL14'SXTXCN QPIP'                                       
         B     PA999                                                            
*                                                                               
PA300    DS    0H                                                               
*                                                                               
PA999    BAS   RE,POST             POST ACCOUNT TO WORKER FILE                  
*                                                                               
         LA    R5,ACCLEN(R5)       BUMP TO NEXT ACCTAB ENTRY                    
         BCT   R2,PA10             REPEAT UNTIL NO MORE ENTRIES                 
*                                                                               
PAX      BAS   RE,POSTEND          ADD TRAILER RECORD AND CLOSE FILE            
*                                                                               
PXIT     XIT1                                                                   
         EJECT                                                                  
* THIS ROUTINE BUILDS A WORKER FILE RECORD USING ACCOUNT, CONTRA,               
* DRCRFLAG, ACCAMNT, AND TPOFFICE FROM POSTALL AND POSTS THE                    
* RECORD TO THE WORKER FILE.                                                    
*                                                                               
POST     NTR1                                                                   
         XC    POSTREC,POSTREC     PRE-CLEAR WORKER RECORD                      
*                                                                               
         LA    R4,POSTREC+4        R4 = A(HEADER ELEMENT)                       
         USING PSHEADD,R4                                                       
*                                  BUILD HEADER ELEMENT                         
         MVC   PSHDEL(2),=X'5046'                                               
         MVC   PSHDACC(1),TGACCHX                                               
         MVC   PSHDACC+1(14),ACCOUNT                                            
         MVC   PSHDANAL,SPACES                                                  
         MVC   PSHDSBAC(1),TGACCHX                                              
         MVC   PSHDSBAC+1(14),CONTRA                                            
*                                                                               
         CLI   CONTRA,C' '         IF CONTRA ACCOUNT STARTS WITH ' '            
         BNE   *+8                                                              
         MVI   PSHDSBAC,C' '       THEN SET COMPANY CODE TO ' '                 
*                                                                               
         LA    R4,70(R4)           BUMP R4 TO TRANSACTION ELEMENT               
         USING TRANSD,R4                                                        
*                                  BUILD TRANSACTION ELEMENT                    
         MVI   TRNSEL,X'44'                                                     
         MVI   TRNSLEN,TRNSLNQ                                                  
         MVC   TRNSDATE,POSTDATE                                                
         MVC   TRNSREF(2),=C'CK'                                                
         MVC   TRNSREF+2(4),POSTREF                                             
         MVI   TRNSTYPE,3                                                       
         MVC   TRNSBTCH(2),POSTMOS                                              
         CLI   DRCRFLAG,C'D'                                                    
         BNE   *+8                                                              
         OI    TRNSSTAT,X'80'                                                   
         ICM   RF,15,ACCAMNT                                                    
         CVD   RF,DUB                                                           
         ZAP   TRNSAMNT,DUB                                                     
         MVC   TRNSANAL(1),TPOFFICE                                             
         MVI   TRNSANAL+1,C' '                                                  
*                                                                               
         CLI   TRNSANAL,C' '       IF NO OFFICE CODE                            
         BNE   PO10                                                             
         MVI   TRNSANAL,TPOFF8     SET OFFICE CODE TO 8                         
         CLI   RECNUM,PC           IF PRINT CHECKS                              
         BE    *+8                                                              
         CLI   RECNUM,EC                                                        
         BNE   *+8                                                              
         MVI   TRNSANAL,TPOFFH     SET OFFICE CODE TO H                         
         CLI   RECNUM,PK           IF PAYROLL SERVICES                          
         BNE   *+8                                                              
         MVI   TRNSANAL,C'V'                                                    
*                                                                               
PO10     CLI   DRCRFLAG,C'D'       IF DEBIT AMOUNT                              
         BNE   PO20                                                             
         AP    TOTCASH,TRNSAMNT    THEN ADD AMOUNT TO TOTAL CASH                
         B     PO30                                                             
*                                                                               
PO20     AP    TOTCRDTS,TRNSAMNT   ADD AMOUNT TO TOTAL CREDITS                  
*                                                                               
PO30     LA    R4,TRNSLNQ(R4)      BUMP R4 TO END OF RECORD                     
*                                                                               
         LA    RF,POSTREC          SAVE RECORD LENGTH IN RECORD HEADER          
         LA    R4,1(R4)                                                         
         SR    R4,RF                                                            
         STCM  R4,3,POSTREC                                                     
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'Z',POSTREC),128,=C'POSTING'                      
*                                                                               
*                                  ADD RECORD TO WORKER FILE                    
         GOTOR CALLWRK,DMCB,=C'ADD'                                             
*                                                                               
         AP    TOTRECS,=P'1'       INCREMENT TOTAL NUMBER OF RECORDS            
*                                                                               
POX      B     PXIT                                                             
         DROP  R4,R5                                                            
         EJECT                                                                  
* THIS ROUTINE ADDS A TRAILER RECORD TO THE WORKER FILE AND CLOSES IT.          
*                                                                               
POSTEND  NTR1                                                                   
         XC    POSTREC,POSTREC     BUILD TRAILER RECORD                         
*                                                                               
         LA    R4,POSTREC+4        BUILD TRAILER ELEMENT                        
         USING PSSUBFD,R4                                                       
         MVC   PSSBEL(2),=X'521D'                                               
         MVC   PSSBDESC(15),=C'TALENT PARTNERS'                                 
         ZAP   PSSBRECS,TOTRECS                                                 
         ZAP   PSSBCASH,TOTCASH                                                 
*                                                                               
         LA    R4,29(R4)           BUMP TO END OF RECORD                        
*                                                                               
         LA    RF,POSTREC          SAVE RECORD LENGTH IN RECORD HEADER          
         LA    R4,1(R4)                                                         
         SR    R4,RF                                                            
         STCM  R4,3,POSTREC                                                     
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'Z',POSTREC),128,=C'TRAILER REC'                  
*                                                                               
*                                  ADD TRAILER RECORD TO WORKER FILE            
         GOTOR CALLWRK,DMCB,=C'ADD'                                             
*                                                                               
*                                                                               
         CP    TOTCASH,TOTCRDTS    CHECK IF DR=CR                               
         BE    POE9                                                             
*                                                                               
         L     RF,AMASTD           IF 'POSTING=N' CONTROL CARD OPTION           
         USING MASTD,RF                                                         
         CLI   MCPOSTNG,C'N'                                                    
         BE    POE9                THEN DON'T BOTHER NOTIFYING                  
*                                                                               
         GOTO1 =A(SENDMAIL),DMCB,0                                              
*                                                                               
*                                  CLOSE WORKER FILE                            
POE9     GOTOR CALLWRK,DMCB,=C'CLOSE'                                           
         TM    PRGSTAT,FQASYS      FQA?                                         
         BO    *+14                                                             
         CP    TOTCASH,TOTCRDTS    CHECK IF DR=CR                               
         BE    PXIT                                                             
         GOTOR CALLWRK,DMCB,=C'KEEP'     MARK FILE ON KEEP                      
*                                                                               
POEX     B     PXIT                                                             
*                                                                               
         DROP  R4,RF                                                            
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
*              NEW POSTINGS FOR CERTAIN UNIT/TYPE                               
*              FOR ACCTYPE=P ONLY, EMPLOYER DOESN'T MATTER                      
NRESTAB  DS    0CL18                                                            
*        DC    CL4'CA F',CL14'SE21314'                                          
*        DC    CL4'CA T',CL14'SE21314'                                          
         DC    CL4'NC C',CL14'SE21314'                                          
         DC    X'FF'                                                            
         SPACE 2                                                                
*              NEW POSTINGS FOR CERTAIN UNIT/TYPE                               
*              FOR EMPLOYER PP ACCTYPE=4 ONLY                                   
NPP4TAB  DS    0CL18                                                            
         DC    CL4'FD F',CL14'SE21302'                                          
         DC    CL4'FD T',CL14'SE21303'                                          
         DC    CL4'CA T',CL14'SE21304'                                          
         DC    CL4'CA D',CL14'SE21308'                                          
         DC    CL4'CO T',CL14'SE21304'                                          
         DC    CL4'CT T',CL14'SE21308'                                          
*        DC    CL4'HI D',CL14'SE21308'                                          
         DC    CL4'HI T',CL14'SE21304'                                          
         DC    CL4'ID T',CL14'SE21304'                                          
         DC    CL4'IL T',CL14'SE21304'                                          
         DC    CL4'IN T',CL14'SE21304'                                          
         DC    CL4'MA T',CL14'SE21304'                                          
         DC    CL4'MS T',CL14'SE21304'                                          
         DC    CL4'NC T',CL14'SE21304'                                          
         DC    CL4'NJ D',CL14'SE21308'                                          
         DC    CL4'NJ F',CL14'SE21308'                                          
         DC    CL4'NJ T',CL14'SE21304'                                          
         DC    CL4'NJ U',CL14'SE21307'                                          
         DC    CL4'NY T',CL14'SE21304'                                          
         DC    CL4'NYCT',CL14'SE21305'                                          
         DC    CL4'PA T',CL14'SE21304'                                          
         DC    CL4'OR T',CL14'SE21304'                                          
         DC    CL4'RI T',CL14'SE21304'                                          
         DC    CL4'WV T',CL14'SE21304'                                          
         DC    X'FF'                                                            
         SPACE 2                                                                
*              NEW POSTINGS FOR CERTAIN UNIT/TYPE                               
*              FOR EMPLOYER TP ACCTYPE=4 ONLY                                   
NTP4TAB  DS    0CL18                                                            
         DC    CL4'FD F',CL14'SE21302'                                          
         DC    CL4'FD T',CL14'SE21303'                                          
         DC    CL4'AL T',CL14'SE21304'                                          
         DC    CL4'AR T',CL14'SE21304'                                          
         DC    CL4'AZ T',CL14'SE21304'                                          
         DC    CL4'CA D',CL14'SE21308'                                          
         DC    CL4'CA T',CL14'SE21304'                                          
         DC    CL4'CO T',CL14'SE21304'                                          
         DC    CL4'CT T',CL14'SE21304'                                          
         DC    CL4'DC T',CL14'SE21304'                                          
         DC    CL4'GA T',CL14'SE21304'                                          
*        DC    CL4'HI D',CL14'SE21308'                                          
         DC    CL4'HI T',CL14'SE21304'                                          
         DC    CL4'ID T',CL14'SE21304'                                          
         DC    CL4'IL T',CL14'SE21304'                                          
         DC    CL4'IN T',CL14'SE21304'                                          
         DC    CL4'KS T',CL14'SE21304'                                          
         DC    CL4'LA T',CL14'SE21304'                                          
         DC    CL4'MA T',CL14'SE21304'                                          
         DC    CL4'MD T',CL14'SE21304'                                          
         DC    CL4'ME T',CL14'SE21304'                                          
         DC    CL4'MI T',CL14'SE21304'                                          
         DC    CL4'MN T',CL14'SE21304'                                          
         DC    CL4'MS T',CL14'SE21304'                                          
         DC    CL4'MT T',CL14'SE21304'                                          
         DC    CL4'NC T',CL14'SE21304'                                          
         DC    CL4'NE T',CL14'SE21304'                                          
         DC    CL4'NJ D',CL14'SE21308'                                          
         DC    CL4'NJ F',CL14'SE21308'                                          
         DC    CL4'NJ T',CL14'SE21304'                                          
         DC    CL4'NJ U',CL14'SE21307'                                          
         DC    CL4'NM T',CL14'SE21304'                                          
         DC    CL4'NY T',CL14'SE21304'                                          
         DC    CL4'NYCT',CL14'SE21305'                                          
         DC    CL4'OK T',CL14'SE21304'                                          
         DC    CL4'OR T',CL14'SE21304'                                          
         DC    CL4'PA T',CL14'SE21304'                                          
         DC    CL4'PA U',CL14'SE21307'                                          
         DC    CL4'PHLT',CL14'SE21305'                                          
         DC    CL4'PR T',CL14'SE21304'                                          
         DC    CL4'RI T',CL14'SE21304'                                          
         DC    CL4'SC T',CL14'SE21304'                                          
         DC    CL4'UT T',CL14'SE21304'                                          
         DC    CL4'VA T',CL14'SE21304'                                          
         DC    CL4'VT T',CL14'SE21304'                                          
         DC    CL4'WI T',CL14'SE21304'                                          
         DC    X'FF'                                                            
         SPACE 2                                                                
*              NEW POSTINGS FOR CERTAIN UNIT/TYPE                               
*              FOR EMPLOYER P+ ACCTYPE=4 ONLY                                   
NPK4TAB  DS    0CL18                                                            
         DC    CL4'AL T',CL14'SE21304'                                          
         DC    CL4'FD F',CL14'SE21302'                                          
         DC    CL4'FD T',CL14'SE21303'                                          
         DC    CL4'CA T',CL14'SE21304'                                          
         DC    CL4'CA D',CL14'SE21308'                                          
         DC    CL4'IL T',CL14'SE21304'                                          
         DC    CL4'IN T',CL14'SE21304'                                          
         DC    CL4'HI T',CL14'SE21304'                                          
         DC    CL4'KS T',CL14'SE21304'                                          
         DC    CL4'MA T',CL14'SE21304'                                          
         DC    CL4'MI T',CL14'SE21304'                                          
         DC    CL4'MN T',CL14'SE21304'                                          
         DC    CL4'NJ T',CL14'SE21304'                                          
         DC    CL4'NJ D',CL14'SE21308'                                          
         DC    CL4'NJ F',CL14'SE21308'                                          
         DC    CL4'NJ U',CL14'SE21307'                                          
         DC    CL4'NY T',CL14'SE21304'                                          
         DC    CL4'NYCT',CL14'SE21305'                                          
         DC    CL4'PA T',CL14'SE21304'                                          
         DC    CL4'OR T',CL14'SE21304'                                          
         DC    CL4'VA T',CL14'SE21304'                                          
         DC    CL4'VT T',CL14'SE21304'                                          
         DC    CL4'WI T',CL14'SE21304'                                          
         DC    X'FF'                                                            
         SPACE 2                                                                
         EJECT                                                                  
* MISCELLANEOUS CODE                                                            
*                                                                               
*                                                                               
CALLWRK  NTR1  BASE=*,LABEL=*                                                   
         L     RF,AMASTD           IF 'POSTING=N' CONTROL CARD OPTION           
         USING MASTD,RF                                                         
         CLI   MCPOSTNG,C'N'                                                    
         JE    PXIT                THEN DON'T POST TO WORKER                    
         DROP  RF                                                               
*                                  ELSE CALL WORKER - PARM1 ALREADY SET         
         GOTO1 WORKER,DMCB,,POSTBUFF,WORKID,POSTREC                             
         J     PXIT                                                             
*                                                                               
POSTBUFF DS    XL4500              FOR WORKER'S INTERNAL USE                    
         LTORG                                                                  
         EJECT                                                                  
*---------------------------------------------------------------------          
* E-MAIL ERROR REPORT                                                           
*---------------------------------------------------------------------          
SENDMAIL NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         MVC   BYTE,0(R1)          0=WORKER FILE, X'80' = P+ AGY COMM           
*                                                                               
         TM    BYTE,X'80'+X'40'+X'20'+X'10'                                     
         BNZ   SM01                                                             
         GOTO1 SETESYS,DMCB,SUBJDSCS                                            
*                                                                               
SM01     TM    BYTE,X'80'          P+ AGY COMM CHANGED                          
         BZ    SM02                                                             
         MVC   SBJDSCCK,CHKNCHR                                                 
         EDIT  SRTPPLA,(10,SBJDSCAM),2                                          
         B     SM15                                                             
                                                                                
SM02     TM    BYTE,X'40'          P+ CANADIAN: ALL TAXES NOT TAKEN             
         BZ    SM02A                                                            
         MVC   SBJDSC3K,CHKNCHR                                                 
         MVC   SBJDSC3S,SPACES                                                  
         GOTO1 SETESYS,DMCB,SBJDSC3S                                            
         B     SM15                                                             
                                                                                
SM02A    TM    BYTE,X'20'          ADVANCE RECONCILIATION:NOT ALL TAKEN         
         BZ    SM02B                                                            
         MVC   SBJDSC4K,CHKNCHR                                                 
         MVC   SBJDSC4S,SPACES                                                  
         GOTO1 SETESYS,DMCB,SBJDSC4S                                            
*                                                                               
         MVC   AIO,ASRTCHK                                                      
         MVI   ELCODE,TANUELQ                                                   
         GOTO1 GETL,DMCB,(1,=AL1(TANUTARE))                                     
         BNE   SM15                                                             
*                                                                               
         L     R4,TGELEM                                                        
         USING TANUD,R4                                                         
         EDIT  (4,TANUOVAM),(10,SBJDSC4D),2,COMMAS=YES,FLOAT=$,        *        
               MINUS=YES                                                        
         B     SM15                                                             
*                                                                               
SM02B    TM    BYTE,X'10'          TACDNET > TAPDGRS + TAPDGUAR                 
         BZ    SM03                                                             
*        MVC   SBJDSC5K,CHKNCHR                                                 
         MVC   SBJDSC5K,SPACES                                                  
         USING TACDD,R4                                                         
         L     R4,ASRTCHK                                                       
         MVI   ELCODE,TACDELQ                                                   
         BRAS  RE,GETEL                                                         
         OC    TACDCHK,TACDCHK                                                  
         BZ    *+10                                                             
         MVC   SBJDSC5K,TACDCHK                                                 
         MVC   SBJDSC5S,SPACES                                                  
         GOTO1 SETESYS,DMCB,SBJDSC5S                                            
         B     SM15                                                             
         DROP  R4                                                               
                                                                                
         USING UKRECD,RF                                                        
SM03     LA    RF,WORKID           NOTIFY TALENT PROGRAMMERS                    
*                                                                               
         EDIT  (B2,UKUSRID),(5,OOBUSID),0,LEFT,FILL=0                           
*                                                                               
         MVC   FILENAME,=CL8'CTFILE' SET TO READ CONTROL FILE                   
         MVI   USEIO,C'Y'                                                       
         XC    KEY,KEY                                                          
         LA    R5,KEY                                                           
         USING CTIREC,R5                                                        
         MVI   CTIKTYP,C'I'        BUILD KEY FOR ID RECORD                      
         MVC   CTIKNUM,UKUSRID                                                  
         GOTO1 HIGH                                                             
*                                                                               
         L     R5,AIO                                                           
         LA    R4,CTIDATA                                                       
SM05     CLI   0(R4),0                                                          
         BE    SM10                                                             
         CLI   0(R4),CTDSCELQ                                                   
         BE    SM08                                                             
         ZIC   R0,1(R4)                                                         
         AR    R4,R0                                                            
         B     SM05                                                             
         DROP  R5                                                               
*                                                                               
SM08     MVC   OOBUSID(5),2(R4)                                                 
*                                                                               
         USING UKRECD,RF                                                        
SM10     LA    RF,WORKID           NOTIFY TALENT PROGRAMMERS                    
         MVC   OOBSYSP(4),UKSYSPRG                                              
         CLI   OOBSYSP+3,0                                                      
         BNE   *+8                                                              
         MVI   OOBSYSP+3,C'*'                                                   
*                                                                               
         MVC   FULL(1),UKDAY                                                    
         MVI   FULL+1,X'0C'                                                     
         UNPK  OOBDAYN(3),FULL(2)                                               
         MVC   OOBCLASS(1),UKCLASS                                              
         EDIT  (B2,UKFILNO),(4,OOBSEQN),0,LEFT,FILL=0                           
         EDIT  TOTCASH,(10,BODYL5A1),2                                          
         EDIT  TOTCRDTS,(10,BODYL5A2),2                                         
*                                                                               
SM15     GOTOR =V(SMTP),DMCB,('SMTPAINI',JESMAIL)                               
         LA    R4,TOWHO            DEFAULT TALENT TEAM                          
         LA    R3,SUBJDSC                                                       
*                                                                               
         TM    BYTE,X'40'          P+ CANADIAN: NOT ALL TAXES TAKEN             
         BZ    SM16                                                             
         LA    R3,SUBJDSC3                                                      
         LA    R4,TOWHO3                                                        
         B     SM19                                                             
*                                                                               
SM16     TM    BYTE,X'20'          ADVANCE RECONCILIATION:NOT ALL TAKEN         
         BZ    SM17                                                             
         LA    R3,SUBJDSC4                                                      
         LA    R4,TOWHO3                                                        
         B     SM19                                                             
*                                                                               
SM17     TM    BYTE,X'10'          GUARANTEE WARNING                            
         BZ    SM18                                                             
         LA    R3,SUBJDSC5                                                      
         LA    R4,TOWHO3                                                        
*                                                                               
SM18     L     RF,AMASTD                                                        
         USING MASTD,RF                                                         
         TM    PRGSTAT,TESTSYS+CSCSYS+FQASYS   TEST FILE?                       
         BNZ   *+12                                                             
         CLI   MCWRITE,C'Y'        IF WRITE=NO                                  
         BE    *+8                                                              
         LA    R4,TOWHO2           ONLY WARN PROGRAMMERS                        
SM19     GOTOR =V(SMTP),DMCB,('SMTPAPRS',(R4)),(L'SUBJDSC,(R3))                 
*                                                                               
         TM    BYTE,X'10'          GUARANTEE WARNING?                           
         BO    SM25                                                             
         TM    BYTE,X'40'+X'20'                                                 
         BNZ   SM90                                                             
*                                                                               
         MVC   BODYERR(L'OOBUSID),OOBUSID     MOVE IN ERROR MSG                 
         LA    RE,BODYERR                                                       
SM20     CLI   0(RE),C' '          FOUND A SPACE                                
         BNH   SM30                                                             
         AHI   RE,1                                                             
         B     SM20                                                             
*                                                                               
SM25     CLC   SBJDSC5K,SPACES                                                  
         BNE   SM90                                                             
         MVC   BOD5SSN,SRTSSN      GUARANTEE WARNING                            
         MVC   BOD5AGY,SRTAGY                                                   
         GOTO1 TINVCON,DMCB,SRTINV,BOD5INV,DATCON                               
SM26     GOTOR =V(SMTP),DMCB,('SMTPAPTL',BOD5LIN1)                              
         B     SM90                                                             
*                                                                               
SM30     MVI   0(RE),C','                                                       
         AHI   RE,1                                                             
         MVC   0(OOBLENQ,RE),OOBSYSP                                            
*                                                                               
         GOTOR =V(SMTP),DMCB,('SMTPAPTL',BODYLIN1)                              
         GOTOR (RF),DMCB,('SMTPAPTL',BODYLIN2)                                  
         GOTOR (RF),DMCB,('SMTPAPTL',BODYLIN3)                                  
         GOTOR (RF),DMCB,('SMTPAPTL',BODYLIN4)                                  
         GOTOR (RF),DMCB,('SMTPAPTL',BODYLIN5)                                  
*                                                                               
SM90     GOTOR (RF),DMCB,('SMTPASND',0)                                         
         GOTOR (RF),DMCB,('SMTPAEND',0) DETACH SMTP                             
*                                                                               
         XC    FILENAME,FILENAME                                                
SENDMX   XIT1                                                                   
*                                                                               
SETESYS  NTR1                                                                   
         L     R2,0(R1)                                                         
         TM    PRGSTAT,TESTSYS                                                  
         BZ    *+10                                                             
         MVC   0(5,R2),=C'(TST)'                                                
         TM    PRGSTAT,CSCSYS                                                   
         BZ    *+10                                                             
         MVC   0(5,R2),=C'(CSC)'                                                
         TM    PRGSTAT,FQASYS                                                   
         BZ    *+10                                                             
         MVC   0(5,R2),=C'(FQA)'                                                
SETESYSX J     XIT                                                              
*                                                                               
OOBDEF   DC    0CL40                                                            
OOBUSID  DC    CL10'00000     '                                                 
OOBSYSP  DC    C'TNN*'                                                          
OOBDAYN  DC    C'DD'                                                            
OOBCLASS DC    C'C,'                                                            
OOBSEQN  DC    C'SSSS'                                                          
OOBLENQ  EQU   *-OOBSYSP                                                        
         DC    CL(L'OOBDEF-(*-OOBDEF))' '     SPARE SPACES                      
*                                                                               
JESMAIL  DC    CL8'JESMAIL '                                                    
*                                                                               
TOWHO    DC    C'US-TALENT_MF_DEV_TEAM,US-TALENT_PRODUCT_TEAM,US-OPERAT*        
               IONS_SUPPORT_NY:'                                                
TOWHO2   DC    C'US-TALENT_MF_DEV_TEAM:'                                        
TOWHO3   DC    C'US-TALENT_PRODUCT_TEAM,US-TALENT_MF_DEV_TEAM:'                 
*                                                                               
SUBJDSC  DC    0CL80                                                            
         DC    C'** Worker File Out-Of-Balance '                                
SUBJDSCS DC    C'     '                                                         
         DC    CL(L'SUBJDSC-(*-SUBJDSC))' '     SPARE SPACES                    
                                                                                
SUBJDSC2 DC    0CL80                            P+ Agy Comm changed             
         DC    C'** P+ Comm Less Than Expected: Check # '                       
SBJDSCCK DC    C'12345678'                                                      
         DC    C' to '                                                          
SBJDSCAM DC    CL10'9999999.99'                                                 
         DC    CL(L'SUBJDSC2-(*-SUBJDSC2))' '     SPARE SPACES                  
                                                                                
SUBJDSC3 DC    0CL80                            P+ Agy Comm changed             
         DC    C'** P+ Not all Canadian Taxes Taken: Check # '                  
SBJDSC3K DC    C'12345678'                                                      
         DC    C' '                                                             
SBJDSC3S DC    C'     '                                                         
         DC    CL(L'SUBJDSC3-(*-SUBJDSC3))' '     SPARE SPACES                  
                                                                                
SUBJDSC4 DC    0CL80                            Advance Reconciliation          
         DC    C'** Not all Advance Reconciliation Taken: Check # '             
SBJDSC4K DC    C'12345678'                                                      
SBJDSC4D DC    CL10'9999999.99'                                                 
         DC    C' '                                                             
SBJDSC4S DC    C'     '                                                         
         DC    CL(L'SUBJDSC4-(*-SUBJDSC4))' '     SPARE SPACES                  
                                                                                
SUBJDSC5 DC    0CL80                            Advance Reconciliation          
         DC    C'** Guarantee + Gross is less than Net '                        
SBJDSC5K DC    C'12345678'                                                      
         DC    C' '                                                             
SBJDSC5S DC    C'     '                                                         
         DC    CL(L'SUBJDSC5-(*-SUBJDSC5))' '     SPARE SPACES                  
                                                                                
BODYLIN1 DC    0CL80                                                            
BODYERR  DC    C'                  '            ERROR MESSAGE                   
         DC    CL(L'BODYLIN1-(*-BODYLIN1))' '   SPARE SPACES                    
*                                                                               
BODYLIN2 DC    CL80' '                                                          
BODYLIN3 DC    CL80'Debits not equal to Credits'                                
BODYLIN4 DC    CL80'==========================='                                
BODYLIN5 DC    0CL80                                                            
BODYL5A1 DC    CL10'9999999.99'                                                 
         DC    C' vs '                                                          
BODYL5A2 DC    CL10'9999999.99'                                                 
         DC    CL(L'BODYLIN5-(*-BODYLIN5))' '   SPARE SPACES                    
*                                                                               
BOD5LIN1 DC    0CL80                                                            
BOD5SSN  DC    C'         '                                                     
         DC    C' '                                                             
BOD5AGY  DC    C'      '                                                        
         DC    C' '                                                             
BOD5INV  DC    C'      '                                                        
         DC    CL(L'BOD5LIN1-(*-BOD5LIN1))' '   SPARE SPACES                    
         DROP  RF                                                               
         LTORG                                                                  
         EJECT                                                                  
* THIS ROUTINE PRINTS THE CHECK.                                                
*&&DO                                                                           
         DS    0D                                                               
PRNTCHK  NMOD1 0,PRNTCHK,RA,R9                                                  
         L     RC,0(R1)                                                         
*----------------------------------------------------------------------         
         MVI   LINE,X'99'          FORCE PAGE EJECT                             
*                                                                               
         LA    R5,P                R5 = A(PRINT LINE)                           
         USING PLINED,R5                                                        
*                                  IF TAX REFUND OR TRANSFER CHECK              
         TM    SRTADJS,TAPDADTR+TAPDADTT                                        
         BZ    PC0100                                                           
*                                                                               
         LA    R2,21               PRINT 21 BLANK LINES AND SKIP TO             
         BAS   RE,CALLSPL2             CHECK COMMENTS                           
         BCT   R2,*-4                                                           
         B     PC2200                                                           
***********************************************************************         
*        PRINT LINE #1                                                *         
***********************************************************************         
PC0100   MVC   P01AGYN,SRTAGYN     AGENCY NAME                                  
         MVC   P01AGY,SRTAGY       AGENCY CODE                                  
*                                                                               
         CLI   RECNUM,PC           IF PRINT CHECKS                              
         BNE   PC0110                                                           
         MVC   P01SVC,SRTSVCNM     SET SERVICE FOR NAME                         
         B     PC0150                                                           
*                                                                               
***      CLC   SRTPDTE,=X'910513'  IF PAY DATE IS ON/AFTER MAY 13               
***      BL    PC0150                                                           
***      CLC   SRTYEAR(2),=C'91'   AND CONTRACT YEAR IS 91                      
***      BNE   PC0150                                                           
***      CLC   SRTUN,=C'SAG'       AND UNION IS SAG                             
***      BE    PC0110                                                           
***      CLC   SRTUN,=C'AFT'                 OR AFT                             
***      BE    PC0110                                                           
***      CLC   SRTUN,=C'AFM'                 OR AFM                             
***      BNE   PC0150                                                           
***110   MVC   P01CMNT,CMNT91      PRINT SPECIAL COMMENT                        
*                                                                               
PC0110   CLC   P01SVC,SPACES       SKIP PRESORT IF SOMETHING IN FIELD           
         BH    PC0150                                                           
         CLI   SRTKSTA,SRTKSAF     IF AFTRA OR SAG AND NO AGENT CODE            
         BE    PC0115                                                           
         CLI   SRTKSTA,SRTKSNA                                                  
         BE    PC0115                                                           
         CLI   SRTKSTA,SRTKSAM     OR AFM                                       
         BNE   PC0150                                                           
PC0115   OC    SRTNCDE,SRTNCDE     AND NO AGENT CODE                            
         BNZ   PC0150                                                           
         CLC   SRTUN,=C'SAG'       ALL SAG LOCALS ARE PRESORTED                 
         BE    PC0130                                                           
         CLC   SRTUN,=C'AFM'       IF AFM, LOCAL MUST BE 47                     
         BNE   PC0118                                                           
         CLC   SRTLOCL,=CL3'47'                                                 
         BNE   PC0150                                                           
         CLC   SRTCAT,=C'ZZZ'      EXCLUDE CATEGORY ZZZ                         
         BE    PC0150                                                           
         B     PC0130                                                           
PC0118   CLC   SRTUN,=C'AFT'       IF AFTRA, LOCALS ARE IN A TABLE              
         BNE   PC0150                                                           
*                                                                               
         LA    RE,LOCLTAB          IF UNION AFT, MATCH ON LOCAL                 
PC0120   CLI   0(RE),X'FF'                                                      
         BE    PC0150              END OF TABLE - NO MATCH                      
         CLC   SRTLOCL,0(RE)                                                    
         BE    PC0130              LOCAL=LA,SAN,SDG,SF,CHI,DAL,HON,NYC          
         LA    RE,L'LOCLTAB(RE)                                                 
         B     PC0120                                                           
*                                                                               
PC0130   MVC   P01SVC(7),=CL7'PRESORT'    PRINT PRESORT ON TOP OF CHECK         
*                                                                               
PC0150   BAS   RE,CALLSPL2                                                      
         BAS   RE,CALLSPL2                                                      
***********************************************************************         
*        PRINT LINE #3                                                *         
***********************************************************************         
         MVC   P03CID,SRTCID       COMMERCIAL ID                                
         CLI   SRTVERS,0           IF HAVE VERSION CODE                         
         BE    PC0310                                                           
         LA    RE,P03CID+L'TGCID-1 START AT END OF CID                          
         CLI   0(RE),C' '                                                       
         BNE   *+8                                                              
         BCT   RE,*-8              KEEP GOING BACK TILL FIRST NON-SPACE         
*                                                                               
         MVC   2(L'SRTVERS,RE),SRTVERS  SKIP A SPACE, SHOW VERSION CODE         
         TM    TGSYSTAT,TASYS3VR                                                
         BZ    PC0310                                                           
         CLI   SRTVERS,0                                                        
         BE    PC0310                                                           
         EDIT  SRTVERS,(3,2(RE)),ALIGN=LEFT                                     
*                                                                               
PC0310   CLI   RECNUM,PC           IF NOT PRINT CHECKS                          
         BE    PC0320                                                           
         MVC   P03ORIG,SRTORIG     ORIGINAL COMMERCIAL ID                       
         MVC   P03EDIT,SRTEDIT     EDIT VERSION NUMBER                          
         B     PC0350                                                           
*                                                                               
PC0320   MVC   P03PHO,SRTPHONM     SET PHOTOGRAPHER NAME                        
*                                                                               
PC0350   BAS   RE,CALLSPL2                                                      
         BAS   RE,CALLSPL2                                                      
***********************************************************************         
*        PRINT LINE #5                                                *         
***********************************************************************         
         MVC   P05CLIN,SRTCLIN     CLIENT NAME                                  
         MVC   P05PRDN,SRTPRDN     PRODUCT NAME                                 
         BAS   RE,CALLSPL2                                                      
         BAS   RE,CALLSPL2                                                      
***********************************************************************         
*        PRINT LINE #7                                                *         
***********************************************************************         
         MVC   P07TITL,SRTTITL     COMMERCIAL TITLE                             
*                                                                               
         CLI   RECNUM,PC           IF PRINT CHECKS                              
         BNE   PC0720                                                           
         MVC   P07PCAT,SRTCAT      CATEGORY CODE                                
         CLC   SRTCST,=6X'FF'      IF AGENT CHECK                               
         BNE   PC0750                                                           
         CLC   SRTCAT,=C'ZZZ'                                                   
         BNE   PC0750                                                           
         CLC   SRTCAT,=C'ZZ '                                                   
         BNE   PC0750                                                           
         MVC   P07PCAT(5),=C'AGENT'  PRINT AGENT FOR CATEGORY                   
         B     PC0750                                                           
*                                                                               
PC0720   MVC   P07UN,SRTUN         UNION                                        
         MVC   P07LOCL,SRTLOCL     LOCAL                                        
         GOTO1 CONVDATE,DMCB,SRTFRST,P07FRST,1 FIRST SERVICES DATE              
         MVC   P07YEAR,SRTYEAR     CONTRACT YEAR                                
         MVC   P07CAT,SRTCAT       CATEGORY CODE                                
         MVC   P07ONOF,SRTONOF     CAMERA (ON/OFF)                              
*                                                                               
         TM    TGCASTAT,SWMULT     IF CATEGORY HAS SWEET/MULTITRACKING          
         BZ    PC0740                                                           
         LA    RE,SRTCAT+L'SRTCAT-1                                             
         CLI   0(RE),C' '                                                       
         BNE   *+6                                                              
         BCTR  RE,0                                                             
         CLI   0(RE),C'M'          AND IT ENDS WITH A 'M'                       
         BNE   *+12                                                             
         MVI   P07MULT,C'X'        THEN PUT AN 'X' IN MULTITRACKING             
         B     PC0740                                                           
         MVI   P07SWEET,C'X'       ELSE, PUT AN 'X' IN SWEETENING               
*                                                                               
PC0740   MVC   P07DBL,SRTDBL       DOUBLES                                      
         ICM   RF,15,SRTOV1        OVERSCALE PERCENTAGE 1                       
         SR    RE,RE                                                            
         TM    SRTOV1,X'80'                                                     
         BZ    PC0745                                                           
         N     RF,=X'7FFFFFFF'     STRIP HOB                                    
*                                                                               
PC0745   D     RE,=F'100'                                                       
         TM    SRTOV1,X'80'                                                     
         BZ    PC0747                                                           
         MVI   P07OV1,C'%'         PERCENT SCALE USED                           
         EDIT  (RF),(2,P07OV1+1)                                                
         B     PC0749                                                           
PC0747   EDIT  (RF),(4,P07OV1)                                                  
*                                                                               
PC0749   ICM   RF,15,SRTOV2        OVERSCALE PERCENTAGE 2                       
         SR    RE,RE                                                            
         D     RE,=F'100'                                                       
         EDIT  (RF),(4,P07OV2)                                                  
*                                                                               
PC0750   BAS   RE,CALLSPL2                                                      
***********************************************************************         
*        PRINT LINE #8                                                *         
***********************************************************************         
         CLI   RECNUM,PC             IF PRINT CHECKS                            
         BE    PC0850                SKIP THIS LINE                             
         EDIT  (1,SRTDAY),(2,P08DAY) DAYS WORKED ON SESSION                     
         EDIT  (1,SRTOT),(2,P08OT)   OVERTIME HOURS WORKED ON SESSION           
         EDIT  (1,SRTDT),(2,P08DT)   DOUBLETIME HOURS WORKED ON SESSION         
PC0850   BAS   RE,CALLSPL2                                                      
***********************************************************************         
*        PRINT LINE #9                                                *         
***********************************************************************         
         CLI   RECNUM,PC           IF PRINT CHECKS                              
         BNE   PC0905                                                           
         MVC   P09SSTU,SRTSTUD                     SHOOT STUDIO                 
         MVC   P09SCTY,SRTCITY                     SHOOT CITY                   
         GOTO1 CONVDATE,DMCB,SRTSHOOT,P09SDAT,1    SHOOT DATE                   
*                                                                               
         L     R4,ASRTCHK          R4=A(CHECK RECORD)                           
         MVI   ELCODE,TACAELQ                                                   
         BRAS  RE,GETEL            IF CHECK SHOOT DATE                          
         BNE   PC0950                                                           
         USING TACAD,R4                                                         
         GOTO1 CONVDATE,DMCB,TACASDTE,P09SDAT,1     USE IT                      
         B     PC0950                                                           
*                                                                               
PC0905   GOTO1 CONVDATE,DMCB,SRTAIR,P09AIR,1   FIRST AIR DATE                   
*        TM    TGCAUNI,AFM         IF PERFORMER IS NOT A MUSICIAN               
         GOTO1 UNITEST,DMCB,TGCAUNIS,AFM,0,0,0                                  
         BO    PC0910                                                           
         MVC   WORK(3),SRTCFCY     FIRST FIXED CYCLE COMES FROM THE             
         OC    SRTCFCY,SRTCFCY     COMMERCIAL UNLESS OVERRIDDEN BY              
         BNZ   *+10                THE CAST RECORD                              
         MVC   WORK(3),SRTFCYC                                                  
         GOTO1 CONVDATE,DMCB,WORK,P09FCYC,1                                     
*                                                                               
         MVC   WORK(3),SRTCEXP     EXPIRATION DATE COMES FROM THE               
         OC    SRTCEXP,SRTCEXP     COMMERCIAL UNLESS OVERRIDDED BY              
         BNZ   *+10                THE CAST RECORD                              
         MVC   WORK(3),SRTEXP                                                   
         GOTO1 CONVDATE,DMCB,WORK,P09EXP,1                                      
*                                                                               
*C0910   TM    TGCAUNI,AFM         IF PERFORMER IS A MUSICIAN                   
PC0910   GOTO1 UNITEST,DMCB,TGCAUNIS,AFM,0,0,0                                  
         BZ    PC0950                                                           
         MVC   P09CON(8),SRTCON    THEN DISPLAY AFM CONTRACT NUMBERS            
         MVC   P09CON+9(8),SRTCON+12                                            
         MVC   P09LEAD,SRTLEAD                AFM LEADER                        
         GOTO1 CONVDATE,DMCB,SRTDUB,P09DUB,1  AFM DUB DATE                      
*                                                                               
PC0950   BAS   RE,CALLSPL2         LINE # 9                                     
         BAS   RE,CALLSPL2         LINE # 10                                    
***********************************************************************         
*        PRINT LINE #11                                               *         
***********************************************************************         
         CLI   RECNUM,PC           IF PRINT CHECKS                              
         BNE   PC1150                                                           
         L     R4,ASRTCHK          R4=A(CHECK RECORD)                           
         MVI   ELCODE,TADLELQ      LOOK FOR DEAL ELEMENT                        
         BRAS  RE,GETEL                                                         
         BNE   PC1120                                                           
         USING TADLD,R4                                                         
         GOTO1 CONVDATE,DMCB,TADLPUB,P11PBDT,1                                  
         GOTO1 CONVDATE,DMCB,TADLEXP,P11EXPDT,1                                 
         CLI   TADLTERM,TADLTUNL   IF SPECIAL TERM                              
         BNE   *+14                                                             
         MVC   P11MNTH,=C'UN'      SET UNLIMITED                                
         B     PC1120                                                           
         CLI   TADLTERM,TADLT1X    IF ONE TIME TERM                             
         BNE   *+14                                                             
         MVC   P11MNTH,=C'1X'      SET ONE TIME                                 
         B     PC1120                                                           
         EDIT  TADLTERM,(2,P11MNTH),ZERO=NOBLANK                                
*                                                                               
PC1120   CLC   SRTCST,=6X'FF'      SKIP IF AGENT CHECK                          
         BNE   PC1122                                                           
         CLC   SRTCAT,=C'ZZZ'                                                   
         BE    PC1150                                                           
         CLC   SRTCAT,=C'ZZ '                                                   
         BE    PC1150                                                           
PC1122   L     R4,ASRTCHK          R4=A(CHECK RECORD)                           
         MVI   ELCODE,TACAELQ      LOOK FOR CAST DETAILS ELEMENT                
         BRAS  RE,GETEL                                                         
         BNE   PC1150                                                           
         USING TACAD,R4                                                         
         CLI   TACAEBAS,TACAEPUB                                                
         BNE   *+14                                                             
         MVC   P11EXPBS,=CL16'PUBLICATION DATE'                                 
         B     *+10                                                             
         MVC   P11EXPBS,=CL16'SHOOT DATE'                                       
*                                                                               
PC1150   BAS   RE,CALLSPL2         LINE # 11                                    
***********************************************************************         
*        PRINT LINE #12                                               *         
***********************************************************************         
         CLI   RECNUM,PC           IF PRINT CHECKS                              
         BE    PC1250              SKIP THIS LINE                               
         CLI   SRTCTYP,CTYIND      COMMERCIAL TYPE IS INDUSTRIAL?               
         BE    *+10                YES                                          
         MVC   P12MED,TGMENAME     ELSE PRINT MEDIA NAME                        
*                                                                               
         MVC   P12UTYP(16),TGUSNAME  USE NAME                                   
         CLI   TGUSEQU,UFGR          IF FGR USE                                 
         BNE   PC1202                                                           
         TM    TGUSTYST,MAJORS       AND IS NEW TYPE WITH MAJORS/AREAS          
         BZ    PC1202                                                           
         LA    R4,P12UTYP+16         FIND END OF USE NAME                       
         CLI   0(R4),X'40'                                                      
         BH    *+8                                                              
         BCT   R4,*-8                                                           
         MVC   2(L'TGMACHAR,R4),TGMACHAR  DISPLAY AREA EXPRESSION               
*                                                                               
PC1202   MVC   P12PTYP,=C'SESSION' IF PAYMENT TYPE IS SESSION                   
         TM    TGUSSTAT,SESSION    THEN PUT 'SESSION' IN PAY TYPE FLD           
         BO    PC1205                                                           
*                                  ELSE PUT 'REUSE' IN PAY TYPE FIELD           
         MVC   P12PTYP,=C'REUSE  '     AND USE TYPE IN USE TYPE FIELD           
*                                                                               
PC1205   OC    SRTSTUS,SRTSTUS     USE RANGE                                    
         BZ    PC1220                                                           
*                                                                               
         SR    R2,R2               R2 = STARTING USE NUMBER                     
         ICM   R2,3,SRTSTUS                                                     
         SR    R3,R3               R3 = ENDING USE NUMBER                       
         ICM   R3,3,SRTUSES                                                     
         AR    R3,R2                                                            
         BCTR  R3,0                                                             
*                                                                               
         CR    R2,R3               IF R2 = R3                                   
         BNE   PC1210              THEN PRINT 'USE N'                           
         MVC   P12UTYP+17(3),=C'USE'                                            
         LA    R4,P12UTYP+21                                                    
         EDIT  (R2),(5,0(R4)),ALIGN=LEFT                                        
         B     PC1220                                                           
*                                  ELSE PRINT 'USES M TO N'                     
PC1210   MVC   P12UTYP+17(4),=C'USES'                                           
         LA    R4,P12UTYP+22                                                    
         EDIT  (R2),(5,0(R4)),ALIGN=LEFT                                        
         AR    R4,R0                                                            
         MVC   1(2,R4),=C'TO'                                                   
         EDIT  (R3),(5,4(R4)),ALIGN=LEFT                                        
*                                                                               
PC1220   CLI   TGUSEQU,UDEM        DON'T PRINT USE WEEKS OR CYCLE DATES         
         BE    PC1250               . . . FOR DEMO PAYMENTS                     
         CLI   TGUSEQU,UCDM                                                     
         BE    PC1250                                                           
         TM    TGUSWKS,X'C0'       IF X'80' OR X'40' SET IN THE NO.             
         BZ    PC1230                  OF WEEKS IN THE CYCLE                    
         MVC   BYTE,TGUSWKS                                                     
         NI    BYTE,X'3F'          THEN CLEAR THEM OUT                          
         EDIT  (1,BYTE),(1,P12WKS) AND DISPLAY THE NUMBER OF MONTHS             
         MVI   P12WKS+1,C'M'                                                    
         TM    TGUSWKS,X'40'       IF X'40' BIT IS SET                          
         BZ    *+8                                                              
         MVI   P12WKS+1,C'D'       THEN DISPLAYING DAYS                         
         B     PC1240                                                           
*                                  ELSE DISPLAY THE NUMBER OF WEEKS             
PC1230   EDIT  (1,TGUSWKS),(2,P12WKS),ALIGN=LEFT                                
*                                  CYCLE START AND END DATES                    
PC1240   GOTO1 CONVDATE,DMCB,SRTCYCS,P12CYCS,1                                  
         GOTO1 CONVDATE,DMCB,SRTCYCE,P12CYCE,1                                  
*                                                                               
PC1250   BAS   RE,CALLSPL2                                                      
***********************************************************************         
*        PRINT LINES #13                                              *         
***********************************************************************         
         CLI   RECNUM,PC           IF PRINT CHECKS                              
         BNE   PC1350                                                           
         L     R4,ASRTCHK          R4=A(CHECK RECORD)                           
         MVI   ELCODE,TADLELQ      LOOK FOR DEAL ELEMENT                        
         BRAS  RE,GETEL                                                         
         BNE   PC1350                                                           
         USING TADLD,R4                                                         
         GOTO1 RECVAL,DMCB,TLUSCDQ,(X'AC',TADLUSE),0                            
         BNE   *+10                                                             
         MVC   P13USE,TGNAME                                                    
         GOTO1 RECVAL,DMCB,TLARCDQ,(X'AC',TADLAREA),0                           
         BNE   *+10                                                             
         MVC   P13AREA,TGNAME                                                   
PC1350   BAS   RE,CALLSPL2                                                      
***********************************************************************         
*        PRINT LINES #14,15 FOR PC REPORT                             *         
***********************************************************************         
         CLI   RECNUM,PC           IF PRINT CHECKS                              
         BNE   PC1400                                                           
         BAS   RE,CALLSPL2         LINE # 14                                    
         GOTO1 TINVCON,DMCB,SRTINV,P15INV,DATCON                                
         BAS   RE,CALLSPL2         LINE # 15                                    
         BAS   RE,CALLSPL2                                                      
         MVC   NUMLINES,=F'12'     NUMBER OF COMMENT LINES                      
         B     PC2205                                                           
***********************************************************************         
*        PRINT LINES #14,16,18                                        *         
***********************************************************************         
PC1400   LA    R4,P14CLA           R4 = A(PRINT LINE #14'S CLA DATES)           
         LA    R2,SRTCLA           R2 = A(CLASS A DATES)                        
         LA    R3,23               R3 = OUTPUT COUNTER                          
*                                                                               
PC1410   OC    0(3,R2),0(R2)       IF DATE IS ZERO                              
         BZ    PC1415              DONE                                         
*                                                                               
         LA    RE,SRTCLA                                                        
         AHI   RE,L'SRTCLA                                                      
         CR    R2,RE                                                            
         BNL   PC1415                                                           
*                                                                               
         TM    1(R2),X'80'         IF PAX PROGRAM                               
         BZ    PC1411                                                           
         OI    CHKSTAT,PAXPRO                                                   
         CLI   TGUSEQU,UPAX        AND USE IS CLASS A                           
         BE    PC1411                                                           
         CLI   SRTYEAR,C'8'        AND CONTRACT YEAR IS 2000                    
         BE    PC1411              OF GREATER                                   
         CLI   SRTYEAR,C'9'                                                     
         BE    PC1411                                                           
         LA    R2,4(R2)            SKIP PRINTING THIS PROGRAM                   
*        BCTR  R3,0                                                             
         NI    CHKSTAT,X'FF'-PAXPRO                                             
         B     PC1410                                                           
*                                                                               
PC1411   TM    CHKSTAT,PAXPRO      IF PAX PROGRAM                               
         BZ    *+8                                                              
         NI    1(R2),X'FF'-X'80'   CORRECT THE DATE FORMAT                      
*                                                                               
         TM    SRTCSTAT,TACASTLF+TACASTLO  IF PERFRMR IS ON MAIN & LIFT         
         BM    PC1412              THEN ALWAYS PRINT THIS USE DATE              
         BO    *+16                                                             
         CLI   3(R2),C'Y'          PERFORMER IS ONLY ON MAIN                    
         BE    PC1415                                                           
         B     PC1412                                                           
         CLI   3(R2),C'Y'          PERFORMER IS ONLY ON LIFT                    
         BNE   PC1415                                                           
*                                                                               
PC1412   MVC   0(3,R4),0(R2)       MOVE DATE TO PRINT LINE                      
         CLC   0(3,R4),=C'TBA'     IF TBA THEN DONE                             
         BE    PC1415                                                           
*                                  ELSE DISPLAY DATE TYPE 2 TO OUTPUT           
         GOTO1 CONVDATE,DMCB,(R2),(R4),2                                        
         LA    R4,5(R4)            BUMP R4 TO NEXT PLACE                        
*                                                                               
         TM    CHKSTAT,PAXPRO      IF PAX PROGRAM                               
         BZ    *+12                                                             
         OI    1(R2),X'80'         SET THE MONTH TO INDICATE IT AGAIN           
         NI    CHKSTAT,X'FF'-PAXPRO                                             
*                                                                               
PC1415   CH    R3,=H'16'           IF R3 = 16                                   
         BNE   PC1420                                                           
         BAS   RE,CALLSPL2         PRINT LINE #14                               
         BAS   RE,CALLSPL2                                                      
*                                                                               
         CLI   SRTCTYP,CTYSPAN     IF COMMERCIAL IS MODE FOR SPANISH            
         BNE   *+10                                                             
         MVC   P16SPAN(3),=C'YES'  THEN DISPLAY 'YES'                           
*                                                                               
         OC    SRTTRV,SRTTRV       IF TRAVEL HOURS FOR SESSION                  
         BZ    PC1417                                                           
*                                  THEN PRINT TRAVEL HOURS                      
         EDIT  (2,SRTTRV),(5,P16TRV),2                                          
*                                                                               
PC1417   OC    SRTPDW,SRTPDW       IF PRIOR WARDROBE HOURS FOR SESSION          
         BZ    PC1418                                                           
*                                  THEN PRINT PRIOR WARDROBE HOURS              
         EDIT  (2,SRTPDW),(5,P16PDW),2                                          
*                                                                               
PC1418   LA    R4,P16CLA           R4 = A(PRINT LINE #16'S CLA DATES)           
         B     PC1440                                                           
*                                                                               
PC1420   CH    R3,=H'8'            ELSE IF R3 = 8                               
         BNE   PC1440                                                           
         BAS   RE,CALLSPL2         PRINT LINE #16                               
         BAS   RE,CALLSPL2                                                      
*                                                                               
         LA    R4,P18CLA           R4 = A(PRINT LINE #18'S CLA DATES)           
         B     PC1440                                                           
*                                                                               
PC1440   LA    R2,4(R2)            BUMP R2 TO NEXT DATE                         
         BCT   R3,PC1410           REPEAT UNTIL NO MORE DATES                   
*                                                                               
PC1445   BAS   RE,CALLSPL2         PRINT LINE #18                               
         BAS   RE,CALLSPL2                                                      
***********************************************************************         
*        PRINT LINE #20                                               *         
***********************************************************************         
         OC    SRTMAJ(3),SRTMAJ    IF MAJORS/UNITS EXISTS                       
         BZ    PC2005                                                           
*                                                                               
         CLI   TGUSEQU,UFGR        SKIP IF FGR USE                              
         BE    PC2005                                                           
*                                                                               
         LA    RF,P20MAJ           INITIAL MAJORS                               
         MVC   BYTE,SRTMAJ         SAVE SRTMAJ IN BYTE                          
         MVC   HALF,SRTUNT         SAVE SRTUNT IN HALF                          
         CLI   SRTVERS,0           IF HAVE VERSION CODE                         
         BE    PC2000                                                           
         MVC   BYTE,SRTVMAJ        USE SRTVMAJ                                  
         MVC   HALF,SRTVUNT        AND SRTVUNT INSTEAD                          
PC2000   TM    BYTE,NY                                                          
         BZ    *+14                                                             
         MVC   0(2,RF),=C'NY'                                                   
         LA    RF,3(RF)                                                         
         TM    BYTE,LA                                                          
         BZ    *+14                                                             
         MVC   0(2,RF),=C'LA'                                                   
         LA    RF,3(RF)                                                         
         TM    BYTE,CHI                                                         
         BZ    *+10                                                             
         MVC   0(3,RF),=C'CHI'                                                  
*                                  INITIAL UNITS                                
         EDIT  (2,HALF),(4,P20UNT),ALIGN=LEFT,ZERO=NOBLANK                      
*                                                                               
PC2005   OC    SRTINS,SRTINS       INITIAL INSERTS                              
         BZ    PC2007                                                           
         EDIT  (2,SRTINS),(4,P20INS),ALIGN=LEFT,ZERO=NOBLANK                    
*                                                                               
PC2007   OC    SRTUMAJ(3),SRTUMAJ  IF UPGRADE INFO EXISTS                       
         BZ    PC2010                                                           
*                                                                               
         LA    RF,P20UMAJ          UPGRADE MAJORS                               
         MVC   BYTE,SRTUMAJ        SAVE SRTUMAJ IN BYTE                         
         MVC   HALF,SRTUUNT        SAVE SRTUUNT IN HALF                         
         CLI   SRTVERS,0           IF HAVE VERSION CODE                         
         BE    PC2008                                                           
         MVC   BYTE,SRTVUMAJ        USE SRTVUMAJ                                
         MVC   HALF,SRTVUUNT        AND SRTVUUNT INSTEAD                        
PC2008   TM    BYTE,NY                                                          
         BZ    *+14                                                             
         MVC   0(2,RF),=C'NY'                                                   
         LA    RF,3(RF)                                                         
         TM    BYTE,LA                                                          
         BZ    *+14                                                             
         MVC   0(2,RF),=C'LA'                                                   
         LA    RF,3(RF)                                                         
         TM    BYTE,CHI                                                         
         BZ    *+10                                                             
         MVC   0(3,RF),=C'CHI'                                                  
*                                  UPGRADE UNITS                                
         EDIT  (2,HALF),(4,P20UUNT),ALIGN=LEFT,ZERO=NOBLANK                     
*                                                                               
PC2010   OC    SRTUINS,SRTUINS     UPGRADE INSERTS                              
         BZ    PC2012                                                           
         EDIT  (2,SRTUINS),(4,P20UINS),ALIGN=LEFT,ZERO=NOBLANK                  
*                                                                               
PC2012   L     RF,SRTGUAR          COMPUTE CREDITS = (GUARANTEE CREDITS         
         A     RF,SRTAPPL              + APPLIED CREDITS) * (-1)                
         LCR   RF,RF                                                            
*                                  PRINT CREDITS                                
         EDIT  (RF),(7,P20CRED)                                                 
*                                                                               
         OC    SRTACDE,SRTACDE     APPLIED CODE COMMENTS                        
         BZ    PC2020                                                           
         MVC   P20ACDE,=CL20'UNKNOWN APPLIED CODE'                              
         GOTO1 APPLVAL,DMCB,SRTACDE                                             
         BNE   PC2020                                                           
         MVC   P20ACDE(7),=C'APPLIED'                                           
         MVC   P20ACDE+8(12),TGAPTEXT                                           
*                                                                               
         CLI   TGMEEQU,RADIO       FOR RADIO INVOICES                           
         BNE   PC2020                                                           
         CLI   SRTACDE,APPLHLD     WHOSE APPLIED CODE IS HLD                    
         BNE   PC2020                                                           
         MVC   P20ACDE+8(12),=C'CYCLE GRT   '    CHANGE TEXT                    
*                                                                               
*                                  INVOICE NUMBER                               
PC2020   GOTO1 TINVCON,DMCB,SRTINV,P20INV,DATCON                                
*                                  NUMBER OF SPOTS ON SESSION                   
         EDIT  (1,SRTSP),(2,P20SP)                                              
*                                  NUMBER OF TAGS ON SESSION                    
         CLI   TGUSEQU,UTAG        IF TAG USE                                   
         BNE   PC2030                                                           
*                                  NUMBER OF TAGS FOR INVOICE                   
         EDIT  (1,SRTITAG),(3,P20TAG-1)                                         
         B     PC2040                                                           
*                                  ELSE NUMBER OF TAGS ON SESSION               
PC2030   CLC   SRTUSE,=C'RTK'                                                   
         BE    PC2040                                                           
         EDIT  (1,SRTTAG),(2,P20TAG)                                            
*                                                                               
PC2040   BAS   RE,CALLSPL2                                                      
         BAS   RE,CALLSPL2                                                      
***********************************************************************         
*        PRINT LINES #22-28                                           *         
***********************************************************************         
PC2200   MVC   NUMLINES,=F'7'      NUMBER OF LINES LEFT = 7                     
*                                                                               
PC2205   MVC   AIO,ASRTCHK         SEARCH CHECK REC FOR CHECK COMMENT           
         MVI   ELCODE,TACMELQ                                                   
         XR    R2,R2               CLEAR L'CHECK COMMENT                        
         GOTO1 GETL,DMCB,(1,=AL1(TACMTYPC))                                     
         MVC   AIO,AIO1                                                         
         BNE   PC2210              SKIP IF NONE FOUND                           
*                                                                               
         L     R4,TGELEM           R4 = A(CHECK COMMENT ELEMENT)                
         USING TACMD,R4                                                         
         IC    R2,TACMLEN          R2 = LENGTH OF CHECK COMMENT                 
         SH    R2,=AL2(TACMLNQ)                                                 
         BZ    PC2210                                                           
         LA    R3,TACMCOMM         R3 = A(CHECK COMMENT)                        
*                                                                               
         CLI   0(R3),C'X'          IF CHECK COMMENT BEGINS WITH 'X'             
         BNE   PC2210                                                           
         BCTR  R2,0                SET TO PRINT ONE LESS CHARACTER              
         LA    R3,1(R3)            BUMP PAST 'X'                                
         B     PC2215              AND SKIP INVOICE COMMENT                     
*                                                                               
PC2210   OC    SRTINVC,SRTINVC     IF INVOICE COMMENTS EXIST                    
         BZ    PC2215              THEN PRINT THEM                              
         GOTO1 ADDCOMM,DMCB,SRTINVC,60                                          
*                                                                               
PC2215   LTR   R2,R2               IF WE FOUND CHECK COMMENT                    
         BZ    PC2220                                                           
         GOTO1 ADDCOMM,DMCB,(R3),(R2)  PRINT IT                                 
*                                                                               
PC2220   L     R4,ASRTCHK          R4 = A(FIRST DUECOMP WITHHLD ELEM)           
         MVI   ELCODE,TADWELQ                                                   
         BRAS  RE,GETEL            SKIP IF NONE FOUND                           
         BNE   PC2250                                                           
         USING TADWD,R4                                                         
*                                                                               
PC2230   ICM   RF,15,TADWREC       IF AMOUNT RECOVERED IS NOT NEGATIVE          
         BNP   PC2240                                                           
*                                                                               
         BAS   RE,DUECOMM          THEN PRINT DUECOMP COMMENT                   
         OC    NUMLINES,NUMLINES                                                
         BZ    PC2290                                                           
*                                                                               
PC2240   MVI   ELCODE,TADWELQ      GET NEXT DW ELEMENT                          
         BRAS  RE,NEXTEL                                                        
         BE    PC2230              REPEAT UNTIL NO MORE DW ELEMENTS             
*                                                                               
PC2250   L     R4,ASRTCHK          R4 = A(LIEN WITHHLD ELEM)                    
         MVI   ELCODE,TALWELQ                                                   
         BRAS  RE,GETEL            SKIP IF NONE FOUND                           
         BNE   PC2255                                                           
         BAS   RE,LINCOMM          PRINT LIEN COMMENT                           
         OC    NUMLINES,NUMLINES                                                
         BZ    PC2290                                                           
*                                                                               
PC2255   MVC   AIO,ASRTCHK         SEARCH FOR W4 TRUSTEE DEDUCTION              
         MVI   ELCODE,TAODELQ                                                   
         GOTO1 GETL,DMCB,(1,=AL1(TAODTYPT))                                     
         MVC   AIO,AIO1                                                         
         BNE   PC2260              SKIP IF NONE FOUND                           
         L     R4,TGELEM                                                        
         BAS   RE,TRSTCOMM         PRINT W4 TRUSTEE COMMENT                     
         OC    NUMLINES,NUMLINES                                                
         BZ    PC2290                                                           
*                                                                               
PC2260   L     R4,ASRTCHK          R4 = A(SOAP EPISODES ELEMENT)                
         MVI   ELCODE,TASOELQ                                                   
         BRAS  RE,GETEL            SKIP IF NONE FOUND                           
         BNE   PC2270                                                           
*                                                                               
         BAS   RE,EPICOMM          PRINT EPISODES COMMENT                       
         OC    NUMLINES,NUMLINES                                                
         BZ    PC2290                                                           
*                                                                               
PC2270   MVC   AIO,ASRTCHK         SET AIO=A(FREE FORM NUMBER ELEMENT)          
         MVI   ELCODE,TANUELQ                                                   
         GOTO1 GETL,DMCB,(1,=AL1(TANUTCST))                                     
         MVC   AIO,AIO1            RESET AIO                                    
         BNE   PC2278                                                           
*                                                                               
         BAS   RE,AGTSSN           PRINT NAMES OF CONTRIBUTING MODELS           
         OC    NUMLINES,NUMLINES                                                
         BZ    PC2290                                                           
*                                                                               
PC2278   L     R4,ASRTCHK          R4=A(CHECK RECORD)                           
         MVI   ELCODE,TACAELQ      LOOK FOR CAST DETAILS ELEMENT                
         BRAS  RE,GETEL                                                         
         BNE   PC2280                                                           
         USING TACAD,R4                                                         
         OC    TACALFTF,TACALFTF                                                
         BZ    PC2280                                                           
         DROP  R4                                                               
*                                                                               
         BAS   RE,LIFTCOMM                                                      
         OC    NUMLINES,NUMLINES                                                
         BZ    PC2290                                                           
*                                                                               
PC2280   GOTO1 ADDCOMM,DMCB,0,999  FILL REMAINING LINES WITH BLANKS             
*                                                                               
PC2290   BAS   RE,CALLSPL2         SKIP ON MORE LNE                             
***********************************************************************         
*        PRINT LINE #30                                               *         
***********************************************************************         
*                                  IF TAX REFUND OR TRANSFER CHECK              
         TM    SRTADJS,TAPDADTR+TAPDADTT                                        
         BNZ   PC3090              SKIP TO EMPLOYER NAME                        
*                                                                               
*                                  GET OFFICE RECORD AND NAME                   
         LA    R2,OFFTAB                                                        
         USING OFFD,R2             R2=A(OFFICE TABLE)                           
         LA    R0,MAXOFFS          R0=MAX N'OFFICES                             
*                                                                               
PC3010   CLC   SRTTPOF,OFFCODE     MATCH ON OFFICE CODE                         
         BE    PC3020                                                           
         LA    R2,OFFNEXT                                                       
         BCT   R0,PC3010                                                        
         B     PC3090              NOT FOUND - SKIP TO EMPLOYER NAME            
*                                                                               
PC3020   MVC   P30INQ(15),OFFNAME  PRINT NAME IN 'INQUIRIES TO:' FIELD          
*                                                                               
         MVC   P30INQ+16(3),OFFTEL PRINT OFFICE PHONE NUMBER                    
         MVI   P30INQ+19,C'-'                                                   
         MVC   P30INQ+20(3),OFFTEL+3                                            
         MVI   P30INQ+23,C'-'                                                   
         MVC   P30INQ+24(4),OFFTEL+6                                            
*                                                                               
PC3090   MVC   P30EMPN,SRTEMPN     EMPLOYER NAME                                
         BAS   RE,CALLSPL2                                                      
         BAS   RE,CALLSPL2                                                      
***********************************************************************         
*        PRINT LINE #32                                               *         
***********************************************************************         
         MVC   P32NAME,SRTNAME     PERFORMER NAME                               
*                                                                               
         MVC   P32CHKN,CHKNCHR     CHECK NUMBER                                 
*                                                                               
         CLI   RECNUM,CN           IF THIS ISN'T CAN$ CHECKS                    
         BE    PC3200                                                           
         CLI   RECNUM,EC           OR EURO CHECKS                               
         BE    PC3200                                                           
         MVC   P32COR,SRTCOR       CITY OF RESIDENCE                            
         MVC   P32COW,SRTCOW       CITY OF WORK                                 
*                                                                               
PC3200   MVC   P32SOR,SRTSOR       STATE OF RESIDENCE                           
*                                                                               
         MVC   P32SOW,SRTSOW       STATE OF WORK                                
*                                                                               
         OC    SRTNCDE,SRTNCDE     IF AGENT EXISTS                              
         BZ    PC3210                                                           
*                                  THEN DISPLAY AGENT CODE                      
         GOTO1 TRNSAGT,DMCB,(X'40',SRTNCDE),P32NCDE                             
*                                  PAY ENDING                                   
PC3210   GOTO1 CONVDATE,DMCB,SRTCDTE,P32PAYE,1                                  
         BAS   RE,CALLSPL2                                                      
         BAS   RE,CALLSPL2                                                      
***********************************************************************         
*        PRINT LINE #34                                               *         
***********************************************************************         
         CLI   SRTW4TY,TAW4TYCO    IF PERFORMER IS NOT CORPORATION              
         BE    PC3410                                                           
         CLI   SRTW4TY,TAW4TYCA    OR CANADIAN                                  
         BE    PC3410                                                           
         CLI   SRTW4TY,TAW4TYTR    OR TRUSTEE                                   
         BE    PC3410                                                           
*                                                                               
         MVC   P34SSN(3),SRTSSN    THEN PRINT SSN IN SSN FIELD                  
         MVC   P34SSN+4(2),SRTSSN+3                                             
         MVC   P34SSN+7(4),SRTSSN+5                                             
         B     PC3420                                                           
*                                                                               
PC3410   MVC   P34FID(2),SRTSSN    ELSE PRINT SSN IN FEDERAL ID FIELD           
         MVC   P34FID+3(7),SRTSSN+2                                             
*                                                                               
PC3420   OC    SRTFSO,SRTFSO       IF FSO EXISTS                                
         BZ    PC3450                                                           
*                                                                               
         MVC   P34SSN(3),SRTFSO    THEN PRINT FSO IN SSN FIELD                  
         MVC   P34SSN+4(2),SRTFSO+3                                             
         MVC   P34SSN+7(4),SRTFSO+5                                             
*                                                                               
         MVC   P34FSON,SRTFSON     PRINT FSO NAME IN CORP FSO                   
*                                                                               
PC3450   MVC   P34SUC,SRTSUC       STATE U.C. #                                 
*                                                                               
         CLI   RECNUM,CN           IF CAN$ CHECKS                               
         BNE   *+14                                                             
         MVC   P34GST#,SRTGST#     GST ID NUMBER                                
         B     PC3455                                                           
*                                                                               
         CLC   SRTUN,=C'ACT'       ELSE IF UNION ACT                            
         BNE   PC3453                                                           
         MVC   AIO,ASRTCHK                                                      
         GOTO1 CHAROUT,DMCB,TANUELQ,0,TANUTMEM  GET ACT MEMBER NUMBER           
         MVC   AIO,AIO1                                                         
         BNE   *+10                                                             
         MVC   P34SUC,TGNAME       PRINT THE NUMBER IN STATE U.C. # BOX         
*                                                                               
PC3453   CLC   SRTGST#,SPACES      IF GST # ON W4                               
         BNH   PC3455                                                           
         L     R1,SRTGSTPD         PRINT GST PAID                               
         LCR   R1,R1                                                            
         EDIT  (R1),(6,P34MPT),MINUS=YES                                        
         B     PC3457                                                           
*                                                                               
PC3455   CLI   RECNUM,PC           IF NOT PRINT CHECKS                          
         BE    PC3460                                                           
         EDIT  (4,SRTMPT),(6,P34MPT),MINUS=YES     MPTR FUND                    
         CLI   RECNUM,CN                           IF CAN$ CHECKS               
         BNE   PC3457                                                           
         EDIT  (4,SRTCHAR),(9,P34CHAR),MINUS=YES   PERMANENT CHARITIES          
         B     PC3460                                                           
*                                                  ELSE FOR US$ CHECKS          
PC3457   EDIT  (4,SRTDUES),(9,P34CHAR),MINUS=YES   SHOW UNION DUES HERE         
*                                                                               
PC3460   BAS   RE,CALLSPL2                                                      
         BAS   RE,CALLSPL2                                                      
***********************************************************************         
*        PRINT LINE #36                                               *         
***********************************************************************         
         CLI   RECNUM,CN           IF CHECK IS NOT CAN$                         
         BE    PC3620                                                           
         CLI   RECNUM,EC           AND NOT EUROS                                
         BE    PC3620                                                           
         CLI   SRTW4TY,TAW4TYCO    AND PERFORMER NOT PAID AS CORP               
         BE    PC3620                                                           
         CLI   SRTW4TY,TAW4TYCA    AND NOT PAID AS CANADIAN                     
         BE    PC3620                                                           
         CLI   SRTW4TY,TAW4TYTR    AND NOT PAID AS TRUSTEE                      
         BE    PC3620                                                           
         CLI   SRTW4TY,TAW4TYFO    AND NOT PAID AS FOREIGNER                    
         BE    PC3620                                                           
*                                                                               
         ICM   RF,15,SRTPAY        THEN IF PAYMENT AMOUNT IS ZERO               
         BNZ   PC3610              THEN PRINT '000' FOR INDIVIDUAL              
         MVC   P36PAYI+7(3),=C'000'                                             
         B     PC3650                                                           
*                                  ELSE PRINT PAYMENT FOR INDIVIDUAL            
PC3610   EDIT  (4,SRTPAY),(11,P36PAYI),MINUS=YES                                
         B     PC3650                                                           
*                                                                               
PC3620   ICM   RF,15,SRTPAY        ELSE IF PAYMENT AMOUNT IS ZERO               
         BNZ   PC3630              THEN PRINT '000' FOR CORPORATION             
         MVC   P36PAYC+6(3),=C'000'                                             
         B     PC3650                                                           
*                                  ELSE PRINT PAYMENT FOR CORPORATION           
PC3630   EDIT  (4,SRTPAY),(10,P36PAYC),MINUS=YES                                
*                                                                               
*                                  REIMBURSED EXPENSES                          
PC3650   EDIT  (4,SRTREXP),(10,P36REXP),MINUS=YES                               
*                                                                               
         L     RF,SRTDUE                                                        
         A     RF,SRTDUETR                                                      
         A     RF,SRTDUENR                                                      
         ST    RF,FULL                                                          
         EDIT  (4,FULL),(10,P36DUE),MINUS=YES                                   
*                                  DUE COMPANY                                  
*        EDIT  (4,SRTDUE),(10,P36DUE),MINUS=YES                                 
*                                                                               
         OC    SRTEARN,SRTEARN     IF TAXABLE EARNINGS IS ZERO                  
         BNZ   *+14                THEN PRINT '000'                             
         MVC   P36EARN+6(3),=C'000'                                             
         B     PC3670                                                           
*                                  ELSE PRINT TAXABLE EARNINGS                  
         EDIT  (4,SRTEARN),(10,P36EARN),MINUS=YES                               
*                                                                               
PC3670   L     R1,SRTMDED          MISC. DEDUCTIONS                             
         CLI   RECNUM,CN           IF NOT CAN$ CHECKS                           
         BE    PC3675                                                           
         CLI   RECNUM,EC           OR EURO CHECKS                               
         BE    PC3675                                                           
         L     R1,SRTTDED          PRINT TOTAL DEDUCTIONS HERE                  
PC3675   EDIT  (R1),(10,P36TDED),MINUS=YES                                      
*                                  TAX/LIENS GARNISHMENTS & W4 TRUSTEE          
         L     R1,SRTTRST                                                       
         LTR   R1,R1                                                            
         BNZ   *+8                                                              
         L     R1,SRTLIEN                                                       
         EDIT  (R1),(10,P36LIEN),MINUS=YES                                      
*                                                                               
         L     R1,SRTNET             PRINT CHECK AMOUNT                         
         LTR   R1,R1                 IF AMOUNT IS NOT ZERO                      
         BNZ   PC3680                                                           
         L     R1,SRTDIRCT           ELSE PRINT DIRECT DEPOSIT                  
         LTR   R1,R1                 IF AMOUNT IS NOT ZERO                      
         BNZ   PC3680                                                           
         L     R1,SRTWIRE            ELSE PRINT WIRE TRANSFER                   
         LTR   R1,R1                 IF AMOUNT IS NOT ZERO                      
         BNZ   PC3680                                                           
         MVC   P36AMNT+6(3),=C'000'  ELSE PRINT '000'                           
         B     PC3690                                                           
*                                                                               
PC3680   EDIT  (R1),(10,P36AMNT),MINUS=YES                                      
*                                                                               
PC3690   BAS   RE,CALLSPL2                                                      
         BAS   RE,CALLSPL2                                                      
***********************************************************************         
*        PRINT LINE #38                                               *         
***********************************************************************         
*                                                                               
         CLI   RECNUM,CN           CAN$ CHECKS                                  
         BNE   PC3805                                                           
         CLC   SRTGST#,SPACES      IF PERFORMER HAS GST NUMBER                  
         BNH   PC3802                                                           
*                                  GOODS AND SERVICES TAX                       
         L     R1,SRTGSTPD                                                      
         LCR   R1,R1                                                            
         EDIT  (R1),(11,P38GST),MINUS=YES                                       
*                                                                               
PC3802   OC    SRTDUES,SRTDUES     IF UNION DUES                                
         BZ    PC3803                                                           
         MVC   P38DTXT,LTDUES      PRINT 'UNION DUES WITHHELD'                  
         EDIT  (4,SRTDUES),(10,P38DUES),2,MINUS=YES,ALIGN=LEFT                  
PC3803   B     PC3890              NO OTHER TAXES FOR CAN$                      
*                                                                               
*                                  FEDERAL TAX                                  
PC3805   EDIT  (4,SRTFTAX),(9,P38FTAX),MINUS=YES                                
*                                  FICA                                         
         EDIT  (4,SRTFICA),(9,P38FICA),MINUS=YES                                
*                                  FOR US$ MISC DEDS BOX                        
         L     R1,SRTMDED          MISCELLANEOUS DEDUCTIONS                     
*NO-OP   A     R1,SRTDUES          AND UNION DUES                               
         EDIT  (R1),(7,P38MDED),MINUS=YES                                       
*                                                                               
         OC    SRTWTAX,SRTWTAX     SOW : STATE OF WORK TAX                      
         BZ    PC3810                                                           
         MVC   P38SOW1,SRTSOW                                                   
         EDIT  (4,SRTWTAX),(8,P38WTAX),MINUS=YES                                
*                                                                               
PC3810   OC    SRTRTAX,SRTRTAX     SOR : STATE OF RESIDENCE TAX                 
         BZ    PC3820                                                           
         MVC   P38SOR1,SRTSOR                                                   
         EDIT  (4,SRTRTAX),(8,P38RTAX),MINUS=YES                                
*                                                                               
PC3820   OC    SRTSUI,SRTSUI       SOW : STATE UNEMPLOYMENT INSURANCE           
         BZ    PC3830                                                           
         MVC   P38SOW2,SRTTXST                                                  
         EDIT  (4,SRTSUI),(9,P38SUI),MINUS=YES                                  
*                                  LOCAL TAX                                    
PC3830   EDIT  (4,SRTLTAX),(7,P38LTAX),MINUS=YES                                
*                                  CANADIAN TAX                                 
         EDIT  (4,SRTCTAX),(7,P38CTAX),MINUS=YES                                
*                                                                               
         OC    SRTSDI,SRTSDI       SOW : STATE DISABILITY                       
         BZ    PC3890                                                           
         MVC   P38SOW3,SRTTXST                                                  
         EDIT  (4,SRTSDI),(6,P38SDI),MINUS=YES                                  
*                                                                               
PC3890   BAS   RE,CALLSPL2                                                      
         BAS   RE,CALLSPL2                                                      
***********************************************************************         
*        PRINT LINE #40                                               *         
***********************************************************************         
         TM    SRTBITS,SRTBNYTD    IF YTD AMOUNTS SHOULDN'T PRINT               
         BO    PC4090              THEN SKIP                                    
*                                                                               
         ICM   RF,15,SRTYEARN      IF YTD TAXABLE EARNINGS IS ZERO              
         BNZ   *+14                THEN PRINT '000'                             
         MVC   P40YEARN+8(3),=C'000'                                            
         B     PC4020                                                           
*                                  ELSE PRINT YTD TAXABLE EARNINGS              
         EDIT  (4,SRTYEARN),(12,P40YEARN),MINUS=YES                             
*                                                                               
PC4020   L     RF,SRTYNTAX         ELSE IF YTD NON-TAXABLE EARNINGS IS          
         S     RF,SRTYREX                                                       
         BP    *+14                ZERO THEN PRINT '000'                        
         MVC   P40YNTAX+6(3),=C'000'                                            
         B     PC4050                                                           
*                                                                               
         EDIT  (RF),(9,P40YNTAX)   ELSE PRINT YTD NON-TAXABLE EARNINGS          
*                                                                               
*                                  YTD REIMBURSED EXPENSES                      
PC4050   EDIT  (4,SRTYREX),(10,P40YREX),MINUS=YES                               
*                                                                               
         CLI   RECNUM,CN           FOR CAN$ CHECKS                              
         BNE   PC4060                                                           
         L     R1,SRTYGST          YTD GOODS AND SERVICES TAX                   
         LCR   R1,R1                                                            
         EDIT  (R1),(11,P40YGST),MINUS=YES                                      
         MVC   AIO,ASRTCHK                                                      
         GOTO1 CHAROUT,DMCB,TANUELQ,0,TANUTMEM  GET MEMBERSHIP NUMBER           
         MVC   AIO,AIO1                                                         
         BNE   PC4090                                                           
         MVC   P40MTXT,LTMEM       PRINT 'MEMBERSHIP NUMBER' TAG                
         MVC   P40MEM,TGNAME       PRINT THE NUMBER                             
         B     PC4090              THAT'S IT FOR CAN$ CHECKS                    
*                                                                               
*                                  YTD FEDERAL TAX                              
PC4060   EDIT  (4,SRTYFED),(10,P40YFED),MINUS=YES                               
*                                  YTD FICA                                     
         EDIT  (4,SRTYFIC),(10,P40YFIC),MINUS=YES                               
*                                  YTD STATE TAX                                
         EDIT  (4,SRTYSTA),(10,P40YSTA),MINUS=YES                               
*                                  YTD LOCAL TAX                                
         EDIT  (4,SRTYLOC),(10,P40YLOC),MINUS=YES                               
*                                  YTD CANADIAN TAX                             
         EDIT  (4,SRTYCAN),(10,P40YCAN),MINUS=YES                               
*                                                                               
PC4090   LA    R2,5                PRINT LINE #40 AND 4 BLANK LINES             
         BAS   RE,CALLSPL2                                                      
         BCT   R2,*-4                                                           
***********************************************************************         
*        PRINT LINES #45-46                                           *         
***********************************************************************         
*                                  CHECK DATE                                   
         GOTO1 CONVDATE,DMCB,SRTCDTE,P45DTE,1                                   
*                                                                               
         MVC   P45CHKN,CHKNCHR     CHECK NUMBER                                 
*                                                                               
         BAS   RE,CALLSPL2         PRINT LINES #45 AND #46                      
         BAS   RE,CALLSPL2                                                      
***********************************************************************         
*        PRINT LINES #47-50                                           *         
***********************************************************************         
         MVC   P47PREF,=C'PAY'                                                  
         MVC   P48PREF,=C'EXACTLY'                                              
*                                  CHECK AMOUNT IN WORDS                        
         GOTO1 =V(NUMTOLET),DMCB,(0,SRTNET),BLOCK                               
         L     R2,0(R1)                                                         
         LA    R1,BLOCK(R2)                                                     
         MVC   0(4,R1),=C'/100'                                                 
         LA    R2,4(R2)                                                         
         LA    R0,L'P48WORD                                                     
         GOTO1 CHOPPER,DMCB,((R2),BLOCK),((R0),P48WORD),(C'P',2)                
*                                                                               
         CLI   11(R1),1            IF IT FIT ON ONE LINE                        
         BH    PC4950                                                           
         MVC   P49,P48             SLIDE EVERYTHING DOWN ONE LINE               
         MVC   P48,P47                                                          
         MVC   P47,SPACES          AND CLEAR LINE 47                            
*                                                                               
PC4950   ICM   RF,15,SRTNET        IF CHECK AMOUNT IS ZERO                      
         BNZ   *+14                THEN PRINT '000'                             
         MVC   P49AMNT(3),=C'000'                                               
         B     PC4990                                                           
*                                  ELSE JUST PRINT CHECK AMOUNT                 
         EDIT  (4,SRTNET),(9,P49AMNT),FLOAT=-,FILL=*                            
*                                                                               
PC4990   BAS   RE,CALLSPL2         PRINT LINES #47-#49                          
*                                                                               
         ICM   RF,15,SRTNET        IF CHECK AMOUNT IS NOT POSITIVE              
         BP    *+10                THEN PRINT '* NOT NEGOTIABLE *'              
         MVC   P50NNEG,=C'* NOT NEGOTIABLE *'                                   
*                                                                               
         BAS   RE,CALLSPL2         PRINT LINE #50                               
***********************************************************************         
*        PRINT LINES #51-56                                           *         
***********************************************************************         
         L     RE,AIO              CLEAR ADDRESS LINES BLOCK                    
         LA    RF,132*8                                                         
         SR    R1,R1                                                            
         MVCL  RE,R0                                                            
*                                                                               
         L     R5,AIO              R5 = A(ADDRESS LINES BLOCK)                  
         LA    R4,132*7(R5)        R4 = A(END OF 7 LINE PRINT BLOCK)            
*                                                                               
*                                  PUT W4 NAME IN BLOCK                         
         MVC   P51CADD,SRTNAME                                                  
         LA    R5,132(R5)                                                       
*                                                                               
         OC    SRTTSSN,SRTTSSN     TRUSTEE?                                     
         BZ    PC5100                                                           
*                                                                               
         BAS   RE,CKLIEN           SKIP, IF THIS IS A LIEN                      
         BE    PC5100                                                           
*                                                                               
         MVC   P51CADD(13),=CL13'IN TRUST FOR '                                 
         MVC   P51CADD+13(32),COMMNAME+8                                        
         LA    R5,132(R5)                                                       
*                                                                               
PC5100   OC    SRTFSO,SRTFSO       IF FSO EXISTS                                
         BZ    PC5110                                                           
*                                                                               
         MVC   P51CADD(3),=C'FSO'  THEN PUT FSO NAME IN BLOCK                   
         MVC   P51CADD+4(32),SRTFSON                                            
         LA    R5,132(R5)                                                       
*                                                                               
PC5110   LA    R2,SRTADD           R2 = A(FIRST W4 ADDRESS LINE)                
*                                                                               
         OC    SRTPNAM,SRTPNAM     IF PAYEE NAME GIVEN                          
         BZ    PC5140                                                           
         MVC   P51CADD(3),=C'C/O'  THEN PUT 'C/O' PAYEE NAME IN BLOCK           
         MVC   P51CADD+4(32),SRTPNAM                                            
         LA    R5,132(R5)                                                       
*                                                                               
PC5140   LA    R0,3                LOOP TROUGH 3 ADDRESS LINES                  
*                                                                               
PC5150   CLC   0(30,R2),SPACES     IF ADDRESS LINE IS NOT CLEAR                 
         BNH   *+14                                                             
         MVC   P51CADD(30),0(R2)   THEN PUT IT IN BLOCK                         
         LA    R5,132(R5)                                                       
         LA    R2,30(R2)           BUMP TO NEXT ADDRES LINE                     
         BCT   R0,PC5150           REPEAT UNTIL NO MORE ADDRESS LINES           
*                                                                               
         CLC   0(39,R2),SPACES     IF HAVE CITY, STATE, ZIP                     
         BNH   *+14                                                             
         MVC   P51CADD(39),0(R2)   THEN PUT IT IN BLOCK                         
         LA    R5,132(R5)                                                       
*                                                                               
         CR    R5,R4               IF THERE ARE 7 LINES IN THE BLOCK            
         BNE   PC5160                                                           
         L     R5,AIO              THEN SQUASH THE FIRST 2 TOGETHER             
         GOTO1 SQUASHER,DMCB,P51CADD,132+32                                     
         LA    RE,132(R5)          AND MOVE THE NEXT 5 PLUS THE BLANK           
         LA    RF,132*6                LINE AT THE END UP ONE LINE              
         LA    R0,132(RE)                                                       
         LR    R1,RF                                                            
         MVCL  RE,R0                                                            
*                                                                               
PC5160   L     R5,AIO              R5 = A(FIRST PRINT LINE IN BLOCK)            
*                                                                               
PC5170   OC    0(132,R5),0(R5)     WHILE PRINT LINE IS NOT CLEAR                
         BZ    PC5190                                                           
         MVC   P,0(R5)             PRINT IT                                     
         BAS   RE,CALLSPL2                                                      
         LA    R5,132(R5)          BUMP TO NEXT PRINT LINE                      
         B     PC5170              LOOP BACK                                    
*                                                                               
PC5190   MVI   LINE,X'99'          FORCE PAGE EJECT                             
*                                                                               
*---------------------------------------------------------------------          
PCX      B     XIT2                                                             
         EJECT                                                                  
*                                                                               
* THIS ROUTINE RETURNS A POSITIVE CC IF A LIEN WITHHLD ELEMENT EXISTS           
*                                                                               
CKLIEN   NTR1                                                                   
         L     R4,ASRTCHK          FIND LIEN WITHHOLDING ELEMENT                
         MVI   ELCODE,TALWELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   NO2                                                              
         B     YES2                                                             
         EJECT                                                                  
*                                                                               
* THIS ROUTINE CONVERTS THE INPUT DATE FORMAT TO THE OUTPUT DATE FORMAT         
* IN THE MANNER AS REQUESTED BY THE CONVERSION TYPE PARAMETER.                  
*                                                                               
*        PARM 1 = A(INPUT DATE)                                                 
*        PARM 2 = A(OUTPUT DATE)                                                
*        PARM 3 = 1 - CONVERT PL3 TO 'MM DD YY'                                 
*                 2 - CONVERT PL3 TO 'MM DD'                                    
*                                                                               
CONVDATE NTR1                                                                   
         LM    R2,R3,0(R1)         R2 = A(INPUT DATE)                           
*                                  R3 = A(OUTPUT DATE)                          
         MVC   BYTE,11(R1)         BYTE = CONVERSION TYPE                       
*                                                                               
         OC    0(3,R2),0(R2)       IF ZERO INPUT DATE THEN RETURN               
         BZ    CVX                                                              
*                                                                               
*                                  SET UP 'MM DD YY' IN DUB                     
         GOTO1 DATCON,DMCB,(1,(R2)),(X'20',WORK)                                
         MVC   DUB,SPACES                                                       
         MVC   DUB(2),WORK+2                                                    
         MVC   DUB+3(2),WORK+4                                                  
         MVC   DUB+6(2),WORK                                                    
*                                                                               
         CLI   DUB,C'0'            IF FIRST DIGIT IN MONTH IS '0'               
         BNE   *+8                                                              
         MVI   DUB,C' '            THEN CLEAR IT                                
*                                                                               
         CLI   BYTE,1              IF CONVERSION TYPE IS 1                      
         BNE   CV10                                                             
         MVC   0(8,R3),DUB         THEN RETURN 'MM DD YY'                       
         B     CVX                                                              
*                                                                               
CV10     CLI   BYTE,2              ELSE IF CONVERSION TYPE IS 2                 
         BNE   CV20                                                             
         MVC   0(5,R3),DUB         THEN RETURN 'MM DD'                          
         B     CVX                                                              
*                                                                               
CV20     DC    H'0'                ELSE INVALID CONVERSION TYPE                 
*                                                                               
CVX      B     XIT2                                                             
         EJECT                                                                  
* THIS ROUTINE BUILDS AND PRINTS THE COMMENT FOR THE DUE COMPANY                
* WITHHOLDING ELEMENT POINTED BY R4.                                            
*                                                                               
DUECOMM  NTR1                                                                   
         USING TADWD,R4                                                         
         MVI   BLOCK,C' '          PRE-SET OUTPUT BLOCK TO SPACES               
         MVC   BLOCK+1(179),BLOCK                                               
*                                  MOVE AMOUNT RECOVERED TO COMMENT             
         EDIT  (4,TADWREC),(12,BLOCK),2,ALIGN=LEFT,ZERO=NOBLANK                 
*                                                                               
*                                  MOVE NARRATIVE TO COMMENT                    
         MVC   BLOCK+14(35),DEDCOMM                                             
         MVC   BLOCK+49(29),=C'BECAUSE OF AN OVERPAYMENT FOR'                   
*                                                                               
*                                  MOVE REFERENCE NUMBER TO COMMENT             
         MVC   BLOCK+120(8),=C'REF='                                            
         MVC   BLOCK+124(6),TADWDUC                                             
*                                                                               
         CLI   TADWDUC,X'FA'       IS IT A YEAR 2000 DATE?                      
         BL    DUEC2               NO                                           
*                                                                               
         MVC   WORK(4),TADWDUC     YES, CONVERT IT                              
         MVC   WORK+4(2),=C'01'                                                 
         GOTO1 DATCON,DMCB,(0,WORK),(X'20',WORK)                                
         MVC   BLOCK+124(4),WORK                                                
         MVC   BLOCK+128(2),TADWDUC+4                                           
*                                                                               
DUEC2    LA    RF,BLOCK+130                                                     
         BAS   RE,BACKRF                                                        
         MVI   1(RF),C'.'                                                       
*                                  MOVE REMAINING BALANCE TO COMMENT            
         MVC   BLOCK+140(8),=C'REM BAL='                                        
         LA    R2,BLOCK+148                                                     
         EDIT  (4,TADWBAL),(11,(R2)),2,ALIGN=LEFT,ZERO=NOBLANK                  
         LA    RF,BLOCK+159                                                     
         BAS   RE,BACKRF                                                        
         MVI   1(RF),C'.'                                                       
*                                                                               
         XC    COMMNAME,COMMNAME   CLEAR FAKE FIELD HEADER FOR NAME             
         MVI   COMMNAME,36+8                                                    
*                                                                               
         LA    R3,KEY              BUILD DUE COMPANY KEY                        
         USING TLDUD,R3                                                         
         XC    KEY,KEY                                                          
         MVI   TLDUCD,TLDUCDQ                                                   
         MVC   TLDUSSN,SRTSSN                                                   
         MVC   TLDUDUC,TADWDUC                                                  
*                                                                               
         GOTO1 HIGH                READ DUE COMPANY KEY                         
         CLC   TLDUKEY,KEYSAVE                                                  
         BNE   DC10                SKIP IF NOT FOUND                            
*                                                                               
         GOTO1 GETREC              GET RECORD                                   
*                                                                               
         L     R4,AIO              R4 = A(DUE COMPANY DETAILS ELEMENT)          
         MVI   ELCODE,TADUELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   DC10                                                             
         USING TADUD,R4                                                         
*                                                                               
         OC    TADUCINV,TADUCINV   MOVE CREDIT INV NUM INTO COMMENT             
         BZ    DC5                                                              
         MVC   BLOCK+161(7),=C'CR INV='                                         
         GOTO1 TINVCON,DMCB,TADUCINV,BLOCK+168,DATCON                           
         MVI   BLOCK+174,C'.'                                                   
*                                  READ AGENCY RECORD AND EXTRACT NAME          
DC5      GOTO1 RECVAL,DMCB,TLAYCDQ,(X'88',TADUAGY),COMMNAME                     
*                                                                               
*                                  MOVE NAME INTO COMMENT                       
DC10     MVC   BLOCK+79(36),COMMNAME+8                                          
         LA    RF,BLOCK+115                                                     
         BAS   RE,BACKRF                                                        
         MVI   1(RF),C'.'                                                       
*                                  SQUASH AND CHOP INTO 80 BYTE BLOCKS          
         GOTO1 SQUASHER,DMCB,BLOCK,180                                          
         GOTO1 CHOPPER,DMCB,(180,BLOCK),(80,ELEM),3                             
         L     R3,8(R1)                                                         
         M     R2,=F'80'                                                        
*                                  PRINT COMMENT                                
         GOTO1 ADDCOMM,DMCB,ELEM,(R3)                                           
*                                                                               
DCX      B     XIT2                                                             
         SPACE 3                                                                
* THIS ROUTINE BUILDS AND PRINTS THE COMMENT FOR LIFTED FROM ON CAST            
*                                                                               
LIFTCOMM NTR1                                                                   
         USING TACAD,R4                                                         
         MVC   AIO,AIO2                                                         
         MVC   FULL,SRTCOM       SAVE INTERNAL COMM ID                          
         GOTO1 RECVAL,DMCB,TLCOCCDQ,(X'A4',TACALFTF)                            
         BNE   XIT2                                                             
         DROP  R4                                                               
         L     R4,AIO2                                                          
         MVI   ELCODE,TACOELQ      GET COMM DETAILS ELEMENT                     
         BRAS  RE,GETEL                                                         
         USING TACOD,R4                                                         
         XC    BLOCK(25),BLOCK     PRE-CLEAR COMMENT AREA                       
         MVC   BLOCK(13),=CL13'LIFTED FROM: '                                   
         MVC   BLOCK+13(L'TACOCID),TACOCID                                      
         DROP  R4                                                               
         GOTO1 RECVAL,DMCB,TLCOCCDQ,(X'A4',FULL) RESTORE INT. CID               
         BE    *+6                                                              
         DC    H'00'                                                            
         MVC   AIO,AIO1                                                         
*                                  PRINT COMMENT                                
         GOTO1 ADDCOMM,DMCB,BLOCK,25                                            
         B     XIT2                                                             
         EJECT                                                                  
* THIS ROUTINE BUILDS AND PRINTS THE COMMENT FOR THE LIEN WITHHOLDING           
* ELEMENT POINTED BY R4.                                                        
*                                                                               
LINCOMM  NTR1                                                                   
         USING TALWD,R4                                                         
         MVI   BLOCK,C' '          PRE-SET OUTPUT BLOCK TO SPACES               
         MVC   BLOCK+1(194),BLOCK                                               
         XC    COMMNAME,COMMNAME   CLEAR FAKE FIELD HEADER FOR NAME             
         MVI   COMMNAME,36+8                                                    
*                                                                               
         ICM   RF,15,TALWREC       AMOUNT RECOVERED                             
         BNP   LC20                THIS IS THE LIEN PAYEE CHECK                 
*                                                                               
*                                  MOVE AMOUNT RECOVERED TO COMMENT             
         EDIT  (4,TALWREC),(12,BLOCK),2,ALIGN=LEFT,ZERO=NOBLANK                 
*                                                                               
*                                  MOVE NARRATIVE TO COMMENT                    
         MVC   BLOCK+14(35),DEDCOMM                                             
         MVC   BLOCK+49(22),=C'BECAUSE OF A LIEN FROM'                          
*                                                                               
*                                  MOVE REFERENCE NUMBER TO COMMENT             
         MVC   BLOCK+120(8),=C'REF='                                            
         MVC   BLOCK+124(6),TALWLIN                                             
         LA    RF,BLOCK+130                                                     
         BAS   RE,BACKRF                                                        
         MVI   1(RF),C'.'                                                       
*                                  MOVE REMAINING BALANCE TO COMMENT            
         MVC   BLOCK+140(8),=C'REM BAL='                                        
         LA    R2,BLOCK+148                                                     
         EDIT  (4,TALWBAL),(11,(R2)),2,ALIGN=LEFT,ZERO=NOBLANK                  
         LA    RF,BLOCK+159                                                     
         BAS   RE,BACKRF                                                        
         MVI   1(RF),C'.'                                                       
*                                                                               
         MVC   TGSSN,SRTSSN        READ LIEN RECORD                             
         GOTO1 RECVAL,DMCB,TLLNCDQ,(X'A0',TALWLIN)                              
         BNE   LC10                                                             
*                                                                               
         L     R4,AIO              R4 = A(LIEN DETAILS ELEMENT)                 
         MVI   ELCODE,TALNELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   LC10                                                             
         USING TALND,R4                                                         
         CLC   TALNTYPE,=AL2(TALNTYGT) IF GROSS TRUSTEE                         
         BNE   LC5                                                              
         MVC   BLOCK+140(20),SPACES   CLEAR REMAINING BALANCE                   
         MVC   BLOCK+49(22),=C'AND PAID TO TRUSTEE - '                          
*                                                                               
*                                  READ LIEN PAYEE W4 REC AND EXTR NAME         
LC5      GOTO1 RECVAL,DMCB,TLW4CDQ,(X'88',TALNPAYE),COMMNAME                    
*                                  MOVE NAME INTO COMMENT                       
LC10     MVC   BLOCK+72(36),COMMNAME+8                                          
         LA    RF,BLOCK+108                                                     
         BAS   RE,BACKRF                                                        
         MVI   1(RF),C'.'                                                       
         B     LC30                                                             
*                                                                               
*                                  MOVE NARRATIVE TO COMMENT                    
LC20     MVC   BLOCK(20),=C'LIEN AMOUNT PAID FOR'                               
         MVC   BLOCK+158(20),=C'LIEN ORDER ATTACHED.'                           
*                                                                               
         USING TALWD,R4                                                         
         LR    R0,R4               SAVE A(LIEN WITHHOLDING ELEMENT)             
         MVC   TGSSN,SRTLSSN       READ LIEN RECORD                             
         GOTO1 RECVAL,DMCB,TLLNCDQ,(X'A0',TALWLIN)                              
         BNE   LC25                                                             
*                                                                               
         L     R4,AIO              R4 = A(LIEN DETAILS ELEMENT)                 
         MVI   ELCODE,TALNELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   LC22                                                             
         USING TALND,R4                                                         
         CLC   TALNTYPE,=AL2(TALNTYGT) IF GROSS TRUSTEE                         
         BNE   LC22                                                             
         MVC   BLOCK+158(20),SPACES  CLEAR LIEN ORDER ATTACHED WORDING          
         MVC   BLOCK(20),=C'AMOUNT IN TRUST FOR '                               
*                                                                               
         USING TACMD,R4                                                         
LC22     MVI   ELCODE,TACMELQ                                                   
         GOTO1 GETL,DMCB,(1,=AL1(TACMTYPL))                                     
         BNE   LC25                                                             
         L     R4,TGELEM                                                        
         ZIC   RE,TACMLEN                                                       
         SHI   RE,TACMLNQ+1                                                     
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   BLOCK+180(0),TACMCOMM                                            
         DROP  R4                                                               
*                                  MOVE REFERENCE NUMBER TO COMMENT             
         USING TALWD,R4                                                         
LC25     LR    R4,R0               RESTORE A(LIEN WITHHOLDING ELEMENT)          
         MVC   BLOCK+120(8),=C'REF='                                            
         MVC   BLOCK+124(6),TALWLIN                                             
         LA    RF,BLOCK+130                                                     
         BAS   RE,BACKRF                                                        
         MVI   1(RF),C'.'                                                       
*                                                                               
*                                  MOVE SSN OF PAYER TO COMMENT                 
         L     R4,ASRTCHK                                                       
         USING TLCKD,R4                                                         
         MVC   BLOCK+100(3),=C'SS#'                                             
         MVC   BLOCK+104(9),SRTLSSN                                             
         LA    RF,BLOCK+115                                                     
         BAS   RE,BACKRF                                                        
         MVI   1(RF),C'.'                                                       
*                                                                               
*                                  READ LIEN PAYER W4 REC AND EXTR NAME         
         GOTO1 RECVAL,DMCB,TLW4CDQ,(X'88',SRTLSSN),COMMNAME                     
*                                                                               
*                                  MOVE NAME INTO COMMENT                       
         MVC   BLOCK+50(36),COMMNAME+8                                          
         LA    RF,BLOCK+86                                                      
         BAS   RE,BACKRF                                                        
         MVI   1(RF),C'.'                                                       
*                                                                               
*                                  SQUASH AND CHOP INTO 80 BYTE BLOCKS          
LC30     GOTO1 SQUASHER,DMCB,BLOCK,195                                          
         GOTO1 CHOPPER,DMCB,(195,BLOCK),(80,ELEM),3                             
         L     R3,8(R1)                                                         
         M     R2,=F'80'                                                        
*                                  PRINT COMMENT                                
         GOTO1 ADDCOMM,DMCB,ELEM,(R3)                                           
*                                                                               
LCX      B     XIT2                                                             
         EJECT                                                                  
* THIS ROUTINE BUILDS AND PRINTS THE COMMENT FOR W4 TRUSTEE DEDUCTION           
*                                                                               
         USING TAODD,R4                                                         
TRSTCOMM NTR1                                                                   
         MVI   BLOCK,C' '          PRE-SET OUTPUT BLOCK TO SPACES               
         MVC   BLOCK+1(179),BLOCK                                               
         XC    COMMNAME,COMMNAME   CLEAR FAKE FIELD HEADER FOR NAME             
         MVI   COMMNAME,36+8                                                    
*                                                                               
         ICM   RF,15,TAODAMT       AMOUNT RECOVERED                             
         LR    R5,RF                                                            
         BNP   TC20                THIS IS THE TRUSTEE CHECK                    
*                                                                               
         EDIT  (4,TAODAMT),(12,BLOCK),2,ALIGN=LEFT,ZERO=NOBLANK                 
         MVC   BLOCK+14(35),=CL35'WAS DEDUCTED FROM YOUR GROSS WAGES'           
         MVC   BLOCK+49(25),=C'PER CALIFORNIA COOGAN LAW'                       
*                                                                               
         CLC   SRTSOW,=C'NY '      IF LIVE OR WORK IN NEW YORK                  
         BE    TC05                                                             
         CLC   SRTW4SOR,=C'NY '    (STATE OF RESIDENCE IF INDIVIDUAL            
         BE    TC05                PAID AS CORP)                                
         CLC   SRTSOR,=C'NY '                                                   
         BNE   *+16                                                             
TC05     MVC   BLOCK+49(25),=C'PER NY CHILD PERFORMER ED'                       
         MVC   BLOCK+74(29),=C'UCATION AND TRUST ACT OF 2003'                   
         CLC   SRTCAT,=C'PM '                                                   
         BNE   TC06                                                             
         MVC   BLOCK+103(23),=C' AND AMENDED AS OF 2013'                        
*                                                                               
TC06     MVC   AIO,ASRTCHK                                                      
         MVI   ELCODE,TANUELQ                                                   
         GOTO1 GETL,DMCB,(1,=AL1(TANUTRST))                                     
         MVC   AIO,AIO1                                                         
         BNE   TC10                                                             
         XC    BLOCK+74(29),BLOCK+74                                            
         XC    BLOCK+103(23),BLOCK+103                                          
         MVC   BLOCK+49(25),=C'AND PAID TO TRUSTEE -    '                       
         L     R4,TGELEM                                                        
         USING TANUD,R4                                                         
         GOTO1 RECVAL,DMCB,TLW4CDQ,(X'88',TANUMBER),COMMNAME                    
         MVC   BLOCK+72(36),COMMNAME+8                                          
TC10     LA    RF,BLOCK+126                                                     
         BAS   RE,BACKRF                                                        
         MVI   1(RF),C'.'                                                       
         B     TC30                                                             
*                                                                               
         USING TAODD,R4                                                         
TC20     TM    SRTADJS,TAPDADTR    IF NOT REFUND CHECK TO TRUSTEE               
         BZ    TC25                                                             
         MVC   AIO,ASRTCHK                                                      
         MVI   ELCODE,TANUELQ                                                   
         GOTO1 GETL,DMCB,(1,=AL1(TANUTRST))                                     
         MVC   AIO,AIO1                                                         
         BNE   TCX                                                              
         OC    SRTNET,SRTNET                                                    
         BZ    TCX                 XIT - MINOR CHECK                            
*                                  FOR TRUSTEE CHECK                            
TC25     MVC   BLOCK(20),=C'AMOUNT IN TRUST FOR '                               
         L     R4,ASRTCHK          MOVE MINOR PAYER SSN TO COMMENT              
         USING TLCKD,R4                                                         
         MVC   BLOCK+50(3),=C'SS#'                                              
         LA    RF,BLOCK+54                                                      
         CLC   SRTUN,=C'ACT'       ACTRA UNION                                  
         BNE   *+10                                                             
         MVC   BLOCK+50(4),=C'SIN#'                                             
         LA    RF,1(RF)                                                         
*                                                                               
         MVC   0(9,RF),SRTTSSN                                                  
         LA    RF,9(RF)                                                         
         BAS   RE,BACKRF                                                        
         MVI   1(RF),C'.'                                                       
*                                  READ W4 REC AND EXTR NAME                    
         GOTO1 RECVAL,DMCB,TLW4CDQ,(X'88',SRTTSSN),COMMNAME                     
         MVC   BLOCK+70(36),COMMNAME+8                                          
         LA    RF,BLOCK+106                                                     
         BAS   RE,BACKRF                                                        
         MVI   1(RF),C'.'                                                       
*                                  SQUASH AND CHOP INTO 80 BYTE BLOCKS          
TC30     GOTO1 SQUASHER,DMCB,BLOCK,180                                          
         GOTO1 CHOPPER,DMCB,(180,BLOCK),(80,ELEM),3                             
         L     R3,8(R1)                                                         
         M     R2,=F'80'                                                        
         GOTO1 ADDCOMM,DMCB,ELEM,(R3) PRINT COMMENT                             
*                                                                               
         LTR   R5,R5               AMOUNT RECOVERED                             
         BP    TCX                 THIS IS THE TRUSTEE CHECK                    
*                                                                               
         L     R4,AIO                                                           
         MVI   ELCODE,TAA2ELQ      ADDRESS ELEMENT                              
         BRAS  RE,GETEL                                                         
         BNE   TCX                                                              
         LR    R3,R4                                                            
         USING TAA2D,R3                                                         
         LA    R2,TAA2ADD1                                                      
*                                                                               
         LHI   R4,3                   SEND DOWN 3 LINES OF ADDRESS              
         CLI   TAA2LEN,TAA2LNQ        FOR OLD STYLE ADDRESSES                   
         BL    TC40                                                             
         CLC   TAA2CTRY,=C'US'        AND NEW STYLE US ADDRESSES                
         BE    TC40                                                             
         LHI   R4,2                   ELSE SEND DOWN 2                          
TC40     CLC   0(30,R2),SPACES                                                  
         BNH   TC45                                                             
         MVC   ELEM,SPACES                                                      
         MVC   ELEM+20(30),0(R2)                                                
         GOTO1 ADDCOMM,DMCB,ELEM,52                                             
TC45     LA    R2,30(R2)                                                        
         BCT   R4,TC40                                                          
*                                                                               
         MVC   WORK(25),TAA2CITY      CITY                                      
         MVC   WORK+26(2),TAA2ST      STATE                                     
         MVC   WORK+29(10),TAA2ZIP    ZIP                                       
         GOTO1 SQUASHER,DMCB,WORK,39  SQUASH IT                                 
         MVC   ELEM,SPACES                                                      
         MVC   ELEM+20(39),WORK                                                 
         GOTO1 ADDCOMM,DMCB,ELEM,61                                             
*                                                                               
         CLI   TAA2LEN,TAA2LNQ        FOR OLD STYLE ADDRESSES                   
         BL    TC45A                                                            
         CLC   TAA2CTRY,=C'US'        AND NEW STYLE US ADDRESSES                
         BE    TC45A                                                            
         GOTO1 VALCTRY,DMCB,(X'80',TAA2CTRY)                                    
         BNE   TC45A                                                            
         MVC   ELEM,SPACES                                                      
         USING CTRYTABD,R1                                                      
         L     R1,TGACTRY                                                       
         ZIC   RE,CTRYDSP                                                       
         LTR   RE,RE                                                            
         BNZ   *+8                                                              
         IC    RE,CTRYLEN                                                       
         SHI   RE,CTRYDESC-CTRYTABD+1                                           
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   ELEM(0),CTRYDESC                                                 
         GOTO1 ADDCOMM,DMCB,ELEM,61                                             
         DROP  R1                                                               
*                                                                               
TC45A    MVI   BLOCK,C' '          PRE-SET OUTPUT BLOCK TO SPACES               
         MVC   BLOCK+1(179),BLOCK                                               
         TM    SRTBITS2,SRTBCATR   CALIFORNIA TRUSTEE?                          
         BZ    TC50                                                             
         MVC   BLOCK(40),=C'THESE FUNDS SUBJECT TO CALIFORNIA FAMILY'           
         MVC   BLOCK+41(18),=C'CODE SECTION 6753.'                              
         GOTO1 ADDCOMM,DMCB,BLOCK,59 PRINT COMMENT                              
*                                                                               
TC50     TM    SRTBITS2,SRTBNYTR   NY TRUSTEE?                                  
         BZ    TCX                                                              
         MVC   BLOCK(31),=C'THESE FUNDS SUBJECT TO NEW YORK'                    
         MVC   BLOCK+32(35),=C'CHILD PERFORMER EDUCATION AND TRUST'             
         MVC   BLOCK+68(12),=C'ACT OF 2003.'                                    
         GOTO1 ADDCOMM,DMCB,BLOCK,80 PRINT COMMENT                              
*                                                                               
TCX      XIT1                                                                   
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
* THIS ROUTINE BUILDS AND PRINTS THE COMMENT FOR THE SOAP EPISODE               
* ELEMENT POINTED BY R4.                                                        
*                                                                               
EPICOMM  NTR1                                                                   
         MVI   BLOCK,C' '          PRE-SET OUTPUT BLOCK TO SPACES               
         MVC   BLOCK+1(239),BLOCK                                               
         MVC   BLOCK+240(240),BLOCK+239                                         
         LA    R5,BLOCK            R5 = OUTPUT BLOCK                            
*                                                                               
         MVC   0(9,R5),=C'EPISODES:'  START COMMENT                             
         LA    R5,10(R5)                                                        
*                                                                               
         USING TASOD,R4                                                         
EC05     ZIC   R0,TASONUM          R0 = N'EPISODE SUB-ELEMENTS                  
         LTR   R0,R0                                                            
         BZ    EC90                                                             
*                                                                               
         LA    R2,TASOSEPI         R2 = A(EPISODE SUB-ELEMENT)                  
         USING TASOSEPI,R2                                                      
*                                                                               
EC10     LH    R1,TASOEPI          CONVERT EPISODE TO CHARACTER                 
         CVD   R1,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  WORK(5),DUB+5(3)                                                 
*                                                                               
         L     R3,AEPITAB          R3 = A(EPISODE TABLE)                        
         USING EPITABD,R3                                                       
         LA    R1,MAXEPIS          R1 = MAX NUMBER OF EPISODE ENTRIES           
*                                                                               
EC20     CLI   0(R3),0             TEST END OF TABLE                            
         BE    EC30                                                             
         CLC   EPIAGY,SRTAGY       MATCH ON AGENCY                              
         BNE   *+14                                                             
         CLC   EPINUM,WORK         AND ON EPISODE NUMBER                        
         BE    EC40                                                             
         LA    R3,EPINEXT          TRY NEXT TABLE ENTRY                         
         BCT   R1,EC20                                                          
*                                                                               
         L     R3,AEPITAB          END OF TABLE - SET TO REUSE BEG.             
         XC    0(10*EPILEN,R3),0(R3)                                            
*                                                                               
EC30     MVC   EPIAGY,SRTAGY       NOT IN TABLE - ADD NEW ENTRY                 
         MVC   EPINUM,WORK                                                      
*                                                                               
         BAS   RE,GETEPI           GET EPISODE RECORD AND FILL IN TABLE         
*                                                                               
EC40     MVC   0(5,R5),EPINUM      PRINT EPISODE NUMBER                         
         LA    R5,5(R5)                                                         
*                                                                               
         TM    TASOSTAT,TASOSH+TASOSB+TASOSS  IF WRITER ROLES DEFINED           
         BZ    EC45                                                             
         MVI   0(R5),C':'                     PRINT THEM                        
         LA    R5,1(R5)                                                         
         TM    TASOSTAT,TASOSH     TEST HEAD WRITER                             
         BZ    *+14                                                             
         MVC   0(5,R5),=C'HEAD+'                                                
         LA    R5,5(R5)                                                         
         TM    TASOSTAT,TASOSB     TEST BREAKDOWN WRITER                        
         BZ    *+14                                                             
         MVC   0(6,R5),=C'BKDWN+'                                               
         LA    R5,6(R5)                                                         
         TM    TASOSTAT,TASOSS     TEST SCRIPT WRITER                           
         BZ    *+14                                                             
         MVC   0(6,R5),=C'SCRPT+'                                               
         LA    R5,6(R5)                                                         
         BCTR  R5,0                ERASE TRAILING '+'                           
         MVI   0(R5),C' '                                                       
*                                                                               
EC45     MVC   TGTHREE,EPIADT      SET TO PRINT AIR DATE                        
         TM    SRTCOSTA,TACOSWDT   IF THIS COMML REQUIRES WORK DATE             
         BZ    *+10                                                             
         MVC   TGTHREE,EPIWDT      SET TO PRINT WORK DATE                       
*                                                                               
         OC    TGTHREE,TGTHREE     IF DATE DEFINED                              
         BZ    EC50                                                             
         MVI   0(R5),C'-'          PRINT IT                                     
         GOTO1 DATCON,DMCB,(1,TGTHREE),(10,1(R5))                               
         MVC   6(3,R5),SPACES      (DON'T PRINT YEAR)                           
         LA    R5,6(R5)                                                         
*                                                                               
EC50     LA    R2,L'TASOSEPI(R2)   BUMP TO NEXT EPISODE SUB-ELEMENT             
         MVI   0(R5),C','          SEPARATE PRINTED ENTRIES WITH COMMA          
         LA    R5,1(R5)                                                         
         BCT   R0,EC10             LOOP                                         
*                                                                               
EC90     MVI   ELCODE,TASOELQ      LOOK FOR ANOTHER SOAP ELEMENT                
         BRAS  RE,NEXTEL                                                        
         BE    EC05                GO PROCESS                                   
*                                                                               
         BCTR  R5,0                                                             
         MVI   0(R5),C' '                                                       
*                                  SQUASH AND CHOP INTO 80 BYTE BLOCKS          
         GOTO1 SQUASHER,DMCB,BLOCK,480                                          
         GOTO1 CHOPPER,DMCB,BLOCK,(80,AIO),6,C'LEN=',480                        
         L     R3,8(R1)                                                         
         M     R2,=F'80'                                                        
*                                  PRINT COMMENT                                
         GOTO1 ADDCOMM,DMCB,AIO,(R3)                                            
*                                                                               
ECX      B     XIT2                                                             
         EJECT                                                                  
*              ROUTINE TO GET EPISODE RECORD AND FILL IN TABLE                  
         SPACE 1                                                                
         USING EPITABD,R3          R3 = A(TABLE ENTRY)                          
GETEPI   NTR1                                                                   
         MVC   TGAGY,SRTAGY        SET TO READ RECORD                           
         GOTO1 RECVAL,DMCB,TLEPCDQ,(X'A0',EPINUM)                               
         BNE   EPIX                                                             
*                                                                               
         L     R4,AIO              R4 = A(EPISODE INFORMATION ELEMENT)          
         MVI   ELCODE,TAEIELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   EPIX                                                             
         USING TAEID,R4                                                         
         MVC   EPIWDT,TAEIWDT      SAVE WORK DATE IN EPISODE TABLE              
         MVC   EPIADT,TAEIADT      AND AIR DATE                                 
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'E',AEPITAB),A(MAXEPIS*EPILEN),=C'EPITAB'         
*                                                                               
EPIX     B     XIT2                                                             
         EJECT                                                                  
* THIS ROUTINE PRINTS THE NAMES OF MODELS WHOSE SSN IS IN TANU ELEMS.           
* THE FIRST ELEMENT IS AT TGELEM.  IT USES AIO2 FOR OUTPUT BLOCK                
*                                                                               
         USING TANUD,R4                                                         
AGTSSN   NTR1                                                                   
         L     R4,TGELEM           R4=A(TANU ELEMENT IN RECORD)                 
         L     RE,AIO2                                                          
         LA    RF,2000             PRE-CLEAR OUTPUT BLOCK                       
         XCEFL                                                                  
         L     R5,AIO2             R5 = A(OUTPUT BLOCK)                         
*                                                                               
         MVC   0(7,R5),=C'MODELS:' START COMMENT                                
         LA    R5,8(R5)                                                         
*                                                                               
AGTS5    GOTO1 RECVAL,DMCB,TLW4CDQ,(X'A0',TANUMBER)                             
         BNE   AGTS15                                                           
         GOTO1 CHAROUT,DMCB,TANAELQ,0                                           
         BNE   AGTS15                                                           
         LA    R0,L'TGNAME                                                      
         GOTO1 SQUASHER,DMCB,TGNAME,(R0)                                        
         L     R0,4(R1)               R0=LENGTH OF STRING W/O BLANKS            
         MVC   0(L'TGNAME,R5),TGNAME  SAVE NAME IN OUTPUT BLOCK                 
         AR    R5,R0                  BUMP R5 PAST END OF STRING                
         MVI   0(R5),C','             PUT COMMA BETWEEN NAMES                   
         LA    R5,2(R5)               SKIP A SPACE                              
*                                                                               
AGTS15   MVI   ELCODE,TANUELQ                                                   
         BRAS  RE,NEXTEL           GET NEXT TANU ELEMENT                        
         BNE   AGTS20                                                           
         CLI   TANUTYPE,TANUTCST   TEST CORRECT TYPE                            
         BE    AGTS5                                                            
*                                                                               
AGTS20   SH    R5,=H'2'                                                         
         MVI   0(R5),0                                                          
*                                  SQUASH AND CHOP INTO 80 BYTE BLOCKS          
         GOTO1 SQUASHER,DMCB,AIO2,2000                                          
         GOTO1 CHOPPER,DMCB,AIO2,(80,AIO1),25,C'LEN=',2000                      
         L     R3,8(R1)                                                         
         M     R2,=F'80'                                                        
*                                  PRINT COMMENT                                
         GOTO1 ADDCOMM,DMCB,AIO1,(R3)                                           
*                                                                               
         B     XIT2                                                             
         EJECT                                                                  
* THIS ROUTINE ADDS THE COMMENT WHOSE ADDRESS IS IN PARAMETER 1 AND             
* LENGTH IN PARAMETER 2 TO THE CHECK SO LONG AS IT TAKES UP NO MORE             
* PRINT LINES THEN THAT SPECIFIED BY NUMLINES.  IF THE COMMENT DOES NOT         
* FIT THEN THE REMAINING PRINT LINES WILL BE BLANKED.  IN EITHER CASE,          
* NUMLINES WILL BE UPDATED WITH THE NEW NUMBER OF LINES LEFT.                   
*                                                                               
ADDCOMM  NTR1                                                                   
         L     R4,0(R1)            R4 = A(BEGINNING OF COMMENT)                 
         L     R3,4(R1)            R3 = LENGTH OF COMMENT                       
         L     R1,NUMLINES         R1 = NUMBER OF LINES LEFT                    
         LTR   R1,R1               NO ROOM LEFT?                                
         BZ    ACX                 JUST LEAVE                                   
*                                                                               
         LA    R5,P                R5 = A(PRINT LINE)                           
         USING PLINED,R5                                                        
*                                                                               
         SR    R2,R2               R3 = NUMBER OF 80 BYTE BLOCKS - 1            
         D     R2,=F'80'           R2 = LENGTH OF LAST BLOCK                    
*                                                                               
         LR    RE,R3               IF NOT ENOUGH LINES LEFT THEN GO             
         LTR   R2,R2                   FILL IN BLANK LINES                      
         BZ    *+8                                                              
         LA    RE,1(RE)                                                         
         CR    RE,R1                                                            
         BH    AC50                                                             
*                                                                               
         LTR   R3,R3               IF THERE IS NO 80 BYTE BLOCK THEN GO         
         BZ    AC20                    PRINT REMAINDER OF COMMENT               
*                                                                               
AC10     MVC   P22COMM,0(R4)       PRINT 80 BYTE BLOCK                          
         BAS   RE,CALLSPL2                                                      
         BCTR  R1,0                DECREMENT NUMBER OF LINES LEFT               
*                                                                               
         LA    R4,80(R4)           BUMP TO NEXT 80 BYTE BLOCK                   
         BCT   R3,AC10             REPEAT UNTIL NO MORE 80 BYTE BLOCKS          
*                                                                               
AC20     LTR   R2,R2               IF THERE IS ANY COMMENT LEFT                 
         BZ    AC100                                                            
*                                                                               
         BCTR  R2,0                THEN PRINT IT                                
         EX    R2,*+8                                                           
         B     *+10                                                             
         MVC   P22COMM(0),0(R4)                                                 
         BAS   RE,CALLSPL2                                                      
         BCTR  R1,0                DECREMENT NUMBER OF LINES LEFT               
         B     AC100                                                            
*                                                                               
AC50     BAS   RE,CALLSPL2         FILL IN LINES LEFT WITH BLANKS               
         BCT   R1,AC50                                                          
*                                                                               
AC100    ST    R1,NUMLINES         SAVE NEW NUMBER OF LINES LEFT                
*                                                                               
ACX      B     XIT2                                                             
         EJECT                                                                  
BACKRF   CLI   0(RF),C' '          BACK RF TO FIRST NON-SPACE                   
         BHR   RE                                                               
         BCT   RF,BACKRF                                                        
         SPACE 2                                                                
CALLSPL2 NTR1                                                                   
         GOTO1 SPOOL,DMCB,(R8)                                                  
         B     XIT2                                                             
         SPACE 3                                                                
YES2     SR    RC,RC                                                            
NO2      LTR   RC,RC                                                            
XIT2     XIT1                                                                   
         SPACE 3                                                                
*                                                                               
*                                                                               
*        TABLE OF AFT LOCALS FOR PRESORTING OF CHECKS                           
*                                                                               
LOCLTAB  DS    0CL3                                                             
         DC    CL3'LA '                                                         
         DC    CL3'SAN'                                                         
         DC    CL3'SDG'                                                         
         DC    CL3'SF '                                                         
         DC    CL3'CHI'                                                         
         DC    CL3'DAL'                                                         
         DC    CL3'HON'                                                         
         DC    CL3'NYC'                                                         
         DC    X'FF'                                                            
         SPACE 3                                                                
LTDUES   DC    CL19'UNION DUES WITHHELD'                                        
DEDCOMM  DC    CL35'WAS DEDUCTED FROM YOUR GROSS WAGES'                         
LTMEM    DC    CL17'MEMBERSHIP NUMBER'                                          
         LTORG                                                                  
         DROP  RA,R9               DROP SECONDARY BASE REGISTERS                
*&&                                                                             
         EJECT                                                                  
UDTLD    DSECT                                                                  
UDTLUNIT DS    CL3                 TAX UNIT                                     
         DS    C                                                                
UDTLUND  DS    C                   UNDERSCORE                                   
UDTLTASK DS    CL6                 TASK                                         
         DS    C                                                                
UDTLUND2 DS    C                   UNDERSCORE 2                                 
UDTLPTYP DS    CL22                PAY TYPE NAME                                
         EJECT                                                                  
PLINED   DSECT                                                                  
***********************************************************************         
*        PRINT LINE #1                                                *         
***********************************************************************         
P01AGYN  DS    CL36                AGENCY NAME                                  
         ORG   PLINED+38                                                        
P01AGY   DS    CL6                 AGENCY CODE                                  
         ORG   PLINED+46                                                        
P01SVC   DS    CL32                SERVICE FOR (PC REPORT ONLY)                 
***      ORG   PLINED+46                                                        
***CMNT  DS    CL(L'CMNT91)        NEW CONTRACT COMMENT AREA                    
***********************************************************************         
*        PRINT LINE #3                                                *         
***********************************************************************         
         ORG   PLINED                                                           
P03CID   DS    CL12                COMMERCIAL ID                                
         ORG   PLINED+26                                                        
P03PHO   DS    CL31                PHOTOGRAPHER (PC REPORT ONLY)                
*                                                                               
         ORG   PLINED+38                                                        
P03ORIG  DS    CL12                ORIGINAL ID #                                
         ORG   PLINED+56                                                        
P03EDIT  DS    CL12                EDIT VERSION #                               
***********************************************************************         
*        PRINT LINE #5                                                *         
***********************************************************************         
         ORG   PLINED                                                           
P05CLIN  DS    CL36                CLIENT NAME                                  
         ORG   PLINED+42                                                        
P05PRDN  DS    CL36                PRODUCT NAME                                 
***********************************************************************         
*        PRINT LINE #7                                                *         
***********************************************************************         
         ORG   PLINED                                                           
P07TITL  DS    CL30                COMMERCIAL TITLE                             
         ORG   PLINED+42                                                        
P07PCAT  DS    CL3                 CATEGORY CODE  (PC REPORT ONLY)              
*                                                                               
         ORG   PLINED+30                                                        
P07UN    DS    CL3                 UNION                                        
         ORG   PLINED+33                                                        
P07LOCL  DS    CL3                 LOCAL                                        
         ORG   PLINED+37                                                        
P07FRST  DS    CL8                 FIRST SERVICES DATE                          
         ORG   PLINED+46                                                        
P07YEAR  DS    CL3                 CONTRACT YEAR                                
         ORG   PLINED+50                                                        
P07CAT   DS    CL3                 CATEGORY CODE                                
         ORG   PLINED+54                                                        
P07ONOF  DS    CL3                 CAMERA (ON/OFF)                              
         ORG   PLINED+58                                                        
P07MULT  DS    CL1                 MULTITRACK                                   
         ORG   PLINED+60                                                        
P07SWEET DS    CL1                 SWEETENING                                   
         ORG   PLINED+62                                                        
P07DBL   DS    CL1                 DOUBLES                                      
         ORG   PLINED+64                                                        
P07OV1   DS    CL4                 OVERSCALE PERCENTAGE 1                       
         ORG   PLINED+68                                                        
P07OV2   DS    CL4                 OVERSCALE PERCENTAGE 2                       
***********************************************************************         
*        PRINT LINE #8                                                *         
***********************************************************************         
         ORG   PLINED+73                                                        
P08DAY   DS    CL2                 NUMBER OF DAYS WORKED                        
         ORG   PLINED+76                                                        
P08OT    DS    CL2                 NUMBER OF OVERTIME HOURS                     
         ORG   PLINED+79                                                        
P08DT    DS    CL2                 NUMBER OF DOUBLE-TIME HOURS                  
***********************************************************************         
*        PRINT LINE #9                                                *         
***********************************************************************         
         ORG   PLINED+1                                                         
P09AIR   DS    CL8                 FIRST AIR DATE                               
         ORG   PLINED+10                                                        
P09FCYC  DS    CL8                 FIRST FIXED CYCLE DATE                       
         ORG   PLINED+19                                                        
P09EXP   DS    CL8                 EXPIRATION DATE                              
         ORG   PLINED+28                                                        
P09CON   DS    CL17                AFM CONTRACT NUMBERS                         
         ORG   PLINED+46                                                        
P09LEAD  DS    CL16                AFM LEADER                                   
         ORG   PLINED+64                                                        
P09DUB   DS    CL8                 AFM DUB DATE                                 
*                                  (PC REPORT ONLY)                             
         ORG   PLINED+6                                                         
P09SDAT  DS    CL8                 SHOOT DATE (CENTERED)                        
         ORG   PLINED+29                                                        
P09SSTU  DS    CL12                SHOOT STUDIO (CENTERED)                      
         ORG   PLINED+60                                                        
P09SCTY  DS    CL12                SHOOT CITY (CENTERED)                        
***********************************************************************         
*        PRINT LINE #11    (PC REPORT ONLY)                           *         
***********************************************************************         
         ORG   PLINED+3                                                         
P11PBDT  DS    CL8                 PUBLICATION DATE (CENTERED)                  
         ORG   PLINED+17                                                        
P11EXPDT DS    CL18                EXPIRATION DATE (CENTERED)                   
         ORG   PLINED+37                                                        
P11EXPBS DS    CL16                EXPIRATION BASED ON (CENTERED)               
         ORG   PLINED+72                                                        
P11MNTH  DS    CL2                 MONTHS OF TERM (CENTERED)                    
***********************************************************************         
*        PRINT LINE #12                                               *         
***********************************************************************         
         ORG   PLINED+1                                                         
P12MED   DS    CL5                 MEDIA                                        
         ORG   PLINED+8                                                         
P12PTYP  DS    CL7                 PAYMENT TYPE                                 
         ORG   PLINED+16                                                        
P12UTYP  DS    CL34                USE TYPE                                     
         ORG   PLINED+51                                                        
P12WKS   DS    CL2                 USE WEEKS                                    
         ORG   PLINED+55                                                        
P12CYCS  DS    CL8                 CYCLE START DATE                             
         ORG   PLINED+64                                                        
P12CYCE  DS    CL8                 CYCLE END DATE                               
***********************************************************************         
*        PRINT LINE #13    (PC REPORT ONLY)                           *         
***********************************************************************         
         ORG   PLINED+2                                                         
P13USE   DS    CL36                USE NAME (CENTERED)                          
         ORG   PLINED+43                                                        
P13AREA  DS    CL36                AREA NAME (CENTERED)                         
***********************************************************************         
*        PRINT LINE #14                                               *         
***********************************************************************         
         ORG   PLINED+5                                                         
P14CAB   DS    CL3                 MADE FOR CABLE (YES/NO)                      
         ORG   PLINED+27                                                        
P14CLA   DS    CL40                CLASS A USE DATES (FIRST LINE)               
***********************************************************************         
*        PRINT LINE #15    (PC REPORT ONLY)                           *         
***********************************************************************         
         ORG   PLINED                                                           
P15PAYEE DS    CL70                PAYEE                                        
         ORG   PLINED+73                                                        
P15INV   DS    CL6                 INVOICE NUMBER (CENTERED)                    
***********************************************************************         
*        PRINT LINE #16                                               *         
***********************************************************************         
         ORG   PLINED+5                                                         
P16SPAN  DS    CL3                 MADE FOR SPANISH (YES/NO)                    
         ORG   PLINED+27                                                        
P16CLA   DS    CL40                CLASS A USE DATES (SECOND LINE)              
         ORG   PLINED+72                                                        
P16TRV   DS    CL5                 TRAVEL HOURS                                 
         ORG   PLINED+77                                                        
P16PDW   DS    CL5                 PRIOR DAY WARDROBE HOURS                     
***********************************************************************         
*        PRINT LINE #18                                               *         
***********************************************************************         
         ORG   PLINED+32                                                        
P18CLA   DS    CL35                CLASS A USE DATES (THIRD LINE)               
         ORG   PLINED+93                                                        
P18INCL  DS    CL8                 INCLUDE CODE                                 
***********************************************************************         
*        PRINT LINE #20                                               *         
***********************************************************************         
         ORG   PLINED                                                           
P20MAJ   DS    CL9                 INITIAL MAJOR CITIES                         
         ORG   PLINED+10                                                        
P20UNT   DS    CL3                 INITIAL UNITS                                
         ORG   PLINED+10                                                        
P20INS   DS    CL3                 INITIAL INSERTS                              
         ORG   PLINED+15                                                        
P20UMAJ  DS    CL9                 UPGRADE TO MAJOR CITIES                      
         ORG   PLINED+25                                                        
P20UUNT  DS    CL3                 UPGRADE TO UNITS                             
         ORG   PLINED+25                                                        
P20UINS  DS    CL3                 UPGRADE TO INSERTS                           
         ORG   PLINED+30                                                        
P20CRED  DS    CL7                 CREDIT AMOUNT                                
         ORG   PLINED+39                                                        
P20ACDE  DS    CL20                APPLIED CODE COMMENTS                        
         ORG   PLINED+61                                                        
P20CAN   DS    CL1                 CANADIAN TAX (Y/N)                           
         ORG   PLINED+65                                                        
P20INV   DS    CL6                 INVOICE NUMBER                               
         ORG   PLINED+74                                                        
P20SP    DS    CL2                 NUMBER OF SPOTS                              
         ORG   PLINED+78                                                        
P20TAG   DS    CL2                 NUMBER OF TAGS                               
***********************************************************************         
*        PRINT LINES #22-28                                           *         
***********************************************************************         
         ORG   PLINED                                                           
P22COMM  DS    CL80                COMMENTS                                     
***********************************************************************         
*        PRINT LINE #30                                               *         
***********************************************************************         
         ORG   PLINED                                                           
P30INQ   DS    CL32                INQUIRIES TO:                                
         ORG   PLINED+51                                                        
P30EMPN  DS    CL32                EMPLOYER NAME                                
***********************************************************************         
*        PRINT LINE #32                                               *         
***********************************************************************         
         ORG   PLINED                                                           
P32NAME  DS    CL35                EMPLOYEE NAME                                
         ORG   PLINED+37                                                        
P32CHKN  DS    CL8                 CHECK NUMBER                                 
         ORG   PLINED+51                                                        
P32COR   DS    CL3                 CITY OF RESIDENCE                            
         ORG   PLINED+55                                                        
P32COW   DS    CL3                 CITY OF WORK                                 
         ORG   PLINED+59                                                        
P32SOR   DS    CL3                 STATE OF RESIDENCE                           
         ORG   PLINED+64                                                        
P32SOW   DS    CL3                 STATE OF WORK                                
         ORG   PLINED+68                                                        
P32NCDE  DS    CL4                 AGENT CODE                                   
         ORG   PLINED+74                                                        
P32PAYE  DS    CL8                 PAY ENDING                                   
***********************************************************************         
*        PRINT LINE #34                                               *         
***********************************************************************         
         ORG   PLINED                                                           
P34SSN   DS    CL11                SOCIAL SECURITY NUMBER                       
         ORG   PLINED+11                                                        
P34FID   DS    CL10                FEDERAL ID NUMBER                            
         ORG   PLINED+22                                                        
P34FSON  DS    CL29                CORPORATION FSO NAME                         
         ORG   PLINED+52                                                        
P34GST#  DS    0CL10               GST ID NUMBER                                
P34SUC   DS    CL14                STATE U.C. #                                 
         ORG   PLINED+67                                                        
P34MPT   DS    CL6                 MPTRF                                        
         ORG   PLINED+73                                                        
P34CHAR  DS    CL9                 PERMANENT CHARITIES                          
***********************************************************************         
*        PRINT LINE #36                                               *         
***********************************************************************         
         ORG   PLINED                                                           
P36PAYI  DS    CL11                PAYMENT AMOUNT (INDIVIDUAL)                  
         ORG   PLINED+11                                                        
P36PAYC  DS    CL10                PAYMENT AMOUNT (CORPORATION)                 
         ORG   PLINED+21                                                        
P36REXP  DS    CL10                REIMBURSED EXPENSES                          
         ORG   PLINED+31                                                        
P36DUE   DS    CL10                DUE COMPANY                                  
         ORG   PLINED+41                                                        
P36EARN  DS    CL10                TAXABLE EARNINGS                             
         ORG   PLINED+51                                                        
P36TDED  DS    CL10                TOT DED IF US$, MISC DED IF CAN$             
         ORG   PLINED+61                                                        
P36LIEN  DS    CL10                TAX/LIENS GARNISHMENTS/W4 TRUSTEE            
         ORG   PLINED+72                                                        
P36AMNT  DS    CL10                AMOUNT OF CHECK                              
***********************************************************************         
*        PRINT LINE #38                                               *         
***********************************************************************         
         ORG   PLINED                                                           
P38GST   DS    CL11                GOODS AND SERVICES TAX (CAN$)                
         ORG   PLINED+11                                                        
P38DTXT  DS    CL19                UNION DUES WITHHELD TEXT (CAN$)              
         ORG   PLINED+31                                                        
P38DUES  DS    CL10                UNION DUES (CAN$)                            
         ORG   PLINED                                                           
P38FTAX  DS    CL9                 FEDERAL TAX                                  
         ORG   PLINED+9                                                         
P38FICA  DS    CL9                 FICA                                         
         ORG   PLINED+18                                                        
P38SOW1  DS    CL2                 STATE OF WORK :                              
         ORG   PLINED+21                                                        
P38WTAX  DS    CL8                 STATE OF WORK TAX                            
         ORG   PLINED+29                                                        
P38SOR1  DS    CL2                 STATE OF RESIDENCE :                         
         ORG   PLINED+32                                                        
P38RTAX  DS    CL8                 STATE OF RESIDENCE TAX                       
         ORG   PLINED+40                                                        
P38SOW2  DS    CL2                 STATE OF WORK :                              
         ORG   PLINED+43                                                        
P38SUI   DS    CL8                 STATE UNEMPLOYEMENT INSURANCE                
         ORG   PLINED+51                                                        
P38LTAX  DS    CL7                 LOCAL TAX                                    
         ORG   PLINED+58                                                        
P38CTAX  DS    CL7                 CANADIAN TAX                                 
         ORG   PLINED+65                                                        
P38MDED  DS    CL7                 MDED + UNION DUES (US$ ONLY)                 
         ORG   PLINED+72                                                        
P38SOW3  DS    CL2                 STATE OF WORK :                              
         ORG   PLINED+75                                                        
P38SDI   DS    CL6                 STATE DISABILITY                             
***********************************************************************         
*        PRINT LINE #40                                               *         
***********************************************************************         
         ORG   PLINED                                                           
P40YEARN DS    CL12                YTD GROSS EARNINGS (INDIVIDUAL)              
         ORG   PLINED+12                                                        
P40YNTAX DS    CL10                YTD GROSS EARNINGS (CORPORATION)             
         ORG   PLINED+22                                                        
P40YREX  DS    CL10                YTD REIMBURSED EXPENSES                      
         ORG   PLINED+32                                                        
P40YGST  DS    CL11                YTD GOODS AND SERVICES TAX  (CAN$)           
         ORG   PLINED+43                                                        
P40MTXT  DS    CL17                MEMBERSHIP NUMBER TEXT (CAN$)                
         ORG   PLINED+61                                                        
P40MEM   DS    CL9                 MEMBERSHIP NUMBER (CAN$)                     
         ORG   PLINED+32                                                        
P40YFED  DS    CL10                YTD FEDERAL TAX                              
         ORG   PLINED+42                                                        
P40YFIC  DS    CL10                YTD FICA                                     
         ORG   PLINED+52                                                        
P40YSTA  DS    CL10                YTD STATE TAX                                
         ORG   PLINED+62                                                        
P40YLOC  DS    CL10                YTD LOCAL TAX                                
         ORG   PLINED+72                                                        
P40YCAN  DS    CL10                YTD CANADIAN TAX                             
***********************************************************************         
*        PRINT LINE #45                                               *         
***********************************************************************         
         ORG   PLINED+12                                                        
P45DTE   DS    CL8                 CHECK DATE                                   
         ORG   PLINED+71                                                        
P45CHKN  DS    CL8                 CHECK NUMBER                                 
***********************************************************************         
*        PRINT LINES #47-#49                                          *         
***********************************************************************         
         ORG   PLINED                                                           
P47      DS    CL(L'P)             LINE 47                                      
         ORG   PLINED+1                                                         
P47PREF  DS    CL3                 PAY                                          
         ORG   PLINED+L'P                                                       
P48      DS    CL(L'P)             LINE 48                                      
         ORG   PLINED+L'P+1                                                     
P48PREF  DS    CL7                 EXACTLY                                      
         ORG   PLINED+L'P+9                                                     
P48WORD  DS    CL59                AMOUNT IN WORDS                              
         ORG   PLINED+2*L'P                                                     
P49      DS    CL(L'P)             LINE 49                                      
         ORG   PLINED+2*L'P+70                                                  
P49AMNT  DS    CL9                 AMOUNT                                       
***********************************************************************         
*        PRINT LINE #50                                               *         
***********************************************************************         
         ORG   PLINED+L'P+63                                                    
P50NNEG  DS    CL18                * NOT NEGOTIABLE *                           
***********************************************************************         
*        PRINT LINES #51-56                                           *         
***********************************************************************         
         ORG   PLINED+9                                                         
P51CADD  DS    CL35                CHECK ADDRESS LINE                           
         EJECT                                                                  
*---------------------------------------------------------------                
         DS    0D                                                               
         DC    CL8'**SSB **'                                                    
SSB      CSECT                                                                  
         DC    X'0000FF',X'00',XL252'00'                                        
*---------------------------------------------------------------                
       ++INCLUDE TACKREPD                                                       
         EJECT                                                                  
       ++INCLUDE DDDLCB                                                         
         EJECT                                                                  
*TAREPFFD                                                                       
*TAREPF4D                                                                       
*DDGENTWA                                                                       
*DDTWADCOND                                                                     
*DDMASTD                                                                        
*DDREMOTED                                                                      
*DDSPOOLD                                                                       
*DDSPLWORKD                                                                     
*DDBIGBOX                                                                       
*DMWRKRK                                                                        
*TAGENFILE                                                                      
*CTGENFILE                                                                      
*ACGENBOTH                                                                      
*ACGENPOST                                                                      
*TASYSDSECT                                                                     
*TASYSEQUS                                                                      
*TAREPWORKD                                                                     
*TALDCPTRD                                                                      
         PRINT OFF                                                              
       ++INCLUDE TAREPFFD                                                       
         ORG   CONTAGH                                                          
       ++INCLUDE TAREPF4D                                                       
       ++INCLUDE DDGENTWA                                                       
       ++INCLUDE DDTWADCOND                                                     
       ++INCLUDE DDMASTD                                                        
       ++INCLUDE DDLOGOD                                                        
       ++INCLUDE DDREMOTED                                                      
       ++INCLUDE DDSPOOLD                                                       
       ++INCLUDE DDSPLWORKD                                                     
       ++INCLUDE DDSMTPD                                                        
       ++INCLUDE DDBIGBOX                                                       
       ++INCLUDE DMWRKRK                                                        
       ++INCLUDE TAGENFILE                                                      
       ++INCLUDE CTGENFILE                                                      
       ++INCLUDE ACGENBOTH                                                      
       ++INCLUDE ACGENPOST                                                      
       ++INCLUDE TASYSDSECT                                                     
       ++INCLUDE TASYSEQUS                                                      
       ++INCLUDE TALDCPTRD                                                      
       ++INCLUDE TAREPWORKD                                                     
       ++INCLUDE TAPAYBRKD                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'210TACKPRT   06/23/16'                                      
         END                                                                    
