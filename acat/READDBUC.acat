*          DATA SET READDBUC   AT LEVEL 002 AS OF 05/28/03                      
*CATALP READDBUC                                                                
         TITLE 'READDBUC REPPAK - ADD OR UPDATE CONTRACT BUCKET'                
***********************************************************************         
*              PARAMETER 1 =       A(RECORD) BYTE 0=X'FF' FOR BUFFERS           
*                                            INSTEAD OF RECORDS WHERE           
*                                            1ST 2 BYTES = LENGTH OF            
*                                            BUFFER                             
*                                                                               
*              PARAMETER 2 =       A(BUCKET TO BE INSERTED)                     
*                                                                               
*              PARAMETER 3 =       A(RECUP)                                     
*                                                                               
***********************************************************************         
ADDBUCK  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKLQ,READDBUC,CLEAR=YES,RR=RE                                  
         USING WORKD,RC                                                         
         ST    RE,RELO                                                          
         L     R2,0(R1)            A(REC)                                       
         L     R6,4(R1)            A(ELEM)                                      
         MVC   VRECUP,8(R1)        A(RECUP)                                     
*-------------*                                                                 
* SET UP BXLE *                                                                 
*-------------*                                                                 
         MVC   HALF,27(R2)         L'REC                                        
         LA    R3,34(R2)           1ST ELEM                                     
         MVI   RUPIND,C'R'         REPPAK IND FOR RECUP                         
*                                                                               
         CLI   0(R1),X'FF'         BUFFER (NOT RECORD)?                         
         BNE   *+18                NO                                           
         MVC   HALF,0(R2)          BUFFER LENGTH                                
         LA    R3,2(R2)            1ST ELEMENT IN BUFFER                        
         MVI   RUPIND,X'FF'        BUFFER IND TO RECUP                          
*                                                                               
         LH    R5,HALF             REC LEN                                      
         LA    R5,0(R5,R2)         REC END                                      
         BCTR  R5,0                                                             
         CR    R3,R5               ELEMENTS YET?                                
         BNL   ADDB100             NO - SO ADD IT                               
         SPACE 3                                                                
*-----------------------*                                                       
* LOOP THROUGH ELEMENTS *                                                       
*-----------------------*                                                       
ADDB050  DS    0H                                                               
         CLC   0(6,R6),0(R3)       NEW ELEM VS OLD                              
         BL    ADDB100             LOW-ADD NEW ELEM                             
         BE    ADDB200             EQUAL-ADD TO OLD ELEM                        
*                                                                               
         CR    R3,R5               CHECK SO WE WON'T BUMP PASS E-O-R            
         BNL   ADDB100                                                          
*                                                                               
         ZIC   R4,1(R3)            ELEM LEN                                     
         BXLE  R3,R4,ADDB050       NEXT ELEM                                    
         EJECT                                                                  
*-------------*                                                                 
* ADD ELEMENT *                                                                 
*-------------*                                                                 
ADDB100  DS    0H                                                               
         CLI   1(R6),X'0E'         EXTENDED (PLUS SPOTS) BUCKET?                
         BE    ADDB102             YES                                          
         OC    6(4,R6),6(R6)       NO - SKIP ADDING FOR ZERO AMOUNT             
*                                       10-CHAR ELEMENT CHECK                   
         BZ    ADDB110                                                          
         B     ADDB104                                                          
ADDB102  DS    0H                                                               
         OC    6(8,R6),6(R6)       SKIP ADDING FOR ZERO $/ZERO SPOTS            
*                                       14-CHAR ELEMENT CHECK                   
         BZ    ADDB110                                                          
ADDB104  DS    0H                                                               
         GOTO1 VRECUP,DMCB+12,(RUPIND,(R2)),(R6),(R3)                           
*                                                                               
ADDB110  DS    0H                                                               
         B     BUCKXIT                                                          
         SPACE 3                                                                
*-----------------------------------------*                                     
* EQUAL BUCKETS - ADD TO EXISTING ELEMENT *                                     
*-----------------------------------------*                                     
ADDB200  EQU   *                                                                
         CLI   1(R6),X'0E'         EXTENDED (PLUS SPOTS) BUCKET?                
         BE    ADDB300             YES                                          
         ICM   RE,15,6(R6)         NEW AMOUNT                                   
         ICM   RF,15,6(R3)         OLD AMOUNT                                   
         AR    RE,RF                                                            
         STCM  RE,15,6(R3)         NEW TOTAL AMOUNT                             
*                                                                               
         LTR   RE,RE               ZERO BUCKET?                                 
         BNZ   ADDB210             NO - DON'T DELETE BUCKET                     
*                                                                               
         GOTO1 VRECUP,DMCB+12,(RUPIND,(R2)),(R3),(R3)                           
*                                                                               
ADDB210  DS    0H                                                               
         B     BUCKXIT                                                          
*                                                                               
ADDB300  DS    0H                                                               
         ICM   RE,15,6(R6)         NEW AMOUNT                                   
         ICM   RF,15,6(R3)         OLD AMOUNT                                   
         AR    RE,RF                                                            
         STCM  RE,15,6(R3)         NEW TOTAL AMOUNT                             
*                                                                               
         ICM   RE,15,10(R6)        NEW NUMBER SPOTS                             
         ICM   RF,15,10(R3)        OLD NUMBER SPOTS                             
         AR    RE,RF                                                            
         STCM  RE,15,10(R3)        NEW TOTAL NUMBER SPOTS                       
*                                                                               
         OC    6(8,R3),6(R3)       ZERO $/NUM SPOTS?                            
         BNZ   ADDB310             NO - DON'T DELETE BUCKET                     
*                                                                               
         GOTO1 VRECUP,DMCB+12,(RUPIND,(R2)),(R3),(R3)                           
*                                                                               
ADDB310  DS    0H                                                               
         B     BUCKXIT                                                          
*                                                                               
BUCKXIT  DS    0H                                                               
         XIT1                                                                   
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
**********************************************************************          
WORKD    DSECT                                                                  
DMCB     DS    6F                                                               
RELO     DS    A                                                                
VRECUP   DS    A                                                                
HALF     DS    H                                                                
RUPIND   DS    X                   RECUP RECORD INDICATOR                       
WORKLQ   EQU   *-WORKD                                                          
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'002READDBUC  05/28/03'                                      
         END                                                                    
