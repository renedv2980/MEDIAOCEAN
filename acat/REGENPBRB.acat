*          DATA SET REGENPBRB  AT LEVEL 015 AS OF 05/01/02                      
*CATALP REGENPBR                                                                
         TITLE 'REGENPBR  - REPPAK BUY LINE DISPLAY- REP-TO-SPOT INFO '         
*              PARAMETER 1 =       A(BUYREC)                                    
*                        2 =       A(OUTPUT AREA)                               
*                                       BYTE 0 = MAXIMUM LINES                  
*                                       OUTPUT AREA = 81 X MAX LINES            
*                                       BYTE 0 = NUMBER LINES ON RETURN         
*                                       THIS CAN BE 0 (DELETES)                 
*                        3 =       A(ROUTINE ADDRESS BLOCK)                     
*                                       WORD 1 = A(UNDAY)                       
*                                            2 = A(UNTIME)                      
*                                            3 = A(DATCON)                      
*                                            4 = A(ADDAY)                       
*                                            IF 4 IS 0 THEN ALTERNATE           
*                                            WEEKS ARE NOT EXPLODED             
*                        4 =       A(CONREC)                                    
*                                                                               
***********************************************************************         
* HISTORY:                                                                      
*                                                                               
* MAY05/93 (BU ) --- ORIGINAL INFORMATION                                       
*                                                                               
* JUL13/95 (SKU) --- FIX CONFIRMATION PRINT BUG                                 
*                                                                               
* OCT13/95 (BU ) --- ADD KATZ DISPLAY CHANGES:  BAD INFO                        
*                                                                               
* JAN06/99 (RHV) --- HANDLE NON-DELETED CANCELLED BUYS                          
*                                                                               
* AUG10/99 (BU ) --- EXPAND SPOTPAK LINE NUMBER FIELD                           
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
REGENPBR CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 12,REGENPBR                                                      
         USING WORKD,RC                                                         
         L     RA,0(R1)            DSECT FOR BUYREC                             
         ST    R1,SAVER1                                                        
         USING BUYREC,RA                                                        
         L     R9,4(R1)            OUTPUT AREA                                  
         USING PD,R9               DISPLAY LINE DSECT                           
         USING CONREC,R8                                                        
         L     R8,12(R1)                                                        
*                                                                               
         L     R2,8(R1)            ROUTINES                                     
         MVC   VOUTDAY(16),0(R2)                                                
         MVI   MAX,0                                                            
         MVC   MAX+1(1),4(R1)      MAX LINES                                    
         MVI   4(R1),0             NO LINES SO FAR                              
         TM    RBUYCNTL,X'C0'      VOID POINTER?                                
         BO    EXIT                                                             
         CLI   RBUYCHGI,C'C'       CANCELLED?                                   
         BE    PBBY0005                                                         
         TM    RBUYCNTL,X'80'      DELETED?                                     
         BZ    PBBY0010                                                         
         CLI   RBUYCHGI,C'X'       DELETED (NOT CANCELLED?)                     
         BE    EXIT                                                             
* MUST HAVE BEEN CANCELLED                                                      
PBBY0005 DS    0H                                                               
         TM    RCONMODR+1,X'C0'    ACE/GRAPHNET                                 
         BNZ   PBBY0010            DO CANCELLED ACE/GRAPHNET BUYS LATER         
         CLC   RBUYCREA,RBUYCHGD   CREATED SAME DAY AS CANCEL?                  
         BE    EXIT                                                             
         CLC   RBUYKMOD,RCONMOD    BUY K LEVEL V K MOD NUM                      
         BL    EXIT                                                             
*                                                                               
* GET TODAY'S DATE                                                              
PBBY0010 GOTO1 VDATCON,DMCB,(5,0),(3,TODAY)                                     
*                                                                               
         LR    RE,R9                                                            
         LH    RF,MAX              MAXIMUM LINES                                
         MH    RF,=Y(L'PRTLN)      LINE COUNT                                   
*                                                                               
*         XCEF                      CLEAR OUTPUT AREA                           
         XCEF                                                                   
         CLI   RBUYKLIN,255        PLAN REC?                                    
         BNE   PBBY0020                                                         
* DISPLAY PLAN TOTAL REC LINE                                                   
         L     R1,SAVER1                                                        
         MVI   4(R1),1             1 LINE                                       
         MVC   PRAT-28(27),=C'WEEKLY RATE FOR PLAN XXX IS'                      
         MVC   PRAT-7(3),RBUYKPLN                                               
         MVC   DUB(4),RBUYCOS                                                   
         L     R4,DUB                                                           
         EDIT  (R4),(10,PRAT),2,FLOAT=-,COMMAS=YES,ALIGN=LEFT                   
         B     EXIT                                                             
*                                                                               
*                                                                               
*              BUILD PRINT LINE                                                 
* CHECK FOR MODIFICATION CODE PRINT                                             
PBBY0020 TM    RCONMODR+1,X'C0'    X'C0'=ACE/GRAPHNET CONTRACT                  
         BZ    PBBY0080                                                         
         SPACE 1                                                                
         SR    R4,R4                                                            
         LA    R1,RCONELEM                                                      
PBBY0030 IC    R4,1(R1)            FIND X'1F' ELEMENT                           
         AR    R1,R4                                                            
         CLI   0(R1),0                                                          
         BNE   *+6                                                              
         DC    H'0'                ELEMENT SHOULD BE THERE                      
         CLI   0(R1),X'1F'                                                      
         BNE   PBBY0030                                                         
         SPACE 1                                                                
         MVC   SVCONF,6(R1)        SAVE CONFIRMED STATUS                        
         TM    6(R1),X'80'         X'80'=NOT CONFIRMED                          
         BZ    PBBY0060                                                         
         SPACE 1                                                                
         IC    R4,1(R1)                                                         
         AR    R1,R4               GET X'20' (SEND) ELEMENT                     
         CLI   0(R1),X'20'                                                      
         BE    *+6                                                              
         DC    H'0'                                                             
         SPACE 1                                                                
         CLI   5(R1),1             DON'T PRINT CHANGES WHEN                     
         BNE   PBBY0040            REP VERSION IS 1 AND                         
         CLI   14(R1),0            STATION VERSION IS 0                         
         BE    PBBY0080                                                         
         SPACE 1                                                                
PBBY0040 CLC   5(1,R1),14(R1)      COMPARE REP TO STATION VERSION               
         BH    PBBY0050  THEN COMPARE BUY VERSION TO HIGHER OF THE TWO          
*                        & DON'T PRINT CHANGES FOR OLD VERSIONS                 
         CLC   RBUYVER,14(R1)      STATION VERSION                              
         BE    PBBY0080                                                         
         TM    RBUYCNTL,X'80'      IF CANCELLED                                 
         BO    EXIT                DON'T PRINT LINE AT ALL                      
         B     PBBY0120            OTHERWISE, DON'T PRINT MOD CODE              
         SPACE 1                                                                
PBBY0050 CLC   RBUYVER,5(R1)       REP VERSION                                  
         BE    PBBY0080                                                         
         TM    RBUYCNTL,X'80'      IF CANCELLED                                 
         BO    EXIT                DON'T PRINT LINE AT ALL                      
         B     PBBY0120            OTHERWISE, DON'T PRINT MOD CODE              
         SPACE 1                                                                
* CONFIRMED -  ONLY PRINT CHANGES FROM LAST MOD NUMBER, AND DON'T               
*              PRINT CHANGE CODE O (ORDER COMMENT)                              
         SPACE 1                                                                
PBBY0060 DS    0H                                                               
*                                                                               
         CLI   RCONMOD,0           DON'T PRINT CHANGES FOR MOD 0                
         BE    PBBY0070                                                         
*        CLI   RBUYKMOD,0          DON'T PRINT CHANGES FOR MOD 0                
*        BE    PBBY0070                                                         
*                                                                               
         ZIC   R4,RCONMOD                                                       
         BCTR  R4,0                GET LAST MOD NUMBER                          
         ZIC   R1,RBUYKMOD                                                      
         CR    R1,R4                                                            
         BE    PBBY0080                                                         
PBBY0070 TM    RBUYCNTL,X'80'      IF CANCELLED                                 
         BO    EXIT                DON'T PRINT LINE AT ALL                      
         B     PBBY0120            OTHERWISE, DON'T PRINT MOD CODE              
         SPACE 1                                                                
PBBY0080 DS    0H                                                               
*                                                                               
*   ALL REFERENCES TO MOD CODE HAVE BEEN DELETED FOR REP-TO-SPOT                
*                                                                               
PBBY0120 EDIT  (1,RBUYKLIN),(3,PLIN)    LINE NUMBER                             
*                                                                               
         MVC   DMCB(2),RBUYDUR     LENGTH                                       
         NI    DMCB,X'7F'          MINUTE IND                                   
         EDIT  (2,DMCB),(3,PLEN)                                                
         TM    RBUYDUR,X'80'       MINUTES?                                     
         BZ    PBBY0130                                                         
         MVC   PLEN-1(3),PLEN                                                   
         MVI   PLEN+2,C'M'                                                      
*                                                                               
PBBY0130 EQU   *                                                                
*                                                                               
* DISPLAY FLIGHT NUMBER IF NO SECTION EXISTS FOR BUY                            
*                                                                               
PBBY0140 EQU   *                                                                
*                                                                               
         CLI   RBUYFLT,0           DOES BUY HAVE A FLIGHT                       
         BE    PBBY0150            NO                                           
         OC    VADDAY,VADDAY       ONLY DO IT IF ON-LINE                        
         BNZ   PBBY0150                                                         
*                                                                               
PBBY0150 EQU   *                                                                
         CLC   RBUYKPLN,=3X'FF'    PLAN                                         
         BE    *+14                                                             
         MVC   PRAT+3(4),=C'PLAN'                                               
         B     PBBY0160                                                         
         EDIT  (4,RBUYCOS),(10,PRAT),2,FLOAT=-,COMMAS=YES                       
*                                                                               
*   RETRIEVE SPOTPAK REP-TO-SPOT TRANFER INFORMATION                            
*                                                                               
         GOTO1 SPOTPAK,DMCB                                                     
*                                                                               
PBBY0160 MVC   DUB(2),RBUYLEN      REC LENGTH                                   
         LH    R5,DUB                                                           
         LA    R5,RBUYREC(R5)      REC END FOR BXLE                             
*              PREPARE TO GET DAY-TIME ELEMENTS                                 
         BCTR  R5,R0                                                            
         BCTR  R5,R0                                                            
         SR    R4,R4                                                            
         LA    R3,RBUYELEM         FIRST ELEM                                   
* GET DAY SPAN                                                                  
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         IC    RE,RBUYSTED                                                      
         SRDL  RE,4                                                             
         SRL   RF,28                                                            
         CR    RE,RF                                                            
         BNH   *+8                                                              
         LA    RF,7(RF)                                                         
         SR    RF,RE                                                            
         ST    RF,DAY1                                                          
*                                                                               
         SH    R9,=Y(L'PRTLN)                                                   
*                                                                               
         SR    R6,R6               LINE CTR FOR DAY-TIME LINES                  
         B     PBBY0180            NO DATE ELEM                                 
*              DISPLAY DAY-TIME                                                 
PBBY0170 EQU   *                                                                
         CLC   =X'FFFF',2(R3)      CONVERT ERROR DATA?                          
         BNE   PBBY0172            NO                                           
         BAS   RE,KZDAYERR         YES - RETRIEVE FROM ERROR ELEMENT            
         B     PBBY0174                                                         
PBBY0172 EQU   *                                                                
         GOTO1 VOUTDAY,DMCB,3(R3),2(R3),PDAY                                    
PBBY0174 EQU   *                                                                
*                                                                               
         CLC   =X'FFFF',4(R3)      CONVERT ERROR DATA?                          
         BNE   PBBY0176            NO                                           
         BAS   RE,KZTIMERR         YES - RETRIEVE FROM ERROR ELEMENT            
         B     PBBY0178                                                         
PBBY0176 EQU   *                                                                
         GOTO1 VUNTIME,DMCB,4(R3),PTIM                                          
PBBY0178 EQU   *                                                                
         B     PBBY0180                                                         
         EJECT                                                                  
*   RETRIEVE THE X'22' ELEMENT, AND INSERT ITS CONTENTS TO THE                  
*        DAY FIELD ON THE SCREEN.  IF THIS ROUTINE IS REFERENCED,               
*        THE BUYLINE WILL ONLY CONTAIN A SINGLE X'02' ELEMENT,                  
*        BECAUSE IT WAS CONVERTED FROM KATZ THAT WAY.                           
*        R6 --> X'02' ELEMENT IN THE RECORD.                                    
*                                                                               
KZDAYERR NTR1                                                                   
         LA    R6,RBUYELEM                                                      
KZDA0020 EQU   *                                                                
         ZIC   RF,1(R6)                                                         
         AR    R6,RF               BUMP TO NEXT ELEMENT                         
         CLI   0(R6),0             END OF RECORD?                               
         BNE   *+6                 NO                                           
         DC    H'0'                ERROR RECORD MUST HAVE X'22'                 
         CLI   0(R6),X'22'         ERROR ELEMENT?                               
         BNE   KZDA0020            NO  - GO BACK FOR NEXT                       
         MVC   PDAY(10),2(R6)      YES - MOVE DATA TO FIELD                     
         XIT1                                                                   
         EJECT                                                                  
*                                                                               
*   RETRIEVE THE X'32' ELEMENT, AND INSERT ITS CONTENTS TO THE                  
*        TIME FIELD ON THE SCREEN.  IF THIS ROUTINE IS REFERENCED,              
*        THE BUYLINE WILL ONLY CONTAIN A SINGLE X'02' ELEMENT,                  
*        BECAUSE IT WAS CONVERTED FROM KATZ THAT WAY.                           
*        R6 --> X'02' ELEMENT IN THE RECORD.                                    
*                                                                               
KZTIMERR NTR1                                                                   
         LA    R6,RBUYELEM                                                      
KZTM0020 EQU   *                                                                
         ZIC   RF,1(R6)                                                         
         AR    R6,RF               BUMP TO NEXT ELEMENT                         
         CLI   0(R6),0             END OF RECORD?                               
         BNE   *+6                 NO                                           
         DC    H'0'                ERROR RECORD MUST HAVE X'32'                 
         CLI   0(R6),X'32'         ERROR ELEMENT?                               
         BNE   KZTM0020            NO  - GO BACK FOR NEXT                       
         MVC   PTIM(11),2(R6)      YES - MOVE DATA TO FIELD                     
         XIT1                                                                   
         EJECT                                                                  
*                                                                               
*                                                                               
PBBY0180 IC    R4,1(R3)            ELEM LEN                                     
         BXLE  R3,R4,*+8           NEXT ELEM                                    
         B     PBBY0500                                                         
*                                                                               
         CLI   0(R3),2             DAY TIME ELEM?                               
         BNE   PBBY0190                                                         
         LA    R9,L'PRTLN(R9)      OUTPUT                                       
         LA    R6,1(R6)            DAY-TIME CTR                                 
         CH    R6,MAX              MAXIMUM LINES?                               
         BH    PBBY0180                                                         
         B     PBBY0170            DISPLAY NEXT DAY-TIME                        
*                                                                               
*              GET DATE ELEMENTS                                                
PBBY0190 SR    R7,R7               DATE LINE CTR                                
         L     R1,SAVER1                                                        
         L     R9,4(R1)                                                         
         SH    R9,=Y(L'PRTLN)                                                   
         CLI   0(R3),3             DATE ELEM                                    
         BNE   PBBY0350            IF NONE JUST CARRY ON                        
         B     PBBY0340                                                         
* PROCESS DATE ELEMENT                                                          
PBBY0200 SR    RE,RE                                                            
         SR    RF,RF                                                            
         IC    RF,9(R3)            NO. PER WEEK                                 
* DISPLAY NPW                                                                   
         EDIT  (RF),(3,PNPW)                                                    
         SR    R2,R2                                                            
         IC    R2,10(R3)           NO. OF WEEKS                                 
* DISPLAY TOTAL SPOTS MINUS MISSED SPOTS                                        
PBBY0210 TM    RBUYCNTL,X'80'                                                   
         BO    PBBY0280                                                         
         SPACE 1                                                                
         LA    R1,RBUYELEM                                                      
         IC    R4,1(R1)                                                         
         AR    R1,R4                                                            
         CLI   0(R1),0                                                          
         BE    PBBY0220                                                         
         CLI   0(R1),4                                                          
         BNE   *-18                                                             
         CLC   2(3,R1),=C'CR='                                                  
         BE    PBBY0230            IGNORE THE MINUS BUY CHECK                   
         SPACE 1                                                                
PBBY0220 TM    RBUYCOS,X'80'                                                    
         BO    PBBY0280                                                         
PBBY0230 MR    RE,R2               TOTAL SPOTS IN RF                            
* GET MISSED SPOTS, IF ANY                                                      
         LA    R1,RBUYELEM                                                      
PBBY0240 CLI   0(R1),0             LAST?                                        
         BE    PBBY0270                                                         
         CLI   0(R1),6             MISSED ELEM?                                 
         BE    PBBY0260                                                         
         CLI   0(R1),7             MISSED CREDIT ISSUED                         
         BE    PBBY0260                                                         
PBBY0250 IC    R4,1(R1)                                                         
         AR    R1,R4               NEXT ELEM                                    
         B     PBBY0240                                                         
* CHECK MISSED DATE AGAINST DATE ELEM                                           
PBBY0260 CLC   2(3,R1),2(R3)        START                                       
         BL    PBBY0250                                                         
         CLC   2(3,R1),5(R3)       END                                          
         BH    PBBY0250                                                         
* MISSED ELEM APPLIES, SUBTRACT MISSED SPOTS                                    
         IC    R4,6(R1)            MISSED SPOTS                                 
         SR    RF,R4                                                            
         B     PBBY0250                                                         
* DISPLAY TOTAL SPOTS FOR DATE ELEM                                             
PBBY0270 EQU   *                                                                
* TEST FOR ALT WEEKS                                                            
PBBY0280 TM    8(R3),X'40'         ALT?                                         
         BZ    PBBY0310                                                         
         OC    VADDAY,VADDAY                                                    
         BZ    PBBY0310                                                         
* MUST BE OFF-LINE CONTRACT - EXPLODE ALTERNATE WEEKS                           
*                                                                               
* GET START-END IN WORK                                                         
         GOTO1 VDATCON,DMCB,(3,2(R3)),WORK                                      
*                                                                               
         GOTO1 (RF),(R1),(3,5(R3)),WORK+6                                       
PBBY0290 GOTO1 VDATCON,(R1),WORK,(4,PDAT)                                       
         MVI   PDAT+11,C'A'                                                     
         L     R2,DAY1                                                          
         LTR   R2,R2               ROTATOR?                                     
         BZ    PBBY0300                                                         
* GET END DATE OF WEEK                                                          
         GOTO1 VADDAY,(R1),WORK,WORK+12,(R2)                                    
*                                                                               
         GOTO1 VDATCON,(R1),WORK+12,(4,PDAT+6)                                  
         MVI   PDAT+5,C'-'                                                      
* GET NEXT ALTERNATE WEEK                                                       
PBBY0300 MVC   DUB(6),WORK                                                      
         GOTO1 VADDAY,(R1),DUB,WORK,14                                          
         CLC   WORK(6),WORK+6      PAST ELEM END DATE?                          
         BH    PBBY0330                                                         
*                                                                               
* DISPLAY NEXT ALTERNATE WEEK                                                   
         LA    R9,L'PRTLN(R9)      OUTPUT                                       
         LA    R7,1(R7)            LINE CTR                                     
         CH    R7,MAX                                                           
         BNH   PBBY0290                                                         
         LR    R6,R7                                                            
         B     PBBY0500                                                         
PBBY0310 GOTO1 VDATCON,DMCB,(3,2(R3)),(4,PDAT)                                  
         CLC   2(3,R3),5(R3)       SAME START-END?                              
         BE    PBBY0320                                                         
*                                                                               
         MVI   PDAT+5,C'-'                                                      
*              CONVERT ELEMENT END DATE TO MMMDD                                
         GOTO1 VDATCON,DMCB,(3,5(R3)),(4,PDAT+6)                                
*                                                                               
PBBY0320 TM    8(R3),X'40'         ALTERNATE WEEKS?                             
         BZ    PBBY0330                                                         
         MVI   PDAT+11,C'A'                                                     
         LA    R9,L'PRTLN(R9)                                                   
         LA    R7,1(R7)                                                         
         CH    R7,MAX                                                           
         BNH   *+10                                                             
         LR    R6,R7                                                            
         B     PBBY0500                                                         
         MVC   PDAT(15),=C'ALTERNATE WEEKS'                                     
*              GET NEXT ELEM                                                    
PBBY0330 IC    R4,1(R3)            ELEM LEN                                     
         BXLE  R3,R4,*+8           NEXT ELEM                                    
         B     PBBY0350                                                         
*                                                                               
         CLI   0(R3),3             ANOTHER DATE ELEM?                           
         BNE   PBBY0350                                                         
         TM    8(R3),X'40'         ALT WEEKS                                    
         BZ    PBBY0340                                                         
         OC    VADDAY,VADDAY                                                    
         BZ    PBBY0340                                                         
         MVI   PDAT+79,C'*'        IND ANOTHER ALT ELEM                         
PBBY0340 LA    R7,1(R7)            DATE CTR                                     
         LA    R9,L'PRTLN(R9)                                                   
         CH    R7,MAX                                                           
         BNH   PBBY0200            DISPLAY NEXT DATE ELEM                       
         LR    R6,R7                                                            
         B     PBBY0500                                                         
*              PREPARE TO DISPLAY COMMENTS                                      
PBBY0350 CR    R6,R7               FIND HIGHEST LINE SO FAR                     
         BNL   *+6                                                              
         LR    R6,R7                                                            
         CH    R6,MAX                                                           
         BE    PBBY0500                                                         
         CLI   0(R3),4             COMMENT ELEM?                                
         BE    PBBY0360                                                         
         CLI   0(R3),0             END OF RECORD?                               
         BE    PBBY0500                                                         
         B     PBBY0460            CHECK FOR ORDER COMMENTS                     
         SPACE 1                                                                
PBBY0360 LR    R7,R6                                                            
         MH    R7,=Y(L'PRTLN)                                                   
         L     R1,SAVER1                                                        
         L     R9,4(R1)            OUTPUT                                       
         LA    R9,0(R7,R9)         POSITION TO NEXT AVAILABLE LINE              
*                                                                               
*                                                                               
*                                                                               
*              DISPLAY COMMENT                                                  
PBBY0370 CLC   2(3,R3),=C'CR='     CREDIT FOR MISSED                            
         BNE   PBBY0390                                                         
         STM   R3,R5,DMCB+12       SAVE BXLE                                    
PBBY0380 IC    R4,1(R3)                                                         
         BXLE  R3,R4,*+8           NEXT ELEM                                    
         B     PBBY0410                                                         
         CLI   0(R3),0                                                          
         BE    PBBY0410                                                         
         CLI   0(R3),5             MAKE-GOOD ELEMENT?                           
         BNE   PBBY0380                                                         
         SPACE 1                                                                
         MVC   PDAY+6(29),=C'CREDIT FOR SPOT          LINE'                     
         CLI   9(R3),2             NO. OF MISSED                                
         BL    *+10                                                             
         MVC   PDAY+6(29),=C'CREDIT FOR SPOTS         LINE'                     
         SPACE 1                                                                
         EDIT  (1,2(R3)),(2,PDAY+36),ALIGN=LEFT                                 
         B     PBBY0410                                                         
         SPACE 1                                                                
PBBY0390 CLC   2(3,R3),=C'MG='     MAKE-GOOD?                                   
         BNE   PBBY0440                                                         
         STM   R3,R5,DMCB+12       SAVE BXLE                                    
         MVC   PDAY+6(18),=C'MAKE-GOOD FOR LINE'                                
* GET MAKE-GOOD ELEMENT                                                         
PBBY0400 IC    R4,1(R3)            ELEM LEN                                     
         BXLE  R3,R4,*+8           NEXT ELEM                                    
         B     PBBY0410                                                         
         CLI   0(R3),0                                                          
         BE    PBBY0410                                                         
         CLI   0(R3),5             MAKE-GOOD ELEMENT?                           
         BNE   PBBY0400                                                         
* DISPLAY MAKE-GOOD REFERENCE                                                   
         EDIT  (1,2(R3)),(3,PDAY+25),ALIGN=LEFT                                 
         GOTO1 VDATCON,DMCB,(3,3(R3)),(4,PDAT)    MISSED DATE                   
*                                                                               
         CLI   9(R3),1             NO. OF MISSED SPOTS                          
         BE    PBBY0410                                                         
         BNL   *+14                                                             
         MVC   PDAT+6(9),=C'(0 SPOTS)'                                          
         B     PBBY0410                                                         
         EDIT  (1,9(R3)),(3,PDAT+5)                                             
         MVC   PDAT+9(6),=C'SPOTS)'                                             
         LA    RE,PDAT+4                                                        
         LA    RE,1(RE)                                                         
         CLI   0(RE),C' '                                                       
         BE    *-8                                                              
         BCTR  RE,R0                                                            
         MVI   0(RE),C'('                                                       
*                                                                               
PBBY0410 LM    R3,R5,DMCB+12       BXLE                                         
* CHECK FOR COMMENT ALONG WITH MG=                                              
         LR    RE,R3                                                            
         SR    RF,RF                                                            
         IC    RF,1(R3)            ELEM LEN                                     
         SR    R1,R1                                                            
PBBY0420 CLI   0(RE),C' '                                                       
         BE    PBBY0430                                                         
         LA    RE,1(RE)                                                         
         LA    R1,1(R1)                                                         
         CR    R1,RF                                                            
         BL    PBBY0420                                                         
         B     PBBY0450                                                         
* MOVE REST OF COMMENT                                                          
PBBY0430 SR    RF,R1                                                            
         SH    RF,=H'2'                                                         
         BM    PBBY0450                                                         
         LA    R6,1(R6)                                                         
         LA    R9,L'PRTLN(R9)      NEXT LINE                                    
         CH    R6,MAX                                                           
         BH    PBBY0500                                                         
*                                                                               
         EX    RF,*+8                                                           
         B     PBBY0450                                                         
         MVC   PDAY+6(0),1(RE)                                                  
*                                                                               
PBBY0440 IC    R4,1(R3)            ELEM LEN                                     
         SH    R4,=H'3'                                                         
         EX    R4,MOVECOM          MOVE COMMENT TO DISPLAY LINE                 
*                                                                               
PBBY0450 LA    R6,1(R6)                                                         
         LA    R9,L'PRTLN(R9)                                                   
         IC    R4,1(R3)            ELEM LEN                                     
         BXLE  R3,R4,*+8           NEXT ELEM                                    
         B     PBBY0500                                                         
*                                                                               
         CLI   0(R3),4             COMMENT?                                     
         BNE   PBBY0470            CHECK FOR ORDER COMMENT                      
         CH    R6,MAX              MAXIMUM LINES?                               
         BNH   PBBY0440            DISPLAY NEXT COMMENT                         
         B     PBBY0500            OR RETURN                                    
         SPACE 1                                                                
PBBY0460 LR    R7,R6               IF NO REGULAR BUY CMTS,                      
         MH    R7,=Y(L'PRTLN)      POSITION TO NEXT AVAILABLE LINE              
         L     R1,SAVER1                                                        
         L     R9,4(R1)            OUTPUT                                       
         LA    R9,0(R7,R9)                                                      
*                                                                               
PBBY0470 CLI   0(R3),X'84'         ORDER COMMENT?                               
         BE    PBBY0480                                                         
         IC    R4,1(R3)                                                         
         BXLE  R3,R4,PBBY0470      END OF RECORD?                               
         B     PBBY0500                                                         
         SPACE 1                                                                
PBBY0480 CH    R6,MAX              MAXIMUM LINES?                               
         BNL   PBBY0500                                                         
         SPACE 1                                                                
         TM    RCONMODR+1,X'C0'    IF ACE OR GRAPHNET                           
         BZ    PBBY0490                                                         
         TM    SVCONF,X'40'        AND CONFIRMED                                
         BZ    PBBY0490                                                         
         TM    RBUYCNTL,X'80'      AND CANCELLED                                
         BO    PBBY0500            DON'T PRINT ORDER COMMENTS                   
         SPACE 1                                                                
PBBY0490 MVC   PDAY+6(4),=C'SOC-'  STATION ORDER CMT                            
         TM    2(R3),X'80'                                                      
         BZ    *+8                                                              
         MVI   PDAY+6,C'R'         REP ORDER CMT (ROC)                          
         IC    R4,1(R3)                                                         
         SH    R4,=H'4'                                                         
         EX    R4,*+8                                                           
         B     *+10                                                             
         MVC   PDAY+10(0),3(R3)                                                 
         CLC   3(3,R3),=C'#DS'     INDICATES DON'T SEND THIS LINE               
         BNE   *+8                                                              
         MVI   NOSEND,C'D'                                                      
         B     PBBY0450            LOOP BACK FOR OTHER ORDER CMTS               
*                                                                               
*              RETURN                                                           
PBBY0500 L     R1,SAVER1                                                        
         CH    R6,MAX                                                           
         BNH   *+8                                                              
         LH    R6,MAX                                                           
         STC   R6,4(R1)            LINE CTR                                     
         MVI   8(R1),0                                                          
         CLI   NOSEND,C'D'                                                      
         BNE   *+8                                                              
         MVI   8(R1),C'D'          INDICATE THIS LINE IS 'DON'T SEND'           
EXIT     XMOD1 1                                                                
*                                                                               
MOVECOM  MVC   PDAY+6(0),2(R3)                                                  
*                                                                               
         EJECT                                                                  
*                                                                               
*   SPOTPAK:  RETRIEVE X'08' ELEMENT, IF PRESENT, AND DISPLAY                   
*       CONTENTS ON PRINTLINE                                                   
*                                                                               
SPOTPAK  NTR1                                                                   
         LA    R3,RBUYELEM         LOCATE X'08' ELEMENT, IF PRESENT             
         USING RBUYSPEL,R3                                                      
SPOT0010 EQU   *                                                                
         CLI   0(R3),0             END OF RECORD?                               
         BE    SPOT0030            YES - FINISHED                               
         CLI   0(R3),8             TRANSFER ELEMENT?                            
         BE    SPOT0020            YES - PROCESS IT                             
         ZIC   RF,1(R3)            NO  - BUMP TO NEXT ELEMENT                   
         AR    R3,RF                                                            
         B     SPOT0010            GO BACK FOR NEXT                             
SPOT0020 EQU   *                                                                
         MVC   PCLI,RBUYSPCL       INSERT SPOTPAK CLIENT                        
         MVC   PPRD,RBUYSPPD       INSERT SPOTPAK PRODUCT                       
         EDIT  RBUYSPES,(3,PEST)   INSERT SPOTPAK ESTIMATE #                    
         EDIT  RBUYSPL#,(3,PBY#)   INSERT SPOTPAK BUYLINE #                     
         PRINT GEN                                                              
         GOTO1 VDATCON,DMCB,(3,RBUYSPDT),(X'20',PDAT2)                          
SPOT0030 EQU   *                                                                
         XIT1                                                                   
         EJECT                                                                  
* PRINT LINE DSECT                                                              
       ++INCLUDE REGENPBRD                                                      
*                                                                               
WORKD    DSECT                                                                  
WORK     DS    CL17                                                             
SVCONF   DS    CL1                 CONFIRMED STATUS FOR ACE/GRAPHNET            
VOUTDAY  DS    A                   A(OUTDAY)                                    
VUNTIME  DS    A                   A(UNTIME)                                    
VDATCON  DS    A                   A(DATCON)                                    
VADDAY   DS    A                   A(ADDAY)                                     
DMCB     DS    6F                                                               
DUB      DS    D                                                                
SAVER1   DS    F                                                                
MAX      DS    H                                                                
TODAY    DS    CL3                                                              
DAY1     DS    F                                                                
NOSEND   DS    X                   DON'T SEND STATUS                            
BUYREC   DSECT                                                                  
       ++INCLUDE REGENBUY                                                       
CONREC   DSECT                                                                  
       ++INCLUDE REGENCON                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'015REGENPBRB 05/01/02'                                      
         END                                                                    
