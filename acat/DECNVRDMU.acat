*          DATA SET DECNVRDMU  AT LEVEL 041 AS OF 06/22/92                      
*CATALP DERDMU                                                                  
         TITLE 'LOOK-UP TPT FILE FOR ADI/TSA PUT DATA'                          
DERDMU   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,**RDMU**                                             
         USING WORKD,RC            RC=A(W/S)                                    
         L     RA,0(R1)                                                         
         USING DEMCOND,RA          RA=A(DEMCON GLOBAL W/S)                      
         L     R7,AIREC                                                         
         USING INTERD,R7           R7=A(SORT RECORD)                            
         LA    R6,MYDBLOCK                                                      
         USING DBLOCKD,R6          R6=A(DBLOCK)                                 
         L     R8,4(R1)            STORAGE AREA FOR MKT UNIVS                   
*                                                                               
         XC    DBLOCK,DBLOCK                                                    
         XC    CNT,CNT                                                          
         MVC   DBFILE,=C'MPA'                                                   
         MVC   DBSELSRC,OUTSRC                                                  
         MVC   DBSELMED,MEDIA                                                   
         MVC   DBSELBK,INTBOOK                                                  
         MVC   DBBTYPE,INTBTYP                                                  
         CLI   BOOKTYPE,0                                                       
         BE    *+10                                                             
         MVC   DBBTYPE,BOOKTYPE                                                 
         XC    WORK,WORK                                                        
         MVC   WORK(3),=C'MPA'                                                  
         MVC   WORK+7(2),EFFS                                                   
         L     RF,ACOMFACS         GET A(DISPLACEMENT TABLES)                   
         ST    RF,DBCOMFCS                                                      
         L     RF,CDEMEL-COMFACSD(RF)                                           
         GOTO1 (RF),MYDMCB,(C'D',WORK),DBLOCKD                                  
         CLI   DBERROR,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   AIDISP,12(R1)                                                    
         MVC   AODISP,16(R1)                                                    
*                                  BUILD A LIST OF DEMOS TO LOOK-UP             
         L     R3,AODISP                                                        
         USING DSPHDRD,R3                                                       
         SR    R4,R4                                                            
         ICM   R4,3,DSPLDE                                                      
         ICM   R5,7,DSPAET                                                      
         LA    R5,0(R3,R5)                                                      
         BCTR  R5,0                                                             
         LA    R3,DSPHDRLN(R3)                                                  
         USING DSPDTAD,R3                                                       
         ST    R3,AODISPS          SAVE A(FIRST DEMO IN LIST)                   
         LA    RE,DEMOLIST                                                      
         LA    RF,DEMODISP                                                      
*                                                                               
RD2      CLI   DSPMOD,C'L'         US UNIVERSE (TO BUILD DEMO LIST)             
         BNE   RD6                                                              
*                                                                               
RD4      MVC   0(1,RE),DSPPREC                                                  
         MVC   1(2,RE),DSPMOD                                                   
         MVI   1(RE),C'U'          READ IN MARKET UNIVERSES INSTEAD             
         ZIC   R1,CNT              NUMBER DEMOS                                 
         LA    R1,1(R1)                                                         
         STC   R1,CNT                                                           
         LR    R1,R3                                                            
         S     R1,AODISPS                                                       
         SR    R0,R0                                                            
         DR    R0,R4                                                            
         SLL   R1,2                                                             
         STCM  R1,3,0(RF)                                                       
         LA    RE,3(RE)                                                         
         LA    RF,2(RF)                                                         
*                                                                               
RD6      BXLE  R3,R4,RD2                                                        
         MVI   0(RE),X'FF'                                                      
         MVI   0(RF),X'FF'                                                      
         B     RD10                                                             
*                                                                               
         EJECT                                                                  
* RD10-  READ IN MARKET UNIVERSES FOR DEMO LIST JUST BUILT                      
*                                                                               
RD10     XC    DBLOCK,DBLOCK                                                    
         MVC   DBFILE,=C'TP '                                                   
         MVI   DBSELMED,C'T'                                                    
         MVC   DBSELBK,INTBOOK                                                  
         MVC   DBBTYPE,INTBTYP                                                  
         MVC   DBSELRMK,INTMRKT    BUILD DBLOCK                                 
         MVC   DBSELSRC,OUTSRC                                                  
         CLI   BOOKTYPE,0                                                       
         BE    *+10                                                             
         MVC   DBBTYPE,BOOKTYPE                                                 
         MVI   DBFUNCT,DBGETTOT                                                 
         MVI   DBSELDAY,X'7C'                                                   
         MVC   DBSELTIM,=AL2(1800,1815)                                         
         LA    R1,IOAREA                                                        
         ST    R1,DBAREC                                                        
         L     RF,ACOMFACS                                                      
         ST    RF,DBCOMFCS                                                      
         L     RF,CDEMAND-COMFACSD(RF)                                          
         GOTO1 (RF),MYDMCB,DBLOCK,RD12                                          
         CLI   DBERROR,X'80'                                                    
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   DMCB(1),CNT         RETURN # DEMOS READ                          
*                                                                               
EXIT     XMOD1                                                                  
*                                                                               
CNT      DC    X'00'                                                            
*                                                                               
* -----------------------------------------------------------------             
* RD12-  GET DEMO VALUES & POST TO BUFFER                                       
* -----------------------------------------------------------------             
RD12     NTR1                                                                   
         LR    RE,R8               CLEAR LINE                                   
         L     RF,=F'600'                                                       
         XCEF                                                                   
         L     RF,ACOMFACS                                                      
         L     RF,CDEMOUT-COMFACSD(RF)                                          
         GOTO1 (RF),MYDMCB,(C'P',DEMOLIST),DBLOCK,(R8)                          
         CLI   DBERROR,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
         XIT1                                                                   
*                                                                               
         EJECT                                                                  
* ********************************************************************          
* LITERALS ETC.                                                                 
* ********************************************************************          
*                                                                               
         LTORG                                                                  
         SPACE 2                                                                
AIDISP   DS    A                                                                
AODISP   DS    A                                                                
AODISPS  DS    A                                                                
DEMOLIST DS    150XL3,X                                                         
         DS    250C           JUST IN CASE SOMEONE BUILDS A LONGER LIST         
         DS    0H                                                               
DEMODISP DS    150XL2                                                           
         DS    250C                SEE ABOVE                                    
         DS    0F                                                               
MYDBLOCK DS    CL256                                                            
         DS    CL4                 DBLOCK IS REALLY 260 BYTES                   
         DS    200C                SPARE                                        
POSTLEN  EQU   150                                                              
NUMDAYS  EQU   7                                                                
NUMQTHR  EQU   80                                                               
DAYBUFFL DC    A(POSTLEN*4)                                                     
QHRBUFFL DC    A(POSTLEN*NUMDAYS*4)                                             
PUTBUFFL DC    A(POSTLEN*NUMDAYS*NUMQTHR*4)                                     
EFFS     DC    X'FFFF'                                                          
         SPACE 1                                                                
* TABLE OF DAYS FOR ADI/TSA PUT DATA LOOK-UP.                                   
*                                                                               
* BYTE 0 = PWDW VALUE (TOP NIBBLE ONLY)                                         
*      1 = START DAY FOR LOOK-UP (1=MON, 7=SUN)                                 
*      2 = NUMBER OF DAYS TO LOOK-UP & AVERAGE                                  
*                                                                               
PWDWTAB  DS    0CL3                                                             
         DC    X'00',AL1(1,5)      M-F                                          
         DC    X'10',AL1(1,1)      MON                                          
         DC    X'20',AL1(2,1)      TUE                                          
         DC    X'30',AL1(3,1)      WED                                          
         DC    X'40',AL1(4,1)      THU                                          
         DC    X'50',AL1(5,1)      FRI                                          
         DC    X'60',AL1(6,1)      SAT                                          
         DC    X'70',AL1(7,1)      SUN                                          
         DC    X'80',AL1(1,7)      M-S                                          
         DC    X'D0',AL1(1,7)      TYPICAL (USE M-SU)                           
         DC    X'E0',AL1(6,2)      S-S                                          
         DC    X'FF',AL1(1,5)      VARIOUS (USE M-F)                            
         EJECT                                                                  
* DSECT TO COVER TEMPORARY WORKING STORAGE                                      
*                                                                               
WORKD    DSECT                                                                  
MYDUB    DS    D                                                                
MYDMCB   DS    6F                                                               
POSTDAYS DS    2F                                                               
POSTLINE DS    150F                                                             
GETPUTSW DS    C                   G=GET, P=PUT                                 
IOAREA   DS    1500C                                                            
WORKX    EQU   *                                                                
         SPACE 1                                                                
* DEDEMFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE DEDEMFILE                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* DEDEMCNVD                                                                     
         PRINT OFF                                                              
       ++INCLUDE DEDEMCNVD                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* DEDBLOCK                                                                      
         PRINT OFF                                                              
DBLOCKD  DSECT                                                                  
       ++INCLUDE DEDBLOCK                                                       
         PRINT ON                                                               
         SPACE 1                                                                
* DEDEMTABD                                                                     
         PRINT OFF                                                              
       ++INCLUDE DEDEMTABD                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* DEINTD                                                                        
         PRINT OFF                                                              
       ++INCLUDE DEINTD                                                         
       ++INCLUDE DEINTPAVD                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* DDCOMFACS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'041DECNVRDMU 06/22/92'                                      
         END                                                                    
