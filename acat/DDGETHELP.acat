*          DATA SET DDGETHELP  AT LEVEL 004 AS OF 11/01/02                      
*CATALP GETHELP                                                                 
         SPACE 1                                                                
*PARM1   XL1    FLAGS                                                           
*        AL3    A(KEY)                                                          
*                                                                               
*PARM2   XL1    MAXLINES            ZERO=23                                     
*        AL3    A('?' FIELD HEADER)                                             
*                                                                               
*PARM3   XL4   ZERO (AUTO)                                                      
*                                                                               
*PARM3   XL1   OFFSET                                                           
*        AL3   A(FIELD HEADER)                                                  
*                                                                               
*PARM4   XL1   INDEX INTO TEXT TABLE 0=START                                    
*        AL3   A(TEXT TABLE)                                                    
*                                                                               
*PARM5   XL1   RETURN CODE                                                      
*        AL3   A(32 BYTE SAVE AREA)                                             
*                                                                               
         TITLE 'READ AND OUTPUT HELP PANEL'                                     
GETHELP  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,XXGHLPXX,RA,RR=RE,CLEAR=YES                          
         USING WORKD,RC                                                         
         LA    RE,*+6                                                           
         BSM   0,RE                SET IN 24-BIT MODE                           
*                                                                               
         LA    R6,KEY                                                           
         USING HV1KEYD,R6                                                       
         LR    R2,R1                                                            
         USING GETHELPD,R2                                                      
         LA    R7,SVEHELP                                                       
         USING SVEHELPD,R7                                                      
         SPACE 1                                                                
***      L     RF,20(R2)           DUMMY LINK PARM 6                            
         L     RF,=V(SYSFAC)       LINK FROM FACPAC                             
         USING SYSFACD,RF                                                       
         L     RE,VSSB             LINK SSB                                     
         ST    RE,ASSB                                                          
         L     RE,VDATAMGR         LINK DATAMGR                                 
         ST    RE,ADATAMGR                                                      
         L     RE,VUTL             LINK UTL (LIST)                              
         ST    RE,AUTL                                                          
         L     RF,VSYSFAC0         LOCATE COMFACS                               
         USING COMFACSD,RF                                                      
         L     RE,CSWITCH          LINK SWITCH                                  
         ST    RE,ASWITCH                                                       
         L     RE,CGETTXT          LINK GETTXT                                  
         ST    RE,AGETTXT                                                       
         L     RE,CCALLOV          LINK CALLOV                                  
         ST    RE,ACALLOV                                                       
         DROP  RF                                                               
         EJECT                                                                  
         ICM   R1,15,ASSB          GET SSB                                      
         BZ    ERRORO              MUST BE ONLINE                               
         CLC   0(2,R1),BZEROES     CHECK FOR DUMMY SSB                          
         BZ    ERRORO                                                           
         SPACE 1                                                                
ONLINE   L     R1,SSBTKADR-SSBD(R1)                                             
         L     R8,TCBUTL-TCBD(R1)  R8=UTL (ENTRY)                               
         L     R4,TCBTWA-TCBD(R1)  R4=TWA                                       
         L     RF,TCBWRKA-TCBD(R1)                                              
         ST    RF,WORKAD                                                        
         MVC   SAVESYS,TCBSYS-TCBD(R1)                                          
         MVC   SAVEOV,TCBSWSOV-TCBD(R1)                                         
         USING UTLD,R8                                                          
         USING T109FFD,R4                                                       
*                                  SWITCH TO CONTROL                            
         GOTO1 ASWITCH,DMCB,X'0AFFFFFF',0                                       
         CLI   DMCB+4,0            TEST SUCCESSFUL SWITCH                       
         BNE   ERRORS              NO                                           
         SPACE 1                                                                
         MVI   MORE,C'Y'           ASSUME MORE WILL FOLLOW                      
         EJECT                                                                  
*********************************************************                       
*     IF PARAMETER 5 FLAGS GHSAVEQ PARM 5 IS ADDRESS    *                       
*     OF THE SAVE AREA PASSED BACK FROM LAST CALL       *                       
*  THIS SECTION BUILDS A PARAMETER LIST FROM THAT BLOCK *                       
*********************************************************                       
         TM    GTHFLAGS,GHSAVEQ    IS THERE A RETURN BLOCK                      
         BNO   INIT20                                                           
         SR    R1,R1                                                            
         ICM   R1,7,GTHASVE                                                     
         CLC   0(4,R1),=C'GHLP'                                                 
         BNE   ERRORR              BAD RETURN BLOCK                             
         MVC   SVEHELP,0(R1)       COPY SAVE BLOCK                              
*                                                                               
         NI    GTHFLAGS,X'FF'-GHTEXTQ CLR TEXT FLAG                             
         MVC   HV1DA,HSVDAKEY      GET DIRECT KEY                               
*                                                                               
         MVC   GTHMAXLN,HSVMAXLN   RESTORE PARM 2                               
         SR    R1,R1                                                            
         ICM   R1,3,HSVQHDR        OFFS FROM TWA                                
         BZ    *+6                                                              
         AR    R1,R4                                                            
         STCM  R1,7,GTHQHDR                                                     
*                                                                               
         MVC   GTHOFFS,HSVOFFS     RESTORE PARM 3                               
         SR    R1,R1                                                            
         ICM   R1,3,HSVAHDR        OFFS FROM TWA                                
         BZ    *+6                                                              
         AR    R1,R4                                                            
         STCM  R1,7,GTHAHDR                                                     
*                                                                               
         XC    GTHATXT,GTHATXT      NO TEXT POSSIBLE                            
         MVC   HSVTTRC,TTRCNT       SAVE FOR NEXT CALL                          
         NI    GTHFLAGS,X'FF'-GHSAVEQ CLR RETURN FLAG                           
         B     GOTHELP              GO STRAIGHT TO GENFIL                       
*                                                                               
INIT20   MVC   HSVTTRC,TTRCNT      SAVE FOR NEXT CALL                           
         MVC   HSVOFFS,GTHOFFS                                                  
         MVC   HSVMAXLN,GTHMAXLN                                                
         SR    R1,R1                                                            
         ICM   R1,7,GTHQHDR        SAVE P2                                      
         BZ    *+6                                                              
         SR    R1,R4                                                            
         STCM  R1,3,HSVQHDR                                                     
         SR    R1,R1                                                            
         ICM   R1,7,GTHAHDR        SAVE P3                                      
         BZ    *+6                                                              
         SR    R1,R4                                                            
         STCM  R1,3,HSVAHDR                                                     
         TM    GTHFLAGS,GHTEXTQ    IS IT ONLY TEXT                              
         BNO   MYKEY                                                            
         LA    R1,0                                                             
         MVI   MORE,C'N'                                                        
         MVI   ALLTX,C'Y'                                                       
         OI    GTHRET,GHTEXTQ                                                   
         B     TXTSTART            IF TEXT SKIP DISK STUFF                      
         EJECT                                                                  
***********************************************************                     
*        BUILD A KEY TO READ GENDIR HELP PANELS           *                     
***********************************************************                     
         SPACE 1                                                                
MYKEY    MVI   HV1KSYS,HV1KSYSQ                                                 
         MVI   HV1TYPE,HV1TYPEQ                                                 
         MVC   HV1SYS,SAVEOV       GET SYSTEM                                   
         CLI   SAVEOV,1                                                         
         BH    *+10                                                             
         MVC   HV1SYS,TOVSYS       GET SYSTEM IF SAVEOV IS ZERO                 
         MVC   HV1PROG,TPRG        GET PROGRAM                                  
*                                                                               
         TM    GTHFLAGS,GHUKEYQ    USE MY KEY ?                                 
         BO    GOTSCRN                                                          
*                                                                               
*                                  CALCULATE SCREEN NUMBER                      
         SR    R1,R1                                                            
         ICM   R1,7,GTHQHDR        R1 = A(? FIELD)                              
         BZ    GOTSCRN                                                          
         TM    1(R1),X'02'         FIELD HAS EXTENDED HEADER?                   
         BZ    USEUTL              NO -- USE UTL MAP                            
         ZIC   RF,0(R1)                                                         
         AR    RF,R1               BUMP RF TO NEXT FIELD. . .                   
         SH    RF,=H'8'            . . . AND BACK UP RF TO EXTENSION            
         CLI   4(RF),0             IS SCREEN NUMBER PRESENT?                    
         BE    USEUTL              NO -- USE UTL MAP                            
         MVC   HV1SCRN,4(RF)       YES -- USE IT                                
         B     GOTSCRN                                                          
*                                                                               
USEUTL   SR    R1,R4                                                            
         STH   R1,HELPDISP         OFFSET FROM TWA                              
         CLI   TSCRNE,0            NUMBER OF SCREENS                            
         BE    ERRORCT             MUST BE AT LEAST ONE SCREEN                  
         CLC   HELPDISP,TSCRNUM+1  HELP MUST BE ON OR AFTER 1ST SCREEN          
         BNL   *+6                                                              
         DC    H'0'                                                             
         CLI   TSCRNE,1            ONLY SCREEN -- WE'VE GOT IT                  
         BNE   *+14                                                             
         MVC   HV1SCRN,TSCRNUM                                                  
         B     GOTSCRN                                                          
         CLC   HELPDISP,TSCRNUM+4  TEST COULD BE IN 2ND OR 3RD SCREEN           
         BNL   *+14                                                             
         MVC   HV1SCRN,TSCRNUM     NO -- IT'S IN FIRST SCREEN                   
         B     GOTSCRN                                                          
         CLI   TSCRNE,2                                                         
         BNE   *+14                                                             
         MVC   HV1SCRN,TSCRNUM+3   MUST BE IN SECOND SCREEN                     
         B     GOTSCRN                                                          
         CLI   TSCRNE,3                                                         
         BNH   *+6                 NO MORE THAN 3 SCREENS                       
         DC    H'0'                                                             
         CLC   HELPDISP,TSCRNUM+7                                               
         BNL   *+14                                                             
         MVC   HV1SCRN,TSCRNUM+3   SECOND SCREEN                                
         B     GOTSCRN                                                          
         MVC   HV1SCRN,TSCRNUM+6   THIRD SCREEN                                 
GOTSCRN  EQU   *                                                                
         SPACE 1                                                                
         MVI   HV1FIELD,0          USER SUPPLIED                                
         MVI   HV1PAGE,1           DEFAULT TO 1                                 
         MVC   HV1LANG,TLANG                                                    
         CLI   HV1LANG,0           IF ENGLISH                                   
         BNE   *+8                                                              
*&&UK*&& MVI   HV1LANG,2           DEFAULT UK ENGLISH                           
*&&US*&& MVI   HV1LANG,1           DEFAULT US ENGLISH                           
         EJECT                                                                  
**************************************                                          
*  MERGE CALLER KEY INTO GENDIR KEY  *                                          
**************************************                                          
         SPACE 1                                                                
KEYMERGE TM    GTHFLAGS,GHUKEYQ    USE MY KEY ?                                 
         BO    MERGE4                                                           
         TM    GTHFLAGS,GHMKEYQ    MERGE MY KEY ?                               
         BO    MERGE1                                                           
         MVC   HV1FIELD,GTHFIELD                                                
         B     MERGE10                                                          
*                                                                               
MERGE1   LA    RE,(HV1KEY+22)-1    RE=REAL KEY -1                               
         SR    RF,RF                                                            
         ICM   RF,7,GTHAKEY                                                     
         BCTR  RF,0                RF=CALLERS KEY -1                            
         LA    R1,10                                                            
*                                                                               
MERGE2   ZIC   R0,0(RF,R1)         READ CALLERS KEY                             
         LTR   R0,R0                                                            
         BZ    MERGE3              IF ZERO DONT USE IT                          
         STC   R0,0(RE,R1)         PLUG INTO REAL KEY                           
MERGE3   BCT   R1,MERGE2                                                        
         B     MERGE10                                                          
*                                                                               
MERGE4   SR    RF,RF               MOVE THE LOT                                 
         ICM   RF,7,GTHAKEY                                                     
         MVC   HV1KEY+22(10),0(RF)                                              
*                                                                               
MERGE10  TM    HV1LANG,X'80'       UNLESS ALREADY DONE                          
         BNZ   *+8                                                              
         XI    HV1LANG,X'FF'       INVERT LANGUAGE                              
*                                                                               
         MVC   LANGNUM,HV1LANG     SAVE LANGUAGE                                
         SPACE 1                                                                
         MVC   KEYSAVE,HV1KEY                                                   
         BAS   RE,BLDID                                                         
         BAS   RE,READHELP         TRY FULL KEY                                 
         BE    GOTHELP                                                          
         MVC   HV1KEY,KEYSAVE                                                   
         CLI   HV1FIELD,0          IF SYS/PRG HELP DONT TRY GENERICS            
         BE    ERRORH                                                           
         MVI   HV1SCRN,0           TRY PROGRAM GENERIC RECORD                   
         BAS   RE,READHELP                                                      
         BE    GOTHELP                                                          
         MVC   HV1KEY,KEYSAVE                                                   
         MVI   HV1PROG,0           TRY SYSTEM GENERIC RECORD                    
         BAS   RE,READHELP                                                      
         BNE   ERRORH              NO HELP                                      
         EJECT                                                                  
******************************************************                          
*    READ HELP RECORD AND/OR CALCULATE PANEL SISE    *                          
******************************************************                          
         SPACE 1                                                                
GOTHELP  MVC   HSVDAKEY,HV1DA      SAVE KEY (DIRECT)                            
         GOTO1 ADATAMGR,DMCB,=C'GETREC',=C'GENFIL',HV1DA,REC,DMWORK             
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   KEYSAVE,REC                                                      
         BAS   RE,BLDID                                                         
         SPACE 1                                                                
         LA    R6,REC                                                           
         MVC   KEY,HV1KEY          COPY KEY BACK                                
         MVC   HSVFKEY,HV1SYS      SAVE SHORT KEY                               
*                                                                               
NXTPAGE  MVC   NEWKEY,KEY          COPY KEY                                     
         LA    R6,NEWKEY                                                        
         SR    RE,RE                                                            
         IC    RE,HV1PAGE                                                       
         LA    RE,1(RE)            FIND NEXT PAGE                               
         STC   RE,HV1PAGE                                                       
         MVC   KEYSAVE,NEWKEY                                                   
         GOTO1 ADATAMGR,DMCB,=C'DMRDHI',=C'GENDIR',NEWKEY,NEWKEY,0              
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
         CLC   KEYSAVE,NEWKEY      IS IT THERE                                  
         BNE   NXTX                                                             
         MVC   NEWDA,HV1DA         YES SO SAVE DAKEY                            
         MVC   NEWFKEY,HV1SYS      SAVE SHORT KEY                               
NXTX     LA    R6,REC                                                           
*                                                                               
RECOUNT  LA    R5,HV1FSTEL                                                      
         USING HV1TXTD,R5                                                       
         MVI   ELCODE,HV1TXTEQ                                                  
         LA    R1,0                                                             
         BAS   RE,FIRSTEL          GET TEXT ELEMENT                             
         BE    COUNT                                                            
         DC    H'0'                !!WOT NO TEXT!!                              
COUNT    XC    ELSAVE,ELSAVE                                                    
COUNT0   LA    R1,1(R1)            COUNT LINES                                  
         BAS   RE,NEXTEL                                                        
         BNE   LASTEL                                                           
         CLI   HV1TXTTX,C'|'       IS IT ONLY A '|'                             
         BNE   COUNT                                                            
         OC    ELSAVE,ELSAVE       AM I STILL HOLDING                           
         BNZ   COUNT0                                                           
         ST    R5,ELSAVE           HOLD THIS ELEMENT                            
         B     COUNT0                                                           
LASTEL   ICM   R5,15,ELSAVE        HAVE I HELD AN ELEMENT                       
         BZ    TXTSTART                                                         
         XC    0(2,R5),0(R5)       MAKE THIS A DUMMY LAST EL                    
         ST    R5,LASTSV                                                        
         B     RECOUNT             AND RECOUNT THEM                             
         EJECT                                                                  
TXTSTART LA    R1,2(R1)            PLUS 2 FOR ASTS                              
         SR    RE,RE                                                            
         ICM   RE,1,HSVSEQ                                                      
         BZ    COUNT1                                                           
         BCTR  RE,0                                                             
         SR    R1,RE               SUBTRACT LAST SEQUENCE NO                    
COUNT1   SR    R9,R9                                                            
         ICM   R9,7,GTHATXT        LOCATE TEXT TABLE                            
         BZ    COUNT4                                                           
         SR    RE,RE                                                            
         IC    RE,GTHNTXT          GET INDEX                                    
         MH    RE,=H'75'                                                        
         AR    R9,RE               MOVE TO INDEX * 75                           
         LR    RF,R9                                                            
COUNT5   CLI   0(RF),0                                                          
         BE    COUNT4                                                           
         LA    R1,1(R1)            COUNT LINES                                  
         LA    RF,75(RF)                                                        
         B     COUNT5                                                           
COUNT4   STH   R1,HLPLINES                                                      
         SR    R1,R1                                                            
         ICM   R1,1,GTHMAXLN                                                    
         BNZ   COUNT2                                                           
         LA    R1,23                                                            
COUNT2   CH    R1,HLPLINES         CHECK MAXIMUM                                
         BNL   COUNT3                                                           
         STH   R1,HLPLINES                                                      
COUNT3   LH    R1,HLPLINES                                                      
         MH    R1,=H'101'          101 BYTES OF TWA PER LINE                    
         SH    R1,=H'28'           LESS 14 FOR EACH AST FIELD                   
         ST    R1,HELPSISE                                                      
         EJECT                                                                  
***************************************                                         
*  CALCULATE ROWS FOR PANEL POSITION  *                                         
***************************************                                         
ROWCALC  SR    R1,R1                                                            
         MVI   AUTO,C'N'                                                        
         ICM   R1,7,GTHAHDR                                                     
         BNZ   ROW2                                                             
*                                                                               
         CLI   GTHOFFS,0           WORK IT OUT MYSELF                           
         BNE   ROW0                                                             
         MVI   AUTO,C'Y'                                                        
         TM    GTHFLAGS,GHCLSQ                                                  
         BNO   AT1                                                              
         OC    NEWDA,NEWDA                                                      
         BNZ   CLEAR                                                            
         CLI   HV1PAGE,X'01'                                                    
         BNE   CLEAR                                                            
AT1      ICM   R1,7,GTHQHDR        WHERE IS HELP REQUIRED                       
         SR    RF,RF                                                            
         ICM   RF,3,2(R1)                                                       
         STH   RF,HLPSCRAD                                                      
         CH    RF,=H'960'          < > HALFWAY                                  
         BH    RTOP                                                             
         B     RBOT                                                             
*                                                                               
ROW0     CLI   GTHOFFS,C'T'        ? TOP                                        
         BNE   ROW1                                                             
RTOP     LA    RF,1                TOPROW = 1                                   
         B     ROW3                                                             
*                                                                               
ROW1     CLI   GTHOFFS,C'B'        ? BOTTOM                                     
         BNE   ROW2                                                             
RBOT     LA    RF,24                                                            
         SH    RF,HLPLINES         TOPROW = 24-HLPLINES                         
         BP    *+6                                                              
         DC    H'0'                DIE IF TOO BIG                               
         B     ROW3                                                             
*                                                                               
ROW2     CLI   GTHOFFS,C'C'                                                     
         BE    CLEAR                                                            
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         ICM   RF,3,2(R1)          USE FIELD HEADER                             
         D     RE,=F'80'                                                        
         SR    RE,RE                                                            
         IC    RE,GTHOFFS          + OFFSET                                     
         LA    RF,0(RE,RF)         TO GET ROW                                   
*                                                                               
ROW3     STH   RF,TOPROW                                                        
         AH    RF,HLPLINES         CALCULATE BOTTOM ROW                         
         CH    RF,=H'24'           TEST FOR END OF SCREEN                       
         BH    CHOP1                                                            
ROW4     EQU   *                                                                
         EJECT                                                                  
***************************************                                         
*     CALCULATE SCREEN ADDRESSES      *                                         
***************************************                                         
         STH   RF,BOTROW                                                        
         MH    RF,=H'80'                                                        
         LA    RF,1(RF)                                                         
         STH   RF,BOTSCRAD                                                      
         LH    RF,TOPROW                                                        
         MH    RF,=H'80'                                                        
         LA    RF,1(RF)                                                         
         STH   RF,TOPSCRAD                                                      
*                                                                               
         CLI   AUTO,C'Y'           IF I WORKED IT OUT                           
         BNE   TWA0                                                             
         CLC   TOPSCRAD,HLPSCRAD   I MUST NOT BLOT OUT                          
         BH    TWA0                                                             
         CLC   BOTSCRAD,HLPSCRAD   THE QUESTION FIELD                           
         BNH   TWA0                                                             
*                                                                               
CHOP1    TM    GTHFLAGS,GHCLSQ     TEST FOR CLEAR OPTION                        
         BO    CLEAR                                                            
         L     R1,HELPSISE         REMOVE 1 LINE OF HELP                        
         SH    R1,=H'101'                                                       
         ST    R1,HELPSISE                                                      
         LH    R1,HLPLINES                                                      
         BCTR  R1,0                                                             
         STH   R1,HLPLINES                                                      
         B     ROWCALC             AND TRY AGAIN                                
*                                                                               
CLEAR    GOTO1 ACALLOV,DMCB,HV1MSGH,X'D90109FF',0,0                             
         LA    R1,HV1MSGH                                                       
         ICM   R1,8,=X'03'         DO IT THE OLD WAY                            
         ST    R1,GTHOFFS                                                       
         OI    GTHRET,GHCLSQ       SET RETURN FLAG                              
         ICM   R1,15,LASTSV                                                     
         BZ    ROWCALC                                                          
         MVC   0(10,R1),DUMMY      INSERT DUMMY SPACE                           
         L     R1,HELPSISE                                                      
         AH    R1,=H'101'                                                       
         ST    R1,HELPSISE                                                      
         LH    R1,HLPLINES                                                      
         LA    R1,1(R1)                                                         
         STH   R1,HLPLINES                                                      
         B     ROWCALC                                                          
         EJECT                                                                  
***************************************                                         
*  CALCULATE MOVE PARAMETERS & MOVE   *                                         
***************************************                                         
         SPACE 1                                                                
TWA0     LA    R5,64(R4)           A(SCREEN)                                    
TWA1     SR    R1,R1                                                            
         ICM   R1,3,2(R5)          FIND 1ST HIDDEN FIELD                        
         CH    R1,TOPSCRAD                                                      
         BNL   TWA2                                                             
         SR    R1,R1                                                            
         ICM   R1,1,0(R5)                                                       
         BZ    TWA2                CHECK FOR END OF SCREEN                      
         AR    R5,R1                                                            
         B     TWA1                                                             
TWA2     ST    R5,PANSTRT                                                       
TWA3     SR    R1,R1                                                            
         ICM   R1,3,2(R5)          FIND NEXT DISPLAYED FIELD                    
         CH    R1,BOTSCRAD                                                      
         BNL   TWA4                                                             
         SR    R1,R1                                                            
         ICM   R1,1,0(R5)                                                       
         BZ    TWA4                CHECK FOR END OF SCREEN                      
         TM    1(R5),X'20'         CHECK PROTECTED                              
         BO    TWA3A                                                            
         CLI   TSVCREQ+1,X'09'    TEST $HELP                                    
         BNE   TWA3A                                                            
*                                                                               
         ST    R1,SAVER1          SAVE R1                                       
         PROT  OFF                DISSABLE CALLER'S STORAGE PROTECTION          
         OI    TSTAT2,TSTATHUN    SET UNPROT HIDDEN                             
         PROT  ON                 RESTORE CALLER'S STORAGE PROTECTION           
         L     R1,SAVER1          RESTORE R1                                    
*                                                                               
TWA3A    AR    R5,R1                                                            
         B     TWA3                                                             
TWA4     ST    R5,PANEND                                                        
*                                                                               
TWA5     SR    R1,R1                                                            
         ICM   R1,1,0(R5)                                                       
         BZ    TWA6                CHECK FOR END OF SCREEN                      
*                                                                               
         SR    R1,R1                                                            
         ICM   R1,3,2(R5)          FIND ANY MORE TO HIDE                        
         CH    R1,TOPSCRAD                                                      
         BL    TWA5A                                                            
         CH    R1,BOTSCRAD                                                      
         BNL   TWA5A                                                            
         MVI   1(R5),X'FF'         DISSABLE THEM                                
*                                                                               
TWA5A    SR    R1,R1                                                            
         ICM   R1,1,0(R5)                                                       
         BZ    TWA6                FIND END OF SCREEN                           
         AR    R5,R1                                                            
         B     TWA5                                                             
TWA6     LA    R5,3(R5)            + 3 FOR END MARKERS                          
         ST    R5,SCREND                                                        
         SPACE 1                   SET MOVE PARMS                               
         L     R1,PANEND                                                        
         ST    R1,P1               FROM PANEND                                  
         LCR   R1,R1                                                            
         A     R1,SCREND                                                        
         ST    R1,P3               L' OF SCREND-PANEND                          
         L     R1,PANSTRT                                                       
         A     R1,HELPSISE                                                      
         ST    R1,P2               TO PANSTRT+HELPSISE                          
         LA    R1,PARMS                                                         
         BAS   RE,MOVE             MAKE ROOM FOR PANEL                          
*                                                                               
         L     R1,P2               CALCULATE SCREEN RELO                        
         S     R1,P1                                                            
         ST    R1,SCRRELO                                                       
*                                                                               
         L     R0,PANSTRT                                                       
         L     R1,HELPSISE                                                      
         L     RE,PANEND                                                        
         SR    RF,RF                                                            
         MVCL  R0,RE               CLEAR PANEL AREA                             
         EJECT                                                                  
***************************************                                         
*    BUILD TOPLINE AND LOCATE TEXT    *                                         
***************************************                                         
         LH    R1,BOTSCRAD                                                      
         SH    R1,=H'80'           BOTSCRAD = LAST LINE OF PANEL                
         STH   R1,BOTSCRAD                                                      
         MVC   WRKSCRAD,TOPSCRAD                                                
         L     R3,PANSTRT                                                       
         MVC   0(8,R3),HLPASTSH    TOP LINE HEADER                              
         MVC   2(2,R3),WRKSCRAD    +SCREEN ADDRESS                              
         MVC   8(1,R3),TL                                                       
         MVC   9(2,R3),TOPCHRS                                                  
         MVC   11(75,R3),9(R3)                                                  
         MVC   86(1,R3),TR                                                      
*                                                                               
         CLI   ALLTX,C'Y'          TEXT ONLY ?                                  
         BNE   DSK                                                              
         L     R3,PANSTRT          THEN NO HEADER                               
         LA    R3,87(R3)                                                        
         B     TXT1                                                             
*                                                                               
DSK      LA    R5,HV1FSTEL                                                      
         USING HV1HEDD,R5                                                       
         MVI   ELCODE,HV1HEDEQ                                                  
         BAS   RE,FIRSTEL          GET HEADER ELEMENT                           
         BNE   TEXTOUT                                                          
         LA    R1,79               POSITION TO CENTRE                           
         SR    RE,RE                                                            
         IC    RE,HV1HEDTL                                                      
         SR    R1,RE                                                            
         SRL   R1,1                                                             
         N     R1,=F'-2'           BUT ALWAYS EVEN                              
         LA    R3,7(R1,R3)                                                      
         MVI   0(R3),C' '          SPACE BEFORE                                 
         LA    R3,1(R3)                                                         
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),HV1HEDTX    INSERT HEADER                                
         AR    R3,RE                                                            
         MVI   1(R3),C' '          SPACE AFTER                                  
         DROP  R5                                                               
*                                                                               
TEXTOUT  L     R3,PANSTRT                                                       
         LA    R3,87(R3)           SKIP TOPLINE                                 
         LA    R5,HV1FSTEL                                                      
         USING HV1TXTD,R5                                                       
         MVI   ELCODE,HV1TXTEQ                                                  
         BAS   RE,FIRSTEL          FIND 1ST TEXT LINE                           
DSK0     CLC   HV1TXTSQ,HSVSEQ                                                  
         BNL   DSK1                                                             
         BAS   RE,NEXTEL                                                        
         BE    DSK0                                                             
         DC    H'0'                BAD SEQUENCE NUMBERS                         
         EJECT                                                                  
***************************************                                         
*    CREATE HEADERS & PANEL LINES     *                                         
***************************************                                         
DSK1     MVC   HSVSEQ,HV1TXTSQ     SAVE SEQUENCE NUMBER                         
TXT1     LA    R1,80                                                            
         AH    R1,WRKSCRAD         UPDATE SCREEN ADDRESS                        
         STH   R1,WRKSCRAD                                                      
         CH    R1,BOTSCRAD                                                      
         BNL   LSTTXT                                                           
*                                                                               
         MVC   0(8,R3),HLPASTH     LEFT AST                                     
         MVC   2(2,R3),WRKSCRAD                                                 
         MVC   8(1,R3),MID                                                      
*                                                                               
         MVC   9(8,R3),HLPTXTH     TEXT HEADER                                  
         LA    R1,2                                                             
         AH    R1,WRKSCRAD                                                      
         STCM  R1,3,11(R3)         INSERT SCREEN ADDRESS                        
         MVC   92(8,R3),HLPASTH    RIGHT AST                                    
         LA    R1,76(R1)                                                        
         STCM  R1,3,94(R3)                                                      
         MVC   100(1,R3),MID                                                    
*                                                                               
         TM    GTHRET,GHTEXTQ      TEXT TABLE OR DISK                           
         BO    TXT2                                                             
         EJECT                                                                  
***************************************                                         
*    INSERT TEXT FROM DISK RECORDS    *                                         
***************************************                                         
         SR    RE,RE                                                            
         IC    RE,HV1TXTLN                                                      
         LA    R0,HV1TXTOV                                                      
         SR    RE,R0               RE=TEXT LENGTH                               
         BZ    DSK2                                                             
         BCTR  RE,0                                                             
         EX    RE,*+8              MOVE IN TEXT                                 
         B     DSK2                                                             
         MVC   17(0,R3),HV1TXTTX                                                
*                                                                               
DSK2     TM    HV1TXTOP,GHSAVEQ    CHECK HIGH INTENSITY                         
         BZ    DSK3                                                             
         OI    10(R3),X'08'         SET HIGH INTENSITY BIT                      
         AR    RE,R3                                                            
         CLI   17(RE),X'4F'        IS THIS A VBAR                               
         BNE   DSK3                                                             
         MVI   17(RE),C' '         REMOVE HIGH INTENSITY CHARACTER              
*                                                                               
DSK3     BAS   RE,NEXTEL                                                        
         LA    R3,101(R3)                                                       
         BE    DSK1                TEST FOR LAST TEXT                           
         MVI   MORE,C'N'           IF FOUND NO MORE                             
         LTR   R9,R9                                                            
         BZ    TXT5                                                             
         OI    GTHRET,GHTEXTQ                                                   
         B     TXT1                                                             
         SPACE 2                                                                
***************************************                                         
*     INSERT TEXT FROM TEXT TABLE     *                                         
***************************************                                         
TXT2     MVC   17(75,R3),0(R9)                                                  
         CLI   0(R9),C'|'                                                       
         BNE   TXT3                                                             
         OI    10(R3),X'08'                                                     
         MVI   17(R3),C' '                                                      
TXT3     IC    R1,GTHNTXT                                                       
         LA    R1,1(R1)                                                         
         STC   R1,GTHNTXT                                                       
         LA    R9,75(R9)                                                        
         LA    R3,101(R3)                                                       
         CLI   0(R9),0                                                          
         BNE   TXT1                                                             
         NI    GTHRET,X'FF'-GHTEXTQ CLR TXT FLAG                                
         EJECT                                                                  
***************************************                                         
*  BUILD BOTTOM LINE CHECK FOR MORE   *                                         
***************************************                                         
TXT5     LA    R1,80                                                            
         AH    R1,WRKSCRAD                                                      
         STH   R1,WRKSCRAD                                                      
LSTTXT   MVC   0(8,R3),HLPASTSH    BOTTOM LINE HEADER                           
         MVC   2(2,R3),WRKSCRAD    +SCREEN ADDRESS                              
         MVC   8(1,R3),BL                                                       
         MVC   9(2,R3),BOTCHRS                                                  
         MVC   11(75,R3),9(R3)                                                  
         TM    TSTAT1,TSTATDDS                                                  
         BZ    *+10                                                             
         MVC   55(26,R3),WORK1     OUTPUT PANEL ID                              
         MVC   86(1,R3),BR                                                      
         MVI   OUT,0                                                            
         CLI   MORE,C'Y'                                                        
         BE    RET1                                                             
         OC    NEWDA,NEWDA         ANOTHER PAGE ?                               
         BZ    EXIT0               NOT FOUND                                    
         MVI   HSVSEQ,0                                                         
         OI    GTHRET,GHPAGEQ                                                   
         MVC   HSVDAKEY,NEWDA      GOT ONE                                      
         MVC   HSVFKEY,NEWFKEY                                                  
         MVI   MORE,C'Y'                                                        
RET1     MVC   HSVIDENT,=C'GHLP'                                                
         OI    GTHRET,GHSAVEQ                                                   
         MVI   OUT,C'?'                                                         
         SR    R1,R1                                                            
         ICM   R1,7,GTHASVE                                                     
         BZ    LASTHELP                                                         
         MVC   0(32,R1),SVEHELP                                                 
EXIT0    CLI   AUTO,C'Y'                                                        
         BNE   EXIT1                                                            
*                                                                               
         SR    R1,R1               FIND ? FIELD                                 
         ICM   R1,7,GTHQHDR                                                     
         C     R1,PANEND           IF AFTER PANEL                               
         BL    *+8                 ADD RELOCATION FACTOR                        
         A     R1,SCRRELO                                                       
         OI    6(R1),X'01'         TRANSMIT ANOTHER ?                           
         CLI   OUT,0                                                            
         BE    *+10                                                             
         MVC   8(1,R1),OUT                                                      
*                                                                               
EXIT1    TM    GTHRET,GHTEXTQ                                                   
         BO    MOREHELP                                                         
         CLI   MORE,C'Y'                                                        
         BNE   LASTHELP                                                         
         OI    GTHRET,GHMOREQ                                                   
         B     MOREHELP                                                         
*                                                                               
EXIT2    TM    GTHFLAGS,GHNMSGQ    ARE MESSAGES OFF ?                           
         BO    EXIT5                                                            
         EJECT                                                                  
EXIT3    LA    R1,DMCB             GET MESSAGE FOR LINE 1                       
         USING GETTXTD,R1                                                       
         XC    GTBLOCK,GTBLOCK                                                  
         MVC   GTMSGNO,MSGNUM                                                   
         CLC   MSGNUM,=AL2(04)     IF HELP UNAVAILABLE                          
         BNE   EXIT4                                                            
         TM    TSTAT1,TSTATDDS     AND A DDS TERM                               
         BZ    EXIT4                                                            
         LA    RF,WORK1            APPEND PANEL ID                              
         STCM  RF,7,GTATXT                                                      
         MVI   GTLTXT,26                                                        
EXIT4    MVC   GTMTYP,MSGTYPE                                                   
         MVI   GTMSYS,1            FORCE SERVICE SYSTEM                         
         L     RF,AGETTXT                                                       
         BASR  RE,RF                                                            
*                                                                               
EXIT5    TM    GTHFLAGS,GHXMONQ    EXIT TO MONITOR ?                            
         BNO   EXIT10                                                           
         L     RF,WORKAD           START OF W/S                                 
         L     RF,8(RF)                                                         
         L     RF,8(RF)                                                         
         LR    RD,RF               RD = ROOT PHASE SAVE AREA                    
*                                                                               
EXIT10   EQU   *                                                                
         PROT  OFF                 DISSABLE CALLER'S STORAGE PROTECTION         
         OI    TFLAG,TFLAGHLP      SET HELP FLAG                                
         CLI   TSVCREQ+1,X'09'     TEST $HELP                                   
         BNE   *+8                                                              
         MVI   TSVCREQ+1,X'0B'     SUPPRESS $RE                                 
         PROT  ON                  RESTORE CALLER'S STORAGE PROTECTION          
         GOTO1 ASWITCH,DMCB,(SAVESYS,X'FFFFFFFF'),0                             
         CLI   DMCB+4,0            TEST SUCCESSFUL SWITCH BACK                  
         BE    *+6                                                              
         DC    H'0'                MUST BE ABLE TO SWITCH BACK                  
         XMOD1                                                                  
         ORG   *-2                                                              
         BSM   0,RE                RESTORE CALLER'S ADDRESSING MODE             
         SPACE 1                                                                
LASTHELP TM    GTHRET,GHCLSQ       HAS SCREEN BEEN CLEARED                      
         BO    LASTHLP1                                                         
         MVC   MSGNUM,=AL2(02)     HELP DISPLAYED ENTER FIELD                   
         B     GTHINFX                                                          
LASTHLP1 MVC   MSGNUM,=AL2(01)     NO MORE HELP                                 
         B     GTHINFX                                                          
*                                                                               
MOREHELP MVC   MSGNUM,=AL2(03)     HELP DISPLAYED HIT ENTER FOR MORE            
         B     GTHINFX                                                          
*                                                                               
NOHELP   MVC   MSGNUM,=AL2(04)     HELP UNAVAILABLE FOR THIS FIELD              
         B     GTHINFX                                                          
*                                                                               
GTHINFX  CLI   TSVCREQ+1,X'09'     TEST $HELP                                   
         BNE   GTHINFX1                                                         
         PROT  OFF                 DISSABLE CALLER'S STORAGE PROTECTION         
         MVI   TSVCREQ+1,X'0B'                                                  
         PROT  ON                  RESTORE CALLER'S STORAGE PROTECTION          
GTHINFX1 MVI   MSGTYPE,GTMINF      MESSAGE TYPE INFO                            
         B     EXIT2               SEE IF MESSAGE WANTED                        
*                                                                               
NOSYS    MVC   MSGNUM,=AL2(50)     HELP TEMPORARILY UNAVAILABLE                 
         B     GTHERRX                                                          
*                                                                               
NOTCON   MVC   MSGNUM,=AL2(30)     MUST BE CONNECTED                            
*                                                                               
GTHERRX  CLI   TSVCREQ+1,X'09'    TEST $HELP                                    
         BNE   GTHERRX1                                                         
         PROT  OFF                 DISSABLE CALLER'S STORAGE PROTECTION         
         MVI   TSVCREQ+1,X'0B'                                                  
         PROT  ON                  RESTORE CALLER'S STORAGE PROTECTION          
GTHERRX1 MVI   MSGTYPE,GTMERR      MESSAGE TYPE IS ERROR                        
         B     EXIT3               ALWAYS SHOW ERROR MESSAGES                   
         EJECT                                                                  
***************************************                                         
*    NON DESTRUCTIVE MOVE ROUTINE     *                                         
***************************************                                         
MOVE     NTR1                                                                   
         L     R2,0(R1)            P1 SOURCE                                    
         L     R4,4(R1)            P2 DEST                                      
         L     R3,8(R1)            P3 LENGTH                                    
         CR    R4,R2               IS IT DOWNWARDS ?                            
         BH    RMOVE                                                            
NMOVE    LR    R5,R3                                                            
         MVCL  R4,R2               NO SO MVCL                                   
         B     XIT                                                              
EXMOVE   MVC   0(0,R4),0(R2)                                                    
RMOVE    LR    R5,R4                                                            
         SR    R5,R2               R5=OFFSET                                    
         CR    R5,R3               WILL IT OVERLAP ?                            
         BNL   NMOVE                                                            
         CH    R5,=H'256'                                                       
         BNH   M1                                                               
         LH    R5,=H'256'                                                       
M1       AR    R2,R3               GO TO END                                    
         AR    R4,R3                                                            
M0       CR    R3,R5               IS LENGTH < BLOCK                            
         BNL   M2                                                               
         LR    R5,R3               IF YES BLOCK = LENGTH                        
M2       SR    R2,R5                                                            
         SR    R4,R5               BACK 1 BLOCK                                 
         BCTR  R5,0                                                             
         EX    R5,EXMOVE           MOVE 1 BLOCK                                 
         LA    R5,1(R5)                                                         
         SR    R3,R5               SUB BLOCK FROM LENGTH                        
         BNE   M0                                                               
XIT      XIT1                                                                   
         EJECT                                                                  
***************************************                                         
*        READ GENDIR DIRECTORY        *                                         
***************************************                                         
READHELP NTR1                                                                   
*                                                                               
         MVC   KEYSAVE,HV1KEY                                                   
         GOTO1 ADATAMGR,DMCB,=C'DMRDHI',=C'GENDIR',HV1KEY,HV1KEY,0              
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         CLC   HV1KEY(HV1LANG-HV1KEY),KEYSAVE                                   
         BNE   READHLPX            NO HELP IN ANY LANGUAGE                      
*                                                                               
         CLC   HV1LANG,LANGNUM     TEST HELP FOUND IN HOST LANGUAGE             
         BE    READHLPX            YES                                          
*                                                                               
         CLI   HV1LANG,X'FD'       TEST ENGLISH                                 
         BL    *+12                NO                                           
         CR    R1,R1               SET CC EQUAL                                 
         BE    READHLPX                                                         
         DC    H'0'                                                             
*                                                                               
         MVI   HV1LANG,X'FD'       LOOK FOR HELP IN ENGLISH                     
         GOTO1 ADATAMGR,DMCB,=C'DMRDHI',=C'GENDIR',HV1KEY,HV1KEY,0              
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         CLC   HV1KEY(HV1LANG-HV1KEY),KEYSAVE                                   
READHLPX B     XIT                 CC SET ON EXIT                               
         SPACE 3                                                                
         GETEL R5,28,ELCODE                                                     
         EJECT                                                                  
****************************                                                    
*     FORMAT PANEL ID      *                                                    
****************************                                                    
BLDID    NTR1                                                                   
         LA    R6,KEYSAVE                                                       
         USING HV1KEYD,R6                                                       
         GOTO1 =V(HEXOUT),DMCB,HV1SYS,DUB,4  EXPAND SYS/PRG/SCR/FLD             
         MVC   WORK1(26),=C' KEY = S../P../../.../... '                         
         L     R5,=V(SELIST)       POINT TO START OF SELIST                     
         LH    R2,0(R5)            PRIME INDICES                                
         L     R3,2(R5)                                                         
         LA    R5,6(,R5)                                                        
         USING SELISTD,R5                                                       
SESCAN   CLC   SEOVSYS,HV1SYS      IS THIS THE SYSTEM ?                         
         BE    GOTSE               YES - SKIP                                   
         BXLE  R5,R2,SESCAN        LOOP TO END OF SELIST                        
*                                                                               
BADSE    MVC   WORK1+8(2),DUB      MOVE X'SS' TO SSS AREA                       
         B     BADPGM                                                           
*                                                                               
GOTSE    MVC   WORK1+7(3),SENAME   MOVE WHOLE SENAME                            
         L     R4,SEPGMS           PROGRAM NAME LIST FOR THIS SYSTEM            
         USING PGMLSTD,R4                                                       
         LH    R2,0(,R4)           PRIME INDEXES                                
         L     R3,2(,R4)                                                        
         LA    R4,6(,R4)                                                        
PGMSCAN  CLC   PGMNUM,HV1PROG      IS THIS THE PROGRAM ?                        
         BE    GOTPGM              YES - SKIP                                   
         BXLE  R4,R2,PGMSCAN       LOOP TO END PGMLIST                          
*                                                                               
BADPGM   MVC   WORK1+12(2),DUB+2   MOVE X'PP' TO PPP AREA                       
         B     *+10                                                             
*                                                                               
GOTPGM   MVC   WORK1+11(3),PGMNAME                                              
*                                                                               
         MVC   WORK1+15(2),DUB+4   MOVE IN SCREEN                               
*                                                                               
         EDIT  (B1,HV1FIELD),(3,WORK1+18),FILL=0                                
         EDIT  (B1,HV1PAGE),(3,WORK1+22),FILL=0                                 
         XIT1                                                                   
         DROP  R5                                                               
         DROP  R4                                                               
****************************                                                    
*     ERROR CONDITIONS     *                                                    
****************************                                                    
ERRORO   DC    H'0'                OFFLINE SO DUMP                              
ERRORR   DC    H'0'                BAD RETURN BLOCK                             
ERRORH   MVI   GTHRET,GHNONEQ      NO HELP                                      
         B     NOHELP                                                           
ERRORS   MVI   GTHRET,GHCTRLQ      NO CTRL SYSTEM                               
         B     NOSYS                                                            
ERRORCT  MVI   GTHRET,GHNONEQ                                                   
         B     NOTCON                                                           
         EJECT                                                                  
**********************************************                                  
*  CONSTANTS INCLUDING DUMMY SCREEN HEADERS  *                                  
**********************************************                                  
DEATH    DC    H'0'                                                             
DUMMY    DC    XL10'200800FFFF0000400000'  DUMMY ELEMENT                        
HLPASTSH DC    XL8'57E8000000008000'  HI PROT TX + 79 ASTS                      
HLPASTH  DC    XL8'09E8000000008000'  HI PROT TX + 1  AST                       
HLPTXTH  DC    XL8'53E0000000008000'  N  PROT TX + 75 TEXT                      
PANCHRS  DS    0D                                                               
         DC    C'*PANELS*'                                                      
         DC    C'TL='                                                           
TL       DC    C'*'                                                             
         DC    C'TR='                                                           
TR       DC    C'*'                                                             
         DC    C'BL='                                                           
BL       DC    C'*'                                                             
         DC    C'BR='                                                           
BR       DC    C'*'                                                             
         DC    C'MID'                                                           
MID      DC    C'*'                                                             
         DC    C'TOPLIN'                                                        
TOPCHRS  DC    C' *'                                                            
         DC    C'BOTLIN'                                                        
BOTCHRS  DC    C' *'                                                            
BZEROES  DC    XL2'00'                                                          
         LTORG                                                                  
         EJECT                                                                  
*                                  WORKING STORAGE DSECT                        
WORKD    DSECT                                                                  
         SPACE 1                                                                
DUB      DS    D                                                                
FULL     DS    F                                                                
SCRRELO  DS    F                                                                
SAVER1   DS    F                   SAVE REGISTER R1                             
TOPROW   DS    H                   1ST LINE OF HELP                             
BOTROW   DS    H                   1ST LINE AFTER HELP                          
TOPSCRAD DS    H                                                                
BOTSCRAD DS    H                                                                
WRKSCRAD DS    H                                                                
HLPSCRAD DS    H                                                                
ELSAVE   DS    F                                                                
PANSTRT  DS    F                                                                
PANEND   DS    F                                                                
SCREND   DS    F                                                                
HELPDISP DS    H                                                                
HLPLINES DS    H                                                                
HELPSISE DS    F                                                                
ELCODE   DS    XL1                                                              
MSGNUM   DS    XL2                                                              
MSGTYPE  DS    XL1                                                              
LASTSV   DS    F                                                                
MORE     DS    C                                                                
AUTO     DS    C                                                                
ALLTX    DS    C                                                                
OUT      DS    C                                                                
DMCB     DS    6F                                                               
PARMS    DS    0F                                                               
P1       DS    1F                                                               
P2       DS    1F                                                               
P3       DS    1F                                                               
DMWORK   DS    12D                                                              
LANGNUM  DS    XL1                                                              
SAVESYS  DS    XL1                                                              
SAVEOV   DS    XL1                                                              
NEWDA    DS    XL4                                                              
NEWFKEY  DS    XL10                                                             
KEY      DS    XL40                                                             
NEWKEY   DS    XL40                                                             
KEYSAVE  DS    XL32                                                             
SVEHELP  DS    XL32                                                             
WORK     DS    CL32                                                             
WORK1    DS    CL32                                                             
REC      DS    2048C                                                            
*                                                                               
WORKAD   DS    A                                                                
ASSB     DS    A                                                                
ADATAMGR DS    A                                                                
ASWITCH  DS    A                                                                
AUTL     DS    A                                                                
AGETTXT  DS    A                                                                
ACALLOV  DS    A                                                                
*                                                                               
WORKX    DS    0C                                                               
         EJECT                                                                  
*                        SAVE BLOCK FOR GETHELP                                 
SVEHELPD DSECT                                                                  
HSVIDENT DS    XL4                                                              
HSVTTRC  DS    XL2                 TRANSACTION COUNT                            
HSVFLG1  DS    XL1                 FLAGS                                        
HSVFLG2  DS    XL1                                                              
HSVDAKEY DS    XL4                 TTBR ADDR OF CURRENT REC                     
HSVSEQ   DS    XL1                 SEQ NO OF LAST LINE OUT                      
HSVMAXLN DS    XL1                 SAVE FROM PARMS                              
HSVOFFS  DS    XL1                 SAVE FROM PARMS                              
HSVQHDR  DS    XL2                 OFFSET FROM TWA                              
HSVAHDR  DS    XL2                 OFFSET FROM TWA                              
HSVFKEY  DS    XL10                FULL DIRECTORY KEY                           
SPARE    DS    XL3                                                              
*                                                                               
         EJECT                                                                  
       ++INCLUDE DDGETHELPD                                                     
         EJECT                                                                  
       ++INCLUDE SRHLPFFD                                                       
         EJECT                                                                  
       ++INCLUDE CTGENHV1                                                       
         EJECT                                                                  
*FADSECTS                                                                       
*DDCOMFACS                                                                      
*FAGETTXTD                                                                      
         PRINT OFF                                                              
       ++INCLUDE FADSECTS                                                       
       ++INCLUDE DDCOMFACS                                                      
       ++INCLUDE FAGETTXTD                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'004DDGETHELP 11/01/02'                                      
         END                                                                    
