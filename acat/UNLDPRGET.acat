*          DATA SET UNLDPRGET  AT LEVEL 003 AS OF 07/24/06                      
*          DATA SET UNLDPRGE   AT LEVEL 023 AS OF 11/18/97                      
*CATALP UNLDPRGB                                                                
*INCLUDE PRINT                                                                  
*INCLUDE PRNTBL                                                                 
*INCLUDE ADDAY                                                                  
*INCLUDE DATCON                                                                 
         TITLE 'LDPRGE - UNIT - LOAD PURGE PROGRAM'                             
* UNLDPRGE - DECIDES IF UNIT RECORDS SHOULD BE DELETED DURING LOAD              
*                                                                               
*  INPUT:  (R1 POINTS TO PARAMETER LIST)                                        
*          PARAMETER 1 - BYTE 1 - IGNORED                                       
*                        BYTE 2-4 - ADDRESS OF UNIT RECORD                      
*                                                                               
*  OUTPUT: PARAMETER 1 : BYTE 1 - X'FF'-IF UNIT RECORD SHOULD                   
*                                              BE DELETED                       
*                                 X'00'-OTHERWISE                               
*                                                                               
*  CALLED FROM: DMLDMOD - GENERAL LOAD/DUMP MODULE                              
********************************************************************            
*                                                                               
         PRINT NOGEN                                                            
LDPURGE  CSECT                                                                  
         NMOD1 0,UNLDPRGE                                                       
*                                                                               
LDP      BC    0,LDP1                                                           
         OI    LDP+1,X'F0'         SET BRANCH                                   
         LR    R0,R1                                                            
         L     R1,=V(UPSIVAL)                                                   
         MVC   UPSI,0(R1)                                                       
*        GOTO1 =V(PRNTBL),DMCB,=C'UPSI',UPSI,C'DUMP',10,=C'1D'                  
         LR    R1,R0                                                            
*                                                                               
LDP1     CLI   0(R1),0             INITIALIZATION/SKIP                          
         BE    LDKEEP                                                           
         CLI   0(R1),X'FF'         EOF SKIP                                     
         BE    LDKEEP                                                           
*                                                                               
LDP2     L     R8,0(R1)            R8 POINTS TO RECORD                          
         LR    R2,R1           **  SAVE R1                                      
         USING NURECD,R8                                                        
         CLI   0(R8),X'04'         IF UNIT                                      
         BNE   LDDEL                                                            
         CLI   1(R8),X'13'           NFNY ONLY                                  
         BNE   LDDEL                                                            
         B     LDKEEP                                                           
*******************                                                             
         TM    NUPACKST,X'01'      CHECK IF NO-SHOW DELETE                      
         BO    LDDEL                                                            
         B     LDKEEP                                                           
*                                                                               
LDPACK   CLI   0(R8),X'02'         IS IT PACKAGE REC                            
         BNE   LDKEEP                                                           
         USING NPRECD,R8           PACKAGE REC                                  
         TM    NPAKSTAT,X'01'      TEST DELETE-NO-SHOW                          
         BO    LDDEL                                                            
         B     LDKEEP                                                           
*                                                                               
LDPRGE   DS    0H                  CHECK UPSI BITS                              
         TM    UPSI,X'02'          DELETE CLOSE OUTS                            
         BO    LDDEL                                                            
         B     LDKEEP                                                           
*                                                                               
LDDEL    LR    R1,R2           **  RESTORE R1                                   
         MVI   0(R1),X'FF'         TELL DMLDMOD TO DELETE IT                    
         B     XITLDP                                                           
*                                                                               
LDKEEP   MVI   0(R1),X'00'                                                      
         B     XITLDP                                                           
*                                                                               
XITLDP   XMOD1 1                                                                
*                                                                               
         EJECT                                                                  
*                                                                               
LDCHK    LR    R6,R8                                                            
         MVI   ELCODE,X'22'                                                     
         BAS   RE,GETEL                                                         
         BE    LDTSTDTE                                                         
         LR    R6,R8                                                            
         MVI   ELCODE,X'23'                                                     
         BAS   RE,GETEL                                                         
         BE    LDTSTDTE                                                         
*                                                                               
         LR    R6,R8                                                            
         MVI   ELCODE,X'21'         ...IF UNIT HAS TRAFFIC ELEMS                
         BAS   RE,GETEL                                                         
         BNE   LDDEL                                                            
         USING NUCMLEL,R6                                                       
         TM    NUCMLFLG,X'10'       ...IF PRINTED ON INSTRUCTIONS               
         BO    LDTSTDTE             ...CHECK TODAY'S DATE                       
         B     LDDEL                ...ELSE DELETE                              
         DROP  R6                                                               
*                                                                               
*                       *** IF UNIT HAS TRAFFIC ELEMS, DO NOT DELETE            
*                       *** UNLESS ONE MONTH PREV TO TODAY'S DATE               
         USING NURECD,R8                                                        
LDTSTDTE DS    0H                                                               
         GOTO1 =V(DATCON),DMCB,(5,0),(0,TODAY)      GETS TODAY'S DTE            
         GOTO1 =V(ADDAY),DMCB,TODAY,TODAY,F'-31'   SUBTRACT 31 DAYS             
         GOTO1 =V(DATCON),DMCB,(0,TODAY),(2,TDAY)   CONVRT TO 2 BYTE            
         CLC   NUKDATE,TDAY               **UNIT DATE MUST BE AT                
         BL    LDDEL                      **LEAST 1 MONTH PREV TO DELET         
         LR    R1,R2            *** RESET R1                                    
         B     LDKEEP                                                           
*                                                                               
*                                                                               
         EJECT                                                                  
CNTR     DC    F'0'                                                             
*                                                                               
         EJECT                                                                  
GETEL    LA    R6,27(R6)                                                        
GT5      CLC   ELCODE,0(R6)                                                     
         BE    GOTEL                                                            
NEXTEL   ZIC   R1,1(R6)                                                         
         LTR   R1,R1                                                            
         BZ    NOEL                                                             
         CLI   0(R6),0                                                          
         BE    NOEL                                                             
         AR    R6,R1                                                            
         B     GT5                                                              
NOEL     LTR   RE,RE                                                            
GOTEL    BR    RE                                                               
         SPACE 2                                                                
ELCODE   DS    CL1                                                              
TODAY    DS    CL6                                                              
TDAY     DS    CL2                                                              
DMCB     DS    6F                                                               
COUNTER  DS    F                                                                
DUB      DS    D                                                                
WORK     DS    CL32                                                             
UPSI     DS    CL1                                                              
         EJECT                                                                  
         LTORG                                                                  
*                                                                               
       ++INCLUDE NEGENUNIT                                                      
       ++INCLUDE NEGENPACK                                                      
       ++INCLUDE DDDPRINT                                                       
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003UNLDPRGET 07/24/06'                                      
         END                                                                    
