*          DATA SET REGENDHT   AT LEVEL 061 AS OF 01/28/03                      
*CATALP REGENDHT                                                                
***********************************************************************         
*                                                                     *         
*         REGENDHT --- UPDATE DARE RECORD WITH ACTIVITY HISTORY       *         
*                                                                     *         
* ------------------------------------------------------------------- *         
* UPDATE HISTORY:                                                     *         
*                                                                     *         
* 21JUL97 SKU --- ORIGINATION DATE                                    *         
*                                                                     *         
***********************************************************************         
         TITLE 'REGENDHT - DARE HISTORY'                                        
***********************************************************************         
*  INPUT:                                                                       
*              PARAMETER 1 BYTE 1   FLAG                                        
*                                   X'80' = SP CODE IS IN DMCB+12               
*                          BYTE 2-4 A(DARE RECORD)                              
*                        2 =       ACTION/ERROR CODE, REVISION NUMBER           
*                                      BYTE 1-2 = ACTION/ERROR CODE             
*                                      BYTE 3   = REVISION NUMBER               
*                                      BYTE 4   = MISC FLAG                     
*                        3 =       A(ROUTINE ADDRESS BLOCK)                     
*                                      WORD 1 = A(HELLO)                        
*                                           2 = A(DATCON)                       
*                                                                               
*                        4 =       A(SALES PERSON CODE)                         
*  OUTPUT:                                                                      
*              ON ERROR SET ^0 CC AND/OR ERROR EXIT                             
***********************************************************************         
REGENDHT CSECT                                                                  
         PRINT GEN                                                              
         NMOD1 WORKX-WORKD,REGENDHT                                             
         USING WORKD,RC                                                         
         MVC   ADAREREC,0(R1)                                                   
         MVC   ACTCODE,4(R1)                                                    
         MVC   REVNUM,6(R1)                                                     
         MVC   MISCFLG,7(R1)                                                    
*                                                                               
         XC    SPCODE,SPCODE                                                    
         TM    0(R1),X'80'          ADD THE SPCODE IN DMCB+12 ?                 
         BZ    AWAY                                                             
         L     RE,12(R1)                                                        
         MVC   SPCODE,0(RE)                                                     
*                                                                               
AWAY     DS    0H                                                               
         L     R1,8(R1)                                                         
         MVC   VHELLO,0(R1)                                                     
         MVC   VDATCON,4(R1)                                                    
*                                                                               
         B     MAIN                                                             
*                                                                               
EXIT     SR    RC,RC                                                            
ERRXIT   LTR   RC,RC               SET CONDITION CODE AT EXIT                   
         XIT1                                                                   
         EJECT                                                                  
*********************************************************************           
* ADD HISTORY ELEMENT TO RECORD                                                 
*********************************************************************           
MAIN     DS    0H                                                               
         XC    ELEM,ELEM                                                        
         LA    R6,ELEM                                                          
         USING RDARHSEM,R6                                                      
         MVI   RDARHSCD,X'50'                                                   
         MVI   RDARHSLN,RDARHL2Q                                                
*                                                                               
         L     R3,ADAREREC         USING OLD LENGTH FOR TV                      
         USING RDARREC,R3                                                       
         CLI   RDARKSTA+4,C'A'                                                  
         BE    MAIN010                                                          
         CLI   RDARKSTA+4,C'F'                                                  
         BE    MAIN010                                                          
         MVI   RDARHSLN,RDARHSLQ                                                
*                                                                               
MAIN010  DS    0H                                                               
         GOTO1 VDATCON,DMCB,(5,0),(2,RDARHSDT)                                  
*                                                                               
         THMS  DDSTIME=YES                                                      
         ST    R0,DUB              ACTUAL TIME ADJUSTMENT                       
         ST    R1,DUB+4            DDS TIME                                     
         AP    DUB(4),DUB+4(4)                                                  
         ICM   R1,15,DUB                                                        
         SRL   R1,4                SHIFT OFF SIGN                               
         STCM  R1,7,RDARHSTM                                                    
*                                                                               
         MVC   RDARHSER,ACTCODE                                                 
         MVC   RDARHSVR,REVNUM                                                  
*                                                                               
         CLI   RDARKSTA+4,C'A'                                                  
         BE    MAIN020                                                          
         CLI   RDARKSTA+4,C'F'                                                  
         BNE   MAIN050                                                          
MAIN020  DS    0H                                                               
         MVC   RDARHSSP,SPCODE                                                  
         MVC   RDARHFG1,MISCFLG                                                 
         DROP  R6                                                               
         DROP  R3                                                               
*                                                                               
MAIN050  DS    0H                                                               
         GOTO1 VHELLO,DMCB,(C'P',=C'REPFILE'),ADAREREC,ELEM,           X        
               =C'ADD=END'                                                      
         TM    DMCB+12,X'FF'                                                    
         BZ    EXIT                                                             
         B     ERRXIT                                                           
*                                                                               
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
WORKD    DSECT                                                                  
DMWORK   DS    12D                                                              
KEY      DS    CL32                                                             
KEYSAVE  DS    CL32                                                             
DMCB     DS    6F                                                               
DUB      DS    D                                                                
ADAREREC DS    A                                                                
VHELLO   DS    F                                                                
VDATCON  DS    F                                                                
DATADISP DS    H                                                                
ACTCODE  DS    XL2                                                              
REVNUM   DS    X                                                                
MISCFLG  DS    X                                                                
SPCODE   DS    XL3                                                              
WORK     DS    CL17                FOR EDIT                                     
ELEM     DS    XL256                                                            
WORKX    EQU   *                                                                
*                                                                               
*                                                                               
RDARED   DSECT                                                                  
       ++INCLUDE REGENDAR                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'061REGENDHT  01/28/03'                                      
         END                                                                    
