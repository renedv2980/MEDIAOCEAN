*          DATA SET DDCCPANA   AT LEVEL 209 AS OF 09/24/07                      
*&&      SET   NOP=N                                                            
*CATALP CCPANA                                                                  
************************************************************                    
*                                                          *                    
* READ PAN LIBRARYS DIRECTLY SEND TO FILE FOR COMPARISONS  *                    
*                                                          *                    
************************************************************                    
         SPACE 1                                                                
         TITLE 'CCPAN9 - READ FROM PAN LIB'                                     
CCPANA   CSECT                                                                  
         PRINT NOGEN                                                            
         NBASE WORKX-WORKD,**PANA**,R9,WORK=V(REGSAVE)                          
*                                                                               
         USING WORKD,RC            SET UP WORKING STORAGE USINGS                
         ST    RD,SAVERD                                                        
*                                                                               
         LA    R4,PANIOA                                                        
         USING PANRECD,R4                                                       
         LA    R5,LIBTAB                                                        
*                                                                               
         BAS   RE,PRINTI           INIT PRINTING                                
         BAS   RE,INIT             INIT CARDS                                   
*                                                                               
         OPEN  (PANOUT,OUTPUT)     OPEN PAN OUT FILE                            
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
*                                                                               
MAIN010  MVC   LIBRARY,0(R5)                                                    
         BAS   RE,PANALLOC         ALLOCATE PAN LIB                             
         BAS   RE,PANBLD           BUILD A DIRECTORY LIST                       
         BAS   RE,PANALLOC         ALLOCATE PAN LIB AGAIN                       
*                                                                               
         OPEN  (PANFILE,INPUT)     OPEN PAN LIST FILE                           
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
*                                                                               
MAIN020  XC    LINES,LINES                                                      
*                                                                               
MAIN021  BAS   RE,PANDIR           READ AN ENTRY                                
         CLI   DMCB,X'FF'          RETURN CODE: END OF PAN LIBRARY              
         BE    MAIN990                                                          
*                                                                               
MAIN050  CLC   PANLANG,=C'OBJCT'   IS THIS AN RM BOOK                           
         BE    *+12                                                             
         BAS   RE,PANPROC          PROCESS PAN BOOK                             
         B     MAIN021                                                          
*                                                                               
         BAS   RE,RMPROC           PROCESS RM BOOK                              
         B     MAIN021                                                          
*                                                                               
MAIN990  CLOSE PANFILE             CLOSE FILES AND EXIT                         
         LA    R5,6(R5)                                                         
         CLC   0(6,R5),=C'XXXXXX'                                               
         BNE   MAIN010                                                          
*                                                                               
MAIN999  CLOSE SYSPRINT                                                         
         CLOSE SYSIN                                                            
         CLOSE PANOUT                                                           
*                                                                               
XBASE    XBASE                                                                  
         EJECT                                                                  
*************************************************************                   
*        INIT - READ CARDS ETC...                           *                   
*************************************************************                   
         SPACE 1                                                                
INIT     NTR1                                                                   
*                                                                               
         OPEN  (SYSIN,INPUT)       OPEN SYSIN CARDS                             
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
*                                                                               
INIT010  GET   SYSIN                                                            
         CLC   0(9,R1),=C'*LIBRARYS'                                            
         BNE   INIT010                                                          
*                                                                               
INIT020  LA    R2,LIBTAB                                                        
*                                                                               
INIT025  GET   SYSIN                                                            
         CLC   0(6,R1),=C'*BOOKS'                                               
         BE    INIT030                                                          
*                                                                               
         MVC   0(6,R2),0(R1)                                                    
         LA    R2,6(R2)                                                         
         B     INIT025                                                          
*                                                                               
INIT030  LA    R2,BOOKTAB                                                       
*                                                                               
INIT035  GET   SYSIN                                                            
         MVC   0(10,R2),0(R1)                                                   
         LA    R2,10(R2)                                                        
         B     INIT035                                                          
*                                                                               
CARDEND  GOTO1 =V(XSORT),DMCB,BOOKTAB,500,10,0,0                                
         GOTO1 =V(XSORT),DMCB,LIBTAB,150,6,0,0                                  
*                                                                               
INITX    XIT1                                                                   
         EJECT                                                                  
*************************************************************                   
*        DYNALLOC PAN DIRECTORY                             *                   
*************************************************************                   
         SPACE 1                                                                
PANALLOC NTR1                                                                   
*                                                                               
         MVC   TXTDSN+6(30),=C'PAN.                          '                  
         MVC   TXTDSN+10(6),LIBRARY                                             
         LA    R1,TXTDSN+10                                                     
         LA    R0,10                                                            
ALLOC010 CLI   0(R1),C' '                                                       
         BE    ALLOC011                                                         
         LA    R1,1(R1)                                                         
         BCT   R0,ALLOC010                                                      
         DC    H'0'                                                             
*                                                                               
ALLOC011 MVC   0(8,R1),=C'.LIBRARY'                                             
*                                                                               
ALLOC020 LA    R1,ARBLK            ALLOCATE PAN LIB                             
         DYNALLOC                                                               
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
         B     PANALLOX                                                         
*                                                                               
         DS    0F                                                               
ARBLK    DC    X'80',AL3(RBLK)     R1 POINTS TO THIS BEFORE DYNALLOC            
         SPACE                                                                  
RBLK     DC    X'1401000000000000',A(ATXT),X'0000000018000000'                  
         SPACE                                                                  
ATXT     DC    X'00',AL3(TXTDD)    DDNAME                                       
         DC    X'00',AL3(TXTDSN)                                                
         DC    X'00',AL3(TXTDISPS)                                              
         DC    X'80',AL3(TXTCLOSE) FREE=CLOSE                                   
         SPACE                                                                  
TXTDD    DC    AL2(DALDDNAM),X'00010006',C'PANDD1'                              
TXTDSN   DC    AL2(DALDSNAM),X'0001002C',CL44' '       DSN=............         
TXTDISPS DC    AL2(DALSTATS),X'0001000108'             DISP=(SHR,.....)         
TXTCLOSE DC    AL2(DALCLOSE),X'00000000'                                        
*                                                                               
PANALLOX B     XIT1                                                             
         EJECT                                                                  
*************************************************************                   
*        BUILD FILE OF DIR ENTRIES                          *                   
*************************************************************                   
         SPACE 1                                                                
PANBLD   NTR1                      FIRST READ ALL DIR ENTRIES                   
         OPEN  (PANFILE,OUTPUT)                                                 
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         MVC   PANBK,=CL10'A'      START FROM A                                 
*                                                                               
PANBLD3  GOTO1 VPANIC,DMCB,(X'40',READ),DIR,PANBK,PANCRD                        
*                                                                               
         CLC   PANCRD(2),=C'/*'    END OF DIRECTORY?                            
         BE    PANBLD4             YES                                          
*                                                                               
         MVC   WORK(10),PANCRD                                                  
         CLC   WORK(2),=C'RM'                                                   
         BE    *+12                                                             
         BAS   RE,BOOKCOMP                                                      
         BNE   PANBLD3                                                          
*                                                                               
         PUT   PANFILE,PANCRD      SAVE DIR TO TMPFILE                          
         B     PANBLD3                                                          
*                                                                               
PANBLD4  CLOSE PANFILE             CLOSE FILES AND EXIT                         
         GOTO1 VPANIC,DMCB,CLOSE,0,0,0                                          
*                                                                               
         XIT1                                                                   
         EJECT                                                                  
*************************************************************                   
*        READ A DIRECTORY ENTRY FROM THE LIST IN THE FILE   *                   
*************************************************************                   
         SPACE 1                                                                
PANDIR   NTR1                                                                   
PANDIR1  GET   PANFILE,PANCRD      READ IN DIR ENTRY                            
*                                                                               
         XC    PANIOL,PANIOL       CLEAR OUTPUT RECORD                          
         LR    R0,R4                                                            
         LH    R1,=H'4096'                                                      
         XR    RE,RE                                                            
         L     RF,=X'40000000'                                                  
         MVCL  R0,RE                                                            
*                                                                               
         MVC   PANBK,PANCRD        SET PANBK TO NAME OF BOOK READ               
         MVC   PANLEV,PANCRD+10                                                 
         MVC   PANDATE,PANCRD+26                                                
         MVC   PANLANG,PANCRD+18                                                
*&&UK*&& MVC   PANLIB(2),=C'UK'                                                 
*&&US*&& MVC   PANLIB(2),=C'US'                                                 
         MVC   PANLIB+2(6),LIBRARY                                              
*                                                                               
         CLC   PANBK(2),=C'DC'     IGNORE DCS                                   
         BE    PANDIR1                                                          
*                                                                               
         MVC   WORK(10),PANBK                                                   
         CLC   WORK(2),=C'RM'                                                   
         BE    *+8                                                              
         BAS   RE,BOOKCOMP                                                      
         BE    PANDIR2                                                          
         B     PANDIR1                                                          
*                                                                               
PANDIR2  GOTO1 VDATCON,DMCB,(4,PANDATE),(0,PANDAT1)                             
         CLI   PANDAT1,X'FA'                                                    
         BNE   *+8                                                              
         MVI   PANDAT1,X'F0'                                                    
         GOTO1 VDATCON,DMCB,(0,PANDAT1),(11,PANDATE)                            
         B     XIT1                                                             
*                                                                               
PANDIRX  DS    0H                  NO MORE ITEMS IN DIRECTORY                   
         GOTO1 VPANIC,DMCB,CLOSE,0,0,0                                          
         CLOSE PANFILE                                                          
         MVI   DMCB,X'FF'          RETURN CODE: END OF PAN LIBRARY              
         XIT1                                                                   
         EJECT                                                                  
*************************************************************                   
*        PROCESS PANBOOK                                    *                   
*************************************************************                   
         SPACE 1                                                                
PANPROC  NTR1                                                                   
*                                                                               
PANP000  GOTO1 VPANIC,PARMS,READ,=C'PAN',PANBK,WORK1                            
         CLI   8(R1),0                                                          
         BE    *+12                                                             
         MVI   EOFP,C'Y'                                                        
         B     PANP001                                                          
         CLC   WORK1(2),=C'/*'     END OF BOOK                                  
         BE    PANP001             YES                                          
*                                                                               
         MVC   IOA(80),WORK1                                                    
         TR    IOA(80),TRANS                                                    
         MVC   IOL,=X'00540000'                                                 
         PUT   PANOUT,IOL                                                       
         B     PANP000                                                          
*                                                                               
PANP001  B     XIT1                                                             
*                                                                               
XIT1     XIT1                                                                   
         EJECT                                                                  
*************************************************************                   
*        PROCESS RM BOOK                                    *                   
*************************************************************                   
         SPACE 1                                                                
RMPROC   NTR1                                                                   
*                                                                               
         B     XIT1                                                             
         EJECT                                                                  
*************************************************************                   
*        COMPARE CURRENT BOOK IN WORK TO BOOKTAB            *                   
*************************************************************                   
         SPACE 1                                                                
BOOKCOMP ST    RE,SAVERE                                                        
*                                                                               
         LA    R1,BOOKTAB                                                       
BOOK010  CLC   0(3,R1),=C'XXX'     TEST EOT                                     
         BE    BOOKNO                                                           
*                                                                               
         LA    R0,10                                                            
         LA    RF,WORK                                                          
         OC    WORK(10),SPACES                                                  
         OC    0(10,R1),SPACES                                                  
         LR    RE,R1                                                            
BOOK020  CLI   0(RE),C'*'          * IS WILDCARD                                
         BE    BOOKYES                                                          
         CLC   0(1,RF),0(RE)                                                    
         BNE   BOOK030                                                          
         LA    RE,1(RE)                                                         
         LA    RF,1(RF)                                                         
         BCT   R0,BOOK020                                                       
         B     BOOKYES                                                          
*                                                                               
BOOK030  LA    R1,10(R1)                                                        
         B     BOOK010                                                          
*                                                                               
BOOKNO   L     RE,SAVERE                                                        
         LTR   RE,RE                                                            
         BR    RE                                                               
*                                                                               
BOOKYES  L     RE,SAVERE                                                        
         CR    RE,RE                                                            
         BR    RE                                                               
         EJECT                                                                  
*************************************************************                   
*        PRINT ROUTINES                                     *                   
*************************************************************                   
         SPACE 1                                                                
PRINTI   ST    RE,SAVERE                                                        
         OPEN  (SYSPRINT,OUTPUT)   PRINT INIT                                   
         ZAP   LINE,=P'0'                                                       
         ZAP   PAGE,=P'1'                                                       
         MVC   PLINE,SPACES                                                     
         L     RE,SAVERE                                                        
         BR    RE                                                               
*                                                                               
PRINTT   ST    RE,SAVERE           PRINT TITLES                                 
         ZAP   LINE,=P'0'          RESET LINECOUNT                              
         AP    PAGE,=P'1'          BUMP PAGECOUNT                               
         PUT   SYSPRINT,TITLE      PRINT TITLE                                  
         L     RE,SAVERE                                                        
         BR    RE                                                               
*                                                                               
PRINTL   ST    RE,SAVERE           PRINT LINE                                   
         AP    LINE,=P'1'          BUMP LINECOUNT                               
         CP    LINE,MAXLINE        TEST FOR MAX LINES                           
         BL    PRINTL2                                                          
*                                                                               
PRINTL1  ZAP   LINE,=P'1'          RESET LINECOUNT                              
         AP    PAGE,=P'1'          BUMP PAGECOUNT                               
         PUT   SYSPRINT,TITLE      PRINT TITLE                                  
*                                                                               
PRINTL2  PUT   SYSPRINT,PLINE      PRINT LINE                                   
         MVC   PLINE,SPACES                                                     
         L     RE,SAVERE                                                        
         BR    RE                  EXIT                                         
*                                                                               
PRINTX   ST    RE,SAVERE           CLOSE PRINT                                  
         CLOSE SYSPRINT                                                         
         L     RE,SAVERE                                                        
         BR    RE                                                               
         EJECT                                                                  
*************************************************************                   
*        PRINT TITLE                                        *                   
*************************************************************                   
         SPACE 1                                                                
TITLE    DC    CL166' '                                                         
         ORG   TITLE                                                            
         DC    C'1',X'40'                                                       
         DC    C'END TIME       TERMINAL  SYS  SE SYS   PROGRAM  TK'            
         DC    C'    SIN     START TIME       CPU     QUE    ET    I/P'         
         DC    C'   O/P   #IO   #OV'                                            
         ORG                                                                    
TITLE1   DC    CL166' '                                                         
         ORG   TITLE1                                                           
         DC    C'1',X'40'                                                       
         DC    C'-----------------------------------------------------'         
         DC    C'------------------ PARAMETER CARDS ------------------'         
         DC    C'-----------------------------------------------------'         
         ORG                                                                    
         EJECT                                                                  
*************************************************************                   
*        TRANSLATE                                          *                   
*************************************************************                   
*                                                                               
* WATCH OUT FOR THESE CHRS                                                      
*                                                                               
*        US             UK            GERMANY        FRANCE                     
*        -----------    -----------   ------------   ------------               
* 4A  ย  CENT           DOLLAR        UPR A UMLAUT   SMALL CIRCLE               
* 5A  !  EXCLAMATION    EXCLAMATION   UPR U UMLAUT   PARAGRAPH                  
* 6A  ยบ  VBAR SPLIT     VBAR SPLIT    LWR O UMLAUT   LWR U GRAVE                
* 79  `  BACK DINK      BACK DINK     BACK DINK      BACK DINK                  
* 5B  $  DOLLAR         POUND         DOLLAR         DOLLAR                     
* 7B  #  NUMBER SIGN    NUMBER SIGN   NUMBER SIGN    POUND                      
* 7C  @  AT SIGN        AT SIGN       PARAGRAPH      LWR A GRAVE                
* 5F       LOGICAL OR     LOGICAL OR    HAT            HAT                      
* A1  ~  TILDA          TOP BAR       DOUBLE SS      DOUBLE DOTS                
* C0  {  CURLY LEFT     CURLY LEFT    LWR A UMLAUT   LWR E ECUTE                
* D0  }  CURLY RIGHT    CURLY RIGHT   LWR U UMLAUT   LWR E GRAVE                
* E0  \  BACK SLASH     BACK SLASH    UPR O UMLAUT   C CEDILA                   
* 4F  |  VBAR           VBAR          EXCLAMATION    EXCLAMATION                
* 7F  "  DOUBLE DINK    DOUBLE DINK   DOUBLE DINK    DOUBLE DINK                
* PLUS                                                                          
* 9F  <  EURO           EURO          EURO           EURO                       
         SPACE 1                                                                
TRANS    DC    X'000102030405060708090A0B0C0D0E0F'                              
         DC    X'101112131415161718191A1B1C1D1E1F'                              
         DC    X'202122232425262728292A2B2C2D2E2F'                              
         DC    X'303132333435363738393A3B3C3D3E3F'                              
         DC    X'40414243444546474849404B4C4D4E40'                              
         DC    X'50515253545556575859405B5C5D5E5F'                              
         DC    X'60616263646566676869406B6C6D6E6F'                              
         DC    X'707172737475767778797A7B7C7D7E7F'                              
         DC    X'808182838485868788898A8B8C8D8E8F'                              
         DC    X'909192939495969798999A9B9C9D9E40'                              
         DC    X'A0A1A2A3A4A5A6A7A8A9AAABACADAEAF'                              
         DC    X'B0B1B2B3B4B5B6B7B8B9BABBBCBDBEBF'                              
         DC    X'C0C1C2C3C4C5C6C7C8C9CACBCCCDCECF'                              
         DC    X'D0D1D2D3D4D5D6D7D8D9DADBDCDDDEDF'                              
         DC    X'E0E1E2E3E4E5E6E7E8E9EAEBECEDEEEF'                              
         DC    X'F0F1F2F3F4F5F6F7F8F9FAFBFCFDFEFF'                              
         EJECT                                                                  
*************************************************************                   
*        ADDCONS                                            *                   
*************************************************************                   
         SPACE 1                                                                
VPANIC   DC    V(PANIC)                                                         
VSORTER  DC    V(SORTER)                                                        
VDATCON  DC    V(DATCON)                                                        
         EJECT                                                                  
*************************************************************                   
*        CONSTANTS TABLES AND DCBS                          *                   
*************************************************************                   
         SPACE 1                                                                
PANFILE  DCB   DDNAME=PANFILE,DSORG=PS,RECFM=FB,MACRF=(GM,PM),         X        
               BLKSIZE=3120,LRECL=80,EODAD=PANDIRX                              
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,RECFM=FBA,LRECL=(166)          
*                                                                               
SYSIN    DCB   DSORG=PS,MACRF=GL,DDNAME=SYSIN,EODAD=CARDEND                     
*                                                                               
PANOUT   DCB   DDNAME=PANOUT,DSORG=PS,MACRF=(PM),RECFM=VB,             X        
               BLKSIZE=8200,LRECL=4096,BUFNO=2                                  
*                                                                               
PUT      DC    CL8'PUT'                                                         
GET      DC    CL8'GET'                                                         
CLOSE    DC    CL8'CLOSE'                                                       
READ     DC    CL8'READ'                                                        
PAN      DC    CL8'PAN'                                                         
DIR      DC    CL8'DIR'                                                         
SPACES   DC    CL166' '                                                         
MAXLINE  DC    P'60'                                                            
         LTORG                                                                  
*                                                                               
LIBTAB   DC    150CL6'XXXXXX'      LIBRARY TO SCAN                              
LIBTABX  DC    CL6'XXXXXX'                                                      
BOOKTAB  DC    500CL10'XXXXXXXXXX' BOOKS TO SCAN                                
BOOKTABX DC    CL10'XXXXXXXXXX'                                                 
*                                                                               
*************************************************************                   
*        WORKING STORAGE                                    *                   
*************************************************************                   
         SPACE 1                                                                
WORKD    DSECT                                                                  
SAVERE   DS    A                                                                
SAVERD   DS    A                                                                
DUB      DS    D                                                                
DUB1     DS    D                                                                
DUB2     DS    D                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    X                                                                
FLAG     DS    X                                                                
LINES    DS    F                                                                
DMCB     DS    6F                                                               
PARMS    DS    6F                                                               
*                                                                               
IOL      DS    F                                                                
IOA      DS    132C                                                             
*                                                                               
TAPCHDR  DS    CL12                                                             
PANCRD   DS    CL80                                                             
TAPCRD   DS    CL80                                                             
PANCHDR  DS    CL12                                                             
WORK     DS    CL132                                                            
WORK1    DS    CL132                                                            
*                                                                               
LIBRARY  DS    CL6                                                              
*                                                                               
LINE     DS    PL2                                                              
PAGE     DS    PL4                                                              
PLINE    DS    CL166                                                            
*                                                                               
CHANGE   DS    0CL2                                                             
CODE     DS    C                                                                
HDR      DS    C                                                                
*                                                                               
EOFS     DS    0CL2                                                             
EOFT     DS    C                                                                
EOFP     DS    C                                                                
*                                                                               
BUF1     DS    CL56                                                             
BUF2     DS    CL56                                                             
BUF3     DS    CL56                                                             
BUF4     DS    CL56                                                             
BUFX     DS    CL56                                                             
*                                                                               
PANIOL   DS    F                                                                
PANIOA   DS    CL4096                                                           
*                                                                               
WORKX    EQU   *                                                                
         EJECT                                                                  
************************************************************                    
*        PAN DSECT                                         *                    
************************************************************                    
         SPACE 1                                                                
PANRECD  DSECT                                                                  
PANBK    DS    CL10                BOOK NAME DDBIGBOOK                          
         DS    CL1                                                              
PANLEV   DS    CL3                 LEVEL 001                                    
         DS    CL1                                                              
PANDAT1  DS    CL7                 DAT1 YYMMDD                                  
         DS    CL1                                                              
PANDATE  DS    CL8                 DATE                                         
         DS    CL1                                                              
PANLANG  DS    CL5                 LANGUAGE ASMB                                
         DS    CL1                                                              
PANLIB   DS    CL8                 LIBRARY UKRMOR UKAPPL UKNYAPPL               
         DS    CL1                                                              
PANTEST  DS    CL8                 TEST PHASE T11000A                           
         DS    CL1                                                              
PANLIVE  DS    CL8                 LIVE PHASE T11000                            
         DS    CL1                                                              
PANRMTST DS    CL8                 RM BOOK TEST FAMNTRA                         
         DS    CL1                                                              
PANRMLIV DS    CL8                 RM BOOK LIVE FAMNTR                          
         DS    CL1                                                              
PANVAR   DS    0CL1                CSECT AND INCLUDE INFO                       
*                                                                               
         SPACE 3                                                                
         IEFZB4D0                                                               
         SPACE 3                                                                
         IEFZB4D2                                                               
         SPACE 3                                                                
         EJECT                                                                  
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'209DDCCPANA  09/24/07'                                      
         END                                                                    
