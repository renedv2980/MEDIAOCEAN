*          DATA SET DDCOMPEL   AT LEVEL 007 AS OF 03/07/90                      
*CATALP COMPEL                                                                  
         TITLE 'GENERAL DFAR ELEMENT CONTROLLER'                                
COMPEL   CSECT                                                                  
         NMOD1 0,**COMPEL                                                       
         LR    R2,R1                                                            
         USING PLISTD,R2           INPUT LIST                                   
         L     R3,ACOPY                                                         
         L     R4,ACHANGE                                                       
         L     R5,AOUTPUT                                                       
         SR    R6,R6                                                            
         XC    0(8,R5),0(R5)       CLEAR DOWN OUTPUT AREA                       
         LA    R3,0(R3)                                                         
         LA    R4,0(R4)                                                         
         USING OLISTD,R5           OUTPUT LIST                                  
         EJECT                                                                  
*              HANDLE COPY RECORD                                               
         SPACE 1                                                                
COMP2    CLI   0(R3),0                                                          
         BE    COMP8                                                            
         CLC   0(1,R3),ELCODE                                                   
         BE    COMP6                                                            
         SPACE 1                                                                
COMP4    IC    R6,1(R3)                                                         
         AR    R3,R6                                                            
         B     COMP2                                                            
         SPACE 1                                                                
COMP6    CLI   ELLEN,0                                                          
         BE    *+14                                                             
         CLC   ELLEN,1(R3)                                                      
         BNE   COMP4                                                            
         ST    R3,COPYEL           FOUND COPY ELEMENT                           
         OI    MODE,X'01'          ?/FOUND                                      
         MVI   0(R3),X'FF'         FOR DUPS                                     
         EJECT                                                                  
*              HANDLE CHANGE RECORD                                             
         SPACE 1                                                                
COMP8    CLI   0(R4),0                                                          
         BE    COMPXT                                                           
         CLC   0(1,R4),ELCODE                                                   
         BE    COMP12                                                           
         SPACE 1                                                                
COMP10   CLI   1(R4),0           IS EL LENGTH ZERO                              
         BE    COMPXT                                                           
         IC    R6,1(R4)                                                         
         AR    R4,R6                                                            
         B     COMP8                                                            
         SPACE 1                                                                
COMP12   CLI   ELLEN,0                                                          
         BE    *+14                                                             
         CLC   ELLEN,1(R4)                                                      
         BNE   COMP10                                                           
         CLI   ELCOMP,0                                                         
         BNE   COMP16                                                           
         SPACE 1                                                                
COMP14   ST    R4,CHNGEL           FOUND CHANGE ELEMENT                         
         OI    MODE,X'02'          FOUND/?                                      
         MVI   0(R4),X'FF'         FOR DUPS                                     
         B     COMPXT                                                           
         EJECT                                                                  
*              HANDLE DUPLICATE ELEMENTS ON RECORD                              
         SPACE 1                                                                
COMP16   OC    COPYEL,COPYEL       IF NO COPY WAS FOUND                         
         BZ    COMP14              WE ARE DONE NOW                              
         IC    R6,ELCOMP           OTHERWISE TRY FOR A DUPLICATE                
         BCTR  R6,0                                                             
         ZIC   R7,ELDISP                                                        
         LA    R8,2(R3,R7)                                                      
         LA    R7,2(R4,R7)                                                      
         EX    R6,*+12                                                          
         BE    COMP14              ELEMENT FOUND                                
         B     COMP10              TRY AGAIN                                    
         CLC   0(0,R8),0(R7)                                                    
         SPACE 1                                                                
COMPXT   TM    MODE,X'03'          TEST FOR FOUND/FOUND                         
         BNO   COMPXX                                                           
         IC    R6,1(R3)            IF SO TEST FOR DUPLICATE                     
         BCTR  R6,0                                                             
         EX    R6,*+8                                                           
         B     *+10                                                             
         CLC   0(0,R3),0(R4)                                                    
         BNE   *+8                                                              
         MVI   MODE,4              FOUND/FOUND DUPLICATE                        
         SPACE 1                                                                
COMPXX   XMOD1 1                                                                
         EJECT                                                                  
*              DSECT TO COVER PARAMETER LIST                                    
         SPACE 1                                                                
PLISTD   DSECT                                                                  
ELCODE   DS    CL1                 ELEMENT CODE                                 
         ORG   ELCODE                                                           
ACOPY    DS    F                   A(COPY RECORD)                               
*                                                                               
ELCOMP   DS    CL1                 COMPARE LENGTH FOR DUPLICATES                
         ORG   ELCOMP                                                           
ACHANGE  DS    F                   A(CHANGE RECORD)                             
ELDISP   DS    CL1                 DISPLACEMENT TO DATA FOR DUPS                
         ORG   ELDISP                                                           
AOUTPUT  DS    F                   A(2 FULLWORD OUTPUT AREA)                    
ELLEN    DS    CL1                 LENGTH EXCEPTION                             
         SPACE 1                                                                
*              DSECT TO COVER RETURNED DATA                                     
         SPACE 1                                                                
OLISTD   DSECT                                                                  
MODE     DS    CL1                 00=NOCHANGE/NOCOPY NN  NOTEL                 
*                                  01=NOCHANGE/COPY   NY  DELEL                 
*                                  02=CHANGE/NOCOPY   YN  ADDEL                 
*                                  03=CHANGE/COPY     YY  CHGEL                 
*                                  04=CHANGE/COPY     XX  DUPEL                 
         ORG   MODE                                                             
COPYEL   DS    F                   A(COPY ELEMENT)                              
CHNGEL   DS    F                   A(CHNG ELEMENT)                              
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'007DDCOMPEL  03/07/90'                                      
         END                                                                    
