*          DATA SET PSIZEVAL   AT LEVEL 002 AS OF 12/02/98                      
*CATALP PSIZEVAL                                                                
*                                                                               
         TITLE 'PSIZEVAL - VALIDATE AD SIZE DESCRIPTION'                        
*                                                                               
*        VALIDATE AND "RECONSTRUCT" A STANDARD 17-BYTE                          
*        AD SIZE DESCRIPTION AND 3-BYTE AD SIZE CODE                            
*                                                                               
*        PARAMETERS VIA R1                                                      
*                                                                               
*        CL1   SET TO X'FF' IF INVALID SOURCE                                   
*              X'00' - VALIDATE DESCRIPTION                                     
*              X'01' - LOOK UP DESCRIPTION USING 3-BYTE CODE                    
*        CL3   A(SOURCE)  (17-BYTE FIELD OR 3-BYTE SIZE CODE)                   
*        CL1                                                                    
*        CL3   A(DESTINATION)                                                   
*               (20-BYTE FIELD - 3-BYTE CODE AND 17-BYTE DESCRIPTION)           
         SPACE 2                                                                
* NOTE:  R3 USED AS AN ACCUMULATER OF SIGNIFICANT CHARACTERS.                   
*        DO NOT USE IT FOR ANY OTHER PURPOSE IN THIS MODULE                     
*        WITHOUT SAVING AND RESTORING IT.                                       
*                                                                               
PSIZEVAL CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WRKDL,**PSIZ**                                                   
         USING WRKD,RC                                                          
         SPACE 2                                                                
*                                                                               
         MVC   TSTFRAC(TST20-WRKTOP),SPACES   CLEAR "WORK" AREAS                
         MVC   TST20,SPACES                      TO SPACES                      
         XC    WRKFLD,WRKFLD                                                    
         L     R2,0(R1)            R2=A(SOURCE)                                 
         LA    R2,0(R2)                                                         
*                                                                               
         CLI   0(R1),X'01'         LOOK UP DESCRIPTION ?                        
         BE    GETDESC             YES                                          
*                                                                               
         MVC   TSTDESC,0(R2)       MOVE SOURCE TO TEST FIELD                    
         OC    TSTDESC(18),SPACES  CHANGE NULLS TO SPACES - INCLUDING           
*                                      1ST POSITION OF WRKFLD                   
*                                  ***** COUNT NON-SPACE CHARACTERS             
         SR    R3,R3               CLEAR                                        
         LA    R2,TSTDESC          POINT TO FIELD TO TEST                       
         LA    R0,17               LOOP COUNTER                                 
COUNT10  DS    0H                                                               
         CLI   0(R2),C' '          SPACE ?                                      
         BE    *+8                 YES - NO ADD                                 
         LA    R3,1(R3)            ADD 1 TO COUNT                               
         LA    R2,1(R2)            NEXT SOURCE POSITION                         
         BCT   R0,COUNT10                                                       
         STH   R3,CHARCNT          DONE - SAVE FOR "LATER" TEST                 
         SR    R3,R3               CLEAR                                        
*                                                                               
*                                  ***** LOOK FOR A FRACTION                    
         LA    R2,TSTDESC          POINT TO FIELD TO TEST                       
         LA    R0,17               LOOP COUNTER                                 
FRTST10  DS    0H                                                               
         CLI   0(R2),C'/'          FRACTION DIVIDER ?                           
         BE    FRTST20             YES - GO TEST FOR VALIDITY                   
         LA    R2,1(R2)            NEXT SOURCE POSITION                         
         BCT   R0,FRTST10                                                       
         B     VHTST               NO FRACTION FOUND - NEXT TEST                
FRTST20  DS    0H                  "BUILD" AND TEST FRACTION                    
         BCTR  R2,0                BACK UP 1 POS'N FROM SLASH                   
         MVC   TSTFRAC,0(R2)       MOVE N/NN TO WORK AREA                       
         LA    RE,PADSDCT          POINT TO AD SIZE DESC CODE TABLE             
FRTST30  CLI   0(RE),X'FF'         END OF TABLE ?                               
         BE    RETURN              YES - INVALID FRACTION                       
         CLC   TSTFRAC,0(RE)       MATCHING FRACTION ?                          
         BE    FRTST80             YES - GO ADD TO TEST COUNT                   
         LA    RE,20(RE)           NEXT TABLE ENTRY                             
         B     FRTST30                                                          
FRTST80  DS    0H                  ADD TO TEST COUNT                            
         LA    R3,3(R3)            MUST BE AT LEAST 3                           
         CLI   TSTFRAC+3,C' '      4TH CHARACTER PRESENT ?                      
         BNH   VHTST               NO                                           
         LA    R3,1(R3)            ADD 1 MORE                                   
*                                                                               
*                                  ***** LOOK FOR VERTICAL/HORIZONTAL           
VHTST    LA    R2,TSTDESC          POINT TO FIELD TO TEST                       
         LA    R0,17               LOOP COUNTER                                 
VHTST10  DS    0H                                                               
         CLC   =C'VERTICAL ',0(R2)  VERTICAL ?                                  
         BNE   VHTST20             NO                                           
         LA    R3,8(R3)            ADD TO COUNT                                 
         B     VHTVER                                                           
VHTST20  CLC   =C'VERT ',0(R2)     VERTICAL ?                                   
         BNE   VHTST30             NO                                           
         LA    R3,4(R3)            ADD TO COUNT                                 
         B     VHTVER                                                           
VHTST30  CLC   =C'VER.',0(R2)      VERTICAL ?                                   
         BNE   VHTST40             NO                                           
         LA    R3,4(R3)            ADD TO COUNT                                 
         B     VHTVER                                                           
VHTST40  CLC   =C'VER ',0(R2)      VERTICAL ?                                   
         BNE   VHTST50             NO                                           
         LA    R3,3(R3)            ADD TO COUNT                                 
         B     VHTVER                                                           
VHTST50  CLC   =C' V ',0(R2)       VERTICAL ?                                   
         BNE   VHTST60             NO                                           
         LA    R3,1(R3)            ADD TO COUNT                                 
         B     VHTVER                                                           
VHTST60  DS    0H                                                               
         CLC   =C'HORIZONTAL ',0(R2)  HORIZONTAL ?                              
         BNE   VHTST70             NO                                           
         LA    R3,10(R3)           ADD TO COUNT                                 
         B     VHTHOR                                                           
VHTST70  CLC   =C'HOR.',0(R2)      HORIZONTAL ?                                 
         BNE   VHTST80             NO                                           
         LA    R3,4(R3)            ADD TO COUNT                                 
         B     VHTHOR                                                           
VHTST80  CLC   =C'HOR ',0(R2)      HORIZONTAL ?                                 
         BNE   VHTST90             NO                                           
         LA    R3,3(R3)            ADD TO COUNT                                 
         B     VHTHOR                                                           
VHTST90  CLC   =C' H ',0(R2)       HORIZONTAL ?                                 
         BNE   VHTMOR              NO                                           
         LA    R3,1(R3)            ADD TO COUNT                                 
         B     VHTHOR                                                           
VHTMOR   LA    R2,1(R2)            NEXT SOURCE POSITION                         
         BCT   R0,VHTST10                                                       
         B     VHTEND              NO VERT/HORIZ FOUND                          
VHTVER   MVI   TSTVH,C'V'                                                       
         B     VHTEND                                                           
VHTHOR   MVI   TSTVH,C'H'                                                       
VHTEND   DS    0H                                                               
*                                                                               
*                                  ***** LOOK FOR PAGE                          
PGTST    LA    R2,TSTDESC          POINT TO FIELD TO TEST                       
         LA    R0,17               LOOP COUNTER                                 
PGTST10  DS    0H                                                               
         CLC   =C'PAGE ',0(R2)     PAGE ?                                       
         BNE   PGTST20             NO                                           
         LA    R3,4(R3)            ADD TO COUNT                                 
         B     PGTPAGE                                                          
PGTST20  CLC   =C'PG.',0(R2)       PAGE ?                                       
         BNE   PGTST30             NO                                           
         LA    R3,3(R3)            ADD TO COUNT                                 
         B     PGTPAGE                                                          
PGTST30  CLC   =C'PG ',0(R2)       PAGE ?                                       
         BNE   PGTMOR              NO                                           
         LA    R3,2(R3)            ADD TO COUNT                                 
         B     PGTPAGE                                                          
PGTMOR   LA    R2,1(R2)            NEXT SOURCE POSITION                         
         BCT   R0,PGTST10                                                       
         B     PGTEND              NO PAGE FOUND                                
PGTPAGE  MVI   TSTPG,C'P'                                                       
PGTEND   DS    0H                                                               
*                                                                               
*                                  LOOK FOR JUNIOR OR TAB                       
JRTST    LA    R2,TSTDESC          POINT TO FIELD TO TEST                       
         LA    R0,17               LOOP COUNTER                                 
JRTST10  DS    0H                                                               
         CLC   =C'JUNIOR ',0(R2)   JUNIOR ?                                     
         BNE   JRTST20             NO                                           
         LA    R3,6(R3)            ADD TO COUNT                                 
         B     JRTSJR                                                           
JRTST20  CLC   =C'JR.',0(R2)       JUNIOR ?                                     
         BNE   JRTST30             NO                                           
         LA    R3,3(R3)            ADD TO COUNT                                 
         B     JRTSJR                                                           
JRTST30  CLC   =C'JR ',0(R2)       JUNIOR ?                                     
         BNE   JRTST40             NO                                           
         LA    R3,2(R3)            ADD TO COUNT                                 
         B     JRTSJR                                                           
JRTST40  CLC   =C'TABLOID ',0(R2)  TABLOID ?                                    
         BNE   JRTST50             NO                                           
         LA    R3,7(R3)            ADD TO COUNT                                 
         B     JRTSTAB                                                          
JRTST50  CLC   =C'TAB.',0(R2)      TABLOID ?                                    
         BNE   JRTST60             NO                                           
         LA    R3,4(R3)            ADD TO COUNT                                 
         B     JRTSTAB                                                          
JRTST60  CLC   =C'TAB ',0(R2)      TABLOID ?                                    
         BNE   JRTSMOR             NO                                           
         LA    R3,3(R3)            ADD TO COUNT                                 
         B     JRTSTAB                                                          
JRTSMOR  LA    R2,1(R2)            NEXT SOURCE POSITION                         
         BCT   R0,JRTST10                                                       
         B     JRTSEND             NO JUNIOR/TABLOID FOUND                      
JRTSJR   MVI   TSTJT,C'J'                                                       
         B     JRTSEND                                                          
JRTSTAB  MVI   TSTJT,C'T'                                                       
JRTSEND  DS    0H                                                               
*                                                                               
*   LOOK FOR SPREAD, ISLAND, SQUARE, INCH, COLUMN ("OTHER" WORDS)               
         LA    R2,TSTDESC          POINT TO FIELD TO TEST                       
         LA    R0,17               LOOP COUNTER                                 
OTHTST   DS    0H                                                               
         CLC   =C'SPREAD ',0(R2)   SPREAD ?                                     
         BNE   OTH10               NO                                           
         CLI   TSTOTH,C' '         ANYTHING IN OTHER ?                          
         BH    RETURN              ERROR - ONLY 1 "OTHER" WORD ALLOWED          
         MVC   TSTOTH(6),=C'SPREAD '                                            
         LA    R3,6(R3)            ADD TO COUNT                                 
         B     OTHBUMP             SEE IF ANY MORE                              
OTH10    CLC   =C'ISLAND ',0(R2)   ISLAND ?                                     
         BNE   OTH20               NO                                           
         CLI   TSTOTH,C' '         ANYTHING IN OTHER ?                          
         BH    RETURN              ERROR - ONLY 1 "OTHER" WORD ALLOWED          
         MVC   TSTOTH(6),=C'ISLAND '                                            
         LA    R3,6(R3)            ADD TO COUNT                                 
         B     OTHBUMP             SEE IF ANY MORE                              
OTH20    CLC   =C'SQUARE ',0(R2)   SQUARE ?                                     
         BNE   OTH20D                                                           
         LA    R3,6(R3)            ADD TO COUNT                                 
         B     OTH20M                                                           
OTH20D   CLC   =C'SQ.',0(R2)       SQUARE ?                                     
         BNE   OTH20H              NO                                           
         LA    R3,3(R3)            ADD TO COUNT                                 
         B     OTH20M                                                           
OTH20H   CLC   =C'SQ ',0(R2)       SQUARE ?                                     
         BNE   OTH30               NO                                           
         LA    R3,2(R3)            ADD TO COUNT                                 
OTH20M   CLI   TSTOTH,C' '         ANYTHING IN OTHER ?                          
         BH    RETURN              ERROR - ONLY 1 "OTHER" WORD ALLOWED          
         MVC   TSTOTH(6),=C'SQUARE '                                            
         B     OTHBUMP             SEE IF ANY MORE                              
OTH30    CLC   =C'INCHES ',0(R2)   INCHES ?                                     
         BE    OTH30MS             NO                                           
         CLC   =C'INCH ',0(R2)     INCH ?                                       
         BNE   OTH40               NO                                           
         CLI   TSTOTH,C' '         ANYTHING IN OTHER ?                          
         BH    RETURN              ERROR - ONLY 1 "OTHER" WORD ALLOWED          
         MVC   TSTOTH(5),=C'INCH '                                              
         LA    R3,4(R3)            ADD TO COUNT                                 
         B     OTHBUMP             SEE IF ANY MORE                              
OTH30MS  CLI   TSTOTH,C' '         ANYTHING IN OTHER ?                          
         BH    RETURN              ERROR - ONLY 1 "OTHER" WORD ALLOWED          
         MVC   TSTOTH(6),=C'INCHES '                                            
         LA    R3,6(R3)            ADD TO COUNT                                 
         B     OTHBUMP             SEE IF ANY MORE                              
OTH40    CLC   =C'COLUMNS ',0(R2)  COLUMNS ?                                    
         BNE   OTH40D              YES                                          
         LA    R3,7(R3)            ADD TO COUNT                                 
         B     OTHOUT                                                           
OTH40D   CLC   =C'COLUMN ',0(R2)   COLUMN ?                                     
         BNE   OTH40H              YES                                          
         LA    R3,6(R3)            ADD TO COUNT                                 
         B     OTHOUT                                                           
OTH40H   CLC   =C'COL ',0(R2)      COLUMN ?                                     
         BNE   OTHBUMP             SEE IF ANY MORE                              
         LA    R3,3(R3)            ADD TO COUNT                                 
*****    B     OTHOUT                                                           
OTHOUT   CLI   TSTOTH,C' '         ANYTHING IN OTHER ?                          
         BH    RETURN              ERROR - ONLY 1 "OTHER" WORD ALLOWED          
         MVC   TSTOTH(7),=C'COLUMNS '                                           
*****    B     OTHBUMP             SEE IF ANY MORE                              
OTHBUMP  LA    R2,1(R2)            NEXT SOURCE POSITION                         
         BCT   R0,OTHTST                                                        
*                                                                               
         CLI   TSTFRAC,C' '        FRACTION FOUND ?                             
         BH    GENVAL              YES - DO NOT COUNT SINGLE DIGITS             
*                                                                               
*                                  LOOK FOR SINGLE DIGIT BETWEEN 1 & 5          
SDTST    LA    R2,TSTDESC          POINT TO FIELD TO TEST                       
         LA    R0,17               LOOP COUNTER                                 
*                                  TEST 1ST POS'N ONLY HERE                     
         CLC   =C'1 ',0(R2)        SINGLE DIGIT ?                               
         BE    SDTFRST             YES                                          
         CLC   =C'2 ',0(R2)        SINGLE DIGIT ?                               
         BE    SDTFRST             YES                                          
         CLC   =C'3 ',0(R2)        SINGLE DIGIT ?                               
         BE    SDTFRST             YES                                          
         CLC   =C'4 ',0(R2)        SINGLE DIGIT ?                               
         BE    SDTFRST             YES                                          
         CLC   =C'5 ',0(R2)        SINGLE DIGIT ?                               
         BNE   SDTNXT              CHECK NEXT POSITIONS                         
SDTFRST  MVC   TSTDIG,0(R2)        FIRST POSITION ONLY                          
         LA    R3,1(R3)            ADD TO COUNT                                 
*                                                                               
SDTNXT   DS    0H                  CHECK NEXT POSITIONS                         
         CLC   =C' 1 ',0(R2)       SINGLE DIGIT ?                               
         BE    SDTDIG              YES                                          
         CLC   =C' 2 ',0(R2)       SINGLE DIGIT ?                               
         BE    SDTDIG              YES                                          
         CLC   =C' 3 ',0(R2)       SINGLE DIGIT ?                               
         BE    SDTDIG              YES                                          
         CLC   =C' 4 ',0(R2)       SINGLE DIGIT ?                               
         BE    SDTDIG              YES                                          
         CLC   =C' 5 ',0(R2)       SINGLE DIGIT ?                               
         BE    SDTDIG              YES                                          
SDTBUMP  LA    R2,1(R2)            NEXT SOURCE POSITION                         
         BCT   R0,SDTNXT                                                        
         B     GENVAL              CHECK "GENERAL" VALIDITY                     
SDTDIG   DS    0H                                                               
         CLI   TSTDIG,C' '         DIGIT ALREADY FOUND ?                        
         BH    RETURN              ERROR - CANNOT HAVE MORE THAN 1              
         MVC   TSTDIG,1(R2)                                                     
         LA    R3,1(R3)            ADD TO COUNT                                 
         B     SDTBUMP             CHECK FOR MORE                               
*                                                                               
*                                                                               
GENVAL   DS    0H                  "GENERAL" VALIDITY CHECKING                  
*                                                                               
*                                  TOTAL CHARACTER COUNT EQUAL                  
         CH    R3,CHARCNT            TO VALID CHARACTER COUNT ?                 
         BNE   RETURN              ERROR - UNMATCHED DATA                       
*                                                                               
         CLI   TSTFRAC,C' '        FRACTION FOUND ?                             
         BNH   GENV10              NO                                           
         CLI   TSTDIG,C' '         SINGLE DIGIT FOUND ?                         
         BH    RETURN              ERROR - CANNOT HAVE BOTH                     
GENV10   DS    0H                                                               
         CLI   TSTVH,C' '          VERT/HORIZ FOUND ?                           
         BNH   GENV20              NO                                           
         CLI   TSTFRAC,C' '        FRACTION FOUND ?                             
         BNH   RETURN              ERR0R - NEEDED WITH VERT/HORIZ               
         CLI   TSTOTH,C' '         SPREAD OR ISLAND, ETC. FOUND ?               
         BH    RETURN              ERROR - NO GOOD WITH VERT/HORIZ              
GENV20   DS    0H                                                               
         CLC   =C'INCH',TSTOTH     INCH FOUND ?                                 
         BE    GENV24              YES                                          
         CLC   =C'COLUMN',TSTOTH   COLUMN FOUND ?                               
         BNE   GENV30              NO                                           
GENV24   CLI   TSTFRAC,C' '        FRACTION FOUND ?                             
         BH    RETURN              ERROR - BAD WITH INCHES/COLUMNS              
* ???????????????????  PAGE BELOW  ???????????????????????                      
         CLI   TSTPG,C' '          PAGE FOUND ?                                 
         BH    RETURN              ERROR - BAD WITH INCHES/COLUMNS              
         B     BLDFLD              GO DO TABLE (PSIZETAB) LOOK-UP               
GENV30   DS    0H                                                               
         CLC   =C'ISLAND',TSTOTH   ISLAND FOUND ?                               
         BE    GENV34              YES                                          
         CLC   =C'SQUARE',TSTOTH   SQUARE FOUND ?                               
         BNE   GENV40              NO                                           
GENV34   CLI   TSTFRAC,C' '        FRACTION FOUND ?                             
         BNH   RETURN              ERROR - NEEDED WITH ISLAND/SQUARE            
* ???????????????????  PAGE BELOW  ???????????????????????                      
         CLI   TSTPG,C' '          PAGE FOUND ?                                 
         BNH   RETURN              ERROR - NEEDED WITH ISLAND/SQUARE            
         B     BLDFLD              GO DO TABLE (PSIZETAB) LOOK-UP               
GENV40   DS    0H                                                               
         CLC   =C'SPREAD',TSTOTH   SPREAD FOUND ?                               
         BNE   BLDFLD              NO - GO DO TABLE (PSIZETAB) LOOK-UP          
         CLC   TSTFRAC(TSTJT-WRKTOP),SPACES                                     
         BNE   RETURN              ERROR - ONLY J OR T OK WITH SPREAD           
*                                                                               
*                                  BUILD A 17-BYTE FIELD TO                     
BLDFLD   DS    0H                  MATCH AGAINST PSIZETAB2                      
         LA    RE,TSTDESC          POINT TO 17-BYTE DESC FIELD                  
         MVC   TSTDESC,SPACES      CLEAR THE FIELD                              
         CLI   TSTFRAC,C' '        FRACTION FOUND ?                             
         BNH   BLDF20              NO                                           
         MVC   0(4,RE),TSTFRAC     PLACE THE FRACTION                           
         LA    RE,5(RE)            SET POINTER AS IF 4-BYTE FRACTION            
         CLI   TSTFRAC+3,C' '      IS IT 4-BYTE FRACTION ?                      
         BNE   *+6                 YES                                          
         BCTR  RE,0                MOVE 1 TO LEFT                               
         CLI   TSTVH,C' '          VERT/HORIZ FOUND ?                           
         BE    BLDF10              NO - SHOULD BE ISLAND OR SQUARE              
         MVC   0(1,RE),TSTVH       PLACE V OR H                                 
         MVC   2(4,RE),=C'PAGE'                                                 
         B     CHKTBL              TABLE LOOK-UP                                
BLDF10   MVC   0(3,RE),=C'PG '                                                  
         MVC   3(6,RE),TSTOTH      SHOULD BE ISLAND OR SQUARE                   
         B     CHKTBL              TABLE LOOK-UP                                
*                                                                               
BLDF20   DS    0H                  NOT A "FRACTION" DESCRIPTION                 
         CLC   =C'SPREAD',TSTOTH                                                
         BNE   BLDF30              NOT SPREAD                                   
         MVC   TSTDESC(6),TSTOTH                                                
         B     CHKTBL              TABLE LOOK-UP                                
*                                                                               
BLDF30   DS    0H                  INCHES ?                                     
         CLC   =C'INCH',TSTOTH                                                  
         BNE   BLDF40              NOT INCH                                     
         MVC   TSTDESC(1),TSTDIG                                                
* ?????????? MAY NEED TO INSERT 1 IF TSTDIG BLANK ???????????????               
         MVC   TSTDESC+2(6),TSTOTH                                              
         B     CHKTBL              TABLE LOOK-UP                                
*                                                                               
BLDF40   DS    0H                  COLUMNS ?                                    
         CLC   =C'COLUMN',TSTOTH                                                
         BNE   BLDF50              NOT COLUMN                                   
         MVC   TSTDESC(1),TSTDIG                                                
         MVC   TSTDESC+2(7),=C'COLUMNS'                                         
* ?????????? MAY NEED TO INSERT 1 IF TSTDIG BLANK ???????????????               
         CLI   TSTDIG,C'1'         MORE THAN 1 ?                                
         BH    CHKTBL              YES - LEAVE 'S'                              
         MVI   TSTDESC+8,C' '      BLANK OUT 'S'                                
         B     CHKTBL              TABLE LOOK-UP                                
*                                                                               
BLDF50   DS    0H                  PAGE ?                                       
         CLI   TSTPG,C' '                                                       
         BE    RETURN              SOMETHING WRONG (NOTHING FOUND)              
         CLI   TSTDIG,C'1'         MORE THAN 1 ?                                
         BH    RETURN              ERROR - CAN ONLY BE 1 OR BLANK               
         MVI   TSTDESC,C'1'                                                     
         MVC   TSTDESC+2(4),=C'PAGE'                                            
*****    B     CHKTBL              TABLE LOOK-UP                                
*                                                                               
CHKTBL   DS    0H                                                               
         LA    RE,PADSDCT          DESCRIPTION, SIZE CODE TABLE                 
CHKTST   CLI   0(RE),X'FF'         END OF TABLE ?                               
         BE    RETURN              YES - NOTHING FOUND (INVALID)                
         CLC   TSTDESC,0(RE)       MATCHING DESCRIPTION ?                       
         BE    DODESC              YES - COMPLETE THE OUTPUT                    
         LA    RE,20(RE)           NEXT TABLE ENTRY                             
         B     CHKTST                                                           
*                                                                               
DODESC   DS    0H                                                               
         MVC   TSTCOD,17(RE)       AD SIZE CODE                                 
         CLI   TSTJT,C' '          J(JR) OR T(TABLOID) ?                        
         BE    RETURN              NO - DONE - DESCRIPTION OK                   
         MVC   WRKFLD,SPACES                                                    
         MVC   WRKFLD+4(16),TSTDESC                                             
         CLI   TSTJT,C'J'          J(JR) ?                                      
         BNE   DDES10              MUST BE TABLOID                              
         MVC   WRKFLD(3),=C'JR.'   "ADJUST" DESCRIPTION                         
         MVI   TSTCOD,C'J'            AND SIZE CODE                             
         B     DDES90                                                           
DDES10   MVC   WRKFLD(3),=C'TAB'   "ADJUST" DESCRIPTION                         
         MVI   TSTCOD,C'T'            AND SIZE CODE                             
DDES90   MVC   TSTDESC,WRKFLD      COMPLETE THE ADJUSTMENT                      
*                                                                               
*                                  OUTPUT AND RETURN                            
RETURN   DS    0H                                                               
         L     R8,4(R1)            A(DESTINATION)                               
         XC    0(20,R8),0(R8)                                                   
         CLI   TST20,C' '                                                       
         BNH   ERROR               INVALID SOURCE                               
         MVI   0(R1),0             CLEAR INVALID INDICATOR                      
         MVC   0(20,R8),TST20                                                   
         B     EXIT                                                             
*                                                                               
ERROR    MVI   0(R1),X'FF'         INVALID SOURCE                               
         SPACE 2                                                                
EXIT     DS    0H                                                               
*NOP*    MVC   20(15,R8),TSTFRAC   **********   FOR TESTING                     
         XMOD1 1                                                                
*                                                                               
         EJECT                                                                  
*                                  USE SIZE CODE TO FIND DESCRIPTION            
GETDESC  DS    0H                                                               
         MVC   TSTCOD,0(R2)        SHOULD BE 3-POSITION SIZE CODE               
         LA    RE,PADSDCT          DESCRIPTION, SIZE CODE TABLE                 
GETDTST  CLI   0(RE),X'FF'         END OF TABLE ?                               
         BE    ERROR               YES - NOTHING FOUND (INVALID)                
         CLC   18(2,RE),TSTCOD+1   MATCHING CODE ? (NUMBER ONLY)                
         BE    CKDESC              YES - COMPLETE THE OUTPUT                    
         LA    RE,20(RE)           NEXT TABLE ENTRY                             
         B     GETDTST                                                          
*                                                                               
CKDESC   DS    0H                                                               
         MVC   TSTDESC,0(RE)       AD SIZE DESCRIPTION                          
         CLI   TSTCOD,C'S'         J(JR) OR T(TABLOID) ?                        
         BE    RETURN              NO - DONE - DESCRIPTION OK                   
         MVC   WRKFLD,SPACES                                                    
         MVC   WRKFLD+4(16),TSTDESC                                             
         CLI   TSTCOD,C'J'         J(JR) ?                                      
         BNE   CKDE10              MUST BE TABLOID                              
         MVC   WRKFLD(3),=C'JR.'   "ADJUST" DESCRIPTION                         
         B     CKDE90                                                           
CKDE10   CLI   TSTCOD,C'T'         T(TABLOID) ?                                 
         BNE   ERROR               INVALID - NOT "S" OR "J" OR "T"              
         MVC   WRKFLD(3),=C'TAB'   "ADJUST" DESCRIPTION                         
CKDE90   MVC   TSTDESC,WRKFLD      COMPLETE THE ADJUSTMENT                      
         B     RETURN              DONE                                         
*                                                                               
         EJECT                                                                  
*                                                                               
SPACES   DC    20C' '                                                           
         SPACE 3                                                                
         LTORG                                                                  
         EJECT                                                                  
       ++INCLUDE PSIZETAB2                                                      
         EJECT                                                                  
WRKD     DSECT                                                                  
WRKTOP   DS    0D                                                               
TSTFRAC  DS    CL4                 1/2, 1/10, ETC., (OR BLANK)                  
TSTVH    DS    CL1                 "V" OR "H" OR BLANK                          
TSTPG    DS    CL1                 "P" OR BLANK                                 
TSTDIG   DS    CL1                 1, 2, 3, 4 OR 5                              
TSTJT    DS    CL1                 "J" OR "T" OR BLANK                          
TSTOTH   DS    CL7                 SPREAD, ISLAND, SQUARE, INCH(ES)             
*                                      COLUMN(S).. (OR BLANK)                   
TST20    DS    0CL20                                                            
TSTCOD   DS    CL3                 AD SIZE CODE                                 
TSTDESC  DS    CL17                AD SIZE DESCRIPTION                          
WRKFLD   DS    CL20                WORK FIELD                                   
CHARCNT  DS    H                   COUNTER                                      
WRKDL    EQU   *-WRKD              LENGTH OF WORKING STORAGE                    
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'002PSIZEVAL  12/02/98'                                      
         END                                                                    
