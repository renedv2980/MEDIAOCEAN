*          DATA SET FACALLOVC  AT LEVEL 015 AS OF 11/12/01                      
*CATALP CALLOV                                                                  
         TITLE 'FACILITIES CALLOV - LOAD OVERLAY PHASE'                         
CALLOV   CSECT                                                                  
         ENTRY XAHOLE                                                           
*                                                                               
         NMOD1 WORKL,*CALLOV*,RA,CLEAR=YES                                      
         USING WORKD,RC                                                         
         USING PROGSPCD,PSREC      CURRENT PHASE RECORD                         
SV       USING PROGSPCD,SPSREC                                                  
         USING LIBUFFD,LIBUFF      DATASPACE     ARREDIT BLOCK                  
C        USING LIBUFFD,LIBUFFC     CORE RESIDENT ARREDIT BLOCK                  
*                                                                               
         ST    R1,SAVER1                                                        
         MVC   IPARMS,0(R1)        INCOMING PARAMETERS                          
         USING CALLOVD,IPARMS                                                   
         L     R9,VSYSFAC                                                       
         USING SYSFACD,R9          RA=A(SYSTEM FACILITY LIST)                   
         BRAS  RE,INIT             DO INITIALISATION                            
*                                                                               
         CLI   CMCMD,CMCHOLE       WANT TO FIX HOLE?                            
         BE    FIXHOLE             YES                                          
         CLI   CMCMD,CMCCND        WANT TO FIX CRNDX IN DATASPACE?              
         BE    FIXCRNDX            YES                                          
         CLI   CMCMD,CMCSYSF       WANT TO FIX ADDRESSES USING CRNDX?           
         BE    RBLDCRND            YES                                          
         CLI   CMCMD,CMCWRT        SOME TYPE OF WRITE REQUESTED?                
         BE    WRITE               YES                                          
         B     READ                ASSUME WANT A READ                           
         EJECT                                                                  
***********************************************************************         
* READ REQUEST                                                        *         
***********************************************************************         
         SPACE 1                                                                
READ     BRAS  RE,BLDKEY           BUILD 0SPPOO/LANG/LVL KEY                    
         CLI   USECIL,YES                                                       
         BNE   READ02                                                           
         CLI   CMCMD,CMCMDSR       SPECIAL READ?                                
         BNE   *+12                                                             
         CLI   CMTYP,CMTDIR        WANT DIRECTORY RECORD?                       
         BE    READ04              YES                                          
         B     READ10              READ FROM CIL                                
*                                                                               
READ02   CLI   CMCMD,CMCMDN        NORMAL CALL?                                 
         BNE   READ04              NO                                           
         OC    CMAPHS,CMAPHS       LOAD ADDRESS SPECIFIED ALREADY?              
         BNZ   READ04              YES                                          
         BRAS  RE,BLDMAP           BUILD MAP TABLE (IF REQUIRED)                
         BRAS  RE,GETDSPC          GET PHASE ENTRY FROM DATASPACE               
         BNE   XMOD                NOT FOUND                                    
*                                                                               
         TM    PSFLAG1,CTPHSCRQ    PHASE IS CORE RESIDENT?                      
         BZ    *+12                NO                                           
         BRAS  RE,GCORERES         GET PHASE ENTRY FOR CORE RESIDENT            
         B     XMOD                                                             
*                                                                               
         BRAS  RE,GETPATH          FIND LONGEST PATH FOR NODE                   
         BRAS  RE,DOPGMLD          LOAD AT PATH ADDRESS                         
         B     XMOD                                                             
         EJECT                                                                  
***********************************************************************         
* CALL IS TO LOAD TO DIRECTED ADDRESS                                 *         
***********************************************************************         
         SPACE 1                                                                
READ04   BRAS  RE,GETDSPC          GET PHASE ENTRY FROM DATASPACE               
         BNE   XMOD                NOT FOUND                                    
         CLI   CMCMD,CMCMDSR       SPECIAL READ?                                
         BNE   READ06                                                           
         CLI   CMTYP,CMTDIR        WANT DIRECTORY INFO ONLY?                    
         BNE   READ06              NO                                           
         XR    RF,RF                                                            
         ICM   RF,7,CMAPHS                                                      
         MVC   0(PROGSPCL,RF),PROGSPCD                                          
         B     READ08                                                           
*                                                                               
READ06   TM    PSFLAG1,CTPHSCRQ    REQUESTED PHASE IS CORE RESIDENT?            
         BNZ   *+12                SFC                                          
         BRAS  RE,LTOADD           LOAD TO SPECIFIED ADDRESS                    
         B     READ08                                                           
*                                                                               
         BRAS  RE,GCORERES         GET PHASE ENTRY FOR CORE RESIDENT            
*                                                                               
READ08   BRAS  RE,SCRMAP           SET SCREEN MAPPING INFO                      
         ICM   RF,15,PSLEN         RETURN DIRECTORY INFO                        
         STCM  RF,3,CMLPHS                                                      
         TM    PSFLAG1,CTPHSCRQ    CORE RESIDENT FLAG                           
         BZ    *+8                                                              
         OI    CMIPHS,PHRES                                                     
         MVI   BYTE,0              TEST LEVEL                                   
         CLI   PSLVL,C'A'                                                       
         BNE   *+8                                                              
         MVI   BYTE,X'10'                                                       
         CLI   PSLVL,C'B'                                                       
         BNE   *+8                                                              
         MVI   BYTE,X'20'                                                       
         CLI   PSLVL,C'C'                                                       
         BNE   *+8                                                              
         MVI   BYTE,X'30'                                                       
         OC    CMIPHS,BYTE                                                      
         B     XMOD                                                             
         EJECT                                                                  
***********************************************************************         
* CALL IS TO LOAD FROM CIL                                            *         
***********************************************************************         
         SPACE 1                                                                
READ10   CLI   CMCMD,CMCMDN        NORMAL CALL?                                 
         BE    READ18              YES                                          
*                                                                               
         MVC   MODID,SPACES                                                     
         GOTO1 AHEXOUT,DMCB,PSNAME,MODID,L'PSNAME,0                             
         MVC   MODID+0(1),LANGDEF  LANGUAGE LETTER OF PHASE NAME                
         CLI   PSLVL,C' '          TEST LEVEL?                                  
         BNH   *+10                NO                                           
         MVC   MODID+6(1),PSLVL    SET LEVEL                                    
*                                                                               
         XR    RF,RF               SET ADDRESS                                  
         ICM   RF,7,CMAPHS                                                      
         ST    RF,OVLYADDR                                                      
         MVC   OVLYLEN,EFFS        SET DUMMY LENGTH                             
*                                                                               
         BRAS  RE,SSET             SUSPEND TIMER                                
         BRAS  RE,GETFRCIL         READ PHASE FROM CIL                          
         BNE   READ16                                                           
*                                                                               
         ICM   R2,15,ATCB          IF LOADING INTO TCBPGMA MAKE SURE IT         
         BZ    READ12              DAMN WELL FITS                               
         USING TCBD,R2                                                          
*                                                                               
         XR    R0,R0               GET LOAD TO ADDRESS                          
         ICM   R0,7,CMAPHS                                                      
         C     R0,TCBPGMA                                                       
         BL    READ12              NOT IN PROGRAMS AREA                         
         C     R0,TCBPGMX                                                       
         BH    READ12              NOT IN PROGRAMS AREA                         
         LR    RF,R0                                                            
         A     RF,OVLYLEN                                                       
         BCTR  RF,0                                                             
         C     RF,TCBPGMX                                                       
         BNH   READ12              IT WILL FIT IN PROGRAMS AREA                 
         MVC   OVLYLEN,EFFS                                                     
         B     READ14              SET PHASE WONT FIT IN PGM AREA               
         DROP  R2                                                               
*                                                                               
READ12   L     RE,OVLYADDR         GET MOVE FROM ADDRESS                        
         L     RF,OVLYLEN                                                       
         XR    R0,R0               GET MOVE TO ADDRESS                          
         ICM   R0,7,CMAPHS                                                      
         LR    R1,RF                                                            
         MVCL  R0,RE               MOVE PHASE TO REQUIRED LOCATION              
*                                                                               
READ14   LA    R0,MODID            REMOVE PHASE                                 
         DELETE EPLOC=(0)                                                       
         BRAS  RE,RSET             RESTART TIMER                                
         MVI   CMCMD,0                                                          
         CLC   OVLYLEN,EFFS                                                     
         BNE   *+6                                                              
         DC    H'0'                DIE IF PHASE WONT FIT                        
*                                                                               
         BRAS  RE,SCRMAP           SET SCREEN MAPPING INFO                      
         B     XMOD                                                             
*                                                                               
READ16   BRAS  RE,RSET                                                          
         MVI   CMCMD,X'FF'         NO MATCHING LEVEL FOUND                      
         B     XMOD                                                             
         EJECT                                                                  
***********************************************************************         
* NORMAL LOAD FROM CIL                                                *         
***********************************************************************         
         SPACE 1                                                                
READ18   L     R3,BASEMAPA                                                      
         USING MPAREAD,R3                                                       
         XR    R0,R0                                                            
READ20   CLI   MPNODE,0            LOOK FOR THIS NODE OR EMPTY SLOT             
         BE    READ22                                                           
         ICM   RF,15,MPLEN         GET LENGTH TO THIS ONE                       
         AR    R0,RF                                                            
         AHI   R3,MPAREAL                                                       
         B     READ20                                                           
*                                                                               
READ22   ICM   R2,15,ATCB                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         USING TCBD,R2                                                          
         A     R0,TCBPGMA                                                       
         STCM  R0,7,CMAPHS         SET LOAD ADDRESS                             
         ST    R0,OVLYADDR                                                      
         MVC   OVLYLEN,EFFS        SET DUMMY LENGTH                             
*                                                                               
         MVC   MODID,SPACES                                                     
         GOTO1 AHEXOUT,DMCB,PSNAME,MODID,L'PSNAME,0                             
         MVC   MODID+0(1),LANGDEF  LANGUAGE LETTER OF PHASE NAME                
         CLI   PSLVL,C' '          TEST LEVEL?                                  
         BNH   *+10                NO                                           
         MVC   MODID+6(1),PSLVL    SET LEVEL                                    
*                                                                               
         BRAS  RE,SSET                                                          
         BRAS  RE,GETFRCIL         READ PHASE FROM CIL                          
         BNE   READ24              NOT THERE                                    
*                                                                               
         XR    R0,R0                                                            
         ICM   R0,7,CMAPHS                                                      
         A     R0,OVLYLEN                                                       
         C     R0,TCBPGMX                                                       
         BNH   *+6                                                              
         DC    H'0'                PHASE IS PAST END OF TCB                     
*                                                                               
         MVI   MPNODE,255          SET THIS NODE USED                           
         MVC   MPLEN,OVLYLEN                                                    
         DROP  R3                                                               
*                                                                               
         L     RE,OVLYADDR         GET MOVE FROM ADDRESS                        
         L     RF,OVLYLEN                                                       
         XR    R0,R0               GET MOVE TO ADDRESS                          
         ICM   R0,7,CMAPHS                                                      
         LR    R1,RF                                                            
         MVCL  R0,RE               MOVE PHASE TO REQUIRED LOCATION              
*                                                                               
         LA    R0,MODID            REMOVE PHASE                                 
         DELETE EPLOC=(0)                                                       
         BRAS  RE,RSET             RESTART TIMER                                
         MVI   CMCMD,0                                                          
         CLC   OVLYLEN,EFFS                                                     
         BNE   *+6                                                              
         DC    H'0'                DIE IF PHASE WONT FIT                        
*                                                                               
         BRAS  RE,SCRMAP           SET SCREEN MAPPING INFO                      
         B     XMOD                                                             
*                                                                               
READ24   BRAS  RE,RSET                                                          
         MVI   CMCMD,X'FF'         NO MATCHING LEVEL FOUND                      
         B     XMOD                                                             
         EJECT                                                                  
***********************************************************************         
* WRITE REQUEST                                                       *         
***********************************************************************         
         SPACE 1                                                                
WRITE    CLI   CMTYP,CMTDIR        WRITE DIRECTORY RECORD?                      
         BNE   WRT02               NO                                           
*                                                                               
         ICM   R2,15,CLADS         GET REQUESTED KEY                            
DS       USING PROGSPCD,R2                                                      
         MVC   PSREC(PSKEYL),DS.PROGSPCD                                        
*                                                                               
         MVI   CLCMD,0             SET OK                                       
         MVC   TPSKEY,PSREC                                                     
         MVI   LIACTN,LIAHIGH      TRY TO READ THE RECORD                       
         LA    RF,PROGSPCD                                                      
         ST    RF,LIAREC                                                        
         GOTO1 VARREDIT,DMCB,LIBUFF                                             
*                                                                               
         MVI   LIACTN,LIAWRT       WRITE RECORD                                 
         CLC   TPSKEY,PROGSPCD     FOUND IT?                                    
         BE    *+8                 YES                                          
         MVI   LIACTN,LIAADD       ADD RECORD                                   
*                                                                               
         MVC   PSREC(PROGSPCL),DS.PROGSPCD                                      
         GOTO1 VARREDIT,DMCB,LIBUFF                                             
         CLI   LIRTN,LIROK                                                      
         BE    XMOD                                                             
         MVI   CLCMD,255                                                        
         B     XMOD                                                             
         DROP  DS                                                               
         EJECT                                                                  
***********************************************************************         
* WRITE FROM LOADLIB TO DATASPACE                                     *         
***********************************************************************         
         SPACE 1                                                                
WRT02    XR    R2,R2                                                            
         ICM   R2,7,CLALL          R2 = A(LOADLIB PHASE)                        
LL       USING PROGSPCD,R2         LOADLIB PHASE RECORD                         
*                                                                               
         CLI   USECIL,YES          WRITE FROM LOADLIB TO DSPACE?                
         BNE   WRT04               NO                                           
         TM    LL.PSFLAG1,CTPHSDMQ DUMMY PHASE?                                 
         BO    WRT04               YES - DO SPECIAL STUFF                       
*                                                                               
         BRAS  RE,SSET             SUSPEND TIMER                                
*                                                                               
         MVC   MODID,SPACES                                                     
         GOTO1 AHEXOUT,DMCB,LL.PSNAME,MODID,L'PSNAME,0                          
         MVC   MODID+0(1),LL.PSLANG                                             
*                                                                               
         CLI   LL.PSLVL,C' '       TEST LEVEL?                                  
         BNH   *+10                NO                                           
         MVC   MODID+6(1),LL.PSLVL                                              
*                                                                               
         LA    R0,MODID            POINT TO MODULE NAME                         
         LOAD  EPLOC=(0),ERRET=WRITERR                                          
*                                                                               
         ST    R0,OVLYADDR                                                      
         LA    RF,0(R1)            CALCULATE PHASE LEN                          
         SLL   RF,3                                                             
         ST    RF,OVLYLEN          AND SET IT                                   
*                                                                               
         L     R2,CLADS            NOW SET DATASPACE KEY                        
DS       USING PROGSPCD,R2                                                      
         MVC   PSREC(PSKEYL),DS.PROGSPCD                                        
*                                                                               
         MVI   CLCMD,0             SET OK                                       
         BRAS  RE,PUTDRCT          PUT PHASE TO DATASPACE                       
*                                                                               
         LA    R0,MODID            DELETE FORCE LOADED PHASE                    
         DELETE EPLOC=(0)                                                       
         B     XMOD                                                             
*                                                                               
WRITERR  BRAS  RE,RSET             RESTART TIMER                                
         MVI   CLCMD,X'FF'         SET ERROR                                    
         B     XMOD                                                             
         DROP  LL                                                               
         EJECT                                                                  
***********************************************************************         
* WRITE FROM CORE TO DATASPACE                                        *         
***********************************************************************         
         SPACE 1                                                                
WRT04    XR    RF,RF               GET A(PHASE) IN CORE                         
         ICM   RF,7,CMAPHS                                                      
         ST    RF,OVLYADDR                                                      
         XR    RF,RF               GET NEW LENGTH (IF SET)                      
         ICM   RF,3,CMLPHS                                                      
         ST    RF,OVLYLEN                                                       
*                                                                               
         ICM   RF,7,CMNPHS         PHASE RECORD DETAILS                         
         MVC   PSREC,0(RF)                                                      
         MVI   CLCMD,0             SET OK                                       
         BRAS  RE,PUTDRCT                                                       
         B     XMOD                                                             
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO WRITE OR ADD A PHASE + RECORD TO DATASPACE               *         
***********************************************************************         
         SPACE 1                                                                
PUTDRCT  NTR1  ,                                                                
         MVC   TPSKEY,PSREC                                                     
         MVI   LIACTN,LIAHIGH      TRY TO READ THE RECORD                       
         LA    RF,PROGSPCD                                                      
         ST    RF,LIAREC                                                        
         GOTO1 VARREDIT,DMCB,LIBUFF                                             
*                                                                               
         MVI   LIACTN,LIAWRT       SET TO WRITE RECORD                          
         CLC   TPSKEY,PROGSPCD     FOUND RECORD?                                
         BE    PTD02               YES                                          
*                                                                               
         MVI   LIACTN,LIAADD       SET TO ADD RECORD                            
         XC    PSREC,PSREC                                                      
         MVC   PSREC(PSKEYL),TPSKEY                                             
*                                                                               
PTD02    ICM   RF,15,=AL4(DPDPGMS)                                              
         BRAS  RE,LOCKME           LOCK TABLE                                   
         BRAS  RE,PUTITIN          MOVE PROGRAM TO DSPACE                       
         BNE   PTDERR                                                           
*                                                                               
         GOTO1 VARREDIT,DMCB,LIBUFF                                             
         CLI   LIRTN,LIROK                                                      
         BNE   PTDERR                                                           
         ICM   RF,15,=AL4(DPDPGMS)                                              
         BRAS  RE,FREEME                                                        
         B     EXITOK                                                           
*                                                                               
PTDERR   ICM   RF,15,=AL4(DPDPGMS)                                              
         BRAS  RE,FREEME                                                        
         MVI   CLCMD,X'FF'         PUTDRCT RETURNED ERROR                       
         B     EXITL                                                            
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO LOAD PROGRAM INTO CURRENT SLOT (IF IT FITS) OR NEW ONE   *         
* NTRY: FAPROGSPCD FILLED IN                                          *         
*       OVLYADDR = A(OVERLAY IN CORE)                                 *         
*       OVLYLEN  = L'OVERLAY IN CORE (IF ZERO - USE OLD LENGTH)       *         
*       ASSUMES TABLE (AND HENCE PGMS AREA) IS LOCKED                 *         
***********************************************************************         
         SPACE 1                                                                
PUTITIN  NTR1  ,                                                                
         XR    R0,R0                                                            
         ICM   R0,3,PSCOUNT        INCREMENT NUMBER OF LOADS                    
         AHI   R0,1                                                             
         STCM  R0,3,PSCOUNT                                                     
*                                  UPDATE TIME OF LOAD                          
         GOTO1 ADATTIM,DMCB,(X'01',PSTIME),0                                    
         CLI   4(R1),0                                                          
         BNE   EXITL               DATTIM NOT HAPPY                             
         TM    PSFLAG1,CTPHSDMQ    DUMMY PHASE                                  
         BO    EXITOK              YES, JUST EXIT - THIS CLEARS SLOT            
*                                                                               
         BRAS  RE,ON31             GET INTO XA                                  
         ICM   RF,15,PSLENH        LENGTH OF SLOT FOR THIS PROGRAM              
         BZ    PIN02               NO SLOT - NEED TO GET ONE                    
*                                                                               
         OC    OVLYLEN,OVLYLEN     USE OLD LENGTH IF ZERO                       
         BNZ   *+10                                                             
         MVC   OVLYLEN,PSLEN       USE OLD LENGTH IF ZERO                       
         L     RE,OVLYLEN          LENGTH OF PHASE TO LOAD                      
         CR    RE,RF                                                            
         BH    PIN02               NEED A NEW SLOT                              
*                                                                               
         LAM   R0,RF,ARZERO        PUT PHASE INTO OLD SLOT                      
         LAM   R2,R2,ALET                                                       
         ICM   R2,15,PSADR         A(PHASE IN DATASPACE)                        
         ICM   R3,15,PSLENH        LENGTH OF SLOT FOR PHASE                     
*                                                                               
         ICM   RE,15,OVLYADDR      A(PHASE IN CORE)                             
         ICM   RF,15,OVLYLEN       LENGTH OF PHASE TO LOAD                      
         SAC   512                                                              
         MVCL  R2,RE               COPY IN PHASE                                
         SAC   0                                                                
         LAM   R2,R2,ARZERO                                                     
         MVC   PSLEN,OVLYLEN                                                    
         B     EXITOK              WILL TAKE US OUT OF XA                       
*                                                                               
PIN02    LAM   R0,RF,ARZERO        PHASE WON'T FIT - GET NEW SLOT               
         LAM   R2,R2,ALET                                                       
         XR    R2,R2                                                            
         SAC   512                                                              
         ICM   R2,15,PGMSPGM-FAPGMSD(R2)                                        
         USING PGAREAD,R2                                                       
*                                                                               
         L     RF,OVLYLEN          LENGTH OF PHASE TO LOAD                      
         STCM  RF,15,PSLEN                                                      
         AHI   RF,63                                                            
         SRL   RF,6                                                             
         SLL   RF,6                ROUND UP TO NEXT 64 BYTES                    
         STCM  RF,15,PSLENH                                                     
*                                                                               
PIN04    ICM   RE,15,PGANOW                                                     
         STCM  RE,15,PSADR                                                      
         LA    R0,0(RE,RF)         MAKE SURE THE FUCKER FITS                    
         CLM   R0,15,PGALAST                                                    
         BNH   PIN06                                                            
*>>>>                                                                           
*>>>>          OUTPUT MESSAGE HERE ABOUT RELOAD                                 
*>>>>                                                                           
         SAC   0                                                                
         LAM   R2,R2,ARZERO                                                     
         B     EXITL                                                            
*                                                                               
PIN06    CS    RE,R0,PGANOW        SWAP IN THE NEW LENGTH                       
         BNE   PIN04                                                            
         DROP  R2                                                               
*                                                                               
         ICM   R2,15,PSADR         A(PHASE IN DATASPACE)                        
         ICM   R3,15,PSLENH        L'SLOT FOR PHASE IN DATASPACE                
         L     RE,OVLYADDR         A(PHASE IN CORE)                             
         L     RF,OVLYLEN          L'PHASE IN CORE                              
         SAC   512                                                              
         MVCL  R2,RE                                                            
         SAC   0                                                                
         LAM   R2,R2,ARZERO                                                     
         B     EXITOK              WILL TAKE US OUT OF XA                       
         EJECT                                                                  
***********************************************************************         
* GET PHASE IN MODID FROM LOADLIB                                     *         
***********************************************************************         
         SPACE 1                                                                
GETFRCIL NTR1  ,                                                                
         MVC   DUB,MODID           SAVE ORIGINAL MODULE NAME                    
GCF02    LA    R0,MODID            POINT TO MODULE NAME                         
         LOAD  EPLOC=(0),ERRET=GCF04                                            
*                                                                               
         ST    R0,OVLYADDR         PHASE IN LIBRARY R0=ADR,R1=LEN               
         LA    RF,0(R1)            CALCULATE PHASE LEN                          
         SLL   RF,3                                                             
         ST    RF,OVLYLEN                                                       
         B     EXITOK                                                           
*                                                                               
GCF04    CLC   LANGDEF,MODID       FOREIGN VERSION NOT FOUND                    
         BE    GCF08                                                            
         CLI   MODID+6,C' '        FOREIGN TEST VERSION?                        
         BE    GCF06               YES                                          
         CLI   CMTYP,CMTADD        FORCE READ FOR ONLY THIS LEVEL?              
         BE    EXITL               YES - NOT FOUND                              
         MVI   MODID+6,C' '        TRY FOR LIVE FOREIGN VERSION                 
         B     GCF02                                                            
*                                                                               
GCF06    MVC   MODID+0(1),LANGDEF  TRY TEST ENGLISH VERSION                     
         MVC   MODID+6(1),DUB+6                                                 
         B     GCF02                                                            
*                                                                               
GCF08    CLI   MODID+6,C' '        ENGLISH VERSION NOT FOUND                    
         BE    EXITL                                                            
         CLI   CMTYP,CMTADD        FORCE READ FOR ONLY THIS LEVEL?              
         BE    EXITL               YES - NOT FOUND                              
         MVI   MODID+6,C' '        TRY FOR LIVE ENGLISH VERSION                 
         B     GCF02                                                            
         EJECT                                                                  
***********************************************************************         
* BUILD INITIAL 0SPPOO/LANG/LVL KEY FOR LOOKUP                        *         
***********************************************************************         
         SPACE 1                                                                
BLDKEY   NTR1  ,                                                                
         L     R2,ATCB                                                          
         USING TCBD,R2                                                          
         XR    R0,R0               DEFAULT IS SYSTEM HOST LANGUAGE              
         ICM   R3,15,AUTL                                                       
         USING UTLD,R3                                                          
         BZ    *+8                                                              
         IC    R0,TLANG            EXTRACT LANGUAGE CODE FROM UTL               
         CHI   R0,8                                                             
         BNH   *+6                                                              
         XR    R0,R0               SET TO ENGLISH IF UNKNOWN                    
*                                                                               
         L     RF,ALANGTAB         GET LANGUAGE CHARACTERS                      
         MH    R0,0(RF)                                                         
         AHI   RF,6                                                             
         USING LANGTABD,RF                                                      
         MVC   LANGDEF,LANGOVL1    DEFAULT LANGUAGE CHARACTER                   
         AR    RF,R0                                                            
         MVC   LANGCHR,LANGOVL1    USER LANGUAGE CHARACTER                      
         MVC   PSLANG,LANGCHR                                                   
         DROP  RF                                                               
*                                                                               
         MVC   PSOVR,CMOVR         NORMAL CALL PASSES X'OO' ONLY                
         CLI   CMCMD,CMCMDN        NORMAL CALL?                                 
         BE    BKY04               YES                                          
*                                                                               
         MVC   PSNAME,CMNPHS       ASSUME MAINT CALL                            
         NI    PSSYS,X'0F'                                                      
         OC    PSNAME(2),PSNAME    CALLER SET X'0SPP' CORRECTLY                 
         BNZ   BKY06               YES - DON'T USE TCB VALUES                   
*                                                                               
BKY04    MVC   PSSYS,TCBOVSYS                                                   
         MVC   PSPRG,TCBPRG                                                     
*                                                                               
         LTR   R3,R3               UTL AROUND?                                  
         BZ    BKY14               NO                                           
         CLI   TCBOVSYS,1          SERVICE REQUEST?                             
         BE    BKY06               YES                                          
         MVC   PSSYS,TCOSYS        SET OVERRIDE SYSTEM                          
*                                                                               
BKY06    LTR   R3,R3               UTL AROUND?                                  
         BZ    BKY14               NO                                           
         OC    TSVCREQ,TSVCREQ     SERVICE REQUEST?                             
         BZ    BKY10               NO                                           
*                                                                               
         TM    TTEST,TESTTSRY      CIL MODE FOR SERVICE REQUEST?                
         BZ    BKY08               NO                                           
         MVI   USECIL,YES                                                       
         TM    TTEST,TTESTSRA                                                   
         BZ    BKY12                                                            
         MVI   PSLVL,C'A'          SET LEVEL A TEST MODE                        
         B     BKY12                                                            
*                                                                               
BKY08    TM    TTEST,TTESTSRA      USE REAL TTEST?                              
         BZ    BKY12               NO                                           
*                                                                               
BKY10    MVC   BYTE,TTEST          EXTRACT TEST STATUS FROM UTL                 
         NI    BYTE,TTESTLVL       SET TEST LEVEL                               
         XR    R1,R1                                                            
         IC    R1,BYTE                                                          
         LA    R1,LVLTRN(R1)                                                    
         MVC   PSLVL,0(R1)         SET TEST LEVEL REQUIRED                      
*                                                                               
BKY12    TM    TTEST,TTESTTAC                                                   
         BZ    BKY14                                                            
         MVC   TERMID,TNUM         SET TEST TERM NUM                            
         MVC   TESTID,TACCS        SET TEST USER ID                             
*                                                                               
BKY14    ICM   RE,15,TCBMAP        CHECK IF PHASE MAP ALREADY EXISTS            
         LA    R1,MPHDRL(RE)                                                    
         ST    R1,BASEMAPA                                                      
         CLI   PSSYS,0             NO PHASE MAP FOR SYS ZERO                    
         BNE   BKY16                                                            
         TM    TTEST,TTESTSRA      USE REAL TTEST?                              
         BNO   BLDKEYX                                                          
*                                                                               
         USING MPHDRD,RE                                                        
BKY16    CLC   MHSP,PSNAME         SAME LEVEL/PROGRAM/LANGUAGE                  
         BNE   BKY18                                                            
         CLC   PSLVL,MHLVL                                                      
         BNE   BKY18                                                            
         CLC   PSLANG,MHLANG                                                    
         BNE   BKY18                                                            
         B     BLDKEYX             YES                                          
*                                                                               
BKY18    XC    MPHDRD(MPHDRL),MPHDRD                                            
         L     R0,BASEMAPA         CLEAR PHASE MAP FOR TASK                     
         LHI   R1,MAPLEN                                                        
         XR    RF,RF                                                            
         MVCL  R0,RE                                                            
         DROP  RE                                                               
*                                                                               
BLDKEYX  TM    TTEST,TTESTSRA      USE REAL TTEST?                              
         BO    *+8                                                              
         BRAS  RE,CHKVRSN          ADJUST FOR VERSION CONTROL                   
         MVC   SPSREC,PSREC        SAVE REQUESTED RECORD INFO                   
         B     EXITOK                                                           
         DROP  R2,R3                                                            
         EJECT                                                                  
***********************************************************************         
* CHECK VERSION TABLE FOR PHASE ENTRY                                 *         
***********************************************************************         
         SPACE 1                                                                
         PUSH  USING                                                            
         USING DMSPACED,WORK                                                    
CHKVRSN  NTR1  ,                                                                
         ICM   RF,15,=AL4(DTVRSN)                                               
         BRAS  RE,ASKME                                                         
         ICM   R2,15,DSPTFRST                                                   
         N     R2,=XL4'0FFFFFFF'                                                
         LH    RE,DSPTWIDE                                                      
         L     RF,DSPTEND                                                       
         LAM   R0,RF,ARZERO                                                     
         LAM   R2,R2,ALETT                                                      
         SAC   512                                                              
         USING VRSNTABD,R2                                                      
*                                                                               
         MVC   FULL(3),PSSYS       COPY PHASE TO FULL                           
         OI    FULL,X'80'          +X'80' BIT                                   
*                                                                               
CKV02    OC    VRSNPGM,VRSNPGM     EOT?                                         
         BZ    CKVX                YES                                          
         CLC   VRSNPHS,FULL        FIND IT IN VERSION TABLE                     
         BE    *+12                                                             
CKV04    BXLE  R2,RE,CKV02                                                      
         B     CKVX                                                             
*                                                                               
CKV08    ICM   R3,15,VSSB                                                       
         USING SSBD,R3                                                          
         OC    VRSNADV,VRSNADV     CHECK ADV NUM                                
         BZ    *+14                                                             
         CLC   SSBSYSID,VRSNADV                                                 
         BE    CKV10                                                            
*                                                                               
         ICM   R3,15,AUTL                                                       
         BZ    CKV04                                                            
         USING UTLD,R3                                                          
         OC    VRSNSEN,VRSNSEN     CHECK SE NUM                                 
         BZ    *+14                                                             
         CLC   TSYS,VRSNSEN                                                     
         BE    CKV10                                                            
*                                                                               
         OC    VRSNAGY,VRSNAGY     CHECK TAGY                                   
         BZ    *+14                                                             
         CLC   TAGY,VRSNAGY                                                     
         BE    CKV10                                                            
         B     CKV04                                                            
*                                                                               
CKV10    XR    RF,RF              SET TEST LEVEL                                
         IC    RF,VRSNABC         SET TEST LEVEL                                
         N     RF,=AL4(TTESTLVL)                                                
         LA    RF,LVLTRN(RF)                                                    
         MVC   PSLVL,0(RF)         SET TEST LEVEL REQUIRED                      
*                                                                               
CKVX     LAM   R2,R2,ARZERO                                                     
         SAC   0                                                                
         B     EXITOK                                                           
         POP   USING                                                            
         EJECT                                                                  
***********************************************************************         
* GET BEST MATCH FOR PHASE ENTRY FROM DATASPACE INTO PSREC            *         
* MATCHES ARE PERFORMED IN THIS ORDER:                                *         
* 1. TEST + LANGAGE            2. LIVE +LANGUAGE                      *         
* 3. TEST + ENGLISH            4. LIVE + ENGLISH                      *         
***********************************************************************         
         SPACE 1                                                                
GETDSPC  NTR1  ,                                                                
         MVI   LIACTN,LIAHIGH                                                   
         LA    RF,PROGSPCD                                                      
         ST    RF,LIAREC                                                        
*                                                                               
         CLC   PSLANG,LANGDEF      ANY LANGUAGE?                                
         BE    GDR04               NO                                           
         CLI   PSLVL,0             ANY TEST LEVEL?                              
         BE    GDR02               NO                                           
         BRAS  R2,GDGETIT          LANGUAGE + TEST LEVEL                        
*                                                                               
GDR02    MVI   PSLVL,0             LANGUAGE + LIVE LEVEL                        
         BRAS  R2,GDGETIT                                                       
*                                                                               
GDR04    CLI   PSLVL,0             ANY TEST LEVEL?                              
         BE    GDR06               NO                                           
         MVC   PSLANG,LANGDEF      ENGLISH + TEST LEVEL                         
         BRAS  R2,GDGETIT                                                       
*                                                                               
GDR06    MVC   PSLANG,LANGDEF      ENGLISH + LIVE LEVEL                         
         MVI   PSLVL,0                                                          
         BRAS  R2,GDGETIT                                                       
         XC    PSREC,PSREC                                                      
         MVI   CMCMD,X'FF'         NO MATCHING PHASE FOUND                      
         B     EXITL                                                            
*                                                                               
GDGETIT  MVC   TPSKEY,PSREC        CALL ARREDIT FOR THIS PHASE                  
         GOTO1 VARREDIT,DMCB,LIBUFF                                             
         CLC   TPSKEY,PSREC        MATCH?                                       
         BNE   GDGNO               NO                                           
         CLI   PSSTAT,255          IS THIS DELETED?                             
         BNE   EXITOK              NO - RETURN IT                               
*                                                                               
GDGNO    MVC   PSREC,SPSREC        RESTORE CALLER'S KEY                         
         BR    R2                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO BUILD MAP TABLE ENTRIES FOR EACH PHASE                   *         
* NTRY: ASSUMES PSREC AND SPSREC FILLED IN CORRECTLY                  *         
***********************************************************************         
         SPACE 1                                                                
BLDMAP   NTR1  ,                                                                
         L     R3,ATCB                                                          
         L     R3,TCBMAP-TCBD(R3)                                               
         USING MPHDRD,R3                                                        
         MVC   MHID,=CL4'IGOR'                                                  
         MVC   MHSP,PSNAME         SET LEVEL/PROGRAM/LANGUAGE                   
         MVC   MHLVL,PSLVL                                                      
         MVC   MHLANG,PSLANG                                                    
         DROP  R3                                                               
*                                                                               
         MVI   PSOVR,0                                                          
         MVI   LIACTN,LIAHIGH                                                   
         LA    RF,PSREC                                                         
         ST    RF,LIAREC                                                        
*                                                                               
BMP02    CLC   PSLANG,LANGDEF      ANY LANGUAGE?                                
         BE    BMP06               NO                                           
         CLI   PSLVL,0             ANY TEST LEVEL?                              
         BE    BMP04               NO                                           
         BRAS  R2,BMP10            LANGUAGE + TEST LEVEL                        
*                                                                               
BMP04    MVI   PSLVL,0             LANGUAGE + LIVE LEVEL                        
         BRAS  R2,BMP10                                                         
*                                                                               
BMP06    CLI   PSLVL,0             ANY TEST LEVEL?                              
         BE    BMP08               NO                                           
         MVC   PSLANG,LANGDEF      ENGLISH + TEST LEVEL                         
         BRAS  R2,BMP10                                                         
*                                                                               
BMP08    MVC   PSLANG,LANGDEF      ENGLISH + LIVE LEVEL                         
         MVI   PSLVL,0                                                          
         BRAS  R2,BMP10                                                         
         B     BMP18                                                            
*                                                                               
BMP10    MVC   TPSKEY,PSREC        CALL ARREDIT                                 
         GOTO1 VARREDIT,DMCB,LIBUFF                                             
         CLC   TPSKEY,PSREC        MATCH?                                       
         BE    BMP12               YES                                          
         MVC   PSREC(PSKEYL),TPSKEY                                             
         BR    R2                                                               
*                                                                               
BMP12    CLI   PSNODES,0                                                        
         BE    BMP18                                                            
         TM    PSFLAG1,(CTPHSSCQ+CTPHSCSQ)                                      
         BNZ   BMP18               IGNORE SCREENS AND CORERES                   
*                                                                               
         L     R3,BASEMAPA                                                      
         USING MPAREAD,R3                                                       
BMP14    CLI   MPNODE,0            LOOK FOR THIS NODE OR EMPTY SLOT             
         BE    BMP16                                                            
         CLC   PSNODES,MPNODE                                                   
         BE    BMP16                                                            
         AHI   R3,MPAREAL                                                       
         B     BMP14                                                            
*                                                                               
BMP16    MVC   MPNODE,PSNODES      SET LONGEST MATCH FOR THIS NODE              
         TM    PSFLAG1,CTPHSCRQ                                                 
         BZ    *+10                                                             
         XC    PSLEN,PSLEN                                                      
         CLC   MPLEN,PSLEN                                                      
         BH    BMP18                                                            
         MVC   MPLEN,PSLEN                                                      
         MVC   MPLANG,PSLANG                                                    
         MVC   MPOVR,PSOVR                                                      
         MVC   MPLVL,PSLVL                                                      
         DROP  R3                                                               
*                                                                               
BMP18    XR    R0,R0               READ HIGH FOR NEXT OVERLAY NUMBER            
         IC    R0,PSOVR                                                         
         AHI   R0,1                                                             
         CHI   R0,255              WITHIN RANGE?                                
         BH    BMP20               NO                                           
*                                                                               
         XC    PSREC,PSREC         READ FOR ONLY 0SPPOO                         
         MVC   PSNAME,SV.PSNAME                                                 
         STC   R0,PSOVR            SET OO TO OO+1                               
*                                                                               
         GOTO1 VARREDIT,DMCB,LIBUFF                                             
         CLC   PSNAME(2),SV.PSNAME CHECK 0SPP IS STILL THE SAME                 
         BNE   BMP20               NO - FINISHED                                
*                                                                               
         MVC   PSLANG,SV.PSLANG    RESET TO OUR LANGUAGE/LEVEL                  
         MVC   PSLVL,SV.PSLVL                                                   
         B     BMP02               CONTINUE                                     
*                                                                               
BMP20    L     R3,BASEMAPA         SORT NODES INTO ASCENDING ORDER              
         USING MPAREAD,R3                                                       
*                                                                               
BMP22    CLI   MPNODE,0            END OF LIST?                                 
         BE    BMP30               YES                                          
         LA    R4,MPAREAL(R3)                                                   
NXT      USING MPAREAD,R4                                                       
*                                                                               
BMP24    CLI   NXT.MPNODE,0        END OF LIST?                                 
         BE    BMP28               YES                                          
         CLC   MPNODE,NXT.MPNODE   UNSORTED NODE IS LOWER THAN SORTED           
         BL    BMP26               NO                                           
         XC    MPNODE(MPAREAL),NXT.MPNODE                                       
         XC    NXT.MPNODE(MPAREAL),MPNODE                                       
         XC    MPNODE(MPAREAL),NXT.MPNODE                                       
*                                                                               
BMP26    AHI   R4,MPAREAL          NEXT IN UNSORTED LIST                        
         B     BMP24                                                            
*                                                                               
BMP28    AHI   R3,MPAREAL                                                       
         B     BMP22                                                            
         DROP  R3,NXT                                                           
*                                                                               
BMP30    MVC   PSREC,SPSREC        RESTORE ORIGINAL PSREC                       
         B     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
* GO FIND LONGEST PATH TO THIS OVERLAY USING MAP TABLE                *         
* EXIT: TOTLEN  = LENGTH OF LONGEST PATH                              *         
***********************************************************************         
         SPACE 1                                                                
GETPATH  NTR1  ,                                                                
         XC    TOTLEN,TOTLEN       CLEAR PATH LENGTH                            
         CLI   PSOVR,0             BASE PHASE HAS NO LINKAGE                    
         BE    EXITOK                                                           
*                                                                               
         XC    WORK,WORK           FIRST BUILD NODE LIST INTO WORK              
         LA    R2,WORK             (BUILD IN REVERSE SEQUENCE ORDER)            
         L     RF,ATCB                                                          
*                                                                               
         L     R4,BASEMAPA                                                      
         USING MPAREAD,R4                                                       
GP01     CLI   0(R4),0                                                          
         BNE   *+6                                                              
         DC    H'0'                                                             
         CLC   PSNODES,MPNODE                                                   
         BE    GP02                                                             
         AHI   R4,MPAREAL                                                       
         B     GP01                                                             
         DROP  R4                                                               
*                                                                               
GP02     LR    R1,R4               R1 = LAST FOUND ENTRY                        
         USING MPAREAD,R1                                                       
         PACK  STNODE,MPNODE       GET START NODE                               
         NI    STNODE,X'0F'                                                     
         CLI   STNODE,0            STNODE IS ZERO FOR ROOT PHASE                
         BE    GP08                                                             
*                                                                               
         L     R1,BASEMAPA                                                      
GP04     MVC   NDNODE,MPNODE       GET END NODE                                 
         NI    NDNODE,X'0F'                                                     
         CLC   STNODE,NDNODE       START NODE = END NODE?                       
         BE    GP06                YES - SAVE NODE                              
         AHI   R1,MPAREAL                                                       
         CLI   0(R1),0                                                          
         BNE   GP04                                                             
         DC    H'0'                DIE IF CANT MATCH NODES                      
*                                                                               
GP06     MVC   0(1,R2),STNODE      SAVE THIS NODE                               
         AHI   R2,1                NEXT NODE SAVE AREA                          
         LR    R4,R1               SAVE NEW START NODE                          
         B     GP02                                                             
*                                                                               
GP08     LA    R2,WORK             GET LONGEST PHASE FOR EACH NODE PAIR         
*                                                                               
GP10     OC    0(2,R2),0(R2)       REACHED END OF NODES?                        
         BZ    EXITOK              YES                                          
*                                                                               
         PACK  BYTE,1(1,R2)        CREATE 'START-END' NODE IN BYTE              
         OC    BYTE,0(R2)                                                       
         XR    R0,R0               R0 = LENGTH OF LONGEST MATCH                 
         L     R1,BASEMAPA                                                      
*                                                                               
GP12     CLC   MPNODE,BYTE         MATCH NODES?                                 
         BNE   GP14                NO                                           
         ICM   R0,15,MPLEN                                                      
*                                                                               
GP14     AHI   R1,MPAREAL                                                       
         CLI   0(R1),0             REACHED HIGH ADDRESS?                        
         BNE   GP12                NO - NEXT NODE                               
         A     R0,TOTLEN                                                        
         ST    R0,TOTLEN                                                        
         AHI   R2,1                POINT TO NEXT START NODE                     
         B     GP10                                                             
         DROP  R1                                                               
         EJECT                                                                  
***********************************************************************         
* DO LOAD TO NODE DETERMINED ADDRESS                                  *         
* NTRY: TOTLEN  = DISPLACEMENT TO PROGRAM AREA LOAD POINT             *         
***********************************************************************         
         SPACE 1                                                                
DOPGMLD  NTR1  ,                                                                
         L     R3,ATCB                                                          
         USING TCBD,R3                                                          
         L     RF,TCBPGMA                                                       
         A     RF,TOTLEN                                                        
         STCM  RF,7,CMAPHS         SET OVERLAY LOAD ADDRESS                     
*                                                                               
         ICM   R0,15,PSLEN         MAKE SURE OVERLAY WILL FIT                   
         BCTR  R0,0                                                             
         AR    RF,R0                                                            
         C     RF,TCBPGMX                                                       
         BNH   *+6                                                              
         DC    H'0'                DIE IF PHASE WONT FIT                        
*                                                                               
         BRAS  RE,LTOADD           NOW GO DO LOAD                               
*                                                                               
         ICM   R1,15,AUTL          TEST FOR DEBUG TERMINAL                      
         BZ    EXITOK                                                           
         TM    TSTAT6-UTLD(R1),TST6DBUG                                         
         BZ    EXITOK                                                           
*                                                                               
         XR    RE,RE                                                            
         ICM   RE,7,CMAPHS                                                      
         C     RE,TCBPGMA          TEST PHASE IS WITHIN TASK                    
         BL    DPL02                                                            
         C     RE,TCBPGMX                                                       
         BH    DPL02                                                            
         CLC   0(2,RE),=X'90EC'                                                 
         BNE   DPL02                                                            
         XC    0(2,RE),0(RE)                                                    
*                                                                               
DPL02    B     EXITOK                                                           
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
* GET ADDRESS OF A CORE RESIDENT PHASE                                *         
***********************************************************************         
         SPACE 1                                                                
GCORERES NTR1  ,                                                                
         MVC   DPSREC,PSREC        SAVE DATASPACE PHASE RECORD FOUND            
DS       USING PROGSPCD,DPSREC                                                  
*                                                                               
         MVI   C.LIACTN,LIAHIGH    SEE IF RECORD ALREADY IN CORE                
         LA    RF,PROGSPCD                                                      
         ST    RF,C.LIAREC                                                      
         GOTO1 VARREDIT,DMCB,C.LIBUFFD                                          
         CLC   PSREC(PSKEYL),DPSREC                                             
         BE    GCO02               YES IT IS                                    
*                                                                               
         MVC   PSREC(PROGSPCL),DPSREC                                           
         MVC   PSADR,=AL4(PSADRL)                                               
         MVC   PSTIME,EFFS                                                      
         XC    PSLENH,PSLENH                                                    
         XC    PSLEN,PSLEN                                                      
         XC    PSCHAIN,PSCHAIN                                                  
         MVI   C.LIACTN,LIAADD     ADD NEW RECORD TO LIST IN CORE               
         GOTO1 VARREDIT,DMCB,LIBUFFC                                            
         CLI   LIRTN,LIROK                                                      
         BE    *+6                                                              
         DC    H'0'                CANNOT ADD TO CAORE                          
*                                                                               
GCO02    CLC   PSTIME,DS.PSTIME    CORE LOADED AT SAME TIME OR SOONER           
         BE    GCO04               YES                                          
         BRAS  RE,RLDACORE         RELOAD CORERES PHASE                         
*                                                                               
GCO04    TM    PSFLAG1,CTPHSCSQ    CORERES SCREEN?                              
         BNO   GCO08               NO                                           
         XR    R0,R0                                                            
         ICM   R0,7,CMAPHS         MUST PASS ADDRESS FOR CORERES SCREEN         
         BNZ   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         ICM   RE,15,PSCHAIN       PHASE IS IN THE HOLE?                        
         BZ    *+12                NO                                           
         LA    RE,HOPHASE-HOLED(RE)                                             
         B     GCO06                                                            
         ICM   RE,15,PSADR         MOVE BACK CORERES SCREEN                     
*                                                                               
GCO06    ICM   R1,15,PSLEN                                                      
         ICM   RF,15,PSLEN                                                      
         MVCL  R0,RE                                                            
         B     EXITOK                                                           
*                                                                               
GCO08    ICM   RF,15,PSCHAIN       PHASE IS IN THE HOLE?                        
         BZ    GCO10               NO                                           
         LA    RF,HOPHASE-HOLED(RF)                                             
         STCM  RF,7,CMAPHS                                                      
         B     EXITOK                                                           
*                                                                               
GCO10    ICM   RF,15,PSADR                                                      
         STCM  RF,7,CMAPHS         RETURN ADDRESS OF CORERES PHASE              
         B     EXITOK                                                           
         DROP  DS                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO RELOAD CORERES PHASE                                     *         
* NTRY: PSREC  = CORERES   PROGSPCD ENTRY                             *         
*       DPSREC = DATASPACE PROGSPCD ENTRY                             *         
***********************************************************************         
         SPACE 1                                                                
DS       USING PROGSPCD,DPSREC                                                  
RLDACORE NTR1  ,                                                                
         BRAS  RE,SSET             SUSPEND TIMER                                
         L     R2,AHOLE            NEED A FRESH SLOT FOR THIS PHASE             
         USING HOLED,R2                                                         
         L     RF,AHOLE                                                         
         A     RF,LHOLE                                                         
         BCTR  RF,0                RF = A(END OF HOLE)                          
*                                                                               
RLC02    OC    HOKEY,HOKEY         END OF STUFF IN THE HOLE?                    
         BZ    RLC04               YES                                          
         ICM   RE,15,HPSLENH       NEXT SLOT                                    
         AHI   RE,HOLELQ                                                        
         BXLE  R2,RE,RLC02                                                      
         B     RLC99               NO FREE CORE TO LOAD PHASE                   
*                                                                               
RLC04    AHI   RF,1                                                             
         SR    RF,R2               RF = LENGTH OF HOLE REMAINING                
*                                                                               
         ICM   RE,15,DS.PSLENH     WILL THIS PHASE FIT IN HOLE?                 
         AHI   RE,HOLELQ                                                        
         CR    RF,RE                                                            
         BL    RLC99               NO - HOLE IS FULL                            
*                                                                               
         ICM   RF,15,PSCHAIN       ALREADY LOADED BEFORE?                       
         BZ    RLC06               NO                                           
OLD      USING HOLED,RF                                                         
         MVC   OLD.HOKEY,EFFS      FLAG OLD HOLE ENTRY AS FREE                  
         DROP  OLD                                                              
*                                                                               
RLC06    STCM  R2,15,PSCHAIN       FILL OUT NEW HOLE DETAILS                    
*                                                                               
         XR    R0,R0                                                            
         ICM   R0,3,PSCOUNT        INCREMENT NUMBER OF LOADS                    
         AHI   R0,1                                                             
         STCM  R0,3,PSCOUNT                                                     
*                                  UPDATE TIME OF LOAD                          
         MVC   PSTIME,DS.PSTIME    SET NEW LOAD TIME                            
         MVC   HPSTIME,DS.PSTIME                                                
*                                                                               
         MVC   HOKEY,DS.PSNAME     SET KEY AND LENGTHS IN HOLE                  
         MVC   HPSLENH,DS.PSLENH                                                
         MVC   HPSLEN,DS.PSLEN                                                  
*                                                                               
         BRAS  RE,ON31                                                          
         LAM   R0,RF,ARZERO                                                     
         LAM   RE,RE,ALET          MOVE PHASE INTO HOLE IN CORE                 
         ICM   RE,15,DS.PSADR      RE = DATASPACE                               
         ICM   RF,15,DS.PSLEN                                                   
         TM    PSFLAG1,CTPHSDMQ    DUMMY PHASE                                  
         BZ    *+6                 NO                                           
         XR    RF,RF               CLEAR PHASE TO ZEROS                         
*                                                                               
         LA    R0,HOPHASE          R0 = CORE                                    
         ICM   R1,15,HPSLENH                                                    
         SAC   512                                                              
         MVCL  R0,RE                                                            
         SAC   0                                                                
         LAM   RE,RE,ARZERO                                                     
         BRAS  RE,OFF31                                                         
*                                                                               
         MVI   C.LIACTN,LIAWRT     AND WRITE IT BACK                            
         LA    RF,PROGSPCD                                                      
         ST    RF,C.LIAREC                                                      
         GOTO1 VARREDIT,DMCB,LIBUFFC                                            
*                                                                               
RLDX     GOTO1 VCALLOV,DMCB,0,(C'D',0),0                                        
         BRAS  RE,RSET             RESTART TIMER                                
         B     EXITOK                                                           
*                                                                               
RLC99    DC    H'0'                NO FREE CORE TO LOAD PHASE                   
         DROP  DS,R2                                                            
         EJECT                                                                  
***********************************************************************         
* FIX HOLE IF YOU ARE THE ONLY TASK RUNNING                           *         
***********************************************************************         
         SPACE 1                                                                
FIXHOLE  MVI   FIXUP,NO            SEE IF HOLE NEEDS FIXING                     
         L     RF,AHOLE                                                         
         USING HOLED,RF                                                         
         OC    HOKEY,HOKEY         HOLE EMPTY?                                  
         BNZ   *+12                                                             
         MVI   CMCMD,0                                                          
         B     XMOD                                                             
         DROP  RF                                                               
*                                                                               
         BRAS  RE,SSET             SUSPEND TIMER                                
*                                                                               
         L     R2,VTCB             ARE WE THE ONLY TASK?                        
         LH    RE,0(R2)                                                         
         L     RF,2(R2)                                                         
         AHI   R2,6                                                             
         USING TCBD,R2                                                          
FHOL02   CLI   TCBSYS,0            FREE TASK?                                   
         BE    *+12                YES                                          
         CLM   R2,15,ATCB          ACTIVE TASK IS ME?                           
         BNE   *+12                NO - EXIT                                    
         BXLE  R2,RE,FHOL02                                                     
         B     FHOL04                                                           
*                                                                               
         BRAS  RE,RSET             FLAG BUSY AND RESTART TIMER                  
         MVI   CMCMD,255                                                        
         B     XMOD                                                             
         DROP  R2                                                               
*                                                                               
FHOL04   BRAS  RE,GOHOME           PUT BACK ANYTHING THAT WILL FIT              
         BRAS  RE,ON31                                                          
         L     R0,XAHOLE           COPY HOLE INTO XAHOLE                        
         L     RE,AHOLE                                                         
         L     R1,LHOLE                                                         
         L     RF,LHOLE                                                         
         MVCL  R0,RE                                                            
*                                                                               
         L     R0,AHOLE            CLEAR HOLE                                   
         L     R1,LHOLE                                                         
         XR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         L     R2,XAHOLE           NOW COPY BACK ALL STUFF STILL THERE          
XA       USING HOLED,R2            XA HOLE COPY                                 
         L     R3,AHOLE                                                         
LC       USING HOLED,R3            REGULAR HOLE COPY                            
         L     R5,LHOLE                                                         
         AR    R5,R2                                                            
         BCTR  R5,0                R5 = A(END OF XA HOLE-1)                     
*                                                                               
FHOL06   OC    XA.HOKEY,XA.HOKEY   END OF ALL IN HOLE?                          
         BZ    FHOL10              YES                                          
*                                                                               
         CLC   XA.HOKEY,EFFS       IGNORE THIS ONE?                             
         BNE   *+12                NO                                           
         MVI   FIXUP,YES                                                        
         B     FHOL08                                                           
*                                                                               
         MVC   LC.HOLED(HOLELQ),XA.HOLED                                        
         LA    R0,LC.HOPHASE                                                    
         ICM   R1,15,LC.HPSLENH                                                 
         LA    RE,XA.HOPHASE                                                    
         ICM   RF,15,XA.HPSLENH                                                 
         MVCL  R0,RE               COPY BACK THE PHASE INFO                     
         BRAS  RE,OFF31                                                         
*                                                                               
         XC    PSREC,PSREC         GET CURRENT PHASE RECORD                     
         MVC   PROGSPCD(PSKEYL),LC.HOKEY                                        
         MVC   TPSKEY,PSREC                                                     
         MVI   C.LIACTN,LIAHIGH                                                 
         LA    RF,PROGSPCD                                                      
         ST    RF,C.LIAREC                                                      
         GOTO1 VARREDIT,DMCB,LIBUFFC                                            
         CLC   TPSKEY,PROGSPCD     YOU HAD BETTER GET A MATCH                   
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         STCM  R3,15,PSCHAIN       SET NEW CHAIN ADDRESS                        
*                                                                               
         MVI   C.LIACTN,LIAWRT     AND WRITE IT BACK                            
         GOTO1 VARREDIT,DMCB,LIBUFFC                                            
*                                                                               
         BRAS  RE,ON31                                                          
         ICM   RF,15,LC.HPSLENH    NEXT SLOT IN LO CORE HOLE                    
         AHI   RF,HOLELQ                                                        
         AR    R3,RF                                                            
*                                                                               
FHOL08   ICM   R4,15,XA.HPSLENH    NEXT SLOT IN XA HOLE                         
         AHI   R4,HOLELQ                                                        
         BXLE  R2,R4,FHOL06                                                     
*                                                                               
FHOL10   BRAS  RE,OFF31                                                         
*                                                                               
         CLI   FIXUP,YES           ANY ADDRESSES CHANGED AT ALL?                
         BNE   FHOL12              NO                                           
         GOTO1 VCALLOV,DMCB,0,(C'D',0),0                                        
*                                                                               
FHOL12   BRAS  RE,RSET             RESTART TIMER                                
         MVI   CMCMD,0                                                          
         B     XMOD                                                             
         DROP  XA,LC                                                            
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO TRY TO PUT A PROGRAM IN THE HOLE BACK IN ORIGINAL SLOT   *         
***********************************************************************         
         SPACE 1                                                                
GOHOME   NTR1  ,                                                                
         L     R2,AHOLE            SEE IF HOLE NEEDS FIXING                     
         USING HOLED,R2                                                         
         L     R5,AHOLE                                                         
         A     R5,LHOLE                                                         
         BCTR  R5,0                RF = A(END OF HOLE)                          
*                                                                               
GHM02    OC    HOKEY,HOKEY         END OF STUFF IN THE HOLE?                    
         BZ    EXITOK              YES                                          
         CLC   HOKEY,EFFS                                                       
         BE    GHM04                                                            
*                                                                               
         XC    PSREC,PSREC         GET PHASE ENTRY FOR HOLE PHASE               
         MVC   PROGSPCD(PSKEYL),HOKEY                                           
         MVI   C.LIACTN,LIAHIGH                                                 
         LA    RF,PROGSPCD                                                      
         ST    RF,C.LIAREC                                                      
         GOTO1 VARREDIT,DMCB,LIBUFFC                                            
         CLC   HOKEY,PROGSPCD                                                   
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         CLC   PSADR,=AL4(PSADRL)  IS THIS A 'NEW' CORE PHASE?                  
         BE    GHM04               YES - NO SLOT TO GO TO                       
         CLC   PSLENH,HPSLEN       WILL IT FIT IN ORIGINAL SLOT?                
         BL    GHM04               NO                                           
*                                                                               
         MVI   FIXUP,YES           ENTRY CHANGED - FIX CRNDX                    
*                                                                               
         ICM   R0,15,PSADR         MOVE PHASE BACK TO ORIGNAL SLOT              
         ICM   R1,15,PSLENH                                                     
         LA    RE,HOPHASE                                                       
         ICM   RF,15,HPSLEN                                                     
         MVCL  R0,RE                                                            
*                                                                               
         MVC   PSLEN,HPSLEN        SET NEW LENGTH                               
         XC    PSCHAIN,PSCHAIN     CLEAR CHAIN INFO                             
*                                                                               
         MVI   C.LIACTN,LIAWRT     AND WRITE IT BACK                            
         GOTO1 VARREDIT,DMCB,LIBUFFC                                            
         MVC   HOKEY,EFFS          SET SLOT AS FREED                            
*                                                                               
GHM04    ICM   R4,15,HPSLENH       NEXT SLOT                                    
         AHI   R4,HOLELQ                                                        
         BXLE  R2,R4,GHM02                                                      
         XC    PSREC,PSREC                                                      
         B     EXITOK                                                           
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* BUILD SCREEN MAP FOR PHASES LOADED INTO TWA                         *         
***********************************************************************         
         SPACE 1                                                                
SCRMAP   NTR1  ,                                                                
         ICM   R3,15,AUTL          POINT TO UTL ENTRY                           
         BZ    EXITOK                                                           
         USING UTLD,R3                                                          
         OC    TSVCREQ,TSVCREQ                                                  
         BNZ   EXITOK              EXIT IF SERVICE REQUEST                      
*                                                                               
         XR    R2,R2                                                            
         ICM   R2,7,CMAPHS                                                      
         S     R2,ATWA                                                          
         BNP   EXITOK              NOT IN TWA                                   
         CHI   R2,4096                                                          
         BH    EXITOK              TOO FAR INTO TWA                             
*                                                                               
         CLI   PSOVR,FF            BASE SCREEN?                                 
         BNE   SMAP02                                                           
         MVI   TSCRNE,1            INITIALISE IF BASE SCREEN                    
         MVI   TSCRNUM,X'FF'                                                    
         STH   R2,TSCRNE+2                                                      
         XC    TSCRNUM+3(6),TSCRNUM+3                                           
         B     EXITOK                                                           
*                                                                               
SMAP02   LA    R4,TSCRNUM          R4=A(ENTRY)                                  
         LHI   R0,3                R0=MAX NUM OF ENTRYS                         
         XR    R1,R1               R1=NUM OF ENTRYS                             
*                                                                               
SMAP04   CLM   R2,3,1(R4)          SKIP ENTRYS WITH LOWER DISPS                 
         BNH   SMAP06                                                           
         CLI   0(R4),0                                                          
         BE    SMAP06                                                           
         AHI   R4,3                                                             
         AHI   R1,1                                                             
         BCT   R0,SMAP04                                                        
         B     EXITOK                                                           
*                                                                               
SMAP06   MVC   0(1,R4),PSOVR       SET OLAY NUMBER                              
         STCM  R2,3,1(R4)          SET DISPLACEMENT                             
         AHI   R4,3                                                             
         AHI   R1,1                                                             
         STC   R1,TSCRNE           SET NUMBER OF ENTRYS                         
         AHI   R0,-1                                                            
         BNP   EXITOK                                                           
*                                                                               
SMAP08   XC    0(3,R4),0(R4)       CLEAR REMAINING ENTRYS                       
         AHI   R4,3                                                             
         BCT   R0,SMAP08                                                        
         B     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
* LOAD PHASE TO A GIVEN ADDRESS FROM DATASPACE                        *         
* NTRY: CMAPHS  = LOAD TO ADDRESS                                     *         
*       PSREC   = A(PROGSPCD ENTRY)                                   *         
***********************************************************************         
         SPACE 1                                                                
LTOADD   NTR1  ,                                                                
         L     R1,ATCB                                                          
         USING TCBD,R1                                                          
         XR    R0,R0                                                            
         ICM   R0,7,CMAPHS                                                      
         C     R0,TCBPGMA          CHECK WITHIN PRGMS AREA                      
         BL    LTOA02                                                           
         C     R0,TCBPGMX                                                       
         BH    LTOA02                                                           
*                                                                               
         ICM   RF,15,PSLEN         MAKE SURE OVERLAY WILL FIT                   
         BCTR  RF,0                                                             
         AR    R0,RF                                                            
         C     R0,TCBPGMX                                                       
         BNH   *+6                                                              
         DC    H'0'                DIE IF PHASE WONT FIT                        
         DROP  R1                                                               
*                                                                               
LTOA02   BRAS  RE,ON31                                                          
         LAM   R0,RF,ARZERO                                                     
         LAM   RE,RE,ALET          MOVE IN PROGRAM TO CORE                      
         ICM   RE,15,PSADR                                                      
         ICM   RF,15,PSLEN                                                      
         XR    R0,R0                                                            
         ICM   R0,7,CMAPHS                                                      
         LR    R1,RF                                                            
         SAC   512                                                              
         MVCL  R0,RE                                                            
         SAC   0                                                                
         LAM   RE,RE,ARZERO                                                     
         B     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
* INITIALISE AND GET PARAMETER LISTS                                  *         
***********************************************************************         
         SPACE 1                                                                
         PUSH  USING                                                            
         USING DMSPACED,WORK                                                    
INIT     NTR1  ,                                                                
         L     R3,VSYSFAC0                                                      
         USING COMFACSD,R3                                                      
         MVC   APROTON,CPROTON     STORAGE PROTECT MACROS                       
         MVC   APROTOFF,CPROTOFF                                                
         MVC   ADATTIM,CDATTIM                                                  
         MVC   AHEXOUT,CHEXOUT                                                  
*                                                                               
         GOTO1 APROTOFF            DISABLE STORAGE PROTECTION                   
*                                                                               
         L     R3,VSSB                                                          
         USING SSBD,R3                                                          
         MVC   ALANGTAB,SSBALANG   SET A(LANGUAGE TABLE)                        
         MVC   ALET,SSBPGMTA                                                    
         MVC   ALETT,SSBTBLET                                                   
         MVC   PHLMAX,SSBPHMAX     SET MAX PHASE LENGTH                         
         MVC   AHOLE,SSBCRADR                                                   
         MVC   LHOLE,SSBCRLEN                                                   
*                                                                               
         ICM   R3,15,SSBTKADR      GET A(TASK)                                  
         BNZ   *+6                                                              
         DC    H'0'                                                             
         ST    R3,ATCB             R3=A(TCB ENTRY)                              
         USING TCBD,R3                                                          
         MVC   ATWA,TCBTWA                                                      
         MVC   AUTL,TCBUTL                                                      
*                                                                               
         ICM   R3,15,AUTL                                                       
         BZ    INIT02                                                           
         ST    R3,AUTL             R3=A(UTL ENTRY)                              
         USING UTLD,R3                                                          
*                                                                               
         TM    TTEST,TTESTCIL      LOADING FROM CIL?                            
         BZ    *+8                 NO                                           
         MVI   USECIL,YES                                                       
         DROP  R3                                                               
*                                                                               
INIT02   ICM   RF,15,=AL4(DPDPGMS) GET DSPACE PARAMETER LIST                    
         BRAS  RE,ASKME                                                         
*                                                                               
         BRAS  RE,ON31             GET INTO XA                                  
         LAM   R0,RF,ARZERO                                                     
         LAM   R2,R2,ALET                                                       
         ICM   R2,15,DSPTFRST                                                   
         N     R2,=XL4'0FFFFFFF'                                                
         SAC   512                                                              
         MVC   LIBUFF,0(R2)        COPY DSPACE PARAMETER LIST                   
         SAC   0                                                                
         LAM   R2,R2,ARZERO                                                     
*                                                                               
         MVC   LIALET,ALET         SET CORRECT ALET                             
         OI    LIFLAG1,LIF1ARS                                                  
         NI    LIFLAG1,255-LIF1INS SET LINKED LIST REQUIRED FOR ANY ADD         
*                                                                               
         ICM   RF,15,VPHLIST       GET LOCAL PARAMETER LIST                     
         MVC   LIBUFFC,0(RF)                                                    
         NI    C.LIFLAG1,255-(LIF1INS+LIF1ARS)                                  
         B     EXITOK              DON'T WORRY - EXIT TURNS OFF XA              
         POP   USING                                                            
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO FIX UP CRNDX IN DATASPACE                                *         
***********************************************************************         
         SPACE 1                                                                
FIXCRNDX ICM   RF,15,=AL4(DPDPGMS)                                              
         BRAS  RE,LOCKME           LOCK DATASPACE                               
         BRAS  RE,SSET             ENSURE NO TIMER POPS                         
*                                                                               
         MVI   FLAG,NO                                                          
         XC    WORK,WORK           GET CRNDX DIRECTORY RECORD FROM CORE         
CR       USING PROGSPCD,WORK                                                    
         MVC   CR.PSNAME,=XL3'000AFF'                                           
         MVI   CR.PSLANG,C'T'                                                   
         MVI   LIACTN,LIAHIGH                                                   
         LA    RF,WORK                                                          
         ST    RF,LIAREC                                                        
         GOTO1 VARREDIT,DMCB,LIBUFF                                             
         CLC   =XL3'000AFF',CR.PSNAME                                           
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
FXC02    ICM   R2,15,CR.PSADR      GET A(CRNDX)                                 
*                                                                               
FXC04    LAM   R2,R2,ARZERO        ATTACK, ATTACK                               
         LAM   R2,R2,ALET                                                       
         USING CRNDXD,R2                                                        
         SAC   512                                                              
*                                                                               
FXC06    OC    0(2,R2),0(R2)       END OF INDEX?                                
         BZ    FXC12               YES                                          
         MVC   FULL+0(2),=X'000A'                                               
         MVC   FULL+2(1),CRNDPHS                                                
         MVI   BYTE,0              CLEAR INDICATORS                             
         SAC   0                                                                
*                                                                               
         LA    R3,CRTEST           R3 WILL BE TEST LEVEL: 0-3 = 0,A,B,C         
FXC08    XC    WORK,WORK           GET CRNDX RECORD FROM DATASPACE              
         MVC   CR.PSNAME,FULL                                                   
         MVI   CR.PSLANG,C'T'                                                   
         MVC   CR.PSLVL,0(R3)                                                   
         MVI   LIACTN,LIAHIGH                                                   
         LA    RF,WORK                                                          
         ST    RF,LIAREC                                                        
         GOTO1 VARREDIT,DMCB,LIBUFF                                             
         CLC   CR.PSNAME,FULL                                                   
         BNE   FXC10                                                            
         CLC   CR.PSLVL,0(R3)                                                   
         BNE   FXC10                                                            
         OC    BYTE,1(R3)                                                       
*                                                                               
FXC10    AHI   R3,2                NEXT TEST LEVEL                              
         CLI   0(R3),X'FF'                                                      
         BNE   FXC08                                                            
*                                                                               
         LAM   R2,R2,ARZERO        MAYBE PARANOID, BUT BETTER OFF SAFE          
         LAM   R2,R2,ALET                                                       
         SAC   512                                                              
         CLC   CRNDINDS,BYTE       ANY CHANGES?                                 
         BE    *+8                 NO                                           
         MVI   FLAG,YES                                                         
         MVC   CRNDINDS,BYTE       SET NEW A,B,C INDICATORS                     
*                                                                               
         LA    R2,CRNDNTRY         NOW GO PAST ADCONS TO NEXT ENTRY             
         CLI   0(R2),X'FF'                                                      
         BE    *+12                                                             
         AHI   R2,4                                                             
         B     *-12                                                             
         AHI   R2,1                GO PAST THE X'FF'                            
         B     FXC06               AND DO THE NEXT ENTRY                        
*                                                                               
FXC12    SAC   0                   RUN AWAY, RUN WAWY                           
         LAM   R0,RF,ARZERO                                                     
         CLI   FLAG,YES            DID WE UPDATE INDEX?                         
         BNE   FXC14               NO                                           
*                                                                               
         MVC   CR.PSNAME,=XL3'000AFF'                                           
         MVI   CR.PSLANG,C'T'                                                   
         MVI   LIACTN,LIAHIGH      GET CRNDX BACK                               
         LA    RF,WORK                                                          
         ST    RF,LIAREC                                                        
         GOTO1 VARREDIT,DMCB,LIBUFF                                             
*                                                                               
         GOTO1 ADATTIM,DMCB,(X'01',CR.PSTIME),0                                 
*                                                                               
         MVI   LIACTN,LIAWRT       WRITE CRNDX WITH UPDATED TIME                
         LA    RF,WORK                                                          
         ST    RF,LIAREC                                                        
         GOTO1 VARREDIT,DMCB,LIBUFF                                             
         DROP  CR,R2                                                            
*                                                                               
FXC14    ICM   RF,15,=AL4(DPDPGMS)                                              
         BRAS  RE,FREEME           FREE DATASPACE                               
         BRAS  RE,RSET             RESET TIMER POPS                             
         B     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO REBUILD ADDRESSES USING CRNDX                            *         
***********************************************************************         
         SPACE 1                                                                
RBLDCRND BRAS  RE,SSET                                                          
         XC    WORK,WORK           GET CRNDX DIRECTORY RECORD FROM CORE         
CR       USING PROGSPCD,WORK                                                    
         MVC   CR.PSNAME,=XL3'000AFF'                                           
         MVI   CR.PSLANG,C'T'                                                   
         MVI   C.LIACTN,LIAHIGH                                                 
         LA    RF,WORK                                                          
         ST    RF,C.LIAREC                                                      
         GOTO1 VARREDIT,DMCB,LIBUFFC                                            
         CLC   =XL3'000AFF',CR.PSNAME                                           
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         ICM   R4,15,CR.PSCHAIN    PHASE IS IN THE HOLE?                        
         BZ    *+12                NO                                           
         AHI   R4,HOPHASE-HOLED                                                 
         B     BRC02                                                            
*                                                                               
         ICM   R4,15,CR.PSADR      GET A(PHASE IN CORE)                         
         USING CRNDXD,R4                                                        
BRC02    OC    0(2,R4),0(R4)       END OF INDEX?                                
         BZ    BRC10               YES                                          
*                                                                               
         LA    R5,CRNDNTRY         R5 = A(ADDRESS LIST)                         
         XC    SAVE,SAVE                                                        
         XC    WORK,WORK                                                        
         MVI   CR.PSLANG,C'T'                                                   
         MVC   CR.PSNAME(2),=X'000A'                                            
         MVC   CR.PSOVR,CRNDPHS                                                 
         MVC   FULL(3),CR.PSNAME                                                
         MVI   C.LIACTN,LIAHIGH                                                 
         LA    RF,WORK                                                          
         ST    RF,C.LIAREC                                                      
         GOTO1 VARREDIT,DMCB,LIBUFFC                                            
         CLC   CR.PSNAME,FULL      MATCH NAME AND LEVEL                         
         BNE   BRC06                                                            
         CLI   CR.PSLVL,0                                                       
         BNE   BRC06                                                            
*                                                                               
         ICM   RF,15,CR.PSCHAIN    PHASE IS IN THE HOLE?                        
         BZ    *+12                NO                                           
         AHI   RF,HOPHASE-HOLED                                                 
         B     *+8                                                              
         ICM   RF,15,CR.PSADR      GET A(PHASE IN CORE)                         
         ST    RF,SAVE             AND SAVE IT                                  
         DROP  CR                                                               
*                                                                               
BRC06    CLI   0(R5),255           TEST END OF LIST                             
         BE    BRC08                                                            
         SR    RE,RE                                                            
         ICM   RE,3,0(R5)                                                       
         L     RE,SYSFACD(RE)      RE=A(SYSTEM FACILITY LIST)                   
         SR    RF,RF                                                            
         ICM   RF,3,2(R5)          RF=DISP OF ADCON INTO LIST                   
         LA    RE,0(RE,RF)                                                      
         MVC   0(4,RE),SAVE        SET A(PHASE) IN LIST                         
         AHI   R5,4                BUMP TO NEXT LIST ENTRY                      
         B     BRC06                                                            
*                                                                               
BRC08    LA    R4,1(R5)            NEXT CRNDX ENTRY                             
         B     BRC02                                                            
*                                                                               
BRC10    BRAS  RE,RSET             RESET SYSTEM CLOCK                           
         B     EXITOK                                                           
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
* EXIT POINTS AND USEFUL ROUTINES                                     *         
***********************************************************************         
         SPACE 1                                                                
ON31     O     RE,=XL4'80000000'                                                
         BSM   0,RE                                                             
*                                                                               
OFF31    N     RE,=XL4'7FFFFFFF'                                                
         BSM   0,RE                                                             
*                                                                               
SSET     LR    R0,RE                                                            
         GOTO1 VTICTOC,DUB,C'SSET' STOP TIMER                                   
         LR    RE,R0                                                            
         BR    RE                                                               
*                                                                               
RSET     LR    R0,RE                                                            
         GOTO1 VTICTOC,DUB,C'RSET' RESTART TIMER                                
         LR    RE,R0                                                            
         BR    RE                                                               
*                                                                               
LOCKME   LR    R0,RE               LOCK TABLE                                   
         GOTO1 VLOCKSPC,DUB,(X'80',(RF)),WORK                                   
         LR    RE,R0                                                            
         BR    RE                                                               
*                                                                               
FREEME   LR    R0,RE               FREE TABLE                                   
         GOTO1 VLOCKSPC,DUB,(X'10',(RF)),WORK                                   
         LR    RE,R0                                                            
         BR    RE                                                               
*                                                                               
ASKME    LR    R0,RE               ENQUIRE ON TABLE                             
         GOTO1 VLOCKSPC,DUB,(X'20',(RF)),WORK                                   
         LR    RE,R0                                                            
         BR    RE                                                               
*                                                                               
EXITL    CLI   *,FF                                                             
         B     EXIT                                                             
*                                                                               
EXITH    CLI   *,255                                                            
         B     EXIT                                                             
*                                                                               
EXITOK   CR    RB,RB                                                            
         B     EXIT                                                             
*                                                                               
XMOD     GOTO1 APROTON             RESTORE STORAGE PROTECTION                   
         L     R1,SAVER1           RETURN UPDATED PARAMETERS                    
         MVC   0(L'IPARMS,R1),IPARMS                                            
*                                                                               
EXIT     XIT1  ,                                                                
         ORG   *-2                                                              
         BSM   0,RE                                                             
         EJECT                                                                  
***********************************************************************         
* LITERALS AND CONSTANTS                                              *         
***********************************************************************         
         SPACE 1                                                                
*                                                                               
         DC    CL16'XAHOLE**XAHOLE**'                                           
XAHOLE   DC    A(0)                                                             
*                                                                               
FF       EQU   X'FF'                                                            
YES      EQU   C'Y'                                                             
NO       EQU   C'N'                                                             
MAPLEN   EQU   1024                LENGTH OF MAP ENTRY                          
*                                                                               
EFFS     DC    16X'FF'                                                          
SPACES   DC    8C' '                                                            
*                                                                               
LVLTRN   DS    0CL4                LEVEL TRANSLATION TABLE                      
         DC    AL1(0),CL3'ABC'                                                  
*                                                                               
CRTEST   DC    X'00',X'00',C'A',X'01',C'B',X'02',C'C',X'04',X'FF'               
*                                                                               
VSYSFAC  DC    V(SYSFAC)                                                        
ARZERO   DC    16F'0'                                                           
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* WORKING STORAGE DSECT                                               *         
***********************************************************************         
         SPACE 1                                                                
WORKD    DSECT                                                                  
DUB      DS    D                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    X                                                                
BYTE1    DS    X                                                                
*                                                                               
SAVER1   DS    A                   A(CALLERS R1)                                
IPARMS   DS    0XL12               MY COPY OF INCOMING PARAMETERS               
IP1      DS    A                                                                
IP2      DS    A                                                                
IP3      DS    A                                                                
*                                                                               
DMCB     DS    6F                                                               
*                                                                               
APROTON  DS    A                   A(PROTON ROUTINE)                            
APROTOFF DS    A                   A(PROTOFF ROUTINE)                           
ADATTIM  DS    A                   A(DATTIM ROUTINE)                            
AHEXOUT  DS    A                   A(HEXOUT ROUTINE)                            
*                                                                               
ATCB     DS    A                   A(TCB)                                       
AUTL     DS    A                   A(UTL)                                       
ATWA     DS    A                   A(TWA)                                       
ALANGTAB DS    A                   A(LANGTAB - FROM SSB)                        
ALET     DS    A                   SSBPGMTA                                     
ALETT    DS    A                   SSBTBLET                                     
*                                                                               
AHOLE    DS    F                                                                
LHOLE    DS    F                                                                
SAVE     DS    F                                                                
*                                                                               
MODID    DS    CL8                                                              
*                                                                               
LIBUFF   DS    XL(LIBUFFL)         DATASPACE ARREDIT PARAMETERS                 
LIBUFFC  DS    XL(LIBUFFL)         CORE      ARREDIT PARAMETERS                 
*                                                                               
PSREC    DS    XL(PROGSPCL)        CURRENT PSREC                                
SPSREC   DS    XL(PROGSPCL)        SAVED PSREC (FROM BLDKEY)                    
DPSREC   DS    XL(PROGSPCL)        SAVED PSREC (FROM DSPACE - CORERES)          
TPSKEY   DS    XL(PSKEYL)                                                       
*                                                                               
BASEMAPA DS    A                                                                
OVLYADDR DS    A                                                                
OVLYLEN  DS    A                                                                
SVADDR   DS    A                                                                
TOTLEN   DS    F                                                                
TERMID   DS    XL2                                                              
TESTID   DS    CL4                                                              
*                                                                               
USECIL   DS    X                                                                
WANTDEL  DS    X                                                                
FIXUP    DS    X                                                                
*                                                                               
WORK     DS    CL64                                                             
*                                                                               
FLAG     DS    X                                                                
STNODE   DS    X                                                                
NDNODE   DS    X                                                                
*                                                                               
PHLMAX   DS    H                   MAXIMUM LENGTH OF A PHASE                    
PHLSWRT  DS    C                                                                
LANGCHR  DS    X                   USER LANGUAGE CHARACTER                      
LANGDEF  DS    X                   DEFAULT LANGUAGE CHARACTER                   
*                                                                               
WORKL    EQU   *-WORKD                                                          
         EJECT                                                                  
***********************************************************************         
* MAP AREA DSECTS                                                     *         
***********************************************************************         
         SPACE 1                                                                
MPHDRD   DSECT                                                                  
MHID     DS    CL4                                                              
MHLANG   DS    XL1                                                              
MHSP     DS    XL2                                                              
MHLVL    DS    XL1                                                              
         DS    XL8                                                              
MPHDRL   EQU   *-MPHDRD                                                         
*                                                                               
MPAREAD  DSECT                                                                  
MPNODE   DS    X                                                                
MPLVL    DS    X                                                                
MPOVR    DS    X                                                                
MPLANG   DS    X                                                                
MPLEN    DS    AL4                                                              
MPAREAL  EQU   *-MPAREAD                                                        
         EJECT                                                                  
***********************************************************************         
* INPUT PARAMETERS                                                    *         
***********************************************************************         
         SPACE 1                                                                
CALLOVD  DSECT                                                                  
CMOVR    DS    X                   OVERLAY                                      
CMAPHS   DS    XL3                 A(PHASE)                                     
*                                                                               
CMCMD    DS    X                   COMMAND                                      
CMCMDN   EQU   0                   NORMAL CALL                                  
CMCRD    EQU   C'R'                MAINT READ                                   
CMCWRT   EQU   C'W'                MAINT WRITE                                  
CMCLD    EQU   C'L'                FORCE LOAD                                   
CMCHOLE  EQU   C'H'                FIX HOLE                                     
CMCMDSR  EQU   C'S'                SPECIAL READ                                 
CMCCND   EQU   C'C'                FIX CRNDX IN DSPACE                          
CMCSYSF  EQU   C'D'                FIX ADDRESSES USING CRNDX IN CORE            
*                                                                               
CMNPHS   DS    AL3                 PHASE NAME X'0SPPOO'                         
*                                                                               
CMTYP    DS    X                   WRITE TYPE                                   
CMTDIR   EQU   C'D'                DIRECTORY                                    
CMTADD   EQU   C'A'                ADD                                          
CMIPHS   DS    XL1                 PHASE IND                                    
CMLPHS   DS    XL2                 PHASE LENGTH (ZERO USE OLD LENGTH)           
*                                                                               
         ORG   CALLOVD                                                          
CLADS    DS    A                   A(PSKEY FOR DATASPACE)                       
CLCMD    DS    X                   C'L'                                         
CLALL    DS    AL3                 A(PSKEY FOR LOADLIB)                         
CLMOD    DS    X                                                                
         DS    AL3                 N/D                                          
         EJECT                                                                  
***********************************************************************         
* OTHER INCLUDED DSECTS                                               *         
***********************************************************************         
         SPACE 1                                                                
*FASYSFAC                                                                       
         PRINT OFF                                                              
       ++INCLUDE FASYSFAC                                                       
         PRINT ON                                                               
*DDCOMFACS                                                                      
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
*FASSB                                                                          
         PRINT OFF                                                              
       ++INCLUDE FASSB                                                          
         PRINT ON                                                               
*FATCB                                                                          
         PRINT OFF                                                              
       ++INCLUDE FATCB                                                          
         PRINT ON                                                               
*FAUTL                                                                          
         PRINT OFF                                                              
       ++INCLUDE FAUTL                                                          
         PRINT ON                                                               
*FALANG                                                                         
         PRINT OFF                                                              
       ++INCLUDE FALANG                                                         
         PRINT ON                                                               
*FAPHLIST                                                                       
         PRINT OFF                                                              
       ++INCLUDE FAPHLIST                                                       
         PRINT ON                                                               
*FAHOLED                                                                        
         PRINT OFF                                                              
       ++INCLUDE FAHOLED                                                        
         PRINT ON                                                               
*FATSTTAB                                                                       
         PRINT OFF                                                              
       ++INCLUDE FATSTTAB                                                       
         PRINT ON                                                               
*FAVRSNTAB                                                                      
         PRINT OFF                                                              
       ++INCLUDE FAVRSNTAB                                                      
         PRINT ON                                                               
*FATABSDEQU                                                                     
         PRINT OFF                                                              
       ++INCLUDE FATABSDEQU                                                     
         PRINT ON                                                               
*FAPGMSDEQU                                                                     
         PRINT OFF                                                              
       ++INCLUDE FAPGMSDEQU                                                     
         PRINT ON                                                               
*DMSPACED                                                                       
         PRINT OFF                                                              
       ++INCLUDE DMSPACED                                                       
         PRINT ON                                                               
*DDARREDITD                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDARREDITD                                                     
         PRINT ON                                                               
*FAPROGSPCD                                                                     
         PRINT OFF                                                              
       ++INCLUDE FAPROGSPCD                                                     
         PRINT ON                                                               
*FAPGMSD                                                                        
         PRINT OFF                                                              
       ++INCLUDE FAPGMSD                                                        
         PRINT ON                                                               
*FACRNDXD                                                                       
         PRINT OFF                                                              
       ++INCLUDE FACRNDXD                                                       
         PRINT ON                                                               
*CTGENFILE                                                                      
         PRINT OFF                                                              
       ++INCLUDE CTGENFILE                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'015FACALLOVC 11/12/01'                                      
         END                                                                    
