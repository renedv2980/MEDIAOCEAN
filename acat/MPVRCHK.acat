*          DATA SET MPVRCHK    AT LEVEL 011 AS OF 01/27/86                      
*CATALP MPVRCHK                                                                 
         TITLE 'MPVRCHK - INSERT PRINT COLS, FOR VXV AND REACH REPORTS'         
         PRINT NOGEN                                                            
MPVRCHK  CSECT                                                                  
         SPACE 2                                                                
**********************************************************************          
*                                                                    *          
* MPVRCHK :- DETERMINES ABSOLUTE COLUMN POSITIONS FOR PRINTING BOTH  *          
*            DATA AND COLUMN HEADINGS.                               *          
*            COLUMN POSITIONS ARE CALCULATED FROM A SUPPLIED START   *          
*            COLUMN AND GAP BETWEEN COLUMNS.                         *          
*                                                                    *          
**********************************************************************          
*                                                                               
*        PARAM 1 - ADDRESS OF PROCESSING TABLE    BYTES 1-4                     
*                                                                               
*        PARAM 2 - PROGRAMME NUMBER               BYTE  5                       
*        PARAM 3 - DEFAULT GAP SETTING            BYTE  6                       
*        PARAM 4 - MAX PRINT WIDTH                BYTE  7                       
*   RET  PARAM 5 - END COLUMN NUMBER              BYTE  8                       
*        PARAM 6 - ADDRESS FIELD HEADER FOR ERROR BYTES 9-12                    
*        PARAM 7 - ADDRESS GENCONS STORAGE        BYTES13-16                    
*                                                                               
         SPACE 3                                                                
         NMOD1 WORKX-WORKD,MPVRCHK,CLEAR=YES                                    
         SPACE 2                                                                
         LR    R7,RC                                                            
         USING WORKD,R7                                                         
         L     RC,12(R1)           A(GENCONS STORAGE)                           
         USING GEND,RC                                                          
         L     RA,ATWA                                                          
         USING CONHEADH-64,RA                                                   
         EJECT                                                                  
***********************************************************************         
*                                                                     *         
*        CALCULATE LOCATIONS FOR UN-PAIRED DATA                       *         
*        CALCULATE LOCATIONS FOR STUB TEXTS                           *         
*                                                                     *         
***********************************************************************         
         SPACE                                                                  
         LR    R8,R1               SAVE PARMS ADDRESS                           
         USING PCMDD,R6                                                         
         SR    R2,R2                                                            
         IC    R2,5(,R8)           GET GAP REQUIRED                             
         MVC   MAXLEN,=H'20'       START WITH STUB WIDTH OF 20                  
VR005    DS    0H                                                               
         L     R6,0(R8)            A(PROCESSING TABLE)                          
         LA    R3,2                START RELATIVE COLUMN NUMBER                 
         SR    R4,R4               START RELATIVE PRINT POSITION                
VR010    DS    0H                                                               
         CLI   PCSWDTH,0           UN-PAIRED TYPE ?                             
         BNE   VR020               NO - GO TO COPE WITH IT                      
         SPACE                                                                  
         MVI   PCHPLOC,3           START COLUMN POSITION                        
         STC   R3,PCHPLNS          START ROW POSITION FOR HEAD                  
         STC   R3,PCDPLOCR         START ROW POSITION FOR DATA                  
         LH    R1,MAXLEN                                                        
         SR    R0,R0                                                            
         IC    R0,PCDWDTH          CALCULATE ..                                 
         SR    R1,R0               .. DATA ..                                   
         STC   R1,PCDPLOCC            .. LOCATION                               
*                                                                               
         SR    R0,R0                                                            
         IC    R0,PCDWDTH          CALCULATE ..                                 
         SR    R1,R1               .. WIDTH OF ..                               
         IC    R1,PCHWDTH          .. STUB WITH ..                              
         AR    R1,R0               .. DATA                                      
         LA    R1,3(R1)                                                         
         CR    R4,R1               CALCULATE LONGEST ..                         
         BNL   *+6                 .. DATA OR HEADING                           
         LR    R4,R1               .. (LONGER FOUND)                            
         LA    R3,1(R3)            UPDATE ROW LOCATION                          
         LA    R6,PCLEN(R6)        NEXT PROCTAB ENTRY                           
         CLI   PCLNTH,0            END ALREADY ???                              
         BE    VR110               YES !!!                                      
         B     VR010                                                            
VR020    DS    0H                                                               
         CH    R4,MAXLEN           DO WE NEED TO EXPAND STUB/DATA ?             
         BNH   *+12                NO - OK                                      
         STH   R4,MAXLEN           UPDATE                                       
         B     VR005               AND RECALCULATE POSITIONS                    
         LH    R4,MAXLEN                                                        
         SPACE                                                                  
         LA    R3,1                RESET COUNT TO 1                             
         ST    R6,WPCADD           SAVE START OF PAIRED ITEMS                   
         SPACE                                                                  
         MVC   WFSTSUF,PCSFX       SAVE FIRST SUFFIX NUMBER                     
VR030    DS    0H                  FIND LONGEST STUB HEADER                     
         CLC   PCSFX,WFSTSUF       FIRST SUFFIX STILL ?                         
         BNE   VR040               NO - LONGEST FOUND BY NOW                    
         STC   R4,PCSPLOCC         SET UP COLUMN NUMBER                         
         STC   R3,PCSPLOCR         AND ROW NUMBER                               
         LA    R3,1(R3)            UPDATE ROW NUMBER                            
         CLC   WMAXSWTH,PCSWDTH    FIND ..                                      
         BNL   *+10                .. THE ..                                    
         MVC   WMAXSWTH,PCSWDTH       .. LARGEST STUB                           
         SPACE                                                                  
         CLC   WMAXDWTH,PCDWDTH    FIND ..                                      
         BNL   *+10                .. THE ..                                    
         MVC   WMAXDWTH,PCDWDTH       .. LARGEST DATA                           
         SPACE                                                                  
         LA    R6,PCLEN(R6)        NEXT PROCTAB ENTRY                           
         CLI   PCLNTH,0            END OF TABLE ???                             
         BE    VR040               YES - ODD, ONLY VEHICLE IN SCHEDULE          
         B     VR030                                                            
         EJECT                                                                  
***********************************************************************         
*                                                                     *         
*        CALCULATE DATA AND HEADER LOCATIONS                          *         
*                                                                     *         
***********************************************************************         
         SPACE                                                                  
VR040    DS    0H                                                               
         L     R6,WPCADD           RESTORE START OF PAIRED ITEMS                
         ZIC   R1,WMAXSWTH         GET LARGEST STUB(OR VEHICLE) LENGTH          
         AR    R4,R1               + RELATIVE POSITION                          
*        LTR   R2,R2               ADD IN GAP ..                                
*        BZ    *+10                .. IF THERE IS ONE ..                        
*        AR    R4,R2                                                            
*        B     *+8                                                              
*        LA    R4,1(,R4)           .. ELSE ADD ONE FOR LUCK                     
         LA    R3,1                SET UP RELATIVE ROW NUMBER                   
         MVC   WSFX,PCSFX          SAVE SUFFIX NUMBER                           
         CLC   WMAXDWTH,PCHWDTH    GET LARGEST DATA(OR SHORT HEADER)..          
         BNL   *+10                .. LENGTH                                    
         MVC   WMAXDWTH,PCHWDTH                                                 
VR050    DS    0H                                                               
         ZIC   R1,WMAXDWTH         CALCULATE ..                                 
         ZIC   R0,PCHWDTH          .. THE ..                                    
         SR    R1,R0                  .. HEADER ..                              
         SRL   R1,1                                                             
         AR    R1,R4                     .. LOCATION                            
         LR    R5,R2               * ADJUSTMENTS FOR                            
         LA    R5,1(,R5)           *                                            
         SRL   R5,1                *    GAP =                                   
*        BCTR  R5,0                *   PARAMATER                                
         AR    R1,R5               *                                            
         STC   R1,PCHPLOC                                                       
         ZIC   R1,WMAXDWTH         CALCULATE ..                                 
         ZIC   R0,PCDWDTH          .. THE ..                                    
         SR    R1,R0                  .. DATA ..                                
         AR    R1,R4                     .. LOCATION                            
*        CLI   4(R8),MPREACH       *                                            
*        BE    *+6                 *  ADJUSTMENTS FOR                           
         AR    R1,R2               *    GAP =                                   
         LA    R1,1(,R1)           *                                            
         STC   R1,PCDPLOCC                                                      
         STC   R3,PCDPLOCR         DATA ROW                                     
         LA    R3,1(R3)            UPDATE ROW NUMBER                            
         LA    R6,PCLEN(R6)        NEXT PROCTAB ENTRY                           
         CLI   PCLNTH,0            END OF TABLE ?                               
         BE    VR100               YES - THIS IS THE RIGHT EXIT                 
         CLC   WSFX,PCSFX          SAME SUFFIX AS BEFORE ?                      
         BE    VR050               YES - POSITIONED BELOW PREVIOUS              
         MVC   WSFX,PCSFX                                                       
         LA    R3,1                START ROW COUNT AGAIN                        
         SR    R1,R1                                                            
         IC    R1,WMAXDWTH         UPDATE HEADER ...                            
         AR    R4,R1               .. START POSITION                            
         LTR   R2,R2               GAP SUPPLIED ?                               
         BZ    *+10                                                             
         AR    R4,R2               YES - UPDATE COUNTER                         
         B     VR050                                                            
         LA    R4,1(,R4)           NO - USE DEFAULT                             
         B     VR050                                                            
         EJECT                                                                  
***********************************************************************         
*                                                                     *         
*        RETURN WITH LINE LENGTH                                      *         
*                                                                     *         
***********************************************************************         
         SPACE                                                                  
VR100    DS    0H                                                               
         ZIC   R1,WMAXDWTH         CALCULATE TOTAL ..                           
         AR    R4,R1               .. LINE LENGTH                               
VR110    DS    0H                                                               
         STC   R4,7(R8)            PUT LENGTH IN PARAM BLOCK                    
         ZIC   R1,6(R8)            MAX LINE LENGTH                              
         CR    R1,R4               LINE TOO LONG ????                           
         BNH   TRAPERR                                                          
         SPACE                                                                  
XIT      DS    0H                                                               
         XMOD1 1                                                                
         SPACE 2                                                                
TRAPERR  DS    0H                                                               
         XC    CONHEAD,CONHEAD                                                  
         LA    R2,CONHEADH                                                      
         OI    6(R2),X'80'                                                      
         LA    R2,8(R2)                                                         
         MVC   0(40,R2),=C'ERROR   PRINT WIDTH ...   PAGE WIDTH ...'            
         CVD   R4,DUB              PRINT WIDTH                                  
         UNPK  20(3,R2),DUB                                                     
         OI    22(R2),X'F0'                                                     
         CVD   R1,DUB              PAGE WIDTH                                   
         UNPK  37(3,R2),DUB                                                     
         OI    39(R2),X'F0'                                                     
         L     R2,8(R8)            A(FIELD HEADER) TO POSN CURSOR               
         GOTO1 ERREX2                                                           
         SPACE 3                                                                
         LTORG                                                                  
         EJECT                                                                  
*THE FOLLOWING DSECTS ARE NOT PRINTED                                           
*MPPCMDD,MPEQUATES,MPRDRFFD,DDSPLWORKD                                          
         PRINT OFF                                                              
       ++INCLUDE MPPCMDD                                                        
       ++INCLUDE MPEQUATES                                                      
       ++INCLUDE MPRDRFFD                                                       
       ++INCLUDE DDSPLWORKD                                                     
         PRINT ON                                                               
         SPACE 2                                                                
*                                                                               
*        WORK AREAS                                                             
*                                                                               
WORKD    DSECT                                                                  
WPCADD   DS    F                                                                
WMAXSWTH DS    X                                                                
WMAXDWTH DS    X                                                                
WSFX     DS    XL2                                                              
WFSTSUF  DS    XL2                 SAVE FIRST SUFFIX NUMBER                     
MAXLEN   DS    H                                                                
WORKX    DS    0C                                                               
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'011MPVRCHK   01/27/86'                                      
         END                                                                    
