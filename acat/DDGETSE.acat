*          DATA SET DDGETSE    AT LEVEL 005 AS OF 04/18/01                      
*CATALP GETSE                                                                   
         TITLE 'SET SENUM FOR OFFLINE FACILITIES PROGRAMS'                      
GETSE    CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,**GETSE                                              
         USING WORKD,RC            RC=A(W/S)                                    
         LR    R2,R1               R2=A(PARM LIST)                              
         L     RE,0(R2)            RE=A(SYSTEM NUMBER)                          
         XC    0(4,R2),0(R2)                                                    
         MVC   SLET,0(RE)                                                       
         MVC   SNUM,1(RE)                                                       
*                                                                               
         LA    RE,SYSLIST          LOOK-UP SYSTEM IN LIST                       
GET2     CLI   0(RE),0             TEST E-O-L                                   
         BE    GETERR1                                                          
         CLC   SLET,0(RE)          MATCH INPUT TO TABLE                         
         BE    *+12                                                             
         LA    RE,L'SYSLIST(RE)                                                 
         B     GET2                                                             
         MVC   SNUMS,1(RE)         SET MAJOR SYSTEM VALUE                       
*                                                                               
         LA    RE,GETLIST          LOOK-UP LETTER IN LIST                       
GET3     CLI   0(RE),0             TEST E-O-L                                   
         BE    GETERR2                                                          
         CLC   SNUM,0(RE)          MATCH INPUT TO TABLE                         
         BE    *+12                                                             
         LA    RE,1(RE)                                                         
         B     GET3                                                             
         LA    RF,GETLIST                                                       
         SR    RE,RF               RE=SYSTEM FILE SET NUMBER                    
         STC   RE,SNUMB                                                         
         B     GET4                                                             
         EJECT                                                                  
* READ & PROCESS SYSTEM LIST RECORD                                             
*                                                                               
GET4     LA    R3,IOAREA                                                        
         USING CTWREC,R3           R3=A(RECORD)                                 
         XC    CTWKEY,CTWKEY                                                    
         MVI   CTWKTYP,C'W'                                                     
         MVI   CTWKREC,C'S'                                                     
         GOTO1 =V(DATAMGR),DMCB,=C'DMREAD',=C'CTFILE',CTWREC,CTWREC             
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                DIE IF CANT READ RECORD                      
*                                                                               
         LA    R3,CTWDATA          FIND SYSTEM ELEMENT ON RECORD                
         USING SYSELD,R3           R3=A(FIRST ELEMENT)                          
         SR    R0,R0                                                            
GET5     CLI   SYSEL,0             TEST E-O-R                                   
         BE    GETERR3                                                          
         CLI   SYSEL,SYSELQ                                                     
         BNE   GET6                                                             
         CLC   SYSSYS,SNUMS        MATCH SYSTEM NUMBER                          
         BNE   GET6                                                             
         CLC   SYSNUM,SNUMB        MATCH SYSTEM RELATIVE NUMBER                 
         BE    GET8                                                             
GET6     IC    R0,SYSLEN                                                        
         AR    R3,R0                                                            
         B     GET5                                                             
*                                                                               
GET8     ST    R3,0(R2)            RETURN A(SYSEL) IN P1                        
         MVC   0(1,R2),SYSSEN                                                   
         B     GETX                                                             
         EJECT                                                                  
* ERRORS & EXIT                                                                 
*                                                                               
GETERR1  MVI   1(R2),1             INVALID FILE SET                             
         B     GETX                                                             
GETERR2  MVI   1(R2),2             INVALID FILE SET                             
         B     GETX                                                             
GETERR3  MVI   1(R2),3             INVALID SYSTEM NUMBER                        
         B     GETX                                                             
         SPACE 1                                                                
GETX     CLI   0(R2),0             SET CC=EQUAL ON ERROR                        
         XIT1  ,                   EXIT FROM MODULE                             
         SPACE 1                                                                
         LTORG                                                                  
         SPACE 1                                                                
SYSLIST  DS    0XL2                                                             
*&&UK                                                                           
         DC    C'M',X'04'          MEDIA                                        
         DC    C'L',X'05'          MPL                                          
         DC    C'A',X'06'          ACCOUNT                                      
*&&                                                                             
*&&US                                                                           
         DC    C'S',X'02'          SPOT                                         
         DC    C'N',X'03'          NETWORK                                      
         DC    C'P',X'04'          PRINT                                        
         DC    C'L',X'05'          MPL                                          
         DC    C'A',X'06'          ACCOUNT                                      
         DC    C'R',X'08'          REP                                          
*&&                                                                             
SYSLISTX DC    X'00'                                                            
         SPACE 1                                                                
GETLIST  DC    C'0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ',X'00'                    
         SPACE 1                                                                
WORKD    DSECT                                                                  
DMCB     DS    6F                                                               
SLET     DS    CL1                                                              
SNUM     DS    CL1                                                              
SNUMS    DS    XL1                                                              
SNUMB    DS    XL1                                                              
IOAREA   DS    2000X                                                            
WORKX    EQU   *                                                                
         SPACE 1                                                                
* CTGENFILE/DDSYSELD                                                            
         PRINT OFF                                                              
       ++INCLUDE CTGENFILE                                                      
       ++INCLUDE DDSYSELD                                                       
         PRINT ON                                                               
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'005DDGETSE   04/18/01'                                      
         END                                                                    
