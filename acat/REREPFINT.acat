*          DATA SET REREPFINT  AT LEVEL 171 AS OF 05/01/02                      
*CATALP REFINT                                                                  
         TITLE 'REREPFINT - INITIALIZATION FOR MAIN FILE CONTROLLER'            
*                                                                               
*********************************************************************           
*                                                                   *           
*- REREPFINT -- REP REPORTER INITIALIZATION AND COMMON ROUTINES FOR *           
*                MAIN FILE CONTROLLER                               *           
*                                                                   *           
*  CATALOG DATASET REFINT                                           *           
*                                                                   *           
* ----------------------------------------------------------------- *           
*                                                                   *           
*  MOD LOG:                                                         *           
*  --------                                                         *           
*                                                                   *           
*  FEB19/97 (BU ) --- ADD CLOSE DATE TO STATION ENTRY IN SPACEND.   *           
*                     TABLE ENTRY IS EXPANDED TO 72 BYTES           *           
*                                                                   *           
*  JAN16/98 (BU ) --- EXPAND RCONREC BUFFER TO 4K                   *           
*                     REGENALLB --> REGENALLB1                      *           
*                                                                   *           
*  APR03/98 (BU ) --- NEW VERSION:  ADD MASTER/SUB RELATIONSHIP     *           
*                     FOR SALESPERSON FOR INTEREP/IR TEST 7S        *           
*                                                                   *           
*  SEP18/98 (BU ) --- UPGRADE FOR MARKET/STATION REQUIREMENTS       *           
*                                                                   *           
*  OCT07/98 (BU ) --- UPGRADE FOR OWNER/MARKET   REQUIREMENTS       *           
*                                                                   *           
*  OCT15/98 (BU ) --- REROUTE 'NO STATIONS' DUMP                    *           
*                                                                   *           
*  FEB11/99 (BU ) --- ADD OFFICE TO SALESPERSON ENTRIES             *           
*                                                                   *           
*  OCT10/01 (BU ) --- ADD BUYCODE/ATTRIBUTES TO SPACEND             *           
*                                                                   *           
*  JAN17/02 (BU ) --- ADD CONTRACT END DATE TO STATION ENTRIES      *           
*                                                                   *           
*  FEB04/02 (BU ) --- ADD INTERFACE CODE TO STATION ENTRIES         *           
*                                                                   *           
*  FEB11/02 (BU ) --- DEL INTERFACE CODE FR STATION ENTRIES         *           
*                                                                   *           
*                                                                   *           
*                     ***  END TOMBSTONE  ***                       *           
*********************************************************************           
*                                                                               
         PRINT NOGEN                                                            
REPFINT  CSECT                                                                  
         NMOD1 60,**FINT**,R9,R8,RR=R2                                          
         L     RA,=V(REWORKC)                                                   
         USING WORKD,RA                                                         
         USING MONARCHD,RC                                                      
         L     RC,AMONARCH                                                      
         L     R7,FILEC                                                         
         USING FILED,R7                                                         
         ST    R2,RELO                                                          
         SPACE 2                                                                
         L     RF,VXADDR                                                        
         USING VXADDRD,RF                                                       
         L     RE,VXDCBAPP         A(APPLMSG DCB)                               
         ST    RE,DCBAPPL          SAVE IN LOCAL STORE                          
         DROP  RF                                                               
         EJECT                                                                  
         ICM   R3,15,0(R1)                                                      
         BNZ   MAIN12                                                           
         CLI   RCSPCEND,C'F'       FORCE NEW SPACEND?                           
         BNE   MAIN10              NO  - LOOK FOR EXISTING FILE                 
MAIN02   EQU   *                                                                
         LA    R2,LDSNMSGF         YES - DON'T SAVE AT END EITHER               
         LA    RE,DSNBUFF                                                       
         LA    RF,DSNVALUF                                                      
         EX    R2,DSNEX                                                         
         L     R1,DCBAPPL                                                       
         PUT   (R1),(RE)                                                        
*******  GOTO1 LOGIO,DMCB,(=X'FF',1),((R2),DSNVALUF)                            
         B     SPAC0020                                                         
*                                                                               
MAIN10   EQU   *                                                                
         BAS   RE,SPCFILOP         TRY TO OPEN SPACEND FILE                     
         BAS   RE,SPCENDFL         USE SPACEND FILE FROM DISK                   
         BZ    FCEXT               SUCCESSFULLY LOADED FROM DISK                
         CLI   SPACFLG1,X'FF'      'NO-FILE' FLAG SET?                          
         BE    SPAC0020            YES - BUILD TABLE, NO SAVE                   
         MVI   SPACFLG1,X'02'      CAN'T LOAD - BUILD AND SAVE                  
         B     SPAC0020               NEW SPACEND                               
*                                                                               
*                                                                               
MAIN12   EQU   *                                                                
         LA    RE,RTNTBLE         TABLE OF ROUTINE ADDRESSES                    
         LA    R2,RTNTYPE         # OF ENTRIES                                  
MAIN15   ZIC   RF,0(RE)           # OF ROUTINE                                  
         CR    R3,RF              IS THIS THE ROUTINE PASSED                    
         BNE   MAIN30                                                           
         ICM   RF,15,1(RE)        CORRECT ROUTINE                               
         GOTO1 (RF)               SO GO TO IT                                   
         CLI   MODE,X'FE'         END OF REQUEST FLAG?                          
         BNE   MAIN18             NO                                            
         LA    RF,1               SET CC NOT = ZERO                             
         LTR   RF,RF                                                            
         B     XITEXIT            EXIT WITH CC NOT = ZERO                       
MAIN18   EQU   *                                                                
         B     XIT                                                              
*                                                                               
MAIN30   LA    RE,L'RTNTBLE(RE)                                                 
         BCT   R2,MAIN15                                                        
         DC    H'0'               PARAM OF ROUTINE PASSED INCORRECTLY           
*                                                                               
* TABLE WITH THE ADDRESS OF ROUTINES                                            
RTNTBLE  DS    0XL5                                                             
         DC    AL1(READEQU),AL4(READ)                                           
         DC    AL1(HIGHEQU),AL4(HIGH)                                           
         DC    AL1(SEQEQU),AL4(SEQ)                                             
         DC    AL1(WRTEQU),AL4(WRT)                                             
         DC    AL1(PATCHEQU),AL4(PATCH)                                         
         DC    AL1(GOEQU),AL4(GO)                                               
         DC    AL1(REPEQU),AL4(GETREP)                                          
         DC    AL1(REGEQU),AL4(GETREG)                                          
         DC    AL1(OFFEQU),AL4(GETOFF)                                          
         DC    AL1(STATEQU),AL4(GETSTAT)                                        
         DC    AL1(GROUPEQU),AL4(GETGROUP)                                      
         DC    AL1(ADVEQU),AL4(GETADV)                                          
         DC    AL1(PRODEQU),AL4(GETPROD)                                        
         DC    AL1(AGYEQU),AL4(GETAGY)                                          
         DC    AL1(CONTEQU),AL4(GETCONT)                                        
         DC    AL1(BUYEQU),AL4(GETBUY)                                          
         DC    AL1(TEAMEQU),AL4(GETTEAM)                                        
         DC    AL1(MANEQU),AL4(GETMAN)                                          
         DC    AL1(CATGYEQU),AL4(GETCATGY)                                      
         DC    AL1(CLASSEQU),AL4(GETCLASS)                                      
         DC    AL1(EOMEQU),AL4(GETEOM)                                          
         DC    AL1(OWNEREQU),AL4(GETOWNER)                                      
         SPACE                                                                  
RTNTYPE  EQU   (*-RTNTBLE)/5                                                    
         EJECT                                                                  
* READ ALL RECORDS                                                              
*      IF RRG RUN:                                                              
*             ADD REPCODE TO OFFICE, STATION, SALESPERSON, REGION               
*             AND CONTRACT TYPE CODES                                           
*             IF MASTER REP, LOAD ABOVE CODES FOR EACH OF THE                   
*                 SUBSIDIARY REP CODES                                          
*                                                                               
SPAC0020 EQU   *                                                                
         L     RE,ADCONLST                                                      
         USING ADCONSD,RE                                                       
         L     RF,VSPNDCTL                                                      
         DROP  RE                                                               
         L     RE,ASPACND4                                                      
         ST    RE,8(RF)            STORE A(NEXT ENTRY: SPACEND)                 
*                                                                               
*  SPNDCNTL (SPACEND CONTROL) CONTAINS 8 BYTES OF FLAGS FOLLOWED                
*    BY ROOM FOR 60 ADDRESSES BY RECORD TYPE.  EACH ADDRESS IS                  
*    THE LOCATION OF THE FIRST ENTRY OF THE TYPE IN SPACEND.  THE               
*    STARTING ADDRESS OF A TYPE MAY BE FOUND BY INSERTING THE TYPE              
*    IN A REGISTER, MULTIPLYING BY 4, AND ADDING THE RESULT TO THE              
*    SPNDCNTL ADDRESS + 8 (PAST THE FLAGS).  THE FIRST, OR ZERO                 
*    TYPE ENTRY, IS USED DURING LOADING, TO INDICATE THE NEXT                   
*    AVAILABLE ADDRESS.                                                         
*                                                                               
         SPACE 3                                                                
*     STATIONS:  NOT UNDER A MASTER REP CODE                                    
         SPACE 3                                                                
STAT0020 EQU   *                                                                
         L     RE,ADCONLST                                                      
         USING ADCONSD,RE                                                       
         L     RF,VSPNDCTL                                                      
         DROP  RE                                                               
         LA    RE,2                STATION RECORD TYPE                          
         SLA   RE,2                SET DISPLACEMENT                             
         AR    RE,RF               ADD A(SPACEND CNTL)                          
         L     RF,8(RF)            A(NEXT AVAILABLE SPACE)                      
         ST    RF,8(RE)            8 SKIPS FLAGS                                
*                                                                               
         B     STAT0040            FORCE TO LOAD REP CODE                       
*                                                                               
STAT0040 EQU   *                                                                
         LA    R4,RCREPFL                                                       
         L     RF,VXADDR                                                        
         USING VXADDRD,RF                                                       
         L     RE,VXSREP           A(MASTER/SUBSIDIARY TABLE)                   
         USING RSREPD,RE                                                        
         CLI   MASTREP,1           MASTER REP?                                  
         BNE   STAT0060            NO  - USE RUN REP ID ONLY                    
         LA    R4,SUBREPTB         SET A(1ST REP IN SUB TABLE)                  
         ST    R4,ACONSREP         SAVE ADDR IN TABLE                           
         DROP  RE,RF                                                            
         SPACE 2                                                                
STAT0060 EQU   *                                                                
         XC    KEY,KEY                                                          
         MVC   LST4,ADSV                                                        
         XC    STANUM,STANUM                                                    
         MVI   KEY,X'02'                                                        
         MVC   KEY+20(2),0(R4)                                                  
         BAS   RE,HIGH                                                          
         SPACE 3                                                                
STAT0080 CLC   KEY(22),KEYSAVE                                                  
         BNE   STAT0200                                                         
         BAS   RE,GETSTAT                                                       
         XC    WORKALT,WORKALT     ZERO OUT WORKALT AREA                        
*                                                                               
****     MVC   WORKALT(2),=X'0254'                                              
*                                  84 BYTES PER ENTRY                           
*                                  CONTRACT END DATE INFO ADDED                 
*                                  INTERFACE CODE ENTERED                       
         MVC   WORKALT(2),=X'024A'                                              
*                                  74 BYTES PER ENTRY                           
*                                  INTERFACE CODE DROPPED                       
*                                                                               
*                                                                               
* NOTE:  REP CODE IS INSERTED INTO THE TABLE AT TWO PLACES.  THE                
*        FIRST IS TO SEPARATE THE STATIONS BY REP CODE.  THE SECOND             
*        IS TO PERMIT SORTING MARKETS WITHIN REP CODE BY RERG02.                
*                                                                               
         MVC   WORKALT+02(2),0(R4) INSERT REP CODE                              
         MVC   WORKALT+4(5),RSTAKSTA                                            
         MVC   WORKALT+9(3),RSTASTRT                                            
         MVC   WORKALT+12(3),RSTAEND                                            
         MVC   WORKALT+15(2),0(R4) INSERT REP CODE                              
         MVC   WORKALT+17(20),RSTAMKT                                           
         OC    WORKALT+17(20),SPACES SET X'00' TO SPACES                        
         MVC   WORKALT+37(2),RSTAGRUP                                           
         MVC   WORKALT+39(3),RSTAAFFL STATION AFFILIATE                         
         MVC   WORKALT+42(2),=C'T ' DEFAULT NY TEAM CODE                        
         MVC   WORKALT+44(2),RSTATVB TVB REGION                                 
         MVC   WORKALT+46(3),RSTAOWN STATION OWNER                              
         MVC   WORKALT+49(1),RSTARANK STATION MARKET RANK                       
         MVC   WORKALT+50(2),=C'T ' DEFAULT CH TEAM CODE                        
         XC    WORKALT+52(2),WORKALT+52 DISP OF LAST STA IN MKT                 
         MVC   WORKALT+68(2),RSTACLDT INSERT CLOSE DATE OF STATION              
         SR    R0,R0                                                            
         LA    R1,RSTAELEM                                                      
*                                                                               
STAT0100 ICM   R0,1,1(R1)          EXTRACT L(ELEMENT)                           
         BNZ   *+6                 LENGTH MUST BE > ZERO                        
         DC    H'0'                DIE IF LENGTH = ZERO                         
         AR    R1,R0                                                            
         CLI   0(R1),0             END OF RECORD?                               
         BE    STAT0180            YES - FINISHED                               
         CLI   0(R1),8             FIND EXTRA DESCRIPTION ELEMENT               
         BNE   STAT0110                                                         
         MVC   WORKALT+54(04),RSTAMKTC-RSTAXXEL(R1) MARKET CODE                 
***      MVC   WORKALT+74(10),RSTAOSI-RSTAXXEL(R1)  INTERFACE CODE              
         B     STAT0100                                                         
STAT0110 EQU   *                                                                
         CLI   0(R1),4             FIND OFFICE/TEAM ELEMENT                     
         BNE   STAT0120                                                         
         CLC   RSTAOTOF-RSTAOTEL(2,R1),=C'NY'                                   
         BNE   STAT0160                                                         
         MVC   WORKALT+42(2),RSTAOTTM-RSTAOTEL(R1) NY TEAM CODE                 
         B     STAT0100                                                         
*                                                                               
*   FOR COMBO STATIONS:  IF STATION IS A COMBO STATION (MEDIA=C)                
*      THE STATION'S CALL LETTERS ARE INSERTED INTO THE TABLE.                  
*      IF STATION BELONGS TO A COMBO (X'0A' ELEMENT PRESENT, AND                
*      MEDIA NOT = C), THE COMBO CALL LETTERS ARE TAKEN FROM THE                
*      X'0A' ELEMENT, AND INSERTED INTO THE TABLE.                              
*                                                                               
STAT0120 EQU   *                                                                
*****>>  B     STAT0100            **REMOVE THIS TO REACTIVATE                  
         CLI   0(R1),X'0A'         COMBO ELEMENT?                               
         BNE   STAT0150            NO  - CHECK JOIN/LEAVE DATE ELT              
         CLI   RSTAKSTA+4,C'C'     YES - COMBO STATION?                         
         BNE   STAT0140            NO  - USE STATION IN X'0A' ELEMENT           
         MVC   WORKALT+58(5),RSTAKSTA YES - USE IT AS COMBO                     
         B     STAT0100            GO BACK FOR NEXT ELEMENT                     
STAT0140 MVC   WORKALT+58(5),RSTACS-RSTACSEL(R1)                                
*                                  STATION FROM X'0A' COMBO ELEMENT             
         B     STAT0100            GO BACK FOR NEXT ELEMENT                     
*                                                                               
STAT0150 EQU   *                                                                
         CLI   0(R1),X'0D'         JOIN/LEAVE REPORT DATE ELEMENT?              
         BNE   STAT0155            NO  - LOOK FOR X'10' ELEMENT                 
         MVC   WORKALT+64(2),RSTAJOIN-RSTAJLEL(R1)                              
*                                  INSERT JOIN DATE O'RIDE INTO ENTRY           
         MVC   WORKALT+66(2),RSTALEAV-RSTAJLEL(R1)                              
*                                  INSERT LEAVEDATE O'RIDE INTO ENTRY           
         B     STAT0100            GO BACK FOR NEXT ELEMENT                     
STAT0155 EQU   *                                                                
         CLI   0(R1),X'10'         CONTRACT END DATE ELEMENT?                   
         BNE   STAT0100            NO  - CHECK NEXT ELEMENT                     
*                                                                               
*   TEST PRINT 1                                                                
**       MVC   P+1(07),=C'10 ELT:'                                              
**       MVC   P+10(16),0(R1)                                                   
**       GOTO1 =V(REREPORT),RR=RELO                                             
*   TEST PRINT 1 END                                                            
*                                                                               
         MVC   WORKALT+70(2),RSTADTCE-RSTADTEL(R1)                              
*                                  INSERT CONTRACT END DATE INTO ENTRY          
         BAS   RE,STADURAT         CALC REMAINING STATION CONTRACT              
*                                                                               
         B     STAT0100            GO BACK FOR NEXT ELEMENT                     
STAT0160 CLC   RSTAOTOF-RSTAOTEL(2,R1),=C'CH'                                   
         BNE   STAT0100                                                         
         MVC   WORKALT+50(2),RSTAOTTM-RSTAOTEL(R1) CH TEAM CODE                 
         B     STAT0100                                                         
*                                                                               
STAT0180 BAS   RE,ADDINFOA                                                      
         L     RF,STATCNTR         GET STATION COUNTER                          
         LA    RF,1(RF)            ADD TO COUNTER                               
         ST    RF,STATCNTR                                                      
*                                                                               
*                                                                               
*   TEST PRINT 1                                                                
***      MVC   P+1(07),=C'STATION'                                              
***      MVC   P+10(74),WORKALT                                                 
***      GOTO1 =V(REREPORT),RR=RELO                                             
*   TEST PRINT 1 END                                                            
*                                                                               
         BAS   RE,SEQ                                                           
         B     STAT0080                                                         
         SPACE 2                                                                
STAT0200 EQU   *                                                                
         MVC   WORK+64(8),=C'**FULL**'                                          
*                                  RESET LITERAL IN WORKSPACE                   
         L     RF,VXADDR                                                        
         USING VXADDRD,RF                                                       
         L     RE,VXSREP           A(MASTER/SUBSIDIARY TABLE)                   
         USING RSREPD,RE                                                        
         CLI   MASTREP,1           MASTER REP?                                  
         BNE   STAT0220            NO  - PROCEED TO LOAD COMPETITIVE            
*                                     AFFILIATES                                
         L     R4,ACONSREP         A(CURRENT REP CODE IN TABLE)                 
         LA    R4,LSUBREP(R4)      ADD L(TABLE ENTRY)                           
         CLC   0(2,R4),=X'FFFF'    END OF TABLE REACHED?                        
         BE    OWMK0020            YES - PROCEED TO LOAD OWNER/MARKET           
         ST    R4,ACONSREP         STORE NEW A(CURRENT REP CODE)                
         B     STAT0060            NO  - PROCESS NEXT REP ID                    
         DROP  RE,RF                                                            
         EJECT                                                                  
STAT0220 EQU   *                                                                
*                                                                               
*   TEST PRINT STATION                                                          
***      MVC   P+1(09),=C'STATIONS:'                                            
***      EDIT  STATCNTR,(6,P+12)                                                
***      GOTO1 =V(REREPORT),RR=RELO                                             
*   TEST PRINT STATION                                                          
*                                                                               
*                                  RERUN THE STATION RECORDS AND BUILD          
*                                     AN AFF ELEMENT FOR TV ONLY                
         XC    KEY,KEY                                                          
         MVI   KEY,X'02'                                                        
         MVC   KEY+20(2),0(R4)                                                  
         BAS   RE,HIGH                                                          
         SPACE 3                                                                
STAT0240 CLC   KEY(22),KEYSAVE                                                  
         BNE   STAT0340                                                         
         BAS   RE,GETSTAT                                                       
         CLI   RSTAKSTA+4,C'T'     TV STATION?                                  
         BE    STAT0260            YES                                          
         CLI   RSTAKSTA+4,C' '     TV STATION?                                  
         BNE   STAT0320            NO  - SKIP THIS STATION                      
STAT0260 EQU   *                                                                
         XC    WORX(64),WORX       ZERO OUT WORX AREA                           
         MVC   WORX(2),=X'500D'                                                 
*                                  INITIAL LENGTH COMPRISED OF:                 
*                                     ELEMENT CODE:  1 BYTE                     
*                                     ELEMENT LEN :  1 BYTE                     
*                                     REP CODE:      2 BYTES                    
*                                     REP'D STN:     4 BYTES                    
*                                     REP'D STN AFF: 3 BYTES                    
*                                     DELIM:         2 BYTES X'00'              
*                                                                               
         MVC   WORX+02(2),0(R4)    INSERT REP CODE                              
         MVC   WORX+04(4),RSTAKSTA INSERT REP'D STATION                         
         MVC   WORX+08(3),RSTAAFFL INSERT REP'D STATION AFFILIATION             
         LA    R2,WORX+11          A(1ST COMP AFFIL)                            
         LA    R1,RSTAELEM                                                      
*                                                                               
STAT0280 ICM   R0,1,1(R1)                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R1,R0                                                            
         CLI   0(R1),0                                                          
         BE    STAT0300                                                         
         CLI   0(R1),2             COMPETITIVE ELEMENT?                         
         BNE   STAT0280            NO  - GO BACK FOR NEXT                       
         MVC   0(4,R2),2(R1)       YES - INSERT COMPETITIVE CALLS               
         MVC   4(3,R2),7(R1)       INSERT COMPETITIVE AFFILIATE                 
         ZIC   RF,WORX+1           BUMP ELEMENT LENGTH                          
         LA    RF,7(RF)                                                         
         STC   RF,WORX+1           PUT LENGTH BACK                              
         LA    R2,7(R2)            BUMP A(NEXT ENTRY)                           
         B     STAT0280            GO BACK FOR NEXT                             
*                                                                               
STAT0300 BAS   RE,ADDINFOX                                                      
STAT0320 EQU   *                                                                
         BAS   RE,SEQ                                                           
         B     STAT0240                                                         
         SPACE 2                                                                
STAT0340 EQU   *                                                                
         L     RF,VXADDR                                                        
         USING VXADDRD,RF                                                       
         L     RE,VXSREP           A(MASTER/SUBSIDIARY TABLE)                   
         USING RSREPD,RE                                                        
         CLI   MASTREP,1           MASTER REP?                                  
         BNE   OWMK0020            NO  - PROCEED TO LOAD OWNER/MARKET           
         L     R4,ACONSREP         A(CURRENT REP CODE IN TABLE)                 
         LA    R4,LSUBREP(R4)      ADD L(TABLE ENTRY)                           
         CLC   0(2,R4),=X'FFFF'    END OF TABLE REACHED?                        
         BE    OWMK0020            YES - PROCEED TO LOAD OWNER/MARKET           
         ST    R4,ACONSREP         STORE NEW A(CURRENT REP CODE)                
         B     STAT0220            NO  - PROCESS NEXT REP ID                    
         DROP  RE,RF                                                            
*                                                                               
WORX     DS    CL120               WORK AREA FOR COMPETITIVE AFF                
WORKALT  DS    CL120               WORK AREA FOR STATION DATA                   
*                                     NEEDS MORE THAN WORK CAN PROVIDE          
STATCNTR DS    F                                                                
         EJECT                                                                  
*                                                                               
*   STADURAT:  FOR THOSE STATIONS WITH A CONTRACT END DATE ENTERED,             
*        CALCULATE THE REMAINING NUMBER OF MONTHS, BASED ON TODAY'S             
*        DATE VS CONTRACT END DATE.                                             
*                                                                               
STADURAT NTR1                                                                   
         GOTO1 DATCON,DMCB,(5,WORK),(0,WORK)                                    
*                                  SET TODAY'S DATE                             
         MVC   WORK+32(2),WORKALT+70                                            
*                                  SET CONTRACT END DATE                        
         MVI   WORK+34,15          SET DAY TO THE 15TH                          
*                                                                               
         GOTO1 DATCON,DMCB,(3,WORK+32),(0,WORK+6)                               
*                                  SET CONTRACT END DATE                        
         CLC   WORK(6),WORK+6      TODAY EARLIER THAN CON END DATE?             
         BNL   STUR0800            NO  - LEAVE REMAINING SET TO 0               
         GOTO1 =V(PERVERT),DMCB,WORK,WORK+6                                     
         ZICM  RF,DMCB+14,2        GET NUMBER OF MONTHS INCLUSIVE               
         BCTR  RF,0                SUBTRACT ONE MONTH (BASED ON                 
*                                     TODAY'S DATE, CURRENT MONTH IS TO         
*                                     BE DISCOUNTED FOR CALCULATION)            
         STCM  RF,3,WORKALT+72                                                  
*                                  INSERT NUMBER OF MONS TILL CON END           
*                                                                               
*   TEST PRINT 1                                                                
***      MVC   P+1(07),=C'PERVERT'                                              
***      MVC   P+10(12),WORK                                                    
***      MVC   P+26(24),DMCB                                                    
***      GOTO1 =V(REREPORT),RR=RELO                                             
*   TEST PRINT 1 END                                                            
*                                                                               
STUR0800 EQU   *                                                                
                                                                                
         XIT1                                                                   
         EJECT                                                                  
*   OWNER/MARKET:  SCAN STATIONS AND SET UP OWNER/MARKET ENTRIES                
*        OWNERS AND MARKETS ARE BOTH MASTER RECORDS, SO A SINGLE                
*        ENTRY FOR EACH COMBINATION WILL SUFFICE.                               
OWMK0020 EQU   *                                                                
*                                                                               
*   TEST PRINT STATION                                                          
***      MVC   P+1(09),=C'STATIONS:'                                            
***      EDIT  STATCNTR,(6,P+12)                                                
***      GOTO1 =V(REREPORT),RR=RELO                                             
*   TEST PRINT STATION                                                          
*                                                                               
         L     R4,ASPACND4         SET A(FIRST TABLE ENTRY)                     
*                                     SHOULD BE STATION                         
         CLI   0(R4),2             STATION ENTRY?                               
         BNE   OWMK0200            NO STATIONS - SKIP                           
OWMK0040 EQU   *                                                                
         ZIC   RE,1(R4)            BUMP TO NEXT ELEMENT                         
         AR    R4,RE                                                            
         CLI   0(R4),2             STATION ENTRY?                               
         BNE   OWMK0200            NO  - FINISHED WITH STATIONS                 
OWMK0060 EQU   *                                                                
         XC    WORK,WORK           CLEAR BUILD AREA                             
         MVC   WORK(2),=X'2209'    INSERT TABLE TYPE/LEN                        
         MVC   WORK+2(3),46(R4)    INSERT OWNER FROM STATION ENTRY              
         MVC   WORK+5(4),54(R4)    INSERT MKT   FROM STATION ENTRY              
*                                                                               
*   TEST PRINT 1                                                                
*        MVC   P+1(06),=C'STA IN'                                               
*        MVC   P+8(5),4(R4)                                                     
*        MVC   P+20(09),WORK       PRINT ENTRY                                  
*        GOTO1 =V(REREPORT),RR=RELO                                             
*   TEST PRINT 1 END                                                            
*                                                                               
         OC    WORK+2(3),WORK+2    ANY ENTRY IN OWNER?                          
         BZ    OWMK0040            NO  - SKIP THIS ENTRY                        
         CLC   WORK+2(3),SPACES    ANY ENTRY IN OWNER?                          
         BE    OWMK0040            NO  - SKIP THIS ENTRY                        
         OC    WORK+5(4),WORK+5    ANY ENTRY IN MKT CODE?                       
         BZ    OWMK0040            NO  - SKIP THIS ENTRY                        
         CLC   WORK+5(4),SPACES    ANY ENTRY IN MKT CODE?                       
         BE    OWMK0040            NO  - SKIP THIS ENTRY                        
         OC    AOWNMKT,AOWNMKT     A(OWNER MKT) SET?                            
         BNZ   OWMK0080            YES - DON'T RESET                            
*                                  NO  - SET TO NEXT AVAILABLE SLOT             
         L     RE,ADCONLST         GET A(SPACEND CONTROL)                       
         USING ADCONSD,RE                                                       
         L     RF,VSPNDCTL         A(SPACEND CONTROL)                           
*                                                                               
         DROP RE                                                                
*                                                                               
         L     R2,8(RF)            GET A(NEXT AVAILABLE SLOT)                   
         ST    R2,AOWNMKT                                                       
         B     OWMK0120            INSERT FIRST ENTRY                           
OWMK0080 EQU   *                                                                
*                                                                               
*   SCAN TO SEE IF ENTRY JUST BUILT IS ALREADY IN TABLE.                        
*        YES  =  BYPASS - DON'T ADD TWICE                                       
*        NO   =  ADD TO TABLE VIA ADDINFO                                       
*                                                                               
         L     R2,AOWNMKT                                                       
OWMK0100 EQU   *                                                                
         CLI   0(R2),0             END OF TABLE?                                
         BE    OWMK0120            YES - ADD ENTRY TO TABLE                     
         CLC   0(9,R2),WORK        ENTRY IN TABLE ALREADY?                      
         BE    OWMK0040            YES - DON'T READD                            
         ZIC   RE,1(R2)            NO  - BUMP TO NEXT ENTRY                     
         AR    R2,RE                                                            
         B     OWMK0100                                                         
OWMK0120 EQU   *                                                                
         BAS   RE,ADDINFO                                                       
         B     OWMK0040            GO BACK FOR NEXT STATION                     
OWMK0200 EQU   *                                                                
*                                                                               
*   TEST PRINT                                                                  
*        MVC   P+1(05),=C'OWMK='                                                
*        L     R4,AOWNMKT          SET A(1ST OWNER/MKT ENTRY)                   
TEST0220 EQU   *                                                                
*        CLI   0(R4),0             END OF TABLE?                                
*        BE    OFFC0020            YES - FINISHED                               
*        MVC   P+20(09),0(R4)      NO  - PRINT ENTRY                            
*        GOTO1 =V(REREPORT),RR=RELO                                             
*        ZIC   RE,1(R4)            BUMP TO NEXT ENTRY                           
*        AR    R4,RE                                                            
*        B     TEST0220            GO BACK FOR NEXT                             
*   TEST PRINT END                                                              
*                                                                               
         EJECT                                                                  
*   OFFICES:  NOT UNDER A MASTER REP CODE                                       
OFFC0020 EQU   *                                                                
         L     RE,ADCONLST                                                      
         USING ADCONSD,RE                                                       
         L     RF,VSPNDCTL                                                      
         DROP  RE                                                               
         LA    RE,4                OFFICE RECORD TYPE                           
         SLA   RE,2                SET DISPLACEMENT                             
         AR    RE,RF               ADD A(SPACEND CNTL)                          
         L     RF,8(RF)            A(NEXT AVAILABLE SPACE)                      
         ST    RF,8(RE)            8 SKIPS FLAGS                                
*                                                                               
*                                                                               
*   TEST PRINT                                                                  
*        MVC   P+1(05),=C'VSPC='                                                
*        L     RE,ADCONLST                                                      
*        USING ADCONSD,RE                                                       
*        L     RF,VSPNDCTL                                                      
*        DROP  RE                                                               
*        STCM  RF,15,P+10                                                       
*        MVC   P+20(32),0(RF)      A(NEXT ENTRY: SPACEND)                       
*        GOTO1 =V(REREPORT),RR=RELO                                             
*   TEST PRINT END                                                              
*                                                                               
         B     OFFC0060            FORCE TO LOAD REP CODE                       
*                                                                               
OFFC0060 EQU   *                                                                
         LA    R4,RCREPFL                                                       
         L     RF,VXADDR                                                        
         USING VXADDRD,RF                                                       
         L     RE,VXSREP           A(MASTER/SUBSIDIARY TABLE)                   
         USING RSREPD,RE                                                        
         CLI   MASTREP,1           MASTER REP?                                  
         BNE   OFFC0080            NO  - USE RUN REP ID ONLY                    
         LA    R4,SUBREPTB         SET A(1ST REP IN SUB TABLE)                  
         ST    R4,ACONSREP         SAVE ADDR IN TABLE                           
         DROP  RE,RF                                                            
         SPACE 2                                                                
OFFC0080 EQU   *                                                                
         XC    KEY,KEY                                                          
         MVI   KEY,X'04'                                                        
         MVC   KEY+23(2),0(R4)     INSERT REP CODE                              
         BAS   RE,HIGH                                                          
         SPACE 2                                                                
OFFC0100 CLC   KEY(25),KEYSAVE                                                  
         BNE   OFFC0120                                                         
         BAS   RE,GETOFF                                                        
         MVC   WORK(2),=X'044E'                                                 
         MVC   WORK+2(2),0(R4)     INSERT REP CODE IN SPACEND                   
         MVC   WORK+4(2),ROFFREG                                                
         MVC   WORK+6(2),ROFFKOFF                                               
         MVC   WORK+8(20),ROFFNAME                                              
         MVC   WORK+28(38),ROFFADD1                                             
         MVC   WORK+66(2),ROFFSTT                                               
         MVC   WORK+68(10),ROFFZIP                                              
*                                                                               
         BAS   RE,ADDINFO                                                       
         BAS   RE,SEQ                                                           
         B     OFFC0100                                                         
         SPACE 2                                                                
OFFC0120 EQU   *                                                                
         L     RF,VXADDR                                                        
         USING VXADDRD,RF                                                       
         L     RE,VXSREP           A(MASTER/SUBSIDIARY TABLE)                   
         USING RSREPD,RE                                                        
         CLI   MASTREP,1           MASTER REP?                                  
         BNE   OFF20040            NO  - PROCEED TO LOAD STATIONS               
         L     R4,ACONSREP         A(CURRENT REP CODE IN TABLE)                 
         LA    R4,LSUBREP(R4)      ADD L(TABLE ENTRY)                           
         CLC   0(2,R4),=X'FFFF'    END OF TABLE REACHED?                        
         BE    OFF20040            YES - PROCEED TO LOAD STATIONS               
         ST    R4,ACONSREP         STORE NEW A(CURRENT REP CODE)                
         B     OFFC0080            NO  - PROCESS NEXT REP ID                    
         DROP  RE,RF                                                            
         EJECT                                                                  
*                                                                               
*   SECONDARY OFFICE RECORD SETUP                                               
*                                                                               
OFF20040 EQU   *                                                                
         L     RE,ADCONLST                                                      
         USING ADCONSD,RE                                                       
         L     RF,VSPNDCTL                                                      
         DROP  RE                                                               
         LA    RE,X'44'            OFFICE2 RECORD TYPE                          
         SLA   RE,2                SET DISPLACEMENT                             
         AR    RE,RF               ADD A(SPACEND CNTL)                          
         L     RF,8(RF)            A(NEXT AVAILABLE SPACE)                      
         ST    RF,8(RE)            8 SKIPS FLAGS                                
*                                                                               
         B     OFF20060            FORCE TO LOAD REP CODE                       
*                                                                               
OFF20060 EQU   *                                                                
         LA    R4,RCREPFL                                                       
         L     RF,VXADDR                                                        
         USING VXADDRD,RF                                                       
         L     RE,VXSREP           A(MASTER/SUBSIDIARY TABLE)                   
         USING RSREPD,RE                                                        
         CLI   MASTREP,1           MASTER REP?                                  
         BNE   OFF20080            NO  - USE RUN REP ID ONLY                    
         LA    R4,SUBREPTB         SET A(1ST REP IN SUB TABLE)                  
         ST    R4,ACONSREP         SAVE ADDR IN TABLE                           
         DROP  RE,RF                                                            
         SPACE 2                                                                
OFF20080 EQU   *                                                                
         XC    KEY,KEY                                                          
         MVI   KEY,X'44'                                                        
         MVC   KEY+23(2),0(R4)     INSERT REP CODE                              
         BAS   RE,HIGH                                                          
         SPACE 2                                                                
OFF20100 CLC   KEY(25),KEYSAVE                                                  
         BNE   OFF20120                                                         
         BAS   RE,GETOFF                                                        
         LA    RE,ROFFREC                                                       
         USING ROFF2REC,RE                                                      
         MVC   WORK(2),=X'4407'                                                 
         MVC   WORK+2(2),0(R4)     INSERT REP CODE                              
         MVC   WORK+4(2),ROFF2OFF  INSERT OFFICE CODE                           
         MVC   WORK+6(1),ROFF2PRF+1                                             
*                                  INSERT NATL/LOCAL CODE FLAG BYTE             
         DROP  RE                                                               
*                                                                               
         BAS   RE,ADDINFO                                                       
         BAS   RE,SEQ                                                           
         B     OFF20100                                                         
         SPACE 2                                                                
OFF20120 EQU   *                                                                
         L     RF,VXADDR                                                        
         USING VXADDRD,RF                                                       
         L     RE,VXSREP           A(MASTER/SUBSIDIARY TABLE)                   
         USING RSREPD,RE                                                        
         CLI   MASTREP,1           MASTER REP?                                  
         BNE   DIVT0020            NO  - PROCEED TO LOAD STATIONS               
         L     R4,ACONSREP         A(CURRENT REP CODE IN TABLE)                 
         LA    R4,LSUBREP(R4)      ADD L(TABLE ENTRY)                           
         CLC   0(2,R4),=X'FFFF'    END OF TABLE REACHED?                        
         BE    DIVT0020            YES - PROCEED TO LOAD STATIONS               
         ST    R4,ACONSREP         STORE NEW A(CURRENT REP CODE)                
         B     OFF20080            NO  - PROCESS NEXT REP ID                    
         DROP  RE,RF                                                            
         EJECT                                                                  
*              READ THE DIVISIONS/TEAMS                                         
*                                                                               
*    DIV/TEAM: MASTER REP CODE FOR CERTAIN REPS:  DVTORIDE IS                   
*        SET TO '**' FOR THESE. REPS=INTEREP,KATZ RADIO                         
*        FOR OTHER REPS, KATZ TV, ORIGINAL PROCESSING IS APPLIED.               
*                                                                               
DIVT0020 EQU   *                                                                
         L     RE,ADCONLST                                                      
         USING ADCONSD,RE                                                       
         L     RF,VSPNDCTL                                                      
         DROP  RE                                                               
         LA    RE,5                DIV/TEAM RECORD TYPE                         
         SLA   RE,2                SET DISPLACEMENT                             
         AR    RE,RF               ADD A(SPACEND CNTL)                          
         L     RF,8(RF)            A(NEXT AVAILABLE SPACE)                      
         ST    RF,8(RE)            8 SKIPS FLAGS                                
*                                                                               
         B     DIVT0040            FORCE TO LOAD REP CODE                       
*                                                                               
DVTORIDE DS   CL2                  DIV/TEAM OVERRIDE                            
*                                                                               
DIVT0040 EQU   *                                                                
         LA    R4,RCREPFL                                                       
         L     RF,VXADDR                                                        
         USING VXADDRD,RF                                                       
         L     RE,VXSREP           A(MASTER/SUBSIDIARY TABLE)                   
         USING RSREPD,RE                                                        
         XC    DVTORIDE,DVTORIDE   CLEAR DIV/TEAM OVERRIDE                      
         CLI   MASTREP,1           MASTER REP?                                  
         BNE   DIVT0080            NO  - USE RUN REP ID ONLY                    
*                                                                               
*   DIV/TEAM AS MASTER FOR INTEREP AND KATZ RADIO, NOT KATZ TV.                 
*                                                                               
         CLC   MASTRREP,=C'MR'     KATZ TV?                                     
         BE    DIVT0060            YES - DON'T SET OVERRIDE                     
*                                                                               
*        KATZ TV WILL ESTABLISH A TABLE OF EACH INDIVIDUAL                      
*        COMPANY.  ALL OTHERS WILL HAVE '**' IN REP CODE.  RERG03               
*        WILL IGNORE THE REP CODE COMPARISON IF '**' IS ENCOUNTERED.            
*                                                                               
         MVC   DVTORIDE,=C'**'     YES - INSERT OVERRIDE VALUE                  
         B     DIVT0080            TREAT AS SINGLE REP CODE                     
DIVT0060 EQU   *                                                                
         LA    R4,SUBREPTB         SET A(1ST REP IN SUB TABLE)                  
         ST    R4,ACONSREP         SAVE ADDR IN TABLE                           
         DROP  RE,RF                                                            
         SPACE 2                                                                
DIVT0080 EQU   *                                                                
         XC    KEY,KEY                                                          
         MVC   LST5,ADSV                                                        
         MVI   KEY,X'05'                                                        
         MVC   KEY+23(2),0(R4)                                                  
         BAS   RE,HIGH                                                          
         SPACE 2                                                                
DIVT0100 CLC   KEY(25),KEYSAVE                                                  
         BNE   DIVT0120                                                         
         BAS   RE,GETTEAM                                                       
         MVC   WORK(2),=X'051A'                                                 
         CLC   DVTORIDE,=C'**'     DIV/TEAM AS MASTER REP?                      
         BNE   DIVT0110            NO                                           
         MVC   WORK+2(2),DVTORIDE  YES - INSERT '**' AS REP                     
         B     DIVT0115                                                         
DIVT0110 EQU   *                                                                
         MVC   WORK+2(2),0(R4)     LOAD REP CODE TO TABLE                       
DIVT0115 EQU   *                                                                
         MVC   WORK+4(2),RTEMKTEM                                               
         MVC   WORK+6(20),RTEMDVNM                                              
         BAS   RE,ADDINFO                                                       
         BAS   RE,SEQ                                                           
         SPACE 2                                                                
         B     DIVT0100                                                         
         SPACE 2                                                                
DIVT0120 EQU   *                                                                
         CLC   DVTORIDE,=C'**'     DIV/TEAM AS MASTER REP?                      
         BE    DESP0020            YES - FINISHED                               
         L     RF,VXADDR           NO  - CHECK MASTER/SUBSIDIARY                
         USING VXADDRD,RF                                                       
         L     RE,VXSREP           A(MASTER/SUBSIDIARY TABLE)                   
         USING RSREPD,RE                                                        
         CLI   MASTREP,1           MASTER REP?                                  
         BNE   DESP0020            NO  - NEXT TABLE LOAD                        
         L     R4,ACONSREP         A(CURRENT REP CODE IN TABLE)                 
         LA    R4,LSUBREP(R4)      ADD L(TABLE ENTRY)                           
         CLC   0(2,R4),=X'FFFF'    END OF TABLE REACHED?                        
         BE    DESP0020            YES - NEXT TABLE LOAD                        
         ST    R4,ACONSREP         STORE NEW A(CURRENT REP CODE)                
         B     DIVT0080            NO  - PROCESS NEXT REP ID                    
         DROP  RE,RF                                                            
         EJECT                                                                  
*              READ THE DEVELOPMENTAL SALESPERSONS                              
*                                                                               
*    DEV S/P: MASTER REP CODE FOR CERTAIN REPS:  DVTORIDE IS                    
*        SET TO '**' FOR THESE. REPS=INTEREP,KATZ RADIO                         
*        FOR OTHER REPS, KATZ TV, ORIGINAL PROCESSING IS APPLIED.               
*                                                                               
DESP0020 EQU   *                                                                
         L     RE,ADCONLST                                                      
         USING ADCONSD,RE                                                       
         L     RF,VSPNDCTL                                                      
         DROP  RE                                                               
         LA    RE,X'3A'            DEV S/P RECORD TYPE                          
         SLA   RE,2                SET DISPLACEMENT                             
         AR    RE,RF               ADD A(SPACEND CNTL)                          
         L     RF,8(RF)            A(NEXT AVAILABLE SPACE)                      
         ST    RF,8(RE)            8 SKIPS FLAGS                                
*                                                                               
         B     DESP0040            FORCE TO LOAD REP CODE                       
*                                                                               
DSPORIDE DS   CL2                  DEV S/P OVERRIDE                             
*                                                                               
DESP0040 EQU   *                                                                
         LA    R4,RCREPFL                                                       
         L     RF,VXADDR                                                        
         USING VXADDRD,RF                                                       
         L     RE,VXSREP           A(MASTER/SUBSIDIARY TABLE)                   
         USING RSREPD,RE                                                        
         XC    DSPORIDE,DSPORIDE   CLEAR DEV S/P OVERRIDE                       
         CLI   MASTREP,1           MASTER REP?                                  
         BNE   DESP0080            NO  - USE RUN REP ID ONLY                    
*                                                                               
*   DEV S/P AS MASTER FOR INTEREP AND KATZ RADIO, NOT KATZ TV.                  
*                                                                               
         CLC   MASTRREP,=C'MR'     KATZ TV?                                     
         BE    DESP0060            YES - DON'T SET OVERRIDE                     
*                                                                               
*        KATZ TV WILL ESTABLISH A TABLE OF EACH INDIVIDUAL                      
*        COMPANY.  ALL OTHERS WILL HAVE '**' IN REP CODE.  RERG03               
*        WILL IGNORE THE REP CODE COMPARISON IF '**' IS ENCOUNTERED.            
*                                                                               
         MVC   DSPORIDE,=C'**'     YES - INSERT OVERRIDE VALUE                  
         B     DESP0080            TREAT AS SINGLE REP CODE                     
DESP0060 EQU   *                                                                
         LA    R4,SUBREPTB         SET A(1ST REP IN SUB TABLE)                  
         ST    R4,ACONSREP         SAVE ADDR IN TABLE                           
         DROP  RE,RF                                                            
         SPACE 2                                                                
DESP0080 EQU   *                                                                
         XC    KEY,KEY                                                          
         MVC   LST5,ADSV                                                        
         MVI   KEY,X'3A'                                                        
         MVC   KEY+22(2),0(R4)                                                  
         BAS   RE,HIGH                                                          
         SPACE 2                                                                
DESP0100 CLC   KEY(24),KEYSAVE                                                  
         BNE   DESP0120                                                         
         BAS   RE,GETDVSP                                                       
         MVC   WORK(2),=X'3A1B'                                                 
         CLC   DSPORIDE,=C'**'     DEV S/P AS MASTER REP?                       
         BNE   DESP0110            NO                                           
         MVC   WORK+2(2),DSPORIDE  YES - INSERT '**' AS REP                     
         B     DESP0115                                                         
DESP0110 EQU   *                                                                
         MVC   WORK+2(2),0(R4)     LOAD REP CODE TO TABLE                       
DESP0115 EQU   *                                                                
         MVC   WORK+4(3),RDSPKSAL                                               
         MVC   WORK+7(20),RDSPNAME                                              
         BAS   RE,ADDINFO                                                       
         BAS   RE,SEQ                                                           
         SPACE 2                                                                
         B     DESP0100                                                         
         SPACE 2                                                                
DESP0120 EQU   *                                                                
         CLC   DSPORIDE,=C'**'     DEV S/P AS MASTER REP?                       
         BE    DVCT0020            YES - FINISHED                               
         L     RF,VXADDR           NO  - CHECK MASTER/SUBSIDIARY                
         USING VXADDRD,RF                                                       
         L     RE,VXSREP           A(MASTER/SUBSIDIARY TABLE)                   
         USING RSREPD,RE                                                        
         CLI   MASTREP,1           MASTER REP?                                  
         BNE   DVCT0020            NO  - NEXT TABLE LOAD                        
         L     R4,ACONSREP         A(CURRENT REP CODE IN TABLE)                 
         LA    R4,LSUBREP(R4)      ADD L(TABLE ENTRY)                           
         CLC   0(2,R4),=X'FFFF'    END OF TABLE REACHED?                        
         BE    DVCT0020            YES - NEXT TABLE LOAD                        
         ST    R4,ACONSREP         STORE NEW A(CURRENT REP CODE)                
         B     DESP0080            NO  - PROCESS NEXT REP ID                    
         DROP  RE,RF                                                            
         EJECT                                                                  
*              READ THE DEVELOPMENTAL CONTRACT TYPES                            
         SPACE 3                                                                
DVCT0020 EQU   *                                                                
         L     RE,ADCONLST                                                      
         USING ADCONSD,RE                                                       
         L     RF,VSPNDCTL                                                      
         DROP  RE                                                               
         LA    RE,59               DEV CONTYPE RECORD TYPE (X'3B')              
         SLA   RE,2                SET DISPLACEMENT                             
         AR    RE,RF               ADD A(SPACEND CNTL)                          
         L     RF,8(RF)            A(NEXT AVAILABLE SPACE)                      
         ST    RF,8(RE)            8 SKIPS FLAGS                                
         XC    KEY,KEY                                                          
         MVI   KEY,X'3B'                                                        
         MVC   KEY+23(2),RCREPFL                                                
         BAS   RE,HIGH                                                          
         SPACE 2                                                                
DVCT0040 CLC   KEY(25),KEYSAVE     SAME TYPE/REP?                               
         BNE   SALE0020            NO  - DEV CONTYPE FINISHED                   
         BAS   RE,GETDVCT                                                       
         MVC   WORK(2),=X'3B18'                                                 
         MVC   WORK+2(2),RDCTKCTY                                               
         MVC   WORK+4(20),RDCTDESC                                              
         BAS   RE,ADDINFO                                                       
         BAS   RE,SEQ                                                           
         B     DVCT0040                                                         
         EJECT                                                                  
*    SALESMEN:  MASTER REP CODE FOR CERTAIN REPS:  REPORIDE IS                  
*        SET TO '**' FOR THESE. REPS=KATZ,...,....                              
*        FOR OTHER REPS, ORIGINAL PROCESSING IS APPLIED.                        
         SPACE 3                                                                
SALE0020 EQU   *                                                                
         L     RE,ADCONLST                                                      
         USING ADCONSD,RE                                                       
         L     RF,VSPNDCTL                                                      
         DROP  RE                                                               
         LA    RE,6                SALESPERSON RECORD TYPE                      
         SLA   RE,2                SET DISPLACEMENT                             
         AR    RE,RF               ADD A(SPACEND CNTL)                          
         L     RF,8(RF)            A(NEXT AVAILABLE SPACE)                      
         ST    RF,8(RE)            8 SKIPS FLAGS                                
*                                                                               
         B     SALE0040            FORCE TO LOAD REP CODE                       
*                                                                               
REPORIDE DS    CL2                 REP VALUE OVERRIDE                           
*                                                                               
SALE0040 EQU   *                                                                
         LA    R4,RCREPFL                                                       
         L     RF,VXADDR                                                        
         USING VXADDRD,RF                                                       
         L     RE,VXSREP           A(MASTER/SUBSIDIARY TABLE)                   
         USING RSREPD,RE                                                        
         XC    REPORIDE,REPORIDE   CLEAR REP OVERRIDE                           
         CLI   MASTREP,1           MASTER REP?                                  
         BNE   SALE0080            NO  - USE RUN REP ID ONLY                    
         CLC   MASTRREP,=C'MR'     KATZ TV?                                     
         BE    SALE0050            YES                                          
         CLC   MASTRREP,=C'7S'     INTEREP TEST SETUP?                          
         BE    SALE0050            YES                                          
         CLC   MASTRREP,=C'IR'     INTEREP LIVE SETUP?                          
         BE    SALE0050            YES                                          
         CLC   MASTRREP,=C'K3'     KATZ?                                        
         BNE   SALE0060            NO                                           
*                                                                               
*   WILL NEED TO ADD ADDITIONAL CODES HERE WHEN DETERMINED...                   
*                                                                               
SALE0050 EQU   *                                                                
         MVC   REPORIDE,=C'**'     YES - INSERT OVERRIDE VALUE                  
         B     SALE0080            TREAT AS SINGLE REP CODE                     
SALE0060 EQU   *                                                                
         LA    R4,SUBREPTB         SET A(1ST REP IN SUB TABLE)                  
         ST    R4,ACONSREP         SAVE ADDR IN TABLE                           
         DROP  RE,RF                                                            
         SPACE 2                                                                
SALE0080 EQU   *                                                                
         XC    KEY,KEY                                                          
         MVC   LST5,ADSV                                                        
         MVI   KEY,X'06'                                                        
         MVC   KEY+22(2),0(R4)                                                  
         BAS   RE,HIGH                                                          
         SPACE 2                                                                
SALE0100 CLC   KEY(24),KEYSAVE                                                  
         BNE   SALE0120                                                         
         BAS   RE,GETMAN                                                        
         MVC   WORK(2),=X'062B'                                                 
         CLC   REPORIDE,=C'**'     SALESPERSON AS MASTER REP?                   
         BNE   SALE0110            NO                                           
         MVC   WORK+2(2),REPORIDE  YES - INSERT '**' AS REP                     
         B     SALE0115                                                         
SALE0110 EQU   *                                                                
         MVC   WORK+2(2),0(R4)     LOAD REP CODE TO TABLE                       
SALE0115 EQU   *                                                                
         MVC   WORK+4(2),RSALTEAM                                               
         MVC   WORK+6(3),RSALKSAL                                               
         MVC   WORK+9(20),RSALNAME                                              
         MVC   WORK+29(12),RSALTEL                                              
         MVC   WORK+41(02),RSALOFF                                              
         BAS   RE,ADDINFO                                                       
         BAS   RE,SEQ                                                           
         SPACE 2                                                                
         B     SALE0100                                                         
         SPACE 2                                                                
SALE0120 EQU   *                                                                
         CLC   REPORIDE,=C'**'     SALESPERSON AS MASTER REP?                   
         BE    CATS0020            YES - FINISHED.                              
         L     RF,VXADDR           NO  - CHECK MASTER/SUBSIDIARY                
         USING VXADDRD,RF                                                       
         L     RE,VXSREP           A(MASTER/SUBSIDIARY TABLE)                   
         USING RSREPD,RE                                                        
         CLI   MASTREP,1           MASTER REP?                                  
         BNE   CATS0020            NO  - PROCEED TO LOAD CATEGORIES             
         L     R4,ACONSREP         A(CURRENT REP CODE IN TABLE)                 
         LA    R4,LSUBREP(R4)      ADD L(TABLE ENTRY)                           
         CLC   0(2,R4),=X'FFFF'    END OF TABLE REACHED?                        
         BE    CATS0020            YES - PROCEED TO LOAD CATEGORIES             
         ST    R4,ACONSREP         STORE NEW A(CURRENT REP CODE)                
         B     SALE0080            NO  - PROCESS NEXT REP ID                    
         DROP  RE,RF                                                            
         EJECT                                                                  
*              READ THE CATEGORY RECORDS                                        
         SPACE 3                                                                
CATS0020 EQU   *                                                                
         L     RE,ADCONLST                                                      
         USING ADCONSD,RE                                                       
         L     RF,VSPNDCTL                                                      
         DROP  RE                                                               
         LA    RE,15               CATEGORY RECORD TYPE                         
         SLA   RE,2                SET DISPLACEMENT                             
         AR    RE,RF               ADD A(SPACEND CNTL)                          
         L     RF,8(RF)            A(NEXT AVAILABLE SPACE)                      
         ST    RF,8(RE)            8 SKIPS FLAGS                                
         XC    KEY,KEY                                                          
         MVC   LST6,ADSV                                                        
         MVI   KEY,X'0F'                                                        
         MVC   KEY+23(2),RCREPFL                                                
         BAS   RE,HIGH                                                          
         SPACE 2                                                                
CATS0040 CLC   KEY(25),KEYSAVE                                                  
         BNE   CLSS0020                                                         
         BAS   RE,GETCATGY                                                      
         MVC   WORK(2),=X'0F24'                                                 
         MVC   WORK+2(2),RCTGKCTG                                               
         MVC   WORK+4(2),RCTGCLSS                                               
         MVC   WORK+6(30),RCTGNAME                                              
         BAS   RE,ADDINFO                                                       
*        BAS   RE,ADDCINFO                                                      
         BAS   RE,SEQ                                                           
         B     CATS0040                                                         
         EJECT                                                                  
*              READ THE CLASS RECORDS                                           
         SPACE 3                                                                
CLSS0020 EQU   *                                                                
         L     RE,ADCONLST                                                      
         USING ADCONSD,RE                                                       
         L     RF,VSPNDCTL                                                      
         DROP  RE                                                               
         LA    RE,13               CLASS RECORD TYPE                            
         SLA   RE,2                SET DISPLACEMENT                             
         AR    RE,RF               ADD A(SPACEND CNTL)                          
         L     RF,8(RF)            A(NEXT AVAILABLE SPACE)                      
         ST    RF,8(RE)            8 SKIPS FLAGS                                
         XC    KEY,KEY                                                          
         MVC   LSTF,ADSV                                                        
         MVI   KEY,X'0D'                                                        
         MVC   KEY+23(2),RCREPFL                                                
         BAS   RE,HIGH                                                          
         SPACE 2                                                                
CLSS0040 CLC   KEY(25),KEYSAVE                                                  
         BNE   GRPS0020                                                         
         BAS   RE,GETCLASS                                                      
         MVC   WORK(2),=X'0D22'                                                 
         MVC   WORK+2(2),RCLSKCLS                                               
         MVC   WORK+4(30),RCLSNAME                                              
         BAS   RE,ADDINFO                                                       
         BAS   RE,SEQ                                                           
         B     CLSS0040                                                         
         EJECT                                                                  
*              READS THE GROUP/SUB-GROUPS                          V 13         
*    GROUP/SUB: MASTER REP CODE FOR CERTAIN REPS:  GRPORIDE IS                  
*        SET TO '**' FOR THESE. REPS=INTEREP,.....                              
*        FOR OTHER REPS, ORIGINAL PROCESSING IS APPLIED.                        
         SPACE 3                                                                
GRPS0020 EQU   *                                                                
         L     RE,ADCONLST                                                      
         USING ADCONSD,RE                                                       
         L     RF,VSPNDCTL                                                      
         DROP  RE                                                               
         LA    RE,7                GROUP RECORD TYPE                            
         SLA   RE,2                SET DISPLACEMENT                             
         AR    RE,RF               ADD A(SPACEND CNTL)                          
         L     RF,8(RF)            A(NEXT AVAILABLE SPACE)                      
         ST    RF,8(RE)            8 SKIPS FLAGS                                
*                                                                               
         B     GRPS0040            FORCE TO LOAD REP CODE                       
*                                                                               
GRPORIDE DS    CL2                 GRP VALUE OVERRIDE                           
*                                                                               
GRPS0040 EQU   *                                                                
         LA    R4,RCREPFL                                                       
         L     RF,VXADDR                                                        
         USING VXADDRD,RF                                                       
         L     RE,VXSREP           A(MASTER/SUBSIDIARY TABLE)                   
         USING RSREPD,RE                                                        
         XC    GRPORIDE,GRPORIDE   CLEAR GRP OVERRIDE                           
         CLI   MASTREP,1           MASTER REP?                                  
         BNE   GRPS0080            NO  - USE RUN REP ID ONLY                    
         CLC   MASTRREP,=C'K3'     KATZ?                                        
         BE    GRPS0060            YES - DON'T SET OVERRIDE                     
*                                                                               
*   WILL NEED TO ADD ADDITIONAL CODES HERE WHEN DETERMINED...                   
*        IDEA IS THAT KATZ WILL ESTABLISH A TABLE OF EACH INDIVIDUAL            
*        COMPANY.  ALL OTHERS WILL HAVE '**' IN REP CODE.  RERG03               
*        WILL IGNORE THE REP CODE COMPARISON IF '**' IS ENCOUNTERED.            
*                                                                               
         MVC   GRPORIDE,=C'**'     YES - INSERT OVERRIDE VALUE                  
         B     GRPS0080            TREAT AS SINGLE REP CODE                     
GRPS0060 EQU   *                                                                
         LA    R4,SUBREPTB         SET A(1ST REP IN SUB TABLE)                  
         ST    R4,ACONSREP         SAVE ADDR IN TABLE                           
         DROP  RE,RF                                                            
         SPACE 2                                                                
GRPS0080 EQU   *                                                                
         XC    KEY,KEY                                                          
         MVC   LST5,ADSV                                                        
         MVI   KEY,X'07'                                                        
         MVC   KEY+23(2),0(R4)                                                  
         BAS   RE,HIGH                                                          
         SPACE 2                                                                
GRPS0100 CLC   KEY(25),KEYSAVE                                                  
         BNE   GRPS0120                                                         
         BAS   RE,GETGROUP                                                      
         MVC   WORK(2),=X'071A'                                                 
         CLC   GRPORIDE,=C'**'     GROUP/SUBGP AS MASTER REP?                   
         BNE   GRPS0110            NO                                           
         MVC   WORK+2(2),GRPORIDE  YES - INSERT '**' AS REP                     
         B     GRPS0115                                                         
GRPS0110 EQU   *                                                                
         MVC   WORK+2(2),0(R4)     LOAD REP CODE TO TABLE                       
GRPS0115 EQU   *                                                                
         MVC   WORK+4(2),RGRPKGRP                                               
         MVC   WORK+6(20),RGRPNAME                                              
         BAS   RE,ADDINFO                                                       
         BAS   RE,SEQ                                                           
         SPACE 2                                                                
         B     GRPS0100                                                         
         SPACE 2                                                                
GRPS0120 EQU   *                                                                
         CLC   GRPORIDE,=C'**'     GROUP/SUBGP AS MASTER REP?                   
         BE    REGN0020            YES - FINISHED                               
         L     RF,VXADDR           NO  - CHECK MASTER/SUBSIDIARY                
         USING VXADDRD,RF                                                       
         L     RE,VXSREP           A(MASTER/SUBSIDIARY TABLE)                   
         USING RSREPD,RE                                                        
         CLI   MASTREP,1           MASTER REP?                                  
         BNE   REGN0020            NO  - PROCEED TO LOAD CATEGORIES             
         L     R4,ACONSREP         A(CURRENT REP CODE IN TABLE)                 
         LA    R4,LSUBREP(R4)      ADD L(TABLE ENTRY)                           
         CLC   0(2,R4),=X'FFFF'    END OF TABLE REACHED?                        
         BE    REGN0020            YES - PROCEED TO LOAD CATEGORIES             
         ST    R4,ACONSREP         STORE NEW A(CURRENT REP CODE)                
         B     GRPS0080            NO  - PROCESS NEXT REP ID                    
         DROP  RE,RF                                                            
         EJECT                                                                  
*              READ THE REGIONS                                                 
         SPACE 3                                                                
REGN0020 EQU   *                                                                
         L     RE,ADCONLST                                                      
         USING ADCONSD,RE                                                       
         L     RF,VSPNDCTL                                                      
         DROP  RE                                                               
         LA    RE,3                REGION RECORD TYPE                           
         SLA   RE,2                SET DISPLACEMENT                             
         AR    RE,RF               ADD A(SPACEND CNTL)                          
         L     RF,8(RF)            A(NEXT AVAILABLE SPACE)                      
         ST    RF,8(RE)            8 SKIPS FLAGS                                
*                                                                               
         B     REGN0040            FORCE TO LOAD REP CODE                       
*                                                                               
REGN0040 EQU   *                                                                
         LA    R4,RCREPFL                                                       
         L     RF,VXADDR                                                        
         USING VXADDRD,RF                                                       
         L     RE,VXSREP           A(MASTER/SUBSIDIARY TABLE)                   
         USING RSREPD,RE                                                        
         CLI   MASTREP,1           MASTER REP?                                  
         BNE   REGN0060            NO  - USE RUN REP ID ONLY                    
         LA    R4,SUBREPTB         SET A(1ST REP IN SUB TABLE)                  
         ST    R4,ACONSREP         SAVE ADDR IN TABLE                           
         DROP  RE,RF                                                            
         SPACE 2                                                                
REGN0060 XC    KEY,KEY                                                          
         MVC   LST7,ADSV                                                        
         MVI   KEY,X'03'                                                        
         MVC   KEY+23(2),0(R4)     LOAD REP CODE                                
         BAS   RE,HIGH                                                          
         SPACE 3                                                                
REGN0080 CLC   KEY(25),KEYSAVE                                                  
         BNE   REGN0100                                                         
         BAS   RE,GETREG                                                        
         MVC   WORK(2),=X'0322'                                                 
         MVC   WORK+2(2),0(R4)     LOAD REP CODE                                
         MVC   WORK+4(2),RREGKREG                                               
         MVC   WORK+6(20),RREGNAME                                              
*                                                                               
*   REGION HQ FIELDS ARE NO LONGER ACTIVE                                       
*                                                                               
**       MVC   WORK+26(6),RREGTHQ  TV REGIONAL HQ                               
**       MVC   WORK+32(2),RREGTHID TV REGIONAL HQ ID                            
         BAS   RE,ADDINFO                                                       
         MVC   LST3,ADSV                                                        
         BAS   RE,SEQ                                                           
         B     REGN0080                                                         
         SPACE 2                                                                
REGN0100 EQU   *                                                                
         L     RF,VXADDR                                                        
         USING VXADDRD,RF                                                       
         L     RE,VXSREP           A(MASTER/SUBSIDIARY TABLE)                   
         USING RSREPD,RE                                                        
         CLI   MASTREP,1           MASTER REP?                                  
         BNE   CTYP0020            NO  - PROCEED TO LOAD CONTYPES               
         L     R4,ACONSREP         A(CURRENT REP CODE IN TABLE)                 
         LA    R4,LSUBREP(R4)      ADD L(TABLE ENTRY)                           
         CLC   0(2,R4),=X'FFFF'    END OF TABLE REACHED?                        
         BE    CTYP0020            YES - PROCEED TO LOAD CONTYPES               
         ST    R4,ACONSREP         STORE NEW A(CURRENT REP CODE)                
         B     REGN0060            NO  - PROCESS NEXT REP ID                    
         DROP  RE,RF                                                            
         EJECT                                                                  
*   CONTRACT TYPES:  NOT UNDER A MASTER REP CODE                                
         SPACE 3                                                                
CTYP0020 EQU   *                                                                
         L     RE,ADCONLST                                                      
         USING ADCONSD,RE                                                       
         L     RF,VSPNDCTL                                                      
         DROP  RE                                                               
         LA    RE,50               CONTRACT TYPE RECORD TYPE                    
         SLA   RE,2                SET DISPLACEMENT                             
         AR    RE,RF               ADD A(SPACEND CNTL)                          
         L     RF,8(RF)            A(NEXT AVAILABLE SPACE)                      
         ST    RF,8(RE)            8 SKIPS FLAGS                                
*                                                                               
         B     CTYP0040            FORCE TO LOAD REP CODE                       
*                                                                               
CTYP0040 EQU   *                                                                
         LA    R4,RCREPFL                                                       
         L     RF,VXADDR                                                        
         USING VXADDRD,RF                                                       
         L     RE,VXSREP           A(MASTER/SUBSIDIARY TABLE)                   
         USING RSREPD,RE                                                        
         CLI   MASTREP,1           MASTER REP?                                  
         BNE   CTYP0060            NO  - USE RUN REP ID ONLY                    
         LA    R4,SUBREPTB         SET A(1ST REP IN SUB TABLE)                  
         ST    R4,ACONSREP         SAVE ADDR IN TABLE                           
         DROP  RE,RF                                                            
         SPACE 2                                                                
CTYP0060 XC    RCTYKEY(L'KEY),RCTYKEY                                           
         MVI   RCTYKTYP,RCTYKTYQ                                                
         MVC   RCTYKREP,0(R4)      LOAD REP CODE                                
         MVC   KEY,RCTYKEY                                                      
         BAS   RE,HIGH                                                          
         SPACE 3                                                                
CTYP0080 CLC   KEY(RCTYKCTY-RCTYKEY),KEYSAVE                                    
         BNE   CTYP0100                                                         
         BAS   RE,GETCONTY                                                      
         MVC   WORK(2),=X'321A'                                                 
         MVC   WORK+2(2),0(R4)     LOAD REP CODE TO TABLE                       
         MVC   WORK+4(1),RCTYKCTY                                               
         MVI   WORK+5,C' '                                                      
         MVC   WORK+6(20),RCTYDESC                                              
         BAS   RE,ADDINFO                                                       
         BAS   RE,SEQ                                                           
         B     CTYP0080                                                         
         SPACE 2                                                                
CTYP0100 EQU   *                                                                
         L     RF,VXADDR                                                        
         USING VXADDRD,RF                                                       
         L     RE,VXSREP           A(MASTER/SUBSIDIARY TABLE)                   
         USING RSREPD,RE                                                        
         CLI   MASTREP,1           MASTER REP?                                  
         BNE   PPER0020            NO  - PROCEED TO LOAD PT PERSONS             
         L     R4,ACONSREP         A(CURRENT REP CODE IN TABLE)                 
         LA    R4,LSUBREP(R4)      ADD L(TABLE ENTRY)                           
         CLC   0(2,R4),=X'FFFF'    END OF TABLE REACHED?                        
         BE    PPER0020            YES - PROCEED TO LOAD PT PERSONS             
         ST    R4,ACONSREP         STORE NEW A(CURRENT REP CODE)                
         B     CTYP0060            NO  - PROCESS NEXT REP ID                    
         DROP  RE,RF                                                            
         EJECT                                                                  
*              READ THE POINT PERSON RECORDS                                    
         SPACE 3                                                                
PPER0020 EQU   *                                                                
         L     RE,ADCONLST                                                      
         USING ADCONSD,RE                                                       
         L     RF,VSPNDCTL                                                      
         DROP  RE                                                               
         LA    RE,49               POINT PERSON RECORD TYPE                     
         SLA   RE,2                SET DISPLACEMENT                             
         AR    RE,RF               ADD A(SPACEND CNTL)                          
         L     RF,8(RF)            A(NEXT AVAILABLE SPACE)                      
         ST    RF,8(RE)            8 SKIPS FLAGS                                
         XC    RPTPKEY(L'KEY),RPTPKEY                                           
         MVI   RPTPKTYP,RPTPKTYQ                                                
         MVC   RPTPKREP,RCREPFL                                                 
         MVC   KEY,RPTPKEY                                                      
         BAS   RE,HIGH                                                          
         SPACE 3                                                                
PPER0040 CLC   KEY(RPTPKREC-RPTPKEY),KEYSAVE                                    
         BNE   MRKT0020                                                         
         BAS   RE,GETPTP                                                        
         MVC   WORK(2),=X'311B'                                                 
         MVC   WORK+2(3),RPTPKREC                                               
         MVC   WORK+5(20),RPTPNAME                                              
         MVC   WORK+25(2),RPTPREP                                               
         BAS   RE,ADDINFO                                                       
         BAS   RE,SEQ                                                           
         B     PPER0040                                                         
         EJECT                                                                  
*              READ THE MARKET RECORDS                                          
         SPACE 3                                                                
MRKT0020 EQU   *                                                                
         L     RE,ADCONLST                                                      
         USING ADCONSD,RE                                                       
         L     RF,VSPNDCTL                                                      
         DROP  RE                                                               
         LA    RE,43               MARKET RECORD TYPE                           
         SLA   RE,2                SET DISPLACEMENT                             
         AR    RE,RF               ADD A(SPACEND CNTL)                          
         L     RF,8(RF)            A(NEXT AVAILABLE SPACE)                      
         ST    RF,8(RE)            8 SKIPS FLAGS                                
         XC    RMKTKEY(L'KEY),RMKTKEY                                           
         MVI   RMKTKTYP,X'2B'                                                   
         MVC   RMKTKREP,RCREPFL                                                 
         MVC   KEY,RMKTKEY                                                      
         BAS   RE,HIGH                                                          
         SPACE 3                                                                
MRKT0040 CLC   KEY(RMKTKMKT-RMKTKEY),KEYSAVE                                    
         BNE   BYCD0020                                                         
         BAS   RE,GETMKT                                                        
         MVC   WORK(2),=X'2B1A'                                                 
         MVC   WORK+2(4),RMKTKMKT                                               
         MVC   WORK+6(20),RMKTNAME                                              
         BAS   RE,ADDINFO                                                       
         BAS   RE,SEQ                                                           
         B     MRKT0040                                                         
         SPACE 2                                                                
***>>>BUYCODE/ATTRIBUTE                                                         
*                                                                               
*   BUYCODES/ATTRIBUTES ARE REP-SPECIFIC.  REQUESTS AT MASTER LEVEL             
*        ARE PROHIBITED WITHIN THE 'REQUEST' PROGRAM.                           
*                                                                               
BYCD0020 EQU   *                                                                
         L     RE,ADCONLST                                                      
         USING ADCONSD,RE                                                       
         L     RF,VSPNDCTL                                                      
         DROP  RE                                                               
         LA    RE,75               BUYLINE CODE RECORD TYPE (X'4B')             
         SLA   RE,2                SET DISPLACEMENT                             
         AR    RE,RF               ADD A(SPACEND CNTL)                          
         L     RF,8(RF)            A(NEXT AVAILABLE SPACE)                      
         ST    RF,8(RE)            8 SKIPS FLAGS                                
         XC    KEY,KEY                                                          
         MVI   KEY,X'4B'                                                        
         MVC   KEY+21(2),RCREPFL                                                
         BAS   RE,HIGH                                                          
         SPACE 2                                                                
BYCD0040 CLC   KEY(23),KEYSAVE     SAME TYPE/REP?                               
         BNE   NEXTTABL            NO  - BUYCODE/ATTRIBUTES FINISHED            
         BAS   RE,GETBYCD                                                       
         MVC   WORK(2),=X'4B1A'                                                 
         MVC   WORK+2(1),RSPBKTCD  INSERT ATTR/CODE INDICATOR                   
         MVC   WORK+3(3),RSPBKCOD  INSERT ACTUAL CODE                           
         MVC   WORK+6(20),RSPBDESC INSERT DESCRIPTION                           
         BAS   RE,ADDINFO                                                       
         BAS   RE,SEQ                                                           
         B     BYCD0040                                                         
         EJECT                                                                  
***>>>BUYCODE/ATTRIBUTE                                                         
NEXTTABL EQU   *                                                                
*        ADD NEXT TABLE ENTRY HERE                                              
*                                                                               
FCEXT    EQU   *                                                                
         CLI   RCSPCEND,C'F'       FORCE SPACEND REBUILD?                       
         BE    FCEXT099            YES - NOTHING TO SAVE                        
         BAS   RE,SPCENDOP         CHECK IF SPACEND CAN BE SAVED                
FCEXT099 EQU   *                                                                
         LA    R1,0                SET CONDITION CODE FOR REFILMST              
         LTR   R1,R1                                                            
         XMOD1 1                                                                
         EJECT                                                                  
*                                                                               
*  ROUTINE TO OPEN SPACEND FILE FOR REP.  IF CONTROL CARD = FORCE,              
*    OR NO CONTROL CARD PRESENT, FILE OPEN WILL BE SKIPPED.  IF                 
*    CONTROL CARD = USE, FILE OPEN WILL BE DONE.  IF FILE COULD                 
*    NOT BE OPENED, RUN WILL CONTINUE AS IF REQUEST WAS FORCE.                  
*                                                                               
*                                                                               
*   DCB                                                                         
*                                                                               
SPCFIL   DCB   DDNAME=SPCFIL,DSORG=PS,RECFM=U,                         X        
               BLKSIZE=9500,MACRF=(GM,PM),EODAD=SPND0093                        
*                                                                               
*    NOTE:  THE EODAD OF THE DCB IS ONLY APPLICABLE TO THE FIRST                
*        ATTEMPT TO READ THE FILE.  IF THE FILE IS EMPTY, THE END               
*        OF DATA ROUTINE WILL TURN THE FILE AROUND FOR OUTPUT.  IN              
*        ANY OTHER EVENT, THE EODAD WILL NEVER BE USED, BECAUSE THERE           
*        IS A LOGICAL END-OF-DATA BUILT INTO THE RECORD STRUCTURE.              
*                                                                               
SPCFILOP NTR1                                                                   
         LA    R2,LDSNMSG7         SET L(ENQ MESSAGE)                           
         LA    RE,DSNBUFF                                                       
         LA    RF,DSNVALU7                                                      
         EX    R2,DSNEX                                                         
         L     R1,DCBAPPL                                                       
         PUT   (R1),(RE)                                                        
*******  GOTO1 LOGIO,DMCB,(=X'FF',1),((R2),DSNVALU7)                            
         GOTO1 =V(DATAMGR),DMCB,=C'ENQDEQ',(C'E',=C'WRKR')                      
         LA    R2,LDSNMSG9         SET L(ENQ MESSAGE)                           
         LA    RE,DSNBUFF                                                       
         LA    RF,DSNVALU9                                                      
         EX    R2,DSNEX                                                         
         L     R1,DCBAPPL                                                       
         PUT   (R1),(RE)                                                        
******   GOTO1 LOGIO,DMCB,(=X'FF',1),((R2),DSNVALU9)                            
         LA    RF,SPCFIL           LOAD AND SAVE A(DCB)                         
         ST    RF,ASPENDCB                                                      
         MVI   SPACFLG1,X'01'      SET 'FILE NOT FOUND' FLAG                    
         MVC   DSNUSER(2),RCREPFL  INSERT USER INTO DSN VALUE                   
         GOTO1 =V(DYNALLOC),DMCB,(X'FF',DDVALUE),DSNVALUE                       
*                                                                               
         OPEN  (SPCFIL,(INPUT))                                                 
         LTR   RF,RF               TEST RESULT OF OPEN                          
         BNZ   SPFL0099            NOT ZERO = NO FILE OPEN                      
         XC    SPACFLG1,SPACFLG1   ZERO = FILE OPEN                             
SPFL0099 EQU   *                                                                
         XIT1                                                                   
         EJECT                                                                  
*                                                                               
*   ROUTINE TO RETRIEVE SPACEND FROM THE DISK FILE OPENED IN RUNMAST.           
*     IF FILE HAS BEEN OPENED, AND DATE AND IDENTIFICATION AGREE,               
*     FILE IS LOADED FROM DISK.  IF FILE CANNOT BE LOADED, A NEW                
*     SPACEND WILL BE BUILT, AND THEN SAVED TO DISK.                            
*   NEW STRUCTURE OF SPACEND FILE:                                              
*     FIRST BLOCK WILL CONTAIN INFORMATION IN FIRST 64 BYTES:                   
*        BYTES 1-6   -  DATE OF FILE:  YYMMDD (EBCDIC)                          
*        BYTES 7-19  -  FILE IDENT: IE:  REP.SPCEND.XX                          
*                                   WHERE XX = REP USER ID                      
*     IF FIRST WORD OF ANY BLOCK IS X'FFFFFFFF', FILE AT EOF                    
*                                                                               
SPCENDFL NTR1                                                                   
         MVC   DSNVAREP(2),RCREPFL INSERT USER ID INTO FILE NAME                
         MVC   DSNVARE3(2),RCREPFL INSERT USER ID INTO FILE NAME                
         MVC   DSNVARE4(2),RCREPFL INSERT USER ID INTO FILE NAME                
         MVC   DSNVARE5(2),RCREPFL INSERT USER ID INTO FILE NAME                
         MVC   DSNVARE6(2),RCREPFL INSERT USER ID INTO FILE NAME                
*                                                                               
         SR    RF,RF               ANY FILE OPEN?                               
         IC    RF,SPACFLG1                                                      
         BNZ   SPND0098            NO  - EXIT CC NOT ZERO                       
         L     R3,ASPACEND         A(START OF WORK AREA)                        
         LA    R3,4(R3)            POINT PAST 'REMAINING SPACE' WORD            
         L     R2,ASPENDCB         A(SPCFIL DCB)                                
         GET   (R2),(3)            RETURN 1ST 9500-CHAR BLOCK                   
*                                                                               
*   NOTE:  IF FILE IS EMPTY, THE EOD ADDRESS SPECIFIED IN THE DCB               
*        IS BRANCHED TO WITH NO APPARENT TRAVERSING OF INTERMEDIATE             
*        CODE.  THIS PROGRAM IS SET TO USE THE EOD ADDRESS AS A ONE-            
*        TIME SWITCH TO SET UP THE FILE INITIALLY.                              
*                                                                               
*  FOR TEST PURPOSES, A REQUEST NAME OF 'NOFILE' WILL FORCE A                   
*    FILE RECREATION                                                            
*                                                                               
         CLC   QUESTOR(6),=C'NOFILE'                                            
         BE    SPND0093                                                         
*                                                                               
*   FOR TESTING PURPOSES:  A REQUESTOR OF 'DIFFNAME' WILL FORCE A               
*     'NOT SAME' CONDITION AND A FILE RECREATION                                
*                                                                               
         CLC   QUESTOR(8),=C'DIFFNAME'                                          
         BE    SPND0093                                                         
         CLC   DSNVALUE(14),6(R3)  CHECK USER ID VS DISK FILE                   
         BNE   SPND0093            NOT SAME - REBUILD IT                        
*                                                                               
*   FOR TESTING PURPOSES:  A REQUESTOR OF 'DIFFDATE' WILL FORCE A               
*     'NOT SAME' CONDITION AND A FILE RECREATION                                
*                                                                               
         CLC   QUESTOR(8),=C'DIFFDATE'                                          
         BE    SPND0093                                                         
         GOTO1 DATCON,DMCB,(5,WORK),(0,WORK)                                    
         CLC   WORK(6),0(R3)       CHECK FOR TODAY'S DATE                       
         BNE   SPND0093            NOT TODAY - REBUILD IT                       
SPND0004 EQU   *                                                                
         CLC   0(4,R3),=X'FFFFFFFF' EOF?                                        
         BE    SPND0090            YES - SET CC = ZERO (OKAY)                   
         A     R3,=F'9500'         A(INPUT AREA)                                
         GET   (R2),(3)            GET NEXT 9500-CHAR RECORD                    
         B     SPND0004                                                         
SPND0090 EQU   *                                                                
         LA    R2,LDSNMSG3         SET L(SUCCESSFUL LOAD MSG)                   
         LA    RE,DSNBUFF                                                       
         LA    RF,DSNVALU3                                                      
         EX    R2,DSNEX                                                         
         L     R1,DCBAPPL                                                       
         PUT   (R1),(RE)                                                        
*******  GOTO1 LOGIO,DMCB,(=X'FF',1),((R2),DSNVALU3)                            
         SR    RF,RF               SET CC = ZERO                                
         B     SPND0099                                                         
SPND0093 EQU   *                                                                
         LA    R2,LDSNMSG4         SET L(DIFF DATES/NAME MSG)                   
         LA    RE,DSNBUFF                                                       
         LA    RF,DSNVALU4                                                      
         EX    R2,DSNEX                                                         
         L     R1,DCBAPPL                                                       
         PUT   (R1),(RE)                                                        
*******  GOTO1 LOGIO,DMCB,(=X'FF',1),((R2),DSNVALU4)                            
         L     R2,ASPENDCB         A(SPCFIL DCB)                                
         CLOSE ((R2),DISP)         CLOSE WITH DISP FROM DD                      
*                                                                               
*  ATTEMPT TO REALLOCATE:  FILE ALLOCATION DD IS GONE AT CLOSE                  
*                                                                               
         GOTO1 =V(DYNALLOC),DMCB,(X'FF',DDVALUE),DSNVALUE                       
*                                                                               
SPND0096 EQU   *                                                                
         MVI   SPACFLG1,X'02'      SET 'REWRITE SPACEND' FLAG                   
         L     R2,ASPENDCB         A(SPCFIL DCB)                                
         OPEN  ((R2),OUTPUT)       REOPEN FOR OUTPUT                            
*                                                                               
*  FOR TEST PURPOSES, A REQUEST NAME OF 'NOFILE' WILL FORCE THE                 
*    CONDITION WHERE THE FILE OPEN AS OUTPUT HAS FAILED                         
*                                                                               
         CLC   QUESTOR(6),=C'NOFILE'                                            
         BE    SPND0097                                                         
         LTR   RF,RF               TEST RESULT OF OPEN                          
         BZ    SPND0098            NOT ZERO = NO FILE OPEN                      
SPND0097 EQU   *                                                                
         LA    R2,LDSNMSG5         SET L(DIFF DATES/NAME MSG)                   
         LA    RE,DSNBUFF                                                       
         LA    RF,DSNVALU5                                                      
         EX    R2,DSNEX                                                         
         L     R1,DCBAPPL                                                       
         PUT   (R1),(RE)                                                        
*******  GOTO1 LOGIO,DMCB,(=X'FF',1),((R2),DSNVALU5)                            
         MVI   SPACFLG1,X'FF'      TURN ON 'NO-FILE' FLAG                       
SPND0098 EQU   *                                                                
         LTR   RB,RB               SET CC NOT = ZERO                            
SPND0099 EQU   *                                                                
         XIT1                      EXIT                                         
         EJECT                                                                  
*                                                                               
*   ROUTINE TO WRITE SPACEND TO DISK.  FILE MUST BE ENQUEUED AND                
*     DEQUEUED.  AFTER THE FINAL DATA BLOCK IS WRITTEN, USING THE               
*     ADDRESS OF THE 'NEXT' AVAILABLE BLOCK (1ST WORD OF SPACEND),              
*     A BLOCK IN WHICH THE FIRST WORD IS X'FFFFFFFF' IS OUTPUT, TO              
*     SERVE AS A FILE SENTINEL/EOF MARKER.                                      
*                                                                               
SPCENDOP NTR1                                                                   
         CLI   SPACFLG1,X'02'      REWRITE SPACEND?                             
         BNE   SPOP0098            NO  - EXIT                                   
         GOTO1 DATCON,DMCB,(5,WORK),(0,WORK)                                    
         L     R3,ASPACEND         A(START OF WORK AREA)                        
         LA    R3,4(R3)            POINT PAST 'REMAINING SPACE' WORD            
         MVC   0(6,R3),WORK        LOAD 'TODAYS DATE' TO CONTROL                
         MVC   6(14,R3),DSNVALU2   LOAD FILE NAME TO CONTROL                    
         L     R2,ASPENDCB         A(SPCFIL DCB)                                
SPOP0004 EQU   *                                                                
         OC    0(32,R3),0(R3)      EMPTY BLOCK?                                 
         BNZ   SPOP0008            NO                                           
         MVC   0(4,R3),=X'FFFFFFFF' YES - SET EOF FLAG                          
SPOP0008 EQU   *                                                                
         PUT   (R2),(R3)           WRITE BLOCK OF 9500 TO DISK                  
         CLC   0(4,R3),=X'FFFFFFFF' EOF BLOCK?                                  
         BE    SPOP0090            YES                                          
         A     R3,=F'9500'         SET FOR NEXT BLOCK                           
         B     SPOP0004                                                         
SPOP0090 EQU   *                                                                
         LA    R2,LDSNMSG          SET L(UPDATE FILE MESSAGE)                   
         LA    RE,DSNBUFF                                                       
         LA    RF,DSNVALU2                                                      
         EX    R2,DSNEX                                                         
         L     R1,DCBAPPL                                                       
         PUT   (R1),(RE)                                                        
******** GOTO1 LOGIO,DMCB,(=X'FF',1),((R2),DSNVALU2)                            
         B     SPOP0099                                                         
SPOP0098 EQU   *                                                                
         LA    R2,LDSNMSG6         SET L(NO REWRITE MESSAGE)                    
         LA    RE,DSNBUFF                                                       
         LA    RF,DSNVALU6                                                      
         EX    R2,DSNEX                                                         
         L     R1,DCBAPPL                                                       
         PUT   (R1),(RE)                                                        
*******  GOTO1 LOGIO,DMCB,(=X'FF',1),((R2),DSNVALU6)                            
SPOP0099 EQU   *                                                                
         L     R2,ASPENDCB         A(SPCFIL DCB)                                
         CLOSE ((R2),DISP)         CLOSE FILE                                   
         LA    R2,LDSNMSG8         SET L(DEQ MESSAGE)                           
         LA    RE,DSNBUFF                                                       
         LA    RF,DSNVALU8                                                      
         EX    R2,DSNEX                                                         
         L     R1,DCBAPPL                                                       
         PUT   (R1),(RE)                                                        
*******  GOTO1 LOGIO,DMCB,(=X'FF',1),((R2),DSNVALU8)                            
         GOTO1 =V(DATAMGR),DMCB,=C'ENQDEQ',(C'D',=C'WRKR')                      
         LA    R2,LDSNMSGA         SET L(DEQ MESSAGE)                           
         LA    RE,DSNBUFF                                                       
         LA    RF,DSNVALUA                                                      
         EX    R2,DSNEX                                                         
         L     R1,DCBAPPL                                                       
         PUT   (R1),(RE)                                                        
******   GOTO1 LOGIO,DMCB,(=X'FF',1),((R2),DSNVALUA)                            
         XIT1                                                                   
         EJECT                                                                  
*              ADD INFORMATION INTO POOL                                        
         SPACE 3                                                                
ADDINFO  NTR1                                                                   
         L     RF,ASPACEND         A(REMAINING SPACEND ROOM)                    
         L     R2,0(RF)            (RF) -> REMAINING SPACEND ROOM               
         SR    R3,R3                                                            
         IC    R3,WORK+1                                                        
         CR    R3,R2                                                            
         BNH   *+6                                                              
         DC    H'0'                NOT ENOUGH ROOM IN SPACEND                   
*                          (INCREASE SIZE OF SPACEND IN REREPWORKC)             
         SR    R2,R3               REMAINING - SIZE OF NEW ENTRY                
         ST    R2,0(RF)            SAVE NEW REMAINING                           
         LR    R6,R3               SAVE LENGTH                                  
         BCTR  R3,R0               -1 FROM LENGTH FOR MOVE                      
*                                                                               
         L     RE,ADCONLST         GET A(SPACEND CONTROL)                       
         USING ADCONSD,RE                                                       
         L     RF,VSPNDCTL         A(SPACEND CONTROL)                           
*                                                                               
         DROP RE                                                                
*                                                                               
         L     R2,8(RF)            GET A(NEXT AVAILABLE SLOT)                   
         EX    R3,ADDINFO6         MOVE DATA TO NEXT SLOT                       
         ST    R2,ADSV                                                          
         AR    R2,R6               BUMP TO A(NEXT AVAILABLE SLOT)               
         ST    R2,8(RF)            SAVE IT IN SPACEND CNTL                      
         B     XIT                                                              
         SPACE 2                                                                
ADDINFO6 MVC   0(0,R2),WORK                                                     
         SPACE                                                                  
*                                                                               
*   STATION ELEMENTS EXCEEDED 64 BYTES AVAILABLE IN 'WORK'.  WORKALT            
*        IS 120 CHARS LONG.                                                     
*                                                                               
ADDINFOA NTR1                                                                   
         L     RF,ASPACEND         A(REMAINING SPACEND ROOM)                    
         L     R2,0(RF)            (RF) -> REMAINING SPACEND ROOM               
         SR    R3,R3                                                            
         IC    R3,WORKALT+1                                                     
         CR    R3,R2                                                            
         BNH   *+6                                                              
         DC    H'0'                NOT ENOUGH ROOM IN SPACEND                   
*                          (INCREASE SIZE OF SPACEND IN REREPWORKC)             
         SR    R2,R3               REMAINING - SIZE OF NEW ENTRY                
         ST    R2,0(RF)            SAVE NEW REMAINING                           
         LR    R6,R3               SAVE LENGTH                                  
         BCTR  R3,R0               -1 FROM LENGTH FOR MOVE                      
*                                                                               
         L     RE,ADCONLST         GET A(SPACEND CONTROL)                       
         USING ADCONSD,RE                                                       
         L     RF,VSPNDCTL         A(SPACEND CONTROL)                           
*                                                                               
         DROP RE                                                                
*                                                                               
         L     R2,8(RF)            GET A(NEXT AVAILABLE SLOT)                   
         EX    R3,ADDINFA6         MOVE DATA TO NEXT SLOT                       
         ST    R2,ADSV                                                          
         AR    R2,R6               BUMP TO A(NEXT AVAILABLE SLOT)               
         ST    R2,8(RF)            SAVE IT IN SPACEND CNTL                      
         B     XIT                                                              
         SPACE 2                                                                
ADDINFA6 MVC   0(0,R2),WORKALT                                                  
         SPACE                                                                  
*              ADD INFORMATION INTO POOL FOR COMP AFFILIATES                    
         SPACE 3                                                                
ADDINFOX NTR1                                                                   
         L     RF,ASPACEND         A(REMAINING SPACEND ROOM)                    
         L     R2,0(RF)            (RF) -> REMAINING SPACEND ROOM               
         SR    R3,R3                                                            
         IC    R3,WORX+1                                                        
         CR    R3,R2                                                            
         BNH   *+6                                                              
         DC    H'0'                NOT ENOUGH ROOM IN SPACEND                   
*                          (INCREASE SIZE OF SPACEND IN REREPWORKC)             
         SR    R2,R3               REMAINING - SIZE OF NEW ENTRY                
         ST    R2,0(RF)            SAVE NEW REMAINING                           
         LR    R6,R3               SAVE LENGTH                                  
         BCTR  R3,R0               -1 FROM LENGTH FOR MOVE                      
*                                                                               
         L     RE,ADCONLST         GET A(SPACEND CONTROL)                       
         USING ADCONSD,RE                                                       
         L     RF,VSPNDCTL         A(SPACEND CONTROL)                           
*                                                                               
         DROP RE                                                                
*                                                                               
         L     R2,8(RF)            GET A(NEXT AVAILABLE SLOT)                   
         EX    R3,ADDX0006         MOVE DATA TO NEXT SLOT                       
         ST    R2,ADSV                                                          
         AR    R2,R6               BUMP TO A(NEXT AVAILABLE SLOT)               
         ST    R2,8(RF)            SAVE IT IN SPACEND CNTL                      
         B     XIT                                                              
         SPACE 2                                                                
ADDX0006 MVC   0(0,R2),WORX                                                     
         SPACE                                                                  
XIT      LA    R1,0               SET CONDITION CODE FOR REFILMST               
         LTR   R1,R1                                                            
XITEXIT  EQU   *                                                                
         XIT1                                                                   
         EJECT                                                                  
*                   ROUTINE TO GO TO APPLICATION PROGRAMS                       
         SPACE 2                                                                
GO       NTR1                                                                   
         CLI   RCTRACE,C'Y'                                                     
         BNE   GO2                                                              
         MVC   P,SPACES                                                         
         MVC   P(10),=C'MODE=X''  '''                                           
         MVI   SKIPSPEC,C'Y'                                                    
         GOTO1 HEXOUT,DMCB,MODE,P+7,1,=C'N'                                     
         BAS   RE,GETMODE                                                       
         GOTO1 =V(REREPORT),RR=RELO                                             
         SPACE 2                                                                
GO2      EQU   *                                                                
         GOTO1 APPLIC,DMCB,WORKC                                                
         CLI   MODE,X'FE'          END OF REQUEST FLAG?                         
         BNE   GO3                 NO                                           
         LA    RF,1                SET CC NOT = ZERO                            
         LTR   RF,RF                                                            
         B     XITEXIT             EXIT WITH CC NOT = ZERO                      
*                                                                               
GO3      EQU   *                                                                
         CLI   RCWRITE,C'Y'        CHECK IF WRITE = NO OPTION IS ON             
         BNE   XIT                                                              
         CLI   MODE,WRITEREC       DOES USER WANT TO WRIT                       
         BNE   XIT                 BACK THE PROCESSED RECORD                    
         BAS   RE,WRITBACK                                                      
         B     XIT                                                              
         SPACE 2                                                                
GETMODE  SR    R2,R2               TRANSLATE MODE FROM                          
         IC    R2,MODE             TABLE ON NEXT PAGE                           
         BCTR  R2,R0                                                            
         SLL   R2,3                                                             
         LA    R2,MODETAB(R2)                                                   
         MVC   P+20(8),0(R2)                                                    
         BR    RE                                                               
         EJECT                                                                  
PATCH    NTR1                                                                   
         SPACE 1                                                                
         GOTO1 =V(HEXIN),DMCB,QPROG+6,FULL+1,6     DISPLACEMENT                 
         SPACE 1                                                                
         OC    DMCB+12(4),DMCB                                                  
         BZ    PATCH20             ERROR                                        
         MVI   FULL,0                                                           
         L     R4,APPLIC                                                        
         A     R4,FULL                                                          
*                                                                               
         LA    R0,29               BCT                                          
         SR    R5,R5                                                            
PATCH2   EQU   *                                                                
         LA    R6,QPROG+13(R5)                                                  
         CLI   0(R6),C' '                                                       
         BE    PATCH4                                                           
         LA    R5,2(R5)                                                         
         BCT   R0,PATCH2                                                        
PATCH4   EQU   *                                                                
         LTR   R5,R5                                                            
         BZ    PATCH20                                                          
         GOTO1 =V(HEXIN),DMCB,QPROG+13,(R4),(R5)                                
         SPACE 1                                                                
         OC    DMCB+12(4),DMCB+12                                               
         BZ    PATCH20                                                          
         MVI   DMCB,0                                                           
         B     PATCHX                                                           
PATCH20  EQU   *                                                                
         MVC   P,SPACES                                                         
         MVC   P(80),QPROG                                                      
         MVC   P+85(11),=C'PATCH ERROR'                                         
         MVI   SKIPSPEC,C'Y'                                                    
         GOTO1 =V(REREPORT),RR=RELO                                             
         SPACE 1                                                                
         MVI   DMCB,X'FF'                                                       
PATCHX   EQU   *                                                                
         B     XIT                                                              
         EJECT                                                                  
*              TABLE OF MODE VALUES                                             
         SPACE 3                                                                
MODETAB  DC    CL8'RUNFRST'                                                     
         DC    CL8'REPFRST'                                                     
         DC    CL8'REQFRST'                                                     
         DC    CL8'REGFRST'                                                     
         DC    CL8'OFFFRST'                                                     
         DC    CL8'STAFRST'                                                     
         DC    CL8'AGYFRST'                                                     
         DC    CL8'ADVFRST'                                                     
         DC    CL8'PRODFRST'                                                    
         DC    CL8'DIVFRST'                                                     
         DC    CL8'TEAMFRST'                                                    
         DC    CL8'MANFRST'                                                     
         DC    CL8'CONFRST'                                                     
         DC    CL8'SUBFRST'                                                     
         DC    CL8'GRUPFRST'                                                    
         DC    CL8'SPARE 16'                                                    
         DC    CL8'RUNLAST'                                                     
         DC    CL8'REPLAST'                                                     
         DC    CL8'REQLAST'                                                     
         DC    CL8'REGLAST'                                                     
         DC    CL8'OFFLAST'                                                     
         DC    CL8'STALAST'                                                     
         DC    CL8'AGYLAST'                                                     
         DC    CL8'ADVLAST'                                                     
         DC    CL8'PRODLAST'                                                    
         DC    CL8'DIVLAST'                                                     
         DC    CL8'TEAMLAST'                                                    
         DC    CL8'MANLAST'                                                     
         DC    CL8'CONTLAST'                                                    
         DC    CL8'SUBLAST'                                                     
         DC    CL8'GRUPLAST'                                                    
         DC    CL8'SPARE 32'                                                    
         DC    CL8'PROCADV'                                                     
         DC    CL8'PROCPROD'                                                    
         DC    CL8'PROCAGY'                                                     
         DC    CL8'PROCMAN'                                                     
         DC    CL8'PROCSTAT'                                                    
         DC    CL8'PROCCONT'                                                    
         DC    CL8'PROCBUY'                                                     
         DC    CL8'PROCSORT'                                                    
         DC    CL8'SPARE 41'                                                    
         DC    CL8'DISKERR'                                                     
         DC    CL8'IGNORERR'                                                    
         DC    CL8'WRITEREC'                                                    
         EJECT                                                                  
*              DATA MANAGER INTERFACE (DIRECTORY)                               
         SPACE 3                                                                
READ     LA    RF,DMREAD                                                        
         B     LINKDIR                                                          
         SPACE 2                                                                
HIGH     LA    RF,DMRDHI                                                        
         MVC   FILTKEY,KEY                                                      
         MVC   KEYSAVE,KEY                                                      
         B     LINKDIR                                                          
         SPACE 2                                                                
SEQ      LA    RF,DMRSEQ                                                        
         MVC   KEYSAVE,KEY                                                      
         B     LINKDIR                                                          
         SPACE 2                                                                
WRT      LA    RF,DMWRT                                                         
         B     LINKDIR                                                          
         SPACE 2                                                                
LINKDIR  NTR1  WORK=(R2,4)                                                      
         ST    RF,DMCB                                                          
         MVC   TRACEKEY,KEY                                                     
         MVC   DMCB(1),DMINBTS                                                  
         MVC   0(32,R2),KEY        INPUT KEY                                    
         ST    R2,DMCB+8                                                        
         GOTO1 DATAMGR,DMCB,,REPDIR,,KEY,0                                      
         B     DMCHECK                                                          
         EJECT                                                                  
*              DATA MANAGER INTERFACE (FILE GETS)                               
         SPACE 3                                                                
GETREP   LA    RF,RREPREC                                                       
         B     LINKFILE                                                         
         SPACE 2                                                                
GETREG   LA    RF,RREGREC                                                       
         B     LINKFILE                                                         
         SPACE 2                                                                
GETOFF   LA    RF,ROFFREC                                                       
         B     LINKFILE                                                         
         SPACE 2                                                                
GETSTAT  LA    RF,RSTAREC                                                       
         B     LINKFILE                                                         
         SPACE 2                                                                
GETGROUP LA    RF,RGRPREC                                          V 13         
         B     LINKFILE                                            V 13         
         SPACE 2                                                                
GETADV   LA    RF,RADVREC                                                       
         B     LINKFILE                                                         
         SPACE 2                                                                
GETPROD  LA    RF,RPRDREC                                                       
         B     LINKFILE                                                         
         SPACE 2                                                                
GETAGY   LA    RF,RAGYREC                                                       
         B     LINKFILE                                                         
         SPACE 2                                                                
GETCONTY LA    RF,RCTYREC                                                       
         B     LINKFILE                                                         
         SPACE 2                                                                
GETPTP   LA    RF,RPTPREC                                                       
         B     LINKFILE                                                         
         SPACE 2                                                                
GETMKT   LA    RF,RMKTREC                                                       
         B     LINKFILE                                                         
         SPACE 2                                                                
GETBYCD  LA    RF,RSPBREC                                                       
         B     LINKFILE                                                         
         SPACE 2                                                                
GETCONT  EQU   *                                                                
         LR    R2,RE               SAVE RETURN ADDRESS                          
         XCEFL RCONREC,4200        PRECLEAR RECORD                              
         LA    RF,RCONREC          SET A(CONTRACT RECORD)                       
         LR    RE,R2               RESTORE RETURN ADDRESS                       
*                                                                               
         OI    DMINBTS,X'08'       PASS DELETES                                 
         B     LINKFILE                                                         
         SPACE 2                                                                
GETBUY   LA    RF,RBUYREC                                                       
         B     LINKFILE                                                         
         SPACE 2                                                                
GETTEAM  LA    RF,RTEMREC                                                       
         B     LINKFILE                                                         
         SPACE 2                                                                
GETMAN   LA    RF,RSALREC                                                       
         B     LINKFILE                                                         
         SPACE 2                                                                
GETDVSP  LA    RF,RDSPREC                                                       
         B     LINKFILE                                                         
         SPACE 2                                                                
GETDVCT  LA    RF,RDCTREC                                                       
         B     LINKFILE                                                         
         SPACE 2                                                                
GETCATGY LA    RF,RCTGREC                                                       
         B     LINKFILE                                                         
         SPACE 2                                                                
GETCLASS LA    RF,RCLSREC                                                       
         B     LINKFILE                                                         
         SPACE 2                                                                
GETEOM   LA    RF,REOMREC                                                       
         B     LINKFILE                                                         
         SPACE 2                                                                
GETOWNER LA    RF,ROWNREC                                                       
         B     LINKFILE                                                         
         SPACE 2                                                                
LINKFILE NTR1                                                                   
         LR    R2,RF                                                            
         GOTO1 DATAMGR,DMCB,(DMINBTS,GETREC),REPFILE,KEY+28,           X        
               (R2),(0,DMWORK)                                                  
         MVC   LASTIO,DMCB+12      SAVE THESE VALUES                            
         MVC   LASTDA,KEY+28                                                    
         MVC   LASTLEN,27(R2)                                                   
         CLI   0(R2),X'0C'         SPECIALLY SAVE CONTRACT VALUES               
         BNE   DMCHECK                                                          
         CLI   KEY,X'0B'                                                        
         BE    DMCHECK                                                          
         NI    DMINBTS,X'F7'       TURN OFF PASS DELETES                        
         MVC   CONTDMWK,DMWORK                                                  
         MVC   CONTDA,LASTDA                                                    
         MVC   CONTIO,LASTIO                                                    
         MVC   CONTLEN,LASTLEN                                                  
         B     DMCHECK                                                          
         EJECT                                                                  
*              DATA MANAGER INTERFACE (PUTREC)                                  
         SPACE 3                                                                
WRITBACK NTR1                                                                   
         L     R2,LASTDA                                                        
         L     R3,LASTIO                                                        
         CLC   27(2,R3),LASTLEN    IS THE NEW RECORD LONGER                     
         BNH   PUTACC2                                                          
         CLI   FCUPFILE,C'Y'       HAS USER OPENED FOR UPDATE                   
         BNE   DMERR2                                                           
         SPACE 2                                                                
PUTACC2  GOTO1 DATAMGR,DMCB,(DMINBTS,PUTREC),REPFILE,(R2),(R3),        X        
               (0,DMWORK)                                                       
         B     DMCHECK                                                          
         EJECT                                                                  
*                   DATA MANAGER INTERFACE (CHECK ERRORS)                       
         SPACE 3                                                                
DMCHECK  TM    DMCB+8,X'FD'                                                     
         BE    ANYTRACE                                                         
         MVC   TRACEWRK,WORK                                                    
         MVC   WORK(27),=C'ERR 1, REQ NNNN-HIT ANY KEY'                         
         B     DMERR4       ERR 1 = DATA MANAGER ERROR                          
         SPACE 2                                                                
DMERR2   MVC   WORK(27),=C'ERR 2, REQ NNNN-HIT ANY KEY'                         
*                           ERR 2 = ILLEGAL FILE UPDATE                         
         SPACE 2                                                                
DMERR4   UNPK  WORK+11(4),RCRQTOT                                               
         OI    WORK+14,X'F0'                                                    
         GOTO1 LOGIO,WORK+32,1,(27,WORK)   WRITE TO CONSOLE                     
         XC    WORK+32(4),WORK+32                                               
         BASR  RE,RF               READ FROM CONSOLE (SO THEY GET MSG)          
*                                                                               
         MVC   WORK,TRACEWRK                                                    
         BAS   RE,DMTRACE                                                       
         MVI   MODE,DISKERR        TELL APPLICATION PROGRAM THE NEWS            
         BAS   RE,GO                                                            
         CLI   MODE,IGNORERR       USER CAN OPT TO IGNORE THEM                  
         BE    XIT                 DEFAULT IS TO GO ON TO NEXT REQUEST          
         SPACE 2                                                                
         L     RD,4(RD)            UNWIND WITHOUT XIT                           
         LM    RE,RC,12(RD)        SORRY ABOUT THIS MEL                         
         LA    R1,1                SET CONDITION CODE FOR REFILMST              
         LTR   R1,R1               IF NONZERO B TO ENDREQ                       
         XIT1                                                                   
         SPACE 2                                                                
TRACEWRK DS    CL64                                                             
         EJECT                                                                  
*                   MONITOR TRACING OF DATA MANAGER CALLS                       
         SPACE 3                                                                
ANYTRACE CLI   RCTRACE,C'Y'        TRACE OPTION                                 
         BNE   XIT                                                              
         L     R2,DMCB+12                                                       
         SPACE 2                                                                
TRACE4   BAS   RE,DMTRACE                                                       
         B     XIT                SET CONDITION CODE TO ZERO                    
         SPACE 2                                                                
         EJECT                                                                  
*                   ROUTINE TO TRACE DATA MANAGER CALLS                         
         SPACE 3                                                                
DMTRACE  NTR1                                                                   
         MVC   P,SPACES                                                         
         LM    R2,R5,DMCB                                                       
         MVC   TRACEDM8,DMCB+8                                                  
         MVC   P(8),0(R2)          COMMAND                                      
         MVC   P+10(8),0(R3)       FILE                                         
         LA    R6,4                                                             
         CLC   3(3,R3),=C'DIR'                                                  
         BNE   DMTRACE2                                                         
         LA    R4,TRACEKEY                                                      
         GOTO1 HEXOUT,DMCB,(R4),P+20,32,=C'N'                                   
         MVI   SKIPSPEC,C'Y'                                                    
         GOTO1 =V(REREPORT),RR=RELO                                             
         GOTO1 HEXOUT,DMCB,(R5),P+20,32,=C'N'                                   
         MVC   P+20+64+4(27),0(R5)   EBCDIC DISPLAY                             
         B     DMTRACE4                                                         
         SPACE 2                                                                
DMTRACE2 GOTO1 HEXOUT,DMCB,(R4),P+20,(R6),=C'N'                                 
         LA    R5,34(R5)                                                        
         GOTO1 HEXOUT,DMCB,(R5),P+75,25                                         
         SPACE 2                                                                
DMTRACE4 DS    0H                                                               
         GOTO1 HEXOUT,DMCB,TRACEDM8,P+130,1                                     
         SPACE 2                                                                
         MVI   SKIPSPEC,C'Y'                                                    
         GOTO1 =V(REREPORT),RR=RELO                                             
         B      XIT               SET CONDITION CODE TO 0                       
         EJECT                                                                  
*                   LITERAL POOL FOR PROGRAM                                    
         SPACE 3                                                                
RELO     DS    F                                                                
DCBAPPL  DS    F                                                                
         SPACE 3                                                                
         DS    0F                                                               
DSNEX    MVC   0(0,RE),0(RF)                                                    
*                   LITERAL POOL FOR PROGRAM                                    
DDVALUE  DC    CL08'SPCFIL  '                                                   
DSNVALUE DC    CL12'REP.SPACEND.'                                               
DSNUSER  DC    CL18'                  '                                         
*                                                                               
DSNBUFF  DS    CL80                                                             
*                                                                               
DSNVALU2 DC    CL12'REP.SPACEND.'                                               
DSNVAREP DC    CL03'   '                                                        
         DC    CL28'HAS BEEN UPDATED BY THIS RUN'                               
LDSNMSG  EQU   *-DSNVALU2                                                       
*                                                                               
DSNVALU3 DC    CL12'REP.SPACEND.'                                               
DSNVARE3 DC    CL03'   '                                                        
         DC    CL28'HAS BEEN LOADED SUCCESSFULLY'                               
LDSNMSG3 EQU   *-DSNVALU3                                                       
*                                                                               
DSNVALU4 DC    CL12'REP.SPACEND.'                                               
DSNVARE4 DC    CL03'   '                                                        
         DC    CL34'DATE/NAME CHANGE:  WILL BE UPDATED'                         
LDSNMSG4 EQU   *-DSNVALU4                                                       
*                                                                               
DSNVALU5 DC    CL12'REP.SPACEND.'                                               
DSNVARE5 DC    CL03'   '                                                        
         DC    CL26'ATTEMPT TO OPEN O/P FAILED'                                 
LDSNMSG5 EQU   *-DSNVALU5                                                       
*                                                                               
DSNVALU6 DC    CL12'REP.SPACEND.'                                               
DSNVARE6 DC    CL03'   '                                                        
         DC    CL28'NOT BEING REWRITTEN THIS RUN'                               
LDSNMSG6 EQU   *-DSNVALU6                                                       
*                                                                               
DSNVALU7 DC    CL28'(PRE) **ENQ** CALL FOR FILE '                               
LDSNMSG7 EQU   *-DSNVALU7                                                       
*                                                                               
DSNVALU8 DC    CL28'(PRE) **DEQ** CALL FOR FILE '                               
LDSNMSG8 EQU   *-DSNVALU8                                                       
*                                                                               
DSNVALU9 DC    CL28'(POST)**ENQ** CALL FOR FILE '                               
LDSNMSG9 EQU   *-DSNVALU9                                                       
*                                                                               
DSNVALUA DC    CL28'(POST)**DEQ** CALL FOR FILE '                               
LDSNMSGA EQU   *-DSNVALUA                                                       
*                                                                               
DSNVALUF DC    CL44'SPACEND REBUILT/NOT SAVED BY DEFAULT/REQUEST'               
LDSNMSGF EQU   *-DSNVALUF                                                       
         SPACE 3                                                                
         LTORG                                                                  
         EJECT                                                                  
*              DSECT FOR MONARCH                                                
         SPACE 1                                                                
MONARCHD DSECT                                                                  
TRACEKEY DS    CL32                                                             
FILTKEY  DS    CL32                                                             
MONINFO  DS    CL192                                                            
MONFORC  DS    CL4                                                              
MONACT   DS    CL8                                                              
CONTDA   DS    CL4                                                              
CONTIO   DS    CL4                                                              
CONTLEN  DS    CL2                                                              
CONTRET  DS    F                                                                
TRACEDM8 DS    CL1                                                              
FT1      DS    CL1                                                              
FT2      DS    CL1                                                              
FT3      DS    CL1                                                              
FTA      DS    CL1                                                              
FTC      DS    CL1                                                              
FTS      DS    CL1                                                              
DEPTH    DS    CL1                                                              
SVGROUP  DS    CL2                                                 V 12         
STANUM   DS    F                                                   V 12         
KEYMONTH DS    CL4                                                              
LASTMON  DS    CL4                                                              
CURDTST  DS    CL2                                                              
CURDTEND DS    CL2                                                              
PRIDTST  DS    CL2                                                              
PRIDTEND DS    CL2                                                              
         EJECT                                                                  
*                                                                               
**       INCLUDES: REREPWORKD                                                   
**                 DDMASTD                                                      
**                 REGENALLB1                                                   
**                 REREPMODES                                                   
**                 REREPFMODE                                                   
*                                                                               
       ++INCLUDE REREPWORKD                                                     
       ++INCLUDE DDMASTD                                                        
       ++INCLUDE REGENALLB1                                                     
         ORG   RCONREC                                                          
       ++INCLUDE REGENDSP                                                       
         ORG   RCONREC                                                          
       ++INCLUDE REGENDCT                                                       
         ORG   RCONREC                                                          
       ++INCLUDE REGENSPEC                                                      
         ORG                                                                    
       ++INCLUDE REREPMODES                                                     
       ++INCLUDE REREPFMODE                                                     
       ++INCLUDE REXADDRD                                                       
       ++INCLUDE RESUBREPS                                                      
       ++INCLUDE REGENREQ2D                                                     
ROFF2    DSECT                                                                  
       ++INCLUDE REGENOFF2                                                      
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'171REREPFINT 05/01/02'                                      
         END                                                                    
