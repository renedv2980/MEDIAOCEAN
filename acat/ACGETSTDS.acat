*          DATA SET ACGETSTDS  AT LEVEL 024 AS OF 09/25/00                      
*CATALP ACGETSTD                                                                
         TITLE 'GET PERIOD DATA'                                                
*----------------------------------------------------------------------         
* NOTE RA IS ASSUMED TO BE SET TO ACREPWORKD                                    
* THE FOLLOWING FIELDS ARE ACCESSED:                                            
* ADLDGHIR                                                                      
*----------------------------------------------------------------------         
*              P1  A(X'YYMM') MONTH                                             
*              P2  A(KEY BUFFER)                                                
*              P3  A(OUTPUT BUFFER)                                             
*              P4  A(OFFICE CODE)                                               
*              P5  A(CALENDER RECORD)                                           
*----------------------------------------------------------------------         
GETPER   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 GPWKLN1Q+DMWKLNQ,**GPER**                                        
         USING GPWORKD,RC                                                       
         USING ACWORKD,RA                                                       
         LA    R7,GPWKLN1Q(RC)                                                  
         USING DMWORKD,R7                                                       
         MVC   GPIPARMS(GPIPARLN),0(R1)                                         
         BAS   RE,GPOPINIT         INITIALIZE OUTPUT BUFFER                     
*                                                                               
         L     R3,GPIABUFF         TABLE OF CAL REC KEYS                        
         CLI   0(R3),CASKTYPQ      ANY CAL KEYS FOR AGENCY                      
         BE    GP10                YES, CONTINUE                                
*                                                                               
         GOTO1 =A(RDCALKEY),GPIABUFF  READ CAL KEYS                             
         CLI   0(R3),CASKTYPQ      ANYTHING READ                                
         BNE   GPX                 NO, AGENCY NOT ON NEW CALEDERS               
*                                                                               
GP10     L     R2,GPIAMNTH         MONTH REQUESTED                              
         L     R4,GPIAOFF          OFFICE REQUESTED                             
*                                                                               
         BAS   RE,GPGETCAL         LOOK FOR CALENDER RECORD                     
         BE    GP50                FOUND OFFICE LEVEL (OR NO OFF REQ)           
*                                                                               
         XR    R4,R4               LOOK FOR AGENCY LEVEL CAL REC                
         BAS   RE,GPGETCAL                                                      
         BNE   GPX                 NO CALENDER RECORDS DEFINED FOR MNTH         
*                                                                               
         USING CASRECD,R3                                                       
GP50     LA    R3,DMIO                                                          
         LA    R3,ACCRFST-ACCKEY(R3) GET FIRST ELEMENT                          
*                                                                               
GP60     CLI   0(R3),0                                                          
         BE    GP80                IF NOT FOUND, LEAVE                          
*                                                                               
         CLI   0(R3),TMPELQ                                                     
         BNE   GP70                                                             
*                                                                               
         USING TMPELD,R3                                                        
         CLC   TMPMTH,0(R2)        IS THIS THE MONTH REQUESTED                  
         BNE   GP70                                                             
         BAS   RE,GPPUTOP                                                       
GP70     ZIC   R1,1(R3)                                                         
         LA    R3,0(R1,R3)                                                      
         B     GP60                                                             
*                                                                               
GP80     B     GPX                                                              
GPX      XIT1                                                                   
         EJECT                                                                  
* --------------------------------------------------------------------          
* SEARCH TABLE OF KEYS FOR ONE WHICH CONTAINS THE MONTH AT 0(R2)                
* IF R4 IS ZERO, SKIP OFFICE MATCH                                              
* ASSUMES R7 POINTS TO DMWORKD AREA                                             
* --------------------------------------------------------------------          
*                                                                               
GPGETCAL NTR1                                                                   
         L     R3,GPIABUFF                                                      
         LA    R0,GPMAXCLQ                                                      
         USING CASRECD,R3                                                       
GPGC10   CLI   CASKTYP,CASKTYPQ    IS THIS A CALENDER KEDY                      
         BNE   GPGCNO              NO, CALENDER KEY FOR DATES NOT FND           
         CLC   0(2,R2),CASKEMOA                                                 
         BH    GPGC30                                                           
         CLC   0(2,R2),CASKSMOA                                                 
         BL    GPGC30                                                           
         LTR   R4,R4               HAS AN OFFICE BEEN PASSED                    
         BZ    GPGCYES             NO, DATE MATCH IS OK                         
         OC    0(L'CASKOFC,R4),0(R4) IS THERE ONE IN REQ                        
         BZ    GPGCYES             NO, DATES ARE OK                             
         CLC   CASKOFC,0(R4)                                                    
         BE    GPGCYES             GOT OFFICE MATCH                             
*                                                                               
GPGC30   LA    R3,ACCKLEN(R3)     NEXT KEY                                      
         BCT   R0,GPGC10                                                        
GPGCNO   CR    RB,R0                                                            
         B     GPX                                                              
GPGCYES  MVC   DMKEY,0(R3)                                                      
         USING CASRECD,R3                                                       
         L     RE,GPIACALR         PREP COPY RECORD TO DMIO FROM IACALR         
         LA    R0,DMIO                                                          
         CLC   CASKEY,0(RE)        DO I ALREADY HAVE THE RECORD                 
         BE    GPGCY50             YES                                          
*                                                                               
         GOTO1 =A(DMGETREC)                                                     
         USING ACCRECD,RE                                                       
         LA    RE,DMIO             SAVE CALENDER RECORD IN GLOBAL WS            
         L     R0,GPIACALR         (PASS IT BACK NEXT CALL)                     
*                                                                               
GPGCY50  XR    RF,RF                                                            
         ICM   RF,3,ACCRLEN                                                     
         LR    R1,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         CR    RB,RB                                                            
         B     GPX                                                              
*                                                                               
         EJECT                                                                  
* --------------------------------------------------------------------          
* INITIALIZE GPOPUT BUFFER WITH DATA FROM THE REQUEST                           
* --------------------------------------------------------------------          
*                                                                               
GPOPINIT NTR1                                                                   
         USING GPOPD,R2                                                         
         L     R2,GPIAOP                                                        
         XC    0(GPOLN1Q,R2),0(R2)                                              
         LA    R3,GPOPERS                                                       
         ST    R3,GPNEXTOP                                                      
         LA    R0,GPMAXPER                                                      
         XC    0(GPOPLN1Q,R3),0(R3)  CLEAR PERIOD OUTPUT BUFFER                 
         LA    R3,GPOPLN1Q(R3)                                                  
         BCT   R0,*-10                                                          
         L     R1,GPIAMNTH                                                      
         MVC   GPOMNTH,0(R1)                                                    
         MVI   GPOSTRT,X'FF'                                                    
         B     GPX                                                              
         DROP  R3                                                               
* --------------------------------------------------------------------          
* EXTRACT DATA FOR THE OUTPUT TABLE FROM THE THE TMPEL AT 0(R3)                 
* --------------------------------------------------------------------          
*                                                                               
         USING TMPELD,R3                                                        
GPPUTOP  NTR1                                                                   
         L     R2,GPNEXTOP                                                      
         USING GPOPERD,R2                                                       
         MVC   GPOPSTRT,TMPSTART                                                
         MVC   GPOPEND,TMPEND                                                   
         GOTO1 =A(GETNWRK),DMCB,GPOPSTRT,GPOPEND,0                              
         L     R1,DMCB                                                          
         STC   R1,GPOPNWRK                                                      
         LA    R2,GPOPLN1Q(R2)     STORE NEXT OP POSITION                       
         ST    R2,GPNEXTOP                                                      
         L     R2,GPIAOP                                                        
         USING GPOPD,R2                                                         
         ZIC   R0,GPONWORK                                                      
         AR    R1,R0               KEEP NUMBER OF WORDAYS IN MONTH              
         STC   R1,GPONWORK                                                      
         CLC   GPOSTRT,TMPSTART                                                 
         BNH   *+10                                                             
         MVC   GPOSTRT,TMPSTART                                                 
         CLC   GPOEND,TMPEND                                                    
         BNL   *+10                                                             
         MVC   GPOEND,TMPEND                                                    
*                                                                               
         ZIC   R1,GPONPER          NUMBER OF PERIODS IN MONTH                   
         LA    R1,1(R1)                                                         
         STC   R1,GPONPER                                                       
         B     GPX                                                              
         DROP  R2,R3                                                            
         LTORG                                                                  
         EJECT                                                                  
         TITLE 'GET STANDARD HOURS'                                             
*----------------------------------------------------------------------         
*              P1  A(X'YYMM') MONTH                                             
*              P2  A(STD HOURS RECORD KEYS)                                     
*              P3  A(RETURN BUFFER - GSOPD)                                     
*              P4  A(12 BYTE 1R ACCOUNT)                                        
*              P5  A(CALENDER KEYS BUFFER)                                      
*--------*-------------------------------------------------------------         
GETSTD   CSECT                                                                  
         NMOD1 GSWKLN1Q+DMWKLNQ,**GSTD**                                        
         USING GSWORKD,RC                                                       
         LA    R7,GSWKLN1Q(RC)     SET DMWORK ADDRESS                           
         USING DMWORKD,R7                                                       
         MVC   GSIPARMS(GSIPARLN),0(R1)                                         
*                                                                               
         USING GPOPD,R2                                                         
         LA    R2,GSPERDAT                                                      
         USING GSOPD,R3                                                         
         L     R3,GSIAOP           A(OUTPUT BUFFER                              
         XC    GSOMNTH,GSOMNTH                                                  
         XC    GSONPER,GSONPER                                                  
         XC    GSONWORK,GSONWORK                                                
         ZAP   GSOHRS,=P'0'                                                     
*                                                                               
         L     R3,GSIABUFF         TABLE OF STD HRS RECORD KEYS                 
         CLI   0(R3),CASKTYPQ      ANY KEYS FOR AGENCY                          
         BE    GS10                YES                                          
*                                                                               
         GOTO1 =A(RDSTDKEY),GSIABUFF                                            
         CLI   0(R3),CASKTYPQ      ANY KEYS FOR AGENCY                          
         BNE   GSX                 NO                                           
*                                                                               
GS10     L     R4,GSIAMNTH         MONTH REQUESTED                              
         OC    0(2,R4),0(R4)       ANY MONTH?                                   
         BZ    GSERR               NOPE                                         
*                                                                               
* CONVERT 1R ACCOUNT INTO STDKEY FORMAT                                         
*                                                                               
         L     R4,GSIAACC          A(1R ACCOUNT)                                
         MVC   GS1RLVA,SPACES                                                   
         GOTO1 =A(MOVE1R),DMCB,GS1RLVA,(R4),1,0                                 
         MVC   GS1RLVB,SPACES                                                   
         GOTO1 =A(MOVE1R),DMCB,GS1RLVB,(R4),2,0                                 
         MVC   GS1RLVC,SPACES                                                   
         GOTO1 =A(MOVE1R),DMCB,GS1RLVC,(R4),3,0                                 
         MVC   GS1RLVD,SPACES                                                   
         GOTO1 =A(MOVE1R),DMCB,GS1RLVD,(R4),4,0                                 
*                                                                               
         USING LDGELD,RE                                                        
         L     R4,GSIAACC                                                       
         L     RE,ADLDGEL                                                       
         MVC   BYTE,LDGOPOS                                                     
         MVC   HALF,SPACES                                                      
         XR    RF,RF               OFFICE LENGTH                                
         TM    BYTE,LDGOKEY2                                                    
         BNO   *+12                                                             
         LA    RF,1(RF)                                                         
         XI    BYTE,LDGOKEY2       TURN OFF 2 BYTE OFFICE BYTE                  
*                                                                               
         CLI   BYTE,LDGOKEY                                                     
         BNH   *+6                                                              
         DC    H'0'                DIE IF OFFICE NOT IN KEY                     
         ZIC   R1,BYTE                                                          
         BCTR  R1,0                                                             
         LA    R4,0(R1,R4)                                                      
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   HALF(0),0(R2)                                                    
*                                                                               
         GOTO1 =A(GETPER),DMCB,GSIAMNTH,GSIACLKY,GSPERDAT,HALF,GSIACLRC         
*                                                                               
         USING GPOPD,R2                                                         
         LA    R2,GSPERDAT                                                      
         USING GSOPD,R3                                                         
         L     R3,GSIAOP           A(OUTPUT BUFFER)                             
         L     RE,GSIAMNTH                                                      
         MVC   GSOMNTH,0(RE)                                                    
         MVC   GSONPER,GPONPER                                                  
         MVC   GSONWORK,GPONWORK                                                
         MVC   GSOSTRT,GPOSTRT                                                  
         MVC   GSOEND,GPOEND                                                    
         ZAP   GSOHRS,=P'0'                                                     
         ZIC   R1,GSONPER          NUMBER OF PERIODS IN MONTH                   
         USING GSOPERD,R4                                                       
         LA    R4,GSOPDATA                                                      
         LA    R2,GPOPERS                                                       
         ZIC   R5,GSONPER                                                       
         USING GPOPERD,R2                                                       
*                                                                               
GS50     MVC   GSOPSTRT,GPOPSTRT                                                
         MVC   GSOPEND,GPOPEND                                                  
         MVC   GSOPNWRK,GPOPNWRK                                                
         GOTO1 =A(GSGETHRS),DMCB,(RC),(R4) GET STANDARD HOURS FOR PER           
         AP    GSOHRS,GSOPHRS                                                   
         LA    R4,GSOPLN1Q(R4)                                                  
         LA    R2,GPOPLN1Q(R2)                                                  
         BCT   R5,GS50                                                          
*                                                                               
GSX      CR    RB,RB                                                            
GSXX     XIT1                                                                   
GSERR    CR    RB,R0                                                            
         B     GSXX                                                             
*                                                                               
         EJECT                                                                  
*----------------------------------------------------------------------         
* GSGETHRS, THE GSOP BUFFER IN R4 HAS THE START AND END DATES OF THE            
*        PERIOD YOU WANT THE STANDARD HOURS FOR.                                
*----------------------------------------------------------------------         
GSGETHRS NTR1                                                                   
         USING STDRECD,R3                                                       
         USING GSOPERD,R4                                                       
         L     R3,GSIABUFF                                                      
         ZAP   GSOPHRS,=P'0'                                                    
         XC    GSAKEY,GSAKEY       SAVE A(KEY WHICH BEST FITS)                  
*                                                                               
GSGET20  CLI   1(R3),STDKSUBQ      IS THIS A STD HRS KEY                        
         BNE   GSGET60                                                          
         BAS   RE,GSGETLEN         GET SIGNIFICANT LENGTH OF KEY (-1)           
*                                                                               
         EX    R1,*+8                                                           
         B     *+10                                                             
         CLC   STDKOFC(0),GS1RACC  DOES THE KEY FIT THIS LEVEL?                 
         BH    GSGET60             IF HIGH, WE HAVE PASSED IT                   
         BL    GSGET50             IF LOW, GET NEXT                             
*                                                                               
         CLC   STDKSTDT,GSOPSTRT   EQUAL!, CHECK DATES                          
         BH    GSGET50                                                          
         CLC   STDKENDT,GSOPEND                                                 
         BL    GSGET50                                                          
*                                                                               
         ST    R3,GSAKEY           FOUND 1 THAT FITS                            
*                                                                               
GSGET50  LA    R3,ACCKLEN(R3)                                                   
         B     GSGET20                                                          
*                                                                               
GSGET60  L     R3,GSAKEY                                                        
         LTR   R3,R3               DID I FIND ANYTHING                          
         BNZ   GSGET67             YES                                          
         L     R3,GSIABUFF         NO, USE COMPANY STANDARD HOURS REC           
*                                                                               
GSGET62  CLC   STDKOFC,SPACES      IS THIS A COMPANY STD HRS REC                
         BNE   GSGETX              NOPE                                         
         CLC   STDKSTDT,GSOPSTRT                                                
         BH    GSGET65             STARTS AFTER MY PERIOD                       
         CLC   STDKENDT,GSOPEND                                                 
         BL    GSGET65             ENDS BEFORE MY PERIOD                        
         B     GSGET67             GOOD, USE IT                                 
*                                                                               
GSGET65  LA    R3,ACCKLEN(R3)      GET NEXT COMPANY STD HRS REC                 
         CLI   0(R3),STDKTYPQ      IS THIS A STD HRS KEY                        
         BNE   GSGETX              YOUR FUCKED                                  
         B     GSGET62             CHECK DATES AGAIN                            
*                                                                               
GSGET67  MVC   DMKEY,0(R3)                                                      
         GOTO1 =A(DMGETREC)                                                     
         LA    R3,DMIO                                                          
         LA    R3,ACCRFST-ACCKEY(R3) GET FIRST ELEMENT                          
         XR    R1,R1                                                            
*                                                                               
GSGET70  CLI   0(R3),0             EOF                                          
         BE    GSGETX                                                           
         USING SHRELD,R3                                                        
         CLI   0(R3),SHRELQ        STANDARD HOURS ELEMENT                       
         BNE   GSGET90             GET NEXT EL                                  
         CLC   SHRSTART,GSOPSTRT                                                
         BH    GSGET90                                                          
         CLC   SHREND,GSOPEND                                                   
         BL    GSGET90                                                          
         ZAP   GSOPHRS,SHRHOURS                                                 
         B     GSGETX                                                           
*                                                                               
GSGET90  IC    R1,1(R3)                                                         
         LA    R3,0(R1,R3)                                                      
         B     GSGET70                                                          
*                                                                               
GSGETX   XIT1                                                                   
*-------------------------------------------------------------------            
* RETURN IN R1 THE SIGNIFICANT LENGTH OF THE STDKEY IN 0(R3)                    
*-------------------------------------------------------------------            
*                                                                               
         USING STDRECD,R3                                                       
GSGETLEN EQU   *                                                                
         XR    R1,R1                                                            
         LA    R1,GSLENPER                                                      
         CLC   STDKPER,SPACES                                                   
         BNER  RE                                                               
*                                                                               
         LA    R1,GSLENSBD                                                      
         CLC   STDKSBD,SPACES                                                   
         BNER  RE                                                               
*                                                                               
         LA    R1,GSLENDPT                                                      
         CLC   STDKDPT,SPACES                                                   
         BNER  RE                                                               
*                                                                               
         LA    R1,GSLENOFC                                                      
         CLC   STDKOFC,SPACES                                                   
         BNER  RE                                                               
         XR    R1,R1                                                            
         BR    RE                                                               
         DROP  R3                                                               
*                                                                               
GSLENPER EQU   (L'STDKOFC+L'STDKDPT+L'STDKSBD+L'STDKPER-1)                      
GSLENSBD EQU   (L'STDKOFC+L'STDKDPT+L'STDKSBD-1)                                
GSLENDPT EQU   (L'STDKOFC+L'STDKDPT-1)                                          
GSLENOFC EQU   (L'STDKOFC-1)                                                    
*                                                                               
         EJECT                                                                  
*                                                                               
* SET THE BUFFER IN P1 WITH ALL THE CALENDER KEYS FOR THIS AGENCY               
RDCALKEY NMOD1 DMWKLNQ,**CLKY**                                                 
         L     R2,0(R1)                                                         
         USING DMWORKD,R7                                                       
         LA    R3,DMKEY                                                         
         USING CASRECD,R3                                                       
         XC    CASKEY,CASKEY      BUILD A TABLE IN CORE OF ALL STDHRS           
         MVI   CASKTYP,CASKTYPQ    RECORD KEYS                                  
         MVI   CASKSUB,CASKSUBQ                                                 
         MVC   CASKCPY,RCCOMPFL                                                 
         MVC   DMKEYSV,DMKEY                                                    
         GOTO1 =A(DMHIGHDR)                                                     
RD10     CLC   CASKEY(CASKCPY-CASKEY+L'CASKCPY),DMKEYSV                         
         BNE   RDX                                                              
         MVC   0(ACCKLEN,R2),CASKEY   SAVE KEY PLUS DA                          
         LA    R2,ACCKLEN(R2)                                                   
         GOTO1 =A(DMSEQDR)                                                      
         B     RD10                                                             
RDX      MVI   0(R2),0                                                          
         XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
* SET ASTDKEYS BUFFER WITH ALL THE STD HRS KEYS FOR THIS AGENCY                 
         DS    0H                                                               
RDSTDKEY NMOD1 DMWKLNQ,*RDSTD*                                                  
         LR    R7,RC                                                            
         L     R2,0(R1)                                                         
         USING DMWORKD,R7                                                       
         LA    R3,DMKEY                                                         
         USING STDRECD,R3                                                       
         XC    STDKEY,STDKEY      BUILD A TABLE IN CORE OF ALL CAL              
         MVI   STDKTYP,STDKTYPQ    RECORD KEYS                                  
         MVI   STDKSUB,STDKSUBQ                                                 
         MVC   STDKCPY,RCCOMPFL                                                 
         MVC   DMKEYSV,DMKEY                                                    
         GOTO1 =A(DMHIGHDR)                                                     
RDS10    CLC   STDKEY(STDKCPY-STDKEY+L'STDKCPY),DMKEYSV                         
         BNE   RDSX                                                             
         MVC   0(ACCKLEN,R2),STDKEY   SAVE KEY PLUS DA                          
         LA    R2,ACCKLEN(R2)                                                   
         GOTO1 =A(DMSEQDR)                                                      
         B     RDS10                                                            
RDSX     MVI   0(R2),0                                                          
         XIT1                                                                   
         EJECT                                                                  
         LTORG                                                                  
* --------------------------------------------------------------------          
* GET THE NUMBER OF WORKING DAYS ENCOMPASED BY P1 AND P2.                       
*                                                                               
* --------------------------------------------------------------------          
GETNWRK  CSECT                                                                  
         USING NWWORKD,RC                                                       
         DS    0H                                                               
         NMOD1 NWWORKLN,**NWRK**                                                
         ST    R1,NWAPARMS         SAVE R1 FOR RETURN                           
         L     RE,0(R1)                                                         
         MVC   NWSTRT,0(RE)                                                     
         L     RE,4(R1)                                                         
         MVC   NWEND,0(RE)                                                      
         GOTO1 DATCON,DMCB,(1,NWSTRT),(0,WORK+10)                               
         GOTO1 GETDAY,DMCB,WORK+10,WORK   FIRST DAY OF WEEK INTO WORK           
         MVC   NWFSTDAY,WORK       SAVE DAY EXPRESSION                          
         GOTO1 DATCON,DMCB,(1,NWSTRT),(0,NWEBSTRT)                              
         GOTO1 DATCON,DMCB,(1,NWEND),(0,NWEBEND)                                
         GOTO1 =V(PERVERT),DMCB,NWEBSTRT,NWEBEND                                
         LH    R2,8(R1)            NUMBER OF DAYS IN PERIOD                     
*                                                                               
         LA    R3,BINWRKNG                                                      
         LA    R4,DAYTABL          MONACCS TABLE OF DAYS                        
         LA    R0,7                                                             
GN10     CLC   0(3,R4),NWFSTDAY                                                 
         BE    GN20                                                             
         LA    R3,1(R3)                                                         
         LA    R4,3(R4)                                                         
         BCT   R0,GN10                                                          
         DC    H'0'                                                             
*                                                                               
GN20     XR    R5,R5               COUNTER FOR N'WORKING DAYS                   
*                                  R2 IS N DAYS IN PERIOD                       
*                                                                               
GN30     LA    R1,7                                                             
         ICM   R6,8,0(R3)          SET R6 WITH WORKING DAYS BIT PATTERN         
GN40     SLA   R6,1                SHIFT OUT A BIT (DAY)                        
         BNO   *+8                 IF NOT ON, ITS A WEEKEND                     
         LA    R5,1(R5)            IF ON, INC N'WORKING DAYS                    
         BCT   R2,*+8              WHEN R2=0, PERIOD RANGE IS EXAUSTED          
         B     GNX                                                              
*                                                                               
         BCT   R1,GN40             NEXT DAY                                     
         B     GN30                RESET R1 AND R6 FOR ANOTHER WEEK             
*                                                                               
GNX      L     R1,NWAPARMS         SET 0(R1) TO N'WORKING DAYS                  
         ST    R5,0(R1)                                                         
         XIT1                                                                   
         LTORG                                                                  
* NOTE LEADING BIT IS SIGN BIT, WHICH IS NOT SHIFTED BY THE SLA INST            
*                                                                               
BINWRKNG DS    0A                                                               
         DC    BL1'01111100'        MON                                         
         DC    BL1'01111001'        TUE                                         
         DC    BL1'01110011'        WED                                         
         DC    BL1'01100111'        THU                                         
         DC    BL1'01001111'        FRI                                         
         DC    BL1'00011111'        SAT                                         
         DC    BL1'00111110'        SUN                                         
         EJECT                                                                  
*----------------------------------------------------------------------         
*       COMPARE THE 1R ACCOUNT PASSED IN P1 WITH THE 1R ACCOUNT                 
*       PASSED IN P2 AT THE LEVEL PASSED IN P3, RC IS IN P4                     
*----------------------------------------------------------------------         
         DS    0H                                                               
COMP1R   NMOD1 0,*CMP1R                                                         
*                                                                               
*                                                                               
         L     R3,0(R1)            P1                                           
         L     R4,4(R1)            P2                                           
         L     R5,8(R1)            P3                                           
         L     RC,12(R1)           P4 = RC                                      
         BAS   RE,SETLEVEL         ADDRESS HEIRARCHY DATA W/R7                  
*                                                                               
         ZIC   R2,0(R7)                                                         
         BCTR  R2,0                                                             
         EX    R2,*+8                                                           
         B     *+10                                                             
         CLC   0(0,R3),0(R4)                                                    
         XMOD1                                                                  
*                                                                               
         EJECT                                                                  
*----------------------------------------------------------------------         
*       SET R7 TO THE A(THE HEIRARCHY DATA OF THE LEVEL IN R5)                  
*---------------------------------------------------------------------          
SETLEVEL EQU   *                                                                
         BCTR  R5,0                                                             
         MH    R5,=H'16'                                                        
         L     R7,ADLDGHIR                                                      
         LA    R7,2(R7)            BUMP TO ELEMENT DATA                         
         LA    R7,0(R5,R7)         ADDRESS THIS LEVELS DATA                     
         BR    RE                                                               
*                                                                               
         EJECT                                                                  
*-------------------------------------------------------                        
*       MOVE INTO P1, THE PIECE OF THE 1R ACCOUNT IN P2 WHICH                   
*       FOR THE LEVEL IN P3, P4 IS RC                                           
*       R5 CONTAINS THE LEVEL YOU WANT (1-4)                                    
*-------------------------------------------------------                        
MOVE1R   NMOD1 0,*MOV1R                                                         
*                                                                               
*                                                                               
         L     R3,0(R1)            P1 = TO FIELD                                
         L     R4,4(R1)            P2 = FROM FIELD                              
         L     R5,8(R1)            P3 = LEVEL                                   
*                                                                               
         BAS   RE,SET1ROFF         GET LENGTH/OFFSET INTO R1/R2                 
         LA    R4,0(R2,R4)                                                      
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),0(R4)                                                    
         XMOD1                                                                  
*                                                                               
*----------------------------------------------------------------------         
*       SET R2 TO THE OFFSET OF BYTEL INTO A 12 BYTE ACCOUNT FIELD              
*       AND R1 TO THE LENGTH OF THAT FIELD                                      
*       USES R6                                                                 
*---------------------------------------------------------------------          
SET1ROFF EQU   *                                                                
         XR    R2,R2                                                            
         XR    R1,R1                                                            
         XR    R0,R0                                                            
         XR    R6,R6                                                            
         LA    R6,1(R6)            SET R6 TO 1                                  
         L     RF,ADLDGHIR                                                      
         LA    RF,2(RF)            BUMP PAST ELCODE/LEN                         
         IC    R1,0(RF)                                                         
         CR    R5,R6                                                            
         BER   RE                                                               
*                                                                               
         LA    R6,1(R6)                                                         
         IC    R0,0(RF)            SAVE LENGTH OF PREV                          
         IC    R2,0(RF)            SET OFFSET TO LEVEL 2                        
         LA    RF,16(RF)                                                        
         IC    R1,0(RF)                                                         
         SR    R1,R0               SET LEN OF LEVEL 2                           
         CR    R5,R6                                                            
         BER   RE                                                               
*                                                                               
         IC    R0,0(RF)                                                         
         IC    R2,0(RF)                                                         
         LA    R6,1(R6)                                                         
         LA    RF,16(RF)                                                        
         IC    R1,0(RF)                                                         
         SR    R1,R0               SET LEN OF LEVEL 3                           
         CR    R5,R6                                                            
         BER   RE                                                               
*                                                                               
         IC    R0,0(RF)                                                         
         IC    R2,0(RF)                                                         
         LA    R6,1(R6)                                                         
         LA    RF,16(RF)                                                        
         IC    R1,0(RF)                                                         
         SR    R1,R0               SET LEN OF LEVEL 3                           
         CR    R5,R6                                                            
         BER   RE                                                               
         DC    H'0'                                                             
         EJECT                                                                  
*                                                                               
*-------------------------------------------------------                        
*       MOVE INTO P1, THE 1R ACCOUNT IN P2,                                     
*       FOR THE LEVEL IN P3, P4 IS RC                                           
*-------------------------------------------------------                        
MOVEK1R  NMOD1 0,*MOK1R                                                         
*                                                                               
*                                                                               
         L     R3,0(R1)            P1 = TO FIELD                                
         L     R4,4(R1)            P2 = FROM FIELD                              
         L     R5,8(R1)            P3 = LEVEL                                   
*                                                                               
         BAS   RE,SET1RLEN         GET LENGTH R1/R2                             
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),0(R4)                                                    
         XMOD1                                                                  
*                                                                               
*---------------------------------------------------------------------          
*       SET R1 TO THE LENGTH OF THE 12 BYTE ACCOUNT AT THE LEVEL IN R5          
*       USES R6                                                                 
*---------------------------------------------------------------------          
SET1RLEN EQU   *                                                                
         XR    R1,R1                                                            
         XR    R0,R0                                                            
         XR    R6,R6                                                            
         LA    R6,1(R6)                                                         
         L     RF,ADLDGHIR                                                      
         LA    RF,2(RF)            BUMP PAST ELCODE/LEN                         
         IC    R1,0(RF)                                                         
         CR    R5,R6                                                            
         BER   RE                                                               
*                                                                               
         LA    R6,1(R6)                                                         
         LA    RF,16(RF)                                                        
         IC    R0,0(RF)                                                         
         AR    R1,R0               SET LEN OF LEVEL 2                           
         CR    R5,R6                                                            
         BER   RE                                                               
*                                                                               
         LA    R6,1(R6)                                                         
         LA    RF,16(RF)                                                        
         IC    R0,0(RF)                                                         
         AR    R1,R0               SET LEN OF LEVEL 3                           
         CR    R5,R6                                                            
         BER   RE                                                               
*                                                                               
         LA    R6,1(R6)                                                         
         LA    RF,16(RF)                                                        
         IC    R0,0(RF)                                                         
         AR    R1,R0               SET LEN OF LEVEL 3                           
         CR    R5,R6                                                            
         BER   RE                                                               
         DC    H'0'                                                             
* --------------------------------------------------------------------          
* DATAMGR INTERFACE                                                             
* NOTE R7 IS ASSUMED TO BE SET TO DMWORKD AREA                                  
* --------------------------------------------------------------------          
         USING DMWORKD,R7                                                       
DMSEQDR  NMOD1 0,SEQ                         READ SEQUENTIAL                    
         MVC   DMCOMM,=C'DMRSEQ'                                                
         MVC   DMKEYSV,DMKEY                                                    
         GOTO1 DATAMGR,DMCB,DMCOMM,=C'ACCDIR ',DMKEY,DMKEY                      
         XIT1                                                                   
*                                                                               
DMHIGHDR NMOD1 0,HIGH                                                           
         MVC   DMCOMM,=C'DMRDHI'             READ HIGH                          
         MVC   DMKEYSV,DMKEY                                                    
         GOTO1 DATAMGR,DMCB,DMCOMM,=C'ACCDIR ',DMKEY,DMKEY                      
         XIT1                                                                   
*                                                                               
DMREADDR NMOD1 0,READ                                                           
         MVC   DMCOMM,DMREAD                 A SPECIFIC READ                    
         MVC   DMKEYSV,DMKEY                                                    
         GOTO1 DATAMGR,DMCB,DMCOMM,=C'ACCDIR ',DMKEY,DMKEY                      
         XIT1                                                                   
*                                                                               
DMGETREC NMOD1 12,GREC                                                          
         USING ACCRECD,R3                                                       
         LA    R3,DMKEY                                                         
         LA    R3,ACCKDA           ADDRESS DISK ADDRESS FOR GETREC              
         GOTO1 DATAMGR,DMCB,=C'GETREC  ',=C'ACCMST ',(R3),DMIO,(RC)             
DMX      XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
GPWORKD  DSECT                     GETPER WORKING STORAGE                       
GPIPARMS DS    0A                                                               
GPIAMNTH DS    A                   A(X'YYMM') MONTH                             
GPIABUFF DS    A                   A(CALENDER RECORD KEY BUFFER)                
GPIAOP   DS    A                   A(OUTPUT BUFFER)                             
GPIAOFF  DS    A                   A(OFFICE)                                    
GPIACALR DS    A                   A(CALENDER RECORD IO BUFFER)                 
GPIPARLN EQU   *-GPIPARMS                                                       
GPNEXTOP DS    A                   A(NEXT SLOT FOR PERIOD DATA)                 
GPWKLN1Q EQU   *-GPWORKD                                                        
*                                                                               
*                                                                               
GSWORKD  DSECT                                                                  
GSIPARMS DS    0A                                                               
GSIAMNTH DS    A                   A(X'YYMM') MONTH                             
GSIABUFF DS    A                   A(STD HRS KEY BUFFER)                        
GSIAOP   DS    A                   A(OUTPUT BUFFER)                             
GSIAACC  DS    A                   A(1R ACCOUNT)                                
GSIACLKY DS    A                   A(CALENDER KEYS BUFFER)                      
GSIACLRC DS    A                   A(LAST CALENDER RECORD READ)                 
GSIPARLN EQU   *-GSIPARMS                                                       
GSNEXTOP DS    A                   A(NEXT SLOT FOR PERIOD DATA)                 
GSAKEY   DS    A                   A(KEY THAT MIGHT BE THE 1 YOU WANT)          
GSKEY    DS    CL(ACCKLEN)                                                      
GSKEYSV  DS    CL(ACCKLEN)                                                      
GSPERDAT DS    (GPOPLENQ)C         INTERFACE FOR GET PER CALL                   
GSCOMM   DS    CL8                 DATAMGR COMMAND                              
GS1RACC  DS    0C                                                               
GS1RLVA  DS    CL(L'STDKOFC)                                                    
GS1RLVB  DS    CL(L'STDKDPT)                                                    
GS1RLVC  DS    CL(L'STDKSBD)                                                    
GS1RLVD  DS    CL(L'STDKPER)                                                    
GSWKLN1Q EQU   *-GSWORKD                                                        
* DATAMGR INTERFACE WORK AREA                                                   
*                                                                               
NWWORKD  DSECT                                                                  
NWAPARMS DS    A                                                                
NWDAYS   DS    H                   TOTAL NUMBER OF DAYS IN PERIOD               
NWSTRT   DS    XL3                                                              
NWEND    DS    XL3                                                              
NWEBSTRT DS    CL6                 YYMMDD FOR PERVERT                           
NWEBEND  DS    CL6                 YYMMDD FOR PERVAL                            
*                                                                               
NWFSTDAY DS    CL3                 MON, TUE, ETC. FOR START DATE                
NWWORK   DS    CL80                                                             
NWWORKLN EQU   *-NWWORKD                                                        
         EJECT                                                                  
DMWORKD  DSECT                                                                  
DMKEY    DS    CL(ACCKLEN)                                                      
DMKEYSV  DS    CL(ACCKLEN)                                                      
DMCOMM   DS    CL8                 DATAMGR COMMAND                              
DMIO     DS    CL2042                                                           
DMWKLNQ  EQU   *-DMWORKD                                                        
       ++INCLUDE ACGETSTDD                                                      
         EJECT                                                                  
* ACGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
       ++INCLUDE ACREPWORKD                                                     
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'024ACGETSTDS 09/25/00'                                      
         END                                                                    
