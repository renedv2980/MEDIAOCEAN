*          DATA SET GEFILINI   AT LEVEL 001 AS OF 03/08/96                      
*CATALP GEFILINI                                                                
GEFILINI TITLE '- GENERAL INITIALIZATION'                                       
GEFILINI CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,GEFILINI,RR=RE                                                 
         USING WORKD,R9            R9=A(GLOBAL WORKING STORAGE)                 
         L     R8,AGWORK                                                        
         USING GWORKD,R8                                                        
         SPACE 1                                                                
***********************************************************************         
* INITIALISE ADDRESSES                                                *         
***********************************************************************         
         SPACE 1                                                                
         ICM   R0,15,=X'D9000AB1'  T00AB1                                       
         GOTO1 VCOLY,BCPARM,0,(R0)                                              
         MVC   AGEN,0(R1)          A(GENERAL OBJECTS)                           
         IC    R0,=X'B2'           T00AB2                                       
         GOTO1 (RF),(R1),0,(R0)                                                 
         MVC   AGEN2,0(R1)                                                      
         IC    R0,=X'B3'                                                        
         GOTO1 (RF),(R1),0,(R0)                                                 
         MVC   AGENLST,0(R1)                                                    
*                                                                               
         GOTO1 VCOLY,BCPARM,(X'04',0),0                                         
         MVC   APRG,0(R1)          A(PROGRAM OBJECTS)                           
*                                                                               
         ICM   R0,15,=X'D9000A6C'                                               
         LA    R1,BCPARM                                                        
         GOTO1 VCOLY,BCPARM,0,(R0)                                              
         CLI   4(R1),X'FF'         SET 6C ROUTINE ADDRESSES                     
         BNE   *+6                                                              
         DC    H'0'                                                             
         L     RF,0(R1)                                                         
         LA    R0,AROUT6CN                                                      
         XR    RE,RE                                                            
         LA    R1,AROUT6C                                                       
INIT04   ST    RF,0(R1)                                                         
         STC   RE,0(R1)                                                         
         LA    RE,1(RE)                                                         
         LA    R1,4(R1)                                                         
         BCT   R0,INIT04                                                        
*                                                                               
         EJECT                                                                  
***********************************************************************         
* INITIALISE GENERAL VALUES                                           *         
***********************************************************************         
         SPACE 1                                                                
         MVC   GCSWSYSN,BCSWSYSN   INITIALISE SWITCHED SYSTEM INFO              
         MVC   GCSWSYSC,BCSWSYSC                                                
         MVC   GCSWSYSP,BCSWSYSP                                                
*                                                                               
         LH    RF,=Y(IOAREC-GWORKD)                                             
         AR    RF,R8                                                            
         AH    RF,=Y(L'IODA+L'IOWORK)                                           
         ST    RF,AIOREC           A(CONTROLLER IO AREA)                        
*                                                                               
         LH    RF,=Y(FDRADDR-GWORKD)                                            
         AR    RF,R8                                                            
         ST    RF,AFDRADDR         A(FDREL FORMAT BLOCK)                        
         LH    RF,=Y(TLSTREC-GWORKD)                                            
         AR    RF,R8                                                            
         ST    RF,ATLST            A(TSAR LIST RECORD)                          
*                                                                               
         LH    RF,=Y(FLTELSV-TWAD)                                              
         A     RF,ATWA                                                          
         ST    RF,AFLTELSV         A(FLTEL FORMAT BLOCK)                        
*                                                                               
         LH    RF,=Y(KYFLDS-TWAD)                                               
         A     RF,ATWA                                                          
         ST    RF,AKYFLD           A(KEY FORMAT BLOCK)                          
*                                                                               
         LH    RF,=Y(TWUSER-TWAD)                                               
         A     RF,ATWA                                                          
         ST    RF,AUSEBLK          A(2K BLOCK FOR USER)                         
*                                                                               
         LH    RF,=Y(TWSAVE-TWAD)                                               
         A     RF,ATWA                                                          
         ST    RF,ASVEBLK          A(512 BYTES SVE/RES ON NTRSES)               
*                                                                               
         LH    RF,=Y(TWDICT-TWAD)                                               
         A     RF,ATWA                                                          
         ST    RF,ADICBLK          A(1K BLOCK FOR DICTIONARY)                   
*                                                                               
         L     RF,ATWA                                                          
         USING TWAD,RF                                                          
         CLI   TWASESNL,0                                                       
         BNE   *+8                                                              
         MVI   TWASESNL,1                                                       
         DROP  RF                                                               
*                                                                               
         XIT1  ,                                                                
         EJECT                                                                  
***********************************************************************         
* INITIALISE GENERAL VALUES                                           *         
***********************************************************************         
         SPACE 1                                                                
         LTORG                                                                  
* GEFILWORK                                                                     
       ++INCLUDE GEFILWORK                                                      
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'001GEFILINI  03/08/96'                                      
         END                                                                    
