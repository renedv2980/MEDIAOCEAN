*          DATA SET APGLIST    AT LEVEL 066 AS OF 05/01/02                      
*CATALP APGLIST                                                                 
*INCLUDE SCANNER                                                                
         PRINT NOGEN                                                            
         TITLE 'APG - LIST OF APG REPORTS BY COMPANY CODE'                      
APGLIST  CSECT                                                                  
         NBASE 0,**LST**,=V(REGSAVE),R9,R8                                      
         L     RA,=V(CPRINT)                                                    
         USING DPRINT,RA                                                        
         XC    PANCOUNT,PANCOUNT                                                
         MVC   PANNAME,=CL10'APG'                                               
         LA    RF,FINDTBL                                                       
         MVI   0(RF),0             MARK AS NO ENTRIES                           
         ST    RF,AFINDTBL                                                      
         BAS   RE,SCANCARD                                                      
         CLI   REPNUM,1                                                         
         BE    APGLST05                                                         
*                                                                               
         MVC   TITLE(19),=C'APG LIST OF REPORTS'                                
         MVC   SUB1(7),=C'COMPANY'                                              
         MVC   SUB2(5),=C' CODE'                                                
         MVC   SUB3(7),=C'-------'                                              
         MVC   SUB1+9(18),=C'------REPORT------'                                
         MVC   SUB2+9(16),=C'  TYPE    FORMAT'                                  
         MVC   SUB3+9(18),=C'------------------'                                
         MVC   SUB2+29(7),=C'LEDGERS'                                           
         MVC   SUB3+29(18),=C'------------------'                               
         MVC   SUB1+49(5),=C'SUPER'                                             
         MVC   SUB2+49(6),=C'LEDGER'                                            
         MVC   SUB3+49(6),=C'------'                                            
         MVC   SUB2+57(10),=C'HOOK PHASE'                                       
         MVC   SUB3+57(10),=C'----------'                                       
         MVC   SUB1+72(5),=C' # OF'                                             
         MVC   SUB2+72(7),=C'REPORTS'                                           
         MVC   SUB3+72(7),=C'-------'                                           
         MVC   SUB2+81(10),=C'PAN SOURCE'                                       
         MVC   SUB3+81(10),=C'----------'                                       
         MVC   SUB1+93(4),=C'LAST'                                              
         MVC   SUB2+93(6),=C'ACCESS'                                            
         MVC   SUB3+93(8),=C'--------'                                          
         MVC   SUB2+104(5),=C'FLAGS'                                            
         MVC   SUB3+104(5),=C'-----'                                            
         B     APGLST08                                                         
*                                                                               
APGLST05 MVC   TITLE(13),=C'APG SCAN FIND'                                      
         MVC   SUB1(3),=C'PAN'                                                  
         MVC   SUB2(6),=C'SOURSE'                                               
         MVC   SUB3(6),=C'------'                                               
         MVC   SUB1+12(3),=C'PAN'                                               
         MVC   SUB2+12(4),=C'LINE'                                              
         MVC   SUB3+12(6),=C'------'                                            
*                                                                               
APGLST08 LA    R3,TYPELST                                                       
         LA    R4,ALPHALST                                                      
APGLST10 MVC   PANNAME+3(5),SPACES                                              
         CLI   0(R3),X'FF'         END OF REPORT TYPES                          
         BNE   APGLST15                                                         
         CLI   0(R4),X'FF'         END OF COMPANY PAN BOOKS                     
         BE    APGLST90            FINISHED                                     
         LA    R3,TYPELST                                                       
         CLI   REPNUM,1                                                         
         BE    APGLST11                                                         
         GOTO1 =V(PRINTER)         PRINT SPACE                                  
APGLST11 CP    LINE,=P'60'                                                      
         BL    APGLST14                                                         
*        MVC   P(L'FOOTLINE),FOOTLINE                                           
*        GOTO1 =V(PRINTER)         PRINT SPACE                                  
APGLST14 LA    R4,2(R4)            BUMP UP TO NEXT COMPANY CODE                 
APGLST15 MVC   PANNAME+3(2),0(R3)                                               
         MVC   PANNAME+5(2),0(R4)                                               
*                                                                               
         LA    R5,ALPHABET                                                      
APGLST20 CLI   0(R5),X'FF'                                                      
         BE    APGLST30                                                         
         MVC   PANNAME+7(1),0(R5)                                               
         GOTO1 =V(PANIC),DMCB,(X'80',=C'READ'),=C'PAN',PANNAME,CARD             
         TM    DMCB+8,X'10'                                                     
         BO    APGLST25                                                         
         LA    RF,P+(PFLAGS-PLINE)                                              
         ST    RF,APRTFLAG                                                      
         MVI   FOUND1,NO           SET TO NO                                    
         BAS   RE,SCANBOOK         GET INFORMATION FROM BOOK                    
APGLST25 LA    R5,1(R5)            BUMP UP TO NEXT ALPHA                        
         B     APGLST20                                                         
APGLST30 LA    R3,2(R3)            BUMP UP TO NEXT REPORT TYPE                  
         B     APGLST10                                                         
APGLST90 EQU   *                                                                
         GOTO1 =V(PRINTER)                                                      
         MVC   P+64(16),=C'TOTAL PAN BOOKS='                                    
         LA    R4,P+82                                                          
         EDIT  (B4,PANCOUNT),(4,(R4))                                           
         GOTO1 =V(PRINTER)                                                      
         GOTO1 =V(PANIC),DMCB,=C'CLOSE',=C'PAN'                                 
         GOTO1 =V(PRINT),DMCB,=C'CLOSE'                                         
         XBASE                                                                  
         EJECT                                                                  
SCANCARD NTR1                                                                   
         LA    R4,FINDTBL                                                       
         ST    R4,AFINDTBL                                                      
         LA    R4,TYPELST                                                       
         ST    R4,ATYPETBL                                                      
         LA    R4,ALPHALST                                                      
         ST    R4,AALPHTBL                                                      
         MVI   REPNUM,0                                                         
SCANCD10 GOTO1 =V(CARDS),DMCB,CARD,=C'RE00'                                     
         CLC   CARD(2),=C'/*'      END OF CARDS?                                
         BE    SCANCD99                                                         
         CLC   CARD(3),=C'AL='     ALPHA CODE FOR COMPANY?                      
         BE    SCANCD15            LOOP TO NEXT FOR NOW                         
         CLC   CARD(3),=C'TYPE='   REPORT TYPE CODE?                            
         BE    SCANCD25            LOOP TO NEXT FOR NOW                         
         CLC   CARD(5),=C'FIND='   FIND STRING AND SUBSTRING                    
         BE    SCANCD40            BUILD FIND TABLE                             
         B     SCANCD10            LOOP TO NEXT FOR NOW                         
*                                                                               
SCANCD15 LA    R0,L'CARD-3                                                      
         LA    R2,CARD+3           POINT TO FIRST COMPANY CODE                  
         L     R4,AALPHTBL                                                      
SCANCD20 CLI   0(R2),C' '          END OF INPUT?                                
         BE    SCANCD22            LOOP FOR NEXT                                
         MVC   0(2,R4),0(R2)       SAVE IN TABLE                                
         LA    R4,2(R4)            BUMP UP IN ALPHA LIST                        
         CLI   2(R2),C','          DELIMITOR                                    
         BNE   SCANCD22            NO, SO END OR ERROR (LOOP)                   
         LA    R2,3(R2)            BUMP UP IN CARDS                             
         SH    R0,=H'03'                                                        
         BP    SCANCD20            LOOP FOR NEXT ALPHA CODE                     
SCANCD22 ST    R4,AALPHTBL                                                      
         MVI   0(R4),X'FF'         MARK END OF TABLE                            
         B     SCANCD10                                                         
*                                                                               
SCANCD25 LA    R0,L'CARD-5                                                      
         LA    R2,CARD+5           POINT TO FIRST COMPANY CODE                  
         L     R4,ATYPETBL                                                      
SCANCD30 CLI   0(R2),C' '          END OF INPUT?                                
         BE    SCANCD32            LOOP FOR NEXT                                
         MVC   0(2,R4),0(R2)       SAVE IN TABLE                                
         LA    R4,2(R4)            BUMP UP IN ALPHA LIST                        
         CLI   2(R2),C','          DELIMITOR                                    
         BNE   SCANCD32            NO, SO END OR ERROR (LOOP)                   
         LA    R2,3(R2)            BUMP UP IN CARDS                             
         SH    R0,=H'03'                                                        
         BP    SCANCD30            LOOP FOR NEXT ALPHA CODE                     
SCANCD32 ST    R4,ATYPETBL                                                      
         MVI   0(R4),X'FF'         MARK END OF TABLE                            
         B     SCANCD10                                                         
*                                                                               
SCANCD40 L     R4,AFINDTBL                                                      
         MVI   REPNUM,1                                                         
         MVI   CARD+4,C','         REPLACE '=' FOR SCANNER                      
         GOTO1 SCANNER,DMCB,(C'C',CARD),(25,BLOCK)                              
         SR    R1,R1                                                            
         IC    R1,DMCB+4           NUMBER OF WORDS                              
         SH    R1,=H'01'                                                        
         BZ    SCANCD45                                                         
         STC   R1,0(R4)            SAVE NUMBER OF WORDS TO SCAN ON              
         AH    R4,=H'01'           BUMP UP BY ONE                               
         LA    R2,BLOCK+32         POINT TO FIRST REAL FIND                     
         SR    RF,RF                                                            
*                                                                               
SCANCD42 IC    RF,0(R2)            LENGTH OF STRING                             
         STC   RF,0(R4)            SAVE LENGTH                                  
         BCTR  RF,0                                                             
         EXMVC RF,1(R4),12(R2)     SAVE STRING                                  
         LA    R4,2(RF,R4)         BUMP TO NEW LOCATION                         
         LA    R2,32(R2)           NEXT ONE IN BLOCK                            
         BCT   R1,SCANCD42                                                      
SCANCD45 ST    R4,AFINDTBL         SAVE NEW LOCATION                            
         MVI   0(R4),0             MARK END OF LIST                             
         B     SCANCD10            LOOP FOR NEXT CARD                           
*                                                                               
SCANCD99 XIT1                                                                   
         EJECT                                                                  
SCANBOOK NTR1                                                                   
         USING PLINE,R5                                                         
         L     R2,PANCOUNT                                                      
         AH    R2,=H'01'                                                        
         ST    R2,PANCOUNT                                                      
         CLC   CARD+34(5),=C'LEVEL'                                             
         BNE   SCANBK10                                                         
         LA    R5,P                                                             
         MVC   PDATE,CARD+50                                                    
SCANBK10 CLC   CARD(2),=C'/*'                                                   
         BE    SCANBK90                                                         
         LA    R4,CKLST                                                         
         CLI   CARD,C'*'           COMMENT CARD                                 
         BE    SCANBK80            GET NEXT CARD                                
         CLI   REPNUM,1            SCAN FIND REPORT?                            
         BNE   SCANBK20                                                         
         BAS   RE,SCANFIND                                                      
         B     SCANBK80                                                         
*                                                                               
SCANBK20 CLI   0(R4),X'FF'                                                      
         BE    SCANBK80            NO MATCH, GET NEXT CARD                      
         CLC   CARD(8),0(R4)                                                    
         BE    SCANBK30            FOUND SO FILL IN SORTREC                     
         LA    R4,12(R4)           BUMP TO NEXT TYPE COMMAND                    
         B     SCANBK20                                                         
*                                                                               
SCANBK30 L     RF,8(R4)                                                         
         BASR  RE,RF                                                            
SCANBK80 EQU   *                                                                
         GOTO1 =V(PANIC),DMCB,(X'80',=C'READ'),=C'PAN',PANNAME,CARD             
         B     SCANBK10                                                         
SCANBK90 CLI   REPNUM,1                                                         
         BNE   SCANBK95                                                         
         CLI   FOUND1,YES                                                       
         BNE   SCANBK99                                                         
SCANBK95 GOTO1 =V(PRINTER)                                                      
SCANBK99 XIT1                                                                   
         EJECT                                                                  
SCANFIND NTR1                                                                   
         LA    R2,FINDTBL                                                       
SCANFD02 CLI   0(R2),0             ANY ENTRIES                                  
         BE    SCANFD90            NO MORE                                      
         SR    R0,R0                                                            
         IC    R0,0(R2)            NUMBER OF STRING TO MATCH                    
         AH    R2,=H'01'                                                        
         LA    R3,CARD                                                          
         LA    RF,L'CARD                                                        
SCANFD05 SR    R1,R1                                                            
         IC    R1,0(R2)            SIZE TO MATCH                                
         BCTR  R1,0                                                             
*                                                                               
SCANFD10 EXCLC R1,0(R3),1(R2)                                                   
         BNE   SCANFD20            NO MATCH                                     
         LA    R3,1(R1,R3)         BUMP UP IN CARD TO SCAN MORE                 
         LA    R2,2(R1,R2)         BUMP UP IN FIND TABLE FOR NEXT WORD          
         SR    RF,R1               ADJUST REMAIN CARD LENGTH                    
         BCT   R0,SCANFD05         DO NEXT SUB FIND                             
         B     SCANFD80            FINISHED SO FOUND                            
*                                                                               
SCANFD20 LA    R3,1(R3)                                                         
         SH    RF,=H'01'                                                        
         CR    RF,R1                                                            
         BNL   SCANFD10            CAN NOT MATCH AT THIS POINT                  
         LA    R2,2(R1,R2)                                                      
         SH    R0,=H'01'                                                        
         BZ    SCANFD02            GET NEXT FIND                                
         IC    R1,0(R2)                                                         
         LA    R2,1(R1,R2)         BUMP TO END OF THIS FIND                     
         BCT   R0,*-8                                                           
         B     SCANFD02            GET NEXT FIND                                
*                                                                               
SCANFD80 MVC   P(L'PANNAME),PANNAME                                             
         MVC   P+12(L'CARD),CARD                                                
         MVI   FOUND1,YES                                                       
         GOTO1 =V(PRINTER)         PRINT SPACE                                  
SCANFD90 XIT1                                                                   
         EJECT                                                                  
PUTPHASE EQU   *                                                                
         USING PLINE,R5                                                         
         MVC   PSOURCE,PANNAME                                                  
         MVC   PTYPE,CARD+9                                                     
         MVC   PCOMP,CARD+11                                                    
         MVC   PFRMT,CARD+13                                                    
         BR    RE                                                               
*                                                                               
PUTLDGRS EQU   *                                                                
         L     RF,APRTFLAG                                                      
         MVC   PLDGE(2),CARD+9                                                  
         CLC   CARD+9(4),=C'LIST'                                               
         BE    PUTLDG4                                                          
         CLC   CARD+9(3),=C'XC='                                                
         BE    PUTLDG4                                                          
         LA    RF,1(RF)                                                         
         ST    RF,APRTFLAG                                                      
         MVI   0(RF),C'B'                                                       
         CLI   CARD+11,C' '                                                     
         BER   RE                                                               
         MVC   0(1,RF),CARD+11                                                  
         BR    RE                                                               
*                                                                               
PUTLDG4  LA    R1,PLDGE                                                         
         LA    R2,CARD+15                                                       
         MVI   PDETAIL,C' '                                                     
         MVI   PBUCKET,C' '                                                     
PUTLDG5  MVC   0(2,R1),0(R2)                                                    
         CLI   2(R2),C'D'                                                       
         BNE   *+8                                                              
         MVI   PDETAIL,YES                                                      
         CLI   2(R2),C'B'                                                       
         BNE   *+8                                                              
         MVI   PBUCKET,YES                                                      
         CLC   4(3,R2),SPACES                                                   
         BE    PUTLDG9                                                          
         MVI   2(R1),C','                                                       
         LA    R1,3(R1)                                                         
         LA    R2,4(R2)                                                         
         B     PUTLDG5                                                          
*                                                                               
PUTLDG9  CLI   PDETAIL,YES                                                      
         BNE   *+12                                                             
         MVI   0(RF),C'D'                                                       
         LA    RF,1(RF)                                                         
         CLI   PBUCKET,YES                                                      
         BNE   *+12                                                             
         MVI   0(RF),C'B'                                                       
         LA    RF,1(RF)                                                         
         ST    RF,APRTFLAG                                                      
         BR    RE                                                               
*                                                                               
PUTSPLDG EQU   *                                                                
         MVC   PSPLDG,CARD+9                                                    
         BR    RE                                                               
         SPACE  1                                                               
PUTHOOK  EQU   *                                                                
         MVC   PHOOK,CARD+9                                                     
         BR    RE                                                               
         SPACE  1                                                               
PUTREPS  EQU   *                                                                
         MVC   PREPS,CARD+9                                                     
         BR    RE                                                               
         SPACE  1                                                               
PUTSPCAL EQU   *                                                                
         L     R2,APRTFLAG                                                      
         MVI   0(R2),C'S'                                                       
         LA    R2,1(R2)                                                         
         ST    R2,APRTFLAG                                                      
         BR    RE                                                               
         SPACE  1                                                               
PUTMWR   EQU   *                                                                
         LA    R2,CARD+15                                                       
PUTMWR05 CLC   0(3,R2),=CL3'MWR'                                                
         BNE   PUTMWR10                                                         
         L     R2,APRTFLAG                                                      
         MVI   0(R2),C'M'                                                       
         LA    R2,1(R2)                                                         
         ST    R2,APRTFLAG                                                      
         BR    RE                                                               
PUTMWR10 CLC   0(3,R2),SPACES                                                   
         BER   RE                                                               
         CLI   0(R2),C','                                                       
         LA    R2,1(R2)                                                         
         BE    PUTMWR05                                                         
         B     PUTMWR10                                                         
         EJECT                                                                  
*        CONSTANTS, MY WORKING STORAGE                                          
FOOTLINE DC    C'FLAGS => M-MWR, B-BUCKETS, D-DETAIL, S-SUPERCAL'               
*                                                                               
TYPELST  DC    CL2'FI'             DEFAULT                                      
         DC    X'FF00'                                                          
         DS    10CL2                                                            
         DC    X'FF'               END OF TABLE                                 
*                                                                               
ALPHABET DC    C' ABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789'                         
         DC    X'FF'               END OF TABLE                                 
*                                                                               
ALPHALST DS    250CL2                                                           
         DC    X'FF'                                                            
         DS    0D                                                               
*                                                                               
CKLST    DC    CL8'PHASE',A(PUTPHASE)                                           
         DC    CL8'READ',A(PUTLDGRS)                                            
         DC    CL8'CONLEDG',A(PUTSPLDG)                                         
         DC    CL8'SUPERCAL',A(PUTSPCAL)                                        
         DC    CL8'REPORT',A(PUTREPS)                                           
         DC    CL8'HOOKLOAD',A(PUTHOOK)                                         
         DC    CL8'COL',A(PUTMWR)                                               
         DC    X'FF'               END OF TABLE                                 
*                                                                               
SORTREC  DS    0CL41                                                            
SORTCOMP DS    CL2                                                              
SORTTYPE DS    CL2                                                              
SORTFRMT DS    CL1                                                              
SORTLGD  DS    12CL2                                                            
SORTSLGD DS    CL1                                                              
SORTREPS DS    CL1                                                              
SORTHOOK DS    CL10                                                             
*                                                                               
SCANNER  DC    V(SCANNER)                                                       
CARD     DC    CL80' '                                                          
PANNAME  DC    CL10' '                                                          
YES      EQU   C'Y'                                                             
NO       EQU   C'N'                                                             
DMCB     DS    6F                                                               
DUB      DC    D'0'                                                             
WORK     DC    XL64'00'                                                         
PANCOUNT DS    F                                                                
APRTFLAG DS    A                                                                
AFINDTBL DS    A                                                                
AALPHTBL DS    A                                                                
ATYPETBL DS    A                                                                
PDETAIL  DS    CL1                                                              
PBUCKET  DS    CL1                                                              
REPNUM   DS    CL1                                                              
FOUND1   DS    CL1                                                              
BLOCK    DS    25CL32                                                           
FINDTBL  DS    150CL32                                                          
         LTORG                                                                  
*                                                                               
PLINE    DSECT                                                                  
         DS    CL2                                                              
PCOMP    DS    CL2                                                              
         DS    CL8                                                              
PTYPE    DS    CL2                                                              
         DS    CL7                                                              
PFRMT    DS    CL1                                                              
         DS    CL7                                                              
PLDGE    DS    CL16                                                             
         DS    CL6                                                              
PSPLDG   DS    CL2                                                              
         DS    CL4                                                              
PHOOK    DS    CL10                                                             
         DS    CL8                                                              
PREPS    DS    CL2                                                              
         DS    CL4                                                              
PSOURCE  DS    CL10                                                             
         DS    CL2                                                              
PDATE    DS    CL8                                                              
         DS    CL3                                                              
PFLAGS   DS    CL1                                                              
       ++INCLUDE DDDPRINT                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'066APGLIST   05/01/02'                                      
         END                                                                    
