*          DATA SET SPLDSETLEN AT LEVEL 010 AS OF 10/23/09                      
*CATALP SPLDSETL                                                                
         TITLE 'LDSETLEN - SPOTPAK LOAD - ADJUST RECORD LENGTHS'                
***********************************************************************         
*                                                                     *         
* PURPOSE IS TO LOAD RECORD WITH SPARE BYTES TO ALLOW                 *         
* FOR ADDING ELEMENTS TO TRAFFIC BUY ACTIVITY RECORDS AND             *         
*  TRAFFIC INSTRUCTIONS RECAP RECS WITH OUT MOVING THEM TO OVERFLOW   *         
*                                                                     *         
*  PROGRAM RECEIVES SAME PARAMETER LIST AS DALDDS                     *         
*                                                                     *         
*  RELEVANT FIELDS ARE   P2      = A(REC)                             *         
*                        P3+2(2) = REC LEN (MODIFIED ON OUTPUT)       *         
*                                                                     *         
***********************************************************************         
*                                                                               
         PRINT NOGEN                                                            
LDSETLEN CSECT                                                                  
         NMOD1 WORKX-WORKD,**SETL**                                             
         USING WORKD,RC                                                         
*                                                                               
         LR    RA,R1               SAVE PARAM REG                               
         USING PARAMS,RA                                                        
*                                                                               
         L     R3,P2               GET REC ADDRESS                              
         USING RECD,R3                                                          
*                                                                               
         BC    0,SETLEN10                                                       
         MVI   *-3,X'F0'           THIS CODE ONE TIME ONLY                      
         BAS   RE,INITIAL                                                       
*                                                                               
SETLEN10 CLC   =X'0A2E',REC        TRAFFIC BUY ACTIVITY                         
         BE    SETRBA                                                           
         CLC   =X'0A24',REC        TRAFFIC INSTR RECAP                          
         BE    SETRIR                                                           
         CLI   REC,X'FF'           TEST EOF                                     
         BE    SETLEN20                                                         
         CLI   REC,X'10'           TEST BUY REC                                 
         BH    SETBUY                                                           
         B     EXIT                                                             
*                                                                               
SETLEN20 L     R9,=V(CPRINT)       PRINT NON-ZERO COUNTERS                      
         USING DPRINT,R9                                                        
PRT1     ICM   R0,15,TRTBAS                                                     
         BZ    PRT2                                                             
         MVC   P(16),=CL16'TBA RECS READ'                                       
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  P+16(10),DUB                                                     
         CLI   P+16,C'0'                                                        
         BNE   *+8                                                              
         MVI   P+16,C' '                                                        
         GOTO1 VPRINTER                                                         
PRT2     ICM   R0,15,TRTBASU                                                    
         BZ    PRT3                                                             
         MVC   P(16),=CL16'TBA RECS UPDATED'                                    
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  P+16(10),DUB                                                     
         CLI   P+16,C'0'                                                        
         BNE   *+8                                                              
         MVI   P+16,C' '                                                        
         GOTO1 VPRINTER                                                         
PRT3     CP    TRTBAB,=P'0'                                                     
         BE    PRT4                                                             
         MVC   P(16),=CL16'TBA BYTES ADDED '                                    
         ZAP   DUB,TRTBAB                                                       
         OI    DUB+7,X'0F'                                                      
         UNPK  P+16(10),DUB                                                     
         CLI   P+16,C'0'                                                        
         BNE   *+8                                                              
         MVI   P+16,C' '                                                        
         GOTO1 VPRINTER                                                         
PRT4     ICM   R0,15,TRTIRS                                                     
         BZ    PRT5                                                             
         MVC   P(16),=CL16'TIR RECS READ   '                                    
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  P+16(10),DUB                                                     
         CLI   P+16,C'0'                                                        
         BNE   *+8                                                              
         MVI   P+16,C' '                                                        
         GOTO1 VPRINTER                                                         
PRT5     CP    TRTIRB,=P'0'                                                     
         BE    PRT6                                                             
         MVC   P(16),=CL16'TIR BYTES ADDED '                                    
         ZAP   DUB,TRTIRB                                                       
         OI    DUB+7,X'0F'                                                      
         UNPK  P+16(10),DUB                                                     
         CLI   P+16,C'0'                                                        
         BNE   *+8                                                              
         MVI   P+16,C' '                                                        
         GOTO1 VPRINTER                                                         
PRT6     EQU   *                                                                
         DROP  R9                                                               
EXIT     XIT1                                                                   
*                                                                               
* CODE FOR BUY RECORDS - FOR WI PUT SPARE FOR COS2 ELEM IF NOT THERE            
*                                                                               
SETBUY   CLC   =C'WI',BUYALPHA     ONLY FOR WI                                  
         BNE   EXIT                                                             
         CLC   BDSTART,=X'640919'  START 4TH QTR 2000  SEP25/00                 
         BL    EXIT                                                             
*                                                                               
         LA    R6,BDELEM           CHECK IF ALREADY HAS 73 ELEM                 
SETBUY10 CLI   0(R6),0                                                          
         BE    SETBUY20                                                         
         CLI   0(R6),X'73'         ALREADY THERE = DONE                         
         BE    EXIT                                                             
         ZIC   R1,1(R6)                                                         
         AR    R6,R1                                                            
         B     SETBUY10                                                         
*                                                                               
SETBUY20 SR    R1,R1                                                            
         LH    R1,P3+2             GET REC LEN                                  
         AHI   R1,6                ADD 6 BYTES FOR X'73' ELEM                   
         C     R1,=F'5972'         COMPARE TO MAX LEN (BUYRLEN)                 
         BNH   *+8                                                              
         L     R1,=F'5972'                                                      
         STH   R1,P3+2             SET NEW LENGTH                               
*                                                                               
         SR    RE,RE                                                            
         ICM   RE,3,REC+13                                                      
         AR    RE,R3               POINT TO EOR                                 
         XC    0(6,RE),0(RE)       AND INSURE NO ELEM CODE/LEN FOLLOWS          
         B     EXIT                                                             
*                                                                               
* CODE FOR BUY RECORDS *NOT* CURRENTLY USED *                                   
*&&DO                                                                           
         CLI   REC,X'10'           TEST BUY REC                                 
         BNH   EXIT                                                             
         MVC   BYTE,REC                                                         
         NI    BYTE,X'0F'          DROP AGY                                     
         CLI   BYTE,1              TEST SPOT TV                                 
         BNE   EXIT                                                             
*                                                                               
         SR    RE,RE                                                            
         ICM   RE,3,REC+13                                                      
         AR    RE,R3               POINT TO EOR                                 
         XC    0(2,RE),0(RE)       AND INSURE NO ELEM CODE/LEN FOLLOWS          
*                                                                               
         XC    CNTRS,CNTRS                                                      
         LA    R6,BDELEM                                                        
*                                                                               
         CLI   REC+3,X'FF'         TEST POL BUY                                 
         BNE   SETLEN40                                                         
         EJECT                                                                  
* POL BUY PROCESSING *                                                          
*                                                                               
SETLEN20 SR    R0,R0                                                            
         ICM   R0,1,1(R6)                                                       
         BZ    EXIT                                                             
         AR    R6,R0                                                            
SETLEN22 CLI   0(R6),0                                                          
         BE    SETLEN50                                                         
         CLI   0(R6),X'0B'                                                      
         BL    SETLEN20                                                         
         CLI   0(R6),X'0D'                                                      
         BH    SETLEN20                                                         
* HAVE POOL DATE ELEM                                                           
         CLI   1(R6),10            TEST ALLOCATED                               
         BH    SETLEN24                                                         
         TM    6(R6),X'04'         TEST HIATUS                                  
         BO    SETLEN20                                                         
         L     RE,UNALL                                                         
         LA    RE,1(RE)                                                         
         ST    RE,UNALL                                                         
         B     SETLEN20                                                         
*                                                                               
* TEST IF WITHIN PERIOD LIKELY TO GET AFFIDS                                    
*                                                                               
SETLEN24 DS    0H                                                               
         OC    4(2,R6),4(R6)       TEST PAID                                    
         BNZ   SETLEN20            YES - IGNORE                                 
         TM    6(R6),X'C0'         TEST MINUS OR MINUSSED                       
         BNZ   SETLEN20                                                         
         CLC   2(2,R6),DATELO                                                   
         BL    SETLEN20                                                         
         CLC   2(2,R6),DATEHI                                                   
         BH    SETLEN20                                                         
         L     RE,NOAFDS                                                        
         LA    RE,1(RE)                                                         
         ST    RE,NOAFDS                                                        
*                                                                               
* AFFID(S) SHOULD IMMEDIATELY FOLLOW *                                          
*                                                                               
SETLEN26 SR    R0,R0                                                            
         ICM   R0,1,1(R6)                                                       
         BZ    EXIT                                                             
         AR    R6,R0                                                            
         CLI   0(R6),X'10'         TEST AFFID                                   
         BNE   SETLEN22                                                         
         L     RE,NOAFDS                                                        
         BCTR  RE,0                                                             
         ST    RE,NOAFDS                                                        
         B     SETLEN26                                                         
         EJECT                                                                  
* NON-POL PROCESSING *                                                          
*                                                                               
SETLEN40 SR    R0,R0                                                            
         ICM   R0,1,1(R6)                                                       
         BZ    EXIT                                                             
         AR    R6,R0                                                            
SETLEN42 CLI   0(R6),0                                                          
         BE    SETLEN50                                                         
         CLI   0(R6),X'06'                                                      
         BL    SETLEN40                                                         
         CLI   0(R6),X'08'                                                      
         BH    SETLEN40                                                         
* HAVE NON-POL DATE ELEM                                                        
         OC    4(2,R6),4(R6)       TEST PAID                                    
         BNZ   SETLEN40            YES - IGNORE                                 
         CLC   2(2,R6),DATELO                                                   
         BL    SETLEN40                                                         
         CLC   2(2,R6),DATEHI                                                   
         BH    SETLEN40                                                         
*                                                                               
         ZIC   R0,7(R6)            GET NUMBER OF SPOTS                          
         TM    6(R6),X'80'         TEST MINUS                                   
         BZ    *+6                                                              
         LNR   R0,R0                                                            
         L     RE,NOAFDS                                                        
         AR    RE,R0                                                            
         ST    RE,NOAFDS                                                        
*                                                                               
SETLEN44 SR    R0,R0                                                            
         ICM   R0,1,1(R6)                                                       
         BZ    EXIT                                                             
         AR    R6,R0                                                            
         CLI   0(R6),X'10'         TEST AFFID                                   
         BNE   SETLEN42                                                         
         L     RE,NOAFDS                                                        
         BCTR  RE,0                                                             
         ST    RE,NOAFDS                                                        
         B     SETLEN44                                                         
         EJECT                                                                  
* ALL SPOTS COUNTED - ADJUST REC LENGTH *                                       
*                                                                               
SETLEN50 L     R0,UNALL                                                         
         SLL   R0,2                X 4                                          
         L     R1,NOAFDS                                                        
         MH    R1,=H'6'            X 6                                          
         AR    R0,R1                                                            
         BNP   EXIT                NEVER MAKE RECORD SHORTER                    
         SR    R1,R1                                                            
         LH    R1,P3+2             GET REC LEN                                  
         AR    R1,R0                                                            
         C     R1,=F'1976'         COMPARE TO MAX LEN                           
         BNH   *+8                                                              
         L     R1,=F'1976'                                                      
         STH   R1,P3+2             SET NEW LENGTH                               
*&&                                                                             
*                                                                               
         EJECT                                                                  
* TRAFFIC BUY ACTIVITY RECORDS *                                                
*                                                                               
SETRBA   XC    ELEMTBL,ELEMTBL                                                  
*                                                                               
         L     R1,TRTBAS           ADD TO TRAFFIC BUY ACTIVITY                  
         LA    R1,1(,R1)                                                        
         ST    R1,TRTBAS                                                        
*                                                                               
         LA    R6,REC+24                                                        
*                                                                               
SETRBA10 CLI   0(R6),05            BUY ACT ELEM                                 
         BNE   SETRBA20                                                         
*                                                                               
         LA    R0,40                                                            
         LA    R5,ELEMTBL                                                       
SETRBA12 OC    0(2,R5),0(R5)       EMPTY ENTRY                                  
         BZ    SETRBA14             YES                                         
*                                                                               
         CLC   4(2,R6),0(R5)       EQUAL ENTRY (SAME SPOT LENGTHS)              
         BE    SETRBA16                                                         
         LA    R5,6(,R5)                                                        
         BCT   R0,SETRBA12                                                      
         B     EXIT                                                             
SETRBA14 MVC   0(2,R5),4(R6)                                                    
         MVC   2(4,R5),=X'FFFF0000'                                             
*                                                                               
SETRBA16 CLC   2(2,R6),2(R5)                                                    
         BNL   *+10                                                             
         MVC   2(2,R5),2(R6)       SAVE LOWER DATE                              
*                                                                               
         CLC   2(2,R6),4(R5)                                                    
         BNH   *+10                                                             
         MVC   4(2,R5),2(R6)       SAVE HIGHER DATE                             
*                                                                               
SETRBA18 ZIC   R0,1(R6)                                                         
         AR    R6,R0                                                            
         B     SETRBA10                                                         
*                                                                               
SETRBA20 OC    ELEMTBL,ELEMTBL     ANY FUTURE DATES FOUND                       
         BZ    EXIT                NO, NO UPDATE NEEDED                         
*                                                                               
         LA    R0,40                                                            
         LA    R5,ELEMTBL                                                       
         LA    R4,24+20            FIXED START OF REC + ACTIVITY ELEM           
*                                                                               
SETRBA30 OC    0(6,R5),0(R5)       AT END OF TABLE                              
         BZ    SETRBA40                                                         
*                                                                               
         CLC   TODAYP,4(R5)        END DATE BEFORE LAST MONDAY                  
         BH    SETRBA36             BYPASS EXPANDING THESE                      
*                                                                               
         GOTO1 VDATCON,DMCB,(2,2(R5)),WORK                                      
         GOTO1 (RF),(R1),(2,4(R5)),WORK+6                                       
         GOTO1 VPERVERT,(R1),WORK,WORK+6                                        
*                                                                               
         LH    RF,12(,R1)          MAX WEEKS BETWEEN LOW AND HIGH DATES         
*                                                                               
         OC    10(2,R1),10(R1)     REMAINDER                                    
         BZ    *+8                                                              
         LA    RF,1(,RF)           ADD 1 TO WEEKS                               
*                                                                               
         MH    RF,=H'7'            TIMES ELEM LENGTH                            
*                                                                               
         AR    R4,RF               ADD POSSIBLE EXPANDED LENGTH                 
*                                                                               
SETRBA36 LA    R5,6(,R5)                                                        
         BCT   R0,SETRBA30                                                      
*                                                                               
* NOW ADD LENGTHS OF ALL ELEMS BEFORE LAST MONDAY (NOT EXPANDED)                
*                                                                               
SETRBA40 LA    R6,REC+24                                                        
*                                                                               
SETRBA42 CLI   0(R6),05            BUY ACT ELEM                                 
         BNE   SETRBA50                                                         
*                                                                               
         ZIC   R0,1(R6)                                                         
*                                                                               
         CLC   TODAYP,2(R6)        THIS BEFORE TODAY                            
         BNH   SETRBA44             DO NOT ADD EXPANDED                         
*                                                                               
         AR    R4,R0               ADD TO RECORD LEN                            
*                                                                               
SETRBA44 AR    R6,R0                                                            
         B     SETRBA42                                                         
*                                                                               
SETRBA50 LA    R7,TRTBASU          BUY ACT RECS UPDATED                         
         LA    R8,TRTBAB           BUY ACTIVITY BYTES ADDED                     
*                                                                               
* NOW ADJUST RECORD LENGTH                                                      
*                                                                               
         C     R4,=F'1976'         COMPARE TO MAX LEN                           
         BNH   *+8                                                              
         L     R4,=F'1976'                                                      
*                                                                               
SETRX    CH    R4,P3+2             IF NOT HIGHER THAN EXISITING REC LEN         
         BNH   EXIT                                                             
*                                                                               
         L     R1,0(,R7)           ADD TO RECORDS UPDATED                       
         LA    R1,1(,R1)                                                        
         ST    R1,0(,R7)                                                        
*                                                                               
         LR    RF,R4               NEW RECORD LENGTH                            
         SH    RF,P3+2             SUBTRACT EXISTING LENGTH                     
         BZ    EXIT                                                             
*                                                                               
         CVD   RF,DUB                                                           
         AP    0(L'TRTBAB,R8),DUB  ADD TO BYTES ADDED                           
*                                                                               
         LH    RF,P3+2             GET ORIGINAL LENGTH                          
         STH   R4,P3+2             SET NEW LENGTH                               
*                                                                               
* SET EXTENDED SPACE TO NULLS                                                   
*                                                                               
         LA    R5,0(RF,R3)                                                      
         SR    R4,RF                                                            
         BCTR  R4,0                                                             
         EX    R4,*+8                                                           
         B     EXIT                                                             
         XC    0(0,R5),0(R5)       CLEAR SPARE AREA AFTER RCORD                 
         EJECT                                                                  
* TRAFFIC INSTRUCTION RECAP RECORDS *                                           
*                                                                               
SETRIR   XC    ELEMTBL,ELEMTBL                                                  
*                                                                               
         L     R1,TRTIRS           ADD TO TRAFFIC INSTR RECAPS                  
         LA    R1,1(,R1)                                                        
         ST    R1,TRTIRS                                                        
*                                                                               
         LA    R6,REC+24                                                        
         SR    R1,R1                                                            
*                                                                               
SETRIR10 CLI   0(R6),X'10'         INST RECAP ELEM                              
         BNE   SETRIR20                                                         
*                                                                               
         CLM   R1,1,1(R6)                                                       
         BNL   *+8                                                              
         IC    R1,1(R6)                                                         
*                                                                               
         ZIC   R0,1(R6)                                                         
         AR    R6,R0                                                            
         B     SETRIR10                                                         
*                                                                               
SETRIR20 LH    R4,P3+2             GET CURRENT LENGTH                           
         AR    R4,R1                                                            
*                                                                               
         C     R4,=AL4(INSMAXLN)   COMPARE TO MAX LEN                           
         BNH   *+8                                                              
         L     R4,=AL4(INSMAXLN)                                                
*                                                                               
         LA    R7,TRTIRSU          INST REC RECS UPDATED                        
         LA    R8,TRTIRB           INSTR RECAP BYTES ADDED                      
         B     SETRX                                                            
         EJECT                                                                  
* INITIALIZATION CODE TO CALCULATE LIMITS FOR AFFIDAVITS *                      
*                                                                               
INITIAL  NTR1                                                                   
         LA    R0,L'ADCONS/4       MAKE SURE ALL ADCONS RESOLVED                
         LA    R1,ADCONS                                                        
INIT2    OC    0(4,R1),0(R1)                                                    
         BZ    INIT20                                                           
         LA    R1,4(R1)                                                         
         BCT   R0,INIT2                                                         
*                                                                               
         GOTO1 VDATCON,DMCB,(5,0),TODAY         GET TODAY IN YYMMDD             
         GOTO1 VGETDAY,DMCB,TODAY,WORK                                          
*                                                                               
* MAKE DATE LAST MONDAY'S DATE                                                  
*                                                                               
         ZIC   R2,DMCB                                                          
         LCR   R0,R2                                                            
         BCTR  R0,0                                                             
*                                                                               
         GOTO1 VADDAY,DMCB,TODAY,WORK,(R0)                                      
         GOTO1 VDATCON,DMCB,WORK,(2,TODAYP)                                     
*                                                                               
* ADVANCE TO SUNDAY IF NECESSARY                                                
*                                                                               
         LA    R0,7                                                             
         LR    R0,R2                                                            
         SR    R0,R1                                                            
         GOTO1 VADDAY,DMCB,TODAY,WORK,(R0)                                      
*                                                                               
* GET BROADCAST MONTH DATES                                                     
*                                                                               
         GOTO1 VGETBRD,DMCB,WORK,WORK+6                                         
         CLC   WORK(6),WORK+12          IS SUNDAY IN THIS BDCST MONTH           
         BL    INIT10                   YES                                     
*                                                                               
* THE SUNDAY WILL END THE BROADCAST MONTH                                       
*                                                                               
         GOTO1 VDATCON,DMCB,WORK+12,(2,DATEHI)                                  
         MVC   WORK(4),WORK+12                 MOVE MONTH END YYMM              
         BAS   RE,BACKUP30                                                      
         GOTO1 VDATCON,DMCB,WORK+6,(2,DATELO)  USE BDCST MONTH START            
         B     EXIT                                                             
*                                                                               
* THIS WEEK WILL NOT END THE BROADCAST MONTH                                    
*                                                                               
INIT10   DS    0H                                                               
         BAS   RE,BACKUP30                                                      
         GOTO1 VDATCON,DMCB,WORK+12,(2,DATEHI)  USE BDCST MONTH END             
*                                                                               
         MVC   WORK(4),WORK+12     NOW BACK UP ANOTHER BDCST MONTH              
         BAS   RE,BACKUP30                                                      
         GOTO1 VDATCON,DMCB,WORK+6,(2,DATELO)                                   
         B     EXIT                                                             
         EJECT                                                                  
* IF UNRESOLVED ADCONS, DISABLE THIS ROUTINE *                                  
*                                                                               
INIT20   MVC   LDSETLEN(2),=X'07FE' DISABLE MODULE                              
         B     EXIT                                                             
*                                                                               
* SUBROUTINE BACKS UP TO MONTH 30 DAYS PRIOR                                    
* AND RETURNS BROADCAST MONTH START/END DATES IN WORK+6(12)                     
*                                                                               
BACKUP30 NTR1                                                                   
         MVC   WORK+4(2),=C'15'                                                 
         LA    R0,30                                                            
         LNR   R0,R0                                                            
         GOTO1 VADDAY,DMCB,WORK,WORK+6,(R0)                                     
         MVC   WORK(6),WORK+6                                                   
         GOTO1 VGETBRD,DMCB,WORK,WORK+6                                         
         B     EXIT                                                             
*                                                                               
DATELO   DC    H'0'                                                             
DATEHI   DC    H'0'                                                             
*                                                                               
         DS    0A                                                               
ADCONS   DS    0XL16                                                            
VDATCON  DC    V(DATCON)                                                        
VADDAY   DC    V(ADDAY)                                                         
VGETDAY  DC    V(GETDAY)                                                        
VGETBRD  DC    V(GETBROAD)                                                      
VPERVERT DC    V(PERVERT)                                                       
VPRINTER DC    V(PRINTER)                                                       
TRTBAS   DC    F'0'                TRAFFIC BUY ACTIVITY RECS READ               
TRTBASU  DC    F'0'                   "     "     "      "   UPDATED            
TRTIRS   DC    F'0'                  "    INSTR RECAP RECS READ                 
TRTIRSU  DC    F'0'                                                             
TRTBAB   DC    PL6'0'              TRAFFIC BUY ACTIVITY BYTES ADDED             
TRTIRB   DC    PL6'0'              TRAFFIC INSTR RECAP BYTES ADDED              
         LTORG                                                                  
         EJECT                                                                  
PARAMS   DSECT                                                                  
P1       DS    A                                                                
P2       DS    A                                                                
P3       DS    A                                                                
P4       DS    A                                                                
P5       DS    A                                                                
P6       DS    A                                                                
*                                                                               
WORKD    DSECT                                                                  
DUB      DS    D                                                                
FULL     DS    F                                                                
BYTE     DS    C                                                                
DMCB     DS    6F                                                               
TODAY    DS    CL6                                                              
TODAYP   DS    XL2                                                              
WORK     DS    CL32                                                             
*                                                                               
         DS    0F                                                               
CNTRS    DS    0XL8                                                             
NOAFDS   DS    F                                                                
UNALL    DS    F                                                                
*                                                                               
ELEMTBL  DS    XL240               40 6 BYTE ENTRIES                            
*                                  ENTRY = 2 SPOT LEN, 2 LOW DATE 2 HI          
WORKX    EQU   *                                                                
         EJECT                                                                  
RECD     DSECT                                                                  
REC      EQU   *                                                                
         ORG   REC                                                              
       ++INCLUDE SPGENBUY                                                       
         EJECT                                                                  
       ++INCLUDE DDDPRINT                                                       
         EJECT                                                                  
       ++INCLUDE SPTRINST                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'010SPLDSETLEN10/23/09'                                      
         END                                                                    
