*          DATA SET DDHEXIN    AT LEVEL 004 AS OF 09/22/15                      
*CATALP HEXIN                                                                   
***********************************************************************         
*        THIS CSECT VALIDATES A SOURCE STRING OF 8 BIT HEX CHRS AND IF          
*        VALID TRANSLATES TO A DESTINATION STRING OF 4 BIT HEX CHRS.            
*        AN INVALID 8 BIT HEX CHR IS TRANSLATED AS C'0'                         
*        PARAM LIST VIA R1 CONTAINS FOLLOWING WORDS :-                          
*        1ST   A(SOURCE CHR STRING 8 BIT HEX) X'80' ON IF XA ADDR  (R2)         
*        2ND   A(DESTN  CHR STRING 4 BIT HEX)                      (R3)         
*        3RD   L'SOURCE CHR STRING                                 (R4)         
*        4TH   VALUE RETURNED ZERO    SOURCE STRING INVALID        (R5)         
*                             NONZERO LENGTH DESTN STRING.                      
***********************************************************************         
         ENTRY HEXI31              31-BIT ADDRESSES VERSION                     
HEXIN    CSECT                                                                  
         MVI   72(RD),0            SET FOR 24 BIT MODE                          
         J     HEXINTO                                                          
*                                                                               
HEXI31   MVI   72(RD),1            SET TO 1 FOR 31 BIT MODE                     
*                                                                               
HEXINTO  NMOD1 1,HEXIN                                                          
         USING HEXINWS,RC                                                       
         SAM31 ,                                                                
         CLI   0(RC),1             31 BIT VERSION?                              
         BE    HEXI0               YES IT IS.                                   
         SAM24 ,                   GET INTO 24-BIT MODE                         
         TM    0(R1),X'80'         TEST 31-BIT ADDRESS(S)                       
         JO    *+12                                                             
         TM    4(R1),X'80'                                                      
         JZ    HEXI0                                                            
         SAM31 ,                   GET INTO 31-BIT MODE                         
*                                                                               
HEXI0    LM    R2,R4,0(R1)                                                      
         NILH  GR2,X'7FFF'                                                      
         NILH  GR3,X'7FFF'                                                      
         LTR   R4,R4                                                            
         JNP   HEXIERR             SOURCE LENGTH NOT POSITIVE                   
         MVI   SCERR,0                                                          
*                                                                               
         SR    R7,R7               TRANLATE SOURCE CHRS TWO AT A TIME           
         LR    R5,R4                                                            
HEXI1    SR    R6,R6               SET TO FIRST OF PAIR                         
HEXI2    IC    R7,0(R2,R6)                                                      
HEXI2B   CHI   R7,C'0'                                                          
         JL    HEXI3               SOURCE CHR LT C'0'                           
         CHI   R7,C'9'                                                          
         JH    HEXI6               SOURCE CHR GT C'9'                           
         STC   R7,SC(R6)                                                        
         J     HEXI4                                                            
*                                                                               
HEXI3    CHI   R7,C'A'                                                          
         JL    HEXI6               SOURCE CHR LT C'A'                           
         CHI   R7,C'F'                                                          
         JH    HEXI6               SOURCE CHR GT C'F'                           
         NILL  GR7,X'000F'         CHR FROM X'CN' TO X'0N'                      
         AHI   R7,9                C'A'=X'01' TO X'0A'                          
         STC   R7,SC(R6)                                                        
HEXI4    LTR   R6,R6                                                            
         JP    HEXI5                                                            
         CHI   R5,1                                                             
         JNE   HEXI4B                                                           
         LA    R5,1(R5)            LAST ODD SOURCE CHR                          
         LA    R7,240              SIMULATE C'0'                                
         LA    R6,1                                                             
         J     HEXI2B                                                           
*                                                                               
HEXI4B   LA    R6,1                SET TO SECOND OF PAIR                        
         J     HEXI2                                                            
*                                                                               
HEXI5    UNPK  SC+1(1),SC+1(1)     SC=X'.N.M' TO X'.NM.'                        
         LH    R6,SC                                                            
         SRL   R6,4                R6=X'.NM.' TO X'..NM'                        
         STC   R6,0(R3)                                                         
         LA    R2,2(R2)            UP SOURCE PTR BY 2                           
         LA    R3,1(R3)            UP DESTN            PTR BY 1                 
         AHI   R5,-1                                                            
         JCT   R5,HEXI1            DOWN SOURCE COUNTER BY 2                     
*                                                                               
         CLI   SCERR,1                                                          
         JE    HEXIERR                                                          
         LA    R5,1(R4)                                                         
         SRA   R5,1                RETURN DESTN LENGTH                          
         J     HEXIX                                                            
*                                                                               
HEXI6    MVI   SCERR,1             SET ERROR FLAG                               
         LHI   R7,C'0'             REPLACE BY C'0'                              
         J     HEXI2B                                                           
*                                                                               
HEXIERR  SR    R5,R5               RETURN ERROR VALUE OF ZERO 00071             
*                                                                               
HEXIX    ST    R5,12(R1)                                                        
         XMOD1 1                                                                
*                                                                               
         LTORG                                                                  
*                                                                               
HEXINWS  DSECT                                                                  
SC       DS    CL2                 PAIR OF MODIFIED SOURCE CHARS                
SCERR    DS    CL1                 ERROR FLAG                                   
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'004DDHEXIN   09/22/15'                                      
         END                                                                    
