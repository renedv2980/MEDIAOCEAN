*          DATA SET ACGETBANK  AT LEVEL 086 AS OF 03/22/10                      
*CATALP ACGTBNK                                                                 
*                                                                               
**********************************************************************          
*                                                                    *          
* PARAMETERS ARE AS FOLLOWS                                          *          
*                                                                    *          
* P1  BYTE  0     X'01' - BANK INFO                                  *          
*                 X'02' - FORMAT INFO                                *          
*     BYTE 1-3    A(BLOCK)                                           *          
*                                                                    *          
* P2  BYTE 0      X'00'                                              *          
*     BYTE 1-3    A(COMFACS)                                         *          
*                                                                    *          
**********************************************************************          
         SPACE 1                                                                
         TITLE 'GETBANK - ACCOUNTING BANK INFORMATION'                          
ACGTBNK  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKL,*ACGBNK*,R9                                                
         USING WORKD,RC            RC=A(GLOBAL W/S)                             
*                                                                               
         MVC   PARMS,0(R1)                                                      
*                                                                               
         MVI   SPACES,X'40'                                                     
         LA    RE,SPACES                                                        
         LHI   RF,L'SPACES                                                      
         SR    R1,R1                                                            
         ICM   R1,8,SPACES         FILL SPACES WITH SPACES                      
         MVCL  RE,R0                                                            
*                                                                               
         USING COMFACSD,RE                                                      
         SR    RE,RE                                                            
         ICM   RE,15,PCOMFACS                                                   
         MVC   VDATAMGR,CDATAMGR                                                
         MVC   VDATCON,CDATCON                                                  
         DROP  RE                                                               
*                                                                               
         CLI   PREC,PBANK          ARE WE READING BANK INFO?                    
         BNE   *+8                                                              
         BRAS  RE,BANKREAD                                                      
*                                                                               
EXIT     XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
* READ BANK INFO                                                                
***********************************************************************         
         SPACE 1                                                                
BANKREAD NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         USING BANKD,R2                                                         
         SR    R2,R2                                                            
         ICM   R2,7,PBLOCK                                                      
*                                                                               
         USING BANKTBD,R5                                                       
         LA    R5,BANKTAB                                                       
BR010    CLI   0(R5),EOF           END OF BANK TABLE READING?                   
         BE    BRX                                                              
         LA    RE,BANCDE           POINT TO START OF STORAGE                    
         SR    R1,R1                                                            
         IC    R1,BNKFLD           ADD DISPLACEMENT TO CUR FIELD                
         AR    RE,R1                                                            
         SR    R1,R1                                                            
         IC    R1,BNKFLN                                                        
         AHI   R1,-1                                                            
         EX    R1,*+8                                                           
         B     *+10                                                             
         CLC   0(0,RE),SPACES                                                   
         BNH   BRX                                                              
*                                                                               
         MVC   SVDISP,BNKNDSP      SAVE OFF DISPL TO NAME FIELD                 
         USING BNKRECD,R3                                                       
         LA    R3,SVKEY                                                         
         MVC   BNKKEY,SPACES                                                    
         MVI   BNKTYP,BNKTYPQ                                                   
         MVI   BNKSUB,BNKSUBQ                                                   
         MVC   BNKCPY,BANCPY                                                    
         MVI   BNKETYP,0                                                        
         SR    R1,R1                                                            
         IC    R1,BNKKYLN                                                       
         AHI   R1,-1                                                            
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   BNKBANK(0),BANCDE   BANK/HUB/BRANCH CODE                         
         OC    BNKBANK(0),SPACES   SPACE OUT REST BANK/HUB/BRANCH CODE          
         EX    R1,*-6                                                           
         ST    R5,SVADDR           SAVE OFF CURRENT TABLE ADDRESS               
         DROP  R5                                                               
*                                                                               
         GOTOR DMHIGHDR,DMCB,(RC)       READ HIGH                               
         B     BR030                                                            
*                                                                               
BR020    GOTOR DMSEQDR,DMCB,(RC)       READ HIGH                                
BR030    CLC   SVKEY(BNKETYP-BNKKEY),IOKEY                                      
         BNE   BR090                                                            
*                                                                               
         GOTOR DMGETREC,DMCB,(RC)      GET THE RECORD                           
*                                                                               
         LA    R3,IOKEY                                                         
         LA    R4,BNKRFST                                                       
BR040    CLI   0(R4),0                                                          
         BE    BR020                                                            
*        CLI   0(R4),NAMELQ         NAME ELEMENT                                
*        BE    BR060                                                            
         CLI   0(R4),FFTELQ         CONTACT INFORMATION                         
         BE    BR070                                                            
         CLI   0(R4),BNKELQ         BANK DETAIL ELEMENT                         
         BE    BR080                                                            
BR050    SR    R0,R0                                                            
         IC    R0,1(R4)                                                         
         AR    R4,R0                                                            
         B     BR040                                                            
*                                                                               
         USING NAMELD,R4                                                        
BR060    LA    RE,BNAMES                                                        
         SR    R1,R1                                                            
         IC    R1,SVDISP                                                        
         AR    RE,R1                                                            
         MVC   0(L'BNAMES,RE),SPACES                                            
         IC    R1,NAMLN                                                         
         CHI   R1,L'BNAMES                                                      
         BNH   *+8                                                              
         LHI   R1,L'BNAMES                                                      
         SHI   R1,NAMLN1Q+1                                                     
         EX    R1,*+8                                                           
         B     BR050                                                            
         MVC   0(0,RE),NAMEREC     BANK/BRANCH NAME                             
         DROP  R4                                                               
*                                                                               
         USING FFTELD,R4                                                        
BR070    CLI   FFTTYPE,FFTTCNAM    CONTACT NAME AND PHONE?                      
         BNE   BR050                                                            
         CLC   FFTNAME,SPACES                ANYTHING IN  CONTACT NAME          
         BNH   BR050                                                            
*                                                                               
         LA    RE,BANCCONT         ASSUME IT'S CLIENT SERVICE CONTACT           
         CLI   FFTITYP,FFTCLI                                                   
         BE    *+8                                                              
         LA    RE,BANTCONT         POINT TO TECHNICAL CONTACT                   
         MVI   BANCIND-BANCCONT(RE),C'A'                  ACC FILE IND          
         MVC   BANCCNME-BANCCONT(L'FFTNAME,RE),FFTNAME    CONTACT NAME          
         MVC   BANCPNO-BANCCONT(L'FFTPHONE,RE),FFTPHONE   PHONE NUMBER          
         SR    R1,R1                                                            
         IC    R1,FFTDLEN                                                       
         SHI   R1,FFT75LNQ+1                                                    
         BM    BR050                                                            
         CHI   R1,L'BANTEML-1                                                   
         BNH   *+8                                                              
         LA    R1,L'BANTEML-1                                                   
         EX    R1,*+8                                                           
         B     BR050                                                            
         MVC   BANCEML-BANCCONT(0,RE),FFTEMAIL  CONTACT EMAIL ADDR              
         DROP  R4                                                               
*                                                                               
         USING BNKELD,R4                                                        
BR080    GOTO1 VDATCON,DMCB,(5,0),(X'20',BANDATE)                               
         TIME  DEC                                                              
         ST    R0,DUB                                                           
         MVI   DUB+4,X'0F'                                                      
         UNPK  WORK(9),DUB(5)                                                   
         MVC   BANTIME(6),WORK                                                  
         LA    R5,BANTRNS                                                       
         USING BANTRNS,R5                                                       
*                                                                               
         USING EDITYTBD,R6                                                      
         LA    R6,EDITYTAB         POINT R6 TO EDI TYPE TAB                     
BR082    CLI   0(R6),EOF           HAS TO BE THERE                              
         BE    BR050                - IF NOT SKIP IT                            
         CLC   EDITYNO,BNKETYP     FIND CORRECT BLOCK                           
         BE    *+12                                                             
         AHI   R6,EDITLNQ                                                       
         B     BR082                                                            
*                                                                               
         SR    R1,R1                                                            
         ICM   R1,3,EDITDSP        GET DSPL TO START OF BLOCK                   
         AR    R5,R1               POINT TO START OF BLOCK                      
*                                                                               
         MVC   BANDFTYP,BKTRNTY    TRANSMISSION TYPE                            
         MVC   BANDFFKY,BKFMKY     FORMAT CODE                                  
         MVC   BANDFDSN,BKDSNM     DATASET NAME                                 
         MVC   BANDFTKY,BKTRNKY    TRANSMISSION KEY                             
         MVC   BANDFUSR,BKADVID    ADVANTIS USER ID                             
         MVC   BANDFACN,BKADVAC    ADVANTIS ACCOUNT NUMBER                      
         MVC   BANDFCLS,BKMSGCL    CLASS NAME                                   
         MVC   BANDFCHR,BKCHRG     CHARGE                                       
         TM    BKSTAT,BKFNID       USING ID FOR FILENAME?                       
         BO    *+12                                                             
         TM    BKSTAT,BKFNNM       USING NAME FOR FILENAME?                     
         BZ    BR050               NO THEN DONE HERE                            
         BRAS  RE,RDIDI            GO READ CONTROL FILE                         
         B     BR050                                                            
         DROP  R4                                                               
*                                                                               
BR090    L     R5,SVADDR                                                        
         AHI   R5,BANKLNQ                                                       
         B     BR010                                                            
*                                                                               
BRX      J     EXIT                                                             
         DROP  R3,R5,R6                                                         
         EJECT                                                                  
***********************************************************************         
* READ IDI RECORD ON CONTROL FILE                                               
***********************************************************************         
         SPACE 1                                                                
         USING BNKELD,R4                                                        
         USING BANTRNS,R5                                                       
RDIDI    NTR1  BASE=*,LABEL=*                                                   
         XC    BANDFORG(L'BANDFFLN),BANDFORG                                    
         XC    SVKEY2,SVKEY2                                                    
         USING CTIREC,R3                                                        
         LA    R3,SVKEY2                                                        
         MVI   CTIKTYP,C'I'                                                     
         MVC   CTIKID+8(2),BANORIG                                              
         GOTO1 VDATAMGR,DMCB,=C'DMRDHI',=C'CTFILE ',SVKEY2,IOKEY2               
         CLC   SVKEY2(BNKETYP-BNKKEY),IOKEY2                                    
         BNE   RDIDIX                                                           
         LA    R3,IO2                                                           
         LA    R2,CTIDATA                                                       
RDIDI10  CLI   0(R2),0                                                          
         BE    RDIDIX                                                           
         CLI   0(R2),X'02'                                                      
         BE    RDIDI20                                                          
         CLI   0(R2),X'36'                                                      
         BE    RDIDI30                                                          
RDIDINX  ZIC   R1,1(R2)                                                         
         AR    R2,R1                                                            
         B     RDIDI10                                                          
*                                                                               
         USING CTDSCD,R2                                                        
RDIDI20  TM    BKSTAT,BKFNID              USING ID FOR FILENAME?                
         BZ    RDIDINX                                                          
         ZIC   R1,1(R2)                                                         
         SHI   R1,3                                                             
         CHI   R1,6                                                             
         BNH   *+8                                                              
         LA    R1,6                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   BANDFORG(0),CTDSC                                                
         B     RDIDIX                                                           
*                                                                               
         USING CTORGD,R2                                                        
RDIDI30  TM    BKSTAT,BKFNNM              USING NAME FOR FILENAME?              
         BZ    RDIDINX                                                          
         LA    RE,CTORGNAM+L'CTORGNAM-1 POINT TO END OF ORIGIN NAME             
         LA    R1,L'CTORGNAM                                                    
RDIDI40  CLI   0(RE),C' '                                                       
         BH    RDIDI50                                                          
         BCTR  RE,0                                                             
         BCT   R1,RDIDI40                                                       
         B     RDIDIX                                                           
*                                                                               
RDIDI50  BCTR  R1,0                                                             
         EXMVC R1,BANDFNME,CTORGNAM                                             
*                                                                               
RDIDIX   XIT1                                                                   
         DROP  R2,R3                                                            
         SPACE 2                                                                
         LTORG                                                                  
***********************************************************************         
* TABLES                                                              *         
***********************************************************************         
         SPACE 1                                                                
EDITYTAB DS    0C                                                               
         DC    AL1(BNKDFT),AL2(BANDFTKY-BANTRNS)                                
         DC    AL1(BNKEFT),AL2(BANEFTKY-BANTRNS)                                
         DC    AL1(BNKPOS),AL2(BAN57TKY-BANTRNS)                                
         DC    AL1(BNKACR),AL2(BANCRTKY-BANTRNS)                                
         DC    AL1(BNK820),AL2(BAN82TKY-BANTRNS)                                
         DC    AL1(EOF)                                                         
         EJECT                                                                  
*                                                                               
* BANK TABLE AND FIELDS                                                         
*                                                                               
         SPACE 1                                                                
BANKTAB  DS    0C                                                               
         DC    AL1(BANCDE-BANCDE),AL1(L'BANCDE),AL1(L'BANCDE),AL1(BNKNM*        
               E-BNAMES)                                                        
         DC    AL1(BANHUB-BANCDE),AL1(L'BANBRN),AL1(L'BANCDE+L'BANBRN),*        
               AL1(BRNNME-BNAMES)                                               
         DC    AL1(EOF)                                                         
         EJECT                                                                  
***********************************************************************         
* LITERALS                                                            *         
***********************************************************************         
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
*********************************************************************           
* DATAMGER INTERFACE                                                *           
*********************************************************************           
         SPACE 1                                                                
DMCTFIL  NMOD1 0,CTF               READ SEQUENTIAL                              
         L     RC,0(R1)            RESET RC                                     
         GOTO1 VDATAMGR,DMCB,=C'DMRDHI',=C'CTFILE ',SVKEY,IOKEY                 
         B     DMX                                                              
*                                                                               
DMWRTDR  NMOD1 0,WRT               WRITE BACK TO DIR                            
         L     RC,0(R1)            RESET RC                                     
         GOTO1 VDATAMGR,DMCB,(X'00',=C'DMWRT'),=C'ACCDIR',IOKEY,IOKEY           
         B     DMX                                                              
*                                                                               
DMADDDR  NMOD1 0,ADD               ADD KEY TO DIR                               
         L     RC,0(R1)            RESET RC                                     
         GOTO1 VDATAMGR,DMCB,(X'00',=C'DMADD'),=C'ACCDIR',IOKEY,IOKEY           
         B     DMX                                                              
*                                                                               
DMSEQDR  NMOD1 0,SEQ               READ SEQUENTIAL                              
         L     RC,0(R1)            RESET RC                                     
         GOTO1 VDATAMGR,DMCB,(X'80',DMRSEQ),=C'ACCDIR ',SVKEY,IOKEY,0           
         B     DMX                                                              
*                                                                               
DMHIGHDR NMOD1 0,HIGH              READ HIGH                                    
         L     RC,0(R1)            RESET RC                                     
         GOTO1 VDATAMGR,DMCB,(X'80',DMRDHI),=C'ACCDIR ',SVKEY,IOKEY,0           
         B     DMX                                                              
*                                                                               
DMREADDR NMOD1 0,READ              READ                                         
         L     RC,0(R1)            RESET RC                                     
         GOTO1 VDATAMGR,DMCB,(X'80',DMREAD),=C'ACCDIR ',SVKEY,IOKEY,0           
         B     DMX                                                              
*                                                                               
DMGETREC NMOD1 0,GREC              GET RECORD                                   
         L     RC,0(R1)            RESET RC                                     
         USING ACCRECD,R3                                                       
         LA    R3,IO                                                            
         MVC   SVDA,ACCKDA         SAVE OFF DISK ADDRESS                        
         GOTO1 VDATAMGR,DMCB,DMGET,=C'ACCMST ',SVDA,IO,DMWORK                   
         B     DMX                                                              
         DROP  R3                                                               
*                                                                               
DMPUTREC NMOD1 0,PREC              PUT RECORD                                   
         L     RC,0(R1)            RESET RC                                     
         GOTO1 VDATAMGR,DMCB,=CL8'PUTREC',=C'ACCMST',SVDA,IO,DMWORK             
*                                                                               
DMX      XIT1                                                                   
         EJECT                                                                  
*********************************************************************           
* CONSTANTS                                                         *           
*********************************************************************           
         SPACE 1                                                                
DMREAD   DC    C'DMREAD  '                                                      
DMRDHI   DC    C'DMRDHI  '                                                      
DMRSEQ   DC    C'DMRSEQ  '                                                      
DMADD    DC    C'DMADD   '                                                      
DMWRT    DC    C'DMWRT   '                                                      
DMGET    DC    C'GETREC  '                                                      
*                                                                               
DMWORK   DC    XL96'00'            FOR GETREC/PUTREC                            
         EJECT                                                                  
*********************************************************************           
* LITERALS                                                          *           
*********************************************************************           
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
*********************************************************************           
* WORKING STORAGE                                                   *           
*********************************************************************           
         SPACE 1                                                                
WORKD    DSECT                                                                  
PARMS    DS    0D                                                               
PREC     DS    XL1                                                              
PBANK    EQU   X'01'                                                            
PBLOCK   DS    AL3                                                              
PCOMFACS DS    AL4                                                              
PARMLNQ  EQU   *-PARMS                                                          
*                                                                               
DUB      DS    D                                                                
VDATAMGR DS    A                                                                
VDATCON  DS    A                                                                
*                                                                               
DMCB     DS    0F                  PARAMETER LIST                               
P1       DS    F                                                                
P2       DS    F                                                                
P3       DS    F                                                                
P4       DS    F                                                                
P5       DS    F                                                                
P6       DS    F                                                                
*                                                                               
SVADDR   DS    A                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    X                                                                
*                                                                               
SVDISP   DS    XL1                                                              
WORK     DS    CL64                                                             
SPACES   DS    CL200                                                            
*                                                                               
SVKEY    DS    CL42                SAVED AREA FOR DATAMGR                       
SVKEY2   DS    CL42                SAVED AREA FOR DATAMGR                       
SVDA     DS    XL4                                                              
IOKEY    DS    0XL42                                                            
IO       DS    XL2042                                                           
IOKEY2   DS    0XL42                                                            
IO2      DS    XL2000                                                           
WORKL    EQU   *-WORKD                                                          
         EJECT                                                                  
*********************************************************************           
* EQUATES                                                           *           
*********************************************************************           
         SPACE 1                                                                
EOF      EQU   X'FF'                                                            
         EJECT                                                                  
*********************************************************************           
* EDIT TYPE TAB DSECT                                               *           
*********************************************************************           
         SPACE 1                                                                
EDITYTBD DSECT                                                                  
EDITYNO  DS    XL1                 EDI TYPE-NUMERIC                             
EDIDF    EQU   X'00'               DEFAULT                                      
EDIEFT   EQU   X'01'               ELECTRONIC FUNDS TRANSMISSION                
EDIPOS   EQU   X'02'               POSPAY                                       
EDICR    EQU   X'03'               CASH RECONCILIATION                          
EDITDSP  DS    XL2                 DISPLACEMENT TO EDI BLOCK                    
EDITLNQ  EQU   *-EDITYTBD                                                       
         EJECT                                                                  
***********************************************************************         
* BANK TABLE DSECT                                                    *         
***********************************************************************         
         SPACE 1                                                                
BANKTBD  DSECT                                                                  
BNKFLD   DS    AL1                 DISPLACEMENT TO CURRENT FIELD                
BNKFLN   DS    XL1                 FIELD LENGTH                                 
BNKKYLN  DS    XL1                 KEY FIELD LENGTH COMBINED                    
BNKNDSP  DS    XL1                 KEY FIELD LENGTH COMBINED                    
BANKLNQ  EQU   *-BANKTBD                                                        
         EJECT                                                                  
*********************************************************************           
* ++INCLUDES                                                        *           
*********************************************************************           
         SPACE 1                                                                
*                                                                               
* ACKEYWRD                                                                      
*                                                                               
       ++INCLUDE ACKEYWRD                                                       
*                                                                               
* DDGETBANKD                                                                    
*                                                                               
       ++INCLUDE DDGETBANKD                                                     
         EJECT                                                                  
*********************************************************************           
* ++INCLUDES                                                        *           
*********************************************************************           
         SPACE 1                                                                
* ACGENFILE                                                                     
* DDCOMFACSD                                                                    
* CTGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
       ++INCLUDE DDCOMFACSD                                                     
       ++INCLUDE CTGENFILE                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'086ACGETBANK 03/22/10'                                      
         END                                                                    
