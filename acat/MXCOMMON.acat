*          DATA SET MXCOMMON   AT LEVEL 019 AS OF 03/22/13                      
*CATALP MXCOMMON                                                                
         TITLE 'MXCOMMON - COMMON ROUTINES FOR SPREPMX02/PPREPMX02'             
         PRINT NOGEN                                                            
         SPACE 2                                                                
MXCOMMON CSECT                                                                  
         NMOD1 WORKDL,MXCOM                                                     
         USING WORKD,RC                                                         
*                                                                               
         SRL   RF,22                                                            
         B     BRANCH(RF)                                                       
         SPACE 2                                                                
BRANCH   B     MXPROF                                                           
         B     SETMOA                                                           
         B     CLRAREA                                                          
         B     GTNAME                                                           
         DC    16X'00'                                                          
         EJECT                                                                  
*              MXPROF - ROUTINE LOOKS UP PROFILE VALUE                          
*                                  P1 - ROW NUMBER                              
*                                  P2 - A(DESTINATION)                          
         SPACE                                                                  
MXPROFL  NTR1  ,                   LOCAL ENTRY                                  
         SPACE                                                                  
MXPROF   DS    0H                                                               
         L     R3,4(R1)            R3=A(RETURN VALUE)                           
         XC    0(L'ACPFVAL,R3),0(R3)                                            
         ZIC   R2,3(R1)            R2=(REQUESTED ROW NUMBER)                    
         BCTR  R2,0                                                             
         SR    RE,RE                                                            
         LA    RF,ACPRTNL          RF=(L'PROFILE ROW)                           
         MR    RE,R2                                                            
         L     R1,=V(PROFREC)                                                   
         AR    R1,RF               BUMP TO ROW REQUESTED IN PROFILE REC         
         USING ACPRTND,R1                                                       
         OC    0(ACPRTNL,R1),0(R1) IF VALUE FOR ROW                             
         BZ    MXPROFX                                                          
         MVC   0(L'ACPFVAL,R3),ACPFVAL  SET IT                                  
MXPROFX  B     XIT                                                              
         DROP  R1                                                               
         EJECT                                                                  
*              SETMOA - ROUTINE SETS MOA DATE BASED ON BILL                     
*                       REQUEST DATE & PROF/MAINT VALUES                        
*                                  P1 - A(DATE TO RETURN)                       
*                                  P2 - A(ADDAY)                                
         SPACE                                                                  
SETMOA   DS    0H                                                               
         L     R2,0(R1)            R2=A(DATE TO RETURN)                         
         L     R3,4(R1)            R3=A(ADDAY)                                  
         GOTO1 MXPROFL,DMCB,MTPFMOSB,PROFWORK                                   
         OC    PROFWORK,PROFWORK   IF BACK-UP DATE                              
         BZ    SETMOA5                                                          
         CLC   =C'00',PROFWORK                                                  
         BE    SETMOA5                                                          
         CLC   4(2,R2),PROFWORK    IF BILL DATE > BACK DATE                     
         BH    SETMOAX             THEN BILL STAYS IN THAT MONTH                
         XC    WORK,WORK           ELSE, BILL GOES TO PREVIOUS MONTH            
         MVC   WORK(4),0(R2)                                                    
         MVC   WORK+4(2),=C'15'                                                 
         GOTO1 (R3),DMCB,WORK,WORK,F'-30'                                       
         MVC   0(6,R2),WORK                                                     
         B     SETMOAX                                                          
*                                                                               
SETMOA5  GOTO1 MXPROFL,DMCB,MTPFMOSF,PROFWORK                                   
         OC    PROFWORK,PROFWORK   IF FORWARD DATE                              
         BZ    SETMOAX                                                          
         CLC   =C'00',PROFWORK                                                  
         BE    SETMOAX                                                          
         CLC   4(2,R2),PROFWORK    IF BILL DATE < FORWARD DATE                  
         BL    SETMOAX             THEN BILL STAYS IN THAT MONTH                
         XC    WORK,WORK           ELSE, BILL GOES TO NEXT MONTH                
         MVC   WORK(4),0(R2)                                                    
         MVC   WORK+4(2),=C'15'                                                 
         GOTO1 (R3),DMCB,WORK,WORK,F'30'                                        
         MVC   0(6,R2),WORK                                                     
*                                                                               
SETMOAX  B     XIT                                                              
         EJECT                                                                  
*              CLRAREA - ROUTINE CLEARS AREA PASSED                             
*                                  P1 - A(AREA BEGINNING)                       
*                                  P2 - A(AREA END)                             
         SPACE                                                                  
CLRAREA  DS    0H                                                               
         L     R2,0(R1)            R2=A(BEGINNING OF AREA)                      
         L     R3,4(R1)            R3=A(END OF AREA)                            
         SR    R3,R2               SIZE OF AREA TO CLEAR                        
         XR    RE,RE                                                            
         LR    RF,RE                                                            
         MVCL  R2,RE               CLEAR THE AREA                               
         B     XIT                                                              
         EJECT                                                                  
*              GTNAME - ROUTINE SETS NAME                                       
*                                  P1 - A(RECORD)                               
*                                  P2 - A(NAME)                                 
         SPACE                                                                  
GTNAME   DS    0H                                                               
         L     R2,0(R1)            R2=A(RECORD)                                 
         L     R3,4(R1)            R3=A(NAME)                                   
         MVC   0(L'NAMEREC,R3),SPACES                                           
         LA    R1,ACCORFST                                                      
         AR    R2,R1               R2=A(FIRST ELEMENT)                          
         USING NAMELD,R2                                                        
*                                                                               
GTNM5    CLI   0(R2),0             TEST END OF RECORD                           
         BNE   *+6                                                              
         DC    H'0'                                                             
         CLI   0(R2),NAMELQ        MATCH ON NAME ELEMENT CODE                   
         BE    GTNM10                                                           
         SR    R0,R0               BUMP TO NEXT ELEMENT IN RECORD               
         ICM   R0,1,1(R2)                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R2,R0                                                            
         B     GTNM5               LOOP                                         
*                                                                               
GTNM10   SR    R1,R1               SET CONTRA ACCOUNT NAME                      
         IC    R1,NAMLN            LEGNTH OF NAME ELEMENT                       
         SH    R1,=Y(NAMLN1Q+1)                                                 
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),NAMEREC                                                  
         B     XIT                                                              
         DROP  R2                                                               
         EJECT                                                                  
*              EXITS, CONSTANTS, ETC...                                         
*                                                                               
XIT      XIT1                                                                   
         SPACE                                                                  
SPACES   DC    36C' '              SPACES                                       
         EJECT                                                                  
*=============*                                                                 
* LITERAL POOL*                                                                 
*=============*                                                                 
*                                                                               
         LTORG                                                                  
         SPACE                                                                  
         DROP  RB                 DROP PREVIOUS BASE REGS                       
         SPACE                                                                  
         EJECT                                                                  
WORKD    DSECT                                                                  
*                                                                               
DMWORK   DS    12D                                                              
DUB      DS    D                   DOUBLE WORD                                  
         DS    2D                  SPARE                                        
*                                                                               
DMCB     DS    6F                  PARAMETER BLOCK                              
FULL     DS    F                   FOUR BYTES                                   
         DS    4F                  SPARE                                        
*                                                                               
BYTE     DS    XL1                 ONE BYTE                                     
PROFWORK DS    CL20                PROFILE WORK AREA                            
WORK     DS    CL100               WORK AREA                                    
WORKDL   EQU   *-WORKD                                                          
         EJECT                                                                  
*=======================*                                                       
* OTHER VARIOUS DSECTS  *                                                       
*=======================*                                                       
*                                                                               
*  ACGENFILE                                                                    
*  ACGENPOST                                                                    
*  ACPOSTD                                                                      
*  SPGENBILL                                                                    
*  PBILLREC                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
       ++INCLUDE ACGENPOST                                                      
       ++INCLUDE ACPOSTD                                                        
       ++INCLUDE MXPRNTD                                                        
       ++INCLUDE MXTBDSECT                                                      
BILLRECD DSECT                                                                  
       ++INCLUDE PBILLREC                                                       
         ORG   BILLRECD                                                         
       ++INCLUDE SPGENBILL                                                      
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'019MXCOMMON  03/22/13'                                      
         END                                                                    
