*          DATA SET ACGETRTEX  AT LEVEL 031 AS OF 05/01/02                      
*CATALP ACGETRTE                                                                
         TITLE 'GETRTE - BUILD CRATE INFO BLOCK'                                
*************************************************************                   
*   THIS MODULE IS USED TO LOOKUP CRATE RECORDS AND PASS    *                   
*   BACK PERTINENT INFORMATION                              *                   
*   1000 BYTE BLOCK COVERED BY DSECT CRTED (BOOK ACCAPCRBLK)*                   
*                                                           *                   
* REQ'D BLOCK INFO - A(DATAMGR),CO CODE, METHOD, OFFICE     *                   
* REQ'D OFFLINE INFO - EFF/START DATE, EFF/END DATE         *                   
*                                                           *                   
*   PARM 1 -- A(BLOCK)                                      *                   
*************************************************************                   
GETRTE   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 CRTX-CRT,*GETRTE*,RA,RR=R2,CLEAR=YES                             
         USING CRT,RC                                                           
         L     R9,0(R1)            PARAMETER 1 =A(COBLOCK)                      
         USING CRBLK,R9                                                         
*                                                                               
         ST    R2,RELO             INITIALIZE                                   
*                                                                               
         MVI   SPACES,C' '                                                      
         MVC   SPACES+1(L'SPACES-1),SPACES                                      
         EJECT                                                                  
*************************************************************                   
*              MAIN LOGIC CONTROL                                               
*************************************************************                   
*                                                                               
*                                                                               
MAIN     LA    RE,CRRTVALS         PRECLEAR CRATE INFO AREA                     
         LA    RF,CRRTVALN                                                      
         XCEFL                                                                  
*                                                                               
         MVC   CRFLAG,=C'**COBL**' DUMP IDENTIFIER                              
         MVI   ANYIO,C'N'          SET I/O CHECKER                              
         MVI   BIT,0                                                            
         MVC   CRNUMS,=H'0'        CLEAR TABLE ENTRY COUNTER                    
         EJECT                                                                  
*************************************************************                   
*              CHECK IF REQUIRED DATA HAS BEEN PASSED BY USER                   
*************************************************************                   
*                                                                               
*                                                                               
OKBLOK   DS    0H                                                               
         XC    CRSTATUS,CRSTATUS   CLEAR CRBLK STATUS                           
         OC    CRDMGR,CRDMGR       DID USER PASS ADDR OF DATAMGR                
         BNZ   *+8                                                              
         OI    CRSTATUS,CRDATMIS   SET DATMGR ADDR MISSING                      
*                                                                               
OKBL03   CLC   CRKCMPY,SPACES       COMPANY AND METHOD ARE REQUIRED             
         BNH   OKBL99                                                           
         CLC   CRKMTH,SPACES      (A BLANK METHOD IS ACCEPTABLE)                
         BL    OKBL99                                                           
*                                                                               
OKBL04   CLC   CRKOFFC,SPACES       IS OFFICE PRESENT?                          
         BNH   OKBL99                                                           
*                                                                               
OKBL06   CLC   CRKDEPT,SPACES      IS DEPT PRESENT?                             
         BH    OKBL08              YES - CONTINUE                               
*                                  NO-OTHER KEY FIELDS MUST BE BLANK            
         CLC   CRKSDPT(L'CRKSDPT+L'CRKPERS+L'CRKTSK),SPACES                     
         BH    OKBL99                                                           
         B     OKBLXX                                                           
*                                                                               
OKBL08   CLC   CRKSDPT,SPACES      IS SUB DEPT PRESENT?                         
         BH    OKBL10              YES - CONTINUE                               
         CLC   CRKPERS(L'CRKPERS+L'CRKTSK),SPACES                               
         BNH   OKBLXX                                                           
*                                                                               
OKBL10   CLC   CRKPERS,SPACES      IS PERSON PRESENT?                           
         BH    OKBLXX              YES -OK DONE                                 
         CLC   CRKTSK,SPACES                                                    
         BNH   OKBLXX                                                           
*                                                                               
OKBL99   OI    CRSTATUS,CRLEVMIS   LEVELS OF KEY MISSING                        
*                                                                               
OKBLXX   OC    CRSTATUS,CRSTATUS                                                
         BNZ   MAINEND             ERROR STATUS SET TO INFORM USER              
         EJECT                                                                  
*&&DO                                                                           
*************************************************************                   
*              BUILD OPTION KEY BUFFER                                          
*************************************************************                   
*                                                                               
*                                                                               
OKSET    DS    0H                                                               
         CLI   OKSTAT,0            ONLY NEED TO DO THIS ONCE                    
         BNE   OKSETX                                                           
         MVI   OKSTAT,2            2 MEANS BUFFER NOT AVAILABLE                 
         MVC   COVAIL,CRACOVL      GET A(COVAIL) FROM BLOCK FIRST               
         OC    COVAIL,COVAIL       TEST IF COVAIL PASSED                        
         BZ    OKSETX              NO - DONT ATTEMPT THE CALL                   
         OC    AOKBUFF,AOKBUFF     DID WE ALREADY ACQUIRE THE BUFFER            
         BZ    OKSET0                                                           
         L     R2,AOKBUFF                                                       
         L     R3,LOKBUFF                                                       
         LTR   R3,R3                                                            
         BP    OKSET1                                                           
         DC    H'0'                DIE FOR NOW                                  
*                                                                               
OKSET0   OC    BINSRCH,BINSRCH     HAVE BINSRCH TO READ IT                      
         BZ    OKSETX                                                           
         GOTO1 COVAIL,DMCB,C'LOOK'                                              
         LM    R2,R3,4(R1)         GET A(BUFFER)/L'BUFFER                       
         LTR   R2,R2               TEST FOR ERROR                               
         BNZ   *+6                 YES-DIE FOR NOW                              
         DC    H'0'                                                             
         S     R3,=F'1000000'      LEAVE AT LEAST A MEG FOR OTHERS              
*        BM    OKSETX                                                           
         BNM   *+6                 DIE FOR NOW                                  
         DC    H'0'                                                             
         L     R2,MINALLOC                                                      
         CR    R3,R2               TEST MINIMUM ALLOCATION AVAILABLE            
*        BL    OKSETX              YES                                          
         BNL   *+6                 DIE FOR NOW                                  
         DC    H'0'                                                             
*                                                                               
         GOTO1 COVAIL,DMCB,C'GET ',(R2),(R3)                                    
         LM    R2,R3,4(R1)         GET A(BUFFER)/L'BUFFER                       
         LTR   R2,R2               TEST FOR ALLOCATION ERROR                    
*        BZ    OKSETX              YES                                          
         BNZ   *+6                 YES-DIE FOR NOW                              
         DC    H'0'                                                             
*                                                                               
OKSET1   ST    R2,AOKBUFF          BEGINNING OF KEY PORTION OF BUFFER           
         ST    R3,LOKBUFF          LENGTH OF ACQUIRED BUFFER                    
         MVI   OKSTAT,1            BUFFER ATTAINED                              
         SRL   R3,1                DIVIDE BUFFER BETWEEN KEYS/DATA              
         LA    R5,0(R3,R2)                                                      
         ST    R5,AOKDATA          BEGINNING OF DATA PORTION                    
         LA    RE,0(R3,R5)                                                      
         BCTR  RE,0                                                             
         ST    RE,AOKDATAX         SET A(END OF DATA BUFFER)                    
*                                                                               
         SR    R2,R2                                                            
         D     R2,=A(COVLNTH)      HOW MANY KEYS I CAN HOLD IN R3               
         BCTR  R3,0                                                             
         USING COVTABD,R2                                                       
         L     R2,AOKBUFF                                                       
         MVC   COVKEY(COVKLNTH),XFF   FIRST ENTRY IS HIGHEST KEY FOUND          
         XC    COVPOINT,COVPOINT      CLEAR POINTER TO RECORD AREA              
         LA    R2,COVLNTH(R2)                                                   
         SR    R4,R4                                                            
*                                  R3-MAX KEYS THAT WILL FIT                    
*                                  R4-COUNTER FOR NUMBER OF KEYS                
*                                  R2-KEY PORTION OF COVAIL BUFFER              
*                                  R5-DATA PORTION OF COVAIL BUFFER             
*                                                                               
         LA    R6,KEY              SET UP TO READ OPTIONS                       
         USING CFURECD,R6                                                       
         MVC   CAPKEY,SPACES                                                    
         MVI   CAPKTYP,CAPKTYPQ    X'3E'                                        
         MVI   CAPKSUB,CAPKSUBQ    X'09'                                        
         MVC   CAPKCPY,COKCPY      COMPANY                                      
         MVC   CAPKMTHD,COKMTHD    METHOD                                       
         BAS   RE,HIGH                                                          
         B     *+8                                                              
OKSET2   BAS   RE,SEQ                                                           
         CLC   CAPKEY(CAPKMIN),KEYSAVE    SAME COMPANY AND METHOD?              
         BNE   OKSET6                     IF NOT WE ARE DONE                    
*                                                                               
         MVC   COVKEY(COVKLNTH),CAPKMTHD  SAVE KEY(MET/OFF/DPT/SUB/PER)         
         XC    COVPOINT,COVPOINT          CLEAR POINTER TO RECORD AREA          
         SR    RE,RE                                                            
         ICM   RE,3,CAPRLEN               GET RECORD LENGTH                     
         SH    RE,DATADISP                DEDUCT DISP TO FIRST ELEMENT          
         LA    RE,1(RE,R5)                + 2 FOR HEADER - 1 FOR EOR=1          
         C     RE,AOKDATAX                TEST ROOM FOR DATA                    
         BH    OKSET4                     NO ROOM FOR IT                        
*                                                                               
         STCM  R5,15,COVPOINT      SET POINTER TO DATA                          
         SR    RF,RF                                                            
         ICM   RF,3,CAPRLEN                                                     
         SH    RF,DATADISP                                                      
         LA    RF,1(RF)            RF=L'DATA BUFFER ENTRY                       
         STCM  RF,3,0(R5)          STORE LENGTH OF DATA ENTRY IN HEADER         
         LA    RE,2(R5)            BUMP PAST HEADER   RE=A(DESTINATION)         
         BCTR  RF,0                ADJUST FOR HEADER LENGTH                     
         BCTR  RF,0                                                             
         LR    R0,R6                                                            
         AH    R0,DATADISP         R0=A(SOURCE) DATA PORTION OF RECORD          
         LR    R1,RF               R1=SOURCE LENGTH=DESTINATION LENGTH          
         MVCL  RE,R0               ATTACH RECORD DATA TO HEADER                 
*                                                                               
         SR    RE,RE                                                            
         ICM   RE,3,0(R5)          PUT LENGTH OF DATA RECORD IN RE              
         LA    R5,0(RE,R5)         BUMP R5 TO NEXT DATA AREA                    
*                                                                               
OKSET4   LA    R2,COVLNTH(R2)      BUMP R2 TO NEXT KEY ENTRY                    
         LA    R4,1(R4)            INCREMENT KEY COUNT                          
         CR    R4,R3               TEST IF KEY BUFFER FILLED                    
         BL    OKSET2              NO                                           
*                                                                               
OKSET5   S     R2,=A(COVLNTH)      BACK UP TO LAST ENTRY                        
         L     R1,AOKBUFF                                                       
         MVC   0(COVKLNTH,R1),0(R2) SET HIGHEST KEY IN BUFFER                   
*                                                                               
OKSET6   ST    R4,NOPTKEYS         SET N'OPTION KEYS                            
*                                                                               
OKSETX   DS    0H                                                               
         DROP  R2                                                               
         EJECT                                                                  
*&&                                                                             
*************************************************************                   
*              HANDLE OPTIONS AT TASK CODE LEVEL                                
*************************************************************                   
*                                                                               
*                                                                               
TSKLEV   DS    0H                                                               
         CLC   CRKTSK,SPACES       DID USER PASS A TASK CODE?                   
         BNH   PERLEV                                                           
         MVI   FROM,CRLVTSK        "FROM" TASK CODE LEVEL                       
         CLC   MYCPY(MYMIN),CRKCMPY SAME COMP AND METHOD?                       
         BNE   TSK3                NO -                                         
*                                  SAME OFFICE/DPT/SUB/PERSON/TSK?              
         CLC   MYOFC(L'MYOFC+L'MYDPT+L'MYSDT+L'MYPER+L'MYTSK),CRKOFFC           
         BNE   TSK3                NO - CONTINUE                                
*                                                                               
         OC    ATSKOPTS,ATSKOPTS   SEE IF RECORD SAVED IN BUFFER                
         BZ    TSK3                                                             
         USING CFURECD,R6                                                       
         L     R6,ATSKOPTS         MAKE SURE IT'S THE RIGHT RECORD              
         CLC   CFUKTSK(MYMIN),MYCPY                                             
         BE    *+6                                                              
         DC    H'0'                                                             
         CLC   MYOFC(L'MYOFC+L'MYDPT+L'MYSDT+L'MYPER+L'MYTSK),CFUKOFC           
         BE    *+6                                                              
         DC    H'0'                                                             
         BAS   RE,APPLYN           APPLY TO COBLOCK                             
         TM    CRSTATUS,CRTABEND   DID WE                                       
         B     TSKLXX                                                           
*                                                                               
TSK3     BAS   RE,LOOKUP                                                        
         TM    BIT,NOREC           DID YOU FIND A REC AT TSK LVL                
         BZ    PERLEV              THEN CHECK PERSON LEVEL                      
         OI    BIT,YESREC                                                       
         MVC   ATSKOPTS,AWRKADDR   ADDRESS INTO BUFFER                          
*                                                                               
TSKLXX   TM    CROPTS,CRSHWALL     SHOWALL OPTION PASSED?                       
         BZ    MAINEND                                                          
         EJECT                                                                  
         DROP  R6                                                               
*************************************************************                   
*              HANDLE OPTIONS AT PERSON LEVEL                                   
*************************************************************                   
*                                                                               
*                                                                               
PERLEV   DS    0H                                                               
         CLC   CRKPERS,SPACES       DID USER PASS A PERSON                      
         BNH   SDTLEV                                                           
         MVI   FROM,CRLVPERS       "FROM" PERSON LEVEL                          
         CLC   MYCPY(MYMIN),CRKCMPY SAME COMP AND METHOD?                       
         BNE   PE3                 NO -                                         
*                                  SAME OFFICE/DPT/SUB/PERSON?                  
         CLC   MYOFC(L'MYOFC+L'MYDPT+L'MYSDT+L'MYPER),CRKOFFC                   
         BNE   PE3                 NO - CONTINUE                                
*                                                                               
         OC    APEROPTS,APEROPTS   SEE IF RECORD SAVED IN BUFFER                
         BZ    PE3                                                              
         USING CFURECD,R6                                                       
         L     R6,APEROPTS         MAKE SURE IT'S THE RIGHT RECORD              
         CLC   CFUKCPY(MYMIN),MYCPY                                             
         BE    *+6                                                              
         DC    H'0'                                                             
         CLC   MYOFC(L'MYOFC+L'MYDPT+L'MYSDT+L'MYPER),CFUKOFC                   
         BE    *+6                                                              
         DC    H'0'                                                             
         BAS   RE,APPLYN           APPLY TO COBLOCK                             
         B     PERLXX                                                           
*                                                                               
PE3      BAS   RE,LOOKUP                                                        
         TM    BIT,NOREC           DID YOU FIND A REC AT PERSON LEVEL           
         BZ    SDTLEV              THEN GO CHECK SUBDEPT LEVEL                  
         OI    BIT,YESREC                                                       
         MVC   APEROPTS,AWRKADDR   ADDRESS INTO BUFFER                          
*                                  CLEAR LOWER LEVEL ADDRESSES                  
         XC    APEROPTS+L'APEROPTS(APEROPLN),APEROPTS+L'APEROPTS                
*                                                                               
PERLXX   TM    CROPTS,CRSHWALL     SHOWALL OPTION PASSED                        
         BZ    MAINEND                                                          
         DROP  R6                                                               
         EJECT                                                                  
*************************************************************                   
*              HANDLE OPTIONS AT SUBDEPT LEVEL                                  
*************************************************************                   
*                                                                               
*                                                                               
SDTLEV   DS    0H                                                               
         CLC   CRKSDPT,SPACES       DID USER PASS A SUB DEPARTMENT              
         BNH   DPTLEV                                                           
         MVI   FROM,CRLVSDPT       "FROM" SUBDEPT LEVEL                         
         CLC   MYCPY(MYMIN),CRKCMPY SAME COMP AND METHOD?                       
         BNE   SD3                 NO -                                         
         CLC   MYOFC(L'MYOFC+L'MYDPT+L'MYSDT),CRKOFFC SAME OFF/DPT/SUB          
         BNE   SD3                                                              
*                                                                               
         OC    ASDTOPTS,ASDTOPTS                                                
         BZ    SD3                                                              
         USING CFURECD,R6                                                       
         L     R6,ASDTOPTS         MAKE SURE IT'S THE RIGHT RECORD              
         CLC   CFUKCPY(MYMIN),MYCPY                                             
         BE    *+6                                                              
         DC    H'0'                                                             
         CLC   MYOFC(L'MYOFC+L'MYDPT+L'MYSDT),CFUKOFC                           
         BE    *+6                                                              
         DC    H'0'                                                             
         BAS   RE,APPLYN           APPLY TO COBLOCK                             
         B     SDTLXX                                                           
*                                                                               
SD3      MVC   MYPER,SPACES        FORCE RE-BUILDING OF BUFFER FOR              
*                                  LOWER LEVEL OPTIONS                          
         BAS   RE,LOOKUP                                                        
         TM    BIT,NOREC           WAS A REC FOUND AT SUDBEPT LEVEL             
         BZ    DPTLEV                                                           
         OI    BIT,YESREC                                                       
         MVC   ASDTOPTS,AWRKADDR   ADDR WITHIN BUFFER                           
*                                  CLEAR LOWER LEVEL ADDRESSES                  
         XC    ASDTOPTS+L'ASDTOPTS(ASDTOPLN),ASDTOPTS+L'ASDTOPTS                
*                                                                               
SDTLXX   TM    CROPTS,CRSHWALL     SHOWALL OPTION PASSED?                       
         BZ    MAINEND                                                          
         DROP  R6                                                               
         EJECT                                                                  
*************************************************************                   
*              HANDLE OPTIONS AT DEPT LEVEL                                     
*************************************************************                   
*                                                                               
*                                                                               
DPTLEV   DS    0H                                                               
         CLC   CRKDEPT,SPACES       DID USER PASS A DEPARTMENT                  
         BNH   OFFLEV                                                           
         MVI   FROM,CRLVDEPT       "FROM" DEPT LEVEL                            
         CLC   MYCPY(MYMIN),CRKCMPY SAME COMP AND METHOD?                       
         BNE   DP3                 NO -                                         
         CLC   MYOFC(L'MYOFC+L'MYDPT),CRKOFFC   SAME OFFICE/DEPT CODE           
         BNE   DP3                                                              
*                                                                               
         OC    ADEPOPTS,ADEPOPTS                                                
         BZ    DP3                                                              
         USING CFURECD,R6                                                       
         L     R6,ADEPOPTS         MAKE SURE IT'S THE RIGHT RECORD              
         CLC   CFUKCPY(MYMIN),MYCPY                                             
         BE    *+6                                                              
         DC    H'0'                                                             
         CLC   MYOFC(L'MYOFC+L'MYDPT),CFUKOFC  SAME OFFICE/DEPT CODE            
         BE    *+6                                                              
         DC    H'0'                                                             
         BAS   RE,APPLYN           APPLY TO COBLOCK                             
         B     DPTLXX                                                           
*                                                                               
DP3      MVC   MYSDT,SPACES        FORCE RE-BUILDING OF BUFFER FOR              
         MVC   MYPER,SPACES        LOWER LEVEL OPTIONS                          
         BAS   RE,LOOKUP                                                        
         TM    BIT,NOREC           WAS A REC FOUND AT DEPT LEVEL                
         BZ    OFFLEV                                                           
         OI    BIT,YESREC                                                       
         MVC   ADEPOPTS,AWRKADDR   ADDR WITHIN BUFFER                           
*                                  CLEAR LOWER LEVEL ADDRESSES                  
         XC    ADEPOPTS+L'ADEPOPTS(ADEPOPLN),ADEPOPTS+L'ADEPOPTS                
*                                                                               
DPTLXX   TM    CROPTS,CRSHWALL     OPTION SHOWALL PASSED                        
         BZ    MAINEND                                                          
         DROP  R6                                                               
         EJECT                                                                  
*************************************************************                   
*              HANDLE OPTIONS AT OFFICE LEVEL                                   
*************************************************************                   
*                                                                               
*                                                                               
OFFLEV   DS    0H                                                               
         CLC   CRKOFFC,SPACES       DID USER PASS AN OFFICE                     
         BH    *+12                                                             
         OI    CRSTATUS,CRLEVMIS                                                
         B     MAINEND                                                          
         MVI   FROM,CRLVOFF        "FROM" OFFICE LEVEL                          
         CLC   MYCPY(MYMIN),CRKCMPY SAME COMP AND METHOD?                       
         BNE   OF3                 NO -                                         
         CLC   MYOFC,CRKOFFC       SAME OFFICE CODE                             
         BNE   OF3                                                              
*                                                                               
         OC    AOFFOPTS,AOFFOPTS   DO WE HAVE A BUFFER ADDRESS?                 
         BZ    OF3                                                              
         USING CFURECD,R6                                                       
         L     R6,AOFFOPTS         MAKE SURE IT'S THE RIGHT RECORD              
         CLC   CFUKCPY(MYMIN),MYCPY                                             
         BE    *+6                                                              
         DC    H'0'                                                             
         CLC   CFUKOFC,MYOFC                                                    
         BE    *+6                                                              
         DC    H'0'                                                             
         BAS   RE,APPLYN           APPLY TO COBLOCK                             
         B     OFFLXX                                                           
*                                                                               
OF3      MVC   MYDPT,SPACES        FORCE RE-BUILDING OF BUFFER FOR              
         MVC   MYSDT,SPACES        LOWER LEVEL OPTIONS                          
         MVC   MYPER,SPACES                                                     
         BAS   RE,LOOKUP                                                        
         TM    BIT,NOREC           WAS A REC FOUND AT OFFICE LEVEL?             
         BZ    OF5                 YES CONTINUE                                 
         TM    BIT,YESREC          WAS THERE AT LEAST 1 REC FOUND AT            
         BO    OF5                 ANOTHER LEVEL?                               
         OI    CRSTATUS,CRNORATE   SET ERROR FOR USER                           
         B     MAINEND                                                          
*                                                                               
OF5      MVC   AOFFOPTS,AWRKADDR   ADDR WITHIN BUFFER                           
*                                  CLEAR LOWER LEVEL ADDRESSES                  
         XC    AOFFOPTS+L'AOFFOPTS(AOFFOPLN),AOFFOPTS+L'AOFFOPTS                
*                                                                               
OFFLXX   DS    0H                                                               
         B     XIT                                                              
         DROP  R6                                                               
         EJECT                                                                  
*************************************************************                   
*              END OF MAIN ROUTINES                                             
*************************************************************                   
*                                                                               
*                                                                               
MAINEND  DS    0H                                                               
         MVC   MYCPY,CRKCMPY       SAVE WHAT WE DID THIS TIME                   
         MVC   MYMET,CRKMTH                                                     
         MVC   MYOFC,CRKOFFC                                                    
         MVC   MYDPT,CRKDEPT                                                    
         MVC   MYSDT,CRKSDPT                                                    
         MVC   MYPER,CRKPERS                                                    
         MVC   MYTSK,CRKTSK                                                     
*                                                                               
MAINEND2 CLI   ANYIO,C'N'          TEST ANY I/O DONE BY GETCAP                  
         BE    MAINENDX            NO                                           
*                                                                               
         ICM   R1,15,CRAKEY        RESET READS FOR USER                         
         BZ    MAINENDX            USER HAS NOT PASSED A KEY                    
         MVC   KEY,0(R1)                                                        
         BAS   RE,READ                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
MAINENDX B     XIT                                                              
         EJECT                                                                  
*************************************************************                   
*              ROUTINE CONTROLS I/O OF OPTION RECORDS                           
*************************************************************                   
*                                                                               
*                                                                               
LOOKUP   NTR1  ,                                                                
         NI    BIT,X'FF'-NOREC                                                  
         LA    R6,KEY                                                           
         USING CFURECD,R6                                                       
         MVC   CFUKEY,SPACES                                                    
         MVI   CFUKTYP,CFUKTYPQ    X'3E'                                        
         MVI   CFUKSUB,CFUKSUBQ    X'06'                                        
         MVC   CFUKCPY,CRKCMPY     COMPANY CODE                                 
         MVC   CFUKOFC,CRKOFFC     OFFICE                                       
         CLI   FROM,CRLVOFF        OFFICE LEVEL LOOKUP?                         
         BE    LOOKUP06            YES - GO DO IT                               
         MVC   CFUKDPT,CRKDEPT     MOVE DEPT IN                                 
         CLI   FROM,CRLVDEPT       DEPT LEVEL LOOKUP?                           
         BE    LOOKUP06            YES - GO DO IT                               
         MVC   CFUKSDPT,CRKSDPT     MOVE SUBDEPT IN                             
         CLI   FROM,CRLVSDPT       SUBDEPT LEVEL LOOKUP?                        
         BE    LOOKUP06            YES - GO DO IT                               
         MVC   CFUKPERS,CRKPERS     MOVE PERSON IN                              
         CLI   FROM,CRLVPERS       PERSON LEVEL LOOKUP?                         
         BE    LOOKUP06            YES - GO DO IT                               
         MVC   CFUKTSK,CRKTSK      MOVE TASK CODE IN                            
         CLI   FROM,CRLVTSK        TASK CODE LEVEL LOOKUP?                      
         BE    LOOKUP06            YES - GO DO IT                               
         DC    H'0'                                                             
*                                                                               
LOOKUP06 DS    0H                                                               
*&&DO                                                                           
*OOKUP06 CLI   OKSTAT,1            DO WE HAVE A OPTION KEY BUFFER?              
         BNE   LOOKUP12                                                         
         L     R2,AOKBUFF                                                       
         USING COVTABD,R2          1ST KEY IN BUFFER IS HIGHEST KEY             
         CLC   CAPKMTHD(COVKLNTH),COVKEY     CHECK KEY NOT PAST EOB             
         BH    LOOKUP12                                                         
         LA    R2,COVLNTH(R2)      SEE IF KEY WAS IN BUFFER                     
         L     R3,NOPTKEYS                                                      
         LTR   R3,R3                                                            
         BZ    LOOKUP99            (NO KEYS AT ALL)                             
         GOTO1 BINSRCH,DMCB,(X'02',CAPKMTHD),(R2),(R3),COVLNTH,        X        
               (0,COVKLNTH),(R3)                                                
         CLI   DMCB,X'01'                                                       
         BE    LOOKUP99            NO KEY SO SKIP IO                            
         L     R2,0(R1)                                                         
         CLC   CAPKMTHD(COVKLNTH),COVKEY     TEST SAME LEVEL                    
         BNE   LOOKUP99            NO-NO OPTIONS                                
*                                                                               
         MH    R3,=Y(COVLNTH)      COMPUTE END OF OPTION KEYS                   
         A     R3,AOKBUFF                                                       
         LA    R3,COVLNTH-1(R3)                                                 
*                                                                               
         MVC   CAPKMTHD(COVKLNTH),0(R2) SET RETURNED KEY                        
         ICM   R5,15,COVPOINT      GET A(DATA)                                  
         BZ    LOOKUP12            NOT THERE - READ THE FILE                    
*                                                                               
         LR    RE,R6               REBUILD RECORD FROM KEY AND DATA             
         AH    RE,DATADISP         RE=A(MOVE DESTINATION)                       
         SR    R1,R1               R1=L'SOURCE DATA                             
         ICM   R1,3,0(R5)          GET LENGTH OF DATA + HEADER                  
         BCTR  R1,0                ADJUST FOR HEADER                            
         BCTR  R1,0                                                             
         LA    RF,1(R1)            ADD ONE BACK IN FOR EOR                      
         LR    R4,RF               SAVE DATA LEN IN R4                          
         LA    R0,2(R5)            R0=A(MOVE SOURCE)                            
         MVCL  RE,R0               ATTACH DATA TO RECORD                        
         AH    R4,DATADISP         COMPUTE FULL REC LEN                         
         STCM  R4,3,CAPRLEN                                                     
         B     LOOKUP14                                                         
*&&                                                                             
*                                                                               
LOOKUP12 BAS   RE,HIGH                                                          
         CLC   CFUKEY,KEYSAVE                                                   
         BNE   LOOKUP99                                                         
*                                                                               
LOOKUP14 DS    0H                  GO PUT TO OPTIMIZE BUFFER                    
         XC    AWRKADDR,AWRKADDR   CLEAR WORK ADDRESS                           
*&&DO                                                                           
                                                                                
         OC    COABUFF,COABUFF     WAS A BUFFER PASSED                          
         BNZ   *+14                YES - CONTINUE                               
         XC    BUFFNOW,BUFFNOW     NO -  MAKER SURE BUFFNOW IS CLEAR            
         B     LOOKUP20                                                         
*                                                                               
         OC    BUFFNOW,BUFFNOW     FORGET IT IF BUFFNOW IS NOT SET              
         BZ    LOOKUP20                                                         
         ICM   R3,3,CAPRLEN                                                     
         LA    RE,BUFFNOW          RE=A(RECEIVING)                              
         LA    R1,0(R3,RE)                                                      
         C     R1,AENDBUFF         WILL RECORD FIT IN BUFFER                    
         BNH   LOOKUP18            YES - THEN PUT IT THERE                      
         XC    BUFFNOW,BUFFNOW     NO -  WE RAN OUT OF SPACE                    
         B     LOOKUP20                                                         
*                                                                               
LOOKUP18 LR    RF,R3               R3=LENGTH OF DATA RECORD                     
         LR    R1,RF               RF=LEN OF RECV  R1=LEN OF SEND               
         LR    R0,R6               R0=A(SENDING)                                
         MVCL  RE,R0               SAVE DATA RECORD IN OPTIMIZED BUFFER         
         LA    RE,BUFFNOW          MARK THE END OF THE RECORD                   
         ST    RE,AWRKADDR         PASS ADDR OF RECORD BACK TO CALLER           
         AR    RE,R3                                                            
         ST    RE,BUFFNOW          UPDATE BUFFNOW ADDRESS AS TO WHERE           
         B     LOOKUP20            WE ARE IN THE BUFFER                         
*&&                                                                             
LOOKUP20 BAS   RE,APPLYN           GO PUT THE OPTIONS TO COBLOCK                
         B     LOOKUPX                                                          
*                                                                               
LOOKUP99 OI    BIT,NOREC           NO REC FOUND AT THIS LEVEL                   
*                                                                               
LOOKUPX  B     XIT                                                              
         EJECT                                                                  
*************************************************************                   
*              ROUTINE APPLIES PROFILE ELEMENTS TO COBLOCK                      
*************************************************************                   
*                                                                               
*        INPUT                     R6=A(RECORD)                                 
*                                                                               
         USING CFURECD,R6          COST CRATE RECORD                            
         USING CREELD,R4           CRATE DETAIL ELEMENT                         
APPLYN   NTR1  ,                                                                
         USING CRRTVALS,R2                                                      
         LA    R2,CRRTVALS         COST RATE INFO TABLE                         
         LR    R4,R6                                                            
         MVI   ELCODE,CREELQ       X'CA'                                        
         BAS   RE,GETEL                                                         
         B     *+8                                                              
APL02    BAS   RE,NEXTEL           NEXT OPTION ELEMENT                          
         BNE   APL99                                                            
*                                                                               
         CLC   CRNUMS,=Y(CRRMAX)   DID WE REACH THE MAX (30)                    
         BL    *+12                                                             
         OI    CRSTATUS,CRTABEND   SET BIT FOR USER                             
         B     MAINEND             AND RETURN                                   
*                                                                               
         MVC   CRRSTDTE,CRESTDT    START DATE                                   
         MVC   CRRENDTE,CREENDT    END DATE                                     
         MVC   CRRPCODE,CREPNUM    PAYROLL CODE                                 
         MVC   SVPNUM,CRRPCODE     SAVE PAYROLL CODE TO READ METHOD             
         BAS   RE,GETTYPE          GET PAYROLL TYPE FROM MTHD REC               
         MVC   CRRPTYPE,WORK       PAYROLL TYPE                                 
         MVC   CRRATE,CREAMT       RATE                                         
         MVC   CRRLEVEL,FROM       WHAT LEVEL IS THIS FROM                      
         MVC   CRRLDATE,CREDLAST   DATE LAST CHANGED                            
         MVC   CRRWHO,CREWHO       WHO LAST CHANGED                             
         MVC   CRRFLAG,CRESTAT     STATUS BYTE                                  
*                                                                               
         LA    R2,CRRTVALN(R2)     BUMP TO NEXT ENTRY                           
         ICM   R1,3,CRNUMS           UPDATE # OF ENTRIES                        
         LA    R1,1(R1)                                                         
         STCM  R1,3,CRNUMS                                                      
         B     APL02                                                            
*                                                                               
APL99    B     XIT                                                              
*                                                                               
         DROP  R2,R4                                                            
         EJECT                                                                  
***********************************************************************         
*        GET TYPE FROM METHOD REC                                               
*        INPUT - SVPNUM HAS PAYROLL CODE NUMBER                                 
*        OUTPUT - BYTE CONTAINS PARYOLL TYPE                                    
***********************************************************************         
*                                                                               
         USING CAHRECD,R3                                                       
GETTYPE  NTR1                                                                   
         XC    BYTE,BYTE           BINARY ZERO'S MEANS PAYCODE NOT              
*                                  ASSIGNED TO THIS METHOD                      
         LA    R3,KEY2                                                          
         MVC   KEY2,SPACES                                                      
         MVC   CAHKEY,SPACES                                                    
         MVI   CAHKTYP,CAHKTYPQ    ALLOCATION METHOD RECORD TYPE BY NUM         
         MVI   CAHKSUB,CAHKSUBQ    METHOD BY NUM SUB TYPE                       
         MVC   CAHKCPY,CRKCMPY     COMPANY                                      
         MVC   CAHKMTHD,CRKMTH     METHOD NUMBER                                
         XC    CAHKOFC,CAHKOFC                                                  
         MVC   SAVEKEY,KEY2                                                     
         GOTO1 CRDMGR,DMCB,=C'DMRDHI ',=C'ACCDIR  ',KEY2,KEY2                   
         CLC   KEY2(L'KEY2),SAVEKEY                                             
         BE    *+12                                                             
         OI    CRSTATUS,CRNOMETH   SET BIT FOR USER                             
         B     GETTYPX                                                          
*                                                                               
         MVC   DA,CFUKDA                                                        
         GOTO1 CRDMGR,DMCB,=C'GETREC ',=C'ACCMST  ',DA,IO2,WORK                 
*                                                                               
         USING PATELD,R3                                                        
         MVI   ELCODE,PATELQ                                                    
         BAS   RE,GETEL                                                         
         B     GT20                                                             
GT10NX   BAS   RE,NEXTEL                                                        
GT20     BNE   GETTYPX                                                          
         CLC   SVPNUM,PATNUM       MATCH ON PAYROLL CODE NUMBER                 
         BNE   GT10NX                                                           
         MVC   BYTE,PATTYPE        PAYROLL TYPE                                 
*                                                                               
GETTYPX  TM    CRSTATUS,CRNOMETH                                                
         BO    MAINEND                                                          
         B     XIT                                                              
         DROP  R3                                                               
*************************************************************                   
*              I/O AND ODDMENTS                                                 
*************************************************************                   
*                                                                               
*                                                                               
READ     MVC   COMMAND,DMREAD                                                   
         B     ACCALL                                                           
*                                                                               
HIGH     MVC   COMMAND,DMRDHI                                                   
         MVC   KEYSAVE,KEY                                                      
         B     ACCALL                                                           
*                                                                               
SEQ      MVC   COMMAND,DMRSEQ                                                   
*                                                                               
ACCALL   NTR1                                                                   
         MVI   ANYIO,C'Y'                                                       
         GOTO1 CRDMGR,DMCB,(0,COMMAND),(0,ACCOUNT),KEY,IO                       
         TM    DMCB+8,X'10'                                                     
         BO    NO                                                               
         CLI   DMCB+8,0                                                         
         BE    YES                                                              
         DC    H'0'                                                             
*                                                                               
NO       LA    R1,1                                                             
         B     *+6                                                              
*                                                                               
YES      SR    R1,R1                                                            
         LTR   R1,R1                                                            
*                                                                               
XIT      XIT1                                                                   
*                                                                               
GETELIO  LA    R4,IO                                                            
         GETEL R4,DATADISP,ELCODE                                               
         EJECT                                                                  
*************************************************************                   
*              CONSTANTS, TABLES                                                
*************************************************************                   
*                                                                               
*                                                                               
MINALLOC DC    F'10000'            MINIMUM BUFFER STORAGE ALLOCATION            
DATADISP DC    H'49'                                                            
XFF      DC    (L'CFUKEY)X'FF'                                                  
*                                                                               
DMRDHI   DC    C'DMRDHI '                                                       
DMREAD   DC    C'DMREAD '                                                       
DMRSEQ   DC    C'DMRSEQ '                                                       
ACCOUNT  DC    C'ACCOUNT'                                                       
ACCDIR   DC    C'ACCDIR '                                                       
ACCMST   DC    C'ACCMST '                                                       
         EJECT                                                                  
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*              WORKING STORAGE FOR GETRTE                                       
*                                                                               
*                                                                               
CRT      DSECT                                                                  
RELO     DS    A                                                                
WORK     DS    CL64                                                             
DUB      DS    D                                                                
DMCB     DS    CL24                                                             
AENDBUFF DS    A                                                                
COVAIL   DS    V                                                                
BINSRCH  DS    V                                                                
         SPACE 1                                                                
HALF     DS    H                                                                
BYTE     DS    XL1                                                              
KEYSAVE  DS    CL42                                                             
SAVEKEY  DS    CL42                                                             
**********                                                                      
KEY      DS    CL42                                                             
KEY2     DS    CL42                                                             
LENGTH   DS    H                                                                
STATUS   DS    CL1                                                              
         DS    CL4                                                              
DA       DS    XL4                                                              
ELEMENTS DS    0C                                                               
IO       DS    2000C                                                            
         ORG   KEY                                                              
IO2      DS    2000C                                                            
**********                                                                      
COMMAND  DS    CL8                                                              
ELCODE   DS    CL1                                                              
         SPACE 1                                                                
SPACES   DS    CL132                                                            
ANYIO    DS    CL1                                                              
FROM     DS    CL1                                                              
SVPNUM   DS    XL1                                                              
BIT      DS    XL1                 STATUS BIT                                   
NOREC    EQU   X'80'               NO REC EXISTS FOR THIS LEVEL                 
YESREC   EQU   X'40'               A REC HAS BEEN FOUND AT A LEVEL              
*                                                                               
CRLVSET  DS    CL1                 LEVEL SETTINGS                               
CRLVOFF  EQU   C'O'                OFFICE LEVEL                                 
CRLVDEPT EQU   C'D'                DEPT LEVEL                                   
CRLVSDPT EQU   C'S'                SUBDEPT LEVEL                                
CRLVPERS EQU   C'P'                PERSON LEVEL                                 
CRLVTSK  EQU   C'T'                TASK CODE LEVEL                              
CRTX     EQU   *                                                                
         SPACE 1                                                                
CRTED    DSECT                                                                  
*                                                                               
       ++INCLUDE ACCAPCRBLK                                                     
         EJECT                                                                  
*              GETRTE SAVED STORAGE IN CRBLK                                    
*                                                                               
         ORG   CRBLK                                                            
CRSTORE  DS    0C                  BEGINNING OF STORAGE                         
CRFLAG   DS    CL8                 **COBL**                                     
OKSTAT   DS    X                                                                
MYSAV    DS    0C                                                               
MYCPY    DS    CL(L'CRKCMPY)       COMPANY                                      
MYMET    DS    CL(L'CRKMTH)        METHOD                                       
MYOFC    DS    CL(L'CRKOFFC)       OFFICE                                       
MYMIN    EQU   *-MYSAV             MINIMUM KEY REQUIRED                         
MYDPT    DS    CL(L'CRKDEPT)       DEPT                                         
MYSDT    DS    CL(L'CRKSDPT)       SUB DEPT                                     
MYPER    DS    CL(L'CRKPERS)       PERSON                                       
MYTSK    DS    CL(L'CRKTSK)        TASK CODE                                    
MYSAVLN  EQU   *-MYSAV                                                          
*                                                                               
AOFFOPTS DS    A                   A(OFFICE OPTION RECORD)                      
ADEPOPTS DS    A                   A(DEPT OPTION RECORD)                        
ASDTOPTS DS    A                   A(SUB DPT OPTION RECORD)                     
APEROPTS DS    A                   A(PERSON OPTION RECORD)                      
ATSKOPTS DS    A                   A(TASK OPTION RECORD)                        
AOFFOPLN EQU   (*-AOFFOPTS)-L'AOFFOPTS LENGTH OF LOWER LVLS TO CLEAR            
ADEPOPLN EQU   (*-ADEPOPTS)-L'ADEPOPTS                                          
ASDTOPLN EQU   (*-ASDTOPTS)-L'ASDTOPTS                                          
APEROPLN EQU   (*-APEROPTS)-L'APEROPTS                                          
*                                                                               
AWRKADDR DS    A                   A(GENERAL AREA TO SAVE/PASS AN ADDR)         
CRSPARER EQU   (CRRESLN)-(*-CRRBLK) AMOUNT OF RESERVED LEFT IN CRBLK            
*                                                                               
*                                                                               
* ACGENFILE                                                                     
* DDSPLWORKD                                                                    
* ACCAP03DST                                                                    
       ++INCLUDE ACGENFILE                                                      
       ++INCLUDE ACCAP03DST                                                     
*                                                                               
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'031ACGETRTEX 05/01/02'                                      
         END                                                                    
