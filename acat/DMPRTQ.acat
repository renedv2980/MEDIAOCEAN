*          DATA SET DMPRTQ     AT LEVEL 006 AS OF 06/21/11                      
*CATALP DMPRTQ                                                                  
                                                                                
* R2 POINTS TO ORIGINAL CALLERS PARAM LIST                                      
*                                                                               
* A(ACTION)    X'20' SET TO SHOW CALLER IS DMPRTQM                              
* A(FILE NAME) GENERICALLY C'PRTQUE' SPECIFICALLY C'PRTQ1'/C'PRTQ2'/.           
* A(KEY)                                                                        
* A(PRTQ REC)  TYPICALLY A 133 BYTE PRINT LINE                                  
* A(PRTQ BUF)  MUST BE LARGE ENOUTH TO HOLD A DISK RECORD                       
*                                                                               
* R1 POINTS TO DATAMGR PARAMS WITH PARAM4=FILNUM/A(DTF)                         
         TITLE 'DMPRTQ - ROUTINES TO PROCESS PRTQUE'                            
         PRINT NOGEN                                                            
DMPRTQUE CSECT                                                                  
         ENTRY PRTQLST                                                          
         ENTRY PRTQSTMP                                                         
         ENTRY PRTQXPE                                                          
         ENTRY PRTQERRS                                                         
*                                                                               
         NMOD1 WRKX-WRKD,**PRTQ**,RA,R9,R8                                      
         USING WRKD,RC                                                          
         ST    R2,APARM                                                         
         MVC   PARM1(24),0(R2)     SAVE PARAMS                                  
         LM    R2,R4,PARM3                                                      
         SAM24                     FORCE 24-BIT MODE                            
*                                                                               
         USING UKRECD,R2           R2=A(KEY)                                    
         USING PQPLD,R3            R3=A(PRINT LINE)                             
         LA    R4,0(R4)            R4=A(CALLERS BUFFER)                         
*                                                                               
DMPRTQ1  CLI   PRTQLST,0           TEST FIRST CALL                              
         BNE   DMPRTQ2                                                          
         BRAS  RE,BLDPQL           BUILD LIST OF PRTQ FILES                     
*                                                                               
DMPRTQ2  BRAS  RE,INIPQW           INITIALIZE LOCAL WORKING STORAGE             
         USING SKBUFFD,R7          R7=A(SAVE AREA AT END OF BUFFER)             
         EJECT                                                                  
* VALIDATE PQFILE ACTION AND PASS CONTROL TO ROUTINE                            
*                                                                               
DMPRTQA  L     RE,PARM1            RE=A(PQFILE ACTION)                          
         LA    RF,PQACTNS          RF=A(ACTION TABLE)                           
DMPRTQA1 CLI   1(RF),0             TEST END OF TABLE                            
         BE    DMPRTQE                                                          
         CLC   1(3,RF),0(RE)       MATCH ON FIRST THREE CHRS                    
         BE    DMPRTQB                                                          
         LA    RF,12(RF)                                                        
         B     DMPRTQA1                                                         
*                                                                               
DMPRTQB  MVC   SKACTN,0(RF)        SAVE THIS ACTION VALUE                       
         MVC   SAVADDR,8(RF)       SAVE A(ROUTINE REQUIRED)                     
*                                                                               
DMPRTQB1 TM    SAVADDR,X'01'       GO DIRECT FOR SPECIAL ACTIONS                
         BO    DMPRTQD                                                          
*                                                                               
DMPRTQB2 TM    SAVADDR,X'80'       ACTION REQUIRES SPECIFIC PRTQ                
         BZ    DMPRTQB3                                                         
         TM    USERFLAG,X'80'                                                   
         BO    *+16                                                             
         CLI   SKACTN,X'41'        INDEX ACTION CAN WORK BOTH WAYS              
         BE    NDXNPQ                                                           
         B     DMPRTQE             DIE IF CALLER DOES NOT SPECIFY PRTQ          
         BRAS  RE,SETPQ                                                         
         B     DMPRTQD                                                          
*                                                                               
DMPRTQB3 TM    SAVADDR,X'40'       ACTION REQUIRES PRTQ NUM IN BUFFER           
         BZ    DMPRTQC                                                          
         CLI   SKINTNO,0                                                        
         BE    DMPRTQE             DIE IF NO INTERNAL NUM IN BUFFER             
         TM    USERFLAG,X'80'                                                   
         BZ    *+14                                                             
         CLC   USERINUM,SKINTNO    TEST IF USER SWITCHED PRTQ FILES             
         BNE   DMPRTQE             YES DIE                                      
         MVC   USERINUM,SKINTNO                                                 
         BRAS  RE,WHATPQI                                                       
         MVC   PQFILE,USERPRTQ     SET ALPHA DMGR PRTQ ID                       
         MVC   VPQFILE,USERXNUM    SET EXTERNAL NUM AND A(DTF)                  
         CLC   SKEXTNO,USERXNUM                                                 
         BNE   DMPRTQE             DOUBLE CHECK BUFFER SAVE AREA                
         BRAS  RE,SETPQ            SET CIDATA                                   
         B     DMPRTQD                                                          
*                                                                               
DMPRTQC  EQU   *                   ACTION WILL DETERMINE WHAT PRTQ              
*                                                                               
DMPRTQD  L     RF,SAVADDR          PASS CONTROL TO ROUTINE FOR ACTION           
         LA    RF,0(RF)                                                         
         BR    RF                                                               
*                                                                               
DMPRTQE  DC    H'0',C'INV PQ ACTN '                                             
*                                                                               
DMPRTQX  MVC   SKLACTN,SKACTN      SAVE LAST ACTION VALUE                       
DMPRTQY  L     R1,APARM                                                         
         MVC   8(1,R1),PARM3                                                    
         XMOD1 1                                                                
         EJECT                                                                  
* CALL TO INITIALISE BUFFER - PASS BACK IN BUFFER                               
*                                                                               
* AL4    V(PQTAB)                                                               
* AL4    V(PQFILE)                                                              
* AL4    DISPLACEMENT OF START OF BUFFER SAVE                                   
* CL40   CIDATA FROM PQTAB                                                      
* CL12   N/D                                                                    
* CL40   PRTQ LIST FRON PRTQL                                                   
*                                                                               
BUF      TM    USERFLAG,X'80'      TEST IF CALLER SPECIFIED PRTQ                
         BO    BUF1                                                             
         XC    0(8,R4),0(R4)       SET UNKNOWN PRTQ DATA                        
         XC    CIDATA,CIDATA                                                    
         B     BUF2                                                             
BUF1     BRAS  RE,SETPQ            GET CIDATA FOR PRTQ                          
         MVC   0(4,R4),VPQTAB                                                   
         MVC   4(4,R4),VPQFILE                                                  
BUF2     LR    R0,R7               GET DISPLACEMENT TO SAVE AREA                
         SR    R0,R4                                                            
         ST    R0,8(R4)                                                         
         MVC   12(L'CIDATA,R4),CIDATA                                           
         MVC   64(PRTQLSTX-PRTQLST,R4),PRTQLST                                  
         MVC   FUL(1),SKACTN                                                    
         XC    SKLABEL(SKEND-SKLABEL),SKLABEL                                   
         MVC   SKLABEL,MYLABEL                                                  
                                                                                
* RESTORE FROM SAVE AREA THE EXTRACT OF DATA BACK TO BUFFER                     
*                                                                               
BUR      CLI   FUL,ACTNBUR         EXIT IF RESTORE NOT REQUIRED                 
         BNE   DMPRTQX                                                          
         LR    RE,R3               POINT TO PRINT LINE RECORD                   
         USING KSBUFFD,RE                                                       
         MVC   SKINDEX,KSINDEX     RESTORE BUFFER DATA FROM RECORD AREA         
         MVC   SKFSTCI,KSFSTCI                                                  
         MVC   SKINTNO,KSINTNO                                                  
         MVC   SKEXTNO,KSEXTNO                                                  
         MVC   UKINDEX,KSINDEX     RESTORE USERS KEY                            
         B     DMPRTQX                                                          
                                                                                
* SAVE EXTRACT OF SAVE DATA IN BUFFER - ENOUGH TO IDENTIFY FILE                 
*                                                                               
BUS      LR    RE,R3               POINT TO PRINT LINE RECORD                   
         USING KSBUFFD,RE                                                       
         MVC   KSINDEX,SKINDEX     SAVE BUFFER DATA INTO RECORD AREA            
         MVC   KSFSTCI,SKFSTCI                                                  
         MVC   KSINTNO,SKINTNO                                                  
         MVC   KSEXTNO,SKEXTNO                                                  
         B     DMPRTQX                                                          
         DROP  RE                                                               
         EJECT                                                                  
* RETURN A(PRTQLST) IN UKUSRINF                                                 
*                                                                               
GLI      LA    RE,PRTQLST          POINT TO LIST OF PRTQ FILES                  
         ST    RE,UKUSRINF                                                      
         LA    RE,PRTQXPE          POINT TO LIST OF PRTQ INDEX DATA             
         ST    RE,UKUSRINF+4                                                    
         B     DMPRTQY                                                          
                                                                                
* RETURN PRTQ FILE NAME IN UKUSRINF FROM USERID NUM IN UKSRCID                  
*                                                                               
GFI      MVC   USERIDNO,UKSRCID    SET USER ID NUM FROM KEY                     
         BRAS  RE,WHATPQU                                                       
         MVC   UKINFO(1),USERINUM  RETURN INT/EXT FILE NUMS IN KEY              
         MVC   UKINFO+1(1),USERXNUM                                             
         MVC   UKREPNOX(3),USERADTF                                             
         MVC   UKUSRINF,USERPRTQ   RETURN DMCB PRTQ NAME IN KEY                 
         B     DMPRTQY                                                          
                                                                                
* INITIALISE PRTQUE REQUESTED BY CALLER                                         
*                                                                               
INI      OC    CIDATA(4),CIDATA    TEST IF INITIALISED ALREADY                  
         BNZ   DMPRTQX                                                          
         L     RF,=A(IPQ)                                                       
         BASR  RE,RF               CALL INITIALISE ROUTINE AND EXIT             
         B     DMPRTQX                                                          
         EJECT                                                                  
* READ PQFILE SEQUENTIALLY FILTERED BY KEY.                                     
* PARM4 POINTS TO A PRINT LINE AREA.                                            
* LENGTH OF TOTAL REC PASSED BACK AT PARM4-4(2).                                
* SET PARM1(1) TO X'80' TO PERFORM INDEX READ FOR UPDATE.                       
* USER INDEX IS NOT CHANGED - ALL NON ZERO FIELDS ARE FILTERS.                  
*                                                                               
* IF INDEX+30=X'80' BIT IS OFF THEN INDEX DATA WILL BE RETURNED IN              
* PARM4 WITH RECORD FORMAT AS FOLLOWS. CTXX IS LAST 4 BYTES OF INDEX.           
*                                                                               
* ..SOFSOF..CTXX.....##..DA.F.<--1ST 20 CHR NDX-->                              
*                                                                               
* IF INDEX+30=X'80' BIT IS ON THEN FILE DATA WILL BE RETURNED BETWEEN           
* HDR AND TRL RECORDS BY SUCESSIVE CALLS AS FOLLOWS.                            
*                                                                               
* ..SOFSOF..CTXX.....##..DA.F.<--1ST 20 CHR NDX--><--REPORT DATA.....>          
* REC-1.....                                                                    
* REC-2.....                                                                    
* ..EOFEOF..                                                                    
*                                                                               
* IF INDEX+31=X'40' ON FIRST CALL THEN FILE HAS ALREADY BEEN LOCATED BY         
* A PRIOR INDEX CALL. MUST ALSO SET INDEX+30=X'80' FOR DATA MODE.               
*                                                                               
* RETURN UKUSRINF+5(1) X'01'=SOF REC,X'40'=ERR REC,X'80'=EOF REC                
* RETURN UKUSRINF+6(2) DISP TO ARCLINE IN USER BUFFER                           
*                                                                               
* IF A READ ERROR THEN EOF LABEL SET TO EOFERR                                  
*                                                                               
SEQ      XC    UKUSRINF+5(3),UKUSRINF+5 CLEAR RETURN AREA                       
         TM    SKSCTRL,X'01'                                                    
         BO    SEQ1                                                             
         MVI   SKSCTRL,X'01'       INITIALISE BUFFER ON FIRST CALL              
         MVI   SKACTRL,0                                                        
         TM    UKFLAG1,UKFLLOCI    TEST LOCATED FILE WITH INDEX CALL            
         BZ    SEQ0                                                             
         TM    UKFLAG,UKFLDAT      MUST BE IN DATA READ MODE                    
         BZ    SEQINV                                                           
         CLC   UKKEY,0(R4)         INDEX KEY MUST MATCH BUFFER KEY              
         BNE   SEQINV                                                           
         CLC   UKCIADDR,SKADDR     INDEX ADDR MUST MATCH BUFFER ADDR            
         BNE   SEQINV                                                           
         CLC   SKINTNO,USERINUM    MUST BE SAME PRTQ AS LAST TIME               
         BNE   SEQINV                                                           
         OI    SKSCTRL,X'80'+X'40' SET IN DATA MODE PLUS LOCATED MODE           
         MVC   CIADDR,SKADDR                                                    
         LR    R5,R4               R5=A(CALLERS BUFFER)                         
         B     SEQB                                                             
SEQINV   DC    H'0'                INVALID CALL FOR LOCATED FILE                
*                                                                               
SEQ0     MVC   SKINTNO,USERINUM    SAVE INTERNAL PRTQ FILE NUM                  
         MVC   SKEXTNO,USERXNUM    SAVE EXTERNAL PRTQ FILE NUM                  
         XC    SKFCTRL,SKFCTRL                                                  
         XC    SKXCTRL,SKXCTRL                                                  
         LA    RF,0(R3)            CLEAR RECORD LEN AND DATA                    
         AHI   RF,-4                                                            
         XC    0(32,RF),0(RF)                                                   
         BRAS  RE,CXLOOPI                                                       
         USING PQRECD,R5           R5=A(FIRST INDEX ENTRY IN CXREC)             
         LH    R0,CXENTRY                                                       
         BCTR  R0,0                                                             
         STH   R0,CXENTRY                                                       
         MVC   SKPAGE(4),CXPAGE                                                 
*                                                                               
SEQ1     CLC   SKINTNO,USERINUM    TEST IF SAME PRTQ AS LAST TIME               
         BE    *+6                                                              
         DC    H'0'                                                             
         TM    SKSCTRL,X'80'       TEST READ DATA MODE                          
         BO    SEQE                                                             
         MVC   CXPAGE(4),SKPAGE    RESTORE LAST INDEX ENTRY DATA                
         LH    R5,CXENTRY                                                       
         MH    R5,CINDXLN                                                       
         LA    R5,CXREC(R5)        R5=A(LAST INDEX ENTRY IN CXREC)              
         MVI   FLAG,0                                                           
*                                                                               
SEQ2     LA    RE,CXREC            READ INDEX PAGE INTO CALLERS BUFFER          
         SR    R5,RE                                                            
         AR    R5,R4                                                            
         BRAS  RE,GETXAD                                                        
         CLC   SKXADDR,CXADDR      TEST IF PAGE ALREADY IN BUFFER               
         BE    SEQ2A                                                            
         MVC   SKXADDR,CXADDR                                                   
         XC    SKADDR,SKADDR                                                    
         LA    R0,X'80'            TEST/SET INDEX READ FOR UPDATE               
         TM    PARM1,X'80'                                                      
         BO    *+6                                                              
         SR    R0,R0                                                            
         GOTO1 DATAMGR,DMCB,((R0),DMREAD),PQFILE,CXADDR,(R4)                    
         CLI   8(R1),0                                                          
         BNE   SEQERR                                                           
SEQ2A    CLI   FLAG,0                                                           
         BNE   SEQ4                                                             
         TM    UKFLAG,X'C0'        TEST IF INDEX LAST AND DATA THIS             
         BNO   SEQ8                                                             
         NI    UKFLAG,255-X'40'                                                 
         B     SEQA                SET TO RETURN LAST FULL DATA HDR             
*                                                                               
SEQ4     CLI   PQSTAT,PQSTTE       IGNORE PURGED/TEMP ENTRYS                    
         BNH   SEQ8                                                             
*                                                                               
SEQ6     OC    UKSRCID,UKSRCID     FILTER ON USER ID                            
         BZ    SEQ6A                                                            
         CLC   UKSRCID,PQSRCID                                                  
         BNE   SEQ8                                                             
SEQ6A    OC    UKSUBID,UKSUBID     FILTER ON SUBID                              
         BZ    SEQ6B                                                            
         CLC   UKSUBID(3),=C'ALL'                                               
         BE    SEQ6B                                                            
         CLC   UKSUBID(1),PQSUBID                                               
         BNE   SEQ8                                                             
         CLI   UKSUBID+1,C'*'      ALLOW X* GENERIC                             
         BE    SEQ6B                                                            
         CLC   UKSUBID+1(1),PQSUBID+1                                           
         BNE   SEQ8                                                             
         CLI   UKSUBID+2,C'*'      ALLOW XX* GENERIC                            
         BE    SEQ6B                                                            
         CLC   UKSUBID+2(1),PQSUBID+2                                           
         BNE   SEQ8                                                             
SEQ6B    EQU   *                                                                
*                                                                               
SEQ6D    CLI   UKCLASS,0           FILTER ON CLASS                              
         BE    SEQ6E                                                            
         CLC   UKCLASS,PQCLASS                                                  
         BNE   SEQ8                                                             
SEQ6E    CLI   UKSTAT,0            FILTER ON STATUS                             
         BE    SEQ6F                                                            
         MVC   DUB(1),UKSTAT                                                    
         TM    DUB,PQSTKE                                                       
         BZ    *+12                                                             
         TM    PQSTAT,PQSTKE                                                    
         BZ    SEQ8                                                             
         NI    DUB,255-PQSTKE                                                   
         BZ    SEQ6F                                                            
         TM    DUB,PQSTIN                                                       
         BZ    *+12                                                             
         TM    PQSTAT,PQSTIN                                                    
         BZ    SEQ8                                                             
         NI    DUB,255-PQSTIN                                                   
         BZ    SEQ6F                                                            
         NC    DUB(1),PQSTAT                                                    
         BZ    SEQ8                                                             
SEQ6F    OC    UKREPNO,UKREPNO     FILTER ON REPORT NUMBER                      
         BZ    SEQ6G                                                            
         CLC   UKREPNO,PQREPNO                                                  
         BE    SEQ6G                                                            
         BH    SEQ8                                                             
         OC    UKREPNOX,UKREPNOX                                                
         BZ    SEQ6G                                                            
         CLC   UKREPNOX,PQREPNO                                                 
         BL    SEQ8                                                             
SEQ6G    B     SEQA                ENTRY FOUND VALID FOR ALL FILTERS            
*                                                                               
SEQ8     MVI   FLAG,1              BUMP TO NEXT INDEX ENTRY                     
         BRAS  RE,CXLOOPX                                                       
         B     SEQ4                                                             
         B     SEQ2                END OF INDEX PAGE                            
         B     SEQEOF                                                           
*                                                                               
SEQA     BRAS  RE,GETCAD           SAVE INDEX DATA IN BUFFER                    
         XC    SKINDEX,SKINDEX                                                  
         MVC   SKKEY,PQKEY                                                      
         MVC   SKSTAT,PQSTAT                                                    
         MVC   SKCIADDR,CIADDR                                                  
         MVC   SKPAGE(4),CXPAGE                                                 
         XC    SKFCTRL,SKFCTRL                                                  
         MVC   SKFSTCI,CIADDR      SAVE DISK ADDR OF FILE IN BUFFER             
*                                                                               
SEQB     XC    0(28,R3),0(R3)      PASS BACK SOF HEADER IN PRINT LINE           
         MVC   0(10,R3),SOFLAB                                                  
         OI    UKUSRINF+5,X'01'    RETURN SOF REC IN USER RECORD                
         MVC   QLREPRNO,PQREPNO                                                 
         MVC   QLREPRCI,CIADDR                                                  
         MVI   QLEXTRA,X'FF'                                                    
         MVC   QLINDEX,PQINDEX                                                  
         MVC   QLAGELT(4),PQINDEX+20                                            
         LA    R6,QLBATTR          POINT TO NEXT CHR IN SOF HEADER              
*                                                                               
SEQC     TM    UKFLAG,UKFLDAT      TEST IF CALLER WANTS DATA AS WELL            
         BO    *+12                                                             
         NI    SKSCTRL,X'7F'       NO-SET IN INDEX READ MODE                    
         B     SEQLEN                                                           
         OI    SKSCTRL,X'80'       YES- SET IN DATA READ MODE                   
         LR    R0,R4                                                            
         TM    SKSCTRL,X'40'       TEST IF DATA READ AND HAVE FIRST CI          
         BO    SEQC1                                                            
         BRAS  RE,RDFFST                                                        
         BE    SEQC1                                                            
         OI    UKUSRINF+5,X'40'    RETURN ERROR IN SOF REC                      
         MVC   5(3,R3),=C'ERR'     ERROR ON FIRST DATA READ                     
         NI    SKSCTRL,X'7F'                                                    
         B     SEQLEN                                                           
SEQC1    CLC   CIADDR,SKFSTCI      TEST IF <ARC=CARD> IN BUFFER                 
         BNE   SEQD                                                             
         CLC   PQDATA1-PQRECD(8,R4),ARCLINE                                     
         BNE   SEQD                                                             
         LA    R0,PQDATA1-PQRECD                                                
         STH   R0,UKUSRINF+6       RETURN DISPLACEMENT TO LLC<ARC=...>          
*                                                                               
SEQD     LR    R5,R4               RETURN EXTENDED REPORT HEADER                
         MVC   QLINDEX,PQINDEX                                                  
         MVC   QLAGELT(4),PQINDEX+20                                            
         MVC   QLBATTR,PQBATTR                                                  
         MVC   QLNCIX,PQNCIX                                                    
         MVC   QLFATTR,PQFATTR                                                  
         MVC   QLPIDNUM,PQPIDNUM                                                
         LA    R6,QLSOFEND         POINT TO END OF DATA                         
         B     SEQLEN                                                           
*                                                                               
SEQE     TM    UKFLAG,X'80'        DATA READ MODE                               
         BO    *+12                                                             
         NI    SKSCTRL,X'7F'       NO LONGER REQUIRED RETURN TO INDEX           
         B     SEQ0                                                             
         BRAS  RE,RDFNXT           READ NEXT DATA RECORD                        
         BNE   SEQF                                                             
         LH    R6,SKLEN            POINT TO END OF DATA                         
         TM    PQLINET,PQLTFL                                                   
         BZ    *+8                                                              
         AHI   R6,-2                                                            
         AR    R6,R3                                                            
         B     SEQLEN              RETURN REC LEN                               
*                                                                               
SEQF     XC    0(28,R3),0(R3)      RETURN EOF/ERR TRAILER RECORD                
         MVC   0(10,R3),EOFLAB                                                  
         OI    UKUSRINF+5,X'80'    RETURN EOF REC IN USER RECORD                
         TM    DMCB+8,X'80'                                                     
         BO    *+14                                                             
         OI    UKUSRINF+5,X'40'    RETURN ERROR IN EOF REC                      
         MVC   5(3,R3),=C'ERR'                                                  
         LA    R6,L'EOFLAB(R3)                                                  
         NI    SKSCTRL,X'7F'       RESET TO INDEX READ MODE                     
         TM    SKSCTRL,X'40'                                                    
         BZ    *+8                                                              
         OI    SKSCTRL,X'20'       SET EOF ON LOCATED FILE                      
*                                                                               
SEQLEN   LA    R3,0(R3)            R6 POINTS PAST END OF RECORD                 
         SR    R6,R3                                                            
         LA    R6,4(R6)            SET RECORD LEN PLUS 4 BYTE HEADER            
         SLL   R6,16                                                            
         LR    RF,R3                                                            
         AHI   RF,-4                                                            
         ST    R6,0(RF)            SET RECORD LENGTH                            
*                                                                               
         TM    SKSCTRL,X'40'+X'20' TEST END-OF-FILE ON LOCATED FILE             
         BNO   DMPRTQX                                                          
         MVI   SKSCTRL,0                                                        
         B     DMPRTQX                                                          
*                                                                               
SEQERR   MVI   PARM3,X'40'         ERROR ON INDEX READ                          
         MVI   SKSCTRL,0                                                        
         XC    SKXCTRL,SKXCTRL                                                  
         DC    H'0'                                                             
*                                                                               
SEQEOF   MVI   PARM3,X'90'         END OF FILE ON INDEX READ                    
         MVI   SKSCTRL,0                                                        
         XC    SKXCTRL,SKXCTRL                                                  
         B     DMPRTQX                                                          
         EJECT                                                                  
* READ PQFILE INDEX - PASS BACK INDEX ENTRY IN USER INDEX (PARM3).              
* SET PARM1(1) TO X'08' TO OBTAIN DEAD INDEX ENTRYS.                            
*                                                                               
* SET USER INDEX BEFORE FIRST CALL TO ONE OF THE FOLLOWING VALUES               
* SET UKFLAG TO X'04' AND UKSRCID/UKREPNO TO LOCATE VIA REPORT NUM.             
* SET UKFLAG TO X'06' AND UKSRCID/UKREPNO TO COMPUTE CI ADDR.                   
* SET UKFLAG TO X'07' AND UKSRCID/UKREPNO TO COMPUTE AND READ CI ADDR.          
* SET UKFLAG TO X'80' AND UKCIADDR TO LOCATE ENTRY WITH THAT CI ADDR.           
* SET UKKEY TO ZERO TO LOCATE FIRST INDEX ENTRY.                                
* SET UKKEY TO VALUE TO LOCATE THAT SPECIFIC KEY IN THE INDEX.                  
*                                                                               
* USER INDEX IS UPDATED BY THIS LOGIC.                                          
* SUBSEQUENT CALLS GIVE NEXT INDEX AFTER THAT GIVEN BY FIRST CALL.              
*                                                                               
NDXNPQ   TM    UKFLAG,X'04'        INDEX CALL WITH NO NAMED PRTQ FILE           
         BZ    DMPRTQE                                                          
         OC    UKSRCID,UKSRCID     MUST PASS A USER ID                          
         BZ    DMPRTQE                                                          
         MVC   USERIDNO,UKSRCID                                                 
         BRAS  RE,WHATPQU          GET PRTQ DATA FROM USER ID NUM               
         MVC   PQFILE,USERPRTQ     SET ALPHA DMGR PRTQ ID                       
         MVC   VPQFILE,USERXNUM    SET EXTERNAL NUM AND A(DTF)                  
         BRAS  RE,SETPQ                                                         
         MVC   UKUSRINF+4(1),USERPRTQ+4 RETURN PRTQ FILE ID CHR                 
*                                                                               
NDX      XC    SKFCTRL,SKFCTRL     RESET FILE I/O CONTROL                       
         MVI   SKACTRL,0                                                        
         MVI   SKSCTRL,0                                                        
         MVC   SKINTNO,USERINUM    SAVE INTERNAL PRTQ FILE NUM                  
         MVC   SKEXTNO,USERXNUM    SAVE EXTERNAL PRTQ FILE NUM                  
         MVI   FLAG,0                                                           
*                                                                               
NDX0     TM    UKFLAG,X'04'        TEST IF CALLER PASSED REPORT NUMBER          
         BZ    NDX0A               NO                                           
         OC    UKSRCID,UKSRCID     MUST PASS A USER ID                          
         BZ    NDXEOF                                                           
         OC    UKREPNO,UKREPNO     MUST PASS A REPORT NUMBER                    
         BZ    NDXEOF                                                           
         MVC   CIRSN,UKREPNO                                                    
         BAS   RE,RSNXPE           DIRECT LOCATE CAN BE USED                    
         BAS   RE,GETCAD                                                        
         B     NDX0B                                                            
NDX0A    TM    UKFLAG,X'80'        TEST IF DISK ADDR PASSED BY CALLER           
         BZ    NDX1                NO                                           
         MVC   CIADDR(2),UKCIADDR  YES EXTRACT PASSED CI ADDR                   
NDX0B    MVC   CIADDR+2(2),=X'0100'                                             
         LH    RE,CICINDX                                                       
         MH    RE,CITRKS                                                        
         CLM   RE,3,CIADDR         TEST CI DISK ADDR WITH MIN VALUE             
         BH    NDXEOF                                                           
         CLC   CJSTTRK,CIADDR      TEST CI DISK ADDR WITH MAX VALUE             
         BNH   NDXEOF                                                           
         BRAS  RE,GETXPE           COMPUTE INDEX PAGE/ENTRY                     
         BRAS  RE,GETXAD           COMPUTE INDEX DISK ADDR                      
         MVC   SKXADDR,CXADDR                                                   
         MVC   UKUSRINF(4),CIADDR  PASS BACK DISK ADDR IN USER INFO             
         MVC   UKUSRINF+4(1),USERPRTQ+4 PLUS PRTQ FILE ID CHR                   
*                                                                               
NDX0C    TM    UKFLAG,UKFLCIA      TEST IF CALLED TO COMPUTE CIADDR             
         BZ    NDX0F                                                            
         XC    SKXCTRL,SKXCTRL                                                  
         MVC   UKCIADDR,CIADDR                                                  
         MVC   SKFSTCI,CIADDR      SAVE DISK ADDR OF FILE IN BUFFER             
NDX0D    TM    UKFLAG,UKFLCIR      TEST IF WANT TO READ FIRST CI                
         BZ    NDX0E                                                            
         LR    R0,R4                                                            
         BRAS  RE,RDFFST                                                        
         MVC   UKINDEX,PQINDEX-PQRECD(R4) SET INDEX ENTRY FROM FIRST CI         
NDX0E    NI    UKFLAG,255-UKFLDAT-UKFLNUM-UKFLCIA-UKFLCIR                       
         B     DMPRTQX                                                          
*                                                                               
NDX0F    GOTO1 DATAMGR,DMCB,(X'00',DMREAD),PQFILE,CXADDR,(R4)                   
         CLI   8(R1),0                                                          
         BNE   NDXERR                                                           
         LH    R5,CXENTRY          POINT TO INDEX ENTRY IN BUFFER               
         MH    R5,CINDXLN                                                       
         AR    R5,R4                                                            
         USING PQRECD,R5                                                        
         TM    UKFLAG,X'04'        TEST IF CALLER PASSED REPORT NUMBER          
         BZ    NDXA                NO                                           
         CLC   UKSRCID,PQSRCID     USER ID MUST MATCH                           
         BNE   NDXEOF                                                           
         CLC   UKREPNO,PQREPNO     REPORT NUMBER MUST MATCH                     
         BNE   NDXEOF                                                           
         OC    UKSUBID,UKSUBID     TEST IF REPORT ID PASSED                     
         BZ    NDXA                NO                                           
         CLC   UKSUBID,PQSUBID     YES REPORT ID MUST MATCH AS WELL             
         BNE   NDXEOF                                                           
         B     NDXA                EXTRACT ENTRY FROM CALLER'S BUFF             
*                                                                               
NDX1     BRAS  RE,CXLOOPI          R5=A(FIRST INDEX ENTRY IN CXREC)             
         USING PQRECD,R5                                                        
         LH    R0,CXENTRY          CUT BACK TO PREVIOUS INDEX ENTRY             
         BCTR  R0,0                                                             
         STH   R0,CXENTRY                                                       
         MVC   SVINDEX,UKINDEX     SAVE CALLERS INDEX ENTRY                     
         OC    SVKEY,SVKEY                                                      
         BZ    NDX2                CALLER WANTS FIRST INDEX ENTRY               
         CLC   SVINDEX,SKINDEX                                                  
         BNE   NDX2                CALLER WANTS SPECIFIC INDEX ENTRY            
         XC    SVKEY,SVKEY                                                      
         MVC   CXPAGE(4),SKPAGE    CALLER WANTS NEXT INDEX ENTRY                
         B     NDX2+6                                                           
*                                                                               
NDX2     XC    SKXADDR,SKXADDR     RESET INDEX DISK ADDR IN BUFFER              
         LH    R5,CXENTRY                                                       
         MH    R5,CINDXLN                                                       
         LA    R5,CXREC(R5)        R5=A(INDEX START ENTRY IN CXREC)             
*                                                                               
NDX3     LA    RE,CXREC            READ INDEX PAGE INTO CALLERS BUFFER          
         SR    R5,RE                                                            
         AR    R5,R4                                                            
         BRAS  RE,GETXAD                                                        
         CLC   SKXADDR,CXADDR      TEST IF PAGE ALREADY IN BUFFER               
         BE    NDX3A                                                            
         MVC   SKXADDR,CXADDR                                                   
         GOTO1 DATAMGR,DMCB,(X'00',DMREAD),PQFILE,CXADDR,(R4)                   
         CLI   8(R1),0                                                          
         BNE   NDXERR                                                           
NDX3A    CLI   FLAG,0              TEST FIRST TIME FLAG                         
         BE    NDX8                                                             
*                                                                               
NDX4     OC    PQKEY(5),PQKEY      IGNORE ENTRYS WITH ZERO KEY                  
         BZ    NDX8                                                             
         CLI   PQSTAT,PQSTTE       IGNORE PURGED/TEMP ENTRYS                    
         BH    NDX5                                                             
         BL    NDX8                                                             
         TM    UKFLAG,UKFLTMP      UNLESS CALLER WANTS TEMP ENTRYS              
         BZ    NDX8                                                             
         NI    UKFLAG,255-UKFLTMP                                               
*                                                                               
NDX5     TM    PQSTAT,PQSTDEAD     IGNORE USED ENTRYS                           
         BZ    *+12                                                             
         TM    PARM1,X'08'         UNLESS CALLER WANTS THEM                     
         BZ    NDX8                                                             
*                                                                               
NDX6     OC    SVKEY(5),SVKEY      CALLER WANTS NEXT ENTRY                      
         BZ    NDXA                                                             
         CLC   SVKEY(5),PQKEY      CALLER WANTS SPECIFIC USERID                 
         BNE   NDX8                                                             
         OC    UKREPNO,UKREPNO     TEST IF CALLER PASSED FILE NUMBER            
         BZ    *+14                                                             
         CLC   UKREPNO,PQREPNO                                                  
         BNE   NDX8                                                             
         B     NDXA                                                             
*                                                                               
NDX8     MVI   FLAG,1                                                           
         BRAS  RE,CXLOOPX          BUMP TO NEXT INDEX ENTRY                     
         B     NDX4                                                             
         B     NDX3                END OF INDEX PAGE                            
         B     NDXEOF                                                           
*                                                                               
NDXA     BRAS  RE,GETCAD           PASS INDEX ENTRY BACK TO CALLER              
         MVC   UKINDEX,PQINDEX                                                  
         MVC   UKCIADDR,CIADDR                                                  
         NI    UKFLAG,255-X'84'    TURN OFF ADDR/REPNO PASSED FLAGS             
         MVC   SKPAGE(4),CXPAGE    SAVE INDEX CTRL DATA IN BUFFER               
         MVC   SKINDEX,UKINDEX                                                  
         MVC   SKFSTCI,CIADDR      SAVE DISK ADDR OF FILE IN BUFFER             
         B     DMPRTQX                                                          
*                                                                               
NDXERR   MVI   PARM3,X'40'         DISK ERROR ON INDEX READ                     
         XC    SKXCTRL,SKXCTRL                                                  
         DC    H'0'                                                             
*                                                                               
NDXEOF   MVI   PARM3,X'90'         EOF ON INDEX READ                            
         XC    SKXCTRL,SKXCTRL                                                  
         B     DMPRTQX                                                          
         EJECT                                                                  
* READ PQFILE DATA RECORDS - PASS BACK RECORD IN PARM4                          
*                                                                               
RDF      MVI   SKACTRL,0           MUST HAVE FILE DEFINED                       
         OC    SKFSTCI,SKFSTCI                                                  
         BNZ   RDF2                                                             
RDF1     DC    H'0',C'INVALID PQFILE READ SEQUENCE'                             
*                                                                               
RDF2     OC    SKADDR,SKADDR       TEST IF FIRST READ                           
         BNZ   RDF4                                                             
         MVC   CIADDR,SKFSTCI                                                   
         LR    R0,R4                                                            
         BRAS  RE,RDFFST           READ FIRST BLOCK AND INITIALISE              
         BE    RDF4                                                             
         MVC   PARM3(1),DMCB+8     RETURN READ ERROR                            
         B     DMPRTQX                                                          
*                                                                               
RDF4     BRAS  RE,RDFNXT           GET NEXT RECORD                              
         MVC   PARM3(1),DMCB+8                                                  
         B     DMPRTQX                                                          
                                                                                
* READ RANDOM PQFILE DATA RECORDS - RECORD INFO IN PRINT LINE AT PARM4          
* XL4  LINE OR PAGE NUMBER                                                      
* XL4  C'LINE' OR C'PAGE'                                                       
* XL4  LINE NUMBER WITHIN PAGE FOR C'PAGE' OPTION                               
*                                                                               
RDR      MVI   SKACTRL,0           MUST HAVE FILE DEFINED                       
         OC    SKFSTCI,SKFSTCI                                                  
         BZ    RDF1                                                             
         MVC   FUL,0(R3)           SET FUL TO PAGE/LINE NUMBER                  
         XC    FUL1,FUL1           DEFAULT LINE WITHIN PAGE                     
*                                                                               
RDR1     MVI   PAGELINE,C'L'       DEFAULT IS LINE IF NO OPTION                 
         CLI   4(R3),C'L'                                                       
         BE    RDR4                                                             
         CLI   4(R3),C'P'          TEST PAGE NUMBER                             
         BNE   RDR2                                                             
         MVI   PAGELINE,C'P'                                                    
         MVC   FUL1,8(R3)          GET LINE WITHIN PAGE                         
         B     RDR4                                                             
RDR2     DC    H'0',C'INVALID PAGE/LINE '                                       
*                                                                               
RDR4     BRAS  RE,RAN              SEARCH FOR REQUIRED PAGE/LINE                
         MVC   PARM3(1),DMCB+8                                                  
         B     DMPRTQX                                                          
         EJECT                                                                  
* WRITE BACK LAST PQFILE RECORD READ - NEW RECORD IN PARM4                      
*                                                                               
WRF      MVI   SKACTRL,0                                                        
         OC    SKFSTCI,SKFSTCI     MUST HAVE FILE DEFINED                       
         BZ    WRF2                                                             
         OC    SKADDR,SKADDR       MUST HAVE BLOCK DEFINED                      
         BZ    WRF2                                                             
WRF2     DC    H'0',C'INVALID PQFILE WRITE SEQUENCE '                           
*                                                                               
WRF4     LH    R1,SKLEN            MOVE REC BACK TO BUFFER                      
         LR    R0,R4                                                            
         AH    R0,SKDISP                                                        
         SR    R0,R1                                                            
         LR    RE,R3                                                            
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
*                                                                               
WRF6     GOTO1 DATAMGR,DMCB,(X'00',DMWRT),PQFILE,SKADDR,(R4)                    
         MVC   PARM3(1),DMCB+8                                                  
         B     DMPRTQX                                                          
         EJECT                                                                  
* CHANGE STATUS OF FILE DEFINED IN BUFFER BY SKFSTCI                            
* SET PARM1(1)=X'40' TO INHIBIT ENQUEUE/DEQUEUE.                                
*                                                                               
STA      MVI   SKACTRL,0           MUST HAVE FILE DEFINED                       
         OC    SKFSTCI,SKFSTCI                                                  
         BNZ   STA2                                                             
         DC    H'0',C'INVALID PQFILE STATUS SEQUENCE'                           
*                                                                               
STA2     L     RF,=A(GETDATE)      SET DATE AND TIME NOW                        
         BASR  RE,RF                                                            
         L     RF,=A(GETTIME)                                                   
         BASR  RE,RF                                                            
         XC    STPARM,STPARM       SET ACTION VALUES FOR STATUS CHANGE          
         MVC   STACTN,SKACTN                                                    
         MVI   STCTRL,X'23'        SET FILE AND INDEX CHANGE REQUIRED           
         TM    PARM1,X'40'                                                      
         BZ    *+8                                                              
         OI    STCTRL,X'40'        SET INHIBIT LOCK                             
         MVC   STFSTCI,SKFSTCI                                                  
         LA    RE,CXREC            USE CXREC AS I/O BUFFER                      
         ST    RE,STBUFFA                                                       
*                                                                               
STAX     BRAS  RE,STAT             GOTO STATUS CHANGE ROUTINE                   
         MVC   PARM3(1),DMCB+8                                                  
         B     DMPRTQX                                                          
         EJECT                                                                  
* CHANGE ATTRIBUTES OF FILE DEFINED IN BUFFER BY SKFSTCI                        
*                                                                               
ATT      MVI   SKACTRL,0           FILE MUST BE DEFINED                         
         OC    SKFSTCI,SKFSTCI                                                  
         BNZ   ATT2                                                             
         DC    H'0',C'INVALID PQFILE ATTRIBUTE CHANGE '                         
*                                                                               
ATT2     MVC   CIADDR,SKFSTCI      READ FIRST CI REC INTO CXREC                 
         LA    R5,CXREC                                                         
         GOTO1 DATAMGR,DMCB,(X'00',DMREAD),PQFILE,CIADDR,(R5)                   
         MVC   PARM3(1),DMCB+8                                                  
         CLI   DMCB+8,0                                                         
         BNE   DMPRTQX                                                          
*                                                                               
ATTDES   CLI   SKACTN,ACTNDES      SET DESCRIPTION DATA                         
         BNE   ATTDESX                                                          
         MVC   PQDESC,QLDESC                                                    
         B     ATTX                                                             
ATTDESX  EQU   *                                                                
*                                                                               
ATTREL   CLI   SKACTN,ACTNREL      SET LIVE RETAIN HOURS                        
         BNE   ATTRELX                                                          
         MVC   PQRETNL,QLRETNL                                                  
         B     ATTX                                                             
ATTRELX  EQU   *                                                                
*                                                                               
ATTRED   CLI   SKACTN,ACTNRED      SET DEAD RETAIN HOURS                        
         BNE   ATTREDX                                                          
         MVC   PQRETND,QLRETND                                                  
         B     ATTX                                                             
ATTREDX  EQU   *                                                                
*                                                                               
         DC    H'0'                UNKNOWN ATTRIBUTE                            
*                                                                               
ATTX     GOTO1 DATAMGR,DMCB,(X'00',DMWRT),PQFILE,CIADDR,(R5)                    
         MVC   PARM3(1),DMCB+8                                                  
         B     DMPRTQX                                                          
         EJECT                                                                  
* ADD RECORDS TO PQFILE. PARM3 NOT DEFINED. REPORT ATTRIBUTES IN PARM4          
*                                                                               
* REPORT CAN BE CREATED BY 'OPEN' COMMAND, FOLLOWED BY SUCCESSIVE 'ADD'         
* COMMANDS, WITH A FINAL 'CLOSE' COMMAND. ALTERNATIVELY A REPORT CAN            
* BE CREATED BY A SUCCESSION OF 'DMPRINT' COMMANDS. IF THE FIRST CHR OF         
* THE PRINT LINE IS X'00' THE AN OPEN IS ASSUMMED AND IF THE FIRST CHR          
* OF THE PRINT LINE IS X'FF' THEN A CLOSE IS PERFORMED.                         
*                                                                               
* CLO     IS USED TO DO A NORMAL CLOSE OF A REPORT (OR X'FF')                   
* CLO/PUR IS USED TO CLOSE AND PURGE A REPORT BEING CREATED                     
* CLO/JOB IS USED TO CLOSE A REPORT THAT CONTAINS JOB JCL DATA                  
* CLO/ERR IS USED TO CLOSE A REPORT THAT HAS ABENDED                            
* CLO/USR IS USED TO CLOSE A REPORT AND PASS SOME USER DATA                     
                                                                                
ADDPRT   TM    SKACTRL,X'01'       TEST IF ALREADY OPEN                         
         BZ    ADDPRT1             NO                                           
         TM    PQLINET-PQRECD(R4),PQLTFL+PQLTCC                                 
         BO    ADDPRT1                                                          
         B     ADD                                                              
ADDPRT1  CLI   PLCC,X'00'          FOR DMPRINT CC=X'00' MEANS OPEN              
         BE    ADDOPN                                                           
         CLI   PLCC,X'FF'          FOR DMPRINT CC IS PRINT CTL CHR              
         BNE   ADD                                                              
         MVI   CLOTYP,CLOTNOR      SET NORMAL CLOSE                             
         B     ADDCLS1             FOR DMPRINT CC=X'FF' MEANS CLOSE             
                                                                                
ADDERR   DC    H'0',C'INVALID SEQ FOR OPEN/CLOSE/ADD'                           
                                                                                
ADDOPN   MVC   SAVECC,PLCC         TEST IF VALID OPEN COMMAND                   
         CLI   SKACTRL,0                                                        
         BNE   ADDERR                                                           
ADDOPN1  MVI   SKSCTRL,0           INITIALISE BUFFER FOR ADD                    
         XC    SKFCTRL,SKFCTRL                                                  
         XC    SKXCTRL,SKXCTRL                                                  
         BRAS  RE,OPN              OPEN NEW REPORT                              
         B     ADDX                                                             
                                                                                
ADDCLS   MVI   CLOTYP,CLOTNOR      SET NORMAL CLOSE                             
         L     RE,PARM1                                                         
         CLC   0(7,RE),=C'CLO/PUR'                                              
         BNE   *+8                                                              
         MVI   CLOTYP,CLOTPUR      SET PURGE CLOSE IF CLO/PUR                   
         CLC   0(7,RE),=C'CLO/JOB'                                              
         BNE   *+8                                                              
         MVI   CLOTYP,CLOTJOB      SET JOB CLOSE IF CLO/JOB                     
         CLC   0(7,RE),=C'CLO/ERR'                                              
         BNE   ADDCLS0                                                          
         MVI   CLOTYP,CLOTERR      SET ERROR CLOSE IF CLO/ERR                   
         MVI   CLOERRRC,0                                                       
         CLI   7(RE),C'='          TEST IF REASON CODE SET                      
         BNE   ADDCLS1                                                          
         MVC   CLOERRRC,8(RE)      EXTRACT REASON CODE                          
         B     ADDCLS1                                                          
ADDCLS0  CLC   0(7,RE),=C'CLO/USR'                                              
         BNE   *+18                                                             
         MVI   CLOTYP,CLOTUSR      SET CLOSE WITH USER DATA                     
         L     RF,PARM3            AND SAVE USER DATA IN KEY AREA               
         MVC   CLOINF,UKUSRINF-UKRECD(RF)                                       
*                                                                               
ADDCLS1  MVC   SAVECC,PLCC                                                      
         TM    SKACTRL,X'01'       EXIT IF NO FILE IS OPEN                      
         BZ    ADDX                                                             
         CLI   SKINTNO,0           MUST HAVE PRTQ FILE IN BUFFER                
         BE    ADDERR                                                           
         MVC   USERINUM,SKINTNO                                                 
         BRAS  RE,WHATPQI                                                       
         MVC   PQFILE,USERPRTQ     SET ALPHA DMGR PRTQ ID                       
         MVC   VPQFILE,USERXNUM    SET EXTERNAL NUM AND A(DTF)                  
         CLC   USERXNUM,SKEXTNO                                                 
         BNE   ADDERR              DOUBLE CHECK BUFFER OK                       
         BRAS  RE,SETPQ                                                         
         OI    SKACTRL,X'04'                                                    
         LR    R5,R4                                                            
*                                                                               
ADDCLS2  OC    PQLINEH,PQLINEH     TEST IF NO LINES IN REPORT                   
         BNZ   ADDCLS3                                                          
         TM    PQATTB,PQATJOBI     TEST IF SUBMITTED SOON JOB                   
         BZ    ADDCLS3                                                          
         ST    R3,AQ               SAVE A(CALLERS PRINT LINE)                   
         LA    R3,Q                                                             
         MVI   Q,C' '                                                           
         MVC   Q+1(L'Q-1),Q                                                     
         MVI   Q,X'09'                                                          
         CTRY  ,                   R0=X'OOAACCLL'                               
         SRDL  R0,8                                                             
         SRL   R1,24               R1=LANGUAGE CODE                             
         SLL   R1,5                                                             
         L     R0,=A(NODATA)                                                    
         AR    R1,R0               POINT TO NO DATA IN REPORT MESSAGE           
         MVC   Q+1(32),0(R1)                                                    
         MVI   PQLINET,PQLTFL+PQLTCC                                            
         MVI   PQLINEW,80                                                       
         MVC   PQRETNL,=H'1'                                                    
         MVC   PQRETND,=H'1'                                                    
         OI    ADDWHY,X'01'        SET EMPTY REPORT FLAG                        
         B     ADD+6               ADD DUMMY PRINT LINE TO REPORT               
ADDCLS2A L     R3,AQ               RESTORE A(CALLERS PRINT LINE)                
         LR    R5,R4                                                            
*                                                                               
ADDCLS3  LR    RF,R4               WRITE LAST BLOCK WITH EOF RECORD             
         AH    RF,SKDISP                                                        
         MVC   0(2,RF),=H'1'                                                    
         OI    ADDWHY,X'02'        SET REASON CODE TO EOF LAST BLOCK            
         B     ADDD                                                             
*                                                                               
ADDCLS4  MVI   P1,0                SET NORMAL CLOSE                             
         OC    PQLINEH,PQLINEH                                                  
         BNZ   *+8                                                              
         MVI   P1,1                SET ERROR CLOSE IF EMPTY FILE                
         TM    CLOTYP,CLOTPUR                                                   
         BZ    *+8                                                              
         MVI   P1,1                SET ERROR CLOSE IF CLO/PUR                   
         BRAS  RE,CLS                                                           
         B     ADDX                                                             
                                                                                
ADD      MVC   SAVECC,PLCC                                                      
         CLI   SKINTNO,0           MUST HAVE PRTQ FILE IN BUFFER                
         BE    ADDERR                                                           
         MVC   USERINUM,SKINTNO                                                 
         BRAS  RE,WHATPQI                                                       
         MVC   PQFILE,USERPRTQ     SET ALPHA DMGR PRTQ ID                       
         MVC   VPQFILE,USERXNUM    SET EXTERNAL NUM AND A(DTF)                  
         CLC   USERXNUM,SKEXTNO                                                 
         BNE   ADDERR              DOUBLE CHECK BUFFER OK                       
         BRAS  RE,SETPQ                                                         
*                                                                               
ADD0     LR    R5,R4               REPORT MUST HAVE BEEN OPENED                 
         TM    SKACTRL,X'01'                                                    
         BZ    ADDERR              INVALID ACTION NO PREV OPEN                  
         TM    PQLINET,PQLTCC      TEST IF LINE HAS CONTROL CHR                 
         BZ    ADD1                                                             
         LR    RF,R3                                                            
         TM    PQLINET,PQLTFL      TEST IF FIXED LENGTH DATA                    
         BO    *+8                                                              
         LA    RF,2(RF)                                                         
         LA    RE,CCTAB            RE=A(TABLE OF VALID CC CHRS)                 
ADD0A    CLC   0(1,RF),0(RE)                                                    
         BE    ADD0B                                                            
         LA    RE,L'CCTAB(RE)                                                   
         CLI   0(RE),0                                                          
         BNE   ADD0A                                                            
         B     ADDX                IGNORE LINES WITH INVALID CC CHR             
ADD0B    TM    SKACTRL,X'02'       TEST FIRST ADD                               
         BO    ADD1                                                             
         CLC   1(5,RF),ARCLINE+3   TEST IF FIRST LINE IS <ARC=...               
         BNE   ADD1                                                             
         TM    SKACTRX,X'04'       TEST IF GENERATED DEFAULT AT OPEN            
         BZ    ADD0C                                                            
         LA    R1,PQDATA1-PQINDEX  YES SET BACK TO OVERWRITE DEFAULT            
         STH   R1,SKDISP                                                        
ADD0C    LA    R1,PQDATA1-PQINDEX                                               
         CH    R1,SKDISP                                                        
         BNE   ADDX                IGNORE IF NOT AT START OF REPORT             
         AR    R1,R5                                                            
ADD0D    MVC   0(4,R1),ARCLINE                                                  
         MVC   4(72,R1),2(RF)      <ARC=AA,.......>                             
         MVI   76(R1),C'>'                                                      
         LA    R1,ARCLINEL(R1)                                                  
         SR    R1,R5                                                            
         STH   R1,SKDISP                                                        
         B     ADDX                SAVE AND SKIP ROUND <ARC=... LINE            
*                                                                               
ADD1     TM    SKACTRL,X'02'       TEST FIRST ADD                               
         BO    ADD1C                                                            
         OI    SKACTRL,X'02'                                                    
         TM    PQLINET,PQLTCC      TEST IF LINE HAS CC CHR                      
         BZ    ADD2                                                             
         CLI   2(RE),X'89'         TEST IF CC CHR TABLE SAYS NEW PAGE           
         BNL   ADD1B                                                            
         TM    SKACTRL,X'30'       TEST IF EXTEND/DROP1ST                       
         BNO   ADD1A                                                            
         OI    SKACTRX,X'01'       SET 1ST SKIP-TO-PAGE                         
         B     ADDX                                                             
ADD1A    MVC   PQPAGEH,=H'1'       FORCE NEW PAGE IF FIRST CC DOES NOT          
         MVC   PQPAGES,=H'1'                                                    
         OI    PQBFLAG,PQBFNP      SET BLOCK CONTAINS NEW PAGE CC CHR           
         OI    SKACTRL,X'40'       SET FIRST PAGE IN BLOCK FLAG                 
         LH    RF,SKDISP                                                        
         AR    RF,R4                                                            
         MVC   0(3,RF),=X'00038B'  SET SKIP TO TOP OF PAGE CC CHR               
         SR    RF,R4                                                            
         LA    RF,3(RF)                                                         
         STH   RF,SKDISP                                                        
         B     ADD2                                                             
ADD1B    TM    SKACTRL,X'30'       TEST IF EXTEND/DROP1ST                       
         BNO   ADD2                                                             
         OI    SKACTRX,X'01'       SET 1ST SKIP-TO-PAGE FOUND                   
         B     ADDX                                                             
ADD1C    TM    SKACTRL,X'30'       TEST IF EXTEND/DROP1ST                       
         BNO   ADD2                                                             
         TM    PQLINET,PQLTCC      TEST IF LINE HAS CC CHR                      
         BZ    ADD2                                                             
         TM    SKACTRX,X'02'       TEST 2ND SKIP-TO-PAGE FOUND                  
         BO    ADD2                                                             
         CLI   2(RE),X'89'         TEST IF CC CHR TABLE SAYS NEW PAGE           
         BL    ADDX                                                             
         OI    SKACTRX,X'02'       SET 2ND SKIP-TO-PAGE FOUND                   
         B     ADDX                                                             
*                                                                               
ADD2     TM    PQLINET,PQLTCC      TEST WHETHER IBM OR ASA CC CHR               
         BZ    ADD3                                                             
         TM    1(RE),X'02'         TEST ASA FORMAT                              
         BO    ADD2A               YES                                          
         TM    1(RE),X'01'         TEST IMMEDIATE TYPE                          
         BO    ADD3                WITH DATA - GO FIND DATA LEN                 
         SR    R1,R1               WITHOUT DATA - SET ZERO DATA LEN             
         B     ADD3X                                                            
ADD2A    LH    RF,CIBLKLN          ASA CC CHR - CONVERT TO IBM CC CHR           
         SH    RF,SKDISP                                                        
         CHI   RF,5                TEST IF ROOM IN BUFFER FOR CONVERT           
         BNL   *+12                YES                                          
         OI    ADDWHY,X'04'        SET REASON CODE TO ASA CONVERSION            
         B     ADDD                WRITE BUFFER AND INIT NEW BUFFER             
ADD2B    LH    RF,SKDISP                                                        
         AR    RF,R4                                                            
         MVC   0(2,RF),=X'00038B'  SET IBM SPACE COMMAND IN BUFFER              
         MVC   2(1,RF),2(RE)                                                    
         LH    RF,SKDISP                                                        
         LA    RF,3(RF)                                                         
         STH   RF,SKDISP                                                        
ADD2C    CLI   2(RE),X'89'         TEST SKIP TOP OF PAGE CC CHR                 
         BL    ADD2D                                                            
         SR    RF,RF                                                            
         ICM   RF,3,PQPAGEH        SET HIGH PAGE IN BLOCK                       
         LA    RF,1(RF)                                                         
         STCM  RF,3,PQPAGEH                                                     
         TM    SKACTRL,X'40'       TEST IF FIRST PAGE IN BLOCK                  
         BO    ADD2D                                                            
         OI    SKACTRL,X'40'                                                    
         OI    PQBFLAG,PQBFNP      SET BLOCK CONTAINS NEW PAGE CC CHR           
         STCM  RF,3,PQPAGES        SET LOW PAGE IN BLOCK                        
ADD2D    LR    RF,R3                                                            
         TM    PQLINET,PQLTFL      TEST IF FIXED LENGTH DATA                    
         BO    *+8                                                              
         LA    RF,2(RF)                                                         
         MVI   0(RF),X'01'         SET IBM PRINT-NO-SPACE IN PRINT LINE         
         B     ADD0                                                             
*                                                                               
ADD3     TM    PQLINET,PQLTFL      TEST IF FIXED LENGTH DATA                    
         BO    ADD3A               YES                                          
         CLC   0(2,R3),=H'3'       V/L DATA HAS LEN IN 1ST TWO BYTES            
         BL    ADDX                                                             
         CLC   0(2,R3),=H'1024'                                                 
         BH    ADDX                IGNORE FUNNY LEN RECORDS                     
         LH    R1,0(R3)                                                         
         AHI   R1,-3               R1 IS DATALEN-1                              
         B     ADD3X                                                            
*                                                                               
ADD3A    TM    SKACTRL,X'08'       TEST WANT DATA DICTIONARY SUPPORT            
         BZ    ADD3D                                                            
         TM    PQLINET,PQLTCC      TEST NORMAL PRINT LINE                       
         BZ    ADD3D                                                            
         CLI   OFFLINE,C'Y'        AND ONLINE PROGRAM                           
         BE    ADD3D                                                            
         SR    R1,R1                                                            
         IC    R1,PQLINEW                                                       
         BCTR  R1,0                                                             
         ST    R2,DMCB+16                                                       
         L     RE,=A(TRTDD)                                                     
         EX    R1,*+8                                                           
         B     *+10                                                             
         TRT   1(0,R3),0(RE)       TEST IF DATA HAS DCTNRY ESCAPE CHRS          
         BZ    ADD3C               NO                                           
ADD3B    MVI   DMCB+0,C'T'         SET TRANSLATE WHOLE PRINT LINE               
         MVI   DMCB+1,C'U'         SET CASE TO UPPER                            
         MVI   DMCB+2,C' '         DEFAULT SYSTEM                               
         MVI   DMCB+3,C' '         DEFAULT LANGUAGE                             
         LA    RF,1(R3)            SET ADR AND LEN OF DATA                      
         ST    RF,DMCB+4                                                        
         MVC   DMCB+4(1),PQLINEW                                                
         ICM   RF,15,=V(DICTATE)   GO TO V(DICTATE) FOR TRANSLATION             
         BZ    ADD3C                                                            
         LA    R1,DMCB                                                          
         BASR  RE,RF                                                            
ADD3C    L     R2,DMCB+16          RESTORE DESTROYED REGISTER                   
*                                                                               
ADD3D    LR    RF,R3               POINT TO FIRST F/L DATA CHR                  
         SR    R1,R1                                                            
         IC    R1,PQLINEW                                                       
         AR    R1,R3               POINT TO LAST F/L DATA CHR                   
         TM    PQLINET,PQLTCC                                                   
         BO    ADD3E                                                            
         BCTR  R1,0                ADJUST FOR NO CC CHR                         
ADD3E    CLI   0(R1),X'00'         CONVERT TRAILING NULLS TO SPACES             
         BNE   *+8                                                              
         MVI   0(R1),C' '                                                       
         CLI   0(R1),C' '                                                       
         BNE   ADD3F                                                            
         BCTR  R1,0                                                             
         CR    R1,RF                                                            
         BH    ADD3E                                                            
ADD3F    SR    R1,RF                                                            
ADD3X    STH   R1,SKLEN            SKLEN IS DATALEN-1                           
*                                                                               
ADD8     LH    RF,CIBLKLN          TEST IF RECORD FITS IN BUFFER                
         SH    RF,SKDISP                                                        
         LH    RE,SKLEN                                                         
         LA    RE,5(RE)            NEED 2+LEN+2 FOR END OF BUFFER               
         CR    RF,RE                                                            
         BNL   *+12                                                             
         OI    ADDWHY,X'08'        SET REASON CODE TO NO ROOM FOR DATA          
         B     ADDD                WRITE BUFFER AND INIT NEW BUFFER             
*                                                                               
ADDA     LH    R1,SKLEN            ADJUST BLOCK ATTRIBUTES                      
         TM    PQLINET,PQLTCC                                                   
         BO    *+8                                                              
         LA    R1,1(R1)            ALLOW FOR NO CC CHR                          
         LTR   R1,R1                                                            
         BZ    ADDA2               LINE HAS NO DATA CHRS                        
ADDA1    L     R0,SKCHRS                                                        
         AR    R0,R1                                                            
         ST    R0,SKCHRS           BUMP TOTAL CHRS                              
         CH    R1,SKCHRMAX                                                      
         BNH   *+8                                                              
         STH   R1,SKCHRMAX         SAVE MAXIMUM CHRS IN PRINT LINE              
         SR    R0,R0                                                            
         ICM   R0,7,PQLINEH        SET HIGH LINE IN BLOCK                       
         AHI   R0,1                                                             
         STCM  R0,7,PQLINEH                                                     
         TM    SKACTRL,X'80'       TEST IF FIRST LINE IN BLOCK                  
         BO    *+12                                                             
         OI    SKACTRL,X'80'                                                    
         STCM  R0,7,PQLINES        SET LOW LINE IN BLOCK                        
*                                                                               
ADDA2    TM    PQLINET,PQLTCC      TEST IF CONTROL CHR                          
         BZ    ADDB                                                             
         LR    RF,R3                                                            
         TM    PQLINET,PQLTFL                                                   
         BO    *+8                                                              
         LA    RF,2(RF)                                                         
         CLI   0(RF),X'89'         TEST IF SKIP TO NEW PAGE                     
         BL    ADDB                                                             
         SR    RF,RF                                                            
         ICM   RF,3,PQPAGEH        SET HIGH PAGE IN BLOCK                       
         LA    RF,1(RF)                                                         
         STCM  RF,3,PQPAGEH                                                     
         TM    SKACTRL,X'40'       TEST IF FIRST PAGE IN BLOCK                  
         BO    ADDB                                                             
         OI    SKACTRL,X'40'                                                    
         OI    PQBFLAG,PQBFNP      SET THIS BLOCK CONTAINS NEW PAGE             
         STCM  RF,3,PQPAGES        SET LOW PAGE IN BLOCK                        
*                                                                               
ADDB     LH    RF,SKDISP           MOVE DATA TO BUFFER                          
         AR    RF,R4                                                            
         ST    RF,FUL                                                           
         TM    PQLINET,PQLTFL      TEST FIXED LENGTH                            
         BZ    ADDB1                                                            
         LH    R1,SKLEN                                                         
         LA    RE,3(R1)            SET LENGTH                                   
         STH   RE,0(RF)                                                         
         EX    R1,*+8              SET CC AND DATA                              
         B     *+10                                                             
         MVC   2(0,RF),0(R3)                                                    
         LH    RF,SKDISP           BUMP DISPLACEMENT                            
         LA    RF,3(RF,R1)                                                      
         STH   RF,SKDISP                                                        
         B     ADDC                                                             
ADDB1    LH    R1,0(R3)            FOR V/L DATA LEN IN 1ST TWO BYTES            
         LR    R0,RF                                                            
         LR    RE,R3                                                            
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
         LH    R1,0(R3)                                                         
         AH    R1,SKDISP                                                        
         STH   R1,SKDISP                                                        
*                                                                               
ADDC     TM    PQLINET,PQLTDC      TEST FOR DATA COMPRESSION                    
         BZ    ADDC1                                                            
         L     RF,=A(COMPRESS)                                                  
         BASR  RE,RF                                                            
         LH    RF,SKDISP                                                        
         S     RF,FUL1             ORIGIONAL LENGTH                             
         A     RF,FUL2             COMPRESSED LENGTH                            
ADDC1    TM    ADDWHY,X'01'                                                     
         BO    ADDCLS2A                                                         
         B     ADDX                                                             
*                                                                               
ADDD     CLC   SKADDR(2),SKENDCI   TEST DISK ADDR WITH LAST IN CI               
         BL    ADDE                WRITE BLOCK IN THIS CI                       
         BH    ADDD1               WRITE BLOCK IN NEXT CI                       
         CLC   SKADDR+2(1),CIHIREC+1                                            
         BNE   ADDE                                                             
         CLI   PQSEQ,LASTSEQ       TEST IF LAST BLOCK IN LAST CI                
         BNE   ADDE                                                             
         TM    PQTYPE,PQTYXCI+PQTYXTN                                           
         BZ    ADDE                                                             
         LR    RF,R5               SET FORCED END OF DATA IN BLOCK              
         AH    RF,SKDISP                                                        
         MVC   0(2,RF),=H'1'                                                    
         GOTO1 DATAMGR,DMCB,(X'00',DMWRT),PQFILE,SKADDR,(R5)                    
         CLI   DMCB+8,0                                                         
         BNE   ADDERR2                                                          
         MVI   EOFERRRC,X'04'      SET REPORT TOO BIG ERROR                     
         MVC   REPTINFO,0(R4)                                                   
         MVI   P1,0                SET NORMAL CLOSE                             
         BRAS  RE,CLS                                                           
         MVI   PARM3,X'84'         RETURN REPORT TOO BIG                        
         B     ADDX                                                             
ADDD1    XC    EOFCNT(8),EOFCNT    CLEAR EOF COUNTERS                           
ADDD2    CLI   OFFLINE,C'Y'                                                     
         BNE   *+10                                                             
         MVC   EOFMAX(6),EOFMAXOF                                               
         MVI   P1,2                SET PART2 CI WANTED                          
         L     RF,=A(GCI)                                                       
         BASR  RE,RF               WRITE BLOCK IN NEW CI                        
         CLC   P1,FFS                                                           
         BE    ADDERR2             DISK ERROR                                   
         OC    P1,P1                                                            
         BNZ   ADDE1                                                            
         CLI   EOFERRRC,2          TEST NO PART1 OR PART2 CIS AVAIL             
         BH    ADDERR3                                                          
         LH    R1,EOFCNT           YES BUMP END OF FILE COUNTER                 
         LA    R1,1(R1)                                                         
         STH   R1,EOFCNT                                                        
         CH    R1,EOFMAX                                                        
         BH    ADDERR3             RETURN EOF IF EXCEEDS MAX COUNT              
         STIMER WAIT,BINTVL=EOFBIN                                              
         B     ADDD2               BACK AND TRY AGAIN                           
*                                                                               
ADDE     GOTO1 DATAMGR,DMCB,(X'00',DMWRT),PQFILE,SKADDR,(R5)                    
         CLI   DMCB+8,0                                                         
         BNE   ADDERR2                                                          
ADDE1    TM    ADDWHY,X'02'        TEST IF HERE TO WRITE LAST BLOCK             
         BZ    *+12                                                             
         NI    ADDWHY,X'FF'-X'02'                                               
         B     ADDCLS4                                                          
*                                                                               
ADDF     LA    RF,PQDATA-PQINDEX   RESET BUFFER DISPLACEMENT                    
         STH   RF,SKDISP                                                        
         IC    RF,SKADDR+2         BUMP DISK ADDR FOR NEXT WRITE                
         LA    RF,1(RF)                                                         
         STC   RF,SKADDR+2                                                      
         CH    RF,CIHIREC                                                       
         BNH   ADDG                                                             
         ICM   RF,3,SKADDR         BUMP TO NEXT TRACK                           
         LA    RF,1(RF)                                                         
         STH   RF,SKADDR                                                        
         MVI   SKADDR+2,1                                                       
*                                                                               
ADDG     EQU   *                                                                
*                                                                               
ADDH     LH    R1,SKDISP           INITIALISE NEW BUFFER                        
         LA    RE,PQDATA                                                        
         LH    RF,CIBLKLN                                                       
         SR    RF,R1                                                            
         XCEF                                                                   
         NI    SKACTRL,X'3F'       RESET FIRST PAGE/LINE                        
         NI    PQBFLAG,255-PQBFNP  RESET NEW PAGE IN BUFFER FLAG                
         MVC   PQPAGES,PQPAGEH     RESET LOW PAGE TO HIGH IN PREV               
*                                                                               
ADDI     TM    ADDWHY,X'04'        RETURN TO ASA CONVERSION                     
         BZ    *+12                                                             
         NI    ADDWHY,X'FF'-X'04'                                               
         B     ADD2B                                                            
         TM    ADDWHY,X'08'        RETURN TO PUT DATA IN BUFFER                 
         BZ    *+12                                                             
         NI    ADDWHY,X'FF'-X'08'                                               
         B     ADDA                                                             
         DC    H'0'                                                             
*                                                                               
ADDERR1  MVI   PARM3,X'41'         FORMAT ERROR                                 
         B     ADDERRX                                                          
ADDERR2  MVI   PARM3,X'40'         DISK ERROR                                   
         B     ADDERRX                                                          
ADDERR3  MVI   PARM3,X'80'         END OF FILE ERROR                            
         OC    PARM3(1),EOFERRRC                                                
ADDERRX  MVC   FLAG,PARM3          SAVE ERROR TYPE                              
         MVC   REPTINFO,0(R4)                                                   
         MVI   P1,1                                                             
         BRAS  RE,CLS              CLOSE FILE DUE TO ERROR ON ADD               
         MVC   PARM3(1),FLAG                                                    
*                                                                               
ADDX     MVC   PLCC(1),SAVECC                                                   
         B     DMPRTQX                                                          
         EJECT                                                                  
* SUBROUTINE TO READ FIRST RECORD IN A PQFILE CONTROL INTERVAL                  
*                                                                               
RDFFST   NTR1                      READ FIRST FILE RECORD                       
         LA    R1,1                                                             
         B     RDFFST0                                                          
RDFADD   NTR1                      READ FIRST FILE RECORD FOR ADD               
         SR    R1,R1                                                            
         B     RDFFST0                                                          
*                                                                               
RDFFST0  MVI   DMCB+8,0                                                         
         LR    R5,R0               R0=A(BUFFER) & CIADDR GIVES CI               
         SR    R7,R4                                                            
         AR    R7,R0                                                            
*                                                                               
         XC    SKXADDR,SKXADDR     RESET INDEX ADDR IN BUFFER                   
         MVC   SKADDR,CIADDR       SET FILE ADDR IN BUFFER                      
         MVC   SKSTRCI,CIADDR      SET START OF THIS CI                         
         LTR   R1,R1                                                            
         BZ    RDFFST2                                                          
         GOTO1 DATAMGR,DMCB,(X'00',DMREAD),PQFILE,CIADDR,(R5)                   
         CLI   DMCB+8,0                                                         
         BE    RDFFST2                                                          
         XC    SKADDR,SKADDR       RESET FILE ADDR ON ERROR                     
         B     RDFFSTX                                                          
*                                                                               
RDFFST2  XC    SKLINES,SKLINES     INIT LINES/PAGES READ                        
         XC    SKPAGES,SKPAGES                                                  
         MVC   SKNXTCI(2),PQCINEXT SET START OF NEXT CI                         
         MVC   SKNXTCI+2(2),=X'0100'                                            
         SR    RF,RF                                                            
         ICM   RF,3,CIADDR                                                      
         LH    R0,CITRKS                                                        
         CLM   RF,3,CJSTTRK                                                     
         BL    *+8                                                              
         LH    R0,CJTRKS                                                        
         AR    RF,R0                                                            
         BCTR  RF,0                                                             
         SLL   RF,16                                                            
         ST    RF,SKENDCI          SET END OF THIS CI                           
         MVC   SKENDCI+2(1),CIHIREC+1                                           
         LA    RF,PQDATA-PQINDEX                                                
         CLC   CIADDR,SKFSTCI                                                   
         BNE   RDFFST4                                                          
         LA    RF,PQDATA1-PQINDEX                                               
*                                                                               
RDFFST3  CLC   PQDATA1(8),ARCLINE  SKIP PAST <ARC=CARD>                         
         BNE   RDFFST4                                                          
         LA    RF,ARCLINEL(RF)                                                  
*                                                                               
RDFFST4  STH   RF,SKDISP                                                        
         XC    SKLEN,SKLEN                                                      
*                                                                               
RDFFSTX  CLI   DMCB+8,0            SET CC TO EQL IF NO ERROR                    
         XIT1  ,                                                                
         EJECT                                                                  
* SUBROUTINE TO READ NEXT RECORD IN A PQFILE FILE                               
*                                                                               
RDFNXT   NTR1                      READ NEXT FILE RECORD                        
         LR    R5,R4                                                            
         MVI   DMCB+8,0                                                         
*                                                                               
RDFNXT0  LR    RE,R5               POINT TO NEXT REC IN BUFFER                  
         AH    RE,SKDISP                                                        
         MVC   SKLEN,0(RE)         SAVE LENGTH OF REC IN BUFFER                 
         CLC   SKLEN,=H'1024'                                                   
         BH    RDFNXTA             FORMAT ERROR IF FUNNY LENGTH                 
         LH    R1,SKLEN                                                         
         CHI   R1,1                                                             
         BE    RDFNXT8             END OF REPORT                                
         BL    RDFNXT4             END OF BLOCK                                 
         LH    R0,CIBLKLN                                                       
         SH    R0,SKDISP                                                        
         CR    R1,R0                                                            
         BH    RDFNXTA             FORMAT ERROR IF BUFFER OVERFLOW              
*                                                                               
RDFNXT1  TM    PQLINET,PQLTFL      TEST FOR F/L REC                             
         BO    RDFNXT1A                                                         
         TM    PARM1,X'01'         TEST IF CALLER WANTS REC LEN                 
         BZ    *+12                                                             
         L     RF,APARM            YES RETURN IN DMCB+6(2)                      
         STH   R1,22(RF)                                                        
         LR    R0,R3                                                            
         LR    RF,R1                                                            
         MVCL  R0,RE               MOVE V/L REC TO CALLERS PARM4                
         B     RDFNXT3                                                          
RDFNXT1A SR    RF,RF               FILL F/L REC WITH SPACES                     
         IC    RF,PQLINEW                                                       
         TM    PQLINET,PQLTCC      ADJUST FOR CC CHR                            
         BO    *+6                                                              
         BCTR  RF,0                                                             
         MVI   0(R3),C' '                                                       
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   1(0,R3),0(R3)                                                    
         AHI   R1,-3               GET ACTUAL DATA LEN-1                        
         BM    RDFNXT1B                                                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),2(RE)       MOVE F/L REC SIGNIFICANT DATA                
RDFNXT1B AHI   R1,1                                                             
         TM    PARM1,X'01'         TEST IF CALLER WANTS REC LEN                 
         BZ    *+12                                                             
         L     RF,APARM            YES RETURN SIGNIF LEN IN DMCB+6(2)           
         STH   R1,22(RF)                                                        
*                                                                               
RDFNXT3  LH    R1,SKLEN            BUMP BUFFER DISPLACEMENT                     
         AH    R1,SKDISP                                                        
         STH   R1,SKDISP                                                        
         TM    PQLINET,PQLTCC      TEST IF DATA HAS CC CHR                      
         BO    RDFNXT3A                                                         
         L     R1,SKLINES          BUMP LINE COUNT FOR NON CC RECS              
         LA    R1,1(R1)                                                         
         ST    R1,SKLINES                                                       
         B     RDFNXTX                                                          
RDFNXT3A LR    RF,R3               POINT TO CC CHR                              
         TM    PQLINET,PQLTFL                                                   
         BO    *+8                                                              
         LA    RF,2(RF)                                                         
         CLI   0(RF),X'89'         TEST CC CHR FOR NEW PAGE                     
         BL    RDFNXT3B                                                         
         L     R1,SKPAGES          BUMP PAGE COUNT                              
         LA    R1,1(R1)                                                         
         ST    R1,SKPAGES                                                       
RDFNXT3B LH    R1,SKLEN                                                         
         AHI   R1,-3                                                            
         BNP   RDFNXTX                                                          
         L     R1,SKLINES          BUMP NON BLANK LINE COUNT                    
         LA    R1,1(R1)                                                         
         ST    R1,SKLINES                                                       
         B     RDFNXTX                                                          
*                                                                               
RDFNXT4  SR    RF,RF               BUMP TO NEXT BLOCK                           
         IC    RF,SKADDR+2                                                      
         LA    RF,1(RF)                                                         
         STC   RF,SKADDR+2                                                      
         CH    RF,CIHIREC                                                       
         BNH   RDFNXT6                                                          
         ICM   RF,3,SKADDR         BUMP TO NEXT TRACK                           
         LA    RF,1(RF)                                                         
         STH   RF,SKADDR                                                        
         MVI   SKADDR+2,1                                                       
         CLM   RF,3,SKENDCI                                                     
         BNH   RDFNXT6                                                          
         MVC   SKSTRCI,SKNXTCI     BUMP TO NEXT CI                              
         MVC   SKADDR,SKNXTCI                                                   
         SR    RF,RF                                                            
         ICM   RF,3,SKADDR                                                      
         BZ    RDFNXTA                                                          
         LH    R0,CITRKS                                                        
         CLM   RF,3,CJSTTRK                                                     
         BL    *+8                                                              
         LH    R0,CJTRKS                                                        
         AR    RF,R0                                                            
         BCTR  RF,0                                                             
         STH   RF,SKENDCI                                                       
*                                                                               
RDFNXT6  GOTO1 DATAMGR,DMCB,(X'00',DMREAD),PQFILE,SKADDR,(R5)                   
         CLI   DMCB+8,0                                                         
         BNE   RDFNXTA+4                                                        
         LA    RF,PQDATA-PQINDEX                                                
         CLC   SKADDR,SKFSTCI                                                   
         BNE   RDFNXT7                                                          
         LA    RF,PQDATA1-PQINDEX                                               
         CLC   PQDATA1(8),ARCLINE  SKIP PAST <ARC=CARD>                         
         BNE   RDFNXT7                                                          
         LA    RF,ARCLINEL(RF)                                                  
RDFNXT7  STH   RF,SKDISP                                                        
         CLC   SKADDR,SKNXTCI      IS THIS FIRST REC OF CI                      
         BNE   RDFNXT0             NO                                           
         MVC   SKNXTCI(2),PQCINEXT YES SAVE START OF NEXT CI                    
         B     RDFNXT0                                                          
*                                                                               
RDFNXT8  MVI   DMCB+8,X'90'        SET EOF                                      
         B     RDFNXTB                                                          
RDFNXTA  MVI   DMCB+8,X'41'        FORMAT ERROR                                 
         CLI   SKACTN,ACTNREA                                                   
         BNE   *+10                                                             
         XC    SKFSTCI,SKFSTCI     RESET FOR NO I/O                             
RDFNXTB  XC    SKADDR,SKADDR       RESET FOR FIRST I/O                          
*                                                                               
RDFNXTX  CLI   DMCB+8,0            SET CC TO EQL IF NO ERROR                    
         XIT1                                                                   
         EJECT                                                                  
* SUBROUTINE TO READ RANDOM PQFILE RECORDS                                      
* PAGELINE IS C'L' FOR LINE WITH FUL=LINE NUM                                   
* PAGELINE IS C'P' FOR PAGE WITH FUL=PAGE NUM & FUL1=LINE NUM                   
*                                                                               
RAN      NTR1                                                                   
         LR    R5,R4                                                            
         MVI   DMCB+8,0                                                         
         CLI   FUL,0                                                            
         BNE   RANE                TEST SILLY LINE/PAGE NUMBER                  
         OC    FUL,FUL                                                          
         BZ    RAN2A               SPECIAL CODE FOR RECORD ZERO                 
*                                                                               
RAN2     OC    SKADDR,SKADDR       READ FIRST BLOCK AND INITIALISE              
         BNZ   RAN4                                                             
RAN2A    LR    R0,R4               USE CALLERS BUFFER                           
         MVC   CIADDR,SKFSTCI                                                   
         BRAS  RE,RDFFST                                                        
         BNE   RANG                                                             
         LR    R1,R7               COPY SAVE INTO CXREC BUFFER                  
         SR    R1,R4                                                            
         LA    RE,CXREC(R1)                                                     
         MVC   0(SKEND-SKBCTRL,RE),0(R7)                                        
         OC    FUL,FUL                                                          
         BNZ   RAN4                                                             
         MVC   0(10,R3),SOFLAB     RETURN HEADER RECORD IF FUL=ZERO             
         MVC   QLREPRCI,CIADDR                                                  
         MVI   QLEXTRA,X'FF'                                                    
         MVC   QLINDEX,PQINDEX                                                  
         MVC   QLAGELT(4),PQINDEX+20                                            
         MVC   QLBATTR,PQBATTR                                                  
         MVC   QLNCIX,PQNCIX                                                    
         MVC   QLFATTR,PQFATTR                                                  
         MVC   QLPIDNUM,PQPIDNUM                                                
         B     RANX                                                             
*                                                                               
RAN4     CLI   PAGELINE,C'L'       TEST IF THIS LINE IS NEXT LINE               
         BNE   RAN4A                                                            
         L     R0,FUL                                                           
         BCTR  R0,0                                                             
         C     R0,SKLINES                                                       
         BNE   RAN4B                                                            
         BRAS  RE,RDFNXT           RECNUM IS ONE HIGHER THAN PREV               
         BE    RANX                                                             
         TM    DMCB+8,X'90'                                                     
         BO    RANE                                                             
         B     RANG                                                             
RAN4A    TM    PQLINET,PQLTCC      TEST IF CC CHR                               
         BZ    RANE                EXIT WITH ERROR IF NO PAGES                  
*                                                                               
RAN4B    LA    R0,1                TEST IF RECORD IN BUFFER                     
         CLC   SKADDR,SKFSTCI                                                   
         BE    RAN4C                                                            
         ICM   R0,7,PQLINES        R0 IS LOW LINE/PAGE                          
         CLI   PAGELINE,C'L'                                                    
         BE    RAN4C                                                            
         SR    R0,R0                                                            
         ICM   R0,3,PQPAGES                                                     
RAN4C    C     R0,FUL              TEST LOW RECORD WITH REQUIRED                
         BH    RAN4E                                                            
         ICM   R0,7,PQLINEH        R0 IS HIGH LINE/PAGE                         
         CLI   PAGELINE,C'L'                                                    
         BE    RAN4D                                                            
         TM    PQBFLAG,PQBFNP      TEST IF THIS BUFF HAS NEW PAGE               
         BZ    RAN4E                                                            
         SR    R0,R0                                                            
         ICM   R0,3,PQPAGEH                                                     
RAN4D    C     R0,FUL              TEST HIGH RECORD WITH REQUIRED               
         BNL   RANC                RECORD IS IN THIS BLOCK                      
RAN4E    CLC   SKADDR,SKFSTCI                                                   
         BE    RAN4F                                                            
         XC    SKADDR,SKADDR       RECORD NOT IN BLOCK SO READ FIRST            
         B     RAN2                                                             
RAN4F    SR    R0,R0               GET MAX LINES/PAGES FROM 1ST BLOCK           
         ICM   R0,7,PQLINES                                                     
         CLI   PAGELINE,C'L'                                                    
         BE    *+10                                                             
         SR    R0,R0                                                            
         ICM   R0,3,PQPAGES                                                     
         C     R0,FUL              TEST MAX RECORD WITH REQUIRED                
         BL    RANE                                                             
         LR    R1,R7               COPY SAVE INTO CXREC BUFFER                  
         SR    R1,R4                                                            
         LA    RE,CXREC(R1)                                                     
         MVC   0(SKEND-SKBCTRL,RE),0(R7)                                        
*                                                                               
RAN6     OC    SKNXTCI(2),SKNXTCI  CALLERS BUFFER HAS FIRST CI REC              
         BZ    RAN8                                                             
         LA    R0,CXREC            READ FIRST REC IN NEXT CI INTO CXREC         
         MVC   CIADDR,SKNXTCI                                                   
         BRAS  RE,RDFFST                                                        
         BNE   RANG                                                             
         SR    R0,R0               R0 CONTAINS LOW LINE/PAGE NEXT CI            
         ICM   R0,7,CXREC+PQLINES-PQINDEX                                       
         CLI   PAGELINE,C'L'                                                    
         BE    RAN6A                                                            
         SR    R0,R0                                                            
         ICM   R0,3,CXREC+PQPAGES-PQINDEX                                       
         C     R0,FUL                                                           
         BL    RAN6B                                                            
         BH    RAN8                                                             
         TM    CXREC+PQBFLAG-PQINDEX,PQBFNP                                     
         BZ    RAN8                                                             
         B     RAN6B                                                            
RAN6A    C     R0,FUL                                                           
         BNL   RAN8                                                             
RAN6B    LR    R0,R4               COPY CXREC INTO CALLERS BUFFER               
         LA    RE,CXREC                                                         
         LR    R1,R7                                                            
         SR    R1,R4                                                            
         LA    R1,SKEND-SKBCTRL(R1)                                             
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
         SR    R0,R0                                                            
         ICM   R0,7,PQLINES                                                     
         CLI   PAGELINE,C'L'                                                    
         BE    RAN6C                                                            
         TM    PQBFLAG,PQBFNP      TEST IF BUFFER CONTAINS NEW PAGE             
         BZ    RAN6                                                             
         SR    R0,R0                                                            
         ICM   R0,3,PQPAGES                                                     
RAN6C    C     R0,FUL                                                           
         BE    RANC                RECORD IS IN 1ST BLOCK OF THIS CI            
         B     RAN6                                                             
*                                                                               
RAN8     CLC   SKADDR(2),SKENDCI   CALLERS BUFFER HAS THE HOME CI               
         BE    RANA                                                             
         MVC   CIADDR,SKADDR       BUMP TO NEXT TRACK                           
         SR    RF,RF                                                            
         ICM   RF,3,CIADDR                                                      
         LA    RF,1(RF)                                                         
         STH   RF,CIADDR                                                        
         GOTO1 DATAMGR,DMCB,(X'00',DMREAD),PQFILE,CIADDR,CXREC                  
         CLI   DMCB+8,0                                                         
         BNE   RANG                                                             
*                                                                               
         CLC   PQSRCID(7),CXREC+PQSRCID-PQINDEX                                 
         BNE   RANA                                                             
         CLC   PQAGELD,CXREC+PQAGELD-PQINDEX                                    
         BNE   RANA                                                             
         CLC   PQAGELT,CXREC+PQAGELT-PQINDEX                                    
         BNE   RANA                                                             
*                                                                               
         SR    R0,R0               SET RO TO LOW PAGE/LINE IN BLOCK             
         ICM   R0,7,CXREC+PQLINES-PQINDEX                                       
         CLI   PAGELINE,C'L'                                                    
         BE    RAN8A                                                            
         SR    R0,R0                                                            
         ICM   R0,3,CXREC+PQPAGES-PQINDEX                                       
         C     R0,FUL                                                           
         BL    RAN8B                                                            
         BH    RANA                                                             
         TM    CXREC+PQBFLAG-PQINDEX,PQBFNP                                     
         BZ    RANA                                                             
         B     RAN8B                                                            
RAN8A    C     R0,FUL                                                           
         BNL   RANA                                                             
RAN8B    MVC   SKADDR,CIADDR                                                    
         LR    R0,R4               COPY CXREC INTO CALLERS BUFFER               
         LA    RE,CXREC                                                         
         LH    R1,CIBLKLN                                                       
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
         SR    R0,R0                                                            
         ICM   R0,7,PQLINES                                                     
         CLI   PAGELINE,C'L'                                                    
         BE    RAN8C                                                            
         TM    PQBFLAG,PQBFNP      TEST IF BUFFER CONTAINS NEW PAGE             
         BZ    RAN8                                                             
         SR    R0,R0                                                            
         ICM   R0,3,PQPAGES                                                     
RAN8C    C     R0,FUL              REC IS IN 1ST BLOCK OF THIS TRK              
         BE    RANC                                                             
         B     RAN8                                                             
*                                                                               
RANA     LA    R0,1                CALLERS BUFFER HAS THE HOME TRK              
         CLC   SKADDR,SKFSTCI                                                   
         BE    RANA2                                                            
         ICM   R0,7,PQLINES        SET R0 TO HIGH PAGE/LINE                     
         CLI   PAGELINE,C'L'                                                    
         BE    RANA2                                                            
         SR    R0,R0                                                            
         ICM   R0,3,PQPAGES                                                     
RANA2    C     R0,FUL                                                           
         BH    RANA4                                                            
         SR    R0,R0                                                            
         ICM   R0,7,PQLINEH        SET R0 TO LOW PAGE/LINE                      
         CLI   PAGELINE,C'L'                                                    
         BE    RANA3                                                            
         TM    PQBFLAG,PQBFNP      TEST IF BUFFER CONTAINS NEW PAGE             
         BZ    RANA4                                                            
         SR    R0,R0                                                            
         ICM   R0,3,PQPAGEH                                                     
RANA3    C     R0,FUL                                                           
         BNL   RANC                RECORD IS IN THIS BLOCK                      
RANA4    IC    RF,SKADDR+2                                                      
         LA    RF,1(RF)                                                         
         STC   RF,SKADDR+2                                                      
         CLC   SKADDR+2(1),SKENDCI+2                                            
         BH    RANF                                                             
         GOTO1 DATAMGR,DMCB,(X'00',DMREAD),PQFILE,SKADDR,(R4)                   
         CLI   DMCB+8,0                                                         
         BNE   RANG                                                             
         B     RANA                                                             
*                                                                               
RANC     LA    R0,1                CALLERS BUFFER HAS THE HOME BLOCK            
         LA    RE,PQDATA1                                                       
         XC    SKLINES,SKLINES                                                  
         XC    SKPAGES,SKPAGES                                                  
         CLC   SKADDR,SKFSTCI      TEST FOR FIRST BLOCK IN FILE                 
         BNE   RANC0A                                                           
         CLC   PQDATA1(8),ARCLINE  SKIP PAST <ARC=CARD>                         
         BNE   RANC0B                                                           
         LA    RE,ARCLINEL(RE)                                                  
         B     RANC0B                                                           
RANC0A   ICM   R0,7,PQLINES        R0 IS LOW PAGE/LINE IN BLOCK                 
         ST    R0,SKLINES                                                       
         TM    PQLINET,PQLTCC                                                   
         BZ    *+10                                                             
         MVC   SKPAGES+2(2),PQPAGES                                             
         CLI   PAGELINE,C'L'                                                    
         BE    *+10                                                             
         SR    R0,R0                                                            
         ICM   R0,3,PQPAGES                                                     
         LA    RE,PQDATA                                                        
RANC0B   L     R1,FUL                                                           
         SR    R1,R0                                                            
         LA    R1,1(R1)            R1=RELATIVE REC NUM (FIRST=1)                
         CLI   PAGELINE,C'L'                                                    
         BNE   RANC3                                                            
*                                                                               
RANC1    MVC   HALF,0(RE)          SEARCH FOR NTH LINE IN BLOCK                 
         LH    R0,HALF                                                          
         CHI   R0,1                                                             
         BNH   RANF                FORMAT ERROR ON END OF BLOCK                 
         TM    PQLINET,PQLTCC                                                   
         BZ    RANC2                                                            
         CHI   R0,3                BLANK CC CHR LINES DONT COUNT                
         BNH   *+12                                                             
RANC2    AHI   R1,-1               DECR LINE NUMBER IN BLOCK                    
         BZ    RANC5                                                            
         AR    RE,R0               BUMP TO NEXT LINE IN BLOCK                   
         B     RANC1                                                            
*                                                                               
RANC3    MVC   HALF,0(RE)          SEARCH FOR NTH PAGE IN BLOCK                 
         LH    R0,HALF                                                          
         CHI   R0,1                                                             
         BNH   RANF                FORMAT ERROR ON END OF BLOCK                 
         CLI   2(RE),X'89'                                                      
         BL    *+12                                                             
RANC4    AHI   R1,-1               DECR PAGE NUMBER IN BLOCK                    
         BZ    RANC5                                                            
         CHI   R0,3                                                             
         BNH   *+16                                                             
         L     RF,SKLINES          BUMP NON BLANK LINE COUNT                    
         LA    RF,1(RF)                                                         
         ST    RF,SKLINES                                                       
         AR    RE,R0               BUMP TO NEXT LINE IN BLOCK                   
         B     RANC3                                                            
*                                                                               
RANC5    MVC   SKLEN,0(RE)         SAVE LEN OF RECORD                           
         CLC   SKLEN,=H'1024'                                                   
         BH    RANF                FORMAT ERROR IF FUNNY LENGTH                 
         LH    R1,SKLEN                                                         
         CHI   R1,1                                                             
         BNH   RANF                FORMAT ERROR IF END OF BLOCK                 
         LA    R0,0(RE,R1)         SAVE DISPLACEMENT OF NEXT REC                
         SR    R0,R4                                                            
         STH   R0,SKDISP                                                        
         CH    R0,CIBLKLN                                                       
         BH    RANF                FORMAT ERROR IF BUFFER OVERFLOW              
*                                                                               
RANC6    CLI   PAGELINE,C'L'       SAVE LINE NUM FOR LINE OPTION                
         BNE   RANC7                                                            
         MVC   SKLINES,FUL                                                      
         XC    SKPAGES,SKPAGES                                                  
         B     RAND                                                             
RANC7    MVC   SKPAGES,FUL         SAVE PAGE NUM FOR PAGE OPTION                
         CLC   FUL1,=F'256'                                                     
         BH    RANE                TEST SILLY LINE WITHIN PAGE NUMBER           
         L     R1,FUL1                                                          
         LTR   R1,R1               LINE ZERO MEANS WE HAVE IT ALREADY           
         BZ    RAND                                                             
RANC8    BRAS  RE,RDFNXT           GET NEXT LINE IN PAGE INTO 0(R3)             
         BE    RANC9                                                            
         TM    DMCB+8,X'90'                                                     
         BO    RANE                                                             
         B     RANG                                                             
RANC9    CLI   0(R3),X'89'         TEST IF NEW PAGE BEFORE LINE FOUND           
         BNL   RANE                                                             
         CLC   SKLEN,=H'3'         EMPTY PRINT LINES DONT COUNT                 
         BNH   RANC8                                                            
         L     RF,SKLINES          BUMP NON BLANK LINE COUNT                    
         LA    RF,1(RF)                                                         
         ST    RF,SKLINES                                                       
         BCT   R1,RANC8                                                         
         B     RANX                                                             
*                                                                               
RAND     TM    PQLINET,PQLTFL      TEST FOR F/L REC                             
         BO    RAND1                                                            
         TM    PARM1,X'01'         TEST IF CALLER WANTS REC LEN                 
         BZ    *+12                                                             
         L     RF,APARM            YES RETURN LEN IN DMCB+6(2)                  
         STH   R1,22(RF)                                                        
         LR    R0,R3                                                            
         LR    RF,R1                                                            
         MVCL  R0,RE               MOVE V/L REC TO CALLERS PARM4                
         B     RANX                                                             
RAND1    SR    RF,RF               FILL F/L REC WITH SPACES                     
         IC    RF,PQLINEW                                                       
         TM    PQLINET,PQLTCC      ADJUST FOR CC CHR                            
         BO    *+6                                                              
         BCTR  RF,0                                                             
         MVI   0(R3),C' '                                                       
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   1(0,R3),0(R3)                                                    
         AHI   R1,-3                                                            
         BM    RAND2                                                            
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),2(RE)       MOVE F/L REC SIGNIFICANT DATA                
RAND2    AHI   R1,1                                                             
         TM    PARM1,X'01'         TEST IF CALLER WANTS REC LEN                 
         BZ    RANX                                                             
         L     RF,APARM            YES RETURN SIGNIF LEN IN DMCB+6(2)           
         STH   R1,22(RF)                                                        
         B     RANX                                                             
*                                                                               
RANE     MVI   DMCB+8,X'90'        SET EOF/NOTFOUND                             
         B     RANH                                                             
RANF     MVI   DMCB+8,X'41'        SET FORMAT ERROR                             
         B     RANH                                                             
RANG     EQU   *                   SET DISK ERROR                               
*                                                                               
RANH     XC    SKADDR,SKADDR       RESET FOR FIRST I/O                          
         B     RANX                                                             
*                                                                               
RANX     CLI   DMCB+8,0                                                         
         XIT1                                                                   
         EJECT                                                                  
* SUBROUTINE TO CHANGE STATUS OF FILE - DRIVEN BY STPARM VALUES                 
*                                                                               
STAT     NTR1                                                                   
         MVI   DMCB+8,0            SET RETURN DATAMGR CODE                      
         MVI   STFLAG,0                                                         
*                                                                               
STAT0    CLI   STACTN,0            SET NI/OI BITS IF ACTION SET                 
         BNE   STAT0A                                                           
         TM    STVAL,PQSTLIVE                                                   
         BZ    *+8                                                              
         OI    STFLAG,X'80'        SET FLAG FOR LIVE REPORT                     
         TM    STVAL,PQSTDEAD                                                   
         BZ    *+8                                                              
         OI    STFLAG,X'40'        SET FLAG FOR DEAD REPORT                     
         B     STAT2                                                            
*                                                                               
STAT0A   CLI   STACTN,ACTNACT      TEST SET STATUS TO ACTIVE                    
         BNE   STAT0B                                                           
         MVI   STNI,255-PQSTDEAD-PQSTHO                                         
         MVI   STOI,PQSTAC                                                      
         OI    STFLAG,X'80'        SET LIVE FLAG                                
         B     STAT1                                                            
*                                                                               
STAT0B   CLI   STACTN,ACTNHOL      TEST SET STATUS TO HOLD                      
         BNE   STAT0C                                                           
         MVI   STNI,255-PQSTDEAD-PQSTAC                                         
         MVI   STOI,PQSTHO                                                      
         OI    STFLAG,X'80'        SET LIVE FLAG                                
         B     STAT1                                                            
*                                                                               
STAT0C   CLI   STACTN,ACTNPRI      TEST SET STATUS TO PRINTED                   
         BNE   STAT0D                                                           
         MVI   STNI,255-PQSTLIVE-PQSTSE                                         
         MVI   STOI,PQSTPR                                                      
         OI    STFLAG,X'40'        SET DEAD FLAG                                
         B     STAT1                                                            
*                                                                               
STAT0D   CLI   STACTN,ACTNSEN      TEST SET STATUS TO SENT                      
         BNE   STAT0E                                                           
         MVI   STNI,255-PQSTLIVE-PQSTPR                                         
         MVI   STOI,PQSTSE                                                      
         OI    STFLAG,X'40'        SET DEAD FLAG                                
         B     STAT1                                                            
*                                                                               
STAT0E   CLI   STACTN,ACTNKEE      TEST SET STATUS TO KEEP                      
         BNE   STAT0F                                                           
         MVI   STNI,255                                                         
         MVI   STOI,PQSTKE                                                      
         OI    STFLAG,X'20'        SET SUB STATUS FLAG                          
         B     STAT2                                                            
*                                                                               
STAT0F   CLI   STACTN,ACTNUNK      TEST SET STATUS TO UNKEEP                    
         BNE   STAT0G                                                           
         MVI   STNI,255-PQSTKE                                                  
         MVI   STOI,0                                                           
         OI    STFLAG,X'20'        SET SUB STATUS FLAG                          
         B     STAT2                                                            
*                                                                               
STAT0G   CLI   STACTN,ACTNPUR      TEST SET STATUS TO PURGED                    
         BNE   STAT0H                                                           
         MVI   STNI,0                                                           
         MVI   STOI,0                                                           
         B     STAT2                                                            
*                                                                               
STAT0H   CLI   STACTN,ACTNRET      TEST SET NEW RETAIN INFO                     
         BNE   STAT0I                                                           
         MVI   STNI,255                                                         
         MVI   STOI,0                                                           
         OI    STFLAG,X'20'        SET SUB STATUS FLAG                          
         MVC   STINFO(2),UKINFO    GET RETAIN INFO FROM USER KEY                
         MVC   STINFO+2(1),UKFLAG                                               
         OC    STINFO(3),STINFO                                                 
         BNZ   STAT0H2                                                          
         DC    H'0',C'INVALID RETAIN DATE/TIME'                                 
STAT0H2  TM    STINFO+2,UKFLHRS    TEST IF PASSING HOURS                        
         BZ    STAT2                                                            
         MVC   DUB(3),DATEC        SET DATEC/TIMEI NOW                          
         MVC   DUB+3(2),STINFO     SET PASSED HOURS                             
         MVI   DUB+5,0                                                          
         L     RF,=A(GETRETN)                                                   
         BASR  RE,RF               COMPUTE NEW RETAIN INFO                      
         MVC   STINFO(3),DUB1                                                   
         B     STAT2                                                            
*                                                                               
STAT0I   CLI   STACTN,ACTNERX      TEST ERROR/UNERROR ATTRIBUTE CHANGE          
         BE    *+12                                                             
         CLI   STACTN,ACTNERR      TEST UNERROR ATTRIBUTE CHANGE                
         BNE   STAT0J                                                           
         MVI   STNI,255                                                         
         MVI   STOI,0                                                           
         OI    STFLAG,X'20'        SET SUB STATUS FLAG                          
         B     STAT2                                                            
*                                                                               
STAT0J   CLI   STACTN,ACTNINV      TEST SET STATUS TO INVISIBLE                 
         BNE   STAT0K                                                           
         MVI   STNI,255                                                         
         MVI   STOI,PQSTIN                                                      
         OI    STFLAG,X'20'        SET SUB STATUS FLAG                          
         B     STAT2                                                            
*                                                                               
STAT0K   CLI   STACTN,ACTNVIS      TEST SET STATUS TO VISIBLE                   
         BNE   STAT0L                                                           
         MVI   STNI,255-PQSTIN                                                  
         MVI   STOI,0                                                           
         OI    STFLAG,X'20'        SET SUB STATUS FLAG                          
         B     STAT2                                                            
*                                                                               
STAT0L   CLI   STACTN,ACTNJIS      TEST ATTB JOB INP/OUT FLAGS                  
         BL    STAT0M                                                           
         CLI   STACTN,ACTNJOU                                                   
         BH    STAT0M                                                           
         MVI   STNI,255                                                         
         MVI   STOI,0                                                           
         OI    STFLAG,X'20'+X'10'  SET SUB STATUS AND ONLY ONE CI               
         B     STAT2                                                            
*                                                                               
STAT0M   CLI   STACTN,ACTNARS      TEST ARCHIVE/BACKUP FLAGS                    
         BL    STAT0N                                                           
         CLI   STACTN,ACTNBKP                                                   
         BH    STAT0N                                                           
         MVI   STNI,255                                                         
         MVI   STOI,0                                                           
         OI    STFLAG,X'20'+X'10'  SET SUB STATUS AND ONLY ONE CI               
*                                                                               
         CLI   STACTN,ACTNADP      TEST SETTING ARCHIVED IN PROCESS             
         BE    *+12                                                             
         CLI   STACTN,ACTNBKP      TEST SETTING BACK UP IN PROCESS              
         BNE   STAT2                                                            
         MVI   STOI,X'01'          SET TEMP FLAG REQUIRED IN STATUS             
         OI    STCTRL,X'04'        SET DONT UPDATE CI REC                       
         B     STAT2                                                            
*                                                                               
STAT0N   CLI   STACTN,ACTNCLS      TEST SET NEW CLASS                           
         BNE   STAT0P                                                           
         MVI   STNI,255                                                         
         MVI   STOI,0                                                           
         L     RE,PARM1            TEST FOR CLARET ACTION                       
         CLC   3(3,RE),=C'RET'                                                  
         BNE   STAT0N2                                                          
         MVI   STNI,255-PQSTDEAD-PQSTHO                                         
         MVI   STOI,PQSTAC         SET TO ACTIVATE REPORT                       
         OI    STFLAG,X'80'        SET LIVE FLAG                                
STAT0N2  OI    STFLAG,X'20'        SET SUB STATUS FLAG                          
         MVC   STINFO+2(1),UKINFO  GET CLASS FROM USER KEY                      
         CLI   UKINFO,C'A'         VALIDATE REPORT CLASS                        
         BNL   STAT0N3                                                          
         CLI   UKINFO,C'$'         ALLOWABLE SPECIAL CLASS CHRS                 
         BE    STAT0N3                                                          
         DC    H'0',C'INVALID CLASS '                                           
STAT0N3  B     STAT1                                                            
*                                                                               
STAT0P   CLI   STACTN,ACTNSEX      TEST UNSECURE ATTRIBUTE CHANGE               
         BE    *+12                                                             
         CLI   STACTN,ACTNSEC      TEST SECURE ATTRIBUTE CHANGE                 
         BNE   STAT0Q                                                           
         MVI   STNI,255                                                         
         MVI   STOI,0                                                           
         OI    STFLAG,X'20'+X'10'  SET SUB STATUS AND ONLY ONE CI               
         B     STAT2                                                            
*                                                                               
STAT0Q   DC    H'0'                INVALID ACTION                               
*                                                                               
STAT1    L     RE,PARM1            TEST FOR C'...RET' IN ACTION NAME            
         CLC   3(3,RE),=C'RET'                                                  
         BNE   *+8                                                              
         MVI   STINFO+3,X'FF'      SET INFO TO UPDATE RETAIN INFO               
*                                                                               
STAT2    XC    BADCNDX,BADCNDX     CLEAR BAD REPORT DATA                        
         XC    BADXNDX,BADXNDX                                                  
         TM    STCTRL,X'01'        SET STATUS IN FILE RECORDS                   
         BZ    STAT4                                                            
         L     R5,STBUFFA          R5=A(I/O BUFFER FOR FILE RECS)               
         LA    R6,STCIAS           R6=A(LIST OF CI ADDRS)                       
         MVC   0(2,R6),STFSTCI                                                  
         MVC   2(2,R6),FFS                                                      
         MVC   SVNEXT(2),STFSTCI                                                
         MVC   SVNEXT+2(2),=X'0100'                                             
         GOTO1 ,DMCB,(X'00',DMREAD),PQFILE,CIADDR,(R5)                          
         TM    STCTRL,X'20'        TEST IF NATIVE CALL FROM USER                
         BZ    STAT2A                                                           
         CLC   SVNEXT,SKADDR       TEST IF FIRST REC IN USERS BUFFER            
         BNE   STAT2A                                                           
         MVC   CIADDR,SVNEXT       COPY USERS BUFFER INTO CXREC                 
         LR    R0,R5                                                            
         LH    R1,CIBLKLN                                                       
         LR    RE,R4                                                            
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
         B     STAT2B                                                           
*                                                                               
STAT2A   OC    SVNEXT(2),SVNEXT    READ FIRST REC OF NEXT CI                    
         BZ    STAT4                                                            
         MVC   CIADDR,SVNEXT                                                    
         MVC   0(2,R6),CIADDR      SAVE CI ADDR IN CASE OF ERROR                
         MVC   2(2,R6),FFS                                                      
         GOTO1 DATAMGR,DMCB,(X'00',DMREAD)                                      
         CLI   DMCB+8,0                                                         
         BNE   STAT2E                                                           
*                                                                               
STAT2B   CLC   CIADDR(2),STFSTCI   SAVE FIRST CI VALUES                         
         BNE   STAT2C                                                           
         MVC   SVDATA,PQINDEX      SVDATA CONTAINS INDEX OF 1ST CI              
         SR    RE,RE                                                            
         IC    RE,PQSEQ                                                         
         AHI   RE,-1                                                            
         STC   RE,SVSEQ            SVSEQ CONTAINS PREVIOUS CI SEQ NUM           
*                                                                               
STAT2B1  CLI   STCTRL,X'53'        TEST IF PURGING MULTI-CI REPORT              
         BNE   STAT2B2                                                          
         CLC   PQKEY,AVLXNDX       KEY IN FIRST CI MUST MATCH INDEX             
         BNE   STAT2C3                                                          
         CLI   PQSEQ,1             MUST BE MULTI CI                             
         BNE   STAT2C3                                                          
         OC    PQCINEXT,PQCINEXT   MUST HAVE VALID LINK TO NEXT CI              
         BZ    STAT2C3                                                          
*                                                                               
STAT2B2  XC    DUB,DUB             SAVE RETENTION INFO                          
         TM    STFLAG,X'C0'        TEST IF SETTING TO LIVE/DEAD                 
         BZ    STAT2C              NO                                           
         CLI   STINFO+3,X'FF'      YES TEST IF NEW RETAIN REQUIRED              
         BNE   STAT2C                                                           
         MVC   DUB(3),DATEC        SET DATEC/TIMEI NOW                          
         MVC   DUB+3(2),PQRETNL    SET LIVE RETENTION                           
         TM    STFLAG,X'80'                                                     
         BO    *+10                                                             
         MVC   DUB+3(2),PQRETND    SET DEAD RETENTION                           
         MVI   DUB+5,0                                                          
         L     RF,=A(GETRETN)                                                   
         BASR  RE,RF               COMPUTE NEW RETAIN INFO                      
*                                                                               
STAT2C   IC    RE,SVSEQ            BUMP SAVED PREVIOUS CI SEQ NUM               
         LA    RE,1(RE)                                                         
         STC   RE,SVSEQ                                                         
         CLC   PQSEQ,SVSEQ         CHECK CI SEQ NUM IS ONE HIGHER               
         BE    STAT2C2                                                          
         TM    PQTYPE,PQTYXCI      TEST IF THIS IS XTNSN CI                     
         BZ    STAT2C3                                                          
         CLI   SVSEQ,LASTSEQP      LAST NORMAL SHOULD BE 255                    
         BNE   STAT2C3                                                          
         CLI   PQSEQ,FRSTXTN       FIRST XTNSN SHOULD BE 2                      
         BNE   STAT2C3                                                          
         MVI   SVSEQ,FRSTXTN                                                    
STAT2C2  CLC   PQKEY,SVKEY         CHECK SAME KEY                               
         BE    STAT2D                                                           
STAT2C3  MVI   DMCB+8,X'41'        SET FORMAT ERROR                             
         B     STAT2E                                                           
*                                                                               
STAT2D   MVC   SVNEXT(2),PQCINEXT  BUILD CI ADDR LIST                           
         MVC   0(2,R6),CIADDR                                                   
         MVC   2(2,R6),FFS                                                      
         LA    R6,2(R6)                                                         
         TM    STFLAG,X'10'        TEST UPDATE FIRST CI ONLY                    
         BZ    STAT3                                                            
         XC    SVNEXT,SVNEXT       SET NO NEXT CI                               
         B     STAT3                                                            
*                                                                               
STAT2E   OI    STFLAG,X'08'        ERROR ON FILE IO                             
         CLI   DMCB+8,X'41'                                                     
         BE    *+8                                                              
         OI    STFLAG,X'04'        SET FLAG FOR DISK ERR                        
         LA    RE,STCIAS                                                        
         CR    R6,RE               TEST IF FIRST CI                             
         BNE   STAT2E1             WE DONT DO ANYTHING DIFFERENT                
*                                                                               
STAT2E1  MVC   BADCNDX,PQINDEX     SAVE THE BAD CI INDEX DATA                   
         BRAS  RE,LOGERR           LOG THE ERROR                                
         LA    RE,STCIAS                                                        
         XC    SVDATA,SVDATA       PURGE INDEX ENTRY ON ERROR                   
         MVC   2(2,RE),FFS         ADJUST LIST SO ONLY ONE CI                   
         CLI   STCTRL,X'53'        TEST IF PURGING MULTI CI REPORT              
         BNE   STAT4                                                            
         MVI   DMCB+8,X'00'        CLEAR ERROR IF REPORT TO BE PURGED           
         NI    STFLAG,255-X'08'-X'04'                                           
         B     STAT4               GO AND DO INDEX                              
*                                                                               
STAT3    CLI   STACTN,0            SET NEW STATUS FROM VALUE                    
         BNE   *+14                                                             
         MVC   PQSTAT,STVAL                                                     
         B     STAT3A                                                           
         NC    PQSTAT,STNI         SET NEW STATUS FROM ACTION                   
         OC    PQSTAT,STOI                                                      
*                                                                               
         CLI   STACTN,ACTNERX      TEST FOR UNERROR                             
         BNE   *+12                                                             
         NI    PQATTB,255-PQATERR                                               
         B     STAT3A                                                           
         CLI   STACTN,ACTNERR      TEST FOR ERROR                               
         BNE   STAT3A                                                           
         OI    PQATTB,PQATERR                                                   
*                                                                               
STAT3A   CLI   PQSTAT,PQSTPU       TEST IF NEW STATUS PURGED                    
         BNE   STAT3B                                                           
         XC    PQINDEX,PQINDEX     CLEAR WHOLE INDEX ENTRY                      
         MVI   SVDATA+PQSTAT-PQINDEX,PQSTPU                                     
         B     STAT3Y                                                           
*                                                                               
STAT3B   TM    STFLAG,X'C0'        TEST IF NEW STATUS LIVE/DEAD                 
         BZ    STAT3C                                                           
         OC    DUB(2),DUB          TEST IF NEW RETAIN REQUIRED                  
         BZ    STAT3C                                                           
         MVC   PQAGERD(3),DUB1     SET NEW COMPUTED RETAIN INFO                 
*                                                                               
STAT3C   CLI   STACTN,ACTNRET      TEST IF SETTING NEW RETAIN                   
         BNE   STAT3D                                                           
         MVC   PQAGERD(3),STINFO   SET PASSED RETAIN INFO                       
         B     STAT3X                                                           
*                                                                               
STAT3D   CLI   STACTN,ACTNJIS      TEST IF SETTING JOB INPUT                    
         BNE   STAT3E                                                           
         OI    PQATTB,PQATJOBI                                                  
         B     STAT3X                                                           
*                                                                               
STAT3E   CLI   STACTN,ACTNJIU      TEST IF UNSETTING JOB INPUT                  
         BNE   STAT3F                                                           
         NI    PQATTB,255-PQATJOBI                                              
         B     STAT3X                                                           
*                                                                               
STAT3F   CLI   STACTN,ACTNJOS      TEST IF SETTING JOB OUTPUT                   
         BNE   STAT3G                                                           
         OI    PQATTB,PQATJOBO                                                  
         B     STAT3X                                                           
*                                                                               
STAT3G   CLI   STACTN,ACTNJOU      TEST IF UNSETTING JOB OUTPUT                 
         BNE   STAT3H                                                           
         NI    PQATTB,255-PQATJOBO                                              
         B     STAT3X                                                           
*                                                                               
STAT3H   CLI   STACTN,ACTNARS      TEST IF SETTING ARCHIVABLE                   
         BNE   STAT3I                                                           
         OI    PQTYP1,PQTYAR                                                    
         NI    PQTYP1,255-PQTYAE-PQTYAD                                         
         B     STAT3X                                                           
*                                                                               
STAT3I   CLI   STACTN,ACTNARU      TEST IF UNSETTING ARCHIVABLE                 
         BNE   STAT3J                                                           
         NI    PQTYP1,255-PQTYAR                                                
         B     STAT3X                                                           
*                                                                               
STAT3J   CLI   STACTN,ACTNADS      TEST IF SETTING ARCHIVED                     
         BNE   STAT3K                                                           
         OI    PQTYP1,PQTYAD                                                    
         NI    PQTYP1,255-PQTYAE-PQTYAR                                         
         CLC   CIADDR(2),STFSTCI                                                
         BNE   STAT3X                                                           
         MVC   PQDATEA,DATEC       SET DATE/TIME NOW IN FIRST CI                
         MVC   PQTIMEA,TIMEB                                                    
         B     STAT3X                                                           
*                                                                               
STAT3K   CLI   STACTN,ACTNADU      TEST IF UNSETTING ARCHIVED                   
         BNE   STAT3L                                                           
         NI    PQTYP1,255-PQTYAD                                                
         B     STAT3X                                                           
*                                                                               
STAT3L   CLI   STACTN,ACTNAES      TEST IF SETTING ARCHIVE ELIGIBLE             
         BNE   STAT3M                                                           
         OI    PQTYP1,PQTYAE                                                    
         NI    PQTYP1,255-PQTYAR-PQTYAD                                         
         B     STAT3X                                                           
*                                                                               
STAT3M   CLI   STACTN,ACTNAEU      TEST IF UNSETTING ARCHIVE ELIGIBLE           
         BNE   STAT3N                                                           
         NI    PQTYP1,255-PQTYAE                                                
         B     STAT3X                                                           
*                                                                               
STAT3N   CLI   STACTN,ACTNBKS      TEST IF SETTING BACKUP FLAG                  
         BNE   STAT3O                                                           
         OI    PQTYP1,PQTYBKU                                                   
         TM    PQTYP1,PQTYAD       TEST IF ARCHIVE DATE/TIME SET                
         BO    STAT3X                                                           
         CLC   CIADDR(2),STFSTCI                                                
         BNE   STAT3X                                                           
         MVC   PQDATEA,DATEC       SET DATE/TIME NOW IN FIRST CI                
         MVC   PQTIMEA,TIMEB                                                    
         B     STAT3X                                                           
*                                                                               
STAT3O   CLI   STACTN,ACTNBKU      TEST IF UNSETTING BACKUP FLAG                
         BNE   STAT3P                                                           
         NI    PQTYP1,255-PQTYBKU                                               
         B     STAT3X                                                           
*                                                                               
STAT3P   CLI   STACTN,ACTNPRI      TEST SET STATUS TO PRINTED                   
         BNE   STAT3Q                                                           
         CLC   CIADDR(2),STFSTCI                                                
         BNE   STAT3Q                                                           
         MVC   PQDATED,DATEC       SET DATE/TIME NOW IN FIRST CI                
         MVC   PQTIMED,TIMEB                                                    
         B     STAT3X                                                           
*                                                                               
STAT3Q   CLI   STACTN,ACTNCLS      TEST SETTING CLASS                           
         BNE   STAT3R                                                           
         MVC   PQCLASS,STINFO+2                                                 
*                                                                               
STAT3R   CLI   STACTN,ACTNSEX      TEST FOR UNSECURE                            
         BNE   *+12                                                             
         NI    PQATTB,255-PQATPW                                                
         B     STAT3S                                                           
         CLI   STACTN,ACTNSEC      TEST FOR SECURE                              
         BNE   STAT3S                                                           
         OI    PQATTB,PQATPW                                                    
*                                                                               
STAT3S   EQU   *                                                                
*                                                                               
STAT3X   CLC   CIADDR(2),STFSTCI   TEST IF FIRST CI                             
         BNE   STAT3Y                                                           
         MVC   SVDATA,PQINDEX      SVDATA CONTAINS UPDATED INDEX DATA           
         TM    STCTRL,X'20'        TEST IF NATIVE CALL FROM USER                
         BZ    STAT3Y                                                           
         CLC   CIADDR,SKADDR       TEST IF FIRST REC IN USERS BUFFER            
         BNE   STAT3Y                                                           
         TM    STCTRL,X'04'        TEST DONT WRITE CI REC                       
         BO    STAT3Y                                                           
         MVC   0(L'PQINDEX,R4),PQINDEX PUT BACK UPDATED VERSION                 
*                                                                               
STAT3Y   TM    STCTRL,X'04'        TEST DONT WRITE CI REC                       
         BO    STAT2A                                                           
         GOTO1 DATAMGR,DMCB,(X'00',DMWRT)                                       
         CLI   DMCB+8,0                                                         
         BE    STAT2A                                                           
         B     STAT2E                                                           
                                                                                
STAT4    TM    STCTRL,X'02'        SET STATUS IN INDEX RECORDS                  
         BZ    STATX                                                            
STAT4A   LA    R6,STCIAS           R6=A(LIST OF CI ADDRS)                       
         MVI   FLAG1,0             TURN OFF SWAPS BIT                           
STAT4B   CLC   2(2,R6),FFS         SORT CI ADDRS INTO ASCENDING ORDER           
         BE    STAT4B2                                                          
         CLC   2(2,R6),0(R6)       COMPARE NEXT ENTRY TO THIS                   
         BNL   STAT4B1                                                          
         XC    2(2,R6),0(R6)       SWAP IF IN WRONG ORDER                       
         XC    0(2,R6),2(R6)                                                    
         XC    2(2,R6),0(R6)                                                    
         OI    FLAG1,X'02'                                                      
STAT4B1  LA    R6,2(R6)            BUMP TO NEXT CI ADDR                         
         B     STAT4B                                                           
STAT4B2  TM    FLAG1,X'02'         DONE IF NO SWAPS THIS PASS                   
         BO    STAT4A                                                           
         ST    R6,STCILAST         SAVE A(LAST ENTRY IN CI ADDR LIST)           
*                                                                               
STAT4C   TM    STCTRL,X'40'        LOCK PQFILE UNLESS SPECIFIED                 
         BO    *+14                                                             
         BRAS  RE,PQLOCK                                                        
         MVC   CPUASID,CIP3                                                     
         MVI   FLAG1,0             SET CURRENT INDEX PAGE NOT UPDATED           
         XC    FUL,FUL             SET CURRENT INDEX PAGE DSKADR                
         TM    CIRSNF,CIXDSPQ                                                   
         BZ    *+8                                                              
         OI    FLAG1,X'80'         X'80'=INDEX PAGES IN DATA SPACE              
         LA    R6,STCIAS           R6=A(SORTED LIST OF CI ADDRS)                
         CLC   2(2,R6),FFS         NO NEED TO RESERVE IF ONLY ONE CI            
         BE    STAT4D                                                           
         TM    STCTRL,X'80'        TEST CALL BY ADD                             
         BZ    STAT4C1             NO                                           
         L     R5,STCILAST         ADD MUST UPDATE FIRST CI LAST                
         MVC   2(2,R5),0(R6)                                                    
         MVC   4(2,R5),FFS         PUT FIRST CI AT END OF LIST                  
         MVC   CIADDR(2),0(R6)                                                  
         LA    R6,2(R6)                                                         
STAT4C1  CLI   OFFLINE,C'Y'        LOCK FIRST INDEX REC IF ONLINE               
         BE    STAT4D                                                           
         MVC   CXADDR,=X'00010100'                                              
         L     R5,STBUFFA                                                       
         GOTO1 DATAMGR,DMCB,(X'80',DMREAD),PQFILE,CXADDR,(R5)                   
         ORG   *-2                                                              
         BRAS  RE,RSRVPQ                                                        
         XC    CXADDR,CXADDR       SET NO INDEX PAGE IN CORE                    
*                                                                               
STAT4D   L     R5,STBUFFA          R5=A(I/O BUFFER FOR INDEX RECS)              
         CLC   0(2,R6),FFS                                                      
         BE    STAT4D1                                                          
         MVC   CIADDR(2),0(R6)     SET INDEX PAGE DISK ADDR                     
         BRAS  RE,GETXPE                                                        
         BRAS  RE,GETXAD                                                        
         CLC   FUL,CXADDR          TEST IF PAGE ALREADY IN BUFFER               
         BNE   STAT4D1             NO                                           
         TM    FLAG1,X'80'         YES IF USING DATA SPACES THEN ..             
         BO    STAT4D2             GET A FRESH COPY OF THIS PAGE                
         B     STAT4D3                                                          
*                                                                               
STAT4D1  TM    FLAG1,X'01'         WRITE BACK LAST PAGE IF UPDATED              
         BZ    STAT4D1A                                                         
         GOTO1 DATAMGR,DMCB,(X'00',DMWRT),PQFILE,FUL,(R5)                       
         CLI   DMCB+8,0                                                         
         BNE   STAT4I              ERROR IN WRITE OF OLD PAGE                   
         NI    FLAG1,X'FE'                                                      
STAT4D1A CLC   0(2,R6),FFS         EXIT IF HERE TO WRITE LAST PAGE              
         BE    STAT4H                                                           
*                                                                               
STAT4D2  GOTO1 DATAMGR,DMCB,(X'80',DMREAD),PQFILE,CXADDR,(R5)                   
         CLI   DMCB+8,0                                                         
         BNE   STAT4I              ERROR IN READ OF NEW PAGE                    
         NI    FLAG1,X'FE'         SET INDEX PAGE NOT UPDATED                   
         MVC   FUL,CXADDR                                                       
*                                                                               
STAT4D3  LH    RE,CXENTRY          GET INDEX ENTRY DISPLACEMENT                 
         MH    RE,CINDXLN                                                       
         AR    R5,RE               R5=A(INDEX ENTRY)                            
*                                                                               
STAT4E   MVC   SVSEQ,PQSEQ         SAVE INDEX ENTRY CI SEQ NUM                  
         MVC   SVTYPE,PQTYPE                                                    
         TM    PQSTAT,PQSTTE                                                    
         BZ    STAT4E1                                                          
         CLI   PQAGERT,X'FE'                                                    
         BNE   STAT4E1                                                          
         TM    STFLAG,X'20'        TEST IF SUB STATUS OF PNTG REPORT            
         BZ    STAT4E1                                                          
         MVC   PQSTAT,SVDATA+PQSTAT-PQINDEX                                     
         OI    PQSTAT,PQSTTE                                                    
         B     STAT4E3                                                          
*                                                                               
STAT4E1  CLC   0(2,R6),STFSTCI     TEST IF FIRST INDEX ENTRY                    
         BNE   STAT4E2                                                          
         OC    SVKEY,SVKEY                                                      
         BZ    STAT4E2                                                          
         CLC   PQKEY,SVKEY         TEST KEY IN INDEX SAME AS KEY IN CI          
         BE    STAT4E2                                                          
         MVC   BADXNDX,PQINDEX                                                  
         OI    STFLAG,X'08'+X'02'  SET FORMAT ERROR ON INDEX                    
         BRAS  RE,LOGERR           LOG THE ERROR                                
         OC    PQKEY,PQKEY         TEST IF INDEX ENTRY PURGED (TYPE#1)          
         BNZ   *+12                IF SO WE WILL IGNORE THE ERROR               
         NI    STFLAG,X'FF'-X'08'-X'02'                                         
         B     STAT4E2                                                          
         MVC   2(2,R6),FFS         TRUNCATE THE LIST OF CI ADDRS                
         XC    SVDATA,SVDATA       SET TO PURGE INDEX ENTRY                     
*                                                                               
STAT4E2  MVC   PQINDEX,SVDATA      COPY INDEX ENTRY FROM FILE FIRST CI          
*                                                                               
STAT4E3  CLI   PQSTAT,PQSTPU       TEST IF PURGING A REPORT                     
         BNE   *+14                                                             
         XC    PQINDEX,PQINDEX     CLEAR INDEX ENTRY FOR PURGE                  
         B     STAT4G                                                           
         TM    PQTYPE,PQTYXTN      TEST IF REPORT HAS XTNSN CI'S                
         BZ    STAT4E4                                                          
         TM    SVTYPE,PQTYXCI      TEST IF INDEX ENTRY OF XTNSN CI              
         BZ    STAT4E4                                                          
         OI    PQTYPE,PQTYXCI                                                   
STAT4E4  TM    STCTRL,X'80'        TEST IF CALL FROM ADD LOGIC                  
         BZ    STAT4E5             NO                                           
         CLC   CIADDR(2),STFSTCI   YES USE FIRST CI SEQ NUM IN INDEX            
         BE    STAT4G                                                           
STAT4E5  MVC   PQSEQ,SVSEQ         RESTORE INDEX ENTRY CI SEQ NUM               
*                                                                               
STAT4G   CLC   STXDADR,CXADDR      UPDATE CALLERS INDEX PAGE IF SAME            
         BNE   STAT4G1                                                          
         LR    RF,R5                                                            
         S     RF,STBUFFA                                                       
         A     RF,STXCADR                                                       
         MVC   0(L'PQINDEX,RF),0(R5)                                            
STAT4G1  OI    FLAG1,X'01'         SET INDEX PAGE UPDATED                       
         TM    FLAG1,X'80'         TEST IF INDEX PAGE IN DATA SPACE             
         BZ    STAT4G3                                                          
STAT4G2  L     R5,STBUFFA          POINT TO INDEX BUFFER                        
         LH    RF,CXENTRY          SET DISP TO ENTRY IN INDEX PAGE              
         MH    RF,CINDXLN                                                       
         GOTO1 DATAMGR,DMCB,(X'00',DMWRT),PQFILE,FUL,(R5),(RF)                  
         CLI   DMCB+8,0                                                         
         BNE   STAT4I                                                           
         NI    FLAG1,X'FE'         SET INDEX PAGE NOT UPDATED                   
STAT4G3  LA    R6,2(R6)                                                         
         B     STAT4D                                                           
*                                                                               
STAT4H   AHI   R6,-2               END OF INDEX UPDATE                          
         LA    RF,STCIAS                                                        
         CR    RF,R6                                                            
         BE    *+20                                                             
         CLC   0(2,R6),0(RF)       ADJUST LIST IF FIRST CI IS LAST              
         BNE   *+10                                                             
         MVC   0(2,R6),FFS                                                      
         TM    STCTRL,X'40'        UNLOCK PQFILE UNLESS SPECIFIED               
         BO    *+12                                                             
         BRAS  RE,PQUNLK                                                        
         BRAS  RE,RLSEPQ                                                        
         TM    STFLAG,X'08'        TEST FOR FORMAT ERROR IN CI                  
         BZ    STATX                                                            
         MVI   DMCB+8,X'41'                                                     
         TM    STFLAG,X'04'        TEST FOR DISK ERROR IN CI                    
         BZ    STATX                                                            
         MVI   DMCB+8,X'40'                                                     
         B     STATX                                                            
*                                                                               
STAT4I   BRAS  RE,PQUNLK           ERROR ON INDEX I/O                           
         BRAS  RE,RLSEPQ                                                        
         DC    H'0'                                                             
*                                                                               
STATX    CLI   DMCB+8,0            SET CC TO EQL IF NO ERROR                    
         XIT1                                                                   
         EJECT                                                                  
* SUBROUTINE TO OPEN FILE - REPORT ATTRIBUTES IN PRINT LINE AT R3               
*                                                                               
OPN      NTR1                                                                   
         XC    AQ,AQ               TEST IF OLD STYLE CALL                       
         CLI   QLEXTRA,X'FF'                                                    
         BE    OPN0                                                             
         ST    R3,AQ               SAVE OLD CALL PRINT LINE ADDR                
         LR    RE,R3                                                            
         XC    Q,Q                                                              
         LA    R3,Q                COPY OLD OPEN DATA TO MY LINE                
         MVC   QLSRCID,PLUSER-PLOLD(RE)                                         
         MVC   QLSUBID,PLSUBID-PLOLD(RE)                                        
         MVC   QLCLASS,PLCLASS-PLOLD(RE)                                        
         MVC   QLSTAT,PLSTAT-PLOLD(RE)                                          
         MVC   QLLPP,PLLPP-PLOLD(RE)                                            
         MVC   QLDESC,PLDESC-PLOLD(RE)                                          
         MVC   QLRETNL,RTLIVEOF    DEFAULT OFFLINE RETAIN HOURS                 
         MVC   QLRETND,RTDEADOF                                                 
         CLI   OFFLINE,C'Y'                                                     
         BE    OPN0                                                             
         MVC   QLRETNL,RTLIVEON    DEFAULT ONLINE RETAIN HOURS                  
         MVC   QLRETND,RTDEADON                                                 
*                                                                               
OPN0     L     RF,=A(GETDATE)      SET DATE AND TIME NOW                        
         BASR  RE,RF                                                            
         L     RF,=A(GETTIME)                                                   
         BASR  RE,RF                                                            
*                                                                               
OPN1     TM    QLFLAG,QLFLCIDA+QLFLKEY TEST IF CI ADDR OR KEY PASSED            
         BZ    OPN2                                                             
         OC    QLSRCID,QLSRCID     YES USER ID MUST BE SET AS WELL              
         BZ    OPNERR1                                                          
         MVC   USERIDNO,QLSRCID    FIND WHAT PRTQ FOR THIS USER                 
         BRAS  RE,WHATPQU                                                       
         MVC   SKINTNO,USERINUM    SAVE INTERNAL FILE NUM IN BUFF               
         MVC   SKEXTNO,USERXNUM    SAVE EXTERNAL FILE NUM IN BUFF               
         MVC   PQFILE,USERPRTQ                                                  
         MVC   VPQFILE,USERXNUM                                                 
         BRAS  RE,SETPQ            SET CI DATA FOR THIS PRTQ FILE               
*                                                                               
OPN1A    TM    QLFLAG,QLFLKEY      PROCESS REPORT KEY PASSED BY CALLER          
         BZ    OPN1D                                                            
         OC    QLREPNO,QLREPNO     REPORT NUMBER MUST BE SET                    
         BZ    OPNERR1                                                          
         MVC   CIRSN,QLREPNO                                                    
         BRAS  RE,RSNXPE           GET INDEX PAGE/ENTRY                         
         BRAS  RE,GETCAD                                                        
         MVC   SKFSTCI,CIADDR      SET DISK ADDR OF FIRST CI                    
         LR    R0,R4               READ CI INTO CALLERS BUFFER                  
         BRAS  RE,RDFFST                                                        
         BNE   OPNERR1                                                          
         CLC   QLSRCID,PQSRCID-PQRECD(R4) REPORT KEYS MUST MATCH                
         BNE   OPNERR1                                                          
         CLC   QLREPNO,PQREPNO-PQRECD(R4)                                       
         BNE   OPNERR1                                                          
         MVC   QLREPRNO,QLREPNO    RETURN ASSIGNED REPORT NUMBER                
         MVC   QLREPINT,USERINUM   RETURN ASSIGNED PRTQ INT NUM                 
         MVI   QLREPRCI-1,0                                                     
         MVC   QLREPRCI,SKADDR     RETURN ASSIGNED DISK ADDRESS                 
         MVC   QLREPCHR,PQFILE+4   RETURN ASSIGNED PRTQ ALPHA ID                
         B     OPN1F                                                            
*                                                                               
OPN1D    MVC   CIADDR(2),QLREPRCI  SET CI DISK ADDR PASSED BY CALLER            
         MVC   CIADDR+2(2),=X'0100'                                             
         LH    RE,CICINDX                                                       
         MH    RE,CITRKS                                                        
         CLM   RE,3,CIADDR         TEST CI DISK ADDR WITH MIN VALUE             
         BH    OPNERR1                                                          
         CLC   CJSTTRK,CIADDR      TEST CI DISK ADDR WITH MAX VALUE             
         BNH   OPNERR1                                                          
         MVC   SKFSTCI,CIADDR      SET DISK ADDR OF FIRST CI                    
         LR    R0,R4               READ CI INTO CALLERS BUFFER                  
         BRAS  RE,RDFFST                                                        
         BNE   OPNERR1                                                          
         CLC   QLSRCID,0(R4)       USER IDS MUST MATCH                          
         BNE   OPNERR1                                                          
*                                                                               
OPN1F    TM    QLFLAG,QLFLRALL     TEST IF EVERYTHING FROM RECORD               
         BZ    OPN1G                                                            
         MVC   QLINDEX,PQINDEX-PQRECD(R4)                                       
         MVC   QLAGELT(4),PQINDEX+20-PQRECD(R4)                                 
         MVC   QLBATTR,PQBATTR-PQRECD(R4)                                       
         MVC   DUB+0(2),QLRETNL    SAVE RETAIN VALUES                           
         MVC   DUB+2(2),QLRETND                                                 
         MVC   QLFATTR,PQFATTR-PQRECD(R4)                                       
         MVC   QLPIDNUM,PQPIDNUM-PQRECD(R4)                                     
         TM    QLFLAG,QLFLRRE      TEST RETAIN FROM PQ                          
         BO    *+16                                                             
         MVC   QLRETNL,DUB+0       NO RESTORE OLD RETAIN VALUES                 
         MVC   QLRETND,DUB+2                                                    
         OI    QLFLAG,QLFLRST+QLFLRDT                                           
*                                                                               
OPN1G    TM    QLFLAG,QLFLKEY+QLFLXTND TEST IF EXTENDING EXISTING FILE          
         BNO   OPN2                                                             
         MVC   DUB(5),PQDAEOR-PQRECD(R4) SAVE LAST BLOCK AND CI ADDRS           
         MVC   DUB1(3),PQLINES-PQRECD(R4)                                       
         MVC   DUB1+3(1),PQMAXCPL-PQRECD(R4)                                    
         MVC   DUB1+4(2),PQAVCPL-PQRECD(R4)                                     
         MVC   SKINDEX,PQINDEX-PQRECD(R4)                                       
         SR    R1,R1                                                            
         ICM   R1,3,DUB+3          DUB+3(2)=TTTT OF LAST CI                     
         BZ    OPNERR1                                                          
         CLM   R1,3,CIADDR         TEST IF LAST CI IS ALSO FIRST CI             
         BE    OPN1H                                                            
         STH   R1,CIADDR                                                        
         BRAS  RE,RDFFST           READ FIRST BLOCK IN LAST CI                  
         BNE   OPNERR1                                                          
*                                                                               
OPN1H    OC    PQCINEXT-PQRECD(2,R4),PQCINEXT-PQRECD(R4)                        
         BNZ   OPNERR1                                                          
         CLC   QLSRCID,PQSRCID-PQRECD(R4)                                       
         BNE   OPNERR1                                                          
         CLC   QLREPNO,PQREPNO-PQRECD(R4)                                       
         BNE   OPNERR1                                                          
         ICM   R1,7,DUB            DUB(3)=TTTBB OF LAST BLOCK                   
         BZ    OPNERR1                                                          
         CLM   R1,7,CIADDR                                                      
         BE    OPN1I                                                            
         STCM  R1,7,CIADDR         READ LAST BLOCK IN LAST CI                   
         GOTO1 DATAMGR,DMCB,(X'00',DMREAD),PQFILE,CIADDR,(R0)                   
         CLI   DMCB+8,0                                                         
         BNE   OPNERR1                                                          
         MVC   SKADDR,CIADDR       SET CURRENT BLOCK DISK ADDRESS               
         LA    RF,PQDATA-PQINDEX                                                
         STH   RF,SKDISP           SET CURRENT BLOCK DISPLACEMENT               
*                                                                               
OPN1I    XC    PQCINEXT-PQRECD(2,R4),PQCINEXT-PQRECD(R4)                        
         LR    RE,R4                                                            
         AH    RE,SKDISP           RE=A(FIRST PRINT LINE IN BLOCK)              
         SR    R1,R1               R1=A(PREVIOUS LINE IN BLOCK)                 
         LR    RF,R4                                                            
         AH    RF,CIBLKLN                                                       
OPN1J    CLC   0(2,RE),=H'1'       TEST END-OF-REPORT                           
         BL    OPNERR1                                                          
         BE    OPN1K                                                            
         LR    R1,RE               SAVE A(PREVIOUS LINE IN BLOCK)               
         AH    RE,0(RE)            BUMP TO NEXT PRINT LINE IN BLOCK             
         CR    RE,RF                                                            
         BL    OPN1J                                                            
         B     OPNERR1                                                          
*                                                                               
OPN1K    XC    0(2,RE),0(RE)       CLEAR END-OF-REPORT                          
         LTR   R1,R1               WAS THERE A PREVIOUS LINE                    
         BZ    OPN1L               NO                                           
         CLC   0(4,R1),=X'0004097A' YES IS THE ENTRY A SINGLE COLON             
         BNE   OPN1L                                                            
         LR    RE,R1               CLEAR PREVIOUS END-OF-REPORT LINE            
         XC    0(4,RE),0(RE)                                                    
OPN1L    SR    RE,R4               SET DISPLACEMENT OF NEXT RECORD              
         STH   RE,SKDISP                                                        
         MVI   SKACTRL,X'01'+X'10' SET OPEN/EXTEND                              
         MVI   SKACTRX,0           CLEAR EXTENSION FLAGS                        
         TM    PQTYPE-PQRECD(R4),PQTYDL+PQTYSQL                                 
         BZ    *+8                                                              
         OI    SKACTRL,X'20'       SET DROP1ST (DROP FIRST PAGE)                
         TM    PQBFLAG-PQRECD(R4),PQBFNP                                        
         BZ    *+8                                                              
         OI    SKACTRL,X'40'       SET FIRST PAGE IN BLOCK FLAG                 
         MVC   CIADDR,SKFSTCI                                                   
         MVI   P1,1                                                             
         L     RF,=A(GCI)                                                       
         BASR  RE,RF               CALL TO FIX 1ST INDEX ENTRY                  
         MVC   CIADDR,SKADDR                                                    
         MVC   SKPAGE,P1           SAVE INDEX PAGE/ENTRY                        
         SR    RF,RF               SET SAVED REPORT COUNT DATA                  
         ICM   RF,1,DUB1+3                                                      
         STH   RF,SKCHRMAX         SET MAXCPL=PQMAXCPL                          
         ICM   RF,7,DUB1                                                        
         MH    RF,DUB1+4                                                        
         ST    RF,SKCHRS           SET TOTAL CHRS=PQLINES*PQAVCPL               
         MVI   PARM3,0             SET ALL OK                                   
         B     OPNX                                                             
*                                                                               
OPN2     OC    QLSRCID,QLSRCID     USER ID MUST BE SPECIFIED                    
         BNZ   OPN2A                                                            
         L     RE,VSSB             UNLESS ONLINE VALUE FROM UTL ENTRY           
         USING SSBD,RE                                                          
         CLI   OFFLINE,C'Y'                                                     
         BE    OPNERR1                                                          
         L     RE,AUTL                                                          
         USING UTLD,RE                                                          
         OC    TUSER,TUSER                                                      
         BZ    OPNERR1                                                          
         MVC   QLSRCID,TUSER                                                    
         DROP  RE                                                               
OPN2A    CLC   QLSRCID,=X'FFFF'    THIS USERID FUCKS UP PQ REAL BAD             
         BE    OPNERR1                                                          
         MVC   USERIDNO,QLSRCID    FIND WHAT PRTQ FOR THIS USER                 
         BRAS  RE,WHATPQU                                                       
         MVC   SKINTNO,USERINUM    SAVE INTERNAL FILE NUM IN BUFF               
         MVC   SKEXTNO,USERXNUM    SAVE EXTERNAL FILE NUM IN BUFF               
         MVC   PQFILE,USERPRTQ                                                  
         MVC   VPQFILE,USERXNUM                                                 
         BRAS  RE,SETPQ            SET CIDATA FOR THIS PRTQ FILE                
*                                                                               
OPN3     CLC   QLSUBID,=C'ALL'     SUBID MUST BE SPECIFIED                      
         BNE   *+8                                                              
         MVI   QLSUBID+1,C' '      ADJUST C'ALL' SUB ID                         
         LA    RE,QLSUBID                                                       
         LA    RF,3                                                             
OPN3A    CLI   0(RE),C'A'          REPLACE SPACES BY DOTS                       
         BNL   *+8                                                              
         MVI   0(RE),C'.'                                                       
         LA    RE,1(RE)                                                         
         BCT   RF,OPN3A                                                         
*                                                                               
OPN4     CLI   QLCLASS,C'A'        DEFAULT REPORT CLASS                         
         BNL   OPN4A                                                            
         CLI   QLCLASS,C'$'        ALLOWABLE SPECIAL CLASS CHRS                 
         BE    OPN4A                                                            
         MVI   QLCLASS,C' '                                                     
OPN4A    CLI   QLREPTY,C'*'        DEFAULT REPORT TYPE                          
         BNE   *+8                                                              
         MVI   QLREPTY,C' '                                                     
         CLI   QLREPTY,C'A'                                                     
         BNL   *+8                                                              
         MVI   QLREPTY,C' '                                                     
         CLI   QLARC,C'*'          DEFAULT ARCHIVE CLASS                        
         BNE   *+8                                                              
         MVI   QLARC,C'A'                                                       
         CLI   QLARC,C'A'                                                       
         BNL   *+8                                                              
         MVI   QLARC,C' '                                                       
OPN4B    OC    QLLINET(2),QLLINET  DEFAULT TO 132 PRINT LINES WITH CC           
         BNZ   *+12                                                             
         MVI   QLLINET,QLLTCC+QLLTFL                                            
         MVI   QLLINEW,132                                                      
         CLI   QLLPP,0             DEFAULT LINES PER PAGE                       
         BNE   *+10                                                             
         MVC   QLLPP,DFLPP                                                      
*                                                                               
OPN5     LR    RE,R4               INITIALISE CALLERS BUFFER                    
         LH    RF,CIBLKLN                                                       
         XCEF                                                                   
         LR    R5,R4               R5=A(CALLERS BUFFER TO BUILD HDR)            
         MVC   PQSRCID,QLSRCID                                                  
         MVC   PQSUBID,QLSUBID                                                  
         MVC   PQCLASS,QLCLASS                                                  
         MVC   PQREPTY,QLREPTY                                                  
         MVC   PQTYPE,QLTYPE                                                    
         NI    PQTYPE,255-PQTYXTN-PQTYXCI-PQTYXTN2-PQTYNEW                      
         CLI   OFFLINE,C'Y'                                                     
         BE    *+8                                                              
         OI    PQTYPE,PQTYONL      SET CREATED ONLINE                           
*                                                                               
         TM    PQTYPE,PQTYDL+PQTYSQL DOWNLOADABLE CANT BE ARCHIVABLE            
         BNZ   OPN5A                                                            
         MVC   PQTYP1,QLTYP1       ARCHIVE STATUS AND CLASS                     
         MVC   PQARC,QLARC                                                      
         CLI   PQARC,C'A'          TEST IF VALID ARCHIVE CLASS                  
         BNL   OPN5A                                                            
         TM    PQTYP1,PQTYAE+PQTYAR+PQTYAD                                      
         BZ    *+8                                                              
         MVI   PQARC,C'A'          SET DEFAULT ARCHIVE CLASS                    
*                                                                               
OPN5A    CLI   DSPACE,C'R'         TEST/SET REP SYSTEM REPORT                   
         BNE   *+8                                                              
         OI    PQTYP1,PQTYREP                                                   
         TM    QLFLAG,QLFLCIDA     SET ATTB BYTE IF CI ADDR PASSED              
         BZ    OPN5A1                                                           
         TM    QLATTB,QLATJOBI+QLATJOBO IF JOB INPUT OR OUTPUT ON               
         BZ    *+8                                                              
         OI    PQATTB,PQATJOBI+PQATJOBO THEN SET BOTH INPUT AND OUTPUT          
OPN5A1   TM    QLATTB,QLATUSR                                                   
         BZ    OPN5B                                                            
         OI    PQATTB,PQATUSR      SET REPORT HAS USER DATA                     
         MVC   PQUSRINF,QLUSRINF                                                
         CLI   PQUSRINF,PQUSRTYW   TEST IF WKFILE TYPE USER DATA                
         BNE   *+8                                                              
         OI    PQATTB,PQATUSRW                                                  
*                                                                               
OPN5B    MVI   PQSTAT,PQSTTE       SET TEMP STATUS                              
         TM    QLFLAG,QLFLRST      TEST IF STATUS FROM RECORD                   
         BZ    OPN5C                                                            
         OC    PQSTAT,QLSTAT       AND COPY ACTUAL STATUS                       
         B     OPN5D                                                            
OPN5C    TM    QLSTAT,PQSTHO                                                    
         BZ    *+12                                                             
         OI    PQSTAT,PQSTHO       SET HOLD STATUS                              
         B     *+8                                                              
         OI    PQSTAT,PQSTAC       SET ACTV STATUS                              
         TM    QLSTAT,PQSTKE                                                    
         BZ    *+8                                                              
         OI    PQSTAT,PQSTKE       SET KEEP STATUS                              
         TM    QLSTAT,PQSTIN                                                    
         BZ    *+8                                                              
         OI    PQSTAT,PQSTIN       SET INVISIBLE STATUS                         
OPN5D    EQU   *                                                                
*                                                                               
OPN5E    MVC   PQAGELD,DATEC       SET CREATED DATE/TIME FROM NOW               
         MVC   PQDATEL,DATEC                                                    
         MVC   PQTIMEL,TIMEB                                                    
         MVC   PQAGELT,TIMEC                                                    
         MVC   PQRETNL,QLRETNL                                                  
         MVC   PQRETND,QLRETND                                                  
*                                                                               
         MVC   DUB+0(2),RTLIVEOF   OFFLINE DEFAULT RETAIN                       
         MVC   DUB+2(2),RTDEADOF                                                
         CLI   OFFLINE,C'Y'                                                     
         BE    *+16                                                             
         MVC   DUB+0(2),RTLIVEON   ONLINE DEFAULT RETAIN                        
         MVC   DUB+2(2),RTDEADON                                                
*                                                                               
         OC    PQRETNL,PQRETNL     IF ZERO USE DEFAULT LIVE                     
         BNZ   *+10                                                             
         MVC   PQRETNL,DUB+0                                                    
         OC    PQRETND,PQRETND     IF ZERO USE DEFAULT DEAD                     
         BNZ   *+10                                                             
         MVC   PQRETND,DUB+2                                                    
*                                                                               
         TM    QLFLAG,QLFLRDT      TEST IF DATE/TIME FROM RECORD                
         BZ    OPN5G                                                            
*                                                                               
OPN5F    MVC   PQAGELD,QLDATEL     SET CREATED DATE/TIME FROM RECORD            
         MVC   PQAGEDD,QLDATED                                                  
         MVC   PQDATEL,QLDATEL                                                  
         MVC   PQTIMEL,QLTIMEL                                                  
         MVC   PQAGELT,QLAGELT                                                  
         MVC   PQDATED,QLDATED                                                  
         MVC   PQTIMED,QLTIMED                                                  
         MVC   PQPRCNT,QLPRCNT     AND LAST KNOWN PRTD/SENT INFO                
         MVC   PQPRLOC,QLPRLOC                                                  
         MVC   PQPRNUM,QLPRNUM                                                  
         MVC   PQPRSYM,QLPRSYM                                                  
*                                                                               
OPN5G    TM    QLFLAG,QLFLRRE      TEST IF RETAIN INFO FROM RECORD              
         BZ    OPN5G1                                                           
         MVC   DUB1(3),QLAGERD                                                  
         OC    DUB1(3),DUB1                                                     
         BNZ   OPN5G2                                                           
OPN5G1   MVC   DUB+0(3),DATEC      COMPUTE RETENTION FROM TODAYS DATE           
         MVC   DUB+3(2),PQRETNL                                                 
         TM    PQSTAT,PQSTDEAD     PLUS LIVE OR DEAD HOURS                      
         BZ    *+10                                                             
         MVC   DUB+3(2),PQRETND                                                 
         MVI   DUB+5,0                                                          
         L     RF,=A(GETRETN)                                                   
         BASR  RE,RF                                                            
OPN5G2   MVC   PQAGERD(3),DUB1     SET RETENTION DATE/TIME                      
         MVC   PQCTRY,GRCTRY       SET COUNTRY CODE                             
*                                                                               
OPN5J    MVC   PQLINET,QLLINET     SET BLOCK ATTRIBUTES                         
         MVC   PQLINEW,QLLINEW                                                  
         MVC   PQLPP,QLLPP                                                      
*                                                                               
OPN5K    MVC   PQFORMS,QLFORMS     SET REPORT FORMS CODE                        
         OC    PQFORMS,SPACES                                                   
         MVC   PQCHARS,QLCHARS     SET REPORT CHARACTER SET CODE                
         OC    PQCHARS,SPACES                                                   
         MVC   PQCOPIES,QLCOPIES   SET NUMBER OF COPIES                         
         CLI   PQCOPIES,X'F0'                                                   
         BL    *+8                 CONVERT C'N' TO X'0N'                        
         NI    PQCOPIES,X'0F'                                                   
         CLI   PQCOPIES,9          ADJUST MAXIMUM VALUE                         
         BNH   *+8                                                              
         MVI   PQCOPIES,9                                                       
         MVC   PQDESC,QLDESC       SET REPORT DESCRIPTION                       
         OC    PQDESC,SPACES                                                    
OPN5K0   L     RE,PARM1            DONT FUSS WITH PQDESC IF PQMAINT             
         CLC   0(4,RE),=C'OPEN'                                                 
         BNE   OPN5K1                                                           
         TM    PARM1,X'20'         DMPRTQM SETS THIS BIT ON                     
         BO    OPN5L                                                            
OPN5K1   CLC   PQDESC(8),=C'$JOBNAME'                                           
         BNE   OPN5K2                                                           
         MVI   PQCLASS,C'$'        $JOBNAME SETS CLASS TO $                     
         B     OPN5K3                                                           
OPN5K2   CLI   PQCLASS,C'$'        TEST SPECIAL CLASS                           
         BNE   OPN5L                                                            
OPN5K3   MVC   PQDESC,SPACES       SET DESCRIPTION TO JOBNAME                   
         LA    R6,FUL              GET JOB NAME                                 
         EXTRACT (6),FIELDS=TIOT                                                
         L     R6,FUL                                                           
         MVC   PQDESC(8),0(R6)                                                  
*                                                                               
OPN5L    XC    FUL,FUL             EXTRACT AND SAVE THE PID DATA                
         XC    FUL1,FUL1                                                        
         ICM   RE,15,AUTL                                                       
         BZ    OPN5L1                                                           
         USING UTLD,RE                                                          
         CLC   TUSER,ZEROS         IGNORE IF NOT CONNECTED                      
         BE    OPN5L2                                                           
         CLC   TAGYPER,ZEROS       IGNORE IF PERSON SEC AGY N/D                 
         BE    OPN5L2                                                           
         CLC   TPERSON,ZEROS       IGNORE IF PID NOT DEFINED                    
         BE    OPN5L2                                                           
         MVC   FUL+0(2),TAGYPER    GET PID FROM UTL                             
         MVC   FUL+2(2),TPERSON                                                 
         MVC   FUL1,FUL                                                         
         B     OPN5L2                                                           
         DROP  RE                                                               
*                                                                               
OPN5L1   L     RE,VSSB             TEST IF OFFLINE EXTENDED SSB                 
         CLI   2(RE),X'FF'                                                      
         BNE   OPN5L2                                                           
         ICM   RE,15,SSOMASTC-SSOOFF(RE)                                        
         BZ    OPN5L2                                                           
         LA    RE,MCRFHDR-DDMASTD(RE)       RE=A(RFHDR=CARD)                    
         CLC   MCRHSAGP-MCRFHDR(2,RE),ZEROS IGNORE IF NO PER SEC AGY            
         BE    OPN5L2                                                           
         CLC   MCRHPSWD-MCRFHDR(2,RE),ZEROS IGNORE IF NO PID NUM                
         BE    OPN5L2                                                           
         MVC   FUL+0(2),MCRHSAGP-MCRFHDR(RE)                                    
         MVC   FUL+2(2),MCRHPSWD-MCRFHDR(RE)                                    
         MVC   FUL1,FUL            GET PID FROM RFHDR=DATA                      
*                                                                               
OPN5L2   OC    QLPIDNUM,QLPIDNUM   TEST IF PID ALREADY SET                      
         BZ    *+10                                                             
         MVC   FUL,QLPIDNUM                                                     
         MVC   PQPIDNUM,FUL        SET PID NUMBER IN REPORT                     
*                                                                               
OPN5L3   MVC   PQPSWD,SPACES       INITIALISE REPORT SECURITY INFO              
         MVI   PQSECF1,0                                                        
         MVI   PQSECF2,0                                                        
         TM    QLSECF1,QLSINONO    TEST IF FIRST SECURITY FLAG INVALID          
         BZ    *+10                NO                                           
         XC    QLSECF1(2),QLSECF1  YES MUST BE OLD CALL WITH 6 CHR PWD          
         CLI   QLPSWD,X'FF'        TEST IF WANT LOGON PID ATTACHED              
         BNE   *+14                                                             
         MVC   FUL,FUL1            USE LOGON PID                                
         B     OPN5L5                                                           
         TM    QLSECF1,QLSIPID     TEST IF WANT SPECIFIC PID PASSED             
         BZ    OPN5L4                                                           
         MVC   FUL,QLPSWD          USE PASSED PID                               
         B     OPN5L5                                                           
*                                                                               
OPN5L4   MVC   PQPSWD,QLPSWD       SET PIN VALUE 1 THRU 4 CHR                   
         OC    PQPSWD,SPACES       CONVERT PIN TO UPPER CASE                    
         CLC   PQPSWD,SPACES                                                    
         BE    OPN5L6                                                           
         MVC   PQSECF1,QLSECF1                                                  
         MVC   PQSECF2,QLSECF2                                                  
         NI    PQSECF1,255-PQSIPID                                              
         OI    PQSECF1,PQSIPIN     SET PASSWORD CONTAINS A PIN                  
         OI    PQATTB,PQATPW       SET PASSWORD PROTECTED ATTRIBUTE             
         B     OPN5L9                                                           
*                                                                               
OPN5L5   MVC   PQSECF1,QLSECF1     CALLER WANTS PID ATTACHED TO REPORT          
         MVC   PQSECF2,QLSECF2                                                  
         CLC   FUL,ZEROS                                                        
         BE    OPN5L6                                                           
         MVC   PQPSWD,FUL                                                       
         NI    PQSECF1,255-PQSIPIN                                              
         OI    PQSECF1,PQSIPID     SET PASSWORD CONTAINS A PID                  
         OI    PQATTB,PQATPW       SET PASSWORD PROTECTED ATTRIBUTE             
         B     OPN5L9                                                           
*                                                                               
OPN5L6   MVC   PQPSWD,SPACES       NO PASSWORD ATTACHED TO REPORT               
         MVC   PQSECF1,QLSECF1                                                  
         MVC   PQSECF2,QLSECF2                                                  
         NI    PQSECF1,255-PQSIPID-PQSIPIN                                      
         CLI   PQSECF1,0                                                        
         BNE   *+12                                                             
         CLI   PQSECF2,0                                                        
         BE    OPN5L9                                                           
         OI    PQATTB,PQATPW       SET SECURITY PROTECTED ATTRIBUTE             
*                                                                               
OPN5L9   MVC   PQMAKER,QLMAKER     SET SYS/PRG/SUBPRG                           
         CLI   PQMAKER,C' '                                                     
         BH    *+10                                                             
         XC    PQMAKER,PQMAKER     CLEAR IF NOT PROPERLY DEFINED                
*                                                                               
OPN5M    XC    EOFCNT(8),EOFCNT    CLEAR EOF COUNTERS                           
OPN5N    CLI   OFFLINE,C'Y'                                                     
         BNE   *+10                                                             
         MVC   EOFMAX(6),EOFMAXOF                                               
         MVI   P1,1                GET FIRST CI FOR FILE                        
         L     RF,=A(GCI)                                                       
         BASR  RE,RF                                                            
         CLC   P1,FFS                                                           
         BE    OPNERR2             DISK ERROR                                   
         OC    P1,P1                                                            
         BNZ   OPN5O                                                            
         LH    R1,EOFCNT           BUMP END OF FILE COUNTER                     
         LA    R1,1(R1)                                                         
         STH   R1,EOFCNT                                                        
         CH    R1,EOFMAX                                                        
         BH    OPNERR3             RETURN EOF IF EXCEEDS MAX COUNT              
         STIMER WAIT,BINTVL=EOFBIN                                              
         B     OPN5N               BACK AND TRY AGAIN                           
*                                                                               
OPN5O    MVC   QLREPRNO,PQREPNO    RETURN ASSIGNED REPORT NUMBER                
         MVC   QLREPINT,USERINUM   RETURN ASSIGNED PRTQ INT NUM                 
         MVI   QLREPRCI-1,0                                                     
         MVC   QLREPRCI,SKADDR     RETURN ASSIGNED DISK ADDRESS                 
         MVC   QLREPCHR,PQFILE+4   RETURN ASSIGNED PRTQ ALPHA ID                
*                                                                               
         MVC   SKPAGE(4),P1        SAVE INDEX DATA IN BUFFER                    
         MVC   SKINDEX,PQINDEX                                                  
         OI    SKACTRL,X'01'       SET FILE OPEN FLAG                           
         TM    QLFLAG,QLFLDD                                                    
         BZ    *+8                                                              
         OI    SKACTRL,X'08'       SET WANT DATA DICTIONARY SUPPORT             
         MVI   PARM3,0                                                          
*                                                                               
OPN6     CLI   OFFLINE,C'Y'        LOOK FOR OFFLINE ARC=CARD                    
         BNE   OPN6E                                                            
         L     RE,VSSB                                                          
         CLI   2(RE),X'FF'         TEST IF OFFLINE EXTENDED SSB                 
         BNE   OPNX                                                             
         ICM   RE,15,SSOMASTC-SSOOFF(RE)                                        
         BZ    OPN6D                                                            
         TM    PQTYPE,PQTYDL+PQTYSQL DOWNLOADABLE CANT BE ARCHIVABLE            
         BNZ   OPN6D                                                            
         LA    RE,MCARC-DDMASTD(RE)                                             
         CLC   0(4,RE),=C'ARC='    RE=A(MASTC SLOT FOR ARC=CARD)                
         BNE   OPN6D                                                            
         LH    R1,SKDISP                                                        
         AR    R1,R5                                                            
         MVC   0(4,R1),ARCLINE                                                  
         MVC   4(72,R1),0(RE)                                                   
         MVI   76(R1),C'>'         <ARC=AA,.......>                             
         LA    R1,77(R1)                                                        
         SR    R1,R5                                                            
         STH   R1,SKDISP                                                        
OPN6A    CLI   30(RE),C'A'         TEST IF REPORT TYPE DEFINED                  
         BL    OPN6B                                                            
         CLI   QLREPTY,C'A'                                                     
         BNL   OPN6B                                                            
         MVC   QLREPTY,30(RE)                                                   
         MVC   PQREPTY,30(RE)                                                   
OPN6B    CLI   32(RE),C'A'         TEST IF ARCHIVE CLASS DEFINED                
         BL    OPN6C                                                            
         CLI   QLARC,C'A'                                                       
         BNL   OPN6C                                                            
         MVC   QLARC,32(RE)                                                     
         MVC   PQARC,32(RE)                                                     
         TM    PQTYP1,PQTYAE+PQTYAR+PQTYAD                                      
         BNZ   *+8                                                              
         OI    PQTYP1,PQTYAR       SET AS ARCHIVABLE REPORT                     
OPN6C    OI    SKACTRX,X'04'       SET GENERATED AN ARC=CARD                    
         B     OPNX                                                             
*                                                                               
OPN6D    TM    PQTYP1,PQTYAE+PQTYAR  TEST IF OFFLINE ARCHIVABLE REPORT          
         BZ    OPNX                                                             
         LH    R1,SKDISP           OFFLINE GENERATE ARC=CARD                    
         AR    R1,R5                                                            
         MVC   0(ARCLINEL,R1),ARCLINE                                           
         LA    R1,4(R1)            ARC=AA,SPP,FFFF  ,NNNNN,NNNNN,T,C            
*&&UK*&& MVC   04(2,R1),=C'D1'                                                  
*&&US*&& MVC   04(2,R1),=C'SJ'                                                  
         MVC   07(3,R1),QLSYS                                                   
         MVC   11(4,R1),=C'OFFL'                                                
         ICM   RE,15,VSSB          GET ALPHA OUT OF MASTC                       
         BZ    OPN6D2                                                           
         ICM   RE,15,SSOMASTC-SSOOFF(RE)                                        
         BZ    OPN6D2                                                           
         MVC   04(2,R1),MCUSER-MCBLOCK(RE)                                      
OPN6D2   SR    R0,R0               DEST AND ORIGIN USER ID NUMS                 
         ICM   R0,3,QLSRCID                                                     
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  18(5,R1),DUB                                                     
         UNPK  24(5,R1),DUB                                                     
         CLI   QLREPTY,C'A'                                                     
         BL    *+10                                                             
         MVC   30(1,R1),QLREPTY                                                 
         CLI   QLARC,C'A'                                                       
         BL    *+10                                                             
         MVC   32(1,R1),QLARC                                                   
         LA    R1,73(R1)                                                        
         SR    R1,R5                                                            
         STH   R1,SKDISP                                                        
         OI    SKACTRX,X'04'       SET GENERATED AN ARC=CARD                    
         B     OPNX                                                             
*                                                                               
OPN6E    TM    PQTYP1,PQTYAE+PQTYAR  TEST ONLINE ARCHIVABLE REPORT              
         BZ    OPNX                                                             
         TM    PQATTB,PQATJOBI                                                  
         BO    OPNX                                                             
         LH    R1,SKDISP           GENERATE ARC=CARD                            
         AR    R1,R5                                                            
         MVC   0(ARCLINEL,R1),ARCLINE                                           
         LA    R1,4(R1)            ARC=AA,SPP,FFFF  ,NNNNN,NNNNN,T,C            
         L     RE,AUTL                                                          
         MVC   04(2,R1),TAGY-UTLD(RE)                                           
         MVC   07(3,R1),QLSYS                                                   
         MVC   11(4,R1),=C'ONLI'                                                
         SR    R0,R0               DEST AND ORIGIN USER ID NUMS                 
         ICM   R0,3,QLSRCID                                                     
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  18(5,R1),DUB                                                     
         UNPK  24(5,R1),DUB                                                     
         CLI   QLREPTY,C'A'                                                     
         BL    *+10                                                             
         MVC   30(1,R1),QLREPTY                                                 
         CLI   QLARC,C'A'                                                       
         BL    *+10                                                             
         MVC   32(1,R1),QLARC                                                   
         LA    R1,73(R1)                                                        
         SR    R1,R5                                                            
         STH   R1,SKDISP                                                        
         OI    SKACTRX,X'04'       SET GENERATED AN ARC=CARD                    
         B     OPNX                                                             
*                                                                               
OPNERR1  MVI   PARM3,X'41'         FORMAT ERROR                                 
         B     OPNERRX                                                          
OPNERR2  MVI   PARM3,X'40'         DISK ERROR                                   
         B     OPNERRX                                                          
OPNERR3  MVI   PARM3,X'80'         END OF FILE                                  
         OC    PARM3(1),EOFERRRC                                                
OPNERRX  MVC   REPTINFO,0(R4)                                                   
*                                                                               
OPNX     OC    AQ,AQ               TEST OLD STYLE CALL                          
         BZ    OPNY                NO                                           
         L     R3,AQ               YES RESTORE OLD OPEN DATA                    
         LA    RE,Q                                                             
         MVC   PLUSER,QLSRCID-PQPLD(RE)                                         
         MVC   PLSUBID,QLSUBID-PQPLD(RE)                                        
         MVC   PLCLASS,QLCLASS-PQPLD(RE)                                        
         MVC   PLSTAT,QLSTAT-PQPLD(RE)                                          
         MVC   PLLPP,QLLPP-PQPLD(RE)                                            
         MVC   PLDESC,QLDESC-PQPLD(RE)                                          
         MVC   PLREPNO,QLREPRNO-PQPLD(RE)                                       
         MVC   PLREPINT,QLREPINT-PQPLD(RE)                                      
         MVI   PLSTRCI-1,0                                                      
         MVC   PLSTRCI,QLREPRCI-PQPLD(RE)                                       
OPNY     CLI   PARM3,0             SET CC TO EQL IF NO ERROR                    
         XIT1                                                                   
         EJECT                                                                  
* SUBROUTINE TO CLOSE FILE - DRIVEN BY CLOSE TYPE IN P1                         
*                                                                               
CLS      NTR1                                                                   
         LR    R5,R4               R5=A(CALLERS BUFFER)                         
         L     RF,=A(GETDATE)      SET DATE AND TIME NOW                        
         BASR  RE,RF                                                            
         L     RF,=A(GETTIME)                                                   
         BASR  RE,RF                                                            
         MVC   P1+1(1),PQSEQ       NORMAL CLOSE SAVE BUFFER DATA                
         CLI   P1+1,0                                                           
         BNE   *+8                                                              
         MVI   P1+1,1              NUMBER OF CIS                                
         XC    P1+2(2),P1+2                                                     
         TM    PQTYPE,PQTYXTN+PQTYXCI                                           
         BZ    CLS0                                                             
         MVC   P1+2(1),PQSEQ       NUMBER OF EXTENSION CIS                      
         MVI   P1+3,1              SET WE ARE NOW IN EXTENSION MODE             
*                                                                               
CLS0     CLI   P1,0                TEST ERROR CLOSE OR CLO/PUR                  
         BNE   CLS1                YES WE DONT NEED THE FOLLOWING DATA          
         MVC   P3+1(3),PQLINEH     NUMBER OF LINES                              
         MVC   P4+2(2),PQPAGEH     NUMBER OF PAGES                              
         MVC   P5,SKCHRS           NUMBER OF CHRS                               
         MVC   P6+2(2),SKCHRMAX    NUMBER OF CHRS PER LINE MAXIMUM              
         XC    P7,P7               RETAIN INFO NOT CHANGED                      
         TM    ADDWHY,X'01'        TEST IF CLOSING EMPTY REPORT                 
         BZ    CLS1                NO                                           
*                                                                               
         MVC   DUB+0(3),DATEC      YES COMPUTE NEW SHORT RETAIN                 
         MVC   DUB+3(2),PQRETNL                                                 
         MVI   DUB+5,0                                                          
         L     RF,=A(GETRETN)                                                   
         BASR  RE,RF                                                            
         MVC   P7(3),DUB1          SAVE NEW RETAIN DATE/TIME                    
*                                                                               
CLS1     MVC   CIADDR,SKSTRCI      SET FIRST RECORD IN LAST CI                  
         XC    FUL,FUL                                                          
         IC    RE,PQSEQ                                                         
         LA    RE,1(RE)                                                         
         STC   RE,SVSEQ                                                         
         LA    R6,STCIAS           R6=A(AREA TO BUILD LIST OF CI ADDRS)         
*                                                                               
CLS2     GOTO1 DATAMGR,DMCB,(X'00',DMREAD),PQFILE,CIADDR,(R5)                   
         CLI   DMCB+8,0                                                         
         BNE   CLSERR2                                                          
         CLC   FUL,FFS                                                          
         BE    CLS2A                                                            
         MVC   DUB(2),PQCINEXT     SAVE LINK BACK TO PREV CI                    
         MVC   PQCINEXT,FUL        SET LINK FORWARD TO NEXT CI                  
*                                                                               
CLS2A    CLI   P1+2,0              TEST IF EXTENSION CI'S PRESENT               
         BE    CLS2B                                                            
         CLI   P1+3,0              TEST IF BACK TO NORMAL CI                    
         BNE   *+12                                                             
         OI    PQTYPE,PQTYXTN                                                   
         B     CLS2B                                                            
         TM    PQTYPE,PQTYXTN+PQTYXCI                                           
         BNO   CLSERR1                                                          
         CLI   PQSEQ,FRSTXTN       TEST IF FIRST EXTENSION CI                   
         BNE   CLS2C                                                            
         CLI   SVSEQ,FRSTXTNP                                                   
         BNE   CLSERR1                                                          
         MVI   P1+1,LASTSEQ        SET 255 NORMAL CIS                           
         MVI   P1+3,0              SET NEXT CI IS NORMAL                        
         MVI   SVSEQ,LASTSEQP      AND SHOULD HAVE PQSEQ=255                    
         B     CLS4                                                             
CLS2B    OC    DUB(2),DUB          TEST FIRST CI                                
         BNZ   CLS2C                                                            
         CLI   SVSEQ,2             ADJUST SEQ NUM OF FIRST CI OF MULTI          
         BNE   *+8                                                              
         MVI   PQSEQ,1                                                          
CLS2C    SR    RE,RE               CHECK SEQ NUM IS VALID                       
         IC    RE,SVSEQ                                                         
         BCTR  RE,0                                                             
         STC   RE,SVSEQ                                                         
         TM    SKACTRL,X'10'       TEST IF EXTENDING REPORT                     
         BO    *+14                                                             
         CLC   PQSEQ,SVSEQ                                                      
         BNE   CLSERR1                                                          
*                                                                               
CLS4     NI    PQSTAT,255-PQSTTE   TURN OFF TEMPORARY FILE FLAG                 
*                                                                               
CLS4A    TM    CLOTYP,CLOTJOB      TEST IF CLO/JOB                              
         BZ    CLS4B                                                            
         NI    PQATTB,255-PQATJOBO                                              
         OI    PQATTB,PQATJOBI     YES SET REPORT CONTAINS JCL                  
*NOP     TM    PQTYPE,PQTYUPDT                                                  
*NOP     BZ    CLS5                                                             
*NOP     OI    PQSTAT,PQSTHO       UPDATIVE SOONS MUST BE ON HOLD               
         B     CLS5                                                             
*                                                                               
CLS4B    TM    CLOTYP,CLOTERR      TEST IF CLO/ERR                              
         BZ    CLS4C                                                            
         OI    PQATTB,PQATERR      YES SET REPORT IS IN ERROR                   
*                                                                               
CLS4C    TM    CLOTYP,CLOTUSR      TEST IF CLO/USR                              
         BZ    CLS4D                                                            
         OI    PQATTB,PQATUSR      YES SET REPORT HAS USER DATA                 
         CLI   CLOINF,PQUSRTYW     AND SEE IF WKFILE TYPE USER DATA             
         BNE   *+8                                                              
         OI    PQATTB,PQATUSRW                                                  
*                                                                               
CLS4D    TM    PQATTB,PQATJOBI     TEST IF PREV CONTAINED JCL                   
         BZ    CLS5                                                             
         NI    PQATTB,255-PQATJOBI                                              
         OI    PQATTB,PQATJOBO     YES SWITCH TO JOB OUTPUT                     
*                                                                               
CLS5     CLI   P1,0                TEST IF ERROR CLOSE OR CLO/PUR               
         BE    CLS6                                                             
         XC    PQINDEX,PQINDEX     YES CLEAR THE WHOLE INDEX DATA               
*                                                                               
CLS6     OC    DUB(2),DUB          SET VALUES IN FIRST CI                       
         BNZ   CLS8                                                             
         TM    SKACTRL,X'10'       TEST EXTENDING EXISTING REPORT               
         BZ    CLS6A                                                            
         CLC   CIADDR,SKFSTCI                                                   
         BNE   CLS8                                                             
         IC    R1,PQXREPS          BUMP NUMBER OF EXTENSION REPORTS             
         LA    R1,1(R1)                                                         
         STC   R1,PQXREPS                                                       
         TM    SKACTRL,X'20'       TEST DROP1ST PAGE OF EXTENSN REPORT          
         BZ    CLS6A                                                            
         OI    PQXFLAG,PQXFNRD     SET EXTRA REPS HAVE NO REQ DETAIL            
CLS6A    MVC   PQNCI,P1+1          SET TOTAL NUMBER OF CIS                      
         SR    R1,R1                                                            
         ICM   R1,1,P1+2           R1=PQSEQ OF HIGHEST XTNSN CI                 
         BZ    *+10                                                             
         BCTR  R1,0                                                             
         STC   R1,PQNCIX           SET TOTAL NUMBER OF XTNSN CIS                
         MVC   PQLINES,P3+1        SET TOTAL NUMBER OF LINES                    
         MVC   PQPAGES,P4+2        SET TOTAL NUMBER OF PAGES                    
*                                                                               
CLS6A0   SR    R0,R0               COMPUTE SIZE FACTOR                          
         ICM   R0,7,PQLINES                                                     
         AHI   R0,31                                                            
         SRL   R0,5                R0=APPROX NUM OF AVG SIZE PAGES              
CLS6A1   CHI   R0,100                                                           
         BNL   CLS6A2                                                           
         STC   R0,PQAGES           000-099 IS PAGES IF LT 100                   
         B     CLS6B                                                            
CLS6A2   CHI   R0,1000                                                          
         BNL   CLS6A3                                                           
         SR    RE,RE                                                            
         LR    RF,R0                                                            
         AHI   RF,-100                                                          
         D     RE,=F'10'                                                        
         AHI   RF,100                                                           
         STC   RF,PQAGES           100-189 IS PAGES/10 IF LT 1000               
         B     CLS6B                                                            
CLS6A3   CHI   R0,7000                                                          
         BNL   CLS6A4                                                           
         SR    RE,RE                                                            
         LR    RF,R0                                                            
         AHI   RF,-1000                                                         
         D     RE,=F'100'                                                       
         AHI   RF,190                                                           
         STC   RF,PQAGES           190-249 IS PAGES/100 IF LT 7000              
         B     CLS6B                                                            
CLS6A4   MVI   PQAGES,250          250 ALL OTHER LARGE REPORTS                  
*                                                                               
CLS6B    SR    RE,RE               COMPUTE AVERAGE RECORD LENGTH                
         L     RF,P5                                                            
         SR    R0,R0                                                            
         ICM   R0,7,PQLINES                                                     
         BNZ   *+10                                                             
         SR    RF,RF                                                            
         B     *+6                                                              
         DR    RE,R0                                                            
         STCM  RF,3,PQAVCPL                                                     
*                                                                               
         MVC   PQMAXCPL,P6+3       SET MAXIMUM CHARACTERS PER LINE              
         CLI   PQMAXCPL,132                                                     
         BNH   *+8                                                              
         OI    PQATTB,PQATWIDE     SET WIDE REPORT ATTRIBUTE                    
*                                                                               
         OC    P7,P7               TEST IF NEW RETAIN REQUIRED                  
         BZ    *+10                                                             
         MVC   PQAGERD(3),P7       SET NEW RETAIN DATE/TIME                     
*                                                                               
         MVC   PQDAEOR,SKADDR      SET TTB OF LAST BLOCK ON FILE                
         MVC   PQCIEOR,SKSTRCI     SET TT OF FIRST REC IN LAST CI               
*                                                                               
         TM    CLOTYP,CLOTUSR      TEST IF CLOSE WITH USER DATA                 
         BZ    *+10                                                             
         MVC   PQUSRINF,CLOINF     SET USER DATA IN FIRST CI                    
*                                                                               
         TM    CLOTYP,CLOTERR      TEST IF CLOSE WITH ERROR                     
         BZ    *+10                                                             
         MVC   PQERRRC,CLOERRRC    SET ERROR REASON CODE                        
*                                                                               
CLS8     GOTO1 DATAMGR,DMCB,(X'00',DMWRT)                                       
         CLI   DMCB+8,0                                                         
         BNE   CLSERR2                                                          
         MVC   0(2,R6),CIADDR                                                   
         MVC   2(2,R6),FFS                                                      
         LA    R6,2(R6)                                                         
         OC    DUB(2),DUB          TEST IF FIRST CI                             
         BNZ   CLS9                                                             
         TM    SKACTRL,X'10'       TEST EXTENDING EXISTING REPORT               
         BZ    CLSA                NO                                           
         CLC   CIADDR(2),SKFSTCI   YES IS THIS THE ORIGINAL FIRST CI            
         BE    CLSA                                                             
         MVC   FUL,FFS             SET SWITCHING TO FIRST CI                    
         MVC   CIADDR(2),SKFSTCI   SET TO ORIGINAL FIRST CI                     
         B     CLS2                                                             
CLS9     MVC   FUL,CIADDR          SAVE ADDR OF THIS CI                         
         MVC   CIADDR(2),DUB       SAVE ADDR OF PREV CI                         
         B     CLS2                                                             
*                                                                               
CLSA     MVC   STFSTCI,CIADDR      SAVE DISK ADDR OF FIRST CI                   
         MVC   SVDATA,PQINDEX      SET PARAMS FOR INDEX STATUS CHANGE           
         XC    STPARM,STPARM                                                    
         MVC   STVAL,PQSTAT        USE STATUS VALUE IN FIRST CI                 
         MVI   STCTRL,X'82'        SET UPDATE INDEX ON ADD                      
         MVC   STFSTCI(2),CIADDR                                                
         LA    RE,CXREC            USE CXREC AS INDEX I/O AREA                  
         ST    RE,STBUFFA                                                       
         BRAS  RE,STAT             GOTO1 INDEX UPDATE ROUTINE                   
         BNE   CLSERR3                                                          
*                                                                               
CLSC     MVI   PARM3,0                                                          
         MVI   SKACTRL,X'00'                                                    
         XC    SKFCTRL,SKFCTRL     CLEAR FILE INFO AND SET FIRST CI             
         MVC   SKFSTCI(2),STFSTCI                                               
         MVC   SKFSTCI+2(2),=X'0100'                                            
         XC    SKXCTRL,SKXCTRL     CLEAR INDEX INFO                             
         LR    R0,R4               SET FILE INFO FOR FIRST CI                   
         MVC   CIADDR,SKFSTCI                                                   
         BRAS  RE,RDFADD                                                        
         B     CLSX                                                             
*                                                                               
CLSERR1  MVI   PARM3,X'42'         FORMAT ERROR ON CLOSE                        
         B     *+8                                                              
CLSERR2  MVI   PARM3,X'40'         DISK ERROR ON FILE UPDATE                    
         XC    SKFCTRL,SKFCTRL                                                  
         XC    SKXCTRL,SKXCTRL                                                  
         MVI   SKACTRL,X'00'                                                    
         B     CLSX                                                             
CLSERR3  XC    SKFCTRL,SKFCTRL     ERROR ON INDEX UPDATE                        
         XC    SKXCTRL,SKXCTRL                                                  
         MVI   SKACTRL,X'00'                                                    
         DC    H'0'                                                             
*                                                                               
CLSX     CLI   OFFLINE,C'Y'        IF ONLINE RETURN GLOBAL PRTQ ID              
         BE    CLSXX                                                            
         TM    CLOTYP,CLOTNOR      BUT ONLY IF A NORMAL CLOSE                   
         BZ    CLSXX                                                            
         L     RE,VSSB                                                          
         L     RE,SSBTKADR-SSBD(RE)                                             
         L     RE,TCBWRKA-TCBD(RE)                                              
         LA    R0,8                                                             
CLSX1    CLC   0(4,RE),=C'MNTR'    FIND MONITORS REGISTERS                      
         BE    CLSX2                                                            
         L     RE,8(RE)                                                         
         BCT   R0,CLSX1                                                         
         B     CLSXX                                                            
CLSX2    LA    RE,72+16(RE)        POINT TO PRTQ GLOBAL IN MNTR'S W/S           
         MVC   0(8,RE),0(R4)       SET PRTQ KEY IN GLOBAL AREA                  
*                                                                               
CLSXX    CLI   PARM3,0             SET CC TO EQL IF NO ERROR                    
         MVC   REPTINFO,0(R4)                                                   
         XIT1                                                                   
         EJECT                                                                  
* PUT THE FIRST INDEX RECORD INTO LOCKTAB                                       
*                                                                               
RSRVPQ   ST    RE,SAVRE            TEST TO RELEASE PQFILE FROM LOCKTAB          
         CLI   OFFLINE,C'Y'                                                     
         BER   RE                                                               
         LA    R1,CIP1                                                          
         MVC   0(1,R1),VPQFILE     SET FILE NUMBER                              
         MVC   1(3,R1),=X'EEEEEE'  SET DUMMY DISK ADDR FOR RESERVE              
         MVI   4(R1),0                                                          
         MVC   5(3,R1),DMCB+13     SET I/O AREA                                 
         MVC   8(4,R1),=V(RDID)    SET COMMAND                                  
         XC    12(8,R1),12(R1)     CLEAR CIP4/CIP5                              
         ST    R2,SAVR2                                                         
         LA    R2,DMCB             R2 MUST POINT TO DMCB                        
         GOTO1 =V(LOCKER)                                                       
         L     R2,SAVR2                                                         
RSRVPQX  L     RE,SAVRE                                                         
         BR    RE                                                               
                                                                                
* RELEASE ALL RECORDS IN ONLINE RECORD LOCK TABLE                               
*                                                                               
RLSEPQ   ST    RE,SAVRE            TEST TO RELEASE PQFILE FROM LOCKTAB          
         CLI   OFFLINE,C'Y'                                                     
         BER   RE                                                               
         LA    R1,CIP1                                                          
         XC    0(20,R1),0(R1)      CLEAR ALL FIVE LOCKER PARAMS                 
         MVC   0(1,R1),VPQFILE     SET FILE NUMBER                              
         GOTO1 =V(LOCKER)                                                       
RLSEPQX  L     RE,SAVRE                                                         
         BR    RE                                                               
                                                                                
* SET CONTROL INTERVAL DATA FOR PRTQ FILE REFERENCED                            
*                                                                               
SETPQ    ST    RE,SAVRESPQ         SET CONTROL INTERVAL DATA                    
         L     RF,VPQFILE                                                       
         LA    RF,0(RF)                                                         
         NI    36(RF),X'FF'-X'40'  TURN OFF NOP BIT IN DTF                      
         AHI   RF,-40                                                           
         ST    RF,VPQTAB           CI DATA LIVES INFRONT OF DTF                 
*                                                                               
SETPQ1   OC    0(4,RF),0(RF)       INITIALISE FILE ON FIRST REFERENCE           
         BNZ   SETPQ2                                                           
         L     RF,=A(IPQ)                                                       
         BASR  RE,RF                                                            
*                                                                               
SETPQ2   L     RF,VPQTAB           SET CI DATA FROM FILE INFO                   
         MVC   CIDATA,0(RF)                                                     
         L     RE,SAVRESPQ                                                      
         BR    RE                                                               
         EJECT                                                                  
* ROUTINES TO GET/SET PRTQUE DATA FROM VARIOUS INPUT FIELDS                     
*                                                                               
WHATPQA  ST    RE,SAVREWPQ         GET PRTQ FROM ALPHA AT USERPRTQ+4            
         LA    RE,PRTQLST+8                                                     
WHATPQA1 CLI   0(RE),0                                                          
         BNE   *+6                                                              
         DC    H'0'                DIE IF INVALID ALPHA PQ ID                   
         CLC   1(1,RE),USERPRTQ+4                                               
         BE    WHATPQX                                                          
         LA    RE,8(RE)                                                         
         B     WHATPQA1                                                         
                                                                                
WHATPQI  ST    RE,SAVREWPQ         GET PRTQ FROM INT NUM IN USERINUM            
         CLC   USERINUM,PRTQLST                                                 
         BNH   *+6                                                              
         DC    H'0'                DIE IF INVALID INTERNAL PQ NUM               
         SR    RE,RE                                                            
         IC    RE,USERINUM                                                      
         SLL   RE,3                                                             
         LA    RE,PRTQLST(RE)                                                   
         B     WHATPQX                                                          
                                                                                
WHATPQU  ST    RE,SAVREWPQ         GET PRTQ FROM USERID USERIDNO                
         SR    RE,RE                                                            
         CLC   USERIDNO,=AL2(32000) PUBLIC REPORTS GO TO FIRST PRTQ             
         BL    WHATPQ1                                                          
         CLC   USERIDNO,=AL2(32100)                                             
         BNH   WHATPQ2                                                          
WHATPQ1  SR    RF,RF                                                            
         ICM   RF,3,USERIDNO                                                    
         SR    R0,R0                                                            
         IC    R0,PRTQLST          GET NUMBER OF PRTQ FILES                     
         DR    RE,R0                                                            
WHATPQ2  LA    RE,1(RE)            RE=RELATIVE PRTQ                             
         SLL   RE,3                                                             
         LA    RE,PRTQLST(RE)      POINT TO LIST ENTRY FOR PRTQ                 
                                                                                
WHATPQX  MVC   USERPRTQ,=CL8'PRTQ'                                              
         MVC   USERPRTQ+4(1),1(RE) SET DMCB PRTQ NAME                           
         MVC   USERINUM,0(RE)      SET INTNUM                                   
         MVC   USERXNUM(4),4(RE)   SET EXTNUM AND A(DTF)                        
         L     RE,SAVREWPQ                                                      
         BR    RE                                                               
         EJECT                                                                  
*DMPRTQR                                                                        
       ++INCLUDE DMPRTQR                                                        
         EJECT                                                                  
DATAMGR  DC    V(DATAMGR)                                                       
VENQDEQ  DC    V(DMENQDEQ)                                                      
VDMPQGCI DC    V(DMPQGCI)                                                       
*DMPRTQC                           RETAIN CLASS TABLE                           
       ++INCLUDE DMPRTQC                                                        
*&&UK                                                                           
EOFCNTOF DC    AL2(0)                                                           
EOFMAXOF DC    AL2(10)             OFFLINE MAX EOF ATTEMPTS                     
EOFBINOF DC    AL4(02*60*100)      OFFLINE BINTVL WAIT TIME (1/100 SEC)         
*                                                                               
RTLIVEOF EQU   PQRETTAB+0,2,C'H'   OFFLINE LIVE RETAIN HOURS                    
RTDEADOF EQU   PQRETTAB+2,2,C'H'   OFFLINE DEAD RETAIN HOURS                    
RTLIVEON EQU   PQRETTAB+6,2,C'H'   ONLINE  LIVE RETAIN HOURS                    
RTDEADON EQU   PQRETTAB+8,2,C'H'   ONLINE  DEAD RETAIN HOURS                    
*                                                                               
DFLPP    DC    AL1(66)             DEFAULT LINES PER PAGE                       
         DC    AL1(0)                                                           
*&&                                                                             
*&&US                                                                           
EOFCNTOF DC    AL2(0)                                                           
EOFMAXOF DC    AL2(10)             OFFLINE MAX EOF ATTEMPTS                     
EOFBINOF DC    AL4(02*60*100)      OFFLINE BINTVL WAIT TIME (1/100 SEC)         
*                                                                               
RTLIVEOF EQU   PQRETTAB+0,2,C'H'   OFFLINE LIVE RETAIN HOURS                    
RTDEADOF EQU   PQRETTAB+2,2,C'H'   OFFLINE DEAD RETAIN HOURS                    
RTLIVEON EQU   PQRETTAB+6,2,C'H'   ONLINE  LIVE RETAIN HOURS                    
RTDEADON EQU   PQRETTAB+8,2,C'H'   ONLINE  DEAD RETAIN HOURS                    
*                                                                               
DFLPP    DC    AL1(88)             DEFAULT LINES PER PAGE                       
         DC    AL1(0)                                                           
*&&                                                                             
MAXRSN   DC    F'65000'                                                         
FFS      DC    8X'FF'                                                           
ZEROS    DC    8X'00'                                                           
PQBLKLN  DC    H'13680'            DEFAULT BLK LEN (GOOD FOR 3390)              
*                                                                               
OFFLINE  DC    C' '                SET TO Y/N ON FIRST CALL                     
DSPACE   DC    C' '                SET FROM SSB ON FIRST CALL                   
*                                                                               
SPACES   DC    CL16' '                                                          
DMREAD   DC    CL8'DMREAD'                                                      
DMWRT    DC    CL8'DMWRT'                                                       
DMGCI    DC    CL8'DMGCI'                                                       
LABEL    DC    CL8'*PQSAVE*'                                                    
TEXTEYE  DC    CL8'PQWSINFO'                                                    
VSSB     DC    V(SSB)                                                           
VSYSFLES DC    V(SYSFLES)                                                       
*                                                                               
SOFLAB   DS    0CL10                                                            
         DC    X'0000',C'SOFSOF',X'0000'                                        
EOFLAB   DS    0CL10                                                            
         DC    X'FFFF',C'EOFEOF',X'FFFF'                                        
*                                                                               
ARCLINE  DC    X'004D09'           <ARC=AA,SPP,FFFF  ,UUUUU,UUUUU,...>          
         DC    C'<'                                                             
ARCCARD  DC    CL72'ARC=AA,SPP,FFFF  ,NNNNN,NNNNN, ,A'                          
         DC    C'>'                                                             
ARCLINEL EQU   *-ARCLINE                                                        
         EJECT                                                                  
* ENTRY POINT - LIST OF PRTQ FILES. BUILT ON FIRST CALL.                        
* FIRST ENTRY CONTAINS NUMBER OF PRTQ FILES IN FIRST BYTE.                      
* EACH OTHER ENTRY CONTAINS PRTQ NUM /PRTQ CHR/FLAGS/EXT NUM/A(DTF)             
         DS    0D                                                               
         DC    CL8'PRTQLST '                                                    
PRTQLST  DS    0XL8                                                             
         DC    AL1(0),C' ',XL2'00',X'00',AL3(0)                                 
         DC    AL1(1),C'U',XL2'00',X'F5',VL3(PRTQUE)                            
         DC    AL1(0),C' ',XL6'00'                                              
         DC    AL1(0),C' ',XL6'00'                                              
         DC    AL1(0),C' ',XL6'00'                                              
         DC    AL1(0),C' ',XL6'00'                                              
         DC    AL1(0),C' ',XL6'00'                                              
         DC    AL1(0),C' ',XL6'00'                                              
         DC    AL1(0),C' ',XL6'00'                                              
         DC    AL1(0),C' ',XL6'00'                                              
         DC    AL1(0),C' ',XL6'00'                                              
         DC    AL1(0),C' ',XL6'00'                                              
         DC    AL1(0),C' ',XL6'00'                                              
         DC    AL1(0),C' ',XL6'00'                                              
         DC    AL1(0),C' ',XL6'00'                                              
         DC    AL1(0),C' ',XL6'00'                                              
         DC    AL1(0),C' ',XL6'00'                                              
         DC    AL1(0),C' ',XL6'00'                                              
PRTQLSTX EQU   *                                                                
                                                                                
* ENTRY POINT - TABLE OF PRTQ FILE CPUID/DSPACE FROM STAMP IN FILE              
*                                                                               
         DC    01XL2'00'           CL2=DDSQDQ STAMP                             
PRTQSTMP DC    17XL2'00'           CL2=STAMP OF EACH PRTQ OPENED                
                                                                                
* ENTRY POINT - TABLE OF PRTQ FILE INFO INDEXED BY USERINUM.                    
* ZEROTH ENTRY HAS CUMULATIVE DATA FOR ALL QUEUES.                              
* ENTRY FOR EACH QUEUE HAS FOLLOWING INFO.                                      
*                                                                               
* +00(4) LAST INDEX PAGE/ENTRY WHERE REPORT WAS ADDED                           
* +04(4) SPARE                                                                  
* +08(4) SEARCH PART1 FOR NEW REPORT - COUNT                                    
* +12(4) SEARCH PART1 FOR NEW REPORT - NUMBER OF I/0'S                          
* +16(4) SEARCH PART1 FOR OLD REPORT TO PURGE - COUNT                           
* +20(4) SEARCH PART1 FOR OLD REPORT TO PURGE - NUMBER OF I/0'S                 
* +24(4) SEARCH PART2 FOR AVAIL CI - COUNT                                      
* +28(4) SEARCH PART2 FOR AVAIL CI - NUMBER OF I/0'S                            
         DS    0D                                                               
         DC    CL8'PRTQXPE '                                                    
PRTQXPE  DS    0XL32                                                            
         DC    XL8'FFFF000000000000',6F'0'                                      
         DC    XL8'FFFF000000000000',6F'0'                                      
         DC    XL8'FFFF000000000000',6F'0'                                      
         DC    XL8'FFFF000000000000',6F'0'                                      
         DC    XL8'FFFF000000000000',6F'0'                                      
         DC    XL8'FFFF000000000000',6F'0'                                      
         DC    XL8'FFFF000000000000',6F'0'                                      
         DC    XL8'FFFF000000000000',6F'0'                                      
         DC    XL8'FFFF000000000000',6F'0'                                      
         DC    XL8'FFFF000000000000',6F'0'                                      
         DC    XL8'FFFF000000000000',6F'0'                                      
         DC    XL8'FFFF000000000000',6F'0'                                      
         DC    XL8'FFFF000000000000',6F'0'                                      
         DC    XL8'FFFF000000000000',6F'0'                                      
         DC    XL8'FFFF000000000000',6F'0'                                      
         DC    XL8'FFFF000000000000',6F'0'                                      
         DC    XL8'FFFF000000000000',6F'0'                                      
         DC    XL8'FFFF000000000000',6F'0'                                      
PRTQXPEX EQU   *                                                                
         EJECT                                                                  
* TABLE OF AVAILABLE REPORTS. SEQUENCE IS ORDER OF USERBILITY                   
* FIRST ENTRY MUST BE FOR PURGED REPORTS                                        
* SAME USERID REQUIRED ENTRYS (X'80') MUST COME NEXT                            
* ANY USERID ENTRYS MUST COME AFTER SAME USERID ENTRYS                          
* ANY ENTRY CAN HAVE USE LARGEST REPORT (X'40') INSTEAD OF OLDEST               
*                                                                               
AVSTAT   DS    0XL2                                                             
         DC    X'00',X'00'         PURGED    N/A                                
*                                                                               
         DC    X'10',X'C0'         SENT      SAMEUSER  LARGEST                  
         DC    X'20',X'C0'         PRTD      SAMEUSER  LARGEST                  
         DC    X'C0',X'80'         LIVE      SAMEUSER                           
*                                                                               
         DC    X'10',X'40'         SENT      ANYUSER   LARGEST                  
         DC    X'20',X'40'         PRTD      ANYUSER   LARGEST                  
         DC    X'C0',X'00'         LIVE      ANYUSER                            
*                                                                               
         DC    X'FF',X'00'         LAST/SENT N/A                                
         DC    X'FF',X'00'         LAST/PRTD N/A                                
         EJECT                                                                  
* TABLE OF VALID CARRIAGE CONTROL CHRS. WITH DATA AND SPACING INFO              
*                                                                               
CCTAB    DS    0XL3                                                             
         DC    X'01',X'0100'       IBM DATA THEN SPACE 0                        
         DC    X'09',X'0101'       IBM DATA THEN SPACE 1                        
         DC    X'0B',X'0001'       IBM IMMEDIATE SPACE 1                        
         DC    X'11',X'0102'       IBM DATA THEN SPACE 2                        
         DC    X'13',X'0002'       IBM IMMEDIATE SPACE 2                        
         DC    X'19',X'0103'       IBM DATA THEN SPACE 3                        
         DC    X'1B',X'0003'       IBM IMMEDIATE SPACE 3                        
         DC    X'89',X'01FF'       IBM DATA THEN CHANL 1                        
         DC    X'8B',X'00FF'       IBM IMMEDIATE CHANL 1                        
*                                                                               
         DC    C' ',X'020B'        ASA SPACE 1 THEN DATA                        
         DC    C'0',X'0213'        ASA SPACE 2 THEN DATA                        
         DC    C'-',X'021B'        ASA SPACE 3 THEN DATA                        
         DC    C'1',X'028B'        ASA CHANL 1 THEN DATA                        
*                                                                               
         DC    X'000000'                                                        
         EJECT                                                                  
* TABLE OF VALID PRTQUE ACTIONS                                                 
*                                                                               
* XL1    INTERNAL ACTION NUMBER                                                 
* CL7    EXTERNAL ACTION NAME (MATCH ON FIRST THREE CHRS)                       
* XL1    FLAGS X'80'=PRTQ FILE MUST BE SET BY CALLER IN DMCB                    
*              X'40'=PRTQ FILE MUST BE DEFINED IN BUFFER                        
*              X'01'=PRTQ FILE NEED NOT BE DEFINED AT ALL                       
* AL3    ADDRESS OF PROCESSING ROUTINE                                          
*                                                                               
PQACTNS  DC    0F'0'                                                            
         DC    X'01',C'DMPRINT',X'00',AL3(ADDPRT)                               
         DC    X'02',C'OPEN   ',X'00',AL3(ADDOPN)                               
         DC    X'03',C'ADD    ',X'00',AL3(ADD)                                  
         DC    X'04',C'CLOSE  ',X'00',AL3(ADDCLS)                               
*                                                                               
         DC    X'21',C'ACTI   ',X'40',AL3(STA)                                  
         DC    X'22',C'HOLD   ',X'40',AL3(STA)                                  
         DC    X'23',C'PRINTED',X'40',AL3(STA)                                  
         DC    X'24',C'SENT   ',X'40',AL3(STA)                                  
         DC    X'25',C'KEEP   ',X'40',AL3(STA)                                  
         DC    X'26',C'UNKEEP ',X'40',AL3(STA)                                  
         DC    X'27',C'PURGE  ',X'40',AL3(STA)                                  
         DC    X'28',C'RETAIN ',X'40',AL3(STA)                                  
         DC    X'29',C'UNERROR',X'40',AL3(STA)                                  
         DC    X'2A',C'INVISIB',X'40',AL3(STA)                                  
         DC    X'2B',C'VISIBLE',X'40',AL3(STA)                                  
         DC    X'2C',C'ERROR  ',X'40',AL3(STA)                                  
         DC    X'2D',C'CLASS  ',X'40',AL3(STA)                                  
         DC    X'2E',C'UNSEC  ',X'40',AL3(STA)                                  
         DC    X'2F',C'SECURE ',X'40',AL3(STA)                                  
*                                                                               
         DC    X'31',C'JISET  ',X'40',AL3(STA)                                  
         DC    X'32',C'JIUNSET',X'40',AL3(STA)                                  
         DC    X'33',C'JOSET  ',X'40',AL3(STA)                                  
         DC    X'34',C'JOUNSET',X'40',AL3(STA)                                  
         DC    X'35',C'ARSET  ',X'40',AL3(STA)                                  
         DC    X'36',C'ARUNSET',X'40',AL3(STA)                                  
         DC    X'37',C'ADSET  ',X'40',AL3(STA)                                  
         DC    X'38',C'ADUNSET',X'40',AL3(STA)                                  
         DC    X'39',C'ADPSET ',X'40',AL3(STA)                                  
         DC    X'3A',C'AESET  ',X'40',AL3(STA)                                  
         DC    X'3B',C'AEUNSET',X'40',AL3(STA)                                  
         DC    X'3C',C'BKSET  ',X'40',AL3(STA)                                  
         DC    X'3D',C'BKUNSET',X'40',AL3(STA)                                  
         DC    X'3E',C'BKPSET ',X'40',AL3(STA)                                  
*                                                                               
         DC    X'41',C'INDEX  ',X'80',AL3(NDX)                                  
         DC    X'42',C'READ   ',X'40',AL3(RDF)                                  
         DC    X'43',C'SEQ    ',X'80',AL3(SEQ)                                  
         DC    X'44',C'WRITE  ',X'40',AL3(WRF)                                  
         DC    X'45',C'RANDOM ',X'40',AL3(RDR)                                  
*                                                                               
         DC    X'61',C'DESC   ',X'40',AL3(ATT)                                  
         DC    X'62',C'RELIVE ',X'40',AL3(ATT)                                  
         DC    X'63',C'REDEAD ',X'40',AL3(ATT)                                  
*                                                                               
         DC    X'81',C'BUFFER ',X'01',AL3(BUF)                                  
         DC    X'82',C'BURSTR ',X'01',AL3(BUF)                                  
         DC    X'83',C'BUSAVE ',X'01',AL3(BUS)                                  
*                                                                               
PQAINI   DC    X'A1',C'INIT   ',X'80',AL3(INI)                                  
         DC    X'A2',C'GLIST  ',X'01',AL3(GLI)                                  
         DC    X'A3',C'GFILE  ',X'01',AL3(GFI)                                  
         DC    X'A4',C'CLEANUP',X'80',AL3(INI)                                  
*                                                                               
PQACTNX  DC    X'00',X'00'                                                      
         EJECT                                                                  
* EQUATES FOR PRTQUE ACTIONS                                                    
*                                                                               
ACTNDMP  EQU   X'01'                                                            
ACTNOPE  EQU   X'02'                                                            
ACTNADD  EQU   X'03'                                                            
ACTNCLO  EQU   X'04'                                                            
*                                                                               
ACTNACT  EQU   X'21'                                                            
ACTNHOL  EQU   X'22'                                                            
ACTNPRI  EQU   X'23'                                                            
ACTNSEN  EQU   X'24'                                                            
ACTNKEE  EQU   X'25'                                                            
ACTNUNK  EQU   X'26'                                                            
ACTNPUR  EQU   X'27'                                                            
ACTNRET  EQU   X'28'                                                            
ACTNERX  EQU   X'29'                                                            
ACTNINV  EQU   X'2A'                                                            
ACTNVIS  EQU   X'2B'                                                            
ACTNERR  EQU   X'2C'                                                            
ACTNCLS  EQU   X'2D'                                                            
ACTNSEX  EQU   X'2E'                                                            
ACTNSEC  EQU   X'2F'                                                            
*                                                                               
ACTNJIS  EQU   X'31'                                                            
ACTNJIU  EQU   X'32'                                                            
ACTNJOS  EQU   X'33'                                                            
ACTNJOU  EQU   X'34'                                                            
ACTNARS  EQU   X'35'                                                            
ACTNARU  EQU   X'36'                                                            
ACTNADS  EQU   X'37'                                                            
ACTNADU  EQU   X'38'                                                            
ACTNADP  EQU   X'39'                                                            
ACTNAES  EQU   X'3A'                                                            
ACTNAEU  EQU   X'3B'                                                            
ACTNBKS  EQU   X'3C'                                                            
ACTNBKU  EQU   X'3D'                                                            
ACTNBKP  EQU   X'3E'                                                            
*                                                                               
ACTNREA  EQU   X'42'                                                            
*                                                                               
ACTNDES  EQU   X'61'                                                            
ACTNREL  EQU   X'62'                                                            
ACTNRED  EQU   X'63'                                                            
*                                                                               
ACTNBUF  EQU   X'81'                                                            
ACTNBUR  EQU   X'82'                                                            
ACTNBUS  EQU   X'83'                                                            
*                                                                               
ACTNINI  EQU   X'A1'                                                            
ACTNGLI  EQU   X'A2'                                                            
ACTNGFI  EQU   X'A3'                                                            
ACTNCLE  EQU   X'A4'                                                            
                                                                                
LASTSEQ  EQU   255                 LAST NORMAL SEQ NUMBER                       
LASTSEQM EQU   254                 LAST NORMAL SEQ NUMBER -1                    
LASTSEQP EQU   0                   LAST NORMAL SEQ NUMBER +1                    
FRSTXTN  EQU   2                   FIRST EXTNS SEQ NUMBER                       
FRSTXTNM EQU   1                   FIRST EXTNS SEQ NUMBER -1                    
FRSTXTNP EQU   3                   FIRST EXTNS SEQ NUMBER +1                    
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
* END OF ADDRESSABILITY VIA R8,R9,RA,RB.                                        
* ALL TABLES AND ROUTINES BEYOND THIS POINT ARE ADDRESSED BY =A(ROUT).          
* EACH ROUTINE MUST BE LESS THAN 4K AND USE RB AS ITS LOCAL BASE REG.           
* FIRST 4K OF MAIN CSECT GOES UNADDRESSABLE.                                    
                                                                                
NODATA   DS    0CL32                                                            
         DC    CL32'<<<<<<<< NO DATA FOUND >>>>>>>>>'                           
         DC    CL32'<<<<<<<< NO DATA FOUND >>>>>>>>>'                           
         DC    CL32'<<<<<<<< NO DATA FOUND >>>>>>>>>'                           
         DC    CL32'<<<<< KEINE DATEN GEFUNDEN >>>>>'                           
         DC    CL32'<<<<< DONNEES NON TROUVEES >>>>>'                           
         DC    CL32'<<<<< DATOS NO ENCONTRADOS >>>>>'                           
         DC    CL32'<<<<<<<< NO DATA FOUND >>>>>>>>>'                           
         DC    CL32'<<<< GEEN GEGEVENS GEVONDEN >>>>'                           
                                                                                
TRTDD    DC    XL16'00000000000000000000000000000000'  00-0F                    
         DC    XL16'00000000000000000000000000000000'  10-1F                    
         DC    XL16'03030404040401000303040404040000'  20-2F                    
         DC    XL16'00000000000000000000000000000000'  30-3F                    
         DC    XL16'00000000000000000000000000000000'  40-4F                    
         DC    XL16'00000000000000000000000000000000'  50-5F                    
         DC    XL16'00000000000000000000000000000000'  60-6F                    
         DC    XL16'00000000000000000000000000000000'  70-7F                    
         DC    XL16'00000000000000000000000000000000'  80-8F                    
         DC    XL16'00000000000000000000000000000000'  90-9F                    
         DC    XL16'00000000000000000000000000000000'  A0-AF                    
         DC    XL16'00000000000000000000000000000000'  B0-BF                    
         DC    XL16'00000000000000000000000000000000'  C0-CF                    
         DC    XL16'00000000000000000000000000000000'  D0-DF                    
         DC    XL16'00000000000000000000000000000000'  E0-EF                    
         DC    XL16'00000000000000000000000000000000'  F0-FF                    
         EJECT                                                                  
* SCAN SYSFLES FOR PRTQ FILES AND BUILD PRTQ FILE LIST TABLE                    
* EXTRACT AND SET OFFLINE AND DSPACE VALUES                                     
*                                                                               
BLDPQL   ST    RE,SAVRE                                                         
         ICM   RE,15,VSYSFLES      POINT TO SYSTEM FILES IN DMGR                
         JNZ   *+6                                                              
         DC    H'0'                                                             
         CLI   0(RE),X'01'         FIRST ENTRY MUST BE SERVICE SYSTEM           
         JE    *+6                                                              
         DC    H'0'                                                             
         LH    R0,2(RE)            SET NUMBER OF FILES IN LIST                  
         LA    RE,4(RE)            POINT TO FIRST FILE IN LIST                  
         LA    RF,PRTQLST+8                                                     
         SR    R1,R1               SET NUMBER OF PRTQ FILES                     
BLDPQL1  TM    0(RE),X'10'         TEST FOR PRTQ FILE                           
         JZ    BLDPQL3                                                          
         LA    R1,1(R1)            BUMP NUM OF PRTQ FILES                       
         STC   R1,0(RF)            SAVE FILE REL NUM                            
         STC   R1,1(RF)            SAVE FILE ALPHA ID CHR FOR PRTQN             
         CLI   1(RF),9                                                          
         JNH   BLDPQL2                                                          
         ZIC   R6,1(RF)                                                         
         LA    R6,183(R6)          COMPUTE ALPHA ID                             
         STC   R6,1(RF)                                                         
         J     *+8                                                              
BLDPQL2  OI    1(RF),C'0'                                                       
         MVC   4(1,RF),3(RE)       SAVE FILE EXT NUM                            
         MVC   5(3,RF),5(RE)       SAVE FILE DTF ADDR                           
         LA    RF,8(RF)                                                         
BLDPQL3  LA    RE,8(RE)            BUMP TO NEXT FILE IN LIST                    
         JCT   R0,BLDPQL1                                                       
         LTR   R1,R1               IF NO FILES USE OLD SINGLE FILE              
         JNZ   *+12                                                             
         OI    PRTQLST+2,X'80'     SET FLAG TO SHOW OLD SINGLE FILE             
         LA    R1,1                                                             
         STC   R1,PRTQLST          SAVE NUMBER OF PRTQ FILES                    
*                                                                               
BLDPQL4  MVI   OFFLINE,C'Y'        SET OFFLINE FLAG                             
         MVI   DSPACE,C' '                                                      
         ICM   RF,15,VSSB          V(SSB) UNRESOLVED MEANS OFFLINE              
         JZ    BLDPQLX                                                          
         OC    0(2,RF),0(RF)                                                    
         JNZ   BLDPQL5                                                          
         CLI   SSOXTND-SSOOFF(RF),X'FF'                                         
         JNE   BLDPQLX                                                          
         MVC   DSPACE,SSODSPAC-SSOOFF(RF)                                       
         J     BLDPQLX                                                          
*                                                                               
BLDPQL5  MVI   OFFLINE,C'N'        SET ONLINE FLAG                              
         MVC   DSPACE,SSBDSPAC-SSBD(RF)                                         
*                                                                               
BLDPQLX  L     RE,SAVRE                                                         
         BR    RE                                                               
         EJECT                                                                  
* INITIALIZE LOCAL WORKING STORAGE                                              
* SET FUL TO A(SAVE AREA AT END OF CALLERS BUFFER)                              
*                                                                               
INIPQW   ST    RE,SAVRE                                                         
         MVI   PARM3,0             SET OK RETURN CODE                           
         MVI   CLOTYP,0            SET UNDEFINED CLOSE TYPE                     
         MVI   ADDWHY,0            SET NORMAL ADD TYPE                          
         MVC   USEREYE,TEXTEYE                                                  
         XC    USERINFO,USERINFO   RESET USER PRTQ FILE INFO BLOCK              
         XC    REPTINFO,REPTINFO   RESET USER REPORT NAME BLOCK                 
         XC    CIDATA(8),CIDATA                                                 
         XC    CXADDR(12),CXADDR                                                
*                                                                               
INIPQW1  MVC   MYLABEL,LABEL       SET OFFLINE/ONLINE DATA                      
         XC    AUTL,AUTL                                                        
         CLI   OFFLINE,C'Y'        V(SSB) UNRESOLVED MEANS OFFLINE              
         JE    INIPQW2                                                          
         L     RF,VSSB             ONLINE - SAVE A(UTL) AND SIN                 
         L     RF,SSBTKADR-SSBD(RF)                                             
         MVC   AUTL,TCBUTL-TCBD(RF)                                             
         L     RE,TCBSIN-TCBD(RF)                                               
         LA    RE,0(RE)                                                         
         STCM  RE,15,MYLABEL+3     SET ONLINE LABEL WITH TASK SIN               
*                                                                               
INIPQW1A C     R4,TCBTIA-TCBD(RF)  TEST IF USING TIA AS PQ BUFFER               
         JNE   INIPQW1B                                                         
         CR    R4,RD               TEST IF RD POINTS PAST TIA                   
         JH    *+6                                                              
         DC    H'0'                WORKING STORAGE TOO LONG                     
INIPQW1B EQU   *                                                                
*                                                                               
INIPQW2  L     RE,PARM2            SET PQ FILE NAME AND DTF ADDR                
         MVC   PQFILE,0(RE)                                                     
         MVC   VPQFILE,12(R1)                                                   
         CLI   PQFILE+4,C'U'       TEST IF SPECIFIC PQ FILE NAMED               
         JNE   INIPQW2A            YES                                          
         TM    PRTQLST+2,X'80'     NO TEST TO SEE IF OLD SINGLE FILE            
         JO    INIPQW2B            YES TREAT AS A SPECIFIC CALL                 
         J     INIPQW3             WE MUST FIGURE IT OUT LATER                  
INIPQW2A TM    PRTQLST+2,X'80'     TEST SPECIFIC CALL TO OLD FILE               
         JZ    INIPQW2B            NO                                           
         MVI   USERPRTQ+4,C'U'     YES FAKE IT TO GO TO OLD SINGLE              
         J     *+10                                                             
INIPQW2B MVC   USERPRTQ+4(1),PQFILE+4                                           
         BRAS  RE,WHATPQA          GET/SET PRTQ INFO FROM ALPHA ID              
         OI    USERFLAG,X'80'      SET CALLER TOLD US WHICH PQ                  
         MVC   PQFILE,USERPRTQ     SET ALPHA DMGR PRTQ ID                       
         MVC   VPQFILE,USERXNUM    SET EXTERNAL NUM AND A(DTF)                  
*                                                                               
INIPQW3  L     RE,VENQDEQ          SAVE CONTROL INTERVAL DATA                   
         ST    RE,CIENQDEQ                                                      
         LA    RE,L'PQINDEX                                                     
         STH   RE,CINDXLN                                                       
*                                                                               
INIPQW4  LR    R7,R4               LOCATE SAVE AREA AT END OF BUFF              
         AH    R7,PQBLKLN                                                       
         LA    R7,3(R7)                                                         
         SRL   R7,2                                                             
         SLL   R7,2                                                             
         USING SKBUFFD,R7          R7=A(SAVE AREA AT END OF BUFFER)             
*                                                                               
INIPQW5  CLC   SKLABEL,MYLABEL     INITIALISE CALLER'S SAVE AREA                
         JE    INIPQW6                                                          
         XC    SKLABEL(SKEND-SKLABEL),SKLABEL                                   
         MVC   SKLABEL,MYLABEL                                                  
*                                                                               
INIPQW6  LR    RE,R7               INITIALISE LOCAL W/S BUFF SAVE AREA          
         SR    RE,R4                                                            
         LA    RE,CXREC(RE)                                                     
         XC    0(SKEND-SKLABEL,RE),0(RE)                                        
*                                                                               
INIPQWX  L     RE,SAVRE                                                         
         BR    RE                                                               
         EJECT                                                                  
* SEARCH INDEX FOR ENTRY FOR NEW CONTROL INTERVAL FOR BUFFER AT R4              
* P1=01 FOR 1ST INDEX  - SMALL CI'S                                             
* P1=02 FOR 2ND INDEX  - LARGE CI'S                                             
* P1(4) SET TO ZEROS=EOF,FFS=ERROR,OR ELSE INDEX PAGE/ENTRY                     
* P2(4) SET TO FILE SEQUENCE NUMBER                                             
* P3(4) USED TO CONTROL ERROR RETRYS                                            
*                                                                               
GCI      NTR1  BASE=*                                                           
         BRAS  RE,PQLOCK           LOCK PQFILE DURING SEARCH OF INDEX           
         MVC   CPUASID,CIP3                                                     
         MVC   REPTINFO,0(R4)                                                   
         XC    P3,P3                                                            
         MVC   P3(1),P1            SAVE WANT SMALL/LARGE CI'S FLAG              
GCIRETRY MVC   FLAG,P3                                                          
         MVI   EOFERRRC,0                                                       
         MVI   AVTYPE,C' '                                                      
         MVI   AVCOUNT,0                                                        
         TM    FLAG,X'01'          SET DATE/TIME FOR RETENTION                  
         BO    GCI0                                                             
         L     RF,=A(GETDATE)      SET DATE AND TIME NOW                        
         BASR  RE,RF                                                            
         L     RF,=A(GETTIME)                                                   
         BASR  RE,RF                                                            
         B     GCI1                                                             
*                                                                               
GCI0     TM    QLFLAG,QLFLCIDA+QLFLKEY TEST IF CIADDR/REPKEY SPECIFIED          
         BZ    GCI1                                                             
         BRAS  RE,GETXPE           GET INDEX PAGE/ENTRY FROM CIADDR             
         MVC   P1,CXPAGE           P1=INDEX PAGE/ENTRY TO USE                   
         XC    P2,P2                                                            
         MVC   P2+2(2),QLREPNO     P2=REPORT NUM TO USE                         
         MVC   PQREPNO,QLREPNO                                                  
         XC    CXADDR,CXADDR       SET NO INDEX PAGE IN CORE                    
         TM    QLFLAG,QLFLCIDA                                                  
         BO    GCIK                                                             
GCI0A    CLI   OFFLINE,C'Y'        LOCK FIRST INDEX REC IF ONLINE               
         BE    GCI0B                                                            
         MVC   CXADDR,=X'00010100'                                              
         GOTO1 DATAMGR,DMCB,(X'80',DMREAD),PQFILE,CXADDR,CXREC                  
         ORG   *-2                                                              
         BRAS  RE,RSRVPQ                                                        
         XC    CXADDR,CXADDR       SET NO INDEX PAGE IN CORE                    
GCI0B    BRAS  RE,GETXAD                                                        
         GOTO1 DATAMGR,DMCB,(X'80',DMREAD),PQFILE,CXADDR,CXREC                  
         CLI   8(R1),0                                                          
         BNE   GCIERR                                                           
         LH    R5,CXENTRY                                                       
         MH    R5,CINDXLN                                                       
         LA    R5,CXREC(R5)        RE=A(INDEX ENTRY OF REPORT TO USE)           
         MVI   AVTYPE,C'A'         SET PRESET SINGLE CI REPORT                  
         CLI   PQSEQ-PQKEY(R5),0                                                
         BE    *+8                                                              
         MVI   AVTYPE,C'B'         SET PRESET MULTIPLE CI REPORT                
         TM    SKACTRL,X'10'       TEST IF EXTENDING EXISTING REPORT            
         BO    GCIL                YES THEN UPDATE 1ST CI INDEX ENTRY           
         B     GCIG1               NO THEN PURGE THIS ONE AND REUSE IT          
*                                                                               
GCI1     XC    P2,P2               SET P2 TO HIGH FILE NUM FOR USERID           
         MVI   AVTYPE,C' '                                                      
         LA    RE,AVSTAT                                                        
         LA    RF,AVTBL                                                         
GCI1A    XC    0(L'AVTBL,RF),0(RF) SET TABLE OF STATUS VALUES                   
         MVC   0(2,RF),0(RE)       SET STATUS BITS/FLAG                         
         MVC   6(3,RF),FFS         SET OLDEST DATE/TIME                         
         CLI   0(RF),X'FF'                                                      
         BNE   GCI1B                                                            
         ST    RF,AVTBLX           SAVE END OF TABLE ADDRESS                    
         MVC   L'AVTBL(L'AVTBL,RF),0(RF) CREATE EXTRA END OF TBL ENTRY          
         B     GCI1D                                                            
GCI1B    LA    RE,2(RE)                                                         
         LA    RF,L'AVTBL(RF)                                                   
         B     GCI1A                                                            
*                                                                               
GCI1D    TM    CIRSNF,CIXDSPQ      TEST IF INDEX RECS IN DATA SPACE             
         BZ    GCI2                                                             
         ICM   RF,15,VDMPQGCI                                                   
         BZ    GCI2                                                             
         MVC   SVINDEX,0(R4)       SET INDEX OF REPORT                          
         XC    PRTQXPEA,PRTQXPEA                                                
         XC    PRTQXPEV,PRTQXPEV                                                
         CLI   OFFLINE,C'Y'        LOCK FIRST INDEX REC IF ONLINE               
         BE    GCI1E                                                            
         MVC   CXADDR,=X'00010100'                                              
         GOTO1 DATAMGR,DMCB,(X'80',DMREAD),PQFILE,CXADDR,CXREC                  
         ORG   *-2                                                              
         BRAS  RE,RSRVPQ                                                        
         XC    CXADDR,CXADDR       SET NO INDEX PAGE IN CORE                    
*                                                                               
GCI1E    LA    RE,DMGCI            PARM LIST FOR C'DMGCI',C'PRTQX'              
         ST    RE,DMCB                                                          
         LA    RE,PQFILE                                                        
         ST    RE,DMCB+4                                                        
         LA    RE,SVINDEX                                                       
         ST    RE,DMCB+8           DMCB3=A(INDEX OF REPORT)                     
         LA    RE,CXREC                                                         
         ST    RE,DMCB+12          DMCB4=A(INDEX BUFFER AREA)                   
         SR    R0,R0                                                            
         IC    R0,FLAG                                                          
         ST    R0,DMCB+16          DMCB5=TYPE OF CI REQUIRED                    
*                                                                               
GCI1F    LA    R1,CIP1             R1=PARAM LIST FOR V(DMPQGCI)                 
         XC    0(24,R1),0(R1)                                                   
         MVC   04(4,R1),DMCB+12    SET I/O AREA                                 
         MVC   12(4,R1),VPQFILE    SET FILE NUMBER AND DTF ADDR                 
         ST    R2,SAVR2                                                         
         LA    R2,DMCB                                                          
         ICM   RF,15,VDMPQGCI                                                   
         BASR  RE,RF               CALL V(DMPQGCI) WITH R2=A(DMCB)              
         L     R2,SAVR2                                                         
         TM    DMCB+8,X'40'        BACK TO NORMAL IF GCI NOT HAPPY              
         BO    GCI2                                                             
         TM    DMCB+8,X'10'        TEST IF NOT FOUND (NO AVAILABLE CI)          
         BO    GCIF                                                             
*                                                                               
GCI1G    MVC   CXADDR,SVINDEX+UKINFO-UKRECD GET INDEX DISK ADDR                 
         SR    R0,R0                                                            
         ICM   R0,3,CXADDR         PAGE=(TRACK-1)*HIREC+(REC-1)                 
         BZ    GCIERR                                                           
         BCTR  R0,0                                                             
         MH    R0,CIHIREC                                                       
         SR    R1,R1                                                            
         ICM   R1,1,CXADDR+2                                                    
         BZ    GCIERR                                                           
         BCTR  R1,0                                                             
         AR    R0,R1                                                            
         STH   R0,CXPAGE           SET INDEX PAGE                               
         SR    R0,R0                                                            
         ICM   R1,15,DMCB+16       GET DISPLACEMENT TO ENTRY                    
         LH    RF,CINDXLN                                                       
         DR    R0,RF                                                            
         STH   R1,CXENTRY          SET INDEX ENTRY                              
*                                                                               
GCI1H    LA    R5,SVINDEX          R5=A(INDEX ENTRY TO USE)                     
         USING PQRECD,R5                                                        
         CLI   PQSTAT,PQSTPU                                                    
         BNE   GCI1J                                                            
         LA    RF,AVTBL            SET PAGE/ENTRY IN PURGED SLOT                
         MVC   2(4,RF),CXPAGE                                                   
         XC    CXADDR,CXADDR                                                    
         B     GCIF                                                             
*                                                                               
GCI1J    LA    RF,AVTBL+L'AVTBL    POINT TO FIRST NON-PURGED ENTRY              
         MVC   0(1,RF),PQSTAT      SAVE STATUS (OVERRIDE)                       
         MVC   2(4,RF),CXPAGE      SAVE PAGE/ENTRY                              
         MVC   6(3,RF),PQAGERD     SAVE DATE/TIME                               
         MVC   9(1,RF),PQAGES      SAVE SIZE FACTOR                             
         XC    CXADDR,CXADDR                                                    
         B     GCIF                                                             
*                                                                               
GCI2     BRAS  RE,CXLOOPI          R5=A(PART1 INDEX ENTRY IN CXREC)             
         USING PQRECD,R5                                                        
         XC    PRTQXPEA,PRTQXPEA                                                
         XC    PRTQXPEV,PRTQXPEV                                                
         TM    FLAG,X'01'                                                       
         BO    GCI3                                                             
         CLI   OFFLINE,C'Y'        LOCK FIRST INDEX REC IF ONLINE               
         BE    GCI2A                                                            
         MVC   CXADDR,=X'00010100'                                              
         GOTO1 DATAMGR,DMCB,(X'80',DMREAD),PQFILE,CXADDR,CXREC                  
         ORG   *-2                                                              
         BRAS  RE,RSRVPQ                                                        
         XC    CXADDR,CXADDR       SET NO INDEX PAGE IN CORE                    
GCI2A    BRAS  RE,CXLOOPJ          R5=A(PART2 INDEX ENTRY IN CXREC)             
         L     RE,PRTQXPE+24       BUMP TOTAL PART2 SEARCHES                    
         LA    RE,1(RE)                                                         
         ST    RE,PRTQXPE+24                                                    
         SR    R1,R1                                                            
         ICM   R1,1,USERINUM       MAKE SURE IM NOT A FUCKING IDIOT             
         BZ    GCI4                                                             
         CLM   R1,1,PRTQLST                                                     
         BH    GCI4                                                             
         SLL   R1,5                EACH PRTQXPE ENTRY IS 32 BYTES WIDE          
         LA    R1,PRTQXPE(R1)                                                   
         L     RE,24(R1)           BUMP PART2 SEARCHES FOR THIS PRTQ            
         LA    RE,1(RE)                                                         
         ST    RE,24(R1)                                                        
         B     GCI4                                                             
*                                                                               
GCI3     TM    FLAG,X'80'          TEST WHICH TYPE OF PART1 SEARCH              
         BO    GCI3A                                                            
         L     RE,PRTQXPE+08       BUMP NEW REPORT PART1 SEARCHES               
         LA    RE,1(RE)                                                         
         ST    RE,PRTQXPE+08                                                    
         B     GCI3B                                                            
GCI3A    L     RE,PRTQXPE+16       BUMP OLD REPORT PART1 SEARCHES               
         LA    RE,1(RE)                                                         
         ST    RE,PRTQXPE+16                                                    
GCI3B    CLC   USERINUM,PRTQLST    FIND WHERE TO START PART1 SEARCH             
         BNH   *+6                                                              
         DC    H'0'                DIE IF INVALID INTERNAL PQ NUM               
GCI3C    LH    R0,CIPAGES                                                       
         OC    CJCITOT,CJCITOT                                                  
         BZ    GCI3D                                                            
         LH    R0,CJPAGE                                                        
         OC    CJENTRY,CJENTRY                                                  
         BZ    *+8                                                              
         AHI   R0,1                                                             
GCI3D    LTR   R0,R0               R0=NUM OF PART1 INDEX PAGES                  
         BNZ   *+6                                                              
         DC    H'0'                                                             
         ST    R0,PRTQXPEV         SET NUMBER OF PART1 INDEX PAGES              
         SR    R1,R1                                                            
         IC    R1,USERINUM                                                      
         SLL   R1,5                EACH PRTQXPE ENTRY IS 32 BYTES WIDE          
         LA    R1,PRTQXPE(R1)                                                   
         ST    R1,PRTQXPEA         R1=A(LAST PAGE/ENTRY FOR THIS PRTQ)          
         TM    FLAG,X'80'                                                       
         BO    GCI3E                                                            
         L     RE,08(R1)           BUMP PART ONE NEW REPORT SEARCHES            
         LA    RE,1(RE)                                                         
         ST    RE,08(R1)                                                        
         B     GCI3F                                                            
GCI3E    L     RE,16(R1)           BUMP PART ONE OLD REPORT SEARCHES            
         LA    RE,1(RE)                                                         
         ST    RE,16(R1)                                                        
GCI3F    CLC   0(2,R1),=X'FFFF'    TEST IF THIS IS FIRST TIME                   
         BNE   GCI3I               NO                                           
         CLC   PRTQXPE(2),=X'FFFF' YES TEST TO START AT A RANDOM PAGE           
         BE    GCI3G               YES                                          
         SR    RE,RE               NO START AT FIRST PAGE                       
         B     GCI3H                                                            
GCI3G    ICM   RE,12,TIMEC         COMPUTE A RANDOM START PAGE                  
         SRDL  RE,48                                                            
         DR    RE,R0                                                            
         SLL   RE,16                                                            
GCI3H    ST    RE,0(R1)            SET START PAGE/ENTRY NUMBER                  
         LTR   RE,RE               TEST IF FIRST PAGE                           
         BNZ   *+10                NO                                           
         MVC   2(2,R1),CICINDX     YES SET FIRST ENTRY IN FIRST PAGE            
GCI3I    MVC   CXPAGE(4),0(R1)     SET START PAGE/ENTRY NUMBER                  
         SR    RE,RE                                                            
         CLC   CXPAGE,PRTQXPEV+2   MAKE SURE INDEX PAGE IS VALID                
         BH    GCI3H                                                            
         CLC   CXENTRY,CIENTRYS    MAKE SURE INDEX ENTRY IS VALID               
         BH    GCI3H                                                            
GCI3J    LH    R5,CXENTRY          POINT TO LAST USED INDEX ENTRY               
         MH    R5,CINDXLN                                                       
         LA    R5,CXREC(R5)        R5=A(START SEARCH ENTRY NUMBER)              
         OC    CXPAGE(2),CXPAGE                                                 
         BZ    GCI4                START PAGE IS THE FIRST                      
         CLI   OFFLINE,C'Y'        LOCK FIRST INDEX REC IF ONLINE               
         BE    GCI4                                                             
         MVC   CXADDR,=X'00010100'                                              
         GOTO1 DATAMGR,DMCB,(X'80',DMREAD),PQFILE,CXADDR,CXREC                  
         ORG   *-2                                                              
         BRAS  RE,RSRVPQ                                                        
         XC    CXADDR,CXADDR       SET NO INDEX PAGE IN CORE                    
*                                                                               
GCI4     ICM   R1,15,PRTQXPEA      READ NEXT INDEX PAGE                         
         BZ    GCI4C                                                            
         LH    RE,PRTQXPEV+0       GET PAGES READ SO FAR                        
         CH    RE,PRTQXPEV+2                                                    
         BH    GCIF                ALL DONE                                     
         LA    RE,1(RE)                                                         
         STH   RE,PRTQXPEV+0       BUMP NUMBER OF PAGES READ                    
         TM    FLAG,X'80'                                                       
         BO    GCI4B                                                            
GCI4A    L     RE,PRTQXPE+12       BUMP TOTAL NEW PART1 INDEX I/O'S             
         LA    RE,1(RE)                                                         
         ST    RE,PRTQXPE+12                                                    
         L     RE,12(R1)           BUMP NUMBER OF NEW PART1 I/OS                
         LA    RE,1(RE)                                                         
         ST    RE,12(R1)                                                        
         B     GCI4D                                                            
GCI4B    L     RE,PRTQXPE+20       BUMP TOTAL OLD PART1 INDEX I/O'S             
         LA    RE,1(RE)                                                         
         ST    RE,PRTQXPE+20                                                    
         L     RE,20(R1)           BUMP NUMBER OF OLD PART1 I/OS                
         LA    RE,1(RE)                                                         
         ST    RE,20(R1)                                                        
         B     GCI4D                                                            
GCI4C    TM    FLAG,X'02'          TEST IF PART2 SEARCH                         
         BZ    GCI4D                                                            
         L     RE,PRTQXPE+28       BUMP TOTAL PART2 INDEX I/O'S                 
         LA    RE,1(RE)                                                         
         ST    RE,PRTQXPE+28                                                    
         SR    R1,R1                                                            
         ICM   R1,1,USERINUM       MAKE SURE IM NOT A FUCKING IDIOT             
         BZ    GCI4D                                                            
         CLM   R1,1,PRTQLST                                                     
         BH    GCI4D                                                            
         SLL   R1,5                EACH PRTQXPE ENTRY IS 32 BYTES WIDE          
         LA    R1,PRTQXPE(R1)                                                   
         L     RE,28(R1)           BUMP PART2 INDEX I/O'S FOR THE PRTQ          
         LA    RE,1(RE)                                                         
         ST    RE,28(R1)                                                        
GCI4D    BRAS  RE,GETXAD                                                        
         GOTO1 DATAMGR,DMCB,(X'80',DMREAD),PQFILE,CXADDR,CXREC                  
         CLI   8(R1),0                                                          
         BNE   GCIERR                                                           
*                                                                               
GCI5     TM    FLAG,X'01'          TEST IF FIRST PART1 INDEX READ               
         BZ    GCI6                                                             
         CLC   PRTQXPEV+0(2),=H'1'                                              
         BNE   GCI6                                                             
         ICM   R1,15,PRTQXPEA      TEST IF ENTRY IS SAVED LAST ENTRY            
         BZ    GCI6                                                             
         CLC   CXPAGE(4),0(R1)                                                  
         BNE   GCI6                                                             
         CLI   PQSTAT,PQSTPU       SKIP IF LAST ENTRY CONTAINS REPORT           
         BNE   GCIE                                                             
*                                                                               
GCI6     TM    FLAG,X'02'          PART2 - EXIT WITH FIRST PURGED SLOT          
         BZ    GCIA                                                             
         CLI   PQSTAT,PQSTPU                                                    
         BNE   GCIE                                                             
         MVI   AVTYPE,C'P'         SET PURGED REPORT FOUND                      
         MVC   AVTBL+2(4),CXPAGE   SAVE PAGE/NTRY VALUES IN AVTBL               
         B     GCIF                                                             
*                                                                               
GCIA     CLI   PQSTAT,PQSTPU       PART1 - PROCESS PURGED SLOT                  
         BNE   GCIB                                                             
         TM    FLAG,X'80'          TEST IF SEARCH FOR LARGE REPORT              
         BO    GCIE                YES IGNORE IT                                
         MVI   AVTYPE,C'P'         SET PURGED REPORT FOUND                      
         MVC   AVTBL+2(4),CXPAGE   NO EXIT WITH THIS PURGE SLOT                 
         B     GCIF                                                             
GCIA1    OC    AVTBL+2(4),AVTBL+2                                               
         BNZ   GCIE                                                             
         MVC   AVTBL+2(4),CXPAGE   SAVE ADDRESS OF FIRST PURGED SLOT            
         B     GCIE                                                             
*                                                                               
GCIB     CLC   PQSRCID,QLSRCID     SAVE HIGH FILE NUMBER FOR USER ID            
         BNE   GCIC                                                             
         CLC   P2+2(2),PQREPNO                                                  
         BH    GCIC                                                             
         MVC   P2+2(2),PQREPNO                                                  
*                                                                               
GCIC     TM    PQSTAT,PQSTTE       IGNORE TEMP AND PRINTING REPORTS             
         BO    GCIE                                                             
         TM    PQTYP1,PQTYAR       IGNORE ARCHIVABLE REPORTS                    
         NOP   GCIE                *NOP* BO                                     
         TM    FLAG,X'80'          IGNORE SINGLE ENTRYS IF PURGE SEARCH         
         BZ    *+12                                                             
         CLI   PQSEQ,0                                                          
         BE    GCIE                                                             
         CLC   DATEC(3),PQAGERD    IGNORE RETAINED REPORTS                      
         BL    GCIDR                                                            
*&&UK*&& TM    PQSTAT,PQSTAC+PQSTKE                                             
*&&UK*&& BNO   GCID                                                             
*&&UK*&& CLC   PQSUBID,=C'LU1'     IGNORE AVAILABLE LINE UP REPORTS             
*&&UK*&& BE    GCIE                                                             
*                                                                               
GCID     LA    RF,AVTBL            SAVE OLDEST REPORT FOR TYPE                  
         LA    RF,L'AVTBL(RF)      PART1 - EXIT WITH 1ST EXPIRED REPORT         
         MVC   2(4,RF),CXPAGE                                                   
         MVC   AVLXNDX,PQINDEX     SET INDEX OF EXPIRED REPORT                  
         MVI   AVTYPE,C'X'         SET EXPIRED SINGLE CI REPORT                 
         CLI   PQSEQ,0                                                          
         BE    GCIF                                                             
         MVI   AVTYPE,C'Y'         SET EXPIRED MULTIPLE CI REPORT               
         B     GCIF                                                             
GCID1    LA    RF,L'AVTBL(RF)                                                   
         CLI   0(RF),X'FF'         EXIT IF END OF TABLE                         
         BE    GCIE                                                             
GCID4    MVC   DUB(1),0(RF)        TEST FOR STATUS BITS                         
         NC    DUB(1),PQSTAT       COMPARE STATUS                               
         BZ    GCID1                                                            
GCID5    TM    1(RF),X'80'         TEST IF MUST HAVE SAME USER ID               
         BZ    GCID6                                                            
         CLC   PQSRCID,PQSRCID-PQRECD(R4)                                       
         BNE   GCID1                                                            
GCID6    TM    1(RF),X'40'         TEST IF REPORT SIZE IMPORTANT                
         BZ    GCID9                                                            
         CLI   PQSEQ,0                                                          
         BE    GCID9                                                            
         CLI   PQAGES,100          ONLY FOR LARGE REPORTS                       
         BL    GCID9                                                            
         CLC   9(1,RF),PQAGES      COMPARE SIZE                                 
         BH    GCIE                EXIT IF ALREADY HAVE LARGER ENTRY            
         BL    GCIDA                                                            
GCID9    CLC   6(3,RF),PQAGERD     COMPARE RETN DATE/TIME                       
         BL    GCIE                EXIT IF ALREADY HAVE OLDER ENTRY             
GCIDA    MVC   2(4,RF),CXPAGE      SAVE PAGE/ENTRY                              
         MVC   6(3,RF),PQAGERD     SAVE DATE/TIME                               
         MVC   9(1,RF),PQAGES      SAVE SIZE FACTOR                             
         B     GCIE                                                             
*                                                                               
GCIDR    TM    PQSTAT,PQSTKE       RETAINED REPORT CANT BE KEEP                 
         BO    GCIE                                                             
         TM    PQSTAT,PQSTDEAD     RETAINED REPORT MUST BE DEAD                 
         BZ    GCIE                                                             
         CLC   PQAGERD,=X'FF9F8F'  RETAINED REPORT CANT BE PERM                 
         BE    GCIE                                                             
         L     RF,AVTBLX           POINT TO LAST ENTRY (SENT STATUS)            
         LA    RE,VUNSNDX                                                       
         TM    PQSTAT,PQSTSE                                                    
         BO    *+12                                                             
         LA    RF,L'AVTBL(RF)      POINT TO LAST ENTRY (PRTD STATUS)            
         LA    RE,VUNPNDX                                                       
*                                                                               
GCIDR1   CLC   6(3,RF),PQAGERD     COMPARE RETN DATE/TIME                       
         BL    GCIE                EXIT IF ALREADY HAVE OLDER ENTRY             
         MVC   2(4,RF),CXPAGE      SAVE PAGE/ENTRY                              
         MVC   6(3,RF),PQAGERD     SAVE DATE/TIME                               
         MVC   9(1,RF),PQAGES      SAVE SIZE FACTOR                             
         MVC   0(24,RE),PQINDEX    SAVE SENT/PRTD VUNERABLE INDEX               
*                                                                               
GCIE     BRAS  RE,CXLOOPX          BUMP TO NEXT INDEX ENTRY                     
         B     GCI6                                                             
         B     GCI4                END OF INDEX PAGE                            
         OC    PRTQXPEA,PRTQXPEA                                                
         BZ    GCIF                END OF INDEX                                 
         BRAS  RE,CXLOOPI                                                       
         B     GCI4                BACK TO START OF INDEX                       
*                                                                               
GCIF     LA    RF,AVTBL            END OF INDEX SEARCH                          
         TM    FLAG,X'80'                                                       
         BZ    *+8                 IGNORE FIRST ENTRY IF PURGE SEARCH           
GCIF0    LA    RF,L'AVTBL(RF)                                                   
         CLI   0(RF),X'FF'         TEST LAST ENTRY IN AVAIL TABLE               
         BE    GCIF2                                                            
         OC    2(4,RF),2(RF)       FIND FIRST AVAIL ENTRY                       
         BZ    GCIF0                                                            
         MVC   P1,2(RF)            SAVE INDEX PAGE/ENTRY                        
         B     GCIG                                                             
GCIF2    TM    FLAG,X'02'          NO SPACE IN PART2 INDEX                      
         BZ    GCIF4                                                            
         MVI   FLAG,X'81'          GO SEARCH PART1 FOR A FILE TO PURGE          
         B     GCI1                                                             
GCIF4    OC    2(4,RF),2(RF)       TEST IF SENT/RETAINED IN LAST ENTRY          
         BZ    GCIF6                                                            
         MVC   P1,2(RF)            SAVE INDEX PAGE/ENTRY                        
         MVC   AVLXNDX,VUNSNDX     SET INDEX OF VUNERABLE SENT REPORT           
         B     GCIF7                                                            
GCIF6    LA    RF,L'AVTBL(RF)                                                   
         OC    2(4,RF),2(RF)       TEST IF PRTD/RETAINED IN LAST ENTRY          
         BZ    GCIF8                                                            
         MVC   P1,2(RF)            SAVE INDEX PAGE/ENTRY                        
         MVC   AVLXNDX,VUNPNDX     SET INDEX OF VUNERABLE PRTD REPORT           
GCIF7    MVI   AVTYPE,C'X'         SET VUNERABLE SINGLE CI REPORT               
         CLI   AVLXNDX+PQSEQ-PQRECD,0                                           
         BE    GCIG                                                             
         MVI   AVTYPE,C'Y'         SET VUNERABLE MULTIPLE CI REPORT             
         B     GCIG                                                             
GCIF8    XC    P1,P1               RETURN EOF IF NO SPACE IN PART1              
         MVI   EOFERRRC,X'01'      SET NO PART1                                 
         TM    FLAG,X'80'                                                       
         BZ    GCIX                                                             
         MVI   EOFERRRC,X'02'      SET NO PART2                                 
         B     GCIX                                                             
*                                                                               
GCIG     ICM   R1,15,PRTQXPEA      TEST IF JUST SEARCHED PART1                  
         BZ    GCIG0               NO                                           
         TM    FLAG,X'80'          TEST IF LOOKING FOR LARGE REPORT             
         BO    GCIG0               YES                                          
         MVC   0(4,R1),P1          NO SAVE PAGE/ENTRY OF THIS REPORT            
*                                                                               
GCIG0    CLI   0(RF),PQSTPU        FOUND A PURGED SLOT TO USE                   
         BNE   GCIG1                                                            
         MVC   CXPAGE(4),P1                                                     
         BRAS  RE,XADRSN           COMPUTE REPORT SEQUENCE NUMBER               
         MVC   P2+2(2),CIRSN                                                    
         B     GCII                                                             
*                                                                               
GCIG1    CLI   AVTYPE,C'A'         TEST PRESET SINGLE CI REPORT                 
         BE    GCIG2                                                            
         CLI   AVTYPE,C'X'         TEST EXPIRED SINGLE CI REPORT                
         BNE   GCIG3                                                            
GCIG2    MVC   CXPAGE(4),P1        DONT NEED TO PURGE SINGLE CI REPORT          
         BRAS  RE,XADRSN           COMPUTE REPORT SEQUENCE NUMBER               
         MVC   P2+2(2),CIRSN                                                    
         B     GCII                                                             
*                                                                               
GCIG3    XC    STPARM,STPARM       PURGE THIS REPORT                            
         MVI   STVAL,PQSTPU                                                     
         MVI   STCTRL,X'53'        SET PURGEMULTI/NOLOCK/INDEX/FILE             
         MVC   CXPAGE(4),P1                                                     
         BRAS  RE,XADRSN           COMPUTE REPORT SEQUENCE NUMBER               
         MVC   P2+2(2),CIRSN                                                    
         BRAS  RE,GETCAD                                                        
         MVC   STFSTCI,CIADDR      SET FILE ADDR                                
         LA    RE,CXREC                                                         
         ST    RE,STBUFFA                                                       
         BRAS  RE,STAT                                                          
         BE    GCIH                                                             
         LLC   R0,AVCOUNT          BUMP PURGE ERROR COUNT                       
         AHI   R0,1                                                             
         STC   R0,AVCOUNT                                                       
         CHI   R0,10                                                            
         BNL   GCIERR                                                           
         MVI   FLAG,X'81'          GO SEARCH PART1 FOR A FILE TO PURGE          
         B     GCI1                                                             
*                                                                               
GCIH     MVC   CIADDR(2),STCIAS    USE FIRST CI OF PURGED FILE IF PART1         
         TM    FLAG,X'80'                                                       
         BZ    GCIH1                                                            
         MVI   FLAG,X'02'          RESET TO PART2 IF PURGE FOR PART2            
         L     RE,STCILAST                                                      
         MVC   CIADDR(2),0(RE)     USE LAST CI OF PURGED FILE IF PART2          
GCIH1    BRAS  RE,GETXPE                                                        
         MVC   P1,CXPAGE                                                        
*                                                                               
GCII     LR    R5,R4               ADJUST PART1 CI DATA IN BUFFER               
         TM    FLAG,X'01'                                                       
         BZ    GCIJ                                                             
         MVC   PQREPNO,P2+2        SET FILE NUMBER IN FIRST CI                  
         B     GCIK                                                             
         L     RF,P2                                                            
         C     RF,MAXRSN                                                        
         BL    GCII1                                                            
         XC    P1,P1               RETURN EOF IF MAX SEQ NUM EXCEEDED           
         MVI   EOFERRRC,X'08'      SET TOO MANY                                 
         B     GCIX                                                             
GCII1    LA    RF,1(RF)                                                         
         ST    RF,P2                                                            
         MVC   PQREPNO,P2+2        SET FILE NUMBER IN FIRST CI                  
         B     GCIK                                                             
*                                                                               
GCIJ     CLI   PQSEQ,LASTSEQ       ADJUST PART2 CI DATA IN BUFFER               
         BNE   GCIJ2                                                            
         TM    PQTYPE,PQTYXCI+PQTYXTN                                           
         BZ    GCIJ1                                                            
         XC    P1,P1               RETURN EOF IF TOO MANY CI'S                  
         MVI   EOFERRRC,X'04'      SET TOO BIG                                  
         B     GCIX                                                             
GCIJ1    OI    PQTYPE,PQTYXCI+PQTYXTN                                           
         MVI   PQSEQ,FRSTXTNM      SET SEQ NUM IN FIRST XTNSN CI                
         B     GCIJ3                                                            
GCIJ2    CLI   PQSEQ,0             SET SEQ NUM IN FIRST PART2 CI                
         BNE   *+8                                                              
         MVI   PQSEQ,1                                                          
GCIJ3    IC    RF,PQSEQ            BUMP SEQ NUM                                 
         LA    RF,1(RF)                                                         
         STC   RF,PQSEQ                                                         
         MVC   PQCINEXT,SKSTRCI    CHAIN THIS CI TO PREVIOUS CI                 
*                                                                               
GCIK     MVC   CXPAGE(4),P1        SET ADDRESS OF CI TO BE USED                 
         BRAS  RE,GETCAD                                                        
         MVC   SKADDR,CIADDR       SAVE CI DATA IN BUFFER                       
         LA    RF,PQDATA-PQINDEX                                                
         TM    FLAG,X'01'                                                       
         BZ    *+8                                                              
         LA    RF,PQDATA1-PQINDEX                                               
         STH   RF,SKDISP                                                        
         MVC   SKSTRCI,CIADDR                                                   
         MVC   SKENDCI,CIADDRX                                                  
         GOTO1 DATAMGR,DMCB,(X'00',DMWRT),PQFILE,CIADDR,(R4)                    
*                                                                               
         MVC   P3+1(1),8(R1)       SAVE RESULT OF WRITE CI                      
*                                                                               
GCIL     MVC   FUL,CXADDR          UPDATE INDEX PAGE                            
         BRAS  RE,GETXAD                                                        
         CLC   FUL,CXADDR          TEST IF STILL IN CXREC                       
         BE    GCIL1                                                            
         GOTO1 DATAMGR,DMCB,(X'80',DMREAD),PQFILE,CXADDR,CXREC                  
         CLI   8(R1),0                                                          
         BNE   GCIERR                                                           
GCIL1    LH    RF,CXENTRY          SET INDEX ENTRY FROM RECORD                  
         MH    RF,CINDXLN                                                       
         ST    RF,DMCB+16          SET ENTRY DISP IN DMCB+16                    
         LA    RF,CXREC(RF)                                                     
         TM    SKACTRL,X'10'       TEST IF EXTENDING EXISTING REPORT            
         BO    GCIL3               YES INDEX ENTRY ALREADY EXISTS               
         MVC   0(L'PQINDEX,RF),PQINDEX                                          
*                                                                               
GCIL2    CLI   P3+1,0              FIX INDEX ENTRY IF BAD CI WRITE              
         BE    GCIL3                                                            
         MVI   PQSTAT-PQINDEX(RF),PQSTHO                                        
         MVI   PQATTB-PQINDEX(RF),PQATSHIT                                      
         MVC   PQAGERD-PQINDEX(3,RF),=X'FF9F8F' SET 31DEC27 RETAIN              
         B     GCIL4                                                            
*                                                                               
GCIL3    MVI   PQSTAT-PQINDEX(RF),PQSTTE     SET INDEX TEMP CREATE              
         MVC   PQAGEDD-PQINDEX(4,RF),CPUASID SET INDEX CPU/ASID                 
         MVI   PQAGERT-PQINDEX(RF),X'FF'     SET INDEX TIME TO FF               
*                                                                               
GCIL4    GOTO1 DATAMGR,DMCB,(X'00',DMWRT),PQFILE,CXADDR,CXREC                   
         CLI   8(R1),0                                                          
         BNE   GCIERR                                                           
         CLI   P3+1,0              EXIT IF GOOD CI FOUND                        
         BE    GCIX                                                             
         SR    RF,RF                                                            
         IC    RF,P3+2             GET RETRY ERROR COUNT                        
         LA    RF,1(RF)                                                         
         STC   RF,P3+2                                                          
         CHI   RF,16                                                            
         BNH   GCIRETRY            BACK FOR ANOTHER GO                          
*                                                                               
GCIERR   MVC   P1,FFS              RETURN DISK ERROR                            
*                                                                               
GCIX     BRAS  RE,PQUNLK           UNLOCK PQFILE                                
         BRAS  RE,RLSEPQ                                                        
         XIT1                                                                   
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
* SET REPORT IN ERROR TABLE. TABLE HAS HEADER PLUS ENTRIES.                     
* HEADER CL8=EYECATCHER,XL2=ENTRYSIZE,XL2=ENTRYMAX,XL2=ENTRYCOUNT               
* ENTRY  XL1=ACTION,XL1=FLAG,XL1=N/D,CL1=PRTQID,XL4=CIADDR                      
*        XL7=REPKEY,XL1=REPSEQ,XL7=CIKEY,XL1=CISEQ                              
*                                                                               
LOGERR   NTR1  BASE=*                                                           
         XC    DUB(16),DUB                                                      
         TIME  DEC,DUB,LINKAGE=SYSTEM                                           
         MVC   PRTQERRT,DUB                                                     
         MVC   LOGMSG1+10(1),PQFILE+4                                           
         ICM   RF,15,=V(HEXOUT)                                                 
         BZ    LOGERR0                                                          
         LA    R1,LOGPL                                                         
         GOTO1 (RF),(R1),PRTQERRT+3,LOGMSG1+12,3,=C'TOG'                        
         GOTO1 (RF),(R1),CXADDR,LOGMSG1+19,4,=C'TOG'                            
         LH    RE,CXENTRY                                                       
         MH    RE,CINDXLN                                                       
         CVD   RE,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  LOGMSG1+28(5),DUB                                                
         GOTO1 (RF),(R1),PQKEY,LOGMSG1+34,10,=C'TOG'                            
         GOTO1 (RF),(R1),CIADDR,LOGMSG2+19,4,=C'TOG'                            
         GOTO1 (RF),(R1),SVDATA,LOGMSG2+34,10,=C'TOG'                           
*                                                                               
LOGERR0  LA    RF,PRTQERRS         RF=A(PRTQERRS TABLE)                         
         LH    R1,14(RF)           BUMP TOTAL OF ERRORS                         
         LA    R1,1(R1)                                                         
         STH   R1,14(RF)                                                        
         CLI   STCTRL,X'53'        TEST IF PURGING MULTI CI REPORT              
         BNE   LOGERR1                                                          
         MVC   LOGAVLX,AVLXNDX     SAVE INDEX DATA OF BAD REPORT                
         MVC   LOGCBAD,BADCNDX                                                  
         MVC   LOGXBAD,BADXNDX                                                  
*                                                                               
LOGERR1  CLI   STFLAG,X'8A'        TYPE#1 ERROR                                 
         BNE   LOGERR2                                                          
         LH    R1,16(RF)           BUMP TYPE#1 COUNT                            
         LA    R1,1(R1)                                                         
         STH   R1,16(RF)                                                        
         MVI   LOGMSG1+6,C'1'                                                   
         MVI   LOGMSG2+6,C'1'                                                   
         LA    RF,PRTQERRS                                                      
         CLC   16(2,RF),=H'4'      ONLY WTO A FEW OF THESE                      
         BH    LOGERR1B                                                         
LOGERR1A LA    R6,LOGMSG1L                                                      
         WTO   TEXT=(R6)                                                        
         LA    R6,LOGMSG2L                                                      
         WTO   TEXT=(R6)                                                        
LOGERR1B LA    RF,PRTQERRS                                                      
         CLC   16(2,RF),=H'4'      ONLY LOG A FEW OF THESE                      
         BH    LOGERRX                                                          
         B     LOGERR4                                                          
*                                                                               
LOGERR2  CLI   STACTN,X'27'        TYPE#2 ERROR                                 
         BNE   LOGERR3                                                          
         MVI   LOGMSG1+6,C'2'                                                   
         MVI   LOGMSG2+6,C'2'                                                   
         OC    PQKEY(7),PQKEY                                                   
         BNZ   LOGERR3                                                          
         CLI   PQSEQ,0                                                          
         BNE   LOGERR3                                                          
         OC    SVKEY(7),SVKEY                                                   
         BNZ   LOGERR3                                                          
         CLI   SVSEQ,1                                                          
         BNE   LOGERR3                                                          
         LH    R1,18(RF)           BUMP TYPE#2 COUNT                            
         LA    R1,1(R1)                                                         
         STH   R1,18(RF)                                                        
         CHI   R1,2                ONLY LOG A FEW OF THESE                      
         BH    LOGERRX                                                          
         B     LOGERR4                                                          
*                                                                               
LOGERR3  CLI   STACTN,C'I'         ERROR DURING PURGING TEMPS ON INIT           
         BNE   LOGERR4                                                          
         LH    R1,20(RF)           BUMP TYPE#0 COUNT                            
         LA    R1,1(R1)                                                         
         STH   R1,20(RF)                                                        
         MVI   LOGMSG1+6,C'0'                                                   
         MVI   LOGMSG2+6,C'0'                                                   
         LA    RF,PRTQERRS                                                      
         CLC   20(2,RF),=H'1'      ONLY WTO ONE OF THESE                        
         BH    LOGERR3B                                                         
LOGERR3A LA    R6,LOGMSG1L                                                      
         WTO   TEXT=(R6)                                                        
         LA    R6,LOGMSG2L                                                      
         WTO   TEXT=(R6)                                                        
LOGERR3B LA    RF,PRTQERRS                                                      
         CLC   20(2,RF),=H'1'      ONLY LOG ONE OF THESE                        
         BH    LOGERRX                                                          
         B     LOGERR4                                                          
*                                                                               
LOGERR4  LH    R1,12(RF)           GET ENTRY COUNT                              
         CH    R1,10(RF)                                                        
         BNL   LOGERRX             TABLE IS FULL                                
*                                                                               
         LA    R1,1(R1)            BUMP ENTRY COUNT                             
         STH   R1,12(RF)                                                        
         MH    R1,=H'24'                                                        
         AR    RF,R1               RF=A(ENTRY)                                  
         CLC   04(04,RF),PRTQERRX                                               
         BE    LOGERRX             INSURANCE                                    
*                                                                               
         MVC   00(01,RF),STACTN    STATUS ACTION                                
         MVC   01(01,RF),STFLAG    STATUS FLAG (08=FORMAT ERROR)                
         MVC   02(01,RF),STCTRL    STATUS CTRL (TYPE OF STATUS CALL)            
         MVC   03(01,RF),PQFILE+4  PRTQ FILE ID                                 
         MVC   04(04,RF),CIADDR    CI IN ERROR DISK ADDR                        
         MVC   08(07,RF),PQKEY     CI IN ERROR KEY                              
         MVC   15(01,RF),PQSEQ     CI IN ERROR CI SEQ                           
         MVC   16(07,RF),SVKEY     REPORT KEY                                   
         MVC   23(01,RF),SVSEQ     REPORT CI SEQ                                
*                                                                               
LOGERRX  XIT1                                                                   
*                                                                               
LOGPL    DC    6F'0'                                                            
LOGMSG1L DC    H'54'                                                            
LOGMSG1  DC    CL54'PQERR#1 PQ1 THMIJU CXADDR..+HHHHH XXXX....'                 
LOGMSG2L DC    H'54'                                                            
LOGMSG2  DC    CL54'PQERR#1            CIADDR..       XXXX....'                 
*                                                                               
         DS    0D                                                               
         DC    C'LOGAVLX '                                                      
LOGAVLX  DC    XL24'00'            INDEX ENTRY TO BE PURGED                     
         DC    C'LOGCBAD '                                                      
LOGCBAD  DC    XL24'00'            BAD INDEX OF CI TO BE PURGED                 
         DC    C'LOGXBAD '                                                      
LOGXBAD  DC    XL24'00'            BAD INDEX OF INDEX TO BE PURGED              
*                                                                               
         LTORG                                                                  
*                                                                               
* HEADER CL8=EYECATCHER,XL2=ENTRYSIZE,XL2=ENTRYMAX,XL2=ENTRYCOUNT               
*            XL2=ERRORTOTAL,XL2=TYPE1TOT,XL2=TYPE2TOT                           
* ENTRY  XL1=ACTION,XL1=FLAG,XL1=N/D,CL1=PRTQID,XL4=CIADDR                      
*        XL7=REPKEY,XL1=REPSEQ,XL7=CIKEY,XL1=CISEQ                              
PRTQERRT DC    D'0'                TIME OF ERROR P'HHMMSSTHMIJU0000'            
PRTQERRS DS    0XL24'00'                                                        
         DC    C'PRTQERRS',H'24',H'20',H'00',H'0',XL8'00'                       
         DC    20XL24'00'                                                       
PRTQERRX DC    8X'FF'                                                           
         EJECT                                                                  
* GET DATE AND SAVE IN SEVERAL FORMATS                                          
*                                                                               
GETDATE  NTR1  BASE=*                                                           
         CLI   OFFLINE,C'Y'                                                     
         BNE   GETDATE1                                                         
         L     RF,VSSB                                                          
         LTR   RF,RF                                                            
         BZ    GETDATE1                                                         
         CLI   2(RF),X'FF'         TEST IF OFFLINE EXTENDED SSB                 
         BNE   GETDATE1                                                         
         TM    5(RF),X'80'         IF SO TEST IF DATE PASSED                    
         BZ    GETDATE1                                                         
         MVC   DATEE(6),8(RF)      EXTRACT C'YYMMDD' FROM OFFLINE SSB           
         MVC   DATEE+6(2),SPACES                                                
         B     GETDATE2                                                         
GETDATE1 DATE  DATEE               DATEE=C'YYMMDDCC'                            
*                                                                               
GETDATE2 MVC   DATEB(2),DATEE      TEST CHANGE IN CENTURY                       
         SR    R1,R1                                                            
         CLI   DATEB,C'9'                                                       
         BNH   GETDATE3                                                         
         IC    R1,DATEB                                                         
         AHI   R1,-10                                                           
         STC   R1,DATEB                                                         
         LA    R1,100                                                           
GETDATE3 PACK  DUB,DATEB+0(2)                                                   
         CVB   R0,DUB                                                           
         AR    R0,R1                                                            
         STC   R0,DATEB+0                                                       
         PACK  DUB,DATEE+2(2)                                                   
         CVB   R0,DUB                                                           
         STC   R0,DATEB+1                                                       
         PACK  DUB,DATEE+4(2)                                                   
         CVB   R0,DUB                                                           
         STC   R0,DATEB+2          DATEB=X'YYMMDD'                              
         ICM   R1,15,DATEB                                                      
         SLDL  R0,8                                                             
         SLL   R1,4                                                             
         SLDL  R0,4                                                             
         SLL   R1,3                                                             
         SLDL  R0,5                                                             
         STCM  R0,3,DATEC          DATEC=B'YYYYYYYMMMMDDDDD'                    
         XIT1                                                                   
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
* GET TIME AND SAVE IN SEVERAL FORMATS                                          
*                                                                               
GETTIME  NTR1  BASE=*                                                           
         TIME  TU                  R0=TIME IN 1/38400 SECS                      
         SRDL  R0,32                                                            
         D     R0,=F'38400'        R1=TIME IN SECONDS                           
         LR    RF,R1                                                            
         MH    R1,=H'3'                                                         
         SRL   R1,2                R1=(SECS*3)/4                                
         STH   R1,TIMEC            TIMEC=TIME IN SPECIAL UNITS                  
         LR    R1,RF                                                            
         SR    R0,R0                                                            
         D     R0,=F'60'           R1=BINARY MINUTES                            
         SR    RE,RE                                                            
         LR    RF,R1                                                            
         D     RE,=F'10'                                                        
         STC   RF,TIMEI            TIMEI=SINGLE BYTE 10MIN INCREMENT            
         SR    R0,R0                                                            
         D     R0,=F'60'           R0=MINS,R1=HOURS                             
         STC   R1,TIMEB                                                         
         STC   R0,TIMEB+1          TIMEB=B'HHHHHHHHMMMMMMMM'                    
         L     RE,SAVRE                                                         
         XIT1                                                                   
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
* PASS   DUB+0(3)  DATEC/TIMEI. TIMEI IGNORED,USE TIMEB (TIME NOW)              
* PASS   DUB+3(2)  RETENTION PERIOD IN HOURS (X'FFFF'=PERMANENT)                
* PASS   DUB+5(1)  SET TO X'FF' TO GO BACKWARDS                                 
*                                                                               
* RETURN DUB1+0(3) NEW DATEC/TIMEI                                              
* RETURN DUB1+6(2) NEW TIME IN (SEC*3)/4 UNITS                                  
*                                                                               
GETRETN  NTR1  BASE=*                                                           
         XC    DUB1,DUB1           INITIALISE RETURN VALUES                     
         XC    GRCB,GRCB                                                        
         OI    GRFLAG,X'20'        SET RETURN COUNTRY CODE USED                 
         MVC   GRHOURS,DUB+3       SET BINARY HOURS RETAIN PERIOD               
         SR    R0,R0               SET BINARY YEAR/MONTH/DAY                    
         ICM   R0,3,DUB                                                         
         SRDL  R0,5                                                             
         SRL   R1,27                                                            
         STC   R1,GRIDAY                                                        
         SRDL  R0,4                                                             
         SRL   R1,28                                                            
         STC   R1,GRIMONTH                                                      
         STC   R0,GRIYEAR                                                       
         MVC   GRIHOUR(2),TIMEB    SET TIME NOW BINARY HOURS/MINS               
         CLI   DUB+5,X'FF'                                                      
         BNE   *+8                                                              
         OI    GRFLAG,X'C4'        SET GO BACK AND IGNORE WEEKENDS              
         LA    R1,GRCB                                                          
         ICM   RF,15,=V(GETRET)    V(GETRET) MUST BE RESOLVED                   
         BNZ   *+6                                                              
         DC    H'0'                                                             
         BASR  RE,RF                                                            
*                                                                               
GRETN1   MVC   DUB2+4(3),GROYEAR   RETURN YEAR/MONTH/DAY                        
         L     R1,DUB2+4                                                        
         SLDL  R0,8                                                             
         SLL   R1,4                                                             
         SLDL  R0,4                                                             
         SLL   R1,3                                                             
         SLDL  R0,5                                                             
         STH   R0,DUB1             RETURN NEW DATE IN BINARY COMPRSD            
*                                                                               
GRETN2   SR    R0,R0               CONVERT TIME TO PQ TIME UNITS                
         IC    R0,GROMIN                                                        
         SR    R1,R1                                                            
         IC    R1,GROHOUR                                                       
         MH    R1,=H'60'                                                        
         AR    R1,R0               R1=BINARY MINUTES                            
         LR    RF,R1               RF=BINARY MINUTES                            
         SR    R0,R0                                                            
         D     R0,=F'10'                                                        
         LTR   R0,R0                                                            
         BZ    *+8                                                              
         AHI   R1,1                                                             
         STC   R1,DUB1+2           RETURN NEW TIME IN 10MIN INCREMENTS          
         MHI   RF,60*3             RF=SECONDS*3                                 
         SRL   RF,2                                                             
         STH   RF,DUB1+6           RETURN NEW TIME IN (SECS*3)/4                
*                                                                               
GRETNX   XIT1                                                                   
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
* COMPRESS PRINT DATA IN BUFFER                                                 
*                                                                               
COMPRESS NTR1  BASE=*                                                           
         L     R6,FUL              R6=A(START OF STRING)                        
         LH    R1,0(R6)                                                         
         ST    R1,FUL1             RETURN OLD TOTAL LENGTH IN FUL1              
         LA    R7,0(R1,R6)         R7=A(END OF DATA)                            
         LA    R6,2(R6)                                                         
         BCTR  R1,0                ADJUST FOR 2 BYTE REC LEN                    
         BCTR  R1,0                                                             
         TM    PQLINET,PQLTCC                                                   
         BZ    *+10                                                             
         LA    R6,1(R6)                                                         
         BCTR  R1,0                ADJUST FOR 1 BYTE CONTROL                    
         LR    R5,R6               R5=A(START OF DATA)                          
*                                                                               
COMP1    CR    R6,R7               SEARCH FOR STRING OF MIN LEN                 
         BE    COMPA                                                            
         CLC   0(L'COMPMIN-1,R6),1(R6)                                          
         BE    COMP3                                                            
COMP2    LA    R6,1(R6)                                                         
         B     COMP1                                                            
*                                                                               
COMP3    CLC   0(1,R6),COMPMAX     IGNORE STRING IF CHR CANT COMPRESS           
         BL    *+14                                                             
         CLC   0(1,R6),COMPMAX+L'COMPMAX-1                                      
         BNH   COMP2                                                            
         LA    R4,L'COMPMIN(R6)    R4=A(END OF STRING + 1)                      
         LA    R0,L'COMPMAX-1      R0=MAX LEN OF STRING                         
         CLC   0(1,R4),0(R6)                                                    
         BNE   COMP4                                                            
         LA    R4,1(R4)                                                         
         BCT   R0,*-14                                                          
*                                                                               
COMP4    LR    RF,R4               RF=LEN OF STRING                             
         SR    RF,R6                                                            
         IC    R0,COMPMIN(RF)      R0=LEN OF STRING CODE CHR                    
         MVI   0(R6),ESC                                                        
         CLI   1(R6),C' '          ESC/LEN FOR BLANK STRING                     
         BE    *+8                                                              
         LA    R6,1(R6)            ESC/CHR/LEN FOR NON BLANK CHR STRING         
         STC   R0,1(R6)                                                         
         LA    R6,2(R6)                                                         
*                                                                               
COMP5    LR    RF,R4               RF=NUM OF CHRS TO DELETE                     
         SR    RF,R6                                                            
         LA    RE,0(R1,R5)         RE=NUM OF CHRS TO SHIFT LEFT                 
         SR    RE,R4                                                            
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R6),0(R4)                                                    
         SR    R1,RF               DECR TOTAL DATA LENGTH                       
         B     COMP1                                                            
*                                                                               
COMPA    LA    R1,2(R1)            SET NEW TOTAL LENGTH                         
         TM    PQLINET,PQLTCC                                                   
         BZ    *+8                                                              
         LA    R1,1(R1)            ADJUST FOR CONTROL CHR                       
         L     RE,FUL                                                           
         STH   R1,0(RE)                                                         
         ST    R1,FUL2             RETURN NEW TOTAL LENGTH IN FUL2              
         AR    RE,R1                                                            
         L     RF,FUL1                                                          
         SR    RF,R1               SET LENGTH TO CLEAR                          
         BNP   COMPX                                                            
         XCEF                                                                   
*                                                                               
COMPX    XIT1                                                                   
*                                                                               
COMPMIN  DC    CL04' '                                                          
COMPMAX  DC    CL36'ABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789'                       
ESC      EQU   X'27'                                                            
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
* SUBROUTINE TO INITIALISE PRTQUE FILE                                          
*                                                                               
IPQ      NTR1  BASE=*                                                           
         CLI   USERINUM,0          PRTQ FILE MUST BE DEFINED                    
         BNE   *+6                                                              
         DC    H'0'                                                             
         MVC   CFPQID,USERPRTQ     SET PRTQ ALPHA ID FOR ENQUEUE                
         MVI   INIFLAGS,0                                                       
*                                                                               
         XC    P1(24),P1           BUILD DADDS PARM LIST TO OPEN FILE           
         MVC   P1,VDAOPEN                                                       
         LA    RE,CXREC                                                         
         ST    RE,P2                                                            
         MVC   P4,VPQFILE          SET DTF ADDR                                 
         LA    RE,CXADDR                                                        
         ST    RE,P5                                                            
         CLI   OFFLINE,C'Y'        OPEN PQFILE IF OFFLINE                       
         BNE   IPQ0                                                             
         L     RE,VPQFILE                                                       
         TM    36(RE),X'20'        TEST IF FILE ALREADY OPEN                    
         BO    IPQ0X                                                            
         GOTO1 VDADDS,P1                                                        
*                                                                               
IPQ0     MVC   P1,VDACPUID         READ RECORD ZERO TO GET CPU ID               
         GOTO1 VDADDS,P1                                                        
         MVC   THISSTMP,P3+2       STAMP IS CL1=CPUID,CL1=DSPACE                
         MVI   DSPACEV,C' '                                                     
         CLI   OFFLINE,C'Y'        GET DSPACE=Z VALUE FROM OFFLINE SSB          
         BNE   IPQ0A                                                            
         L     RE,VSSB                                                          
         CLI   SSOXTND-SSOOFF(RE),X'FF'                                         
         BNE   *+10                                                             
         MVC   DSPACEV,SSODSPAC-SSOOFF(RE)                                      
*                                                                               
IPQ0A    TM    P3+1,X'80'          TEST IF CPUID MATCHES                        
         BZ    IPQ0B                                                            
         MVC   INIMSG1+10(1),PQFILE+4                                           
         MVC   INIMSG1+12(2),THISSTMP                                           
         LA    R6,INIMSG1L                                                      
         WTO   TEXT=(R6)                                                        
         OI    INIFLAGS,X'01'      THIS PQ STAMP DONT MATCH CPUID               
*                                                                               
IPQ0B    CLI   DSPACEV,C' '        TEST IF DSPACE=CARD MATCHES STAMP            
         BNH   IPQ0C                                                            
         CLI   OFFLINE,C'Y'        ONLY IF OFFLINE                              
         BNE   IPQ0C                                                            
         CLC   DSPACEV,THISSTMP+1                                               
         BE    IPQ0C                                                            
         MVC   INIMSG2+10(1),PQFILE+4                                           
         MVC   INIMSG2+12(2),THISSTMP                                           
         MVC   INIMSG2+39(1),DSPACEV                                            
         LA    R6,INIMSG2L                                                      
         WTO   TEXT=(R6)                                                        
         OI    INIFLAGS,X'02'      THIS PQ STAMP DONT MATCH DSPACE=X            
*                                                                               
IPQ0C    SR    RE,RE               INDEX INTO STAMP TABLE                       
         IC    RE,USERINUM                                                      
         CHI   RE,16                                                            
         BH    IPQ0X                                                            
         SLL   RE,1                                                             
         LA    RE,PRTQSTMP(RE)                                                  
         MVC   0(2,RE),THISSTMP    SAVE THE STAMP OF THIS PRTQ                  
*                                                                               
IPQ0D    LA    RE,PRTQSTMP         POINT TO FIRST ENTRY                         
         OC    0(2,RE),0(RE)                                                    
         BNZ   *+10                                                             
         MVC   0(2,RE),THISSTMP    SAVE THE CURRENT STAMP                       
         MVC   LASTSTMP,0(RE)                                                   
         CLC   THISSTMP,LASTSTMP   TEST THE PRTQ STAMPS MATCH                   
         BE    IPQ0E                                                            
         MVC   INIMSG3+10(1),PQFILE+4                                           
         MVC   INIMSG3+12(2),THISSTMP                                           
         MVC   INIMSG3+37(2),LASTSTMP                                           
         LA    R6,INIMSG3L                                                      
         WTO   TEXT=(R6)                                                        
         OI    INIFLAGS,X'04'      THIS PQ STAMP DONT MATCH LAST PQ             
*                                                                               
IPQ0E    LA    RE,PRTQSTMP         POINT TO DDSQDQ STAMP                        
         AHI   RE,-2                                                            
         OC    0(2,RE),0(RE)                                                    
         BZ    IPQ0X                                                            
         MVC   QDQFSTMP,0(RE)                                                   
         CLC   QDQFSTMP,THISSTMP   TEST THE PRTQ STAMP WITH DDSQDQ              
         BE    IPQ0X                                                            
         MVC   INIMSG4+10(1),PQFILE+4                                           
         MVC   INIMSG4+12(2),THISSTMP                                           
         MVC   INIMSG4+39(2),QDQFSTMP                                           
         LA    R6,INIMSG4L                                                      
         WTO   TEXT=(R6)                                                        
         OI    INIFLAGS,X'08'      THIS PQ STAMP DONT MATCH DDSQDQ              
*                                                                               
IPQ0X    XC    P3(4),P3            CLEAR CPUID/DSPACE DATA                      
*                                                                               
IPQ1     MVC   P1,VRDID            READ FIRST RECORD OF PRTQ FILE               
         MVC   CXADDR,=X'00010100'                                              
         GOTO1 VDADDS,P1                                                        
         OC    P3(2),P3                                                         
         BNZ   IPQERR                                                           
         L     RE,VPQTAB                                                        
         MVC   0(20,RE),CXREC      SET PART1 INDEX DATA                         
         MVC   20(20,RE),PART2IND                                               
         TM    0(RE),X'80'                                                      
         BZ    *+14                                                             
         MVC   20(20,RE),CXREC+L'PQINDEX SET PART2 INDEX DATA                   
         NI    0(RE),X'7F'                                                      
         MVC   14(1,RE),USERINUM   SET INTERNAL PRTQ FILE NUM                   
         MVC   15(5,RE),USERPRTQ   SET ALPHA PRTQ FILE NAME                     
         MVC   CIDATA,0(RE)                                                     
*                                                                               
IPQ2     L     RE,VPQFILE          SET F/L RECORD SIZE IN PQFILE DTF            
         LA    RE,52(RE)                                                        
         MVC   0(2,RE),CIBLKLN                                                  
         OI    0(RE),X'80'                                                      
         CLC   CIBLKLN,PQBLKLN     TEST ACTUAL VRS ASSUMED BLOCK LEN            
         BNH   IPQ2A                                                            
         DC    H'0'                I DONT THINK I CAN COPE WITH THIS            
IPQ2A    CLI   INIFLAGS,0                                                       
         BE    IPQ3                DONT DO ANYTHING IF ERRORS                   
*                                                                               
IPQ3     CLI   SKACTN,ACTNCLE      TEST IF CALLED TO CLEANUP TEMP CIS           
         BNE   IPQY                                                             
         NI    INIFLAGS,255-X'80'                                               
         CLC   UKUSRINF(7),=C'WRITE=NO'                                         
         BNE   *+8                                                              
         OI    INIFLAGS,X'80'      SET WRITE=NO                                 
         XC    UKUSRINF,UKUSRINF   CLEAR RETURN VALUES                          
         L     RF,=A(GETDATE)      GET DATE AND TIME NOW                        
         BASR  RE,RF                                                            
         L     RF,=A(GETTIME)                                                   
         BASR  RE,RF                                                            
         MVC   DUB+0(3),DATEC      SET DATE/TIME NOW (DATEC/TIMEI)              
         SR    R0,R0                                                            
         ICM   R0,3,UKINFO         CALLER SETS NUM OF HOURS TO GO BACK          
         BNZ   *+8                                                              
         LHI   R0,24               DEFAULT IS 24 IF NOT SET                     
         STCM  R0,3,DUB+3                                                       
         MVI   DUB+5,X'FF'         SET NEGATIVE FLAG                            
         L     RF,=A(GETRETN)                                                   
         BASR  RE,RF                                                            
         MVC   DUB3+0(2),DUB1      DUB3+0(4)=CLEANUP DATE/TIME                  
         MVC   DUB3+2(2),DUB1+6                                                 
         MVC   DUB3+4(2),DATEC     DUB3+4(4)=NOW DATE/TIME                      
         MVC   DUB3+6(2),TIMEC                                                  
         CLC   DUB3+0(4),DUB3+4                                                 
         BNL   IPQY                EXIT IF RETURN DATE/TIME NOT GOOD            
*                                                                               
         BRAS  RE,PQLOCK           LOCK PQFILE                                  
         MVC   CPUASID,CIP3                                                     
         GOTO1 DATAMGR,DMCB,(X'80',DMREAD),CFPQID,CXADDR,CXREC                  
*                                                                               
IPQ4     MVI   FLAG,0              POINT TO FIRST INDEX ENTRY                   
         BRAS  RE,CXLOOPI                                                       
         USING PQRECD,R5                                                        
         B     IPQ8                                                             
*                                                                               
IPQ6     TM    FLAG,X'01'          WRITE BACK PAGE IF UPDATED                   
         BZ    IPQ6A                                                            
         TM    INIFLAGS,X'80'      TEST WRITE=NO                                
         BO    IPQ6A                                                            
         GOTO1 DATAMGR,DMCB,(X'00',DMWRT)                                       
         CLI   8(R1),0                                                          
         BNE   IPQERR                                                           
IPQ6A    BRAS  RE,GETXAD           READ IN NEXT PAGE                            
         GOTO1 DATAMGR,DMCB,(X'80',DMREAD)                                      
         CLI   8(R1),0                                                          
         BNE   IPQERR                                                           
         NI    FLAG,255-X'01'                                                   
*                                                                               
IPQ8     TM    FLAG,X'80'          TEST IF ALREADY DONE THIS                    
         BO    IPQ8X                                                            
         LH    RE,CJPAGE           TEST FOR END OF INDEX MISSING                
         LH    RF,CJENTRY                                                       
         AHI   RF,-1                                                            
         BNM   *+14                                                             
         AHI   RE,-1                                                            
         LH    RF,CIENTRYS                                                      
         BCTR  RF,0                                                             
         CH    RE,CXPAGE           IS THIS PAGE END OF INDEX PAGE               
         BNE   IPQ8X               NO                                           
         CH    RF,CXENTRY          IS THIS ENTRY END OF INDEX ENTRY             
         BNE   IPQ8X               NO                                           
         MVC   PQINDEX,=32X'FF'    PUT BACK MISSING END OF INDEX                
         MH    RF,CINDXLN                                                       
         ST    RF,DMCB+16                                                       
         TM    INIFLAGS,X'80'      TEST WRITE=NO                                
         BO    IPQ8A                                                            
         GOTO1 DATAMGR,DMCB,(X'00',DMWRT)                                       
         CLI   8(R1),0                                                          
         BNE   IPQERR                                                           
IPQ8A    OI    FLAG,X'80'+X'10'    SET END OF INDEX AND REPAIRED FLAGS          
         B     IPQB                                                             
IPQ8X    EQU   *                                                                
*                                                                               
IPQ9     TM    PQSTAT,PQSTTE       TEST IF TEMPORARY INDEX ENTRY                
         BZ    IPQA                                                             
         CLI   PQAGERT,X'FF'       TEST IF CPU/ASID IN INDEX ENTRY              
         BNE   IPQA                                                             
         MVC   DUB3+4(2),PQAGELD   DUB3+4(4)=CREATION DATE/TIME                 
         MVC   DUB3+6(2),PQAGELT                                                
         CLC   DUB3+4(4),DUB3      TEST IF OLDER THAN NN HOURS                  
         BH    IPQA                                                             
*                                                                               
IPQ9A    OI    FLAG,X'40'          SET TEMP INDEX ENTRY PURGED                  
         XC    PQINDEX,PQINDEX     PURGE TEMPORARY INDEX ENTRY                  
         SR    R0,R0                                                            
IPQ9C    TM    FLAG,X'80'          TEST IF PART1 OR PART2 INDEX ENTRY           
         BO    IPQ9D                                                            
         ICM   R0,3,UKUSRINF+0     UPDATE PART1 CI PURGE COUNT                  
         AHI   R0,1                                                             
         STCM  R0,3,UKUSRINF+0                                                  
         B     IPQ9E                                                            
IPQ9D    ICM   R0,3,UKUSRINF+2     UPDATE PART2 CI PURGE COUNT                  
         AHI   R0,1                                                             
         STCM  R0,3,UKUSRINF+2                                                  
IPQ9E    TM    CIRSNF,CIXDSPQ      TEST IF INDEX RECS IN DATA SPACE             
         BO    *+12                YES                                          
         OI    FLAG,X'01'          NO SET INDEX PAGE UPDATED                    
         B     IPQA                                                             
         LH    RF,CXENTRY          SET DISPLACEMENT TO ENTRY IN DMCB            
         MH    RF,CINDXLN                                                       
         ST    RF,DMCB+16                                                       
         TM    INIFLAGS,X'80'      TEST WRITE=NO                                
         BO    IPQA                                                             
         GOTO1 DATAMGR,DMCB,(X'00',DMWRT)                                       
         CLI   8(R1),0                                                          
         BNE   IPQERR                                                           
*                                                                               
IPQA     BRAS  RE,CXLOOPX          BUMP TO NEXT INDEX ENTRY                     
         B     IPQ8                                                             
         B     IPQ6                END OF INDEX PAGE                            
IPQB     OI    FLAG,X'80'          SET END OF PART ONE INDEX                    
         MVC   FUL(2),CXPAGE       SAVE LAST INDEX PAGE NUMBER                  
         CLC   CXPAGE(4),CJPAGE                                                 
         BH    IPQC                EXIT IF END OF PART2 INDEX                   
         OC    CJCITOT,CJCITOT                                                  
         BZ    IPQC                EXIT IF NO PART2 INDEX                       
         BRAS  RE,CXLOOPJ          POSN TO FIRST PART2 INDEX ENTRY              
         CLC   CXPAGE,FUL                                                       
         BE    IPQ8                STARTS IN SAME PAGE AS END OF PART1          
         B     IPQ6                                                             
*                                                                               
IPQC     TM    FLAG,X'01'          WRITE BACK LAST PAGE IF UPDATED              
         BZ    IPQD                                                             
         TM    INIFLAGS,X'80'      TEST WRITE=NO                                
         BO    IPQD                                                             
         GOTO1 DATAMGR,DMCB,(X'00',DMWRT)                                       
         CLI   8(R1),0                                                          
         BNE   IPQERR                                                           
*                                                                               
IPQD     TM    FLAG,X'10'          TEST FOR END OF INDEX REPAIR                 
         BZ    IPQX                                                             
         XC    STPARM,STPARM       SET VALUES TO LOG CLEANUP EVENT              
         XC    SVDATA,SVDATA                                                    
         MVI   STACTN,C'I'         SET CALL FROM INIT                           
         MVC   SVDATA+0(4),DUB3    DATEC/TIMEC NN HOURS AGO                     
         MVC   SVDATA+4(4),DUB3+4  CREATION DATE/TIME OF TEMP FILE              
         BRAS  RE,LOGERR           LOG THE INIT EVENT                           
         B     IPQX                                                             
*                                                                               
IPQERR   BRAS  RE,RLSEPQ           DISK ERROR ON INITIALISATION                 
         BRAS  RE,PQUNLK                                                        
         MVI   PARM3,X'40'                                                      
         MVC   INIMSG0+10(1),PQFILE+4                                           
         MVC   INIMSG0+12(2),THISSTMP                                           
         LA    R6,INIMSG0L         PQX=YY DISK ERROR DURING INIT                
         WTO   TEXT=(R6)                                                        
         CLI   OFFLINE,C'Y'        EXIT WITH ERROR IF ONLINE                    
         BNE   IPQZ                                                             
         DC    H'0',C'PRTQ INITIALISE ERROR '                                   
*                                                                               
IPQX     BRAS  RE,RLSEPQ           PQFILE INITIALISED OK                        
         BRAS  RE,PQUNLK                                                        
IPQY     MVI   PARM3,0                                                          
*                                                                               
IPQZ     CLI   PARM3,0             EXIT WITH CC EQUAL IF OK                     
         XIT1                                                                   
*                                                                               
PART2IND DC    XL4'00',XL10'7FFF7FFF000000010100',XL6'00'                       
VDADDS   DC    V(DADDS)                                                         
VDAOPEN  DC    V(DAOPEN)                                                        
VRDID    DC    V(RDID)                                                          
VDACPUID DC    V(DACPUID)                                                       
MAXTIMEC DC    AL4(64800)          ((24*60*60)*3)/4                             
*                                                                               
         LTORG                                                                  
*                                                                               
INIMSG0L DC    H'54'                                                            
INIMSG0  DC    CL54'PQERR#S PQX=YY DISK ERROR DURING INIT'                      
INIMSG1L DC    H'54'                                                            
INIMSG1  DC    CL54'PQERR#S PQX=YY STAMP DONT MATCH CPUID'                      
INIMSG2L DC    H'54'                                                            
INIMSG2  DC    CL54'PQERR#S PQX=YY STAMP DONT MATCH DSPACE=Z '                  
INIMSG3L DC    H'54'                                                            
INIMSG3  DC    CL54'PQERR#S PQX=YY STAMP DONT MATCH PRTQ=ZZ'                    
INIMSG4L DC    H'54'                                                            
INIMSG4  DC    CL54'PQERR#S PQX=YY STAMP DONT MATCH DDSQDQ=ZZ'                  
*                                                                               
THISSTMP DC    CL2' '                                                           
LASTSTMP DC    CL2' '                                                           
QDQFSTMP DC    CL2' '                                                           
DSPACEV  DC    CL1' '                                                           
INIFLAGS DC    XL1'00'                                                          
         EJECT                                                                  
WRKD     DSECT                                                                  
USEREYE  DS    CL8                 USER INFO FIRST IN W/S EYECATCHER            
USERINFO DS    0XL16               USER INFO FOR WHICH PRTQ FILE                
USERIDNO DS    H                   USER ID NUM                                  
USERFLAG DS    XL1                 USER FLAG 80=SET BY CALLER                   
USERINUM DS    X                   USER PRTQ INTERNAL FILE NUMBER               
USERPRTQ DS    CL8                 USER PRTQ NAME FOR DATAMGR                   
USERXNUM DS    X                   USER PRTQ EXTERNAL FILE NUMBER               
USERADTF DS    AL3                 USER PRTQ A(DTF)                             
*                                                                               
REPTINFO DS    XL8                 REPORT INFO USERID/REQPORTID/SEQNUM          
*                                                                               
DUB      DS    D                                                                
DUB1     DS    D                                                                
DUB2     DS    D                                                                
DUB3     DS    D                                                                
FUL      DS    F                                                                
FUL1     DS    F                                                                
FUL2     DS    F                                                                
SAVR2    DS    F                                                                
SAVRE    DS    F                                                                
SAVREWPQ DS    F                                                                
SAVRESPQ DS    F                                                                
PRTQXPEA DS    A                                                                
PRTQXPEV DS    F                                                                
SAVADDR  DS    A                                                                
AUTL     DS    A                                                                
EOFCNT   DS    H                                                                
EOFMAX   DS    H                                                                
EOFBIN   DS    F                                                                
DMCB     DS    6F                                                               
*                                                                               
DATEE    DS    CL8                 EBCDIC DATE C'YYMMDD  '                      
DATEB    DS    XL3                 BINARY DATE X'YMD'                           
DATEC    DS    XL2                 CMPRSD DATE B'YYYYYYYMMMMDDDDD'              
TIMEI    DS    XL1                 BINARY TIME X'I' I=10MIN INTERVAL            
TIMEB    DS    XL2                 BINARY TIME X'HM'                            
TIMEC    DS    XL2                 BINARY TIME CREATED UNITS=SECS*3/4           
         DS    XL2                 N/D                                          
*                                                                               
TEMPWS   DS    0XL28               AVAILABLE AS TEMP W/S                        
PLIST    DS    0XL28               AVAILABLE AS PARAMETER LIST                  
GRCB     DS    0XL16               GET RETAIN CONTROL BLOCK                     
GRRETC   DS    X                   RETURN CODE                                  
GRCTRY   DS    X                   COUNTRY CODE                                 
GRHOURS  DS    XL2                 NUMBER OF HOURS                              
GRFLAG   DS    XL1                 FLAGS 80=BACK,40=NOHOLS,20=RETCTRY           
GRINFO   DS    XL1                 RETURN CODE                                  
GRIYEAR  DS    X                   INPUT YEAR                                   
GRIMONTH DS    X                   INPUT MONTH                                  
GRIDAY   DS    X                   INPUT DAY                                    
GRIHOUR  DS    X                   INPUT HOUR                                   
GRIMIN   DS    X                   INPUT MIN                                    
GROYEAR  DS    X                   OUTPUT YEAR                                  
GROMONTH DS    X                   OUTPUT MONTH                                 
GRODAY   DS    X                   OUTPUT DAY                                   
GROHOUR  DS    X                   OUTPUT HOUR                                  
GROMIN   DS    X                   OUTPUT MIN                                   
         DS    XL12                N/D                                          
*                                                                               
CPUASID  DS    F                   CPUID/ASID                                   
VPQTAB   DS    A                                                                
VPQFILE  DS    A                                                                
PQFILE   DS    CL8                                                              
MYLABEL  DS    CL8                                                              
         DS    C                                                                
SAVECC   DS    X                                                                
PAGELINE DS    X                                                                
*                                                                               
CLOTYP   DS    X                   CLOSE TYPE                                   
CLOTNOR  EQU   X'01'               CLOSE NORMAL (DEFAULT)                       
CLOTPUR  EQU   X'02'               CLOSE AND PURGE REPORT                       
CLOTJOB  EQU   X'04'               CLOSE AND SET JOB JCL                        
CLOTERR  EQU   X'08'               CLOSE AND SET IN ERROR                       
CLOTUSR  EQU   X'10'               CLOSE AND SET USER DATA                      
*                                                                               
CLOERRRC DS    X                   CLO/ERR REASON CODE                          
EOFERRRC DS    X                   EOF ERR REASON CODE                          
         DS    XL2                 N/D                                          
*                                                                               
CLOINF   DS    XL8                                                              
ADDWHY   DS    X                                                                
         DS    XL3                                                              
*                                                                               
PARM1    DS    F                                                                
PARM2    DS    F                                                                
PARM3    DS    F                                                                
PARM4    DS    F                                                                
PARM5    DS    F                                                                
PARM6    DS    F                                                                
*                                                                               
P1       DS    F                                                                
P2       DS    F                                                                
P3       DS    F                                                                
P4       DS    F                                                                
P5       DS    F                                                                
P6       DS    F                                                                
P7       DS    F                                                                
*                                                                               
APARM    DS    F                                                                
HALF     DS    H                                                                
FLAG     DS    X                                                                
FLAG1    DS    X                                                                
SVSEQ    DS    X                                                                
SVTYPE   DS    X                                                                
         DS    XL2                                                              
SVNEXT   DS    F                                                                
*                                                                               
SVINDEX  DS    0CL24               LEN=L'USER-INDEX                             
SVDATA   DS    0CL24               LEN=L'ACTUAL-INDEX                           
SVKEY    DS    0CL7                LEN=L'KEY                                    
SVSRCID  DS    XL2                                                              
SVSUBID  DS    CL3                                                              
SVREPNO  DS    XL2                                                              
         DS    CL17                REST OF PQ INDEX ENTRY                       
SVINFO   DS    CL16                AREA DEFINED IN DMPRTQK                      
*                                                                               
BADCNDX  DS    CL24                BAD CI INDEX                                 
BADXNDX  DS    CL24                BAD INDEX INDEX                              
AVLXNDX  DS    CL24                AVAILABLE INDEX INDEX                        
VUNSNDX  DS    CL24                VUNERABLE INDEX SENT                         
VUNPNDX  DS    CL24                VUNERABLE INDEX PRTD                         
AVTYPE   DS    C                   AVAILABLE REPORT TYPE                        
AVCOUNT  DS    X                   COUNT OF PURGE ATTEMPTS                      
         DS    XL2                                                              
AVTBLX   DS    A                   ADR LAST ENTRY IN TABLE                      
AVTBL    DS    12XL10              NUM ENTRYS GE NUM ENTRYS IN AVSTAT           
                                                                                
STPARM   DS    0CL24                                                            
STACTN   DS    XL1                 ACTION VALUE FROM PQACTNS TABLE              
STVAL    DS    XL1                 STATUS VALUE IF ZERO ACTION VALUE            
STCTRL   DS    XL1                 CONTROL 01=FILE,02=INDEX,80=ADD              
STFLAG   DS    XL1                 FLAG FOR WORK                                
STNI     DS    XL1                 NI STATUS BITS                               
STOI     DS    XL1                 OI STATUS BITS                               
STINFO   DS    XL4                 EXTRA INFO REQUIRED FOR ACTION               
STFSTCI  DS    XL2                 TTTT OF FIRST CI                             
STBUFFA  DS    A                   A(I/O BUFFER)                                
STXCADR  DS    A                   CORE ADDR OF COPY INDEX PAGE                 
STXDADR  DS    F                   DISK ADDR OF COPY INDEX PAGE                 
*                                                                               
AQ       DS    A                   ADDR OF OLD STYLE OPEN DATA                  
Q        DS    XL156               PRINT LINE TO SIMULATE NEW CALL              
*                                                                               
STCILAST DS    A                   A(LAST ENTRY IN CI ADDR LIST)                
STCIAS   DS    514XL2              TTTT OF EACH CI IN FILE                      
                                                                                
       ++INCLUDE DMPRTQW                                                        
         DS    3F                  EXTRA PARAMS CIP4/5/6                        
                                                                                
CXREC    DS    14336C                                                           
*                                                                               
WRKX     EQU   *                                                                
         EJECT                                                                  
*DMPRTQD                                                                        
       ++INCLUDE DMPRTQD                                                        
         EJECT                                                                  
*DMPRTQS                                                                        
       ++INCLUDE DMPRTQS                                                        
         EJECT                                                                  
*DMPRTQK                                                                        
       ++INCLUDE DMPRTQK                                                        
                                                                                
KSBUFFD  DSECT                     BUFFER SAVE/RESTORE AREA                     
KSDATA   DS    0XL30                                                            
KSINDEX  DS    XL24                                                             
KSFSTCI  DS    XL4                                                              
KSINTNO  DS    X                                                                
KSEXTNO  DS    X                                                                
         EJECT                                                                  
*DMPRTQL                                                                        
       ++INCLUDE DMPRTQL                                                        
         EJECT                                                                  
*FASSB                                                                          
*FATCB                                                                          
*FAUTL                                                                          
*FASSBOFF                                                                       
*DDMASTC                                                                        
*DMREQHDRA                                                                      
         PRINT OFF                                                              
       ++INCLUDE FASSB                                                          
         EJECT                                                                  
       ++INCLUDE FATCB                                                          
         EJECT                                                                  
       ++INCLUDE FAUTL                                                          
SSOOFFD  DSECT                                                                  
       ++INCLUDE FASSBOFF                                                       
DDMASTD  DSECT                                                                  
       ++INCLUDE DDMASTC                                                        
         PRINT ON                                                               
                                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'006DMPRTQ    06/21/11'                                      
         END                                                                    
