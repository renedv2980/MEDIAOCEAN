*          DATA SET REGENDRB   AT LEVEL 105 AS OF 06/27/00                      
*CATALP REGENDRB                                                                
***********************************************************************         
*                                                                     *         
*         REGENDRB --- PERFORMS LOOK UP OF REP-ID RECORD IN CONTROL   *         
*                  --- FILE                                           *         
*                                                                     *         
* ------------------------------------------------------------------- *         
* UPDATE HISTORY:                                                     *         
*                                                                     *         
* 03MAY00 (FDOY) --- ORIGINATION DATE                                 *         
*                                                                     *         
* 22JUN00 (FDOY) --- CHANGED TO WORK OFFLINE AND ALLOW                *         
*                    FULL REP TABLE BUILD                             *         
*                                                                     *         
***********************************************************************         
         TITLE 'REGENDRB - REP ID LOOKUP ROUTINE'                               
***********************************************************************         
*  INPUT:                                                             *         
*              PARAMETER 1 =       A(3 CHAR REP CODE)                 *         
*                                      BYTE 1 C'R' = RADIO REP        *         
*                                             C'T' = TELEVISION REP   *         
*                                                                     *         
*                        2 =       A(25 BYTE STORAGE ENTRY)           *         
*                                  A(REP TABLE BUFFER) IF BYTE 1=X'20'*         
*                                      BYTE 1 X'80' READ HIGH FOR     *         
*                                             CODE IN P1, RETURN      *         
*                                             1ST RECORD FOUND WITH   *         
*                                             CORRECT MEDIA CODE, AND *         
*                                             PLACE NEXT RECORD'S     *         
*                                             CODE IN P1 IF VALID     *         
*           *!* ---------------------->> !!!!!OVERWRITES INPUT FIELD!!*         
*                                                                     *         
*                                             X'40' CALLED BY OFFLINE *         
*                                             PROGRAM.  P3 CONTAINS   *         
*                                             A(DATAMANAGER), NOT     *         
*                                             A(COMFACS).             *         
*                                                                     *         
*                                             X'20' BUILD REPID TABLE *         
*                                             STARTING AT ADDRESS IN  *         
*                                             P2.                     *         
*           *!* ---------------------->> !!!!!ENSURE ADEQUATE BUFFER  *         
*                                             SIZE FOR TABLE.         *         
*                                                                     *         
*                                                                     *         
*                        3 =       A(COMFACS)                         *         
*                                  A(DATAMANAGER)IF P2 BYTE1 = X'40'  *         
*                                                                     *         
*                                      BYTE 1 X'80'SET ON RETURN      *         
*                                             IF REP RECORD NOT FOUND *         
*                                                                     *         
*                                             X'40' SET ON RETURN     *         
*                                             IF NO RECORDS FOLLOW    *         
*                                                                     *         
*                                                                     *         
*                        4 =       A(END OF BUFFER) USED IF P2 BYTE 1 *         
*                                                    X'20' BIT SET    *         
*  OUTPUT:                                                            *         
*              MOVES 25 BYTE RECORD EXTRACT INTO ADDRESS SPECIFIED BY *         
*              INPUT PARAM 2 (FORMATTED SAME AS DDDARETAB ENTRY). IF  *         
*              NO RECORD FOUND FOR INPUT CODE, PARAM 3, BYTE 1 SET    *         
*              TO NON-ZERO VALUE.                                     *         
*                ALTERNATELY, IT WILL BUILD A REPID TABLE AT ADDRESS  *         
*              IN P2 IF P2 BYTE 1'S X'20' BIT IS SET.                 *         
*                                                                     *         
***********************************************************************         
REGENDRB CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,REGENDRB,CLEAR=YES                                   
         USING WORKD,RC                                                         
                                                                                
         BAS   RE,INIT              INITIALIZE PROGRAM                          
                                                                                
         TM    OPTFLAG,OFFLINEQ                                                 
         BZ    *+12                                                             
         BAS   RE,OFF#MAIN          OFFLINE ROUTINE                             
         B     REGENDX                                                          
*                                                                               
         BAS   RE,ON#MAIN           ONLINE ROUTINE                              
                                                                                
REGENDX  B     EXIT                                                             
         EJECT                                                                  
                                                                                
*******************************************************************             
*                                                                 *             
*              INIT ROUTINE                                       *             
*                                                                 *             
*******************************************************************             
                                                                                
INIT     NTR1                                                                   
                                                                                
*-------------------------------------------------------------*                 
*                 STORE PARAMETERS                            *                 
*-------------------------------------------------------------*                 
         ST    R1,APARAMS         SAVE ADDRESS OF PARAM LIST                    
*                                       FOR FUTURE REFERENCE                    
                                                                                
         MVC   OPTFLAG,4(R1)      SAVE INPUT OPTIONS FIRST                      
                                                                                
         MVC   MEDIA,0(R1)        SAVE 1ST PARAMETER PART A                     
                                                                                
         TM    OPTFLAG,TABLBLDQ   BUILD TABLE REQUEST?                          
         BZ    INIT10                                                           
         XC    REPCODE,REPCODE                                                  
         XC    REPADDR,REPADDR                                                  
         MVC   VEOBUFF,12(R1)     SAVE 4TH PARAMETER A(END OF BUFFER)           
         B     INIT20                                                           
                                                                                
                                                                                
INIT10   ZICM  R2,1(R1),3           (RETRIEVE REP CODE WITH                     
         MVC   REPCODE,0(R2)                     P1  PART B)                    
         ST    R2,REPADDR                  AND STORE PART B                     
                                                                                
                                                                                
INIT20   ZICM  R2,5(R1),3         SAVE 2ND PARAMETER                            
         ST    R2,OUTADDR                            PART B                     
                                                                                
         TM    OPTFLAG,OFFLINEQ   3RD PARAMETER                                 
         BZ    INIT50                                                           
         MVC   VDATAMGR,8(R1)          OFFLINE=A(DATAMANAGER)                   
         B     INIT100                                                          
                                                                                
INIT50   L     R3,8(R1)                ONLINE =A(COMFACS)                       
         USING COMFACSD,R3                                                      
         L     R4,CDATAMGR                                                      
         ST    R4,VDATAMGR        SAVE A(DATAMGR)                               
         L     R4,CGETFACT                                                      
         ST    R4,VGETFACT        SAVE A(GETFACT)                               
         L     R4,CSWITCH                                                       
         ST    R4,VSWITCH         SAVE A(SWITCH)                                
         DROP  R3                                                               
                                                                                
*-------------------------------------------------------------*                 
*             GET UTL, AND EXTRACT CALLER'S SE#               *                 
*-------------------------------------------------------------*                 
                                                                                
         GOTO1 VGETFACT,DMCB,(X'80',0),F#UTLD                                   
         L     R1,0(R1)                                                         
         USING F@UTLD,R1                                                        
         MVC   CALLSYS#,F@TSYS    SAVE CALLER'S SYS SE#                         
         DROP  R1                                                               
                                                                                
*-------------------------------------------------------------*                 
*                 SET VARIOUS FIELDS                          *                 
*-------------------------------------------------------------*                 
                                                                                
INIT100  MVC   DATADISP,=AL2(REPELDQ) RECORD DATA DISPLACEMENT                  
*                                     FOR GETEL                                 
                                                                                
         MVI   DMINBTS,0              NO SPECIAL DATAMGR BITS                   
                                                                                
         XC    REPENTRY,REPENTRY      CLEAR REP ENTRY                           
                                                                                
                                                                                
INITX    B     EXIT                                                             
         EJECT                                                                  
*******************************************************************             
*                                                                 *             
*             ON#MAIN ROUTINE (ONLINE MODULE)                     *             
*                                                                 *             
*******************************************************************             
                                                                                
ON#MAIN  NTR1                                                                   
         MVI   SWSYS#,X'0A'         SET FOR CONTROL SYSTEM SWITCH               
         BAS   RE,MYSWITCH          SWITCH TO CONTROL SYS                       
                                                                                
         TM    OPTFLAG,TABLBLDQ     BUILD TABLE REQUEST?                        
         BZ    ON#10                 NO                                         
         BAS   RE,BLDTABLE           YES                                        
         B     ONX                                                              
                                                                                
ON#10    BAS   RE,GETREP            DO REP LOOKUP                               
                                                                                
         MVC   SWSYS#,CALLSYS#      SET FOR SWITCH TO CALLER'S SYS              
         BAS   RE,MYSWITCH          SWITCH BACK TO CALLING SYS                  
ONX      B     EXIT                                                             
         EJECT                                                                  
*******************************************************************             
*                                                                 *             
*            OFF#MAIN ROUTINE (FOR OFFLINE MODULES)               *             
*                                                                 *             
*******************************************************************             
                                                                                
OFF#MAIN NTR1                                                                   
         TM    OPTFLAG,TABLBLDQ     BUILD TABLE REQUEST?                        
         BZ    OFF#10                NO                                         
         BAS   RE,BLDTABLE           YES                                        
         B     OFFX                                                             
                                                                                
OFF#10   BAS   RE,GETREP            DO REP LOOKUP                               
                                                                                
OFFX     B     EXIT                                                             
         EJECT                                                                  
                                                                                
*******************************************************************             
*                                                                 *             
*              GETREP ROUTINE                                     *             
*                                                                 *             
*******************************************************************             
                                                                                
GETREP   NTR1                                                                   
                                                                                
         BAS   RE,BLDKEY                 BUILD KEY FOR REPID                    
         BAS   RE,HIGH                   LOOK FOR REP ID RECORD                 
         TM    OPTFLAG,HIGHOPTQ          GET HIGH OPTION?                       
         BZ    GETREP20                   NO                                    
                                                                                
         CLC   KEY(REPKREP-REPKEYD),KEYSAVE CORRECT MEDIA?                      
         BNE   GETREP50                       NO                                
         BAS   RE,GETREC                      YES                               
         BAS   RE,SETSEQ                    SET NEXT RECORD CODE                
         B     GETREP30                     BUILD REPENTRY                      
                                                                                
GETREP20 CLC   KEY(REPKLENQ),KEYSAVE     EXACT RECORD FOUND?                    
         BNE   GETREP50                    NO                                   
         BAS   RE,GETREC                   YES                                  
                                                                                
*----------------------------------------------------------*                    
*             BUILD REPENTRY FOR RETURN                    *                    
*----------------------------------------------------------*                    
                                                                                
GETREP30 LA    R6,IOAREA                                                        
         USING REPREPD,R6                                                       
         MVI   ELCODE,REPREPCQ                                                  
         BAS   RE,GETEL                  GET REP ELEMENT                        
         BE    *+6                                                              
         DC    H'0'                      MUST BE THERE                          
                                                                                
         MVC   CODE,REPCODE              FILL IN REP ENTRY                      
         OC    CODE,SPACES               AND ENSURE CHAR                        
         MVC   NAME,REPNAME              FIELDS ARE SPACE                       
         OC    NAME,SPACES               PADDED                                 
         MVC   PREFIX,REPPREF                                                   
         OC    PREFIX,SPACES                                                    
         MVC   LENGTH,REPPRELN                                                  
         MVC   FLAG,REPFLAG                                                     
         B     GETREPX                   AND EXIT                               
         DROP  R6                                                               
                                                                                
                                                                                
GETREP50 L     R1,APARAMS                PARAMETER LIST ADDR                    
         OI    8(R1),REPNOTFQ            RETURN REP NOT FOUND                   
         XC    REPENTRY,REPENTRY                                                
                                                                                
GETREPX  L     R1,OUTADDR                 ADDRESS OF OUTPUT BLOCK               
         MVC   0(L'REPENTRY,R1),REPENTRY  RETURN REPENTRY                       
         B     EXIT                                                             
         EJECT                                                                  
                                                                                
*******************************************************************             
*                                                                 *             
*              BUILD TABLE ROUTINE                                *             
*                                                                 *             
*******************************************************************             
                                                                                
BLDTABLE NTR1                                                                   
         TM    OPTFLAG,HIGHOPTQ         IF GET HIGH OPTION CHOSEN               
         BZ    *+6                       THEN ERROR, BECAUSE IT'S               
         DC    H'0'                      NOT COMPATIBLE WITH BLDTABLE           
                                                                                
         L     R3,OUTADDR               TABLE BUFFER ADDRESS                    
         L     R1,VEOBUFF               END OF BUFFER ADDRESS                   
         BAS   RE,BLDKEY                                                        
         BAS   RE,HIGH                                                          
                                                                                
BLDT10   CLC   KEY(REPKREP-REPKEYD),KEYSAVE    CORRECT MEDIA?                   
         BNE   BLDT50                                                           
         LA    R2,LASTSPCQ(R3)                                                  
         CR    R2,R1                    ENOUGH SPACE FOR THIS ENTRY?            
         BNL   BLDTERR                                                          
                                                                                
         BAS   RE,GETREC                                                        
         XC    REPENTRY,REPENTRY                                                
         LA    R6,IOAREA                                                        
         USING REPKEYD,R6                                                       
         MVC   CODE,REPKREP                                                     
         OC    CODE,SPACES                                                      
         DROP  R6                                                               
*            BUILD REPENTRY                                                     
         MVI   ELCODE,REPREPCQ                                                  
         BAS   RE,GETEL                  GET REP ELEMENT                        
         BE    *+6                                                              
         DC    H'0'                      MUST BE THERE                          
         USING REPREPD,R6                                                       
                                                                                
         MVC   NAME,REPNAME              FIELDS ARE SPACE                       
         OC    NAME,SPACES               PADDED                                 
         MVC   PREFIX,REPPREF                                                   
         OC    PREFIX,SPACES                                                    
         MVC   LENGTH,REPPRELN                                                  
         MVC   FLAG,REPFLAG                                                     
         DROP  R6                                                               
                                                                                
         MVC   0(L'REPENTRY,R3),REPENTRY *PUT ENTRY IN TABLE                    
         LA    R3,L'REPENTRY(R3)                                                
                                                                                
         BAS   RE,SEQ                    GET NEXT RECORD                        
                                                                                
         B     BLDT10                    AND CONTINUE LOOP                      
                                                                                
                                                                                
BLDT50   MVI   0(R3),X'FF'               MARK 'EOT'                             
         B     BLDTX                                                            
                                                                                
BLDTERR  GOTO1 =V(LOGIO),DMCB,X'FF000001',                             *        
               =C'BUFFER OVERFLOW ON REPID TABLE BUILD'                         
         DC    H'0'                     OVERFLOW, SO BLOW UP!!!!                
*                                       ALLOCATE MORE SPACE FOR REPID           
*                                       TABLE IN CALLING MODULE                 
                                                                                
BLDTX    B     EXIT                                                             
         EJECT                                                                  
*******************************************************************             
*                                                                 *             
*              BUILD KEY ROUTINE                                  *             
*                                                                 *             
*******************************************************************             
                                                                                
BLDKEY   NTR1                                                                   
         XC    KEY,KEY                                                          
         LA    R1,KEY                                                           
         USING REPKEYD,R1                                                       
                                                                                
         MVI   REPKSYS,REPKSYSQ          BUILD SEARCH KEY                       
         MVI   REPKTYP,REPKTYPQ                                                 
         MVC   REPKMEDA,MEDIA                                                   
         MVC   REPKREP,REPCODE                                                  
         MVC   KEYSAVE,KEY                                                      
         DROP  R1                                                               
                                                                                
         B     EXIT                                                             
         EJECT                                                                  
                                                                                
*******************************************************************             
*                                                                 *             
*                 SWITCH ROUTINE                                  *             
*                                                                 *             
*******************************************************************             
                                                                                
MYSWITCH NTR1                                                                   
         XC    DMCB(8),DMCB              CLEAR PARAMETER LIST                   
         GOTO1 VSWITCH,DMCB,(SWSYS#,0)   DO SWITCH                              
         CLI   4(R1),0                   SUCCESSFUL?                            
         BE    *+6                          YES                                 
         DC    H'0'                         NO                                  
         B     EXIT                                                             
         EJECT                                                                  
*******************************************************************             
*                                                                 *             
*                 DATAMANAGER CALLS                               *             
*                                                                 *             
*******************************************************************             
                                                                                
HIGH     NTR1                                                                   
         GOTO1 VDATAMGR,DMCB,(DMINBTS,=C'DMRDHI'),=C'GENDIR',          *        
               KEYSAVE,KEY,0                                                    
         B     EXIT                                                             
                                                                                
SEQ      NTR1                                                                   
         GOTO1 VDATAMGR,DMCB,(DMINBTS,=C'DMRSEQ'),=C'GENDIR',          *        
               KEYSAVE,KEY,0                                                    
         B     EXIT                                                             
                                                                                
GETREC   NTR1                                                                   
         GOTO1 VDATAMGR,DMCB,(DMINBTS,=C'GETREC'),=C'GENFILE',         *        
               KEY+(REPKDA-REPKEYD),IOAREA,DMWORK                               
         CLI   DMCB+8,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         B     EXIT                                                             
         EJECT                                                                  
*******************************************************************             
*                                                                 *             
*                 SETSEQ ROUTINE                                  *             
*                                                                 *             
*******************************************************************             
                                                                                
SETSEQ   NTR1                                                                   
         GOTO1 VDATAMGR,DMCB,(DMINBTS,=C'DMRSEQ'),=C'GENDIR',          *        
               KEYSAVE,KEY,0                                                    
         CLC   KEY(REPKREP-REPKEYD),KEYSAVE CORRECT MEDIA?                      
         BNE   SETSEQ50                                                         
                                                                                
         L     R2,REPADDR                                                       
         LA    R1,KEY                                                           
         USING REPKEYD,R1                                                       
         MVC   0(L'REPKREP,R2),REPKREP                                          
         DROP  R1                                                               
         B     SETSEQX                                                          
                                                                                
SETSEQ50 L     R1,APARAMS                PARAMETER LIST ADDR                    
         OI    8(R1),NOMOREQ             RETURN REP NOT FOUND                   
                                                                                
SETSEQX  B     EXIT                                                             
         EJECT                                                                  
*******************************************************************             
*                                                                 *             
*                 GETEL ROUTINE                                   *             
*                                                                 *             
*******************************************************************             
                                                                                
         PRINT GEN                                                              
         GETEL R6,DATADISP,ELCODE                                               
         PRINT NOGEN                                                            
         EJECT                                                                  
                                                                                
*******************************************************************             
*                                                                 *             
*                 COMMON BRANCHES                                 *             
*                                                                 *             
*******************************************************************             
                                                                                
YES      SR    RC,RC                                                            
NO       LTR   RC,RC              SET CONDITION CODE AT EXIT                    
                                                                                
EXIT     XIT1                                                                   
         EJECT                                                                  
                                                                                
                                                                                
*/////////////////////////////////////////////////////////////////*             
*                       STORAGE                                   *             
*/////////////////////////////////////////////////////////////////*             
                                                                                
SPACES   DC    10X'40'                                                          
                                                                                
WORKD    DSECT                                                                  
DMWORK   DS    12D                                                              
KEY      DS    CL48                                                             
KEYSAVE  DS    CL48                                                             
DMCB     DS    6F                                                               
DUB      DS    D                                                                
DATADISP DS    H                                                                
ELCODE   DS    X                                                                
DMINBTS  DS    X                                                                
OPTFLAG  DS    X                  OPTIONS CHOSEN BY CALLING ROUTINE             
HIGHOPTQ EQU   X'80'              RETURN RESULT OF READ HIGH                    
OFFLINEQ EQU   X'40'              CALLED BY OFFLINE PROGRAM                     
TABLBLDQ EQU   X'20'              BUILD FULL REPTABLE                           
                                                                                
CALLSYS# DS    XL1                CALLING SYSTEM SE#                            
SWSYS#   DS    XL1                'SWITCH TO'    SE#                            
MEDIA    DS    CL1                MEDIA TYPE FOR REP (T OR R)                   
REPCODE  DS    CL3                                                              
                                                                                
REPENTRY DS    0CL25           REPID TABLE ENYTRY                               
CODE     DS    CL3                                                              
NAME     DS    CL10                                                             
FLAG     DS    XL1                                                              
LENGTH   DS    XL1                                                              
PREFIX   DS    CL10                                                             
LASTSPCQ EQU   L'REPENTRY+1    SPACE NEEDED FOR LAST ENTRY+EOT(X'FF')           
                                                                                
APARAMS  DS    A                                                                
REPNOTFQ EQU   X'80'              "REP NOT FOUND" RETURN FLAG                   
NOMOREQ  EQU   X'40'              "NO MORE RECORDS" RETURN FLAG                 
REPADDR  DS    A                  ADDRESS OF REP CODE IN CALLING MODULE         
OUTADDR  DS    A                  ADDRESS FOR OUTPUT OF REPENTRY/TABLE          
VDATAMGR DS    A                  ADDRESS OF DATAMANAGER                        
VGETFACT DS    A                  ADDRESS FOR GETFACT                           
VSWITCH  DS    A                  ADDRESS FOR FASWITCH                          
VEOBUFF  DS    A                  END OF BUFFER POINTER                         
IOAREA   DS    1000X                                                            
                                                                                
WORKX    EQU   *                                                                
                                                                                
                                                                                
       ++INCLUDE DDCOMFACS                                                      
       ++INCLUDE FAFACTS                                                        
       ++INCLUDE CTGENREPD                                                      
       ++INCLUDE REGENCON                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'105REGENDRB  06/27/00'                                      
         END                                                                    
