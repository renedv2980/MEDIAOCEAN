*          DATA SET REPRPUPV   AT LEVEL 031 AS OF 04/14/10                      
*&&      SET   NOP=N                                                            
*CATALP REPRPUPV                                                                
***********************************************************************         
* ROUTINE TO VALIDATE UPGRADE EXPRESSION                                        
*                                                                               
* ON ENTRY:    PARAM 1  A(FIELD HEADER OF UPGRADE FIELD)                        
*              PARAM 2  A(OUTPUT AREA)                                          
*              PARAM 3  A(ADDRESS BLOCK)                                        
*                         A(BOOKVAL)                                            
*                         A(COMFACS)                                            
*                         A(UPVAL)                                              
*                                                                               
* ON EXIT:                                                                      
*    CC NOT EQUAL, EXPRESSION INVALID. OUTPUT CONTAINS 2 BYTE ERROR             
*                                                                               
*    CC EQUAL, OUTPUT AREA FORMAT:                                              
*                                                                               
*              BYTE 00-13   X'05' UPGRADE ELEMENT                               
*              BYTE 14      SPECIAL BOOK TYPE                                   
*              BYTE 15+     SHARE BOOK LIST(EACH BOOK IS 3 BYTES)               
*                                                                               
***********************************************************************         
REPRPUPV CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKLQ,NEWUPVAL,RR=RE                                            
         USING WORKD,RC                                                         
         ST    RE,RELO                                                          
*                                                                               
         L     R2,0(R1)            R2 = A(FIELD HEADER)                         
         MVC   AOUTP,4(R1)                                                      
         L     RF,8(R1)                                                         
         MVC   VBOOKVAL(3*4),0(RF)                                              
         L     RF,ACOMFACS                                                      
         L     RF,CSCANNER-COMFACSD(RF)                                         
         ST    RF,VSCANNER                                                      
*                                                                               
         XC    OUTPUT,OUTPUT                                                    
*                                                                               
         CLC   =C'UPT=BK/',8(R2)   FORMAT  UPT=BK/MYY(/MYY/MYY/MYY) ??          
         BNE   VLOOP00                                                          
*                                                                               
         ZIC   RE,5(R2)            YES, ALTER FORMAT TO                         
         SH    RE,=H'5'                    UPT=IX/100,BK=MYY(/MYY/....)         
         BM    EXITINV                                                          
*                                                                               
         MVC   WORK(7),=C'IX/100,'                                              
         EX    RE,*+4                                                           
         MVC   WORK+7(0),8+4(R2)                                                
         MVI   WORK+9,C'='       CHANGE THE 'BK/' TO 'BK='                      
         LA    RE,7(RE)                                                         
         EX    RE,*+4                                                           
         MVC   8+4(0,R2),WORK                                                   
         LA    RE,5(RE)                                                         
         STC   RE,5(R2)                                                         
         LA    RE,8(RE)                                                         
         STC   RE,0(R2)                                                         
*                                                                               
VLOOP00  GOTO1 VSCANNER,DMCB,(30,(R2)),(5,SCANBLK),C',=,='                      
         MVC   SCANLNS,4(R1)    SAVE NUMBER OF SCANNER LINES                    
         MVI   FLDINDS,0           FIELD INPUT INDICATORS                       
*                                                                               
         CLI   SCANLNS,0                                                        
         BE    EXITINV                                                          
         LA    R6,SCANBLK          R6 = A(SCANNER BLOCK)                        
         MVI   CURLINES,1                                                       
*                                                                               
VLOOP20  CLI   0(R6),0             TEST L'FIRST HALF OF ENTRY                   
         BE    EXITINV                                                          
*                                                                               
         CLI   0(R6),L'UPGNAME                                                  
         BH    EXITINV                                                          
*                                                                               
         CLI   1(R6),0             TEST L'SECOND HALF OF ENTRY                  
         BE    EXITINV                                                          
*                                                                               
         ZIC   R1,0(R6)                                                         
         BCTR  R1,0                R1=L'FIRST HALF OF INPUT-1                   
         LA    R3,UPGTAB                                                        
         USING UPGTABD,R3          R3=A(UPGRADE TABLE)                          
*                                                                               
VLOOP30  CLI   UPGNAME,0           TEST E-O-T                                   
         BE    EXITINV                                                          
*                                                                               
         CLI   UPGSHRT,C' '        TEST ENTRY HAS SHORT KEYWORD                 
         BE    VLOOP40                                                          
*                                                                               
         LA    RE,2                ADJUST RE (LENGTH) FOR EX                    
         CLI   UPGSHRT+2,C' '                                                   
         BNE   *+6                                                              
         BCTR  RE,0                                                             
         CLI   UPGSHRT+1,C' '                                                   
         BNE   *+6                                                              
         BCTR  RE,0                                                             
*                                                                               
         CR    R1,RE               TEST INPUT LEN = SHORT NAME LEN              
         BNE   VLOOP40                                                          
*                                                                               
         EX    R1,*+8              YES - MATCH ON SHORT NAME                    
         B     *+10                                                             
         CLC   UPGSHRT(0),12(R6)                                                
         BE    VLOOP60                                                          
*                                                                               
VLOOP40  EX    R1,*+8              MATCH ON LONG NAME                           
         B     *+10                                                             
         CLC   UPGNAME(0),12(R6)                                                
         BE    VLOOP60                                                          
*                                                                               
VLOOP50  LA    R3,UPGTABL(R3)      BUMP TO NEXT UPGRADE TABLE ENTRY             
         B     VLOOP30                                                          
*                                                                               
VLOOP60  MVC   BYTE,FLDINDS          TEST KEYWORD INPUT PREVIOUSLY              
         NC    BYTE,UPGIND1                                                     
         BNZ   EXITNDUP                                                         
*                                                                               
         XC    WORK,WORK                                                        
         ZIC   R1,1(R6)                                                         
         EX    R1,*+8              DON'T CARE IF WE COPY ONE MORE BYTE          
         B     *+10                                                             
         MVC   WORK+8(0),22(R6)                                                 
         STC   R1,WORK+5                                                        
         LA    R1,8(R1)                                                         
         STC   R1,WORK                                                          
*                                                                               
         OC    FLDINDS,UPGIND1     SET THIS KEYWORD INPUT                       
         L     RF,UPGROUT                                                       
         A     RF,RELO                                                          
         BASR  RE,RF                                                            
         BNE   EXITL                                                            
*                                                                               
         ZIC   R1,CURLINES         ANY MORE SCANNER ENTRIES?                    
         LA    R1,1(R1)                                                         
         CLM   R1,1,SCANLNS                                                     
         BH    VLOOPX              NO                                           
*                                                                               
         STC   R1,CURLINES         YES                                          
         LA    R6,22+30(R6)        BUMP TO NEXT SCANNER TABLE ENTRY             
         B     VLOOP20                                                          
*                                                                               
VLOOPX   DS    0H                                                               
         L     RE,AOUTP                                                         
         MVC   0(50,RE),OUTPUT                                                  
         B     EXITOK                                                           
         EJECT                                                                  
***********************************                                             
* VALIDATE UPGRADE EXPRESSION                                                   
***********************************                                             
VALUPE   NTR1                                                                   
         GOTO1 VUPVAL,DMCB,WORK,(C'Y',OUTPUT),(C'/',ACOMFACS)                   
*                                                                               
         CLI   0(R1),0                                                          
         BE    EXITIUP                                                          
         B     EXITOK                                                           
***********************************                                             
* VALIDATE OVERRIDE BOOK                                                        
***********************************                                             
VALUPB   NTR1                                                                   
         ZIC   RF,WORK+5                                                        
         LA    R1,WORK+8                                                        
*                                                                               
VBOOK10  CLI   0(R1),C'/'          ALTER SLASHES TO COMMAS IF THEY              
         BNE   VBOOK12             SEPARATE BOOKS                               
         CLI   1(R1),C'0'                                                       
         BNL   VBOOK12                                                          
         MVI   0(R1),C','                                                       
VBOOK12  LA    R1,1(R1)                                                         
         BCT   RF,VBOOK10                                                       
*                                                                               
         GOTO1 VBOOKVAL,DMCB,(C'N',WORK),(4,OUTPUT+15),                X        
               (C'B',VSCANNER),FULL,(C'C',ACOMFACS)                             
         SR    RF,RF                                                            
         ICM   RF,1,4(R1)          ANY BOOKS?                                   
         BZ    EXITIBK             NO - INVALID                                 
*                                                                               
         CHI   RF,4                TOO MANY BOOKS?                              
         BH    EXITIBK             YES                                          
*                                                                               
***      CLI   FULL,C' '           SPECIAL BOOK TYPE?                           
***      BNH   *+10                                                             
         MVC   OUTPUT+14(1),FULL   YES - SAVE IT                                
*                                                                               
UE       USING RAVLNEL,OUTPUT                                                   
         CLI   UE.RAVLNBT,0             ANY BOOK TYPE IN UPGRADE?               
         BE    *+14                     NO                                      
         CLC   UE.RAVLNBT,OUTPUT+14     SAME BOOK TYPE?                         
         BNE   EXITIBK                  NO, ERROR                               
         DROP  UE                                                               
*                                                                               
         LA    RE,OUTPUT+15                                                     
VBOOK30  DS    0H                                                               
         TM    0(RE),X'BF'         FUNNY BOOK FORMATS?                          
         BNZ   EXITIBK             YES INVALID                                  
*                                                                               
         LA    RE,3(RE)                                                         
         BCT   RF,VBOOK30                                                       
*                                                                               
         B     EXITOK                                                           
         EJECT                                                                  
*************************************************************                   
EXITNDUP L     RE,AOUTP                                                         
         MVC   0(2,RE),=Y(401)                                                  
         B     EXITL                                                            
EXITINV  L     RE,AOUTP                                                         
         MVC   0(2,RE),=Y(2)                                                    
         B     EXITL                                                            
EXITIUP  L     RE,AOUTP                                                         
         MVC   0(2,RE),=Y(235)                                                  
         B     EXITL                                                            
EXITIBK  L     RE,AOUTP                                                         
         MVC   0(2,RE),=Y(232)                                                  
         B     EXIT                                                             
EXITH    CLI   *,0                                                              
         B     EXIT                                                             
EXITL    CLI   *,X'FF'                                                          
         B     EXIT                                                             
EXITOK   CR    RB,RB                                                            
         B     EXIT                                                             
EXIT     DS    0H                                                               
         XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
*************************************************************                   
UPGTAB   DS    0H               ** UPGRADE VALIDATION TABLE **                  
         DC    C'TUPGRADE',C'UPT',X'8000',A(VALUPE)                             
         DC    C'BOOK    ',C'BK ',X'4000',A(VALUPB)                             
UPGTABX  DC    X'00'                                                            
*                                                                               
UPGTABD  DSECT                                                                  
UPGNAME  DS    CL8                 KEYWORD NAME                                 
UPGSHRT  DS    CL3                 SHORT KEYWORD NAME                           
UPGIND1  DS    XL1                 INDICATORS                                   
UPGIND2  DS    XL1                 INDICATORS                                   
UPGROUT  DS    A                   DISPLACEMENT OF VALIDATION ROUTINE           
UPGTABL  EQU   *-UPGTABD                                                        
*************************************************************                   
WORKD    DSECT                                                                  
RELO     DS    A                                                                
*                                                                               
VBOOKVAL DS    A                                                                
ACOMFACS DS    A                                                                
VUPVAL   DS    A                                                                
*                                                                               
VSCANNER DS    A                                                                
*                                                                               
AOUTP    DS    A                                                                
FULL     DS    F                                                                
*                                                                               
DMCB     DS    6F                                                               
*                                                                               
SCANLNS  DS    X                                                                
CURLINES DS    X                   CURRENT NUMBER OF LINES PROCCESSED           
FLDINDS  DS    X                                                                
BYTE     DS    X                                                                
*                                                                               
WORK     DS    CL255                                                            
OUTPUT   DS    CL255                                                            
*                                                                               
SCANBLK  DS    CL(52*5)                                                         
WORKLQ   EQU   *-WORKD                                                          
*                                                                               
       ++INCLUDE REGENAVLN                                                      
       ++INCLUDE DDCOMFACS                                                      
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'031REPRPUPV  04/14/10'                                      
         END                                                                    
