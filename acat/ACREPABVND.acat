*          DATA SET ACREPABVND AT LEVEL 019 AS OF 04/20/20                      
*CATALP ACABVNDR                                                                
         TITLE 'ACREPABVND - READ VENDOR DETAILS FOR RECEIVABLES'               
                                                                                
*======================================================================         
* GHOA 019 20APR20 SPEC-22435 AB ISSUE RUNNING FOR MULTIPLE MEDIA FILES         
*======================================================================         
* CALLING PARMS ARE                                                             
* P1   A(ACREPWORKD)                                                            
* P2   A(RECEIVABLE POSTING)                                                    
* P3   A(OUTPUT BUFFER)                                                         
* P4   A(3 BYTE BINARY START/END MOS)                                           
* P5   A(RECEIVABLE AGENCY ALPHA ID)                                            
*=============================================================                  
                                                                                
ABVNDR   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,ABVNDR                                                         
*                                                                               
         LARL  RC,COMMON                                                        
         USING COMMON,RC                                                        
*                                                                               
         MVC   SVPARMS,0(R1)                                                    
*                                                                               
         L     RA,ABVNDWRK                                                      
         USING ACWORKD,RA,R9                                                    
         LA    R9,2048(RA)                                                      
         LA    R9,2048(R9)                                                      
*                                                                               
         J     FX00                                                             
*                                                                               
EQXIT    CR    RC,RC                                                            
         J     *+6                                                              
NEQXIT   LTR   RC,RC                                                            
*                                                                               
EXIT     XIT1                                                                   
*                                                                               
FX00     CLI   SVSEACC,0           TEST FIRST TIME                              
         JNE   FX02                                                             
*                                                                               
         MVC   DUB(8),=CL8'T00A14  '    LOAD CLPACK                             
         GOTO1 LOADER,DMCB,DUB,0,0                                              
         ICM   RF,15,4(R1)                                                      
         JZ    *+2                                                              
         ST    RF,VCLPACK                                                       
*                                                                               
         MVC   DUB(8),=CL8'T00A7A  '    LOAD STAPACK                            
         GOTO1 LOADER,DMCB,DUB,0,0                                              
         ICM   RF,15,4(R1)                                                      
         JZ    *+2                                                              
         ST    RF,VSTAPACK                                                      
*                                                                               
         MVC   DUB(8),=CL8'T00A9E  '    LOAD CABLETAB FOR STAPACK               
         GOTO1 LOADER,DMCB,DUB,0,0                                              
         ICM   RF,15,4(R1)                                                      
         JZ    *+2                                                              
         ST    RF,VCBLTAB                                                       
*                                                                               
         LA    R1,STAWORK          INITIALIZE CABLETAB                          
         USING STAPACKD,R1                                                      
         XC    STAWORK,STAWORK                                                  
         MVI   STAPACT,QSTP_T00A9E  SET ACTION = HERE IS ADDRESS!               
         MVC   STAPACOM,VCBLTAB                                                 
         GOTO1 VSTAPACK,(R1)                                                    
*                                                                               
         L     RF,ADMASTC                                                       
         L     RF,MCUTL-MASTD(RF)                                               
         MVC   SVSEACC,TSYS-UTLD(RF) SAVE ACC SE NUMBER                         
                                                                                
FX02     L     R7,THISSE           POINT TO CURRENT SETAB ENTRY                 
         USING SETABD,R7                                                        
*                                                                               
         L     RE,ABVNDID          GET ALPHA AGY PASSED THIS TIME               
         OC    0(2,RE),0(RE)       TEST IT'S THERE                              
         JNZ   FX04                YES                                          
         LA    R7,SETAB            IF NOT, USE FIRST AGY                        
         ST    R7,THISSE                                                        
         J     FX10                                                             
*                                                                               
FX04     CLC   SEAGYA,0(RE)        SAME AS LAST TIME?                           
         JE    FX10                                                             
*                                                                               
         BAS   RE,GETSES           LOOK UP IN SETAB OR READ NEW                 
         L     R7,THISSE           AND POINT TO NEW ENTRY                       
         EJECT                                                                  
FX10     L     R8,ABVNDRCV         POINT TO RECEIVABLE RECORD                   
         USING AC_TRNRECD,R8                                                    
*                                                                               
         LA    R6,AC_TRNRFST       IF NOT A TRNEL, SKIP REC                     
         CLI   0(R6),AC_TRNELQ                                                  
         JNE   *+2                                                              
*                                                                               
         USING AC_TRNELD,R6                                                     
         TM    AC_TRNSTAT,AC_TRNSDR  TEST DEBIT                                 
         JZ    EXIT                  NO - IGNORE                                
*                                                                               
         CLI   AC_TRNTYPE,9        TEST MEDIA BILLING ELEM                      
         JNE   EXIT                                                             
         DROP  R6                                                               
*                                                                               
         ST    R6,SVTRNEL                                                       
*                                                                               
         LA    R6,AC_TRNRFST                                                    
         MVI   ELCDLO,AC_MBIELQ                                                 
         MVI   ELCDHI,AC_MBIELQ                                                 
         BRAS  RE,NEXTEL2                                                       
         BNE   FX20                                                             
*                                                                               
         USING AC_MBIELD,R6                                                     
         MVC   SVSYS,AC_MBISYS                                                  
         MVC   SVQMED,AC_MBIMED                                                 
         MVC   SVQCLT,AC_MBICLI                                                 
         MVC   SVQPRD,AC_MBIPRD                                                 
         PACK  DUB,AC_MBIEST                                                    
         CVB   R0,DUB                                                           
         STC   R0,SVEST1                                                        
         STH   R0,SVEST2                                                        
         MVC   SVMOSP,AC_MBIMOS                                                 
         B     FX40                                                             
         DROP  R6                                                               
*                                                                               
FX20     LA    R6,AC_TRNRFST                                                    
         MVI   ELCDLO,AC_MDPELQ                                                 
         MVI   ELCDHI,AC_MDPELQ                                                 
         BRAS  RE,NEXTEL2                                                       
         BNE   FX30                                                             
*                                                                               
         USING AC_MDPELD,R6                                                     
         MVC   SVSYS,AC_MDPSYS                                                  
         MVC   SVQMED,AC_MDPMED                                                 
         MVC   SVQCLT,AC_MDPCLI                                                 
         MVC   SVQPRD,AC_MDPPRD                                                 
         PACK  DUB,AC_MDPEST                                                    
         CVB   R0,DUB                                                           
         STC   R0,SVEST1                                                        
         STH   R0,SVEST2                                                        
         MVC   SVMOSP,AC_MDPMOS                                                 
         B     FX40                                                             
                                                                                
*                                                                               
FX30     LA    R6,AC_TRNRFST                                                    
         MVI   ELCDLO,AC_MDTELQ                                                 
         MVI   ELCDHI,AC_MDTELQ                                                 
         BRAS  RE,NEXTEL2                                                       
         JNE   *+2                                                              
*                                                                               
         USING AC_MDTELD,R6                                                     
         MVC   SVSYS,AC_MDTSYS                                                  
         MVC   SVQMED,AC_MDTMED                                                 
         MVC   SVQCLT,AC_MDTCLI                                                 
         MVC   SVQPRD,AC_MDTPRD                                                 
         PACK  DUB,AC_MDTEST                                                    
         CVB   R0,DUB                                                           
         STC   R0,SVEST1                                                        
         STH   R0,SVEST2                                                        
         MVC   SVMOSP,AC_MDTMOS                                                 
*                                                                               
FX40     MVI   SVMOSP+2,1          CONVERT PWOS TO 2-BYTE BINARY                
         UNPK  WORK(7),SVMOSP(4)       E.G., FBF4F0F8F0F1YX                     
         GOTO1 DATCON,DMCB,WORK,(3,SVMOS)                                       
*                                                                               
         L     R6,SVTRNEL                                                       
         USING AC_TRNELD,R6                                                     
*                                                                               
         MVC   SVINV,AC_TRNREF     SAVE BILLMON/INV NUM                         
*                                                                               
         PACK  DUB,AC_TRNREF+2(4)  PACK THE INVOICE NUMBER                      
         CVB   R0,DUB                                                           
         STH   R0,SVINVB                                                        
*                                                                               
         LA    R6,AC_TRNRFST                                                    
         MVI   ELCDLO,AC_GDAELQ                                                 
         MVI   ELCDHI,AC_GDAELQ                                                 
*                                                                               
FX42     BRAS  RE,NEXTEL                                                        
         JNE   *+2                                                              
         USING AC_GDAELD,R6                                                     
         CLI   AC_GDATYPE,2         TEST MEDIA BILLING DATE                     
         BNE   FX42                                                             
*                                                                               
         GOTO1 DATCON,DMCB,(1,AC_GDADATE),(2,SVBILLD2)                          
         GOTO1 (RF),(R1),(1,AC_GDADATE),(3,SVBILLD3)   FOR PRINT                
         DROP  R6                                                               
         EJECT                                                                  
*==============================================================                 
* SEE IF CONNECTED TO THE RIGHT SYSTEM FOR THIS RECORD                          
*==============================================================                 
                                                                                
         MVC   THISSENUM,SENUMPRT                                               
         CLI   SVSYS,C'P'                                                       
         JE    FX50                                                             
*                                                                               
         MVC   THISSENUM,SENUMSPT                                               
         CLI   SVSYS,C'S'          TEST SPOT                                    
         JNE   *+2                                                              
*                                                                               
         CLI   RCCTRY,X'08'        TEST CANADA                                  
         JE    FX50                YES -ACCEPT ALL MEDIA                        
                                                                                
*=======================================================                        
* US NETWORK SYSTEMS HAVE SYSTEM CODE S FOR SPOT                                
* BUT A UNIQUE SET OF MEDIA CODES                                               
*=======================================================                        
                                                                                
         LA    R0,L'MDTAB          TEST MEDIA CODE                              
         LA    R1,MDTAB                                                         
*                                                                               
FX44     CLC   SVQMED,0(R1)        TEST FOR A NETWORK MEDIA CODE                
         JE    FX46                                                             
         LA    R1,1(R1)                                                         
         JCT   R0,FX44                                                          
         J     FX50                                                             
*                                                                               
MDTAB    DC    C'NCSDOV'           EXCLUDE MEDIA EXCEPT FOR CANADA              
*                                                                               
FX46     MVC   THISSENUM,SENUMNET  SET SYSTEM TO NET                            
         MVI   SVSYS,C'N'          AND REMEMBER IT!                             
         EJECT                                                                  
*====================================================================           
* SET UTL ENTRY TO READ CORRECT MEDIA FILES AND OPEN THEM IF NOT OPEN           
*====================================================================           
                                                                                
FX50     L     RF,ADMASTC                                                       
         L     RF,MCUTL-MASTD(RF)                                               
         MVC   TSYS-UTLD(1,RF),THISSENUM SET THE SYSTEM IN UTL                  
*                                                                               
         CLC   LASTSENUM,THISSENUM       SAME SYSTEM AS LAST TIME               
         JE    FX60                                                             
                                                                                
* OPEN THE MEDIA FILES IF NOT ALREADY OPEN                                      
                                                                                
FX52     MVC   LASTSENUM,THISSENUM                                              
*                                                                               
         LA    R1,SESPTOPN                                                      
         LA    R4,=CL8'SPOT'                                                    
         LA    R5,SPTFLIST                                                      
         CLI   SVSYS,C'S'                                                       
         JE    FX54                                                             
*                                                                               
         LA    R1,SENETOPN                                                      
         LA    R4,=CL8'NET'                                                     
         LA    R5,NETFLIST                                                      
         CLI   SVSYS,C'N'                                                       
         JE    FX54                                                             
*                                                                               
         LA    R1,SEPRTOPN                                                      
         LA    R4,=C'PRINT'                                                     
         LA    R5,PRTFLIST                                                      
         CLI   SVSYS,C'P'                                                       
         JNE   *+2                                                              
*                                                                               
FX54     CLI   0(R1),C'Y'          TEST SYSTEM ALREADY OPEN                     
         JE    FX60                                                             
         MVI   0(R1),C'Y'          SET OPEN FLAG                                
*                                                                               
         GOTO1 DATAMGR,DMCB,DMOPEN,(R4),(R5),REC                                
*                                                                               
FX60     CLI   SVSYS,C'S'                                                       
         JNE   FX62                                                             
         BRAS  RE,SP00                                                          
         J     FX70                                                             
*                                                                               
FX62     CLI   SVSYS,C'N'                                                       
         JNE   FX64                                                             
         BRAS  RE,NET00                                                         
         J     FX70                                                             
*                                                                               
FX64     CLI   SVSYS,C'P'                                                       
         JNE   *+2                                                              
         BRAS  RE,PR00                                                          
*                                                                               
FX70     L     RF,ADMASTC                                                       
         L     RF,MCUTL-MASTD(RF)                                               
         MVC   TSYS-UTLD(1,RF),SVSEACC  RESTORE ACC TO UTL ENTRY                
         SAM24                          AND EXIT IN 24-BIT MODE                 
         J     EXIT                                                             
         EJECT                                                                  
*================================================================               
* CHECK SETAB TO SEE IF THIS AGYA ALREADY THERE ELSE,                           
* READ ACCESS RECORD FOR SPOT/PRINT/NET SE NUMS/AGYB CODES                      
*================================================================               
                                                                                
GETSES   NTR1  BASE=*,LABEL=*                                                   
         XC    LASTMDCL,LASTMDCL   MAKE SURE NO RANDOM MATCH                    
                                                                                
* FIRST CHECK CURRENT SE TABLE TO SEE IF AGENCY THERE                           
                                                                                
         L     RE,ABVNDID          POINT TO OVERRIDE AGY                        
         LA    R7,SETAB            FIRST TABLE ENTRY                            
         USING SETABD,R7                                                        
*                                                                               
         CLI   SEAGYA,0            TEST FIRST TIME                              
         JE    GETSE10                                                          
*                                                                               
GETSE02  CLC   SEAGYA,0(RE)        RIGHT AGY                                    
         JE    GETSE20                                                          
         AHI   R7,L'SETAB                                                       
         CLI   SEAGYA,0                                                         
         JNE   GETSE02                                                          
                                                                                
*==============================================================                 
* BUILD A NEW ENTRY IN SETAB FOR THIS AGYA                                      
*==============================================================                 
                                                                                
GETSE10  MVC   SEAGYA,0(RE)                                                     
*                                                                               
         XC    WORK,WORK                                                        
         LA    R4,WORK                                                          
         USING CT5REC,R4                                                        
*                                                                               
         MVI   CT5KTYP,CT5KTYPQ                                                 
         L     RE,ABVNDID                                                       
         MVC   CT5KALPH,0(RE)                                                   
         DROP  R4                                                               
*                                                                               
         GOTO1 DATAMGR,DMCB,DMREAD,CTFILE,WORK,REC                              
         CLI   8(R1),0                                                          
         JNE   *+2                                                              
*                                                                               
         LA    R4,REC                                                           
         LA    R4,CT5DATA-CT5REC(R4)                                            
         USING CTSYSD,R4                                                        
         SR    R0,R0                                                            
         B     GETSE14                                                          
*                                                                               
GETSE12  IC    R0,1(R4)                                                         
         AR    R4,R0                                                            
*                                                                               
GETSE14  CLI   0(R4),0                                                          
         JE    GETSE20                                                          
*                                                                               
         CLI   CTSYSEL,CTSYSELQ    X'21' ELEM?                                  
         BNE   GETSE12                                                          
*                                                                               
         CLI   CTSYSNUM,X'02'      SPOT?                                        
         JNE   *+16                                                             
         MVC   SENUMSPT,CTSYSSE    SAVE SPOT SE NUMBER                          
         MVC   SESPTAGB,CTSYSAGB   AND BINARY COMPANY                           
*                                                                               
         CLI   CTSYSNUM,X'03'      NETWORK                                      
         JNE   *+16                                                             
         MVC   SENUMNET,CTSYSSE    SAVE NET SE NUMBER                           
         MVC   SENETAGB,CTSYSAGB   AND BINARY COMPANY                           
*                                                                               
         CLI   CTSYSNUM,X'04'      PRINT                                        
         JNE   *+10                                                             
         MVC   SENUMPRT,CTSYSSE    SAVE PRINT SE NUMBER                         
*                                                                               
         J     GETSE12                                                          
*                                                                               
GETSE20  ST    R7,THISSE           SET SE ENTRY ADDRESS                         
         J     EXIT                                                             
         DROP  R4                                                               
                                                                                
         EJECT                                                                  
*================================================================               
* GET THE CLIENT RECORD TO TRANSLATE THE PRODUCT CODE                           
*================================================================               
                                                                                
SP00     NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         CLI   RCCTRY,X'08'        TEST CANADA                                  
         JNE   SP10                                                             
         OC    SENETLST,SENETLST   ANY NETLIST YET THIS AGY                     
         JNZ   SP10                YES                                          
         BRAS  RE,GETNETS          BUILD CANADIAN NETWORK TABLE                 
*                                                                               
SP10     CLC   LASTMDCL,SVQMED     TEST SAME MEDIA/CLIENT                       
         JE    SP20                                                             
*                                                                               
         MVC   LASTMDCL,SVQMED     SAVE NEW MEDIA/CLIENT                        
         BAS   RE,GETSPCLT                                                      
*                                                                               
SP20     LA    RE,SVCLTHDR                                                      
         LA    RE,CLIST-CLTHDRD(RE)  POINT TO PRDLIST                           
*                                                                               
SP22     CLC   SVQPRD,0(RE)                                                     
         BE    SP24                                                             
         LA    RE,4(RE)                                                         
         CLI   0(RE),C'A'                                                       
         JNL   SP22                                                             
         DC    H'0'                                                             
*                                                                               
SP24     MVC   SVPRDCD,3(RE)        SAVE BINARY PRD                             
                                                                                
*===============================================================                
* NOW READ STATION BILLING RECORDS                                              
*===============================================================                
                                                                                
         SAM31                                                                  
         L     R4,ABVNDBUF         POINT TO OUTPUT BUFFER                       
         USING BUFFERD,R4                                                       
         XC    0(BUFFERLN,R4),0(R4) CLEAR FIRST ENTRY                           
         SAM24                                                                  
                                                                                
         LA    R8,KEY                                                           
         USING STABUCKD,R8                                                      
*                                                                               
         XC    KEY,KEY                                                          
         MVC   STABKCOD,=X'0E01'                                                
         MVC   STABKAM(3),SVCLTHDR+1  MOVE A-M/CLT FROM CLTHDR                  
         MVC   STABKPRD,SVPRDCD                                                 
         MVC   STABKEST,SVEST1                                                  
         DROP  R8                                                               
*                                                                               
         MVC   KEYSAVE,KEY                                                      
         GOTO1 DATAMGR,DMCB,DMRDHI,SPTDIR,KEYSAVE,KEY                           
         J     SP32                                                             
*                                                                               
SP30     GOTO1 DATAMGR,DMCB,DMRSEQ,SPTDIR,KEYSAVE,KEY                           
*                                                                               
SP32     CLC   KEY(7),KEYSAVE      SAME 0E01/AM/CLT/PRD/EST                     
         JNE   EXIT                                                             
*                                                                               
         LA    R8,REC                                                           
         USING STABUCKD,R8                                                      
*                                                                               
         GOTO1 DATAMGR,DMCB,DMGET,SPTFILE,KEY+14,REC,DMWORK                     
*                                                                               
         LA    R6,STABELEM                                                      
         USING STABELEM,R6                                                      
*                                                                               
         MVI   ELCDLO,X'0E'        GET NORMAL ELS                               
         MVI   ELCDHI,X'1E'        AND UNBILLING ELS                            
         BRAS  RE,NEXTEL2                                                       
         B     *+8                                                              
*                                                                               
SP40     BRAS  RE,NEXTEL                                                        
         BNE   SP30                                                             
*                                                                               
         CLC   STABPER,SVMOS       MATCH Y/M OF SERVICE                         
         BNE   SP40                                                             
*                                                                               
         CLC   STABBDT,SVBILLD2    RIGHT BILL DATE                              
         BNE   SP40                NO - SKIP                                    
*                                                                               
         MVC   HALF,STABINV                                                     
         NI    HALF,X'3F'          DROP X'80'+X'40'                             
         CLC   HALF,SVINVB         RIGHT INVOICE                                
         BNE   SP40                                                             
                                                                                
*=============================================================                  
* MOVE VENDOR/DOLLARS TO BUFFER                                                 
*=============================================================                  
                                                                                
         LA    R1,STAWORK          UNPACK MKT/STATION                           
         USING STAPACKD,R1                                                      
*                                                                               
         XC    STAWORK,STAWORK                                                  
         MVC   STAPACOM,ADCOMFAC                                                
         MVI   STAPACT,C'U'                                                     
*                                                                               
         MVI   STAPCTRY,C'U'                                                    
         CLI   RCCTRY,X'08'        TEST CANADA                                  
         JNE   *+8                                                              
         MVI   STAPCTRY,C'C'                                                    
*                                                                               
         MVC   STAPAGY,SEAGYA                                                   
         MVC   STAPMED,SVQMED                                                   
         MVC   STAPMKST,STABKMKT   PACKED MKT-STA                               
*                                                                               
         GOTO1 VSTAPACK,STAWORK                                                 
*                                                                               
         SAM31                                                                  
         MVC   BUFFVNDR,SPACES                                                  
         MVC   BUFFVNDR(8),STAPQSTA   MOVE 8 CHAR STATION                       
         CLI   SVQMED,C'T'                                                      
         BNE   *+8                                                              
         MVI   BUFFVNDR+4,C' '                                                  
*                                                                               
         MVC   SVSTA,STAPQSTA         SAVE 5 CHAR STATION                       
         CLI   SVQMED,C'T'                                                      
         BNE   *+8                                                              
         MVI   SVSTA+4,C'T'                                                     
*                                                                               
         CLI   STAPQSTA+4,C'N'        TEST NETWORK                              
         BNE   SP50                                                             
         LLC   RE,STABKSTA+2          GET NETWORK NUMBER                        
         SLL   RE,2                   X 4                                       
         A     RE,SENETLST                                                      
         MVC   BUFFVNDR+4(4),0(RE)    MOVE NETWORK                              
         DROP  R1                                                               
*                                                                               
SP50     ICM   R0,15,STABGRS                                                    
         CLI   0(R6),X'1E'         TEST UNBILLING ELEM                          
         BNE   *+6                                                              
         LCR   R0,R0                                                            
         CVD   R0,DUB                                                           
         ZAP   BUFFGRS,DUB                                                      
*                                                                               
         ICM   R0,15,STABNET                                                    
         CLI   0(R6),X'1E'         TEST UNBILLING ELEM                          
         BNE   *+6                                                              
         LCR   R0,R0                                                            
         CVD   R0,DUB                                                           
         ZAP   BUFFNET,DUB                                                      
* GET STATION ADDRESS                                                           
         BRAS  RE,GETSTADD         RUNS IN 24-BIT MODE                          
*                                                                               
         SAM31                                                                  
         AHI   R4,BUFFERLN                                                      
         XC    0(BUFFERLN,R4),0(R4) CLEAR NEXT ENTRY                            
         SAM24                                                                  
         J     SP30                                                             
         LTORG                                                                  
         EJECT                                                                  
*===============================================================                
* PROCESSING FOR PRINTPAK                                                       
*===============================================================                
                                                                                
PR00     NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         XC    KEY,KEY                                                          
         LA    R8,KEY                                                           
         USING PCLTKEY,R8                                                       
*                                                                               
         MVC   PCLTKAGY,SEAGYA                                                  
         MVC   PCLTKMED,SVQMED                                                  
         MVI   PCLTKRCD,X'02'                                                   
         MVC   PCLTKCLT,SVQCLT                                                  
*                                                                               
         CLC   PCLTKEY,KEY         TEST ALREADY HAVE IT                         
         BE    PR10                                                             
         DROP  R8                                                               
*                                                                               
         MVC   KEYSAVE,KEY                                                      
         GOTO1 DATAMGR,DMCB,DMRDHI,PRTDIR,KEYSAVE,KEY                           
         CLC   KEY(25),KEYSAVE                                                  
         JNE   *+2                                                              
*                                                                               
         GOTO1 DATAMGR,DMCB,DMGET,KEY+27,SVCLTHDR,DMWORK                        
*                                                                               
* NOW READ INSERTIONS                                                           
                                                                                
PR10     L     R4,ABVNDBUF                                                      
         USING BUFFERD,R4                                                       
         SAM31                                                                  
         XC    0(2*BUFFERLN,R4),0(R4) CLEAR 2 ENTRIES                           
         SAM24                                                                  
*                                                                               
         XC    LASTPUB,LASTPUB                                                  
*                                                                               
         XC    KEY,KEY                                                          
         LA    R8,KEY                                                           
         USING PBUYKEY,R8                                                       
*                                                                               
         MVC   PBUYKAGY,SEAGYA                                                  
         MVC   PBUYKMED,SVQMED                                                  
         MVI   PBUYKRCD,X'20'                                                   
         MVC   PBUYKCLT,SVQCLT                                                  
         MVC   PBUYKPRD,SVQPRD                                                  
*                                                                               
PR12     MVI   DMINBTS,X'08'       READ DELETED RECORDS !                       
         MVC   KEYSAVE,KEY                                                      
         GOTO1 DATAMGR,DMCB,(X'08',DMRDHI),PRTDIR,KEYSAVE,KEY                   
         B     PR16                                                             
*                                                                               
PR14     LA    R8,KEY                                                           
         GOTO1 DATAMGR,DMCB,(X'08',DMRSEQ),PRTDIR,KEY,KEY                       
*                                                                               
PR16     CLC   KEY(10),KEYSAVE     SAME AG/MED/TY/CL/PRD                        
         JNE   EXIT                NO- DONE                                     
*                                                                               
         CLC   PBUYKEST,SVEST2     ACTUAL TO BILLED ESTIMATE                    
         BNE   PR14                                                             
         DROP  R8                                                               
*                                                                               
         GOTO1 DATAMGR,DMCB,DMGET,PRTFILE,KEY+27,REC,DMWORK                     
*                                                                               
         LA    R8,REC                                                           
         USING PBUYREC,R8                                                       
*                                                                               
         CLC   PBDBDATE(2),SVMOS   MATCH Y/M OF SERVICE                         
         BNE   PR14                                                             
* MAKE SURE X'00' FOLLOWS REC                                                   
         SR    RE,RE                                                            
         ICM   RE,3,PBUYLEN                                                     
         LA    RE,0(RE,R8)                                                      
         XC    0(2,RE),0(RE)                                                    
*                                                                               
         MVI   ELCDLO,X'26'                                                     
         MVI   ELCDHI,X'26'                                                     
         LA    R6,PBDELEM                                                       
         J     *+8                                                              
*                                                                               
PR20     L     R6,SV26EL                                                        
         BRAS  RE,NEXTEL                                                        
*                                                                               
PR22     JNE   PR14                                                             
         ST    R6,SV26EL                                                        
*                                                                               
         USING PBILELD,R6                                                       
*                                                                               
PR24     CLC   PBPRD,SVQPRD                                                     
         BNE   PR20                                                             
*                                                                               
         CLC   PBINVNO,SVINVB                                                   
         BNE   PR20                                                             
*                                                                               
         CLC   PBLDATE,SVBILLD3    RIGHT BILL DATE                              
         BNE   PR20                NO - SKIP                                    
*                                                                               
         ICM   R0,15,PBGROSS                                                    
         CVD   R0,DUB                                                           
         ZAP   GROSS,DUB                                                        
*                                                                               
         ICM   RE,15,PBAGYCOM                                                   
         SR    R0,RE               GIVES NET                                    
         CVD   R0,DUB                                                           
         ZAP   NET,DUB                                                          
         EJECT                                                                  
*==============================================================                 
* OUTPUT VENDOR DATA TO BUFFER                                                  
*==============================================================                 
                                                                                
* WHILE R6 STILL POINTS TO PBILELEM!                                            
                                                                                
         SAM31                                                                  
         CLC   PBUYKPUB,LASTPUB    TEST SAME PUB                                
         JE    PR28                YES - ADD TO THIS PUB TOTAL                  
         OC    LASTPUB,LASTPUB     TEST FIRST TIME                              
         JZ    PR27                                                             
         AHI   R4,BUFFERLN                                                      
         XC    0(2*BUFFERLN,R4),0(R4)                                           
*                                                                               
PR27     ZAP   BUFFGRS,=P'0'                                                    
         ZAP   BUFFNET,=P'0'                                                    
*                                                                               
PR28     AP    BUFFGRS,GROSS                                                    
         AP    BUFFNET,NET                                                      
*                                                                               
         CLC   LASTPUB,PBUYKPUB    TEST SAME PUB                                
         JE    PR32                                                             
*                                                                               
         MVC   LASTPUB,PBUYKPUB    SAVE LAST PUB LOOKED UP                      
*                                                                               
         LA    R0,6                                                             
         CLI   PBUYKEDT,0          DO WE HAVE AN EDITION?                       
         JH    PR30                                                             
         LA    R0,5                                                             
         CLI   PBUYKZON,0          DO WE HAVE A ZONE?                           
         JH    PR30                                                             
         LA    R0,4                                                             
*                                                                               
* NOTE HEXOUT CALL IN 31-BIT MODE                                               
*                                                                               
PR30     GOTO1 HEXOUT,DMCB,PBUYKPUB,BUFFVNDR,(R0),(31,=C'TOG')                  
*                                                                               
         SAM24                                                                  
         MVC   SVPRTKEY,KEY        SAVE CURRENT PRTDIR KEY                      
         BAS   RE,GETPUBNM                                                      
                                                                                
* REMEMBER READ PUBDIR, NOT PRTDIR SO JUST MOVE KEYS BACK                       
                                                                                
         MVC   KEY,SVPRTKEY        RESTORE PRTDIR KEY                           
         MVC   KEYSAVE,KEY          AND KEYSAVE                                 
*                                                                               
PR32     J     PR20                                                             
         LTORG                                                                  
         EJECT                                                                  
*===========================================================                    
* GET PUB NAME                                                                  
*===========================================================                    
                                                                                
         USING BUFFERD,R4                                                       
GETPUBNM NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         XC    KEY,KEY                                                          
         LA    R8,KEY                                                           
         USING PUBKEY,R8                                                        
*                                                                               
         LA    RE,REC                                                           
         MVC   PUBKMED,PBUYKMED-PBUYKEY(RE)                                     
         MVC   PUBKPUB(6),PBUYKPUB-PBUYKEY(RE)                                  
         MVC   PUBKAGY,SEAGYA                                                   
         MVI   PUBKCOD,X'81'                                                    
         DROP  R8                                                               
*                                                                               
         MVC   KEYSAVE,KEY                                                      
         GOTO1 DATAMGR,DMCB,DMRDHI,PUBDIR,KEYSAVE,KEY                           
*                                                                               
         CLC   KEY(L'PUBKEY),KEYSAVE                                            
         JNE   *+2                                                              
*                                                                               
         LARL  R8,MYPUBREC                                                      
         USING PUBREC,R8                                                        
*                                                                               
         GOTO1 DATAMGR,DMCB,DMGET,PUBFILE,KEY+27,(R8),DMWORK                    
*                                                                               
         LA    R6,PUBREC+33        FIND PUB NAMEL TO SEND NAME/ZONE             
         MVI   ELCDLO,X'10'                                                     
         MVI   ELCDHI,X'10'                                                     
         BRAS  RE,NEXTEL2                                                       
         JNE   *+2                                                              
*                                                                               
GETPUBN2 SAM31                                                                  
         MVC   BUFFNAME(20),PUBNAME-PUBNAMEL(R6)                                
         CLI   PUBZNAME-PUBNAMEL(R6),C' '                                       
         JNH   GETPUBNX                                                         
         MVI   BUFFNAME+16,C'/'                                                 
         MVC   BUFFNAME+17(7),PUBZNAME-PUBNAMEL(R6)                             
*                                                                               
GETPUBNX OC    BUFFNAME,SPACES                                                  
         SAM24                                                                  
         J     EXIT                                                             
         DROP  R8                                                               
         LTORG                                                                  
         EJECT                                                                  
*============================================================                   
* PROCESSING FOR NETWORK SYSTEM                                                 
*============================================================                   
                                                                                
NET00    NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         L     R4,ABVNDBUF         POINT TO OUTPUT BUFFER                       
         USING BUFFERD,R4                                                       
*                                                                               
         SAM31                                                                  
         XC    0(BUFFERLN,R4),0(R4)                                             
         SAM24                                                                  
*                                                                               
NET02    CLC   LASTMDCL,SVQMED     TEST SAME MEDIA/CLIENT                       
         JE    NET10                                                            
*                                                                               
         MVC   LASTMDCL,SVQMED     SAVE NEW MEDIA/CLIENT                        
         BAS   RE,GETSPCLT         READ THE CLIENT HEADER                       
         BAS   RE,BLDBUFF          BUILD BUFFERIN RECORDS FOR THIS CLT          
                                                                                
NET10    XC    BFKEY,BFKEY         LOOK FOR RECORDS IN BUFFER                   
         MVC   BFQPRD,SVQPRD                                                    
         MVC   BFEST,SVEST1                                                     
         MVC   BFBILDAT,SVBILLD2   BILL DATE                                    
         PACK  DUB,SVINV+2(4)                                                   
         CVB   R0,DUB                                                           
         STCM  R0,3,BFBILINV                                                    
*                                                                               
         MVI   SVMOSP+2,1          ADD DAY FOR DATCON                           
         GOTO1 DATCON,DMCB,(1,SVMOSP),(3,FULL)                                  
         MVC   BFBILMOS(2),FULL    YEAR/MONTH OF SVC - YYMM                     
*                                                                               
         MVC   BFKEYOLD,BFREC      SAVE KEY TRHU MOS                            
         GOTO1 =V(BUFFERIN),DMCB,('BUFFARDH',BUFFET),BFREC,ADCOMFAC             
         TM    4(R1),BUFFEEOF                                                   
         JO    EXIT                                                             
*                                                                               
NET20    CLC   BFKEYOLD,BFREC                                                   
         JNE   EXIT                                                             
*                                                                               
         SAM31                                                                  
         MVC   BUFFVNDR(4),BFBILNTW   MOVE NETWORK                              
         OC    BUFFVNDR,SPACES                                                  
         MVC   SVSTA,BUFFVNDR                                                   
         MVI   SVSTA+4,C'N'                                                     
*                                                                               
         ZAP   BUFFGRS,BFBILGRS                                                 
         ZAP   BUFFNET,BFBILNET                                                 
*                                                                               
         BRAS  RE,GETSTADD         RUNS IN 24-BIT MODE                          
*                                                                               
         SAM31                                                                  
         AHI   R4,BUFFERLN                                                      
         XC    0(BUFFERLN,R4),0(R4)                                             
         SAM24                                                                  
*                                                                               
         GOTO1 =V(BUFFERIN),DMCB,('BUFFASEQ',BUFFET),BFREC,ADCOMFAC             
         TM    4(R1),BUFFEEOF                                                   
         JO    EXIT                                                             
*                                                                               
         J     NET20                                                            
*                                                                               
*============================================================                   
* READ CLIENT RECORD FOR SPOT                                                   
*============================================================                   
                                                                                
GETSPCLT NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         XC    KEY,KEY                                                          
         MVC   KEY+1(1),SESPTAGB                                                
         CLI   SVSYS,C'S'          TEST SPOT                                    
         JE    *+10                                                             
         MVC   KEY+1(1),SENETAGB                                                
*                                                                               
         MVI   BYTE,X'01'                                                       
         CLI   SVQMED,C'T'                                                      
         BE    GETSPC2                                                          
         MVI   BYTE,X'02'                                                       
         CLI   SVQMED,C'R'                                                      
         BE    GETSPC2                                                          
         MVI   BYTE,X'03'                                                       
         CLI   SVQMED,C'N'         MEDIA N FOR CANADIAN AGY                     
         BE    GETSPC2                                                          
         CLI   SVSYS,C'N'          MEDIA N FOR NETPAK AGY                       
         BE    GETSPC2                                                          
         MVI   BYTE,X'04'                                                       
         CLI   SVQMED,C'X'                                                      
         BE    GETSPC2                                                          
         DC    H'0'                                                             
*                                                                               
GETSPC2  OC    KEY+1(1),BYTE                                                    
         GOTO1 VCLPACK,DMCB,SVQCLT,KEY+2                                        
*                                                                               
         MVC   KEYSAVE,KEY                                                      
         GOTO1 DATAMGR,DMCB,DMRDHI,SPTDIR,KEYSAVE,KEY                           
         CLC   KEY(13),KEYSAVE                                                  
         JNE   *+2                                                              
*                                                                               
         GOTO1 DATAMGR,DMCB,DMGET,SPTFILE,KEY+14,SVCLTHDR                       
         J     EXIT                                                             
         LTORG                                                                  
         EJECT                                                                  
*==============================================================                 
* GET STATION ADDRESS RECORD                                                    
*==============================================================                 
                                                                                
GETSTADD NTR1  BASE=*,LABEL=*                                                   
         SAM24                                                                  
         XC    STAKEY,STAKEY                                                    
         LA    R8,STAKEY                                                        
         USING ADDRREC,R8                                                       
*                                                                               
         MVI   ADDKTYPE,C'A'                                                    
         MVC   ADDKMED,SVQMED                                                   
         CLI   SVSYS,C'N'          TEST NETWORK                                 
         JNE   *+8                                                              
         MVI   ADDKMED,C'N'        ALWAYS READ MEDIA N                          
         MVC   ADDKCALL,SVSTA      5 CHAR STATION                               
         MVC   ADDKAGY,SEAGYA                                                   
*                                                                               
         LARL  R8,MYPUBREC                                                      
         USING ADDRREC,R8                                                       
*                                                                               
         GOTO1 DATAMGR,DMCB,DMRDHI,STATION,STAKEY,(R8)                          
         CLC   STAKEY(9),ADDRKEY                                                
         JE    GETSTAD2                                                         
         SAM31                                                                  
         MVC   BUFFNAME(24),=CL24'***** UNKNOWN *****'                          
         J     GETSTADX                                                         
*                                                                               
GETSTAD2 SAM31                                                                  
         MVC   BUFFNAME(12),ANAME                                               
         MVI   BUFFNAME+12,C'/'                                                 
         MVC   BUFFNAME+13(11),A2LINE       USUALLY CITY                        
*                                                                               
GETSTADX OC    BUFFNAME,SPACES                                                  
         SAM24                                                                  
         J     EXIT                                                             
         LTORG                                                                  
         EJECT                                                                  
*=========================================================                      
* SINCE NETWORK BILLS ARE NOT IN SEQUENCE BY PRODUCT                            
* BUILD A BUFFER OF ALL UNIT BILLING RECORDS FOR CLIENT                         
*=========================================================                      
                                                                                
BLDBUFF  NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         L     R8,ABVNDDTS         POINT TO REQ START/END DATES                 
*                                                                               
         GOTO1 DATCON,DMCB,(3,0(R8)),(2,QSTARTB)                                
         GOTO1 DATCON,DMCB,(3,3(R8)),(2,QENDB)                                  
*                                                                               
         GOTOR =V(BUFFERIN),DMCB,('BUFFAINI',BUFFET),0,ADCOMFAC                 
                                                                                
*===============================================================                
* NOW READ UNIT BILLING RECORDS FOR THIS CLIENT FROM XSPFILE!!!                 
* AND TABLE RECORDS WITHIN THE LAST 7 DAYS                                      
*===============================================================                
                                                                                
         LA    R8,BIGKEY                                                        
         USING NUBK0KEY,R8                                                      
*                                                                               
         XC    NUBK0KEY,NUBK0KEY                                                
         MVI   NUBK0SYS,NUBK0SYQ   X'0E'                                        
         MVI   NUBK0STY,NUBK0STQ   X'0A'                                        
         MVC   NUBK0AM(3),SVCLTHDR+1 A-M/CLT                                    
*                                                                               
         MVC   BIGKEYSV,BIGKEY                                                  
         GOTO1 DATAMGR,DMCB,DMRDHI,XSPDIR,BIGKEYSV,BIGKEY                       
         B     BLDB12                                                           
*                                                                               
BLDB10   GOTO1 DATAMGR,DMCB,DMRSEQ,XSPDIR,BIGKEYSV,BIGKEY                       
*                                                                               
BLDB12   CLC   BIGKEY(5),BIGKEYSV  SAME A-M/CLT                                 
         JNE   BLDBPRT                                                          
*                                                                               
         LA    R8,BIGKEY                                                        
         USING NUBK0KEY,R8                                                      
*                                                                               
         CLC   NUBK0DAT,QSTARTB    TEST AIR DATE IN REQUEST PERIOD              
         JNH   BLDB10              PRIOR -IGNORE                                
         CLC   NUBK0DAT,QENDB      AFTER REQUEST PERIOD                         
         JH    BLDB10                                                           
         DROP  R8                                                               
*                                                                               
         LA    R8,REC                                                           
         USING NUBRECD,R8                                                       
*                                                                               
         LA    R0,BIGKEY+NUBDA-NUBKEY                                           
         GOTO1 DATAMGR,DMCB,DMGET,XSPFILE,(R0),REC,DMWORK                       
*                                                                               
         LA    R6,NUBDATA                                                       
         USING NBILD,R6                                                         
*                                                                               
         MVI   ELCDLO,NBILELQ      GET X'10' BILLING ELEMENTS                   
         MVI   ELCDHI,NBILELQ                                                   
         BRAS  RE,NEXTEL2                                                       
         J     *+8                                                              
*                                                                               
BLDB20   BRAS  RE,NEXTEL                                                        
         BNE   BLDB10                                                           
*                                                                               
         XC    BFREC,BFREC                                                      
         MVC   BFQPRD,NBILPRDC     3-CHAR PRD CODE                              
         MVC   BFEST,NUBK0EST                                                   
         MVC   BFBILDAT,NUBK0BDT                                                
         MVC   BFBILINV,NBILNUM                                                 
         MVC   BFBILMOS,NBILMOS                                                 
         MVC   BFBILNTW,NUBK0NET                                                
*&&DO                                                                           
*------------------->>>                                                         
*------------------->>>                                                         
         CLC   BFQPRD,SVQPRD       TEST TO FILTER ON ONE PRD                    
         JNE   BLDB10                                                           
         CLC   BFEST,SVEST1                                                     
         JNE   BLDB10                                                           
*------------------->>>                                                         
*------------------->>>                                                         
*&&                                                                             
*                                                                               
BLDB22   L     R0,NBILGRS                                                       
         CVD   R0,DUB                                                           
         ZAP   BFBILGRS,DUB                                                     
*                                                                               
         L     R0,NBILNET                                                       
         CVD   R0,DUB                                                           
         ZAP   BFBILNET,DUB                                                     
*                                                                               
         CP    BFBILGRS,=P'0'                                                   
         JNE   BLDB32                                                           
         CP    BFBILNET,=P'0'                                                   
         JZ    BLDB20                                                           
                                                                                
BLDB32   GOTO1 =V(BUFFERIN),DMCB,('BUFFAPUT',BUFFET),BFREC,ADCOMFAC             
         J     BLDB20                                                           
         EJECT                                                                  
*===============================================================                
* PRINT THE RECORDS IN BUFFER                                                   
*===============================================================                
                                                                                
BLDBPRT  DS    0H                                                               
         J     EXIT                                                             
* ROUTINE IS NOP UNTIL NEEDED AGAIN!                                            
         XC    BFKEY,BFKEY                                                      
         MVC   BFQPRD,SVQPRD                                                    
*                                                                               
         MVC   BFKEYOLD,BFREC      SAVE KEY                                     
         GOTO1 =V(BUFFERIN),DMCB,('BUFFARDH',BUFFET),BFREC,ADCOMFAC             
*                                                                               
BLDBPRT2 CLC   BFKEYOLD(3),BFREC                                                
         JNE   EXIT                                                             
         GOTO1 HEXOUT,DMCB,BFREC,P,32,(31,=C'TOG')                              
         GOTO1 ACREPORT                                                         
*                                                                               
         GOTO1 =V(BUFFERIN),DMCB,('BUFFASEQ',BUFFET),BFREC,ADCOMFAC             
         TM    4(R1),BUFFEEOF                                                   
         JO    EXIT                                                             
         J     BLDBPRT2                                                         
*==============================================================                 
* READ NETWORK DEFINITION PASSIVES TO DECODE THE BUYKEY NETWORK                 
*==============================================================                 
                                                                                
GETNETS  NTR1  BASE=*,LABEL=*                                                   
         LARL  R1,NETLIST                                                       
*                                                                               
GETNET02 OC    0(4,R1),0(R1)       ENTRY USED YET                               
         JZ    GETNET04                                                         
         LA    R1,L'NETLIST(R1)    1024 BYTE ENTRIES                            
         J     GETNET02                                                         
*                                                                               
GETNET04 ST    R1,SENETLST         SAVE A(NEW LIST) IN SE TABLE                 
*                                                                               
         LA    R0,4                                                             
         XC    0(256,R1),0(R1)                                                  
         LA    R1,256(R1)                                                       
         JCT   R0,*-10                                                          
*                                                                               
         XC    KEY,KEY                                                          
         MVC   KEY(2),=X'0D91'                                                  
         MVC   KEY+2(2),SEAGYA                                                  
         MVC   KEYSAVE,KEY                                                      
         GOTO1 DATAMGR,DMCB,DMRDHI,SPTDIR,KEYSAVE,KEY                           
         J     GETNET12                                                         
*                                                                               
GETNET10 GOTO1 DATAMGR,DMCB,DMRSEQ,SPTDIR,KEY,KEY                               
*                                                                               
GETNET12 CLC   KEY(4),KEYSAVE                                                   
         JNE   EXIT                                                             
*                                                                               
         LA    R4,KEY                                                           
         USING NWKPASS,R4                                                       
*                                                                               
         LLC   RE,NWKPSEQ                                                       
         SLL   RE,2                X 4 BYTES/NETWORK                            
         A     RE,SENETLST                                                      
         MVC   0(4,RE),NWKPNET     SAVE CALL LETTERS                            
         J     GETNET10                                                         
         LTORG                                                                  
         EJECT                                                                  
NEXTEL   CLI   0(R6),0                                                          
         JE    NEXTELX                                                          
         LLC   R0,1(R6)                                                         
         LTR   R0,R0                                                            
         JZ    *+2                                                              
         AR    R6,R0                                                            
NEXTEL2  CLI   0(R6),0                                                          
         JE    NEXTELX                                                          
         CLC   ELCDLO,0(R6)                                                     
         JH    NEXTEL                                                           
         CLC   ELCDHI,0(R6)                                                     
         JL    NEXTEL                                                           
         CR    RB,RB                                                            
         J     *+6                                                              
NEXTELX  LTR   RB,RB                                                            
         BR    RE                                                               
         EJECT                                                                  
         DC    CL8'*COMMON*'                                                    
COMMON   DS    0D                                                               
SVSEACC  DS    X                                                                
THISSE   DS    A                                                                
THISSENUM DS   X                                                                
LASTSENUM DS   X                                                                
*                                                                               
SPTDIR   DC    CL8'SPTDIR'                                                      
SPTFILE  DC    CL8'SPTFILE'                                                     
STATION  DC    CL8'STATION'                                                     
PRTDIR   DC    CL8'PRTDIR'                                                      
PRTFILE  DC    CL8'PRTFILE'                                                     
PUBDIR   DC    CL8'PUBDIR'                                                      
PUBFILE  DC    CL8'PUBFILE'                                                     
UNTDIR   DC    CL8'UNTDIR'                                                      
UNTFILE  DC    CL8'UNTFILE'                                                     
XSPDIR   DC    CL8'XSPDIR'                                                      
XSPFILE  DC    CL8'XSPFILE'                                                     
CTFILE   DC    CL8'CTFILE'                                                      
DMOPEN   DC    CL8'DMOPEN'                                                      
*                                                                               
          DS    0D                                                              
          DC    CL8'*SVPARMS'                                                   
SVPARMS   DS   0XL24                                                            
ABVNDWRK  DS   A                ACREPWORKD                                      
ABVNDRCV  DS   A                RECEIVABLE POSTING                              
ABVNDBUF  DS   A                OUTPUT BUFFER                                   
ABVNDDTS  DS   A                PWOS REQUEST START/END DATES                    
ABVNDID   DS   A                A(RECVBLE AGY ALPHA ID)                         
          DS   A                                                                
*                                                                               
SV26EL   DS    A                                                                
SVTRNEL  DS    A                                                                
VCLPACK  DS    A                                                                
VSTAPACK DS    A                                                                
VCBLTAB  DS    A                                                                
*                                                                               
LASTSE   DC    X'00'                                                            
*                                                                               
SVSENUMS DS    0XL8                                                             
SVSESPT  DC    X'00'                                                            
SVSENET  DC    X'00'                                                            
SVSEPRT  DC    X'00'                                                            
SPOTOPEN DC    X'00'                                                            
PRNTOPEN DC    X'00'                                                            
NETOPEN  DC    X'00'                                                            
         DC    X'00'                                                            
*                                                                               
ELCDLO   DS    X                                                                
ELCDHI   DS    X                                                                
SVAGYA   DS    CL2                                                              
SVINVB   DS    H                                                                
SVBILLD2 DS    XL2                                                              
SVBILLD3 DS    XL3                                                              
SVSYS    DS    CL1                                                              
SVSPTAGB DS    XL1                                                              
SVNETAGB DS    XL1                                                              
SVMOS    DS    XL3                                                              
SVMOSP   DS    XL3                 PWOS(2)+1 FOR DAY                            
SVINV    DS    CL6                                                              
SVQMED   DS    XL1                                                              
SVQCLT   DS    CL3                                                              
SVQPRD   DS    CL3                                                              
SVPRDCD  DS    XL1                                                              
SVEST1   DS    XL1                                                              
SVEST2   DS    H                   PRINT EST IS 2 BYTES!                        
SVPRTKEY DS    XL32                                                             
STAKEY   DS    XL15                                                             
SVSTA    DS    CL5                                                              
LASTMDCL DS    CL4                 PREVIOUS A-M/CLT                             
LASTPUB  DS    XL4                                                              
GROSS    DS    PL8                                                              
NET      DS    PL8                                                              
*                                                                               
QSTARTB  DS    XL2                                                              
QENDB    DS    XL2                                                              
*                                                                               
         DS    0D                                                               
         DC    CL8'*STAWORK'                                                    
STAWORK  DS    XL64                                                             
*                                                                               
         DS    CL8'*BIGKEY*'                                                    
BIGKEY   DS    XL64                                                             
BIGKEYSV DS    XL64                                                             
*                                                                               
BFREC    DS    0XL32                                                            
BFKEY    DS    0XL16                                                            
BFQPRD   DS    CL3                 PRODUCT NUMBER                               
BFEST    DS    XL1                 ESTIMATE NUMBER                              
BFBILDAT DS    XL2                 BILLING Y/M                                  
BFBILINV DS    XL2                 INVOICE NUMBER                               
BFBILMOS DS    XL2                 BILLING Y/M OF SVC                           
BFBILNTW DS    CL4                 NETWORK                                      
         DS    XL2                                                              
*                                                                               
BFBILGRS DS    PL8                 GROSS DOLLARS                                
BFBILNET DS    PL8                 NET DOLLARS                                  
BFRECL   EQU   *-BFREC                                                          
*                                                                               
         DC    CL8'BFKEYOLD'                                                    
BFKEYOLD DS    XL10                PRD/EST/BILLDT/INV/MOS                       
*                                                                               
         LTORG                                                                  
*                                                                               
SPTFLIST DS    0D                                                               
         DC    CL1'N',CL7'SPTDIR'                                               
         DC    CL1'N',CL7'SPTFIL'                                               
         DC    CL1'N',CL7'STAFILE'                                              
         DC    CL8'X      '                                                     
*                                                                               
PRTFLIST DS    0D                                                               
         DC    CL1'N',CL7'PRTDIR'                                               
         DC    CL1'N',CL7'PRTFIL'                                               
         DC    CL1'N',CL7'PUBDIR'                                               
         DC    CL1'N',CL7'PUBFIL'                                               
         DC    CL8'X      '                                                     
*                                                                               
NETFLIST DS    0D                                                               
         DC    CL1'N',CL7'SPTDIR'                                               
         DC    CL1'N',CL7'SPTFIL'                                               
         DC    CL1'N',CL7'UNTDIR'                                               
         DC    CL1'N',CL7'UNTFIL'                                               
         DC    CL1'N',CL7'XSPDIR'                                               
         DC    CL1'N',CL7'XSPFIL'                                               
         DC    CL1'N',CL7'STAFILE'                                              
         DC    CL8'X      '                                                     
*                                                                               
*                                                                               
         DS    0D                                                               
         DC    CL8'*SVCLT*'                                                     
SVCLTHDR DS    2000C                                                            
*                                                                               
         DC    CL8'*SETAB*'                                                     
SETAB    DC    8XL16'00'                                                        
SETABX   EQU   *                                                                
*                                                                               
BUFFET   BUFFD TYPE=P,KEYLEN=L'BFKEY,COLUMNS=2,FILE=BUFFWK,            *        
               BUFFERS=20                                                       
*                                                                               
         DS    0D                                                               
         DC    CL8'**REC***'                                                    
REC      DS    6000C                                                            
*                                                                               
         DS    0D                                                               
         DC    CL8'*PUBREC*'                                                    
MYPUBREC DS    2000C                                                            
*                                                                               
         DS    0D                                                               
         DC    CL8'*NETLIST'       CANADIAN NETWORK LIST                        
NETLIST  DS    8XL1024                                                          
NETLISTX EQU   *                                                                
*                                                                               
SETABD   DSECT                                                                  
SEAGYA   DS    CL2                                                              
SENUMSPT DS    XL1                 SPOT SE NUMBER                               
SESPTAGB DS    XL1                 BINARY AGY CODE                              
SESPTOPN DS    XL1                 C'Y' IF FILE IS OPEN                         
SENUMNET DS    XL1                                                              
SENETAGB DS    XL1                                                              
SENETOPN DS    XL1                                                              
SENETLST DS    AL4                                                              
SENUMPRT DS    XL1                                                              
SEPRTOPN DS    XL1                                                              
*                                                                               
BUFFERD  DSECT                                                                  
BUFFVNDR DS    CL12                                                             
BUFFGRS  DS    PL6                                                              
BUFFNET  DS    PL6                                                              
BUFFNAME DS    CL24                                                             
BUFFERLN EQU   *-BUFFERD                                                        
*                                                                               
         PRINT OFF                                                              
       ++INCLUDE ACREPWORKD                                                     
STABUCKD DSECT                                                                  
       ++INCLUDE SPGENSTAB                                                      
       ++INCLUDE SPGENNDEF                                                      
CLTHDRD  DSECT                                                                  
       ++INCLUDE SPGENCLT                                                       
PCLTRECD DSECT                                                                  
       ++INCLUDE PCLTREC                                                        
PBUYRECD DSECT                                                                  
       ++INCLUDE PBUYREC                                                        
       ++INCLUDE PBDELEM                                                        
PBILELD  DSECT                                                                  
       ++INCLUDE PBILELEM                                                       
PUBRECD  DSECT                                                                  
       ++INCLUDE PUBREC                                                         
PUBNAMELD DSECT                                                                 
       ++INCLUDE PUBNAMEL                                                       
         PRINT OFF                                                              
       ++INCLUDE CTGENFILE                                                      
*PREFIX=AC_                                                                     
       ++INCLUDE ACGENFILE                                                      
*PREFIX=                                                                        
       ++INCLUDE DDREPMASTD                                                     
       ++INCLUDE DDBUFFD                                                        
       ++INCLUDE FAUTL                                                          
       ++INCLUDE SPSTAPACKD                                                     
       ++INCLUDE NEGENUBILL                                                     
       ++INCLUDE SPGENADD                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'019ACREPABVND04/20/20'                                      
         END                                                                    
