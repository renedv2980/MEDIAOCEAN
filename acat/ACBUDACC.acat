*          DATA SET ACBUDACC   AT LEVEL 019 AS OF 05/01/02                      
*CATALP BUDACC                                                                  
         TITLE 'BUDACC - MODULE TO READ OR TABULARIZE BUDGET RECORDS'           
BUDACC   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 015,*BUDACC*                                                     
         L     RA,0(R1)                                                         
         USING BUDACCD,RA                                                       
         USING BUDAWRKD,RC                                                      
*                                                                               
         L     RF,RELOC            ESTABLISH RELOCATION FACTOR.                 
         LA    RE,RELOC                                                         
         SR    RE,RF                                                            
         ST    RF,WRELOC                                                        
         ST    R1,APARM                                                         
         ST    RD,SAVED                                                         
*                                                                               
         ZIC   RF,BUTYPE           GO TO PROPER CODE FOR TYPE OF CALL.          
         SLL   RF,2                                                             
         B     ATYPRTN-4(RF)                                                    
*                                                                               
ATYPRTN  DS    0A                                                               
         B     TBLRTN              TABLE BUILDING CODE.                         
         B     READRTN             SIMPLE READING CODE.                         
         EJECT                                                                  
READRTN  OC    BULSTKEY,BULSTKEY                                                
         BNZ   *+8                 NOT FIRST ENTRY.                             
         BAS   RE,BUDAINIT         FIRST ENTRY.                                 
         ZICM  RF,BUCMND                                                        
         BZ    READ040             NO PREEMPTIVE COMMAND.                       
         SLL   RF,2                                                             
         B     ACMNDRTN-4(RF)                                                   
*                                                                               
READ040  CLI   BUMODE,TYPEFRST     WAS LAST MODE FIRST FOR TYPE RECORD.         
         BNE   READ060             NO.                                          
         MVI   BUMODE,PROCAMNT     THIS MODE IS PROCESS AMOUNT RECORD.          
         B     OKEXIT                                                           
*                                                                               
READ060  CLI   BUMODE,PROCAMNT     WAS LAST MODE PROCAMNT.                      
         BNE   READ140             NO.                                          
READ070  BAS   RE,NEXTAMNT         READ NEXT AMOUNT RECORD.                     
         MVC   BULSTKEY,BUAMTREC   SAVE THIS KEY.                               
         LA    RF,BUSERKEY+L'BUSERKEY-1                                         
         LA    R1,L'BUSERKEY                                                    
*                                                                               
READ080  CLI   0(RF),C' '          FIND SIGNIFICANT L'USER KEY.                 
         BNE   READ100                                                          
         BCTR  RF,0                                                             
         BCT   R1,READ080                                                       
         B     READ108                                                          
*                                                                               
READ100  BCTR  R1,0                R1 = SIGNIFICANT L'USERKEY-1.                
         EX    R1,*+8                                                           
         B     *+10                                                             
         CLC   BUAMTREC+1(0),BUSERKEY                                           
         BNE   READEND             RAN OUT OF KEYS.                             
READ108  BAS   RE,BUFILT                                                        
         BNE   READ070              FAILED FILTERING, TRY THE NEXT.             
*                                                                               
         USING ACKEYD,R8                                                        
         LA    R8,BUAMTREC                                                      
         CLC   ACBTKBNO,BUTYPREC+ACBTKNO1-ACKEYD                                
         BNE   READ120             CHANGE OF BUDGET NUMBER.                     
         MVI   BUMODE,PROCAMNT     SAME NUMBER, CONTINUE.                       
         B     RETURNER                                                         
*                                                                               
READ120  BAS   RE,GETTYPE          GET TYPE FOR AMOUNT RECORD.                  
         CLC   ACBTKNO1,BUAMTREC+(ACBTKBNO-ACKEYD)                              
         BE    *+14                TYPE CODE MATCHES AMOUNT TYPE.               
         XC    BUTYPREC(42),BUTYPREC SILLY MISMATCH SO CLEAR TYPE REC           
         B     READ070             AND READ THE NEXT AMOUNT RECORD.             
         BAS   RE,BUFILT                                                        
         BNE   READ070             FAILED FILTERING, GET NEXT AMOUNT.           
         MVI   BUMODE,TYPEFRST     PASS FIRST FOR TYPE.                         
         B     RETURNER                                                         
*                                                                               
READ140  DC    H'0'                NO OTHER MODES AS YET.                       
*                                                                               
READEND  MVI   BUMODE,FINISHED     TELL USER HE IS FINISHED.                    
         XC    BULSTKEY,BULSTKEY   MAKE LIFE DIFFICULT FOR USER WHO             
         MVI   BUCMND,0            IGNORES THIS INFORMATION.                    
         XC    BUTYPREC(42),BUTYPREC                                            
         CR    RB,RB               GIVE CC OF EQ FOR GOOD EXIT.                 
         B     FASTXIT             RETURN TO USER.                              
         EJECT                                                                  
*              BUILD TABLE OF BUDGETS FOR SPECIFIC USER KEY.                    
*                                                                               
TBLRTN   OC    BULSTKEY,BULSTKEY                                                
         BNZ   *+8                 NOT FIRST ENTRY.                             
         BAS   RE,BUDAINIT         FIRST ENTRY.                                 
         L     RF,APARM                                                         
         XC    4(4,RF),4(RF)       CLEAR USER PARA2 FOR NUM OF ENTRIES.         
         L     RE,BUATYTBL         A(USER SUPPLIED TYPE TABLE AREA).            
         L     RF,BUTYTBLN         L'THAT AREA.                                 
         XCEF                                                                   
         L     RE,BUAMTTBL         A(USER SUPPLIED AMOUNT TABLE AREA).          
         L     RF,BUAMTBLN         L'THAT AREA.                                 
         XCEF                                                                   
         MVC   WANXTAMT,BUAMTTBL   INIT A(NEXT AMOUNT RECORD ENTRY).            
         MVC   WANXTTYP,BUATYTBL   INIT A(NEXT TYPE RECORD ENTRY).              
         MVC   BUTYTEND,BUATYTBL   INIT A(TYPE TABLE END).                      
         MVC   BUAMTEND,BUAMTTBL   INIT A(AMOUNT TABLE END).                    
         CLI   BUTBLPER,1                                                       
         BH    *+8                                                              
         MVI   BUTBLPER,1                                                       
*                                                                               
TBL020   L     RF,BUAMTTBL         IS THERE ROOM FOR BASIC AMOUNT REC.          
         A     RF,BUAMTBLN         DETAILS + ONE PERIOD + 1.                    
         S     RF,WANXTAMT                                                      
         C     RF,=A(BUAMLN1Q+BUAMLN2Q)                                         
         BNL   TBL040                                                           
         DC    H'0'                AMOUNT TABLE FULL                            
*                                                                               
TBL040   BAS   RE,NEXTAMNT         GET AN AMOUNT RECORD                         
         CLC   BUAMTREC+1(L'ACKEYACC),BUSERKEY                                  
         BNE   READEND             END OF BUDGETS FOR USER KEY.                 
         BAS   RE,BUFILT                                                        
         BNE   TBL050              FAILED FILTERS.                              
         BAS   RE,TYPCHEK                                                       
         BL    TBL045              TYPE NOT YET IN TABLE.                       
         L     R7,WANXTPTR         R7 = A(1ST FREE SLOT FOR AMT POINTR)         
         MVC   0(4,R7),WANXTAMT    MOVE IN A(NEXT AMOUNT SLOT).                 
         B     TBL100                                                           
TBL045   BAS   RE,GETTYPE          PASSED, GET THE TYPE RECORD.                 
         CLC   BUAMTREC+(ACBTKBNO-ACKEYD)(2),BUTYPREC+(ACBTKNO1-ACKEYD)         
         BNE   TBL050              TYPE AND AMOUNT DON'T MATCH.                 
         BAS   RE,BUFILT                                                        
         BE    TBL060              PASSED THE FILTERS.                          
TBL050   MVC   BULSTKEY,BUAMTREC   SET UP TO GET NEXT SEQ AMOUNT REC.           
         B     TBL040                                                           
*                                                                               
         USING BUTYTBLD,R7                                                      
         USING ACKEYD,R8           R8 = A(TYPE RECORD) FROM GETTYPE.            
TBL060   L     R7,WTHISTYP         R7 = A(THIS TYPE ENTRY).                     
         MVC   BUTNUM,ACBTKNO1     MOVE IN THE BUDGET NUMBER.                   
         MVC   BUTCODE,ACBTKCOD    AND BUDGET CODE.                             
         MVC   BUTSTAT,ACSTATUS    SET TYPE RECORD STATUS.                      
         MVC   BUTAPTRS,WANXTAMT   SET A(THIS AMOUNT TABLE ENTRY).              
         LA    R8,ACRECORD-ACKEYD(R8)                                           
         USING ACBCD,R8            R8 = A(1ST ELEMENT).                         
TBL070   CLI   ACBCEL,0                                                         
         BNE   *+6                                                              
         DC    H'0'                END OF REC WITHOUT HEADER ELEMENT.           
         CLI   ACBCEL,ACBCELEQ                                                  
         BE    TBL080              FOUND THE COLUMN NAME ELEMENT.               
TBL075   ZIC   RF,ACBCLEN          GET THE NEXT ELEMENT.                        
         AR    R8,RF                                                            
         B     TBL070                                                           
*                                                                               
TBL080   MVC   BUTHED1,ACBCCOL1    FIRST HEADER FOR COLUMN.                     
         MVC   BUTHED2,ACBCCOL2    SECOND HEADER FOR COLUMN.                    
         L     RF,APARM            RF = A(USERS PARAMETER LIST).                
         L     R1,4(RF)            R1 = NO. OF TYPE ENTRIES SO FAR.             
         LA    R1,1(R1)            BUMP IT BY ONE.                              
         ST    R1,4(RF)                                                         
         B     TBL100              NOT INTERESTED IN THE REST.                  
         DROP  R7                                                               
*                                                                               
         USING ACKEYD,R8                                                        
         USING BUAMTBLD,R7                                                      
TBL100   L     R7,WANXTAMT         R7 = A(NEXT AMOUNT ENTRY SLOT).              
         MVI   BUANOMTH,1           SET NUM OF PER ENTRIES TO ONE.              
         LA    R8,BUAMTREC                                                      
         MVC   BUANUM,ACBTKBNO     SET BUDGET TYPE NUMBER.                      
         MVC   BUACON,ACBTKCON                                                  
         LA    R8,BUAMTREC+ACRECORD-ACKEYD   R8 = A(1ST AMOUNT ELEMENT)         
         LA    R6,BUAMNTHS         R6 = A(1ST PERIOD SLOT FOR AMOUNT).          
*                                                                               
         USING BUAMNTHD,R6                                                      
         MVI   BUASYYMM,0                                                       
         XC    WNXTPER,WNXTPER     CLEAR NEXT PERIOD ST DATE.                   
         OC    BUSTDATE,BUSTDATE   IF START DATE IS SUPPLIED, FIRST             
         BZ    *+14                PERIOD ENTRY BEGINS WITH IT.                 
         MVC   BUASYYMM,BUSTDATE                                                
         BAS   RE,GETEND           SET END DATE IN PERIOD ENTRY.                
         ZAP   BUAMUSER,=P'0'                                                   
         USING ACBAD,R8            R8 = A(1ST MNTH EL FOR AMOUNT REC).          
TBL120   CLI   ACBAEL,0                                                         
         BE    TBL160              END OF RECORD.                               
         CLI   ACBAEL,ACBAELEQ                                                  
         BE    TBL140              FOUND A MONTH ELEMENT.                       
TBL130   ZIC   RF,ACBALEN          GET THE NEXT ELEMENT.                        
         AR    R8,RF                                                            
         B     TBL120                                                           
*                                                                               
TBL140   MVC   WLASTDAT,ACBAMNTH                                                
         CLC   ACBAMNTH,BUSTDATE   ONLY USE MONTH ENTRIES WHICH FALL            
         BL    TBL130              WITHIN DATE FILTERS.                         
         CLC   ACBAMNTH,BUNDDATE                                                
         BH    TBL130                                                           
*                                                                               
TBL144   OC    BUASYYMM,BUASYYMM                                                
         BNZ   TBL150                                                           
         OC    WNXTPER,WNXTPER                                                  
         BNZ   TBL146              WE HAVE A NEXT PER START DATE.               
         MVC   WNXTPER,ACBAMNTH    WE DON'T, USE 1ST AMOUNT MONTH.              
TBL146   MVC   BUASYYMM,WNXTPER                                                 
         BAS   RE,GETEND                                                        
         ZAP   BUAMUSER,=P'0'      ZEROIZE USER'S ACCUMULATOR.                  
         B     TBL150                                                           
TBL148   ZAP   BUAMBUD,ACBABUDG    SET BUDGET AMOUNT.                           
         B     TBL130                                                           
*                                                                               
TBL150   CLC   ACBAMNTH,BUAEYYMM                                                
         BH    TBL154                                                           
         OC    BUAMBUD,BUAMBUD                                                  
         BZ    TBL148              NO PREVIOS AMNT ENTERED, MUST ZAP.           
         AP    BUAMBUD,ACBABUDG                                                 
         B     TBL130                                                           
*                                                                               
TBL154   CLC   WNXTPER,BUNDDATE    NEXT PERIOD WOULD BEGIN BEYOND               
         BH    TBL180              END DATE, WE'RE FINISHED.                    
         LA    R6,BUAMLN2Q(R6)                                                  
         ZIC   RF,BUANOMTH         COUNT AND STORE NUMBER OF                    
         LA    RF,1(RF)            MONTH ENTRIES FOR TYPE.                      
         STC   RF,BUANOMTH                                                      
         L     RF,BUAMTTBL                                                      
         A     RF,BUAMTBLN                                                      
         SR    RF,R6                                                            
         C     RF,=A(BUAMLN2Q)                                                  
         BNL   TBL144                                                           
         DC    H'0'                AMOUNT TABLE FULL                            
*                                                                               
TBL160   CLC   BUNDDATE,=X'FFFE'                                                
         BNL   TBL170              NO END DATE, USE LAST FOR BUDGET.            
         MVI   ACBALEN,0           BUDGETTING ENDS BEFORE REQUESTED             
         MVC   ACBAMNTH,=X'FFFF'   PERIOD. FORCE COMPLETION OF TABLE.           
         B     TBL144                                                           
*                                                                               
TBL170   MVC   BUAEYYMM,WLASTDAT   EQ PER ENDDATE TO BUDGET ENDDATE.            
*                                                                               
TBL180   MVC   BULSTKEY,BUAMTREC   THIS IS NOW LAST SUCCESSFUL KEY.             
         LA    R6,BUAMLN2Q(R6)     R6 = A(START OF NEXT AMOUNT ENTRY).          
         ST    R6,WANXTAMT         SAVE A(NEXT FREE AMOUNT REC SLOT).           
         ST    R6,BUAMTEND         SET CURRENT END-OF-AMOUNT TABLE.             
         B     TBL020                                                           
         DROP  R6,R7,R8                                                         
         EJECT                                                                  
*              ROUTINES FOR PREEMPTIVE USER COMMANDS.                           
*                                                                               
CMGETAMT XC    WKEY,WKEY           SET UP TO READ SPECIFIC AMOUNT REC,          
         MVI   WKEY,ACBTKTEQ       OR THE 1ST ONE IF NO BUDGET NUMBER           
         MVC   WKEY+1(L'BUSERKEY),BUSERKEY             IS SPECIFIED.            
         MVC   WKEY+L'BUSERKEY+1(2),BUBUDNO                                     
         MVC   WCOMMAND,DMRDHI                                                  
         LA    R8,BUAMTREC                                                      
         BAS   RE,READER                                                        
         MVC   BULSTKEY,BUAMTREC   SAVE THIS KEY.                               
         BAS   RE,BUFILT                                                        
         BNE   CMNXTAMT             FAILED FILTERING, TRY THE NEXT.             
         MVI   BUMODE,PROCAMNT                                                  
         B     RETURNER                                                         
*                                                                               
CMNXTAMT BAS   RE,NEXTAMNT                                                      
         MVC   BULSTKEY,BUAMTREC   SAVE THIS KEY.                               
         BAS   RE,BUFILT                                                        
         BNE   CMNXTAMT             FAILED FILTERING, TRY THE NEXT.             
         MVI   BUMODE,PROCAMNT                                                  
         B     RETURNER                                                         
*                                                                               
CMGETTYP XC    WKEY,WKEY           GET TYPE RECORD BY BUDGET NUMBER             
         MVI   WKEY,ACBTKTEQ       OR BUDGET CODE SUPPLIED BY USER.             
         MVC   WKEY+1(1),BUSERKEY  COMPANY CODE.                                
         OC    BUBUDNO,BUBUDNO     BUDGET NUMBER TAKES PRECEDENCE.              
         BZ    *+14                                                             
         MVC   WKEY+3(L'ACBTKNO1),BUBUDNO                                       
         B     *+10                                                             
         MVC   WKEY+ACBTKCOD-ACKEYD(L'ACBTKCOD),BUBUDCDE                        
         MVC   WCOMMAND,DMRDHI                                                  
         LA    R8,BUTYPREC                                                      
         BAS   RE,READER                                                        
         BAS   RE,BUFILT                                                        
         BNE   CMNXTYP             FAILED FILTERING, GET THE NEXT ONE.          
         MVI   BUMODE,PROCTYPE                                                  
         B     OKEXIT                                                           
*                                                                               
CMNXTYP  BAS   RE,NEXTYPE                                                       
         CLI   2(R8),0                                                          
         BNE   READEND             NOT A TYPE RECORD, WE'RE FINISHED.           
         BAS   RE,BUFILT                                                        
         BNE   CMNXTYP             FAILED FILTERING, TRY THE NEXT ONE.          
         MVI   BUMODE,PROCTYPE                                                  
         B     OKEXIT                                                           
         EJECT                                                                  
BUDAINIT NTR1                                                                   
         LA    RE,BUTYPREC         CLEAR RECORD AREAS.                          
         LA    RF,L'BUTYPREC+L'BUAMTREC                                         
         XCEF                                                                   
         XC    BULSTKEY,BULSTKEY                                                
         XC    BUBUNNEG(3),BUBUNNEG CLEAR NEGATIVE FILTER FLAGS.                
         MVI   BUMODE,PROCAMNT     FORCE 1ST READ TO GET AMOUNT REC.            
*                                  SET UP DATE FILTERS.                         
         OC    BUNDDATE,BUNDDATE                                                
         BNZ   BUINT05             END DATE FILTER SUPPLIED                     
         MVC   BUNDDATE,=X'FFFE'   FORCE HI ENDDATE                             
*                                                                               
*                                  CHECK FILTERS FOR NEGATIVE ETC.              
BUINT05  TM    BUBUDNO,X'80'                                                    
         BZ    *+12                BUDGET NUMBER FILTER NOT NEGATIVE.           
         MVI   BUBUNNEG,1          SET BUDGET NUM NEG FILT FLAG.                
         NI    BUBUDNO,X'7F'       UNSET FLAG IN NUMBER.                        
         OC    BUBUDCDE,BUBUDCDE                                                
         BZ    BUINT10             NO BUDGET CODE FILTER.                       
         CLC   BUBUDCDE,=CL40' '                                                
         BNE   *+14                BUDGET CODE FILTER PRESENT.                  
         XC    BUBUDCDE,BUBUDCDE   NONE, SO FORCE TO X'00'S.                    
         B     BUINT10                                                          
         TM    BUBUDCDE,X'40'                                                   
         BNZ   *+12                FILTER IS NOT NEGATIVE.                      
         MVI   BUBUCNEG,1          SET NEGATIVE BUD CODE FILT FLAG.             
         OI    BUBUDCDE,X'40'      UNSET FIELD MARKER.                          
*                                                                               
*                                  READ LIST RECORD IF NEEDED.                  
BUINT10  OC    BULSTCDE,BULSTCDE                                                
         BZ    BUINT15             NO LIST CODE TO READ.                        
         CLC   BULSTCDE,=CL40' '                                                
         BNE   *+14                LIST CODE IS PRESENT.                        
         XC    BULSTCDE,BULSTCDE   NONE, SO FORCE TO X'00'S.                    
         B     BUINT15                                                          
         CLI   BULSTCDE,C'A'                                                    
         BNL   *+12                LIST IS NOT NEGATIVE.                        
         MVI   BUNEGLST,1          X'40' OFF, LIST IS NEGATIVE.                 
         OI    BULSTCDE,X'40'      FIX THE CODE.                                
         XC    WKEY,WKEY           SET UP KEY FOR READ OF LIST RECORD.          
         MVI   WKEY,ACLKCEQU       SET RECORD TYPE.                             
         MVC   WKEY+1(5),BULSTCDE  SET LIST CODE IN KEY.                        
         MVC   WCOMMAND,DMREAD     SET COMMAND.                                 
         LA    R8,BULSTREC         R8 = A(LIST RECORD AREA).                    
         BAS   RE,READER           GET IT.                                      
*                                                                               
BUINT15  MVI   BULSTKEY,ACBTKTEQ                                                
         MVC   BULSTKEY+1(L'BUSERKEY),BUSERKEY                                  
         B     OKEXIT                                                           
         EJECT                                                                  
*              FILTER THE RECORD JUST READ.                                     
*                                  R8 = A(RECORD JUST READ).                    
*                                                                               
         USING ACKEYD,R8                                                        
BUFILT   NTR1                                                                   
         OC    BUBUDNO,BUBUDNO                                                  
         BZ    BUFIL040            NO BUDGET NUMBER FILTER.                     
         ZIC   RE,BUBUNNEG         RE = 1, FILTER IS NEGATIVE.                  
         IC    R1,BUBRMSK(RE)      R1 = BRANCHING MASK FOR BE/BNE.              
         CLI   ACBTKCMP+1,0                                                     
         BE    BUFIL020            IT'S A 'TYPE' RECORD.                        
         CLC   ACBTKBNO,BUBUDNO    IT'S AN 'AMOUNT' RECORD.                     
         B     BUFIL030                                                         
*                                                                               
BUFIL020 CLC   ACBTKNO1,BUBUDNO                                                 
BUFIL030 EX    R1,*+8              BE/BNE   ERREXIT.                            
         B     BUFIL040            PASSED THE FILTER, CONTINUE.                 
         BC    0,ERREXIT           FAILED.                                      
*                                                                               
BUFIL040 CLI   ACBTKCMP+1,0                                                     
         BNE   BUFIL060            NO CODE ON AMOUNT RECORDS.                   
         OC    BUBUDCDE,BUBUDCDE                                                
         BZ    BUFIL060            NO BUDGET CODE FILTER.                       
         ZIC   RE,BUBUCNEG         RE = 1, FILTER IS NEGATIVE.                  
         IC    R1,BUBRMSK(RE)      R1 = BRANCHING MASK FOR BE/BNE.              
*                                                                               
         LA    RF,BUBUDCDE+L'BUBUDCDE-1                                         
         LA    RE,L'BUBUDCDE                                                    
BUFIL050 CLI   0(RF),C' '          GET SIGNIFICANT L'BUDCODE FILTER.            
         BNE   *+14                                                             
         BCTR  RF,0                                                             
         BCT   RE,BUFIL050                                                      
         DC    H'0'                SHOULDN'T BE HERE IF ALL SPACES.             
*                                                                               
         BCTR  RE,0                RE = SIGNIFICANT L'BUDCODE - 1.              
         EX    RE,*+8              COMPARE ON SIGNIFICANT LENGTH.               
         B     *+10                                                             
         CLC   ACBTKCOD(0),BUBUDCDE                                             
         EX    R1,*+8              BE/BNE   ERREXIT.                            
         B     BUFIL060            PASSED THE FILTER, CONTINUE.                 
         BC    0,ERREXIT           FAILED.                                      
*                                                                               
BUFIL060 CLI   BULSTCDE,C' '                                                    
         BL    BUFIL180            NO LIST RECORD TO FILTER ON.                 
         LA    R7,BULSTREC+(ACRECORD-ACKEYD) R7 = A(1ST EL IN LIST REC)         
         USING ACLDATAD,R7                                                      
*                                                                               
BUFIL080 CLI   ACLDEL,0                                                         
         BE    BUFIL100            END OF LIST.                                 
         CLI   ACLDEL,ACLDELEQ                                                  
         BE    BUFIL120            USE DATA ELEMENTS ONLY.                      
BUFIL090 ZIC   R1,ACLDLEN                                                       
         AR    R7,R1                                                            
         B     BUFIL080            NEXT ELEMENT.                                
*                                                                               
BUFIL100 CLI   BUNEGLST,0                                                       
         BE    ERREXIT             POS FILTER & REC END = FAILED.               
         B     BUFIL180            NEG FILTER & REC END = PASSED.               
*                                                                               
BUFIL120 ZIC   RF,ACLDLEN                                                       
         SH    RF,=H'2'                                                         
         SRL   RF,1                RF = NUM OF 2 BYTE BUD NUM ENTRIES.          
         LA    R6,ACLDATA          R6 = A(FIRST ENTRY).                         
*                                                                               
BUFIL140 CLI   ACBTKCMP+1,0                                                     
         BE    BUFIL150            IT'S A BUDGET TYPE RECORD.                   
         CLC   ACBTKBNO,0(R6)                                                   
         B     BUFIL160                                                         
BUFIL150 CLC   ACBTKNO2,0(R6)                                                   
BUFIL160 BNE   BUFIL170            NOT A MATCH, CONTINUE.                       
         CLI   BUNEGLST,1                                                       
         BE    ERREXIT             MATCH & NEG FILTER = FAILED.                 
BUFIL170 LA    R6,2(R6)            R6 = A(NEXT ENTRY).                          
         BCT   RF,BUFIL140         FINISH THE LIST RECORD.                      
         B     BUFIL090            NEXT ELEMENT.                                
*                                                                               
BUFIL180 CLI   ACBTKCMP+1,0                                                     
         BE    BUFIL240            TYPE RECS HAVE NO DATES.                     
         OC    BUSTDATE,BUSTDATE                                                
         BNZ   *+14                THERE'S AT LEAST A START DATE.               
         OC    BUNDDATE,BUNDDATE                                                
         BZ    BUFIL240            NO DATE FILTERS                              
         LA    R7,BUAMTREC+(ACRECORD-ACKEYD) R7 = A(1ST ELEMENT ).              
*                                                                               
         USING ACBAD,R7                                                         
BUFIL200 CLI   ACBAEL,0                                                         
         BE    ERREXIT             END OF RECORD = FAILED.                      
         CLI   ACBAEL,ACBAELEQ                                                  
         BE    BUFIL220            USE AMOUNT ELEMENTS ONLY.                    
BUFIL210 ZIC   R1,ACBALEN                                                       
         AR    R7,R1                                                            
         B     BUFIL200            NEXT ELEMENT.                                
*                                                                               
BUFIL220 CLC   ACBAMNTH,BUSTDATE                                                
         BL    BUFIL210            ELEMENT DATE IS TOO LOW.                     
         CLC   ACBAMNTH,BUNDDATE                                                
         BH    BUFIL210            ELEMENT DATE IS TOO HIGH.                    
*                                                                               
BUFIL240 B     OKEXIT              IF ANY DATE QUALIFIES, RECORD PASSES         
         DROP  R7                                                               
         EJECT                                                                  
*              VARIOUS SUBROUTINES AND EXITS.                                   
*                                                                               
         USING BUTYTBLD,R7                                                      
         USING ACKEYD,R8                                                        
TYPCHEK  NTR1                                                                   
         L     R7,BUATYTBL         R7 = A(TYPE TABLE START).                    
         L     RE,APARM            R1 = A(USER'S PARAMETER LIST).               
         ZICM  R1,4(RE),4          R1 = NUM OF TYPE ENTRIES SO FAR.             
         BZ    TYC060              THIS IS THE FIRST.                           
*                                                                               
TYC020   CLC   BUTNUM,ACBTKBNO                                                  
         BE    TYC040              TYPE ALREADY IN TABLE.                       
         LA    R7,BUTYTBLQ(R7)     TRY NEXT TYPE ENTRY.                         
         BCT   R1,TYC020                                                        
         B     TYC060              TYPE NOT YET IN TBL, DO WE HAVE ROOM         
*                                                                               
TYC040   ST    R7,WTHISTYP         SAVE A(THIS TYPE ENTRY).                     
         LA    R7,BUTAPTRS                                                      
         LA    R1,(BUTYTBLQ-(BUTAPTRS-BUTYTBLD))/L'BUTAPTRS                     
TYC050   CLI   0(R7),X'FF'                                                      
         BE    TYCPTR              FOUND AN EMPTY POINTER SLOT.                 
         LA    R7,4(R7)            TRY NEXT SLOT.                               
         BCT   R1,TYC050                                                        
         DC    H'0'                THIS TYPE ENTRY IS FULL.                     
*                                                                               
TYC060   ST    R7,WTHISTYP         SAVE A(THIS TYPE ENTRY).                     
         L     RF,BUATYTBL         RF = A(TYPE TABLE START).                    
         A     RF,BUTYTBLN         RF = A(TYPE TABLE END).                      
         SR    RF,R7                                                            
         C     RF,=A(BUTYTBLQ)                                                  
         BNL   *+6                                                              
         DC    H'0'                TYPE TABLE IS FULL                           
         LA    RE,BUTYTBLQ(R7)                                                  
         ST    RE,BUTYTEND         SET CURRENT END-OF-TYPE TABLE.               
         LA    RE,BUTAPTRS         RE = A(1ST AMOUNT POINTER FOR TYPE).         
         ST    RE,WANXTPTR         SAVE IT.                                     
         MVI   0(RE),X'FF'         SET END-OF-POINTERS FLAG.                    
         MVI   4(RE),X'FF'                                                      
         CLI   =X'01',2            SET CC OH LOW FOR EXIT WITH                  
         B     EXIT                NEW TYPE ENTRY.                              
*                                                                               
TYCPTR   ST    R7,WANXTPTR         SAVE A(NEXT AMOUNT POINTER SLOT).            
         MVI   0(R7),0             ERASE PREVIOUS END-OF-POINTERS FLAG.         
         MVI   4(R7),X'FF'         SET END-OF-POINTER LIST FLAG.                
         CR    RB,RB               SET CC OF EQ FOR EXIT WITH TYPE              
         B     EXIT                ALREADY IN TABLE AND A(NEXT POINTER)         
         DROP  R7,R8                                                            
         EJECT                                                                  
         USING BUAMNTHD,R6                                                      
         USING BUTYTBLD,R7                                                      
GETEND   NTR1                                                                   
         CLI   BUTBLPER,X'FF'      X'FF' = USE SUPPLIED PERIOD OR, IF           
         BNE   GET020              NO PERIOD IS SUPPLIED, USE WHOLE             
         MVC   BUAEYYMM,BUNDDATE   BUDGET RECORD DATE SPAN AS PERIOD.           
         GOTO1 BINDAT,BUAEYYMM     FORCE NEXT PERIOD START TO THIS              
         LA    RF,1                END + 1 MONTH.                               
         B     GET030                                                           
*                                                                               
GET020   L     R7,WTHISTYP         R7 = A(TYPE ENTRY FOR THIS AMOUNT).          
         GOTO1 BINDAT,BUASYYMM     CONVERT PER ST DATE TO BIN YYMM.             
         ZIC   RF,BUTBLPER         RF = L'PERIOD IN 'MONTHS'.                   
GET030   ZIC   RE,PARA1+1          RE = MM OF BINARY DATE.                      
         AR    RE,RF                                                            
         LA    R1,12                                                            
         TM    BUTSTAT,ACBTK13Q                                                 
         BZ    *+8                 12 MONTH YEAR                                
         LA    R1,13               13 'MONTH' YEAR.                             
         CR    RE,R1                                                            
         BNH   GET040              SAME YEAR.                                   
         IC    RF,PARA1            BUMP YEAR BY ONE.                            
         LA    RF,1(RF)                                                         
         STC   RF,PARA1                                                         
         SR    RE,R1               DECREMENT MONTH BY 12 OR 13.                 
GET040   STC   RE,PARA1+1          SAVE THE MONTH.                              
         MVC   WNXTPER,PARA1       SAVE NEXT PERIOD START DATE.                 
         MVC   BUAEYYMM,WNXTPER    SET TO DERIVE THIS PERIOD END.               
         CLI   BUAEYYMM+1,1        IS IT FIRST MONTH.                           
         BNE   GET060              NO, IT'S EASY.                               
         ZIC   RF,BUAEYYMM         DECREMENT YEAR BY ONE.                       
         BCTR  RF,0                                                             
         STC   RF,BUAEYYMM         SET NEW YEAR.                                
         STC   R1,BUAEYYMM+1       LAST MONTH FOR YEAR IS 12 OR 13.             
         B     GET080                                                           
*                                                                               
GET060   ZIC   RF,BUAEYYMM+1       JUST DECREMENT THE MONTH BY ONE.             
         BCTR  RF,0                                                             
         STC   RF,BUAEYYMM+1                                                    
GET080   GOTO1 PWOSDAT,BUAEYYMM    CONVERT DATES TO PWOS.                       
         GOTO1 PWOSDAT,WNXTPER                                                  
         CLC   BUAEYYMM,BUNDDATE   NO PERIOD END DATE CAN EXCEED USER           
         BNH   EXIT                SUPPLIED END DATE.                           
         MVC   BUAEYYMM,BUNDDATE                                                
         B     EXIT                                                             
         DROP  R6,R7                                                            
         EJECT                                                                  
PWOSDAT  LR    R0,RE               CONVERT BINARY TO PWOS                       
         LR    R2,R1                                                            
         MVC   DUB(2),0(R2)        YYMM BINARY                                  
         MVI   DUB+2,1                                                          
         GOTO1 BUDTCON,DMCB,(3,DUB),(1,DUB+4)                                   
         MVC   0(2,R2),DUB+4       RETURN PWOS                                  
         LR    RE,R0                                                            
         BR    RE                                                               
*                                                                               
BINDAT   LR    R0,RE               CONVERT PWOS TO BINARY                       
         CLI   0(R1),X'FF'         TEST HIGH VALUE                              
         BNE   *+12                                                             
         MVC   PARA1(2),=X'770C'   2019 (DECEMBER) BINARY                       
         BR    RE                                                               
         MVC   DUB(2),0(R1)        YYMM PWOS                                    
         MVI   DUB+2,1                                                          
         GOTO1 BUDTCON,DMCB,(1,DUB),(3,DUB+4)                                   
         MVC   PARA1(2),DUB+4       RETURN BINARY                               
         LR    RE,R0                                                            
         BR    RE                                                               
*                                                                               
*&&DO                                                                           
PWOSDAT  ZIC   RF,0(R1)            RF = BINARY YY.                              
         CVD   RF,DUB              CONVERT TO STANDARD DECIMAL.                 
         LH    RF,DUB+6            GET LAST TWO BYTES OF PACKED YY.             
         SRL   RF,4                REMOVE THE SIGN, IE. MAKE PWOS.              
         STC   RF,0(R1)            RESET THE YEAR IN PLACE.                     
         ZIC   RF,1(R1)            SAME FOR MONTH.                              
         CVD   RF,DUB                                                           
         LH    RF,DUB+6                                                         
         SRL   RF,4                                                             
         STC   RF,1(R1)                                                         
         BR    RE                                                               
         SPACE                                                                  
BINDAT   XC    DUB,DUB                                                          
         CLI   0(R1),X'FF'                                                      
         BNE   *+12                                                             
         MVC   PARA1(2),=X'630C'                                                
         BR    RE                                                               
         ZIC   RF,0(R1)            CONVERT PWOS YEAR TO BINARY.                 
         SLL   RF,4                                                             
         STH   RF,DUB+6                                                         
         OI    DUB+7,X'0C'                                                      
         CVB   RF,DUB                                                           
         STC   RF,PARA1                                                         
         XC    DUB+6(2),DUB+6                                                   
         ZIC   RF,1(R1)            CONVERT PWOS MONTH TO BINARY.                
         SLL   RF,4                                                             
         STH   RF,DUB+6                                                         
         OI    DUB+7,X'0C'                                                      
         CVB   RF,DUB                                                           
         STC   RF,PARA1+1                                                       
         BR    RE                                                               
*&&                                                                             
RETURNER MVC   BULSTKEY,BUAMTREC                                                
         B     OKEXIT                                                           
         SPACE 1                                                                
OKEXIT   CR    RB,RB                                                            
         B     EXIT                                                             
*                                                                               
ERREXIT  LTR   RB,RB                                                            
         B     EXIT                                                             
*                                                                               
FASTXIT  L     RD,SAVED                                                         
         B     EXIT                                                             
*                                                                               
EXIT     XMOD1 1                                                                
*                                                                               
XITR8    XIT1  REGS=(R8)                                                        
         EJECT                                                                  
*              I/O PROCEDURES.                                                  
*                                                                               
         USING ACKEYD,R8                                                        
NEXTAMNT NTR1                                                                   
         XC    WKEY,WKEY                                                        
         MVC   WKEY(L'BULSTKEY),BULSTKEY                                        
         LA    R8,WKEY             READ HI ON LAST AMOUNT KEY + 1.              
         ZIC   RF,ACBTKBNO+2                                                    
         LA    RF,1(RF)                                                         
         STC   RF,ACBTKBNO+2                                                    
         LA    R8,BUAMTREC          R8 = A(AMOUNT RECORD AREA).                 
         MVC   WCOMMAND,DMRDHI                                                  
         B     READIT                                                           
*                                                                               
NEXTYPE  NTR1                                                                   
         XC    WKEY,WKEY                                                        
         MVI   WKEY,ACBTKTEQ                                                    
         MVC   WKEY+1(1),BUSERKEY   COMPANY CODE.                               
         LA    R8,BUTYPREC                                                      
         MVC   WCOMMAND,DMRDHI     READ HI ON LAST BUDGET NUMBER + 1.           
         OC    ACBTKNO1,ACBTKNO1                                                
         BZ    *+14                                                             
         MVC   WKEY+3(L'ACBTKNO1),ACBTKNO1                                      
         B     *+10                                                             
         MVC   WKEY+3(L'ACBTKNO1),ACBTKNO2                                      
         ZICM  RF,WKEY+3,2         BUMP THE BUDGET NUMBER.                      
         LA    RF,1(RF)            HOWE'S THAT FOR A MODERATE INCREASE.         
         STCM  RF,3,WKEY+3                                                      
         B     READIT                                                           
*                                                                               
GETTYPE  NTR1                                                                   
         XC    WKEY,WKEY           GET TYPE RECORD FOR EXISTING                 
         MVI   WKEY,ACBTKTEQ       AMOUNT RECORD.                               
         MVC   WKEY+1(1),BUSERKEY  COMPANY CODE.                                
         LA    R8,BUAMTREC                                                      
         MVC   WKEY+3(L'ACBTKNO1),ACBTKBNO                                      
         MVC   WCOMMAND,DMRDHI                                                  
         LA    R8,BUTYPREC                                                      
         B     READIT                                                           
         SPACE 1                                                                
READER   NTR1                                                                   
*                                                                               
READIT   GOTO1 BUADMGR,DMCB,WCOMMAND,ACCOUNT,WKEY,(R8),BUDMWORK                 
         CLI   DMCB+8,0                                                         
         BNE   READIT4             I/O ERROR.                                   
         CLI   0(R8),ACBTKTEQ                                                   
         BNE   READEND             END OF FILE (END OF BUCKET RECORDS).         
         CLC   1(1,R8),BUSERKEY                                                 
         BNE   READEND              END OF COMPANY.                             
         B     XITR8               GOOD READ.                                   
*                                                                               
READIT4  L     RF,APARM            PASS DATAMGR PARAS TO USER                   
         MVC   0(24,RF),DMCB       BAD I/O.                                     
         MVI   BUMODE,DMGRERR                                                   
         LTR   RB,RB               SET CC TO NEQ.                               
         B     FASTXIT                                                          
         DROP  R8                                                               
         EJECT                                                                  
         LTORG                                                                  
         SPACE 1                                                                
RELOC    DC    A(*)                                                             
ACMNDRTN DS    0A                                                               
         B     CMGETTYP                                                         
         B     CMNXTYP                                                          
         B     CMGETAMT                                                         
         B     CMNXTAMT                                                         
*                                                                               
DMREAD   DC    CL6'DMREAD'                                                      
DMRDHI   DC    CL6'DMRDHI'                                                      
ACCOUNT  DC    C'ACCOUNT'                                                       
*                                                                               
BUBRMSK  DC    X'70'               MASK FOR POS FILT. IE BNE ERREXIT.           
         DC    X'80'               MASK FOR NEQ FILT. IE BE  ERREXIT.           
         EJECT                                                                  
BUDAWRKD DSECT                                                                  
DUB      DS    D                                                                
WRELOC   DS    A                                                                
APARM    DS    A                                                                
SAVED    DS    A                                                                
*                                                                               
DMCB     DS    0F                                                               
PARA1    DS    F                                                                
PARA2    DS    F                                                                
PARA3    DS    F                                                                
PARA4    DS    F                                                                
PARA5    DS    F                                                                
PARA6    DS    F                                                                
*                                                                               
WANXTTYP DS    A                                                                
WANXTAMT DS    A                                                                
WANXTPTR DS    A                                                                
WTHISTYP DS    A                                                                
WNXTPER  DS    CL2                                                              
WLASTDAT DS CL2                                                                 
ACBTK13Q EQU   X'08'                                                            
WCOMMAND DS    CL6                                                              
WKEY     DS    CL42                                                             
         SPACE 1                                                                
*              INCLUDED HERE ARE - ACGENBOTH                                    
*                                  ACBUDACCD                                    
         PRINT OFF                                                              
       ++INCLUDE ACGENBOTH                                                      
         PRINT ON                                                               
         EJECT                                                                  
       ++INCLUDE ACBUDACCD                                                      
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'019ACBUDACC  05/01/02'                                      
         END                                                                    
