*          DATA SET AXTRBVND   AT LEVEL 128 AS OF 11/16/20                      
*CATALP AXTRBVND                                                                
         TITLE 'AXTRBVND - READ VENDOR DETAILS FOR RECEIVABLES'                 
*=============================================================                  
* GHOA 002 2018NOV16 RIGHT SPACE PAD MEDIA AUTHORIZATION                        
* GHOA 003 2018NOV17 CHANGES FOR PAYABLES                                       
* GHOA 005 2019JAN14 FIX ABEND ITMF-32822                                       
* GHOA 006 2019APR12 CHANGES FOR CASH RECEIVE AND CASH PAY                      
*=============================================================                  
* CALLING PARMS ARE                                                             
* P1   A(AXWORKG)                                                               
* P2   A(RECEIVABLE POSTING)                                                    
* P3   A(OUTPUT BUFFER)                                                         
* P4   A(3 BYTE BINARY START/END MOS)                                           
* P5   A(RECEIVABLE AGENCY ALPHA ID)                                            
*=============================================================                  
                                                                                
AXTRBVND CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,AXTRBVND                                                       
*                                                                               
         LARL  RC,COMMON                                                        
         USING COMMON,RC                                                        
*                                                                               
         MVC   SVPARMS,0(R1)                                                    
         L     RF,AXVNDDTS                                                      
         MVC   STBDTE,0(RF)                                                     
         MVC   ENBDTE,3(RF)                                                     
*                                                                               
         L     R9,AXVNDWRK                                                      
         USING WORKD,R9                                                         
*                                                                               
         L     RA,AADDRESS                                                      
         USING ADDRESSD,RA                                                      
         L     R6,VDXTAB                                                        
         USING SXDTABD,R6          R6=A(EXTRACT SYSTEM TABLE ENTRY)             
         MVC   COUNTRY,SXDTCTRY                                                 
*                                                                               
         J     FX00                                                             
*                                                                               
EQXIT    CR    RC,RC                                                            
         J     *+6                                                              
NEQXIT   LTR   RC,RC                                                            
*                                                                               
EXIT     XIT1                                                                   
*                                                                               
FX00     CLI   SVSEACC,0           TEST FIRST TIME                              
         JNE   FX02                                                             
*                                                                               
         MVC   DUB(8),=CL8'T00A14  '    LOAD CLPACK                             
         GOTO1 VLOADER,DMCB,DUB,0,0                                             
         ICM   RF,15,4(R1)                                                      
         JZ    *+2                                                              
         ST    RF,VCLPACK                                                       
*                                                                               
         MVC   DUB(8),=CL8'T00A7A  '    LOAD STAPACK                            
         GOTO1 VLOADER,DMCB,DUB,0,0                                             
         ICM   RF,15,4(R1)                                                      
         JZ    *+2                                                              
         ST    RF,VSTAPACK                                                      
*                                                                               
         MVC   DUB(8),=CL8'T00A9E  '    LOAD CABLETAB FOR STAPACK               
         GOTO1 VLOADER,DMCB,DUB,0,0                                             
         ICM   RF,15,4(R1)                                                      
         JZ    *+2                                                              
         ST    RF,VCBLTAB                                                       
*                                                                               
         LA    R1,STAWORK          INITIALIZE CABLETAB                          
         USING STAPACKD,R1                                                      
         XC    STAWORK,STAWORK                                                  
         MVI   STAPACT,QSTP_T00A9E  SET ACTION = HERE IS ADDRESS!               
         MVC   STAPACOM,VCBLTAB                                                 
         GOTO1 VSTAPACK,(R1)                                                    
*                                                                               
         MVC   SVSEACC,SXDTSEN     SAVE ACC SE NUMBER                           
                                                                                
FX02     L     R7,THISSE           POINT TO CURRENT SETAB ENTRY                 
         USING SETABD,R7                                                        
*                                                                               
         L     RE,AXVNDID          GET ALPHA AGY PASSED THIS TIME               
         OC    0(2,RE),0(RE)       TEST IT'S THERE                              
         JNZ   FX04                YES                                          
         LA    R7,SETAB            IF NOT, USE FIRST AGY                        
         ST    R7,THISSE                                                        
         J     FX10                                                             
*                                                                               
FX04     CLC   SEAGYA,0(RE)        SAME AS LAST TIME?                           
         JE    FX10                                                             
*                                                                               
         BAS   RE,GETSES           LOOK UP IN SETAB OR READ NEW                 
         L     R7,THISSE           AND POINT TO NEW ENTRY                       
         EJECT                                                                  
FX10     L     R8,AXVNDRCV         POINT TO RECEIVABLE RECORD                   
         USING AC_TRNRECD,R8                                                    
         LA    R6,AC_TRNRFST       IF NOT A TRNEL, SKIP REC                     
         CLI   0(R6),AC_TRNELQ                                                  
         JNE   *+2                                                              
*                                                                               
         USING AC_TRNELD,R6                                                     
         MVC   SVTRNSTY,AC_TRNTYPE                                              
*                                                                               
         CLI   RTYP,C'P'           IGNORE THE 2 TESTS FOR PAYABLES              
         BE    FX11                                                             
*                                                                               
         TM    AC_TRNSTAT,AC_TRNSDR  TEST DEBIT                                 
         JZ    EXIT                  NO - IGNORE                                
*                                                                               
         CLI   AC_TRNTYPE,9        TEST MEDIA BILLING ELEM                      
         JNE   EXIT                                                             
         DROP  R6                                                               
*                                                                               
FX11     ST    R6,SVTRNEL                                                       
*                                                                               
         LA    R6,AC_TRNRFST                                                    
         MVI   ELCDLO,AC_MBIELQ                                                 
         MVI   ELCDHI,AC_MBIELQ                                                 
         BRAS  RE,NEXTEL2                                                       
         BNE   FX20                                                             
*                                                                               
         USING AC_MBIELD,R6                                                     
         MVC   SVSYS,AC_MBISYS                                                  
         MVC   SVQMED,AC_MBIMED                                                 
         MVC   SVQCLT,AC_MBICLI                                                 
         MVC   SVQPRD,AC_MBIPRD                                                 
         PACK  DUB,AC_MBIEST                                                    
         CVB   R0,DUB                                                           
         STC   R0,SVEST1                                                        
         STH   R0,SVEST2                                                        
         MVC   SVMOSP,AC_MBIMOS                                                 
         B     FX40                                                             
         DROP  R6                                                               
*                                                                               
FX20     LA    R6,AC_TRNRFST                                                    
         MVI   ELCDLO,AC_MDPELQ                                                 
         MVI   ELCDHI,AC_MDPELQ                                                 
         BRAS  RE,NEXTEL2                                                       
         BNE   FX30                                                             
*                                                                               
         USING AC_MDPELD,R6                                                     
         MVC   SVSYS,AC_MDPSYS                                                  
         MVC   SVQMED,AC_MDPMED                                                 
         MVC   SVQCLT,AC_MDPCLI                                                 
         MVC   SVQPRD,AC_MDPPRD                                                 
         PACK  DUB,AC_MDPEST                                                    
         CVB   R0,DUB                                                           
         STC   R0,SVEST1                                                        
         STH   R0,SVEST2                                                        
         MVC   SVMOSP,AC_MDPMOS                                                 
         B     FX40                                                             
                                                                                
*                                                                               
FX30     LA    R6,AC_TRNRFST                                                    
         MVI   ELCDLO,AC_MDTELQ                                                 
         MVI   ELCDHI,AC_MDTELQ                                                 
         BRAS  RE,NEXTEL2                                                       
*        JNE   *+2                                                              
         JNE   FX40                                                             
*                                                                               
         USING AC_MDTELD,R6                                                     
         MVC   SVSYS,AC_MDTSYS                                                  
         MVC   SVQMED,AC_MDTMED                                                 
         MVC   SVQCLT,AC_MDTCLI                                                 
         MVC   SVQPRD,AC_MDTPRD                                                 
         PACK  DUB,AC_MDTEST                                                    
         CVB   R0,DUB                                                           
         STC   R0,SVEST1                                                        
         STH   R0,SVEST2                                                        
         MVC   SVMOSP,AC_MDTMOS                                                 
*                                                                               
FX40     MVI   SVMOSP+2,1          CONVERT PWOS TO 2-BYTE BINARY                
         UNPK  WORK(7),SVMOSP(4)       E.G., FBF4F0F8F0F1YX                     
         GOTO1 VDATCON,DMCB,WORK,(3,SVMOS)                                      
*                                                                               
FX41     CLI   RTYP,C'P'                                                        
         BNE   FX41X                                                            
         XC    SVPSEQ,SVPSEQ                                                    
*                                                                               
         LA    R6,AC_TRNRFST       GET PAY INVOICE                              
         MVI   ELCDLO,AC_XPYELQ                                                 
         MVI   ELCDHI,AC_XPYELQ                                                 
         BRAS  RE,NEXTEL2                                                       
         JNE   FX41F                                                            
*                                                                               
         USING AC_XPYELD,R6                                                     
         MVC   SVPINV,AC_XPYINV    GET INVOICE                                  
         MVI   SVPSEQ,0            GET SEQUENCE NUMBER                          
         MVC   SVPSEQ+1(1),AC_XPYSEQ                                            
         MVC   SVPMKT,AC_XPYMKT       MARKET NUMBER                             
         MVC   SVPBMKT,AC_XPYBMKT     BINARY MARKET NUMBER                      
*                                                                               
         GOTO1 VDATCON,DMCB,(0,AC_XPYPER),(3,SVPPYST)                           
         CLC   AC_XPYPER+6(6),MYSPACES                                          
         JH    FX41D                                                            
         MVC   SVPPYEN,SVPPYST                                                  
         J     FX41E                                                            
*                                                                               
FX41D    GOTO1 VDATCON,DMCB,(0,AC_XPYPER+6),(3,SVPPYEN)                         
*                                                                               
FX41E    CLC   SVPBMKT,MYSPACES    MAYBE BE SPACES IF NONE                      
         JNE   FX41Z                                                            
         XC    SVPBMKT,SVPBMKT     ZERO IT OUT                                  
         J     FX41Z                                                            
*                                                                               
FX41F    LA    R6,AC_TRNRFST       GET SUPPLIER INVOICE                         
         MVI   ELCDLO,AC_FFTELQ                                                 
         MVI   ELCDHI,AC_FFTELQ                                                 
FX41N    BRAS  RE,NEXTEL                                                        
         JNE   FX41Z                                                            
*                                                                               
         USING AC_FFTELD,R6                                                     
         CLI   AC_FFTTYPE,FFTTINVN                                              
         JNE   FX41N                                                            
         MVC   SVPINV,AC_FFTDATA                                                
         J     FX41Z                                                            
*                                                                               
FX41X    L     R6,SVTRNEL                                                       
         USING AC_TRNELD,R6                                                     
*                                                                               
         MVC   SVINV,AC_TRNREF     SAVE BILLMON/INV NUM                         
         MVC   SVPINV(L'SVINV),SVINV                                            
         OC    SVPINV,MYSPACES                                                  
*                                                                               
         PACK  DUB,AC_TRNREF+2(4)  PACK THE INVOICE NUMBER                      
         CVB   R0,DUB                                                           
         STH   R0,SVINVB                                                        
*                                                                               
FX41Z    LA    R6,AC_TRNRFST                                                    
         MVI   ELCDLO,AC_GDAELQ                                                 
         MVI   ELCDHI,AC_GDAELQ                                                 
         XC    SVBILLD2,SVBILLD2                                                
         XC    SVBILLD3,SVBILLD3                                                
*                                                                               
FX42     BRAS  RE,NEXTEL                                                        
         JNE   FX43                                                             
         USING AC_GDAELD,R6                                                     
         CLI   RTYP,C'R'           RECEIVABLES?                                 
         JNE   FX42H                                                            
         CLI   AC_GDATYPE,2         TEST MEDIA BILLING DATE                     
         BNE   FX42                                                             
         J     FX42X                                                            
*                                  PAY                                          
FX42H    CLI   AC_GDATYPE,5         CLEARANCES - DATE ITEM WAS CLEARED          
         BNE   FX42                                                             
*                                                                               
FX42X    GOTO1 VDATCON,DMCB,(1,AC_GDADATE),(2,SVBILLD2)                         
         GOTO1 (RF),(R1),(1,AC_GDADATE),(3,SVBILLD3)   FOR PRINT                
         DROP  R6                                                               
         EJECT                                                                  
*==============================================================                 
* SEE IF CONNECTED TO THE RIGHT SYSTEM FOR THIS RECORD                          
*==============================================================                 
                                                                                
FX43     MVC   THISSENUM,SENUMPRT                                               
         CLI   SVSYS,C'P'                                                       
         JE    FX50                                                             
*                                                                               
         MVC   THISSENUM,SENUMSPT                                               
         CLI   SVSYS,C'S'          TEST SPOT                                    
         JNE   *+2                                                              
*                                                                               
         CLI   COUNTRY,X'08'        TEST CANADA                                 
         JE    FX50                YES -ACCEPT ALL MEDIA                        
                                                                                
*=======================================================                        
* US NETWORK SYSTEMS HAVE SYSTEM CODE S FOR SPOT                                
* BUT A UNIQUE SET OF MEDIA CODES                                               
*=======================================================                        
                                                                                
         LA    R0,L'MDTAB          TEST MEDIA CODE                              
         LA    R1,MDTAB                                                         
*                                                                               
FX44     CLC   SVQMED,0(R1)        TEST FOR A NETWORK MEDIA CODE                
         JE    FX46                                                             
         LA    R1,1(R1)                                                         
         JCT   R0,FX44                                                          
         J     FX50                                                             
*                                                                               
MDTAB    DC    C'NCSDOV'           EXCLUDE MEDIA EXCEPT FOR CANADA              
*                                                                               
FX46     MVC   THISSENUM,SENUMNET  SET SYSTEM TO NET                            
         MVI   SVSYS,C'N'          AND REMEMBER IT!                             
         EJECT                                                                  
*====================================================================           
* SET UTL ENTRY TO READ CORRECT MEDIA FILES AND OPEN THEM IF NOT OPEN           
*====================================================================           
                                                                                
FX50     L     RF,VUTL                                                          
         CLI   SVSYS,C'S'                                                       
         JNE   FX51                                                             
         CLI   SVQMED,C'N'                                                      
         JNE   FX51                                                             
         MVC   THISSENUM,SENUMNET                                               
*                                                                               
FX51     MVC   TSYS-UTLD(1,RF),THISSENUM SET THE SYSTEM IN UTL                  
*                                                                               
         CLC   LASTSENUM,THISSENUM       SAME SYSTEM AS LAST TIME               
         JE    FX60                                                             
                                                                                
* OPEN THE MEDIA FILES IF NOT ALREADY OPEN                                      
                                                                                
FX52     MVC   LASTSENUM,THISSENUM                                              
*                                                                               
         LA    R1,SESPTOPN                                                      
         LA    R4,=CL8'SPOT'                                                    
         LA    R5,SPTFLIST                                                      
         CLI   SVSYS,C'S'                                                       
         JE    FX54                                                             
*                                                                               
         LA    R1,SENETOPN                                                      
         LA    R4,=CL8'NET'                                                     
         LA    R5,NETFLIST                                                      
         CLI   SVSYS,C'N'                                                       
         JE    FX54                                                             
*                                                                               
         LA    R1,SEPRTOPN                                                      
         LA    R4,=C'PRINT'                                                     
         LA    R5,PRTFLIST                                                      
         CLI   SVSYS,C'P'                                                       
         JNE   *+2                                                              
*                                                                               
FX54     CLI   0(R1),C'Y'          TEST SYSTEM ALREADY OPEN                     
         JE    FX60                                                             
         MVI   0(R1),C'Y'          SET OPEN FLAG                                
*                                                                               
         GOTO1 VDATAMGR,DMCB,DMOPEN,(R4),(R5),REC                               
*                                                                               
FX60     CLI   SVSYS,C'S'                                                       
         JNE   FX62                                                             
         BRAS  RE,SP00                                                          
         J     FX70                                                             
*                                                                               
FX62     CLI   SVSYS,C'N'                                                       
         JNE   FX64                                                             
         BRAS  RE,NET00                                                         
         J     FX70                                                             
*                                                                               
FX64     CLI   SVSYS,C'P'                                                       
         JNE   *+2                                                              
         BRAS  RE,PR00                                                          
*                                                                               
FX70     L     RF,VUTL                                                          
         MVC   TSYS-UTLD(1,RF),SVSEACC  RESTORE ACC TO UTL ENTRY                
         SAM24                          AND EXIT IN 24-BIT MODE                 
         J     EXIT                                                             
         EJECT                                                                  
*================================================================               
* CHECK SETAB TO SEE IF THIS AGYA ALREADY THERE ELSE,                           
* READ ACCESS RECORD FOR SPOT/PRINT/NET SE NUMS/AGYB CODES                      
*================================================================               
                                                                                
GETSES   NTR1  BASE=*,LABEL=*                                                   
         XC    LASTMDCL,LASTMDCL   MAKE SURE NO RANDOM MATCH                    
                                                                                
* FIRST CHECK CURRENT SE TABLE TO SEE IF AGENCY THERE                           
                                                                                
         L     RE,AXVNDID          POINT TO OVERRIDE AGY                        
         LA    R7,SETAB            FIRST TABLE ENTRY                            
         USING SETABD,R7                                                        
*                                                                               
         CLI   SEAGYA,0            TEST FIRST TIME                              
         JE    GETSE10                                                          
*                                                                               
GETSE02  CLC   SEAGYA,0(RE)        RIGHT AGY                                    
         JE    GETSE20                                                          
         AHI   R7,L'SETAB                                                       
         CLI   SEAGYA,0                                                         
         JNE   GETSE02                                                          
                                                                                
*==============================================================                 
* BUILD A NEW ENTRY IN SETAB FOR THIS AGYA                                      
*==============================================================                 
                                                                                
GETSE10  MVC   SEAGYA,0(RE)                                                     
*                                                                               
         L     RE,VUTL                                                          
         MVI   4(RE),10                                                         
         XC    WORK,WORK                                                        
         LA    R4,WORK                                                          
         USING CT5REC,R4                                                        
*                                                                               
         MVI   CT5KTYP,CT5KTYPQ                                                 
         L     RE,AXVNDID                                                       
         MVC   CT5KALPH,0(RE)                                                   
         DROP  R4                                                               
*                                                                               
         GOTO1 VDATAMGR,DMCB,DMREAD,CTFILE,WORK,REC                             
         CLI   8(R1),0                                                          
         JNE   *+2                                                              
*                                                                               
         LA    R4,REC                                                           
         LA    R4,CT5DATA-CT5REC(R4)                                            
         USING CTSYSD,R4                                                        
         SR    R0,R0                                                            
         B     GETSE14                                                          
*                                                                               
GETSE12  IC    R0,1(R4)                                                         
         AR    R4,R0                                                            
*                                                                               
GETSE14  CLI   0(R4),0                                                          
         JE    GETSE20                                                          
*                                                                               
         CLI   CTSYSEL,CTSYSELQ    X'21' ELEM?                                  
         BNE   GETSE12                                                          
*                                                                               
         CLI   CTSYSNUM,X'02'      SPOT?                                        
         JNE   *+16                                                             
         MVC   SENUMSPT,CTSYSSE    SAVE SPOT SE NUMBER                          
         MVC   SESPTAGB,CTSYSAGB   AND BINARY COMPANY                           
*                                                                               
         CLI   CTSYSNUM,X'03'      NETWORK                                      
         JNE   *+16                                                             
         MVC   SENUMNET,CTSYSSE    SAVE NET SE NUMBER                           
         MVC   SENETAGB,CTSYSAGB   AND BINARY COMPANY                           
*                                                                               
         CLI   CTSYSNUM,X'04'      PRINT                                        
         JNE   *+10                                                             
         MVC   SENUMPRT,CTSYSSE    SAVE PRINT SE NUMBER                         
*                                                                               
         J     GETSE12                                                          
*                                                                               
GETSE20  ST    R7,THISSE           SET SE ENTRY ADDRESS                         
         J     EXIT                                                             
         DROP  R4                                                               
                                                                                
         EJECT                                                                  
*================================================================               
* GET THE CLIENT RECORD TO TRANSLATE THE PRODUCT CODE                           
*================================================================               
                                                                                
SP00     NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         L     R3,AXVNDBUF                                                      
         USING BUFFERD,R3                                                       
         SAM31                                                                  
         MVC   BUFFCLNM,MYSPACES                                                
         MVC   BUFFPRNM,MYSPACES                                                
         MVC   BUFFESNM,MYSPACES                                                
         MVC   BUFFPUD1,MYSPACES                                                
         MVC   BUFFPUD2,MYSPACES                                                
         MVC   BUFFEUD1,MYSPACES                                                
         MVC   BUFFEUD2,MYSPACES                                                
         MVC   BUFFVOUC,MYSPACES                                                
*                                                                               
         LA    R4,BUFFDATA                                                      
         USING BUFFDATA,R4                                                      
         LH    RF,=Y(2*BUFFERLN)    CLEAR 2 ENTRIES                             
         LR    RE,R4                                                            
         XCEFL                                                                  
         SAM24                                                                  
*                                                                               
         CLI   COUNTRY,X'08'        TEST CANADA                                 
         JNE   SP100                                                            
         OC    SENETLST,SENETLST   ANY NETLIST YET THIS AGY                     
         JNZ   SP100               YES                                          
         BRAS  RE,GETNETS          BUILD CANADIAN NETWORK TABLE                 
*                                                                               
SP100    CLC   LASTMDCL,SVQMED     TEST SAME MEDIA/CLIENT                       
         JE    SP200                                                            
*                                                                               
         MVC   LASTMDCL,SVQMED     SAVE NEW MEDIA/CLIENT                        
         MVI   SVFNDCLT,C'N'                                                    
         BRAS  RE,GETSPCLT                                                      
*        JNE   SP240                                                            
         JNE   EXIT                                                             
         MVI   SVFNDCLT,C'Y'                                                    
*                                                                               
SP200    CLI   SVFNDCLT,C'Y'                                                    
         JNE   EXIT                                                             
         SAM31                                                                  
         MVC   BUFFCLNM,SVQCLTNM                                                
         SAM24                                                                  
*                                                                               
         LA    RE,SVCLTHDR                                                      
         LA    RE,CLIST-CLTHDRD(RE)  POINT TO PRDLIST                           
*                                                                               
SP220    CLC   SVQPRD,0(RE)                                                     
         BE    SP240                                                            
         LA    RE,4(RE)                                                         
         CLI   0(RE),C'A'                                                       
         JNL   SP220                                                            
*        DC    H'0'                                                             
         J     EXIT                                                             
*                                                                               
SP240    MVC   SVPRDCD,3(RE)        SAVE BINARY PRD                             
         BRAS  RE,GETSPPRD          GET SPOT PRODUCT NAME                       
         BRAS  RE,GETSPEST          GET SPOT ESTIMATE NAME                      
*                                                                               
         CLI   RTYP,C'P'            PAY?                                        
         JE    SP1000               YES, DIFFERENT LOOKUP                       
*===============================================================                
* NOW READ STATION BILLING RECORDS                                              
*===============================================================                
SP250    LA    R8,KEY                                                           
         USING STABUCKD,R8                                                      
*                                                                               
         MVI   SVCOS2,C'N'                                                      
         BAS   RE,HASCOS2                                                       
         BNE   *+8                                                              
         MVI   SVCOS2,C'Y'                                                      
*                                                                               
         XC    KEY,KEY                                                          
         MVC   STABKCOD,=X'0E01'                                                
         MVC   STABKAM(3),SVCLTHDR+1  MOVE A-M/CLT FROM CLTHDR                  
         MVC   STABKPRD,SVPRDCD                                                 
         MVC   STABKEST,SVEST1                                                  
*                                                                               
         MVC   KEYSAVE,KEY                                                      
         GOTO1 VDATAMGR,DMCB,DMRDHI,SPTDIR,KEYSAVE,KEY                          
         J     SP320                                                            
*                                                                               
SP300    GOTO1 VDATAMGR,DMCB,DMRSEQ,SPTDIR,KEYSAVE,KEY                          
*                                                                               
SP320    CLC   KEY(7),KEYSAVE      SAME 0E01/AM/CLT/PRD/EST                     
         JNE   EXIT                                                             
*                                                                               
         CLI   SVCOS2,C'Y'         DO WE HAVE COS2?                             
         JNE   SP350               NO, JUST USE COS1                            
         CLI   STABKCUR,0          YES, DON'T USE COS1                          
         JE    SP300                                                            
*                                                                               
SP350    GOTO1 VDATAMGR,DMCB,GETREC,SPTFILE,KEY+14,REC,DMWORK                   
         LA    R8,REC                                                           
         USING STABUCKD,R8                                                      
*                                                                               
         LA    R6,STABELEM                                                      
         USING STABELEM,R6                                                      
*                                                                               
         MVI   ELCDLO,X'0E'        GET NORMAL ELS                               
         MVI   ELCDHI,X'1E'        AND UNBILLING ELS                            
         BRAS  RE,NEXTEL2                                                       
         B     *+8                                                              
*                                                                               
SP400    BRAS  RE,NEXTEL                                                        
         BNE   SP300                                                            
*                                                                               
         CLC   STABPER,SVMOS       MATCH Y/M OF SERVICE                         
         BNE   SP400                                                            
*                                                                               
         CLC   STABBDT,SVBILLD2    RIGHT BILL DATE                              
         BNE   SP400               NO - SKIP                                    
*                                                                               
         MVC   HALF,STABINV                                                     
         NI    HALF,X'3F'          DROP X'80'+X'40'                             
         CLC   HALF,SVINVB         RIGHT INVOICE                                
         BNE   SP400                                                            
                                                                                
*=============================================================                  
* MOVE VENDOR/DOLLARS TO BUFFER                                                 
*=============================================================                  
                                                                                
         LA    R1,STAWORK          UNPACK MKT/STATION                           
         USING STAPACKD,R1                                                      
*                                                                               
         XC    STAWORK,STAWORK                                                  
         MVC   STAPACOM,ACOMFACS                                                
         MVI   STAPACT,C'U'                                                     
*                                                                               
         MVI   STAPCTRY,C'U'                                                    
         CLI   COUNTRY,X'08'        TEST CANADA                                 
         JNE   *+8                                                              
         MVI   STAPCTRY,C'C'                                                    
*                                                                               
         MVC   STAPAGY,SEAGYA                                                   
         MVC   STAPMED,SVQMED                                                   
         MVC   STAPMKST,STABKMKT   PACKED MKT-STA                               
*                                                                               
         GOTO1 VSTAPACK,STAWORK                                                 
*                                                                               
         SAM31                                                                  
         MVC   BUFFVNDR,MYSPACES                                                
         MVC   BUFFVNDR(8),STAPQSTA   MOVE 8 CHAR STATION                       
         CLI   SVQMED,C'T'                                                      
         BNE   SP450                                                            
         CLI   BUFFVNDR+4,C'T'                                                  
         JNE   SP450                                                            
         MVI   BUFFVNDR+4,C' '                                                  
SP450    SAM24                                                                  
*                                                                               
         MVC   SVSTA,STAPQSTA         SAVE 5 CHAR STATION                       
         CLI   SVQMED,C'T'                                                      
         BNE   SP470                                                            
         CLI   SVSTA+4,C' '                                                     
         JNE   SP470                                                            
         MVI   SVSTA+4,C'T'                                                     
*                                                                               
SP470    CLI   STAPQSTA+4,C'N'        TEST NETWORK                              
         BNE   SP500                                                            
         LLC   RE,STABKSTA+2          GET NETWORK NUMBER                        
         SLL   RE,2                   X 4                                       
         A     RE,SENETLST                                                      
         SAM31                                                                  
         MVC   BUFFVNDR+4(4),0(RE)    MOVE NETWORK                              
         SAM24                                                                  
         DROP  R1                                                               
*                                                                               
SP500    ICM   R0,15,STABGRS                                                    
         CLI   0(R6),X'1E'         TEST UNBILLING ELEM                          
         BNE   *+6                                                              
         LCR   R0,R0                                                            
         CVD   R0,DUB                                                           
         SAM31                                                                  
         ZAP   BUFFGRS,DUB                                                      
         SAM24                                                                  
*                                                                               
         ICM   R0,15,STABNET                                                    
         CLI   0(R6),X'1E'         TEST UNBILLING ELEM                          
         BNE   *+6                                                              
         LCR   R0,R0                                                            
         CVD   R0,DUB                                                           
         SAM31                                                                  
         ZAP   BUFFNET,DUB                                                      
         SAM24                                                                  
* GET STATION ADDRESS                                                           
         BRAS  RE,GETSTADD         RUNS IN 24-BIT MODE                          
*                                                                               
         SAM31                                                                  
         AHI   R4,BUFFERLN                                                      
         LH    RF,=Y(BUFFERLN)                                                  
         LR    RE,R4                                                            
         XCEFL                                                                  
         SAM24                                                                  
         J     SP300                                                            
*===============================================================                
* PAY - LOOK AT THE CLRST AND INVOICE                                           
*===============================================================                
SP1000   DS    0H                                                               
         L     RF,AXVNDRCV         POINT TO TRANSACTION RECORD                  
         USING AC_TRNRECD,RF                                                    
         MVC   SVPSTA,4(RF)        STATION FROM ACCOUNT                         
         DROP  RF                                                               
*                                                                               
         CLC   SVPMKT,MYSPACES                                                  
         JH    SP1100                                                           
*                                                                               
         LA    R8,STAKEY                                                        
         USING SP_STARECD,R8                                                    
*                                     GET MARKET NUMBER FROM STAREC             
         XC    SP_STAKEY,SP_STAKEY                                              
         MVI   SP_STAKTYPE,SP_STAKTYPQ   C'S'                                   
         MVC   SP_STAKMED,SVQMED         MEDIA                                  
         MVC   SP_STAKCALL,SVPSTA        STATION CALL LETTERS                   
         MVC   SP_STAKAGY,SEAGYA         ALPHA                                  
         MVC   SP_STAKCLT,SVQCLT         CLIENT                                 
*                                                                               
SP1010   LARL  R8,MYPUBREC                                                      
*                                                                               
         GOTO1 VDATAMGR,DMCB,DMRDHI,STATION,STAKEY,(R8)                         
         CLC   STAKEY(SP_STAKCLT-SP_STAKEY),0(R8)                               
         JNE   EXIT                                                             
         CLC   STAKEY(SP_STAKFILL-SP_STAKEY),0(R8)                              
         JE    SP1050                           GRAB THAT MARKET                
         CLC   SP_STAKCLT,=C'000'                                               
         JE    SP1050                                                           
         LA    R8,STAKEY                                                        
         MVC   SP_STAKCLT,=C'000'                                               
         MVC   SP_STAKFILL,=C'000'                                              
         J     SP1010                                                           
*                                                                               
SP1050   MVC   SVPMKT,SP_SMKT      MARKET CODE (CHAR)                           
         PACK  DUB,SVPMKT          COVERT TO BINARY                             
         CVB   R0,DUB                                                           
         STH   R0,SVPBMKT                                                       
*                                                                               
SP1100   DS    0H                                                               
         LA    R1,STAWORK          PACK MKT/STATION                             
         USING STAPACKD,R1                                                      
*                                                                               
         XC    STAWORK,STAWORK                                                  
         MVC   STAPACOM,ACOMFACS                                                
         MVI   STAPACT,C'P'        PACK                                         
*                                                                               
         MVI   STAPCTRY,C'U'       DEFAULT US                                   
         CLI   COUNTRY,X'08'       TEST CANADA                                  
         JNE   *+8                                                              
         MVI   STAPCTRY,C'C'       CANADA                                       
*                                                                               
         MVC   STAPAGY,SEAGYA      AGENCY                                       
         MVC   STAPMED,SVQMED      MEDIA                                        
         MVC   STAPQMKT,SVPMKT     MARKET                                       
         MVC   STAPQSTA,SVPSTA     STATION                                      
*                                                                               
         GOTO1 VSTAPACK,STAWORK                                                 
         MVC   SVPBMKT,STAPMKT                                                  
         MVC   SVPBSTA,STAPSTA                                                  
*                                                                               
         USING CLRSTATD,R8                                                      
         LA    R8,KEY                                                           
         XC    KEY,KEY                                                          
         MVC   CLSKTYPE,=X'0D76'        CLRST RECORD                            
         MVC   CLSKAGMD(3),SVCLTHDR+1   MOVE A-M/CLT FROM CLTHDR                
         MVC   CLSKMKT,SVPBMKT          MARKET                                  
         MVC   CLSKSTA,SVPBSTA          STATION                                 
*                                                                               
         MVC   KEYSAVE,KEY                                                      
         GOTO1 VDATAMGR,DMCB,DMRDHI,SPTDIR,KEYSAVE,KEY                          
         J     SP1250                                                           
*                                                                               
SP1200   GOTO1 VDATAMGR,DMCB,DMRSEQ,SPTDIR,KEYSAVE,KEY                          
*                                                                               
SP1250   CLC   KEY(CLSKDATE-CLSKEY),KEYSAVE     CHECK UPTO STATION              
         JNE   EXIT                                                             
*                                                                               
SP1300   GOTO1 VDATAMGR,DMCB,GETREC,SPTFILE,KEY+14,REC,DMWORK                   
*                                                                               
         LARL  R5,INVTAB                                                        
         USING INVTABD,R5                                                       
         LA    R8,REC                                                           
*                                                                               
         LA    R6,CLSELEMS                                                      
         USING CLSTEL01,R6                                                      
*                                                                               
SP1350   CLI   0(R6),0                                                          
         JE    EXIT                                                             
         CLI   0(R6),1                  CLEARANCE DETAIL                        
         JE    SP1400                                                           
         CLI   0(R6),3                  INVOICE NUMBER                          
         JE    SP1500                                                           
SP1380   LLC   R0,1(R6)                 BUMP TO NEXT CLUSTER                    
         AR    R6,R0                                                            
         J     SP1350                                                           
*                                                                               
SP1400   DS    0H                                                               
         CLC   CLSTSTDT(4),SVPPYST      CHECK IF PAY PERIOD SAME                
         JNE   SP1380                                                           
         MVC   ITSEQ+1,CLSTCLSQ                                                 
         MVC   ITGROSS,CLSTGRS                                                  
         MVC   ITNET,CLSTNET                                                    
         J     SP1380                                                           
*                                                                               
         USING CLSTEL03,R6                                                      
SP1500   CLC   CLS3INV,SVPINV                                                   
         JE    SP1550                                                           
         XC    0(ITABLEN,R5),0(R5)   NO, CLEAR TABLE                            
         J     SP1380                                                           
*                                                                               
SP1550   CLC   ITSEQ,SVPSEQ                                                     
         JNE   SP1380                                                           
         OC    ITSEQ(ITABLEN-L'ITINV),ITSEQ                                     
         JZ    SP1380                                                           
         MVC   ITINV,CLS3INV                                                    
*                                                                               
         ICM   R0,15,ITGROSS       GROSS AMOUNT                                 
         CVD   R0,DUB                                                           
         ZAP   GROSS,DUB                                                        
                                                                                
         ICM   R0,15,ITNET         NET AMOUNT                                   
         CVD   R0,DUB                                                           
         ZAP   NET,DUB                                                          
*                                                                               
         J     EXIT                                                             
         EJECT                                                                  
                                                                                
*===============================================================                
* PROCESSING FOR PRINTPAK                                                       
*===============================================================                
         USING STABUCKD,R8                                                      
HASCOS2  NTR1                                                                   
         LA    R8,KEY                                                           
         XC    KEY,KEY                                                          
         MVC   STABKCOD,=X'0E01'                                                
         MVC   STABKAM(3),SVCLTHDR+1  MOVE A-M/CLT FROM CLTHDR                  
         MVC   STABKPRD,SVPRDCD                                                 
         MVC   STABKEST,SVEST1                                                  
*                                                                               
         MVC   KEYSAVE,KEY                                                      
         GOTO1 VDATAMGR,DMCB,DMRDHI,SPTDIR,KEYSAVE,KEY                          
         J     HCOS2200                                                         
*                                                                               
HCOS2100 GOTO1 VDATAMGR,DMCB,DMRSEQ,SPTDIR,KEYSAVE,KEY                          
*                                                                               
HCOS2200 CLC   KEY(7),KEYSAVE      SAME 0E01/AM/CLT/PRD/EST                     
         JNE   NEQXIT              NO, LEAVE WITH CC=NE                         
                                                                                
         CLI   STABKCUR,1          COS2                                         
         JE    EQXIT               YES, LEAVE WITH CC=EQ                        
         J     HCOS2100                                                         
                                                                                
         LTORG                                                                  
SVCOS2   DS    CL1                                                              
         DROP  R4                                                               
         EJECT                                                                  
*===============================================================                
* PROCESSING FOR PRINTPAK                                                       
*===============================================================                
                                                                                
PR00     NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         L     R8,AXVNDRCV         POINT TO RECEIVABLE RECORD                   
         USING AC_TRNRECD,R8                                                    
         MVI   BUYFND,C'N'                                                      
                                                                                
         CLI   RTYP,C'P'           PROCESSING PAY?                              
         BNE   PR100               NO                                           
*                                                                               
         MVC   WORK(8),AC_TRNKACT+1   SAVE PUB NUMBER                           
         CLI   AC_TRNKACT+5,C' '                                                
         JH    PR030                                                            
         MVC   WORK(8),=C'00000000'                                             
         MVC   WORK+4(4),AC_TRNKACT+1                                           
PR030    MVC   SVPUB8,WORK                                                      
         PACK  DUB,WORK(8)                                                      
         MP    DUB(8),=P'10'                                                    
         MVC   SVPUB,DUB+3         PUB BINARY VERSION                           
*                                                                               
         CLC   AC_TRNKULC(8),=C'00000000'     PUB MAY BE IN CONTRA              
         JNH   PR100                                                            
         MVC   WORK(8),AC_TRNKULC                                               
         PACK  DUB,WORK(8)                                                      
         MP    DUB(8),=P'10'                                                    
         MVC   SVPUB,DUB+3         PUB BINARY VERSION                           
*                                                                               
* NOW READ INSERTIONS                                                           
PR100    L     R3,AXVNDBUF                                                      
         USING BUFFERD,R3                                                       
         SAM31                                                                  
         MVC   BUFFCLNM,MYSPACES                                                
         MVC   BUFFPRNM,MYSPACES                                                
         MVC   BUFFESNM,MYSPACES                                                
         MVC   BUFFPUD1,MYSPACES                                                
         MVC   BUFFPUD2,MYSPACES                                                
         MVC   BUFFEUD1,MYSPACES                                                
         MVC   BUFFEUD2,MYSPACES                                                
         MVC   BUFFVOUC,MYSPACES                                                
*                                                                               
         LA    R4,BUFFDATA                                                      
         USING BUFFDATA,R4                                                      
         LH    RF,=Y(2*BUFFERLN)    CLEAR 2 ENTRIES                             
         LR    RE,R4                                                            
         XCEFL                                                                  
         SAM24                                                                  
*                                                                               
* GET CLIENT AND PRODUCT NAMES                                                  
*                                                                               
         XC    KEY,KEY                                                          
         LA    R8,KEY                                                           
         USING PCLTREC,R8          CLIENT                                       
*                                                                               
         MVC   PCLTKAGY,SEAGYA                                                  
         MVC   PCLTKMED,SVQMED                                                  
         MVI   PCLTKRCD,X'02'                                                   
         MVC   PCLTKCLT,SVQCLT                                                  
         MVC   KEYSAVE,KEY                                                      
         GOTO1 VDATAMGR,DMCB,(X'08',DMRDHI),PRTDIR,KEYSAVE,KEY                  
         CLC   KEY(7),KEYSAVE      SAME AG/MED/TY/CL                            
         JNE   PR110               NO- DONE                                     
         GOTO1 VDATAMGR,DMCB,GETREC,PRTFILE,KEY+27,REC,DMWORK                   
         LA    R8,REC                                                           
         SAM31                                                                  
         MVC   BUFFCLNM(L'PCLTNAME),PCLTNAME                                    
         OC    BUFFCLNM(L'PCLTNAME),MYSPACES                                    
         SAM24                                                                  
*                                                                               
         XC    KEY,KEY                                                          
         LA    R8,KEY                                                           
         USING PPRDREC,R8                                                       
*                                                                               
         MVC   PPRDKAGY,SEAGYA                                                  
         MVC   PPRDKMED,SVQMED                                                  
         MVI   PPRDKRCD,X'06'                                                   
         MVC   PPRDKCLT,SVQCLT                                                  
         MVC   PPRDKPRD,SVQPRD                                                  
         MVC   KEYSAVE,KEY                                                      
         GOTO1 VDATAMGR,DMCB,(X'08',DMRDHI),PRTDIR,KEYSAVE,KEY                  
         CLC   KEY(10),KEYSAVE     SAME AG/MED/TY/CL/PRD                        
         JNE   PR110               NO- DONE                                     
         GOTO1 VDATAMGR,DMCB,GETREC,PRTFILE,KEY+27,REC,DMWORK                   
         LA    R8,REC                                                           
         SAM31                                                                  
         MVC   BUFFPRNM(L'PPRDNAME),PPRDNAME                                    
         OC    BUFFPRNM(L'PPRDNAME),MYSPACES                                    
         SAM24                                                                  
*                                                                               
         BAS   RE,GETESTNM                                                      
*                                                                               
PR110    CLI   RTYP,C'P'                                                        
         BNE   PR240                                                            
*                                                                               
         XC    KEY,KEY             GET CLEARANCE RECORDS                        
         LA    R8,KEY                                                           
         USING PPCLRST,R8                                                       
*                                                                               
         MVC   PPCLAGY,SEAGYA                                                   
         MVC   PPCLMED,SVQMED                                                   
         MVI   PPCLTYPE,X'25'                                                   
         MVC   PPCLCLT,SVQCLT                                                   
         MVC   PPCLPUB(4),SVPUB                                                 
*                                                                               
         MVC   KEYSAVE,KEY                                                      
         GOTO1 VDATAMGR,DMCB,DMRDHI,PRTDIR,KEYSAVE,KEY                          
         B     PR118                                                            
*                                                                               
PR115    LA    R8,KEY                                                           
         GOTO1 VDATAMGR,DMCB,(X'08',DMRSEQ),PRTDIR,KEY,KEY                      
*                                                                               
PR118    CLC   KEY(PPCLDATE-PPCLKEY),KEYSAVE      SAME CLEARANCE?               
         JNE   EXIT                NO- DONE                                     
         MVC   CLRKEY,KEY                                                       
*                                                                               
         LARL  R8,CLRREC                                                        
         GOTO1 VDATAMGR,DMCB,GETREC,PRTFILE,KEY+27,(R8),DMWORK                  
*                                                                               
         LARL  R5,INVTAB                                                        
         USING PPCLRST,R8                                                       
         LA    R6,PPCLELEM                                                      
PR120    CLI   0(R6),X'00'                                                      
         JE    PR115               GET NEXT CLEARANCE RECORD                    
         CLI   0(R6),X'01'         CLEARANCE DETAIL                             
         JE    PR130                                                            
         CLI   0(R6),X'03'         INVOICE NUMBER ELEMENT                       
         JE    PR135                                                            
         CLI   0(R6),X'05'         INVOICE NUMBER ELEMENT                       
         JE    PR150                                                            
PR123    LLC   R0,1(R6)                                                         
         AR    R6,R0                                                            
         J     PR120                                                            
*                                                                               
         USING PPCLEL01,R6                                                      
         USING INVTABD,R5                                                       
PR130    CLC   PPCLCLRD,SVBILLD3     MATCH CLEARANCE DATE                       
         JNE   PR123                                                            
         XC    0(ITABLEN,R5),0(R5)   CLEAR TABLE                                
         CLI   PPCLCLSQ,X'FF'                                                   
         BNE   PR133                                                            
*                                                                               
         TM    PPCLSTAT,X'04'        NEW SQN ACTIVE                             
         BZ    PR133                                                            
         MVC   ITSEQ,PPCLCLS2        YES, USE IT                                
         J     *+10                                                             
*                                                                               
PR133    MVC   ITSEQ+1(1),PPCLCLSQ   1 BYTE SEQUENCE NUMBER                     
         MVC   ITGROSS,PPCLGRS     GROSS                                        
         MVC   ITNET,PPCLNET       NET                                          
         J     PR123                                                            
*                                                                               
         USING PPCLEL03,R6                                                      
PR135    CLC   PPCLINV,SVPINV      INVOICE MATCHED?                             
         JNE   PR123               NO                                           
         OC    SVPSEQ,SVPSEQ       NO SEQUENCE NUMBER FROM ACC?                 
         JZ    PR140                                                            
         CLC   ITSEQ,SVPSEQ        AND SEQUENCE NUMBER MATCHED?                 
         JNE   PR123                                                            
PR140    OC    ITSEQ(ITABLEN-L'ITINV),ITSEQ                                     
         JZ    PR123                                                            
         MVC   ITINV,PPCLINV       SAVE INVOICE NUMBER                          
*                                                                               
         CLI   RTYP,C'P'           PAY NEEDS AMOUNT FROM X'05'                  
         JNE   PR230               NOT PAY, GOT AMTS FROM X'01'                 
         J     PR123                                                            
*                                                                               
         USING PPCLEL05,R6                                                      
PR150    CLC   ITINV,SVPINV        INVOICE MATCHED?                             
         JNE   PR123               NO                                           
         OC    SVPSEQ,SVPSEQ       NO SEQUENCE NUMBER FROM ACC?                 
         JZ    PR155                                                            
         CLC   ITSEQ,SVPSEQ        AND SEQUENCE NUMBER MATCHED?                 
         JNE   PR123                                                            
PR155    MVC   ITGROSS,PCL5GRS                                                  
         ICM   RF,15,PCL5NET       ADD 5ET + CD                                 
         ICM   RE,15,PCL5CD                                                     
         AR    RF,RE                                                            
         STCM  RF,15,ITNET         SAVE IN NET                                  
         OC    ITGROSS,ITGROSS     NO GROSS?                                    
         JNZ   *+10                                                             
         MVC   ITGROSS,ITNET       USE NET THEN                                 
*                                                                               
PR230    DS    0H                                                               
PR240    XC    LASTPUB,LASTPUB                                                  
         XC    LASTSEQ,LASTSEQ                                                  
         XC    KEY,KEY                                                          
         LA    R8,KEY                                                           
         USING PBUYKEY,R8                                                       
*                                                                               
         MVC   PBUYKAGY,SEAGYA                                                  
         MVC   PBUYKMED,SVQMED                                                  
         MVI   PBUYKRCD,X'20'                                                   
         MVC   PBUYKCLT,SVQCLT                                                  
         MVC   PBUYKPRD,SVQPRD                                                  
*                                                                               
         CLI   RTYP,C'P'                                                        
         BNE   PR242                                                            
         MVC   PBUYKPUB,SVPUB      PAY CAN ISOLATE BY PUB                       
*                                                                               
PR242    DS    0H                                                               
*R242    MVI   DMINBTS,X'08'       READ DELETED RECORDS !                       
         MVC   KEYSAVE,KEY                                                      
         GOTO1 VDATAMGR,DMCB,(X'08',DMRDHI),PRTDIR,KEYSAVE,KEY                  
         B     PR250                                                            
*                                                                               
PR244    LA    R8,KEY                                                           
         GOTO1 VDATAMGR,DMCB,(X'08',DMRSEQ),PRTDIR,KEY,KEY                      
*                                                                               
PR250    CLC   KEY(10),KEYSAVE     SAME AG/MED/TY/CL/PRD                        
         JNE   EXIT                NO- DONE                                     
*                                                                               
         CLI   RTYP,C'P'           PROCESSING PAY?                              
         BNE   PR252               NO                                           
         CLC   KEY(14),KEYSAVE     YES, CHECK PUB TOO                           
         JNE   EXIT                                                             
         OC    SVEST2,SVEST2       WASN'T CLEARED BY ESTIMATE                   
         JZ    PR253               YES, READ RECORD                             
*                                                                               
PR252    CLC   PBUYKEST,SVEST2     ACTUAL TO BILLED ESTIMATE                    
         BNE   PR244                                                            
         DROP  R8                                                               
*                                                                               
PR253    GOTO1 VDATAMGR,DMCB,GETREC,PRTFILE,KEY+27,REC,DMWORK                   
         MVC   BUYKEY,KEY                                                       
*                                                                               
         LA    R8,REC                                                           
         USING PBUYREC,R8                                                       
         XC    SV26EL,SV26EL                                                    
*                                                                               
         CLI   RTYP,C'P'           RECEIVABLES?                                 
         JE    PR254                                                            
         CLC   PBDBDATE(2),SVMOS   MATCH Y/M OF SERVICE                         
         BNE   PR244                                                            
* MAKE SURE X'00' FOLLOWS REC                                                   
PR254    SR    RE,RE                                                            
         ICM   RE,3,PBUYLEN                                                     
         LA    RE,0(RE,R8)                                                      
         XC    0(2,RE),0(RE)                                                    
*                                                                               
         CLI   RTYP,C'R'           RECEIVABLES?                                 
         JE    PR259                                                            
*                                  NO, THIS IS FOR PAY                          
         CLC   PBUYKPUB,SVPUB      MATCH PUB                                    
         JNE   PR244                                                            
*                                                                               
* AS PER KEVIN, DON'T MATCH INVOICES (10/07/2020)                               
*        BRAS  RE,MTCHPINV         MATCH PAY INVOICE NUMBERS                    
*        JNE   PR244                                                            
*                                                                               
         LA    R6,PBDELEM                                                       
         J     *+8                                                              
*                                                                               
PR255    L     R6,SV25EL                                                        
         MVI   ELCDLO,X'25'        PAYABLES                                     
         MVI   ELCDHI,X'25'                                                     
         BRAS  RE,NEXTEL                                                        
         JNE   PR244                                                            
         ST    R6,SV25EL                                                        
*                                                                               
         USING PPAYELD,R6                                                       
         CLC   PPDDATE,SVBILLD3    MACTH PAY DATE                               
         JNE   PR255               NO - SKIP                                    
*                                                                               
         LARL  R5,INVTAB                                                        
         CLI   PPAYELLN,26         2 BYTE SEQUENCE NUMBER?                      
         JE    PR257                                                            
         MVI   SVSEQNUM,0          NO                                           
         MVC   SVSEQNUM+1(1),PPDSEQNO                                           
         J     PR257C                                                           
*                                                                               
PR257    MVC   SVSEQNUM,PPSEQN2    SAVE SEQUENCE NUMBER                         
*                                                                               
PR257C   OC    ITINV,ITINV                                                      
         JZ    PR255                                                            
         CLC   SVSEQNUM,ITSEQ      MATCH SEQ NUMBER                             
         JNE   PR255                                                            
*        JE    PR258                                                            
*        AHI   R5,ITABLEN                                                       
*        J     PR257C                                                           
*                                                                               
PR258    ICM   R0,15,PPGROSS                                                    
         CLI   RTYP,C'P'                                                        
         JNE   *+8                                                              
         ICM   R0,15,ITGROSS       PAY, USE THE GROSS IN TABLE                  
         CVD   R0,DUB                                                           
         ZAP   GROSS,DUB                                                        
*                                                                               
         ICM   RE,15,PPAGYCOM      GROSS - AGY COMM                             
         SR    R0,RE               GIVES NET                                    
         CLI   RTYP,C'P'                                                        
         JNE   *+8                                                              
         ICM   R0,15,ITNET         PAY, USE THE NET IN TABLE                    
         CVD   R0,DUB                                                           
         ZAP   NET,DUB                                                          
         J     PR270                                                            
*                                                                               
PR259    MVI   ELCDLO,X'26'        THIS IS FOR RECEIVABLES                      
         MVI   ELCDHI,X'26'                                                     
         LA    R6,PBDELEM                                                       
         J     *+8                                                              
*                                                                               
PR260    L     R6,SV26EL                                                        
         BRAS  RE,NEXTEL                                                        
         JNE   PR244                                                            
         ST    R6,SV26EL                                                        
*                                                                               
         USING PBILELD,R6                                                       
*                                                                               
         CLC   PBPRD,SVQPRD                                                     
         BNE   PR260                                                            
*                                                                               
         CLC   PBINVNO,SVINVB                                                   
         BNE   PR260                                                            
*                                                                               
         CLC   PBLDATE,SVBILLD3    RIGHT BILL DATE                              
         BNE   PR260               NO - SKIP                                    
*                                                                               
         ICM   R0,15,PBGROSS                                                    
         CVD   R0,DUB                                                           
         ZAP   GROSS,DUB                                                        
*                                                                               
         ICM   RE,15,PBAGYCOM                                                   
         SR    R0,RE               GIVES NET                                    
         CVD   R0,DUB                                                           
         ZAP   NET,DUB                                                          
         EJECT                                                                  
*==============================================================                 
* OUTPUT VENDOR DATA TO BUFFER                                                  
*==============================================================                 
                                                                                
* WHILE R6 STILL POINTS TO PBILELEM!                                            
                                                                                
PR270    DS    0H                                                               
         SAM31                                                                  
*&&DO                                                                           
         CLI   RTYP,C'P'           PROCESSING PAY?                              
         BNE   PR273               NO                                           
         CLC   SVSEQNUM,LASTSEQ    TEST SAME SEQ NUMBER                         
         JE    PR280                                                            
         OC    LASTSEQ,LASTSEQ     TEST FIRST TIME                              
         JNZ   PR275                                                            
         JZ    PR277                                                            
*&&                                                                             
PR273    CLC   PBUYKPUB,LASTPUB    TEST SAME PUB                                
         JE    PR280               YES - ADD TO THIS PUB TOTAL                  
         OC    LASTPUB,LASTPUB     TEST FIRST TIME                              
         JZ    PR277                                                            
PR275    AHI   R4,BUFFERLN                                                      
         LH    RF,=Y(2*BUFFERLN)    CLEAR 2 ENTRIES                             
         LR    RE,R4                                                            
         XCEFL                                                                  
*                                                                               
PR277    ZAP   BUFFGRS,=P'0'                                                    
         ZAP   BUFFNET,=P'0'                                                    
         MVC   LASTSEQ,SVSEQNUM                                                 
*                                                                               
PR280    AP    BUFFGRS,GROSS                                                    
         AP    BUFFNET,NET                                                      
         MVC   BUFFINSO,MYSPACES                                                
         MVC   BUFFCMPG,MYSPACES                                                
         MVC   BUFFMEDA,MYSPACES                                                
         MVC   BUFFSEQ,SVSEQNUM                                                 
         GOTO1 VDATCON,DMCB,(3,PBUYKDAT),(23,DUB)                               
         MVC   BUFFINSD(2),DUB+8                                                
         MVC   BUFFINSD+2(2),DUB+5                                              
         MVC   BUFFINSD+4(4),DUB                                                
         MVC   BUFFSPAC,MYSPACES                                                
         MVI   BUYFND,C'Y'         BUY FOUND                                    
*                                                                               
         LA    R6,PBDELEM                                                       
         USING PBDELEM,R6                                                       
         MVC   BUFFSPAC,PBDSPACE   SPACE DESCRIPTION                            
         OC    BUFFSPAC,MYSPACES                                                
*                                                                               
         CLI   PBUYKMED,C'O'       OUTDOOR                                      
         JNE   PR283                                                            
*                                                                               
         MVI   ELCDLO,X'66'        FIND SPACE                                   
         MVI   ELCDHI,X'66'                                                     
         LA    R6,PBDELEM                                                       
         BRAS  RE,NEXTEL                                                        
         JNE   PR283                                                            
*                                                                               
         MVC   BUFFSPAC,MYSPACES                                                
         USING PCOMELEM,R6                                                      
         IC    R1,1(R6)                                                         
         SHI   R1,3                                                             
         CHI   R1,16                                                            
         JNH   *+8                                                              
         LHI   R1,16                                                            
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   BUFFSPAC(0),PCOMDATA   SPACE DESCRIPTION                         
*                                                                               
PR283    MVI   ELCDLO,X'CC'        FIND INSERTION NUMBER                        
         MVI   ELCDHI,X'CC'                                                     
         LA    R6,PBDELEM                                                       
PR285    BRAS  RE,NEXTEL                                                        
         JNE   PR300                                                            
*                                                                               
         USING BYCCELD,R6                                                       
         LA    R2,BUFFINSO                                                      
         CLC   =H'8204',BYCCSQN    INSERTION ORDER NUMBER                       
         BE    PR288                                                            
         LA    R2,BUFFCMPG                                                      
         CLC   =H'8215',BYCCSQN    CAMPAIGN                                     
         BE    PR288                                                            
         LA    R2,BUFFMEDA                                                      
         CLC   =H'8249',BYCCSQN    MEDIA AUTHORIZATION NUMBER                   
         BNE   PR285                                                            
*                                                                               
PR288    DS    0H                                                               
         SR    R1,R1                                                            
         IC    R1,BYCCLEN                                                       
         SHI   R1,BYCCHDRL+1                                                    
         EX    R1,*+8                                                           
         J     *+10                                                             
         MVC   0(0,R2),BYCCDATA                                                 
         J     PR285                                                            
*                                                                               
PR300    MVC   BUFFBUYU,MYSPACES   FIND BUY UNIQUE NUMBER (SERIAL #)            
         MVI   ELCDLO,X'99'                                                     
         MVI   ELCDHI,X'99'                                                     
         LA    R6,PBDELEM                                                       
         BRAS  RE,NEXTEL                                                        
         JNE   PR320                                                            
*                                                                               
         USING PSERELEM,R6                                                      
         EDIT  (P5,PSERNUM),(9,BUFFBUYU),0,COMMAS=NO,ZERO=NOBLANK,     X        
               ALIGN=LEFT                                                       
*                                                                               
* BUILD THE VOUCHER                                                             
* AGY/MED/CLT/SUPPLIER/INVOICE/SEQUENCE                                         
*                                                                               
PR320    MVC   BUFFVOUC(3),PBUYREC           AGENCY/MEDIA                       
         MVC   BUFFVOUC+3(3),PBUYREC+4                                          
         MVC   BUFFVOUC+6(14),SVPINV                                            
*                                                                               
*        MVC   BUFFVOUC+6(8),SVPUB8          AS PER SONNY 6/4/20                
*        MVC   BUFFVOUC+14(14),SVPINV                                           
*&&DO                                                                           
         OC    SVSEQNUM,SVSEQNUM                                                
         JZ    PR400                                                            
         LA    RF,BUFFVOUC+27                                                   
PR350    CLI   0(RF),C' '                                                       
         JH    PR380                                                            
         AHI   RF,-1                                                            
         J     PR350                                                            
*                                                                               
PR380    AHI   RF,1                                                             
         MVI   0(RF),C'-'                                                       
         AHI   RF,1                                                             
         EDIT  SVSEQNUM,(5,(RF)),0,ALIGN=LEFT                                   
*&&                                                                             
PR400    OC    SVEST2,SVEST2                                                    
         JNZ   PR600                                                            
         MVC   SVPRTKEY,KEY        SAVE CURRENT PRTDIR KEY                      
         MVC   SVEST2,PBUYREC+19                                                
         BAS   RE,GETESTNM                                                      
         MVC   KEY,SVPRTKEY                                                     
*                                                                               
PR600    MVC   BUFFEST,SVEST2                                                   
         MVI   ELCDLO,X'A7'        FIND PID                                     
         MVI   ELCDHI,X'A7'                                                     
         LA    R6,PBDELEM                                                       
         BRAS  RE,NEXTEL                                                        
         JNE   PR700                                                            
*                                                                               
         USING PPIDELD,R6                                                       
         MVC   BUFFPID,PPIDPID     PERSONAL ID                                  
         MVC   BUFFSECA,PPUSRID    USER ID FOR ADD (CONNECT ID NUMBER)          
*                                                                               
PR700    MVI   ELCDLO,X'26'                                                     
         MVI   ELCDHI,X'26'                                                     
         L     R6,SV26EL                                                        
         OC    SV26EL,SV26EL                                                    
         JNZ   PR710                                                            
         LA    R6,PBDELEM                                                       
         ST    R6,SV26EL                                                        
*                                                                               
PR710    CLC   LASTPUB,PBUYKPUB    TEST SAME PUB                                
         JE    PR900                                                            
*                                                                               
         MVC   LASTPUB,PBUYKPUB    SAVE LAST PUB LOOKED UP                      
*                                                                               
         LA    R0,6                                                             
         CLI   PBUYKEDT,0          DO WE HAVE AN EDITION?                       
         JH    PR800                                                            
         LA    R0,5                                                             
         CLI   PBUYKZON,0          DO WE HAVE A ZONE?                           
         JH    PR800                                                            
         LA    R0,4                                                             
*                                                                               
* NOTE HEXOUT CALL IN 31-BIT MODE                                               
*                                                                               
PR800    GOTO1 VHEXOUT,DMCB,PBUYKPUB,BUFFVNDR,(R0),(31,=C'TOG')                 
*                                                                               
         SAM24                                                                  
         MVC   SVPRTKEY,KEY        SAVE CURRENT PRTDIR KEY                      
         BAS   RE,GETPUBNM                                                      
                                                                                
* REMEMBER READ PUBDIR, NOT PRTDIR SO JUST MOVE KEYS BACK                       
                                                                                
         MVC   KEY,SVPRTKEY        RESTORE PRTDIR KEY                           
         MVC   KEYSAVE,KEY          AND KEYSAVE                                 
*                                                                               
         LA    R6,PBDELEM                                                       
*        ST    R6,SV26EL                                                        
*                                                                               
PR900    MVC   KEY,BUYKEY                                                       
         MVC   KEYSAVE,KEY                                                      
         GOTO1 VDATAMGR,DMCB,(X'08',DMRDHI),PRTDIR,KEYSAVE,KEY                  
         CLI   RTYP,C'P'           PROCESSING PAY?                              
         JNE   PR260               NO, LOOK BILLABLE                            
         CLI   BUYFND,C'Y'         FOUND BUY, NO NEED TO LOOP ANYMORE           
         JE    EXIT                                                             
         J     PR255               YES, LOOP PAYABLES                           
         LTORG                                                                  
WKSERNUM DS    CL9                                                              
         DROP  R4                                                               
         EJECT                                                                  
*===========================================================                    
* MATCH PAY INVOICE NUMBERS                                                     
*===========================================================                    
MTCHPINV NTR1                                                                   
         MVI   ELCDLO,X'CC'        FIND PAY INVOICE NUMBER                      
         MVI   ELCDHI,X'CC'          GROUPM                                     
         LA    R6,PBDELEM                                                       
MTCHP110 BRAS  RE,NEXTEL                                                        
         JNE   MTCHP500                                                         
*                                                                               
         USING BYCCELD,R6                                                       
         CLC   =H'8213',BYCCSQN    PAY INVOICE NUMBER                           
         BNE   MTCHP110                                                         
         XR    R1,R1                                                            
         IC    R1,BYCCLEN                                                       
         SHI   R1,BYCCHDRL+1                                                    
         EX    R1,*+8                                                           
         J     *+10                                                             
         CLC   SVPINV(0),BYCCDATA  MATCH?                                       
         JNE   MTCHP110            NO, GET NEXT ONE                             
         J     MTCHPYES            YES, LEAVE                                   
*                                                                               
MTCHP500 DS    0H                                                               
         MVI   ELCDLO,X'51'        FIND PAY INVOICE NUMBER                      
         MVI   ELCDHI,X'51'          NON-GROUPM                                 
         LA    R6,PBDELEM                                                       
MTCHP510 BRAS  RE,NEXTEL                                                        
         JNE   MTCHPNO                                                          
*                                                                               
         USING PBNVELMD,R6                                                      
         CLC   SVPINV,PBNVINV#     MATCH?                                       
         JNE   MTCHP510            NO, GET NEXT ONE                             
         J     MTCHPYES            YES, LEAVE                                   
                                                                                
MTCHPYES J     EQXIT                                                            
MTCHPNO  J     NEQXIT                                                           
         LTORG                                                                  
         EJECT                                                                  
*===========================================================                    
* GET ESTIMATE NAME                                                             
*===========================================================                    
GETESTNM NTR1                                                                   
         XC    KEY,KEY                                                          
         LA    R8,KEY                                                           
         USING PESTREC,R8                                                       
*                                                                               
         MVC   PESTKAGY,SEAGYA                                                  
         MVC   PESTKMED,SVQMED                                                  
         MVI   PESTKRCD,X'07'                                                   
         MVC   PESTKCLT,SVQCLT                                                  
         MVC   PESTKPRD,SVQPRD                                                  
         MVC   PESTKEST,SVEST2                                                  
         MVC   KEYSAVE,KEY                                                      
         GOTO1 VDATAMGR,DMCB,(X'08',DMRDHI),PRTDIR,KEYSAVE,KEY                  
         CLC   KEY(12),KEYSAVE     SAME AG/MED/TY/CL/PRD/EST                    
         JNE   EXIT                NO- DONE                                     
         LARL  R8,ESTREC                                                        
         GOTO1 VDATAMGR,DMCB,GETREC,PRTFILE,KEY+27,(R8),DMWORK                  
         SAM31                                                                  
         MVC   BUFFESNM(L'PESTNAME),PESTNAME                                    
         OC    BUFFESNM(L'PESTNAME),MYSPACES                                    
         SAM24                                                                  
*                                                                               
         AHI   R8,33                                                            
GESTNM10 CLI   0(R8),X'00'         EOR                                          
         JE    EXIT                                                             
         CLI   0(R8),X'08'         USER DEFINED FIELDS                          
         BE    GESTNM20                                                         
         XR    R1,R1                                                            
         IC    R1,1(R8)                                                         
         AR    R8,R1                                                            
         J     GESTNM10                                                         
*                                                                               
         USING PESTUDEF,R8                                                      
GESTNM20 SAM31                                                                  
         MVC   BUFFEUD1,PEUSER1    ESTIMATE UDEF 1                              
         MVC   BUFFEUD2,PEUSER2                  2                              
         OC    BUFFEUD1,MYSPACES                                                
         OC    BUFFEUD2,MYSPACES                                                
         SAM24                                                                  
*                                                                               
         J     EXIT                                                             
         EJECT                                                                  
*===========================================================                    
* GET PUB NAME                                                                  
*===========================================================                    
                                                                                
         USING BUFFDATA,R4                                                      
GETPUBNM NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         XC    KEY,KEY                                                          
         LA    R8,KEY                                                           
         USING PUBKEY,R8                                                        
*                                                                               
         LA    RE,REC                                                           
         MVC   PUBKMED,PBUYKMED-PBUYKEY(RE)                                     
         MVC   PUBKPUB(6),PBUYKPUB-PBUYKEY(RE)                                  
         MVC   PUBKAGY,SEAGYA                                                   
         MVI   PUBKCOD,X'81'                                                    
         DROP  R8                                                               
*                                                                               
         MVC   KEYSAVE,KEY                                                      
         GOTO1 VDATAMGR,DMCB,DMRDHI,PUBDIR,KEYSAVE,KEY                          
*                                                                               
         CLC   KEY(L'PUBKEY),KEYSAVE                                            
         JNE   *+2                                                              
*                                                                               
         LARL  R8,MYPUBREC                                                      
         USING PUBREC,R8                                                        
*                                                                               
         GOTO1 VDATAMGR,DMCB,GETREC,PUBFILE,KEY+27,(R8),DMWORK                  
*                                                                               
         LA    R6,PUBREC+33        FIND PUB NAMEL TO SEND NAME/ZONE             
         MVI   ELCDLO,X'10'                                                     
         MVI   ELCDHI,X'10'                                                     
         BRAS  RE,NEXTEL2                                                       
         JNE   *+2                                                              
*                                                                               
GETPUBN2 SAM31                                                                  
         MVC   BUFFNAME(20),PUBNAME-PUBNAMEL(R6)                                
*&&DO                                                                           
         CLI   PUBZNAME-PUBNAMEL(R6),C' '                                       
         JNH   GETPUBNX                                                         
         MVI   BUFFNAME+16,C'/'                                                 
         MVC   BUFFNAME+17(7),PUBZNAME-PUBNAMEL(R6)                             
*&&                                                                             
GETPUBNX OC    BUFFNAME,MYSPACES                                                
         SAM24                                                                  
         J     EXIT                                                             
         LTORG                                                                  
         DROP  R4,R8                                                            
         EJECT                                                                  
*============================================================                   
* PROCESSING FOR NETWORK SYSTEM                                                 
*============================================================                   
                                                                                
NET00    NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         L     R3,AXVNDBUF         POINT TO OUTPUT BUFFER                       
         USING BUFFERD,R3                                                       
         SAM31                                                                  
         MVC   BUFFCLNM,MYSPACES                                                
         MVC   BUFFPRNM,MYSPACES                                                
         MVC   BUFFESNM,MYSPACES                                                
         MVC   BUFFPUD1,MYSPACES                                                
         MVC   BUFFPUD2,MYSPACES                                                
         MVC   BUFFEUD1,MYSPACES                                                
         MVC   BUFFEUD2,MYSPACES                                                
         MVC   BUFFVOUC,MYSPACES                                                
*                                                                               
         LA    R4,BUFFDATA                                                      
         USING BUFFDATA,R4                                                      
         LH    RF,=Y(2*BUFFERLN)    CLEAR 2 ENTRIES                             
         LR    RE,R4                                                            
         XCEFL                                                                  
         SAM24                                                                  
*                                                                               
*        CLC   LASTMDCL,SVQMED     TEST SAME MEDIA/CLIENT                       
*        JE    NET10                                                            
*                                                                               
         MVC   LASTMDCL,SVQMED     SAVE NEW MEDIA/CLIENT                        
         MVI   SVFNDCLT,C'N'                                                    
         BAS   RE,GETSPCLT         READ THE CLIENT HEADER                       
         JNE   EXIT                                                             
         BRAS  RE,GETSPPRD          GET SPOT PRODUCT NAME                       
         BRAS  RE,GETSPEST          GET SPOT ESTIMATE NAME                      
         CLI   RTYP,C'P'           PAYABLES?                                    
         JNE   NET05                                                            
         CLI   SVSYS,C'N'          AND NET, SKIP THE REST                       
         JE    EXIT                                                             
*                                                                               
NET05    MVI   SVFNDCLT,C'Y'                                                    
         BAS   RE,BLDBUFF          BUILD BUFFERIN RECORDS FOR THIS CLT          
                                                                                
NET10    CLI   SVFNDCLT,C'Y'                                                    
         JNE   EXIT                                                             
*                                                                               
         XC    BFKEY,BFKEY         LOOK FOR RECORDS IN BUFFER                   
         MVC   BFQPRD,SVQPRD                                                    
         MVC   BFEST,SVEST1                                                     
         MVC   BFBILDAT,SVBILLD2   BILL DATE                                    
         PACK  DUB,SVINV+2(4)                                                   
         CVB   R0,DUB                                                           
         STCM  R0,3,BFBILINV                                                    
*                                                                               
         MVI   SVMOSP+2,1          ADD DAY FOR DATCON                           
         GOTO1 VDATCON,DMCB,(1,SVMOSP),(3,FULL)                                 
         MVC   BFBILMOS(2),FULL    YEAR/MONTH OF SVC - YYMM                     
*                                                                               
         MVC   BFKEYOLD,BFREC      SAVE KEY TRHU MOS                            
         GOTO1 =V(BUFFERIN),DMCB,('BUFFARDH',BUFFET),BFREC,ACOMFACS             
         TM    4(R1),BUFFEEOF                                                   
         JO    EXIT                                                             
*                                                                               
NET20    CLC   BFKEYOLD,BFREC                                                   
         JNE   EXIT                                                             
*                                                                               
         SAM31                                                                  
         MVC   BUFFVNDR(4),BFBILNTW   MOVE NETWORK                              
         OC    BUFFVNDR,MYSPACES                                                
         MVC   SVSTA,BUFFVNDR                                                   
         MVI   SVSTA+4,C'N'                                                     
*                                                                               
         ZAP   BUFFGRS,BFBILGRS                                                 
         ZAP   BUFFNET,BFBILNET                                                 
*                                                                               
         BRAS  RE,GETSTADD         RUNS IN 24-BIT MODE                          
*                                                                               
         SAM31                                                                  
         AHI   R4,BUFFERLN                                                      
         LH    RF,=Y(BUFFERLN)     CLEAR AN ENTRY                               
         LR    RE,R4                                                            
         XCEF                                                                   
         SAM24                                                                  
*                                                                               
         GOTO1 =V(BUFFERIN),DMCB,('BUFFASEQ',BUFFET),BFREC,ACOMFACS             
         TM    4(R1),BUFFEEOF                                                   
         JO    EXIT                                                             
         J     NET20                                                            
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*============================================================                   
* READ CLIENT RECORD FOR SPOT                                                   
*============================================================                   
GETSPCLT NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         XC    KEY,KEY                                                          
         MVC   KEY+1(1),SESPTAGB                                                
         CLI   SVSYS,C'S'          TEST SPOT                                    
         JE    *+10                                                             
         MVC   KEY+1(1),SENETAGB                                                
*                                                                               
         CLI   SVTRNSTY,33         HAS TO BE TYPE 33,34, OR 35                  
         JL    GETSPC2             OTHERWISE, CONTINUE                          
         CLI   SVTRNSTY,35                                                      
         JH    GETSPC2                                                          
*                                                                               
         MVI   BYTE,X'01'                                                       
         CLI   SVQMED,C'T'                                                      
         BE    GETSPC2                                                          
         MVI   BYTE,X'02'                                                       
         CLI   SVQMED,C'R'                                                      
         BE    GETSPC2                                                          
         MVI   BYTE,X'03'                                                       
         CLI   SVQMED,C'N'         MEDIA N FOR CANADIAN AGY                     
         BE    GETSPC2                                                          
         CLI   SVSYS,C'N'          MEDIA N FOR NETPAK AGY                       
         BE    GETSPC2                                                          
         MVI   BYTE,X'04'                                                       
         CLI   SVQMED,C'X'                                                      
         BE    GETSPC2                                                          
         DC    H'0'                                                             
*                                                                               
GETSPC2  OC    KEY+1(1),BYTE                                                    
         GOTO1 VCLPACK,DMCB,SVQCLT,KEY+2                                        
*                                                                               
         MVC   KEYSAVE,KEY                                                      
         GOTO1 VDATAMGR,DMCB,DMRDHI,SPTDIR,KEYSAVE,KEY                          
         CLC   KEY(13),KEYSAVE                                                  
         JNE   EXIT                                                             
*                                                                               
GETSPC5  GOTO1 VDATAMGR,DMCB,GETREC,SPTFILE,KEY+14,SVCLTHDR                     
*                                                                               
         LA    RE,SVCLTHDR                                                      
         SAM31                                                                  
         MVC   BUFFCLNM(L'CNAME),CNAME-CLTHDRD(RE)                              
         OC    BUFFCLNM,MYSPACES                                                
         MVC   SVQCLTNM,BUFFCLNM                                                
         SAM24                                                                  
*                                                                               
         J     EQXIT                                                            
         LTORG                                                                  
         DROP  R4                                                               
         EJECT                                                                  
*============================================================                   
* READ PRODUCT RECORD FOR SPOT                                                  
*============================================================                   
GETSPPRD NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         XC    KEY,KEY                                                          
         LA    RE,SVCLTHDR                                                      
         MVC   KEY(SP_PKEYPRD-SP_PKEY),0(RE)                                    
         MVC   KEY+4(L'SVQPRD),SVQPRD                                           
*                                                                               
         MVC   KEYSAVE,KEY                                                      
         GOTO1 VDATAMGR,DMCB,DMRDHI,SPTDIR,KEYSAVE,KEY                          
         CLC   KEY(13),KEYSAVE                                                  
         JNE   EXIT                                                             
*                                                                               
         LARL  R8,MYPUBREC                                                      
         GOTO1 VDATAMGR,DMCB,GETREC,SPTFILE,KEY+14,(R8)                         
*                                                                               
         SAM31                                                                  
         MVC   BUFFPRNM(L'SP_PNAME),SP_PNAME-SP_PRDHDRD(R8)                     
         MVC   BUFFPUD1(L'SP_PUSER1),SP_PUSER1-SP_PRDHDRD(R8)                   
         MVC   BUFFPUD2(L'SP_PUSER2),SP_PUSER2-SP_PRDHDRD(R8)                   
         OC    BUFFPRNM,MYSPACES                                                
         OC    BUFFPUD1,MYSPACES                                                
         OC    BUFFPUD2,MYSPACES                                                
         SAM24                                                                  
*                                                                               
         J     EQXIT                                                            
         LTORG                                                                  
         EJECT                                                                  
*============================================================                   
* READ ESTIMATE RECORD FOR SPOT                                                 
*============================================================                   
GETSPEST NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         OC    SVEST1,SVEST1       NO ESTIMATE, DON'T GET NAME                  
         JZ    EQXIT                                                            
*                                                                               
         XC    KEY,KEY                                                          
         LARL  RE,MYPUBREC                                                      
         MVC   KEY(EKEYEST-EKEY),0(RE)                                          
         MVC   KEY+(EKEYEST-EKEY)(L'EKEYEST),SVEST1                             
*                                                                               
         MVC   KEYSAVE,KEY                                                      
         GOTO1 VDATAMGR,DMCB,DMRDHI,SPTDIR,KEYSAVE,KEY                          
         CLC   KEY(13),KEYSAVE                                                  
         JNE   EXIT                                                             
*                                                                               
         LARL  R8,MYPUBREC                                                      
         GOTO1 VDATAMGR,DMCB,GETREC,SPTFILE,KEY+14,(R8)                         
*                                                                               
         SAM31                                                                  
         MVC   BUFFESNM(L'EDESC),EDESC-ESTHDRD(R8)                              
         MVC   BUFFEUD1(L'EUSER1),EUSER1-ESTHDRD(R8)                            
         MVC   BUFFEUD2(L'EUSER2),EUSER2-ESTHDRD(R8)                            
         OC    BUFFESNM,MYSPACES                                                
         OC    BUFFEUD1,MYSPACES                                                
         OC    BUFFEUD2,MYSPACES                                                
         SAM24                                                                  
*                                                                               
         J     EQXIT                                                            
         LTORG                                                                  
         EJECT                                                                  
*==============================================================                 
* GET STATION ADDRESS RECORD                                                    
*==============================================================                 
         USING BUFFDATA,R4                                                      
GETSTADD NTR1  BASE=*,LABEL=*                                                   
         SAM24                                                                  
         XC    STAKEY,STAKEY                                                    
         LA    R8,STAKEY                                                        
         USING ADDRREC,R8                                                       
*                                                                               
         MVI   ADDKTYPE,C'A'                                                    
         MVC   ADDKMED,SVQMED                                                   
         CLI   SVSYS,C'N'          TEST NETWORK                                 
         JNE   *+8                                                              
         MVI   ADDKMED,C'N'        ALWAYS READ MEDIA N                          
         MVC   ADDKCALL,SVSTA      5 CHAR STATION                               
         MVC   ADDKAGY,SEAGYA                                                   
*                                                                               
         LARL  R8,MYPUBREC                                                      
         USING ADDRREC,R8                                                       
*                                                                               
         GOTO1 VDATAMGR,DMCB,DMRDHI,STATION,STAKEY,(R8)                         
         CLC   STAKEY(9),ADDRKEY                                                
         JE    GETSTAD2                                                         
         SAM31                                                                  
         MVC   BUFFNAME(24),=CL24'***** UNKNOWN *****'                          
         J     GETSTADX                                                         
*                                                                               
GETSTAD2 SAM31                                                                  
         MVC   BUFFNAME(12),ANAME                                               
         MVI   BUFFNAME+12,C'/'                                                 
         MVC   BUFFNAME+13(11),A2LINE       USUALLY CITY                        
*                                                                               
GETSTADX OC    BUFFNAME,MYSPACES                                                
         SAM24                                                                  
         J     EXIT                                                             
         LTORG                                                                  
         EJECT                                                                  
*=========================================================                      
* SINCE NETWORK BILLS ARE NOT IN SEQUENCE BY PRODUCT                            
* BUILD A BUFFER OF ALL UNIT BILLING RECORDS FOR CLIENT                         
*=========================================================                      
                                                                                
BLDBUFF  NTR1  BASE=*,LABEL=*                                                   
*                                                                               
*        L     R8,AXVNDDTS         POINT TO REQ START/END DATES                 
*                                                                               
*        GOTO1 VDATCON,DMCB,(3,0(R8)),(2,QSTARTB)                               
*        GOTO1 VDATCON,DMCB,(3,3(R8)),(2,QENDB)                                 
         GOTO1 VDATCON,DMCB,(3,STBDTE),(2,QSTARTB)                              
         GOTO1 VDATCON,DMCB,(3,ENBDTE),(2,QENDB)                                
*                                                                               
         GOTOR =V(BUFFERIN),DMCB,('BUFFAINI',BUFFET),0,ACOMFACS                 
                                                                                
*===============================================================                
* NOW READ UNIT BILLING RECORDS FOR THIS CLIENT FROM XSPFILE!!!                 
* AND TABLE RECORDS WITHIN THE LAST 7 DAYS                                      
*===============================================================                
                                                                                
         LA    R8,BIGKEY                                                        
         USING NUBK0KEY,R8                                                      
*                                                                               
         XC    NUBK0KEY,NUBK0KEY                                                
         MVI   NUBK0SYS,NUBK0SYQ   X'0E'                                        
         MVI   NUBK0STY,NUBK0STQ   X'0A'                                        
         MVC   NUBK0AM(3),SVCLTHDR+1 A-M/CLT                                    
*                                                                               
         MVC   BIGKEYSV,BIGKEY                                                  
         GOTO1 VDATAMGR,DMCB,DMRDHI,XSPDIR,BIGKEYSV,BIGKEY                      
         B     BLDB12                                                           
*                                                                               
BLDB10   GOTO1 VDATAMGR,DMCB,DMRSEQ,XSPDIR,BIGKEYSV,BIGKEY                      
*                                                                               
BLDB12   CLC   BIGKEY(5),BIGKEYSV  SAME A-M/CLT                                 
         JNE   BLDBPRT                                                          
*                                                                               
         LA    R8,BIGKEY                                                        
         USING NUBK0KEY,R8                                                      
*                                                                               
         CLC   NUBK0DAT,QSTARTB    TEST AIR DATE IN REQUEST PERIOD              
         JNH   BLDB10              PRIOR -IGNORE                                
         CLC   NUBK0DAT,QENDB      AFTER REQUEST PERIOD                         
         JH    BLDB10                                                           
         DROP  R8                                                               
*                                                                               
         LA    R8,REC                                                           
         USING NUBRECD,R8                                                       
*                                                                               
         LA    R0,BIGKEY+NUBDA-NUBKEY                                           
         GOTO1 VDATAMGR,DMCB,GETREC,XSPFILE,(R0),REC,DMWORK                     
*                                                                               
         LA    R6,NUBDATA                                                       
         USING NBILD,R6                                                         
*                                                                               
         MVI   ELCDLO,NBILELQ      GET X'10' BILLING ELEMENTS                   
         MVI   ELCDHI,NBILELQ                                                   
         BRAS  RE,NEXTEL2                                                       
         J     *+8                                                              
*                                                                               
BLDB20   BRAS  RE,NEXTEL                                                        
         BNE   BLDB10                                                           
*                                                                               
         XC    BFREC,BFREC                                                      
         MVC   BFQPRD,NBILPRDC     3-CHAR PRD CODE                              
         MVC   BFEST,NUBK0EST                                                   
         MVC   BFBILDAT,NUBK0BDT                                                
         MVC   BFBILINV,NBILNUM                                                 
         MVC   BFBILMOS,NBILMOS                                                 
         MVC   BFBILNTW,NUBK0NET                                                
*&&DO                                                                           
*------------------->>>                                                         
*------------------->>>                                                         
         CLC   BFQPRD,SVQPRD       TEST TO FILTER ON ONE PRD                    
         JNE   BLDB10                                                           
         CLC   BFEST,SVEST1                                                     
         JNE   BLDB10                                                           
*------------------->>>                                                         
*------------------->>>                                                         
*&&                                                                             
*                                                                               
BLDB22   L     R0,NBILGRS                                                       
         CVD   R0,DUB                                                           
         ZAP   BFBILGRS,DUB                                                     
*                                                                               
         L     R0,NBILNET                                                       
         CVD   R0,DUB                                                           
         ZAP   BFBILNET,DUB                                                     
*                                                                               
         CP    BFBILGRS,=P'0'                                                   
         JNE   BLDB32                                                           
         CP    BFBILNET,=P'0'                                                   
         JZ    BLDB20                                                           
                                                                                
BLDB32   GOTO1 =V(BUFFERIN),DMCB,('BUFFAPUT',BUFFET),BFREC,ACOMFACS             
         J     BLDB20                                                           
         EJECT                                                                  
*===============================================================                
* PRINT THE RECORDS IN BUFFER                                                   
*===============================================================                
                                                                                
BLDBPRT  DS    0H                                                               
         J     EXIT                                                             
* ROUTINE IS NOP UNTIL NEEDED AGAIN!                                            
         XC    BFKEY,BFKEY                                                      
         MVC   BFQPRD,SVQPRD                                                    
*                                                                               
         MVC   BFKEYOLD,BFREC      SAVE KEY                                     
         GOTO1 =V(BUFFERIN),DMCB,('BUFFARDH',BUFFET),BFREC,ACOMFACS             
*                                                                               
BLDBPRT2 CLC   BFKEYOLD(3),BFREC                                                
         JNE   EXIT                                                             
*        GOTO1 VHEXOUT,DMCB,BFREC,P,32,(31,=C'TOG')                             
*        GOTO1 ACREPORT                                                         
*                                                                               
         GOTO1 =V(BUFFERIN),DMCB,('BUFFASEQ',BUFFET),BFREC,ACOMFACS             
         TM    4(R1),BUFFEEOF                                                   
         JO    EXIT                                                             
         J     BLDBPRT2                                                         
*==============================================================                 
* READ NETWORK DEFINITION PASSIVES TO DECODE THE BUYKEY NETWORK                 
*==============================================================                 
                                                                                
GETNETS  NTR1  BASE=*,LABEL=*                                                   
         LARL  R1,NETLIST                                                       
*                                                                               
GETNET02 OC    0(4,R1),0(R1)       ENTRY USED YET                               
         JZ    GETNET04                                                         
         LA    R1,L'NETLIST(R1)    1024 BYTE ENTRIES                            
         J     GETNET02                                                         
*                                                                               
GETNET04 ST    R1,SENETLST         SAVE A(NEW LIST) IN SE TABLE                 
*                                                                               
         LA    R0,4                                                             
         XC    0(256,R1),0(R1)                                                  
         LA    R1,256(R1)                                                       
         JCT   R0,*-10                                                          
*                                                                               
         XC    KEY,KEY                                                          
         MVC   KEY(2),=X'0D91'                                                  
         MVC   KEY+2(2),SEAGYA                                                  
         MVC   KEYSAVE,KEY                                                      
         GOTO1 VDATAMGR,DMCB,DMRDHI,SPTDIR,KEYSAVE,KEY                          
         J     GETNET12                                                         
*                                                                               
GETNET10 GOTO1 VDATAMGR,DMCB,DMRSEQ,SPTDIR,KEY,KEY                              
*                                                                               
GETNET12 CLC   KEY(4),KEYSAVE                                                   
         JNE   EXIT                                                             
*                                                                               
         LA    R4,KEY                                                           
         USING NWKPASS,R4                                                       
*                                                                               
         LLC   RE,NWKPSEQ                                                       
         SLL   RE,2                X 4 BYTES/NETWORK                            
         A     RE,SENETLST                                                      
         MVC   0(4,RE),NWKPNET     SAVE CALL LETTERS                            
         AHI   R5,4                                                             
         J     GETNET10                                                         
         LTORG                                                                  
         EJECT                                                                  
NEXTEL   CLI   0(R6),0                                                          
         JE    NEXTELX                                                          
         LLC   R0,1(R6)                                                         
         LTR   R0,R0                                                            
         JZ    *+2                                                              
         AR    R6,R0                                                            
NEXTEL2  CLI   0(R6),0                                                          
         JE    NEXTELX                                                          
         CLC   ELCDLO,0(R6)                                                     
         JH    NEXTEL                                                           
         CLC   ELCDHI,0(R6)                                                     
         JL    NEXTEL                                                           
         CR    RB,RB                                                            
         J     *+6                                                              
NEXTELX  LTR   RB,RB                                                            
         BR    RE                                                               
         EJECT                                                                  
         DC    CL8'*COMMON*'                                                    
COMMON   DS    0D                                                               
SVSEACC  DS    X                                                                
THISSE   DS    A                                                                
THISSENUM DS   X                                                                
LASTSENUM DS   X                                                                
COUNTRY  DS    X                                                                
STBDTE   DS    XL3                 START DATE                                   
ENBDTE   DS    XL3                 END DATE                                     
*                                                                               
SPTDIR   DC    CL8'SPTDIR'                                                      
SPTFILE  DC    CL8'SPTFILE'                                                     
STATION  DC    CL8'STATION'                                                     
PRTDIR   DC    CL8'PRTDIR'                                                      
PRTFILE  DC    CL8'PRTFILE'                                                     
PUBDIR   DC    CL8'PUBDIR'                                                      
PUBFILE  DC    CL8'PUBFILE'                                                     
UNTDIR   DC    CL8'UNTDIR'                                                      
UNTFILE  DC    CL8'UNTFILE'                                                     
XSPDIR   DC    CL8'XSPDIR'                                                      
XSPFILE  DC    CL8'XSPFILE'                                                     
*TFILE   DC    CL8'CTFILE'                                                      
*MOPEN   DC    CL8'DMOPEN'                                                      
*                                                                               
          DS    0D                                                              
          DC    CL8'*SVPARMS'                                                   
SVPARMS   DS   0XL24                                                            
AXVNDWRK  DS   A                AXWORKG                                         
AXVNDRCV  DS   A                RECEIVABLE POSTING                              
AXVNDBUF  DS   A                OUTPUT BUFFER                                   
AXVNDDTS  DS   A                PWOS REQUEST START/END DATES                    
AXVNDID   DS   A                A(RECVBLE AGY ALPHA ID)                         
          DS   A                                                                
*                                                                               
SV25EL   DS    A                                                                
SV26EL   DS    A                                                                
SVTRNEL  DS    A                                                                
VCLPACK  DS    A                                                                
VSTAPACK DS    A                                                                
VCBLTAB  DS    A                                                                
VUTL     DC    V(UTL)                                                           
*                                                                               
BUYKEY   DS    CL32                                                             
CLRKEY   DS    CL32                                                             
*                                                                               
ELCDLO   DS    X                                                                
ELCDHI   DS    X                                                                
SVINVB   DS    H                                                                
SVBILLD2 DS    XL2                                                              
SVBILLD3 DS    XL3                                                              
SVPUB    DS    XL4                                                              
SVPUB8   DS    CL8                                                              
SVSYS    DS    CL1                                                              
SVMOS    DS    XL3                                                              
SVMOSP   DS    XL3                 PWOS(2)+1 FOR DAY                            
SVINV    DS    CL6                                                              
SVQMED   DS    XL1                                                              
SVQCLT   DS    CL3                                                              
SVQPRD   DS    CL3                                                              
SVPRDCD  DS    XL1                                                              
SVEST1   DS    XL1                                                              
SVEST2   DS    H                   PRINT EST IS 2 BYTES!                        
SVQCLTNM DS    CL36                                                             
SVPINV   DS    CL14                                                             
SVPSEQ   DS    XL2                                                              
SVPMKT   DS    CL4                 MARKET                                       
SVPBMKT  DS    XL2                 BINARY MARKET                                
SVPSTA   DS    CL5                 STATION CALL LETTERS                         
SVPBSTA  DS    XL3                 PACKED STATION                               
SVPPYST  DS    XL2                 PAY PERIOD START                             
SVPPYEN  DS    XL2                 PAY PERIOD END                               
SVPRTKEY DS    XL32                                                             
STAKEY   DS    XL15                                                             
SVSTA    DS    CL5                                                              
SVTRNSTY DS    XL1                 TRANSACTION TYPE                             
SVSEQNUM DS    XL2                                                              
LASTMDCL DS    CL4                 PREVIOUS A-M/CLT                             
LASTPUB  DS    XL4                                                              
LASTSEQ  DS    XL2                                                              
GROSS    DS    PL8                                                              
NET      DS    PL8                                                              
*                                                                               
QSTARTB  DS    XL2                                                              
QENDB    DS    XL2                                                              
BUYFND   DS    CL1                 BUY FOUND?                                   
*                                                                               
         DS    0D                                                               
         DC    CL8'*STAWORK'                                                    
STAWORK  DS    XL64                                                             
*                                                                               
         DS    CL8'*BIGKEY*'                                                    
BIGKEY   DS    XL64                                                             
BIGKEYSV DS    XL64                                                             
*                                                                               
BFREC    DS    0XL32                                                            
BFKEY    DS    0XL16                                                            
BFQPRD   DS    CL3                 PRODUCT NUMBER                               
BFEST    DS    XL1                 ESTIMATE NUMBER                              
BFBILDAT DS    XL2                 BILLING Y/M                                  
BFBILINV DS    XL2                 INVOICE NUMBER                               
BFBILMOS DS    XL2                 BILLING Y/M OF SVC                           
BFBILNTW DS    CL4                 NETWORK                                      
         DS    XL2                                                              
*                                                                               
BFBILGRS DS    PL8                 GROSS DOLLARS                                
BFBILNET DS    PL8                 NET DOLLARS                                  
BFRECL   EQU   *-BFREC                                                          
*                                                                               
         DC    CL8'BFKEYOLD'                                                    
BFKEYOLD DS    XL10                PRD/EST/BILLDT/INV/MOS                       
*                                                                               
         LTORG                                                                  
*                                                                               
SPTFLIST DS    0D                                                               
         DC    CL1'N',CL7'SPTDIR'                                               
         DC    CL1'N',CL7'SPTFIL'                                               
         DC    CL1'N',CL7'STAFILE'                                              
         DC    CL8'X      '                                                     
*                                                                               
PRTFLIST DS    0D                                                               
         DC    CL1'N',CL7'PRTDIR'                                               
         DC    CL1'N',CL7'PRTFIL'                                               
         DC    CL1'N',CL7'PUBDIR'                                               
         DC    CL1'N',CL7'PUBFIL'                                               
         DC    CL8'X      '                                                     
*                                                                               
NETFLIST DS    0D                                                               
         DC    CL1'N',CL7'SPTDIR'                                               
         DC    CL1'N',CL7'SPTFIL'                                               
         DC    CL1'N',CL7'UNTDIR'                                               
         DC    CL1'N',CL7'UNTFIL'                                               
         DC    CL1'N',CL7'XSPDIR'                                               
         DC    CL1'N',CL7'XSPFIL'                                               
         DC    CL1'N',CL7'STAFILE'                                              
         DC    CL8'X      '                                                     
*                                                                               
*                                                                               
SVFNDCLT DS    C                                                                
         DS    0D                                                               
         DC    CL8'*SVCLT*'                                                     
SVCLTHDR DS    2000C                                                            
*                                                                               
         DC    CL8'*SETAB*'                                                     
SETAB    DC    8XL16'00'                                                        
SETABX   EQU   *                                                                
*                                                                               
BUFFET   BUFFD TYPE=P,KEYLEN=L'BFKEY,COLUMNS=2,FILE=BUFFWK,            *        
               BUFFERS=20                                                       
*                                                                               
         DS    0D                                                               
         DC    CL8'**REC***'                                                    
REC      DS    6000C                                                            
*                                                                               
         DS    0D                                                               
         DC    CL8'*PUBREC*'                                                    
MYPUBREC DS    2000C                                                            
*                                                                               
         DS    0D                                                               
         DC    CL8'*ESTREC*'                                                    
ESTREC   DS    2000C                                                            
*                                                                               
         DS    0D                                                               
         DC    CL8'*CLRREC*'                                                    
CLRREC   DS    4000C                                                            
*                                                                               
         DS    0D                                                               
         DC    CL8'*INVTAB*'                                                    
INVTAB   DS    50C                                                              
*                                                                               
         DS    0D                                                               
         DC    CL8'*NETLIST'       CANADIAN NETWORK LIST                        
NETLIST  DS    8XL1024                                                          
NETLISTX EQU   *                                                                
*                                                                               
INVTABD  DSECT                                                                  
ITINV    DS    CL11                INVOICE                                      
ITSEQ    DS    XL2                 SEQUENCE                                     
ITGROSS  DS    XL4                 GROSS                                        
ITNET    DS    XL4                 NET                                          
ITABLEN  EQU   *-INVTABD                                                        
*                                                                               
SETABD   DSECT                                                                  
SEAGYA   DS    CL2                                                              
SENUMSPT DS    XL1                 SPOT SE NUMBER                               
SESPTAGB DS    XL1                 BINARY AGY CODE                              
SESPTOPN DS    XL1                 C'Y' IF FILE IS OPEN                         
SENUMNET DS    XL1                                                              
SENETAGB DS    XL1                                                              
SENETOPN DS    XL1                                                              
SENETLST DS    AL4                                                              
SENUMPRT DS    XL1                                                              
SEPRTOPN DS    XL1                                                              
*                                                                               
       ++INCLUDE AXTRBBFD                                                       
         PRINT OFF                                                              
       ++INCLUDE AXTRBWRK                                                       
STABUCKD DSECT                                                                  
       ++INCLUDE SPGENSTAB                                                      
       ++INCLUDE SPGENNDEF                                                      
CLTHDRD  DSECT                                                                  
       ++INCLUDE SPGENCLT                                                       
PCLTRECD DSECT                                                                  
       ++INCLUDE PCLTREC                                                        
PPRDRECD DSECT                                                                  
       ++INCLUDE PPRDREC                                                        
PESTRECD DSECT                                                                  
       ++INCLUDE PESTREC                                                        
PBUYRECD DSECT                                                                  
       ++INCLUDE PBUYREC                                                        
       ++INCLUDE PBDELEM                                                        
       ++INCLUDE PCOMELEM                                                       
       ++INCLUDE PPGENPBNV                                                      
       ++INCLUDE PPCLRST                                                        
PPAYELD  DSECT                                                                  
       ++INCLUDE PPAYELEM                                                       
PBILELD  DSECT                                                                  
       ++INCLUDE PBILELEM                                                       
       ++INCLUDE PPGENBYCC                                                      
       ++INCLUDE PPPPIDEL                                                       
       ++INCLUDE PSERELEM                                                       
PUBRECD  DSECT                                                                  
       ++INCLUDE PUBREC                                                         
PUBNAMELD DSECT                                                                 
       ++INCLUDE PUBNAMEL                                                       
         PRINT OFF                                                              
       ++INCLUDE CTGENFILE                                                      
*PREFIX=AC_                                                                     
       ++INCLUDE ACGENFILE                                                      
*PREFIX=SP_                                                                     
PRDHDRD  DSECT                                                                  
       ++INCLUDE SPGENPRD                                                       
STARECD  DSECT                                                                  
       ++INCLUDE SPGENSTA                                                       
*PREFIX=                                                                        
ESTHDRD  DSECT                                                                  
       ++INCLUDE SPGENEST                                                       
       ++INCLUDE SPGENCLRST                                                     
       ++INCLUDE DDREPMASTD                                                     
       ++INCLUDE DDBUFFD                                                        
       ++INCLUDE FAUTL                                                          
       ++INCLUDE SPSTAPACKD                                                     
       ++INCLUDE NEGENUBILL                                                     
       ++INCLUDE SPGENADD                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'128AXTRBVND  11/16/20'                                      
         END                                                                    
