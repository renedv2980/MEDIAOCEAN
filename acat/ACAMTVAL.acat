*          DATA SET ACAMTVAL   AT LEVEL 010 AS OF 11/26/87                      
*CATALP AMTVAL                                                                  
         SPACE 1                                                                
*PARM1   XL1   NUMBER OF DECIMAL POINTS (C'N' FOR NONE)                         
*        AL3   A(INPUT DATA)                                                    
*PARM2   CL1   C'S' FOR SWAP DECIMAL POINTS WITH COMMAS                         
*        XL3   L'INPUT DATA                                                     
*PARM3   XL1   LANGUAGE CODE                                                    
*        AL3   A(8 BYTE PACKED NUMBER GIVING MAXIMUM VALUE)                     
         TITLE 'ACAMTVAL - GENERALISED CASH OR DECIMAL VALIDATION'              
         PRINT NOGEN                                                            
AMTVAL   CSECT                                                                  
         NMOD1 CASHX-CASHD,XXCASHXX                                             
         USING CASHD,RC                                                         
         LM    R2,R4,0(R1)         R2=A(INPUT),R3=L'INPUT,R4=A(MAX)             
*                                                                               
         MVC   SWAP,4(R1)          SAVED PASSED SWAP DECPNT/COMMAS              
         MVI   4(R1),0                                                          
         LA    R3,0(R3)                                                         
*                                                                               
         MVC   LANG,8(R1)          SAVE PASSED LANGUAGE CODE                    
         MVI   8(R1),0                                                          
         LA    R4,0(R4)                                                         
         CLI   LANG,0                                                           
         BE    *+12                                                             
         CLI   LANG,7                                                           
         BNH   *+10                                                             
         MVC   LANG,DEFLANG        SET DEFAULT LANGUAGE                         
*                                                                               
         ZAP   MAX,0(8,R4)                                                      
         ZAP   MIN,0(8,R4)                                                      
         MP    MIN,=P'-1'                                                       
         ZAP   PWK1,=P'0'                                                       
         ZAP   PWK2,=P'0'                                                       
         ZAP   PWK3,=P'0'                                                       
         LA    R4,WORK             R4 OUTPUT POINTER                            
         SR    R5,R5               R5 OUTPUT LENGTH                             
         SR    R6,R6               R6 DECIMALS                                  
         LR    R7,R1               R7 PARAMETER SAVE                            
         SR    R8,R8               R8 DECIMAL POINTS                            
         IC    R8,0(R1)                                                         
         XC    MAGCNT,MAGCNT                                                    
         XC    MAGPOS,MAGPOS                                                    
*                                                                               
         LTR   R8,R8               TEST NUMBER OF DECIMAL POINTS                
         BNZ   *+12                                                             
         LA    R8,2                ZERO DEFAULTS TO TWO                         
         B     CA0                                                              
         CLI   0(R1),C'N'          C'N' MEANS NONE                              
         BNE   *+10                                                             
         SR    R8,R8                                                            
         B     CA0                                                              
         CH    R8,=H'6'                                                         
         BH    CAERROR             ERROR IF MORE THAN SIX                       
*                                                                               
CA0      MVI   0(R7),X'00'         RETURN OK RESULT                             
         MVC   MINUS(4),=C'NNNN'                                                
         LTR   R3,R3               LENGTH MUST BE POSITIVE                      
         BNP   CAERROR                                                          
         CH    R3,=H'16'           AND NOT MORE THAN 16                         
         BH    CAERROR                                                          
*                                                                               
         CLI   SWAP,C'S'           TEST IF DECPNT IS COMMA                      
         BE    CA0A                                                             
         MVI   MAGSEP,C','                                                      
         MVI   DECSEP,C'.'                                                      
         B     CA0B                                                             
CA0A     MVI   MAGSEP,C'.'                                                      
         MVI   DECSEP,C','                                                      
*                                                                               
CA0B     CLI   0(R2),C' '          IGNORE ANY LEADING SPACES                    
         BNE   CA1                                                              
         LA    R2,1(R2)                                                         
         BCT   R3,*-12                                                          
         SPACE 2                                                                
CAERROR  MVI   0(R7),X'FF'         RETURN INVALID RESULT                        
         SPACE 2                                                                
CAZERO   ZAP   PWK1,=P'0'          SET ZERO AMOUNT                              
         SPACE 2                                                                
CAEXIT   LA    R1,PWK1+8     RETURN ADDRESS OF 8 BYTE PACKED NUMBER             
         CP    PWK1,MAX                                                         
         BH    CAERROR                                                          
         CP    PWK1,MIN                                                         
         BL    CAERROR                                                          
         ST    R1,4(R7)                                                         
         XMOD1 1                                                                
         EJECT                                                                  
CA1      LA    RE,TABLEN           POINT TO KEYWORD TABLE                       
         SR    RF,RF               MODIFY LANG CODE                             
         CLI   LANG,X'02'                                                       
         BNH   CA1A                                                             
         IC    RF,LANG                                                          
         SH    RF,=H'2'                                                         
CA1A     STH   RF,HALF             STORE MODDED VALUE                           
         MH    RE,HALF             LENGTH * MOD = START                         
         LA    RE,KEYTAB(RE)       POINT TO START                               
         LA    RF,4                LOOP CTR                                     
CA1B     CLC   0(3,R2),0(RE)       TEST FOR KEYWORDS MEANING 'ZERO'             
         BE    CAZERO                                                           
         LA    RE,3(RE)                                                         
         BCT   RF,CA1B                                                          
*                                                                               
         ST    RE,RSAV             STORE ADDR OF 1ST CURR SYMBOL                
CA2      LA    RF,2                SET SYMBOL LOOP CTR                          
         L     RE,RSAV             RESTORE 1ST SYMBOL ADDR                      
CA2A     CLC   0(1,R2),0(RE)       TEST FOR INPUT CURRENCY SYMBOL(S)            
         BE    CA3                                                              
         LA    RE,1(RE)                                                         
         BCT   RF,CA2A                                                          
         CLI   0(R2),C'+'          IGNORE LEADING PLUS SIGN                     
         BE    CA3                                                              
         CLI   0(R2),C'-'          LEADING MINUS SIGN SETS SIGN                 
         BE    *+12                                                             
         STCM  R2,7,MAGPOS                                                      
         B     CA6                                                              
         MVI   MINUS,C'Y'                                                       
CA3      LA    R2,1(R2)                                                         
         BCT   R3,CA2                                                           
         B     CAERROR                                                          
         SPACE 2                                                                
CA4      LA    R2,1(R2)            BUMP INPUT POINTER                           
         BCT   R3,CA6                                                           
         B     CAERROR                                                          
         SPACE 2                                                                
CA6      CLC   0(1,R2),MAGSEP      IGNORE MAGNITUDE SEPARATOR                   
         BE    CA7                                                              
         CLC   0(1,R2),DECSEP      DECIMAL SEPARATOR SETS DECIMAL MODE          
         BNE   CA8                                                              
         MVI   DECMODE,C'Y'                                                     
         CLI   MAGCNT,X'00'                                                     
         BE    CA4                                                              
         MVI   MAGCNT,DECPNT                                                    
         B     CA7A                                                             
         SPACE 2                                                                
CA7      CLI   DECMODE,C'Y'                                                     
         BE    CAERROR                                                          
         SR    R9,R9                                                            
         IC    R9,MAGCNT           KEEP COUNT OF MAGSEPS                        
         LA    R9,1(R9)                                                         
         STC   R9,MAGCNT                                                        
         CLI   MAGCNT,MAGMAX                                                    
         BH    CAERROR                                                          
*                                                                               
CA7A     SR    R9,R9                                                            
         LR    R9,R2                                                            
         S     R9,MAGPOS           CALC DISTANCE FROM START                     
         CH    R9,=H'0'            OR PREVIOUS MAGNITUDE SEPARATOR              
         BE    CAERROR             LEADING MAGSEP INVALID                       
         CLI   MAGCNT,MAG1ST       IF 1ST MAGSEP                                
         BNE   CA7B                                                             
         CH    R9,=H'3'            1, 2, OR 3 ARE VALID                         
         BH    CAERROR                                                          
         B     CA7C                                                             
CA7B     CH    R9,=H'4'            ELSE MUST BE 4                               
         BNE   CAERROR                                                          
CA7C     ST    R2,MAGPOS                                                        
         B     CA4                                                              
         SPACE 2                                                                
CA8      CLI   0(R2),C'+'          PLUS/MINUS/TIMES SIGN IMPLIES THAT           
         BE    CA10                A PERCENTAGE ADJUSTMENT FOLLOWS              
         CLI   0(R2),C'-'                                                       
         BE    CA10                                                             
         CLI   0(R2),C'X'                                                       
         BNE   CA12                                                             
CA10     CLI   MAGCNT,X'00'        END OF NUMBER                                
         BE    CA11                                                             
         CLI   MAGCNT,DECPNT                                                    
         BE    CA11                                                             
         LR    R9,R2               SO VALIDATE LAST MAGSEP POSITION             
         S     R9,MAGPOS                                                        
         CH    R9,=H'4'                                                         
         BNE   CAERROR                                                          
CA11     CLI   PERCENT,C'N'        ONLY ONE PLUS/MINUS/TIMES ALLOWED            
         BNE   CAERROR                                                          
         MVC   PERCENT(1),0(R2)    SAVE PERCENT ACTION +/-/X                    
         XC    MAGCNT,MAGCNT                                                    
         B     CA30                                                             
         SPACE 2                                                                
CA12     CLI   0(R2),X'6C'         PCT SIGN ENDS PCT EXPRESSION                 
         BNE   CA14                                                             
         CH    R3,=H'1'                                                         
         BNE   CAERROR                                                          
         CLI   PERMODE,C'Y'                                                     
         BNE   CAERROR                                                          
         B     CA30                                                             
         SPACE 2                                                                
CA14     CLI   0(R2),C'0'          CHECK FOR VALID NUMERIC                      
         BL    CAERROR                                                          
         CLI   0(R2),C'9'                                                       
         BH    CAERROR                                                          
         MVC   0(1,R4),0(R2)       MOVE A NUMBER INTO WORK                      
         LA    R4,1(R4)                                                         
         LA    R5,1(R5)                                                         
         CLI   DECMODE,C'Y'                                                     
         BNE   CA15                                                             
         LA    R6,1(R6)            BUMP NUM OF DEC POINTS                       
         CR    R6,R8                                                            
         BH    CAERROR             ERROR IF MORE THAN MAX POINTS                
         SPACE 2                                                                
CA15     CLI   PERMODE,C'Y'                                                     
         BE    CA4                                                              
         SPACE 2                                                                
CA16     LA    R2,1(R2)                                                         
         BCT   R3,CA6                                                           
*                                                                               
         CLI   MAGCNT,X'00'                                                     
         BE    CA30                                                             
         CLI   MAGCNT,DECPNT                                                    
         BE    CA30                                                             
         LR    R9,R2                                                            
         S     R9,MAGPOS                                                        
         CH    R9,=H'4'                                                         
         BNE   CAERROR                                                          
         SPACE 2                                                                
CA30     CR    R6,R8               NOW GENERALISE ON N DECIMAL POINTS           
         BE    CA32                                                             
         MVI   0(R4),C'0'                                                       
         LA    R4,1(R4)                                                         
         LA    R5,1(R5)                                                         
         LA    R6,1(R6)                                                         
         B     CA30                                                             
         SPACE 2                                                                
CA32     CH    R5,=H'15'                                                        
         BH    CAERROR                                                          
         BCTR  R5,0                GET VAR NUMERIC INTO 15                      
         EX    R5,VARPACK          AND CHECK FOR MINUS                          
         CP    PWK3,MAX                                                         
         BH    CAERROR                                                          
         CP    PWK3,MIN                                                         
         BL    CAERROR                                                          
         CLI   PERCENT,C'-'        WAS TRAILING MINUS ENTERED                   
         BNE   CA33                                                             
         CH    R3,=H'1'            AS LAST CHARACTER                            
         BNE   CA33                                                             
         CLI   MINUS,C'Y'                                                       
         BE    CAERROR             IT'S AN ERROR TWO MINUS SIGNS                
         MVI   PERCENT,C'N'                                                     
         MVI   MINUS,C'Y'                                                       
         SPACE 1                                                                
CA33     CLI   MINUS,C'Y'                                                       
         BNE   CA34                                                             
         MP    PWK3,=P'-1'                                                      
         SPACE 2                                                                
CA34     CLI   PERMODE,C'Y'                                                     
         BE    CA36                                                             
         ZAP   PWK1,PWK3                                                        
         CLI   PERCENT,C'N'                                                     
         BE    CAEXIT                                                           
         LA    R4,WORK             AMOUNT STORED NOW SET UP                     
         SR    R5,R5               FOR EDITING A PERCENT EXPRESSION             
         SR    R6,R6                                                            
         MVI   MINUS,C'N'                                                       
         MVI   DECMODE,C'N'                                                     
         MVI   PERMODE,C'Y'                                                     
         LA    R8,2                SET MAX POINTS TO 2 IN PERCENT MODE          
         B     CA4                                                              
         SPACE 2                                                                
CA36     ZAP   PWK2,PWK3                                                        
         MP    PWK2,PWK1+8(8)                                                   
         SRP   PWK2,60,5                                                        
         CLI   PERCENT,C'X'                                                     
         BE    CA40                                                             
         CLI   PERCENT,C'-'                                                     
         BNE   CA38                                                             
         MP    PWK2,=P'-1'                                                      
         SPACE 2                                                                
CA38     AP    PWK1,PWK2                                                        
         B     CAEXIT                                                           
         SPACE 2                                                                
CA40     ZAP   PWK1,PWK2                                                        
         B     CAEXIT                                                           
         EJECT                                                                  
VARPACK  PACK  PWK3,WORK(0)                                                     
         SPACE 1                                                                
KEYTAB   DC    C'BONFREZERNIL$Â›'                                                
TABLEN   EQU   *-KEYTAB                                                         
GERKEY   DC    C'BONKOSNULNULDM'                                                
FREKEY   DC    C'PRIGRAZERZERFF'                                                
         SPACE 1                                                                
* GET DEFAULT LANG CODE                                                         
*                                                                               
DEFLANG  DS    0C                                                               
*&&UK*&& DC    X'01'                                                            
*&&US*&& DC    X'02'                                                            
         SPACE 1                                                                
         LTORG                                                                  
         SPACE 1                                                                
CASHD    DSECT                                                                  
DUB      DS    D                                                                
RSAV     DS    2F                                                               
MAGPOS   DS    F                                                                
HALF     DS    H                                                                
WORK     DS    CL16                                                             
PWK1     DS    PL16                                                             
PWK2     DS    PL16                                                             
PWK3     DS    PL16                                                             
MAX      DS    PL16                                                             
MIN      DS    PL16                                                             
MINUS    DS    CL1                                                              
DECMODE  DS    CL1                                                              
PERCENT  DS    CL1                                                              
PERMODE  DS    CL1                                                              
LANG     DS    CL1                                                              
SWAP     DS    CL1                                                              
MAGSEP   DS    CL1                                                              
DECSEP   DS    CL1                                                              
MAGCNT   DS    XL1                                                              
MAGMAX   EQU   X'03'                                                            
MAG1ST   EQU   X'01'                                                            
DECPNT   EQU   X'FF'                                                            
CASHX    DS    0C                                                               
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'010ACAMTVAL  11/26/87'                                      
         END                                                                    
