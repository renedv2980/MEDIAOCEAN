*          DATA SET DDCASHVUS  AT LEVEL 007 AS OF 06/27/01                      
*CATALP CASHVAL                                                                 
         TITLE 'DDCASHVAL - GENERALISED CASH OR DECIMAL VALIDATION'             
         PRINT NOGEN                                                            
CASHVAL  CSECT                                                                  
         NMOD1 CASHX-CASHD,**CASH**,CLEAR=YES                                   
         USING CASHD,RC                                                         
         LR    RA,R1               RA=A(PARAMETER LIST)                         
         TM    4(RA),X'80'         TEST CURRENCY SYMBOLS NOT ALLOWED            
         BZ    *+8                                                              
         OI    MODE,MODENOC        SET INDICATOR                                
         TM    4(RA),X'40'         TEST DON'T DEFAULT TO 2 DPS                  
         BZ    *+8                                                              
         OI    MODE,MODENDP        SET INDICATOR                                
         TM    4(RA),X'01'         TEST FORCE US/UK EDIT                        
         BZ    *+8                                                              
         OI    MODE,MODEFRC        SET INDICATOR                                
         NI    4(RA),X'FF'-X'C1'                                                
         LM    R2,R3,0(RA)         R2=A(INPUT), R3=L'INPUT                      
         LA    R2,0(R2)                                                         
         LTR   R3,R3               TEST INPUT LENGTH IS POSITIVE                
         BNP   CAERROR                                                          
*                                                                               
         MVC   WORK(1),0(RA)       EXTRACT DECIMAL PLACES                       
         NI    WORK,X'FF'-X'80'                                                 
         SR    R8,R8               R8=DECIMAL POINTS                            
         ICM   R8,1,WORK                                                        
         BNZ   *+8                                                              
         TM    MODE,MODENDP        TEST DON'T SET DEFAULT                       
         BNZ   *+8                                                              
         LA    R8,2                DEFAULT TO 2 IF UNSPECIFIED                  
         CLI   0(RA),C'N'          C'N' MEANS NONE WITH XL4 OUTPUT              
         BNE   *+10                                                             
         SR    R8,R8                                                            
         B     CA2                                                              
         CLI   0(RA),C'0'          C'0' MEANS NONE WITH PL8 OUTPUT              
         BNE   *+14                                                             
         SR    R8,R8                                                            
         OI    MODE,MODEPL8                                                     
         B     CA2                                                              
         CH    R8,=H'6'            ELSE CHECK NUMBER OF DECIMAL PLACES          
         BH    CAERROR                                                          
         TM    0(RA),X'80'         TEST DECIMAL OUTPUT REQUESTED                
         BZ    *+8                                                              
         OI    MODE,MODEPL8                                                     
*                                                                               
CA2      STC   R8,POINTS           SAVE N'DECIMAL POINTS                        
         MVI   0(RA),0             RESET ERROR RETURN BYTE                      
         CTRY  ,                                                                
         STCM  R0,15,AGCOPT        GET AGCOPT/AGCTRY/LANG/CTRY                  
         TM    MODE,MODEFRC        TEST FORCE US/UK EDIT                        
         BZ    *+8                                                              
         MVI   LANG,LANGEUS                                                     
         LA    R1,LANGTAB                                                       
         USING LANGTABD,R1                                                      
CA4      CLI   LANGLANG,LANGEOT    USE DEFAULTS IF END OF TABLE                 
         BE    CA4A                                                             
         CLC   LANGLANG,LANG       MATCH ON LANGUAGE CODE                       
         BE    CA4A                                                             
         LA    R1,LANGTABL(R1)                                                  
         B     CA4                                                              
CA4A     MVC   MAGSEP,LANGMAG      EXTRACT LANGUAGE SPECIFIC VALUES             
         MVC   DECSEP,LANGDEC                                                   
         MVC   CURSYM,LANGSYM                                                   
         MVC   SPC(SPCL),LANGSPC                                                
         DROP  R1                                                               
*                                                                               
         LA    R4,WORK             R4=A(OUTPUT)                                 
         SR    R5,R5               R5=L'OUTPUT                                  
         SR    R6,R6               R6=DECIMALS                                  
         CLI   0(R2),C' '          IGNORE ANY LEADING SPACES                    
         BNE   *+16                                                             
         LA    R2,1(R2)                                                         
         BCT   R3,*-12                                                          
         B     CAERROR                                                          
*                                                                               
         LA    RE,SPC              POINT TO SPECIAL WORD LIST                   
         LA    RF,LANGSPCN         SET LOOP COUNTER                             
         CLC   0(L'LANGSPC,R2),0(RE)                                            
         BE    CAZERO                                                           
         LA    RE,L'LANGSPC(RE)                                                 
         BCT   RF,*-14                                                          
         TM    MODE,MODENOC                                                     
         BNZ   CA8                 CURRENCY SYMBOLS NOT ALLOWED                 
*                                                                               
CA5      CLI   CURSYM+1,C' '       TEST 2 CHARACTER CURRENCY SYMBOL             
         BNE   CA6                                                              
         CLC   0(1,R2),CURSYM      DEAL WITH ONE CHARACTER CURRENCY             
         BNE   CA8                                                              
         B     CA10                                                             
*                                                                               
CA6      CLC   0(2,R2),CURSYM      DEAL WITH TWO CHARACTER CURRENCY             
         BNE   CA8                                                              
         LA    R2,1(R2)                                                         
         BCTR  R3,0                                                             
         B     CA10                                                             
*                                                                               
CA8      CLI   0(R2),C'X'          CAN'T LEAD WITH MULTIPLY SIGN                
         BE    CAERROR                                                          
         CLI   0(R2),C'*'          CAN'T LEAD WITH MULTIPLY SIGN                
         BE    CAERROR                                                          
         CLI   0(R2),C'+'          IGNORE LEADING PLUS SIGN                     
         BE    CA10                                                             
         CLI   0(R2),C'-'          LEADING MINUS SIGN SETS SIGN                 
         BE    *+12                                                             
         ST    R2,MAGPOS           SAVE START ADR                               
         B     CA14                                                             
         OI    MODE,MODENEG                                                     
*                                                                               
CA10     LA    R2,1(R2)            NEXT INP CHAR                                
         BCT   R3,CA5              R3 = INP LENGTH                              
         B     CAERROR                                                          
*                                                                               
CA12     LA    R2,1(R2)            BUMP INPUT POINTER                           
         BCT   R3,CA14                                                          
         B     CAERROR                                                          
*                                                                               
CA14     CLC   MAGSEP,0(R2)        VALIDATE MAGNITUDE SEPARATOR                 
         BE    CA15                                                             
         CLC   DECSEP,0(R2)        DECIMAL SEPARATOR SET DEC MODE               
         BNE   CA17                                                             
         OI    MODE,MODEDEC                                                     
         CLI   MAGCNT,0            VALIDATE DEC POINT POSITION...               
         BE    CA12                                                             
         MVI   MAGCNT,DECPNT                                                    
         B     CA16                ... ONLY IF MAGSEPS EXIST                    
*                                                                               
CA15     TM    MODE,MODEDEC                                                     
         BNZ   CAERROR             NO MAGSEPS AFTER DEC POINT                   
         IC    R1,MAGCNT           KEEP COUNT OF SEPARATORS                     
         LA    R1,1(R1)                                                         
         CLM   R1,1,=AL1(MAGMAX)                                                
         BH    CAERROR                                                          
         STC   R1,MAGCNT                                                        
*                                                                               
CA16     LR    R1,R2                                                            
         S     R1,MAGPOS           CALC DISTANCE FROM START                     
         BNP   CAERROR             LEADING MAGSEP INVALID                       
         CLI   MAGCNT,1            IF 1ST MAGSEP                                
         BNE   *+16                                                             
         CH    R1,=H'3'            1,2 OR 3 ARE VALID                           
         BH    CAERROR                                                          
         B     *+12                                                             
         CH    R1,=H'4'            ELSE MUST BE 4                               
         BNE   CAERROR                                                          
         ST    R2,MAGPOS           SAVE ADDRESS OF MAGSEP                       
         B     CA12                                                             
*                                                                               
CA17     CLI   0(R2),C'+'          PLUS/MINUS/TIMES SIGN IMPLIES THAT           
         BE    CA18                A PERCENTAGE ADJUSTMENT FOLLOWS              
         CLI   0(R2),C'*'                                                       
         BE    CA18                                                             
         CLI   0(R2),C'X'                                                       
         BE    CA18                                                             
         CLI   0(R2),C'-'                                                       
         BNE   CA20                                                             
         CH    R3,=H'1'            TEST TRAILING SIGN                           
         BNE   CA18                                                             
         TM    MODE,MODENEG        ENSURE SIGN WAS NOT LEADING                  
         BNZ   CA18                                                             
         OI    MODE,MODENEG        SET NEGATIVE NUMBER                          
         B     CA28                                                             
*                                                                               
CA18     CLI   MAGCNT,0            END OF NUMBER - CHECK SEPS VALID             
         BE    CA19                                                             
         CLI   MAGCNT,DECPNT                                                    
         BE    CA19                                                             
         LR    R1,R2               R2 POINTS BEYOND NUMBER                      
         S     R1,MAGPOS                                                        
         CH    R1,=H'4'                                                         
         BNE   CAERROR                                                          
*                                                                               
CA19     CLI   PERCENT,0           ONLY ONE PLUS/MINUS/TIMES ALLOWED            
         BNE   CAERROR                                                          
         MVC   PERCENT,0(R2)       SAVE PERCENT ACTION +/-/X                    
         MVI   MAGCNT,0            RESET MAGCNT                                 
         B     CA28                                                             
*                                                                               
CA20     CLI   0(R2),C'%'          PCT SIGN ENDS PCT EXPRESSION                 
         BNE   CA22                                                             
         CH    R3,=H'1'                                                         
         BNE   CAERROR                                                          
         TM    MODE,MODEPCT                                                     
         BZ    CAERROR                                                          
         B     CA28                                                             
*                                                                               
CA22     CLI   0(R2),C'0'          CHECK FOR VALID NUMERIC                      
         BL    CAERROR                                                          
         CLI   0(R2),C'9'                                                       
         BH    CAERROR                                                          
         MVC   0(1,R4),0(R2)       MOVE A NUMBER INTO WORK                      
         LA    R4,1(R4)                                                         
         LA    R5,1(R5)                                                         
         TM    MODE,MODEDEC                                                     
         BZ    CA24                                                             
         LA    R6,1(R6)            BUMP NUM OF DEC POINTS                       
         CR    R6,R8                                                            
         BH    CAERROR             ERROR IF MORE THAN MAX POINTS                
*                                                                               
CA24     TM    MODE,MODEPCT                                                     
         BNZ   CA12                                                             
*                                                                               
CA26     LA    R2,1(R2)                                                         
         BCT   R3,CA14                                                          
*                                                                               
         CLI   MAGCNT,0            END OF INPUT - CHECK SEPS ARE VALID          
         BE    CA28                                                             
         CLI   MAGCNT,DECPNT                                                    
         BE    CA28                                                             
         LR    R1,R2                                                            
         S     R1,MAGPOS                                                        
         CH    R1,=H'4'                                                         
         BNE   CAERROR                                                          
*                                                                               
CA28     CR    R6,R8               NOW GENERALISE ON N DECIMAL POINTS           
         BE    CA30                                                             
         MVI   0(R4),C'0'                                                       
         LA    R4,1(R4)                                                         
         LA    R5,1(R5)                                                         
         LA    R6,1(R6)                                                         
         B     CA28                                                             
*                                                                               
CA30     CHI   R5,15                                                            
         BH    CAERROR                                                          
         BCTR  R5,0                GET VAR NUMERIC INTO 15                      
         EX    R5,*+8              AND CHECK FOR MINUS                          
         B     *+10                                                             
         PACK  SIXTEEN,WORK(0)                                                  
         OC    SIXTEEN(8),SIXTEEN  MUST FIT INTO PL8                            
         BNZ   CAERROR                                                          
         TM    MODE,MODEPL8                                                     
         BNZ   CA32                                                             
         CP    DUB,=P'2100000000'                                               
         BH    CAERROR                                                          
         CVB   RF,DUB                                                           
         TM    MODE,MODENEG                                                     
         BZ    *+6                                                              
         LCR   RF,RF                                                            
         B     CA34                                                             
*                                                                               
CA32     TM    MODE,MODENEG                                                     
         BZ    *+10                                                             
         MP    SIXTEEN,=P'-1'                                                   
*                                                                               
CA34     TM    MODE,MODEPCT                                                     
         BNZ   CA36                                                             
         ST    RF,4(RA)                                                         
         TM    MODE,MODEPL8                                                     
         BZ    *+10                                                             
         ZAP   4(8,RA),DUB                                                      
         CLI   PERCENT,0                                                        
         BE    CAEXIT                                                           
         LA    R4,WORK             AMOUNT STORED NOW SET UP                     
         SR    R5,R5               FOR EDITING A PERCENT EXPRESSION             
         SR    R6,R6                                                            
         NI    MODE,X'FF'-(MODENEG+MODEDEC)                                     
         OI    MODE,MODEPCT                                                     
         LA    R8,2                SET MAX POINTS TO 2 IN PERCENT MODE          
         B     CA12                                                             
*                                                                               
CA36     TM    MODE,MODEPL8                                                     
         BNZ   CA42                                                             
         SLA   RF,1                NOW WORK OUT PERCENT ADJUSTMENTS             
         M     RE,4(RA)            FROM THE TWO VALID EXPRESSIONS               
         D     RE,=F'10000'                                                     
         LTR   RF,RF                                                            
         BL    *+8                                                              
         AH    RF,=H'1'            ROUNDING                                     
         SRA   RF,1                                                             
         CLI   PERCENT,C'*'                                                     
         BE    *+12                                                             
         CLI   PERCENT,C'X'                                                     
         BNE   *+12                                                             
         ST    RF,4(RA)                                                         
         B     CAEXIT                                                           
         CLI   PERCENT,C'-'                                                     
         BNE   *+6                                                              
         LCR   RF,RF                                                            
         A     RF,4(RA)                                                         
         ST    RF,4(RA)                                                         
         B     CAEXIT                                                           
*                                                                               
CA42     MP    SIXTEEN,4(8,RA)                                                  
         SRP   SIXTEEN,64-4,5                                                   
         CLI   PERCENT,C'*'                                                     
         BE    *+12                                                             
         CLI   PERCENT,C'X'                                                     
         BNE   *+14                                                             
         ZAP   4(8,RA),SIXTEEN                                                  
         B     CAEXIT                                                           
         CLI   PERCENT,C'-'                                                     
         BNE   *+10                                                             
         MP    SIXTEEN,=P'-1'                                                   
         AP    4(8,RA),SIXTEEN                                                  
         B     CAEXIT                                                           
         EJECT                                                                  
CAERROR  MVI   0(RA),X'FF'         RETURN INVALID RESULT                        
*                                                                               
CAZERO   XC    4(4,RA),4(RA)       SET ZERO AMOUNT                              
         TM    MODE,MODEPL8                                                     
         BZ    *+10                                                             
         ZAP   4(8,RA),=P'0'                                                    
*                                                                               
CAEXIT   XIT1  ,                                                                
         EJECT                                                                  
         LTORG                                                                  
         SPACE 2                                                                
LANGTAB  DC    AL1(LANGENG),C',.',C'$ ',C'BONFREZERNIL'                         
         DC    AL1(LANGEUK),C',.',C'$ ',C'BONFREZERNIL'                         
         DC    AL1(LANGEUS),C',.',C'$ ',C'BONFREZERNIL'                         
         DC    AL1(LANGGER),C'.,',C'DM',C'BONKOSNULNUL'                         
         DC    AL1(LANGFRE),C'.,',C'FF',C'PRIGRAZERZER'                         
         DC    AL1(LANGSPA),C'.,',C'PT',C'BONGRACERNAD'                         
         DC    AL1(LANGITA),C'.,',C'LI',C'BONGRAZERNIE'                         
         DC    AL1(LANGDUT),C'.,',C'FL',C'GRANIENULNUL'                         
LANGTABX DC    AL1(LANGEOT),C'.,',C'? ',C'BONFREZERNIL'                         
         SPACE 2                                                                
LANGTABD DSECT                                                                  
LANGLANG DS    XL1                 LANGUAGE CODE                                
LANGEOT  EQU   255                 END OF TABLE INDICATOR                       
LANGMAG  DS    CL1                 MAGNITUDE CHARACTER                          
LANGDEC  DS    CL1                 DECIMAL POINT CHARACTER                      
LANGSYM  DS    CL2                 CURRENCY SYMBOL                              
LANGSPCN EQU   4                                                                
LANGSPC  DS    (LANGSPCN)CL3       SPECIAL WORDS FOR ZERO                       
LANGTABL EQU   *-LANGTABD                                                       
         EJECT                                                                  
CASHD    DSECT                     ** DSECT FOR PROGRAM **                      
SIXTEEN  DS    PL16                                                             
         ORG   *-8                                                              
DUB      DS    D                                                                
MAGPOS   DS    A                                                                
WORK     DS    CL32                                                             
*                                                                               
MODE     DS    XL1                                                              
MODEPL8  EQU   X'80'               OUTPUT IS PL8 NOT XL4                        
MODEPCT  EQU   X'40'               PERCENTAGE FOUND                             
MODEDEC  EQU   X'20'               DECIMAL POINT FOUND                          
MODENEG  EQU   X'10'               NEGATIVE NUMBER                              
MODENOC  EQU   X'08'               DON'T ALLOW CURRENCY SYMBOLS                 
MODENDP  EQU   X'04'               DON'T DEFAULT TO 2 DECIMAL PLACES            
MODEFRC  EQU   X'01'               FORCE US/UK EDIT (PERIOD NOT COMMA)          
*                                                                               
AGCOPT   DS    XL1                 AGENCY COUNTRY OPTIONS                       
AGCTRY   DS    XL1                 AGENCY COUNTRY                               
CTRY     DS    XL1                 ACTUAL COUNTRY                               
LANG     DS    XL1                 LANGUAGE                                     
*                                                                               
POINTS   DS    XL1                 NUMBER OF DECIMAL POINTS                     
PERCENT  DS    XL1                 PERCENTAGE INDICATOR                         
MAGSEP   DS    CL1                 MAGNITUDE CHARACTER                          
DECSEP   DS    CL1                 DECIMAL POINT CHARACTER                      
CURSYM   DS    CL2                 CURRENCY SYMBOL                              
SPC      DS    (LANGSPCN)CL(L'LANGSPC)                                          
SPCL     EQU   *-SPC                                                            
MAGCNT   DS    XL1                 MAGNITUDE COUNTER                            
MAGMAX   EQU   3                   MAXIMUM N'MAGNITUDE CHARACTERS               
DECPNT   EQU   255                                                              
CASHX    DS    0D                                                               
         SPACE 1                                                                
* DDLANGEQUS                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDLANGEQUS                                                     
         PRINT ON                                                               
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'007DDCASHVUS 06/27/01'                                      
         END                                                                    
