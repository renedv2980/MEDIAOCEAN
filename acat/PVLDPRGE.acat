*          DATA SET PVLDPRGE   AT LEVEL 005 AS OF 05/22/16                      
*$PANAPT01S$ *******  FIRST LINE TO DELETE  *****************                   
*                                                           *                   
*  ATTN: THE LEVEL STAMPS ARE *LEGITIMATELY* OUT-OF-SYNC!   *                   
*        DELETE THIS COMMENT BLOCK ON THE NEXT PROMOTION!   *                   
*                                                           *                   
*  THIS SOURCE MODULE IS AT A HIGHER LEVEL NUMBER THAN ITS  *                   
*  CORRESPONDING LOAD OR OBJECT MODULE, BECAUSE THE MEMBER  *                   
*  WAS PROMOTED USING THE SRCE LIBCODE VIA MR# 046660.      *                   
*                                                           *                   
*  THIS COMMENT BLOCK WAS INSERTED *PROGRAMMATICALLY* BY    *                   
*  PANAPT, TO EXPLAIN WHY THE LEVEL STAMPS ARE OUT-OF-SYNC. *                   
*  BEFORE THIS MEMBER CAN BE PROMOTED AGAIN, THIS ENTIRE    *                   
*  COMMENT BLOCK *MUST* BE MANUALLY DELETED.                *                   
*                                                           *                   
*$PANAPT01X$ *******  LAST LINE TO DELETE  ******************                   
*PROCESS USING(WARN(15))                                                        
*CATALP PVLDPRGE                                                                
         TITLE 'LDPURGE - DEMOS - DIRECTORY LOAD PURGE TEST'                    
*        PARAMS VIA R1                                                          
*        XL1   X'00'  RETURN  X'00'=NOPURGE  OR  X'FF'=PURGE                    
*        AL3   A(RECORD)                                                        
*        AL4   A(ISDTF#)           INTERNAL I/S FILE NUMBER                     
*                                                                               
* NOTE: SEE JIRA TICKET MODEMOS-182                                             
*  WE ARE NOW ALLOWED TO PURGE LOCAL DAILIES DATA OLDER THAN THREE              
*  YEARS, EXCEPT FOR SPECIFIC WEEKS OF SPORTS PROGRAMMING. WE ARE NOT           
*  BOTHERING TO CODE THAT RULE IN THIS PROGRAM, BECAUSE IT ISN'T                
*  WORTH IT. THE PROGRAM LINEUP DATA DOESN'T TAKE UP THAT MUCH                  
*  SPACE. WE JUST CONTINUE TO PURGE IT WHEN IT'S FOUR YEARS OLD, AS WE          
*  ORIGINALLY DID.                                                              
*  NOTE THAT WE DO NOW PURGE THE TIME PERIOD DATA AFTER THREE YEARS.            
*  THIS INCONSISTENCY DOESN'T MATTER, BECAUSE THE LEFTOVER LINEUP DATA          
*  CAN'T BE ACCESSED WITHOUT READING VIA THE TIME PERIOD DATA.                  
*                                                                               
         PRINT NOGEN                                                            
LDPURGE  CSECT                                                                  
         NMOD1 0,PVLDPRGE                                                       
*                                                                               
         L     RA,=V(CPRINT)                                                    
         USING DPRINT,RA                                                        
*                                                                               
         LR    R9,R1               SAVE A(PARMS)                                
*                                                                               
         BC    0,CHECKREC                                                       
         MVI   *-3,X'F0'           *** SELF-MODIFYING CODE ***                  
*                                                                               
         L     R1,4(R9)                                                         
         CLC   =AL1(PAVDIR#),0(R1) PROCESSING PAVDIR ?                          
         BNE   CHECKREC            NO: NO LOCAL DAILIES TO PURGE                
*                                                                               
* PURGE LOCAL DAILIES PROGRAM NAMES DATA OLDER THAN FOUR YEARS.                 
* CALCULATE THAT DATE AND SAVE IT.                                              
*                                                                               
         LHI   R0,-((365*4)+1)     4 YEARS AGO                                  
         GOTO1 =V(DATCON),DMCB,(X'35',0),DUB,(6,0),(R0)                         
*                                                                               
         XC    DMCB(24),DMCB                                                    
*                                  SET START DAY TO MONDAY                      
         GOTO1 =V(NSIWEEK),DMCB,DUB,(1,V(GETDAY)),V(ADDAY),V(DATCON)            
*                                                                               
         MVC   OLDWEEK,0(R1)       WEEK                                         
         MVC   OLDYEAR,4(R1)       YEAR                                         
*                                                                               
* CONVERT THAT YEAR/WEEK TO A PRINTABLE DATE. THIS WILL BE THE MONDAY           
* THAT STARTS THE WEEK.                                                         
*                                                                               
         GOTO1 =V(NSIWEEK),DMCB,(C'D',OLDEST_BOOK_TO_KEEP),            +        
               (1,V(GETDAY)),V(ADDAY),V(DATCON)                                 
         SR    R0,R0                                                            
         ICM   R0,B'0111',1(R1)    "FUNNY" YYMMDD DATE                          
         MVC   P(42),=C'ELIGIBLE TO PURGE: LOCAL DAILIES PRIOR TO '             
         GOTO1 =V(DATCON),DMCB,(R0),(21,P+42)                                   
         MVC   P+52(10),=C' (WEEK NN)'                                          
         EDIT  OLDWEEK,(2,P+59)                                                 
         GOTO1 =V(PRINTER)                                                      
         MVI   P,0                 SKIP A LINE                                  
         GOTO1 =V(PRINTER)                                                      
*                                                                               
CHECKREC DS    0H                                                               
         L     R3,0(R9)            R3 = A(RECORD)                               
         TM    18(R3),STATUS_DEL   MARKED FOR DELETION?                         
         BO    PURGE               YES                                          
*                                                                               
*************************************************************                   
* NIELSEN LOCAL DAILIES/OVERNIGHT                                               
*************************************************************                   
*                                                                               
         CLC   =C'PON',0(R3)       PROGRAM LINEUP                               
         BNE   KEEP                                                             
         USING PRKEY,R3                                                         
         CLI   PRPNMFLG,C'P'-X'40' LOWER-CASE 'P'                               
         BNE   KEEP                                                             
         CLC   PRBOOK,OLDEST_BOOK_TO_KEEP                                       
         BL    PURGE                                                            
         B     KEEP                                                             
         DROP  R3                                                               
*                                                                               
*************************************************************                   
PURGE    DS    0H                                                               
         MVI   0(R9),X'FF'         RETURN PURGE FLAG                            
         OI    18(R3),STATUS_DEL   SET DELETE BIT                               
*                                                                               
KEEP     DS    0H                                                               
         SPACE 2                                                                
         XMOD1                                                                  
         SPACE 2                                                                
         LTORG                                                                  
         SPACE 2                                                                
NTIDIR#  EQU   X'38'                                                            
PAVDIR#  EQU   X'2C'                                                            
*                                                                               
STATUS_DEL EQU X'80'                                                            
         SPACE 2                                                                
DUB      DS    D                                                                
DMCB     DS    6F                                                               
WORK     DS    CL17                                                             
OLDEST_BOOK_TO_KEEP DS 0XL2                                                     
OLDYEAR  DS    X                                                                
OLDWEEK  DS    X                                                                
         EJECT                                                                  
* ++INCLUDE DEDEMFILE                                                           
* ++INCLUDE DDDPRINT                                                            
         PRINT OFF                                                              
       ++INCLUDE DEDEMFILE                                                      
         EJECT                                                                  
       ++INCLUDE DDDPRINT                                                       
         PRINT ON                                                               
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'005PVLDPRGE  05/22/16'                                      
         END                                                                    
