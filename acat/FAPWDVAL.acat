*          DATA SET FAPWDVAL   AT LEVEL 002 AS OF 11/06/14                      
*CATALP PWDVAL                                                                  
***********************************************************************         
* XL1    L'PASSWORD    FOR ACTIONS 2 AND 3                            *         
* AL3    A(PASSWORD)   MODIFIED IF CONVERTED TO UPPER CASE            *         
*                                                                     *         
* XL1    RULE NUMBER   PASS RULE NUMBER                               *         
* AL3    A(TEXT AREA)  IF NONEZERO PASS CTRY=N TO PASS BINARY CTRY NUM*         
*                      IF NONEZERO RETURN TEXT OR UPPERCASE PASSWORD  *         
*                                                                     *         
* XL1    ACTION/RESULT 1=VALIDATE RULE NUMBER                         *         
*                      2=PROCESS PASSWORD - CONVERT TO UPPER IF RULE  *         
*                      3=VALIDATE NEW PASSWORD                        *         
*                      RETURN NONZERO IF INVALID                      *         
* AL3    A(SSB)                                                       *         
***********************************************************************         
         TITLE 'FAPWDVAL - PROCESSES AND VALIDATES PASSWORDS'                   
         PRINT NOGEN                                                            
PWDVAL   CSECT                                                                  
         NMOD1 WORKX-WORKD,XXPWVLXX                                             
         USING WORKD,RC                                                         
         ST    R1,SAVER1                                                        
         LR    R2,R1               R2=A(PARAM LIST)                             
         LM    R3,R5,0(R2)                                                      
         NILH  GR3,X'00FF'         R3=A(INPUT PASSWORD)                         
         NILH  GR4,X'00FF'         R4=A(RETURN TEXT AREA) OR ZERO               
         NILH  GR5,X'00FF'         R5=A(SSB)                                    
*                                                                               
         MVC   ACTION,8(R2)        SAVE ACTION IN P3(1)                         
         MVI   8(R2),0             CLEAR RETURN CODE                            
         CLI   ACTION,2                                                         
         BE    PWDV2                                                            
         BH    PWDV3                                                            
         EJECT                                                                  
***********************************************************************         
* ACTION#1 - VALIDATE PASSWORD RULE NUMBER AND PASS BACK RULE TEXT    *         
***********************************************************************         
PWDV1    CLI   ACTION,1            VALIDATE RULE NUMBER                         
         BNE   PWDVE1                                                           
         LLC   R6,4(R2)            R6=RULE NUMBER                               
         CHI   R6,PWDMAX                                                        
         BH    PWDVE2                                                           
         MHI   R6,L'PWDRULE                                                     
         LA    R6,PWDRULE(R6)      R6=A(PASSWORD RULE TABLE ENTRY)              
         USING PWDRD,R6                                                         
         LTR   R4,R4               EXIT IF NO TEXT AREA PASSED                  
         BZ    PWDVX                                                            
         MVC   0(30,R4),PWDRTEXT   RETURN RULE TEXT                             
         B     PWDVX                                                            
         EJECT                                                                  
***********************************************************************         
* ACTION#2 - PROCESS PASSWORD - CONVERT TO UPPER CASE IF RULE REQUIRES*         
* PASS BACK UPPER CASE VERSION IF TEXT AREA PASSED IN PARAM2          *         
***********************************************************************         
PWDV2    LLC   R1,0(R2)            R1=L'PASSWORD                                
         CHI   R1,1                ERROR IF LENGTH NOT SET                      
         BL    PWDVE3                                                           
         CHI   R1,10                                                            
         BNH   *+8                                                              
         LHI   R1,10               IF TOO LONG SET TO MAXIMUM                   
         BCTR  R1,0                                                             
         STH   R1,TXTLEX                                                        
         MVC   TXTINP,SPACES       EXTRACT INPUT PASSWORD                       
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   TXTINP(0),0(R3)                                                  
         MVC   TXTUPR,TXTINP                                                    
         BAS   R9,PWDUPPER         CONVERT INPUT TO UPPER CASE                  
         LTR   R4,R4                                                            
         BZ    PWDV2A                                                           
         MVC   0(10,R4),TXTUPR     RETURN UPPER CASE VALUE IF TEXT AREA         
*                                                                               
PWDV2A   LLC   R6,4(R2)            R6=RULE NUMBER                               
         CHI   R6,PWDMAX                                                        
         BH    PWDVE2                                                           
         MHI   R6,L'PWDRULE        R6=A(PASSWORD RULE TABLE ENTRY)              
         LA    R6,PWDRULE(R6)                                                   
         USING PWDRD,R6                                                         
*                                                                               
PWDV2B   TM    PWDRFLAG,X'01'      TEST IF PASSWORD IS CASE SENSITIVE           
         BO    PWDV2C                                                           
         LH    R1,TXTLEX           NO-OVERWRITE PASSWORD WITH UPPER             
         EX    R1,*+8                                                           
         B     PWDV2C                                                           
         MVC   0(0,R3),TXTUPR                                                   
*                                                                               
PWDV2C   B     PWDVX               EXIT-DONT CHECK LENGTH OF PASSWORD           
*NOP*    CLI   0(R2),10                                                         
*NOP*    BH    PWDVE3              ERROR IF PASSWORD LONGER THAN MAX            
*NOP*    B     PWDVX                                                            
        EJECT                                                                   
***********************************************************************         
* ACTION#3 - VALIDATE PASSWORD - CONVERT TO UPPER CASE IF RULE STATES *         
* PASS BACK UPPER CASE VERSION IF TEXT AREA PASSED IN PARAM2          *         
***********************************************************************         
PWDV3    CLI   ACTION,3            VALIDATE PASSWORD                            
         BNE   PWDVE1                                                           
         LLC   R1,0(R2)                                                         
         CHI   R1,1                CHECK PASSWORD LENGTH                        
         BL    PWDVE3                                                           
         CHI   R1,10                                                            
         BH    PWDVE3                                                           
         BCTR  R1,0                                                             
         STH   R1,TXTLEX                                                        
         MVC   TXTINP,SPACES       EXTRACT INPUT PASSWORD                       
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   TXTINP(0),0(R3)                                                  
         MVC   TXTUPR,TXTINP                                                    
         BAS   R9,PWDUPPER         CONVERT INPUT TO UPPER CASE                  
         LTR   R4,R4                                                            
         BZ    *+10                                                             
         MVC   0(10,R4),TXTUPR     RETURN UPPER CASE VALUE IF TEXT AREA         
*                                                                               
         LLC   R6,4(R2)            CHECK RULE NUMBER                            
         CHI   R6,PWDMAX                                                        
         BH    PWDVE2                                                           
         MHI   R6,L'PWDRULE                                                     
         LA    R6,PWDRULE(R6)      R6=A(PASSWORD RULE TABLE ENTRY)              
         USING PWDRD,R6                                                         
*                                                                               
         TM    PWDRFLAG,X'01'      TEST IF PASSWORD CASE SENSITIVE              
         BO    *+10                                                             
         MVC   TXTINP,TXTUPR       NO-CONVERT TO UPPER CASE                     
         LLC   R0,0(R2)            R0=L'PASSWORD                                
         LA    R1,TXTINP           R1=A(NEXT PASSWORD CHARACTER)                
         XC    PWDDATA,PWDDATA     CLEAR VALIDATION FLAGS AND COUNTERS          
*                                                                               
PWDV3A   LLC   RE,0(R1)            INDEX INTO PASSWORD CHARACTER TABLE          
         LA    RE,VALCHRS(RE)                                                   
         CLI   0(RE),X'FF'         TEST INVALID CHARACTER                       
         BE    PWDVE4                                                           
*                                                                               
PWDV3B   LLC   RF,0(RE)            GET A(ROUTINE) TO PROCESS CHARACTER          
         SLL   RF,2                                                             
         B     *(RF)                                                            
         B     PWDV3B1             01=LOWER                                     
         B     PWDV3B2             02=UPPER                                     
         B     PWDV3B3             03=NUMBER                                    
         B     PWDV3B4             04=SPECIAL CHR                               
         B     PWDV3B5             05=NATIONAL CHR                              
         B     PWDV3B6             06=SPACE CHR                                 
*                                                                               
PWDV3B1  OI    FLAG,B'00000001'    SET LOWER CASE CHR PRESENT                   
         LLC   RF,LOWER                                                         
         AHI   RF,1                                                             
         STC   RF,LOWER                                                         
         B     PWDV3C                                                           
*                                                                               
PWDV3B2  OI    FLAG,B'00000010'    SET UPPER CASE CHR PRESENT                   
         LLC   RF,UPPER                                                         
         AHI   RF,1                                                             
         STC   RF,UPPER                                                         
         B     PWDV3C                                                           
*                                                                               
PWDV3B3  OI    FLAG,B'00000100'    SET INTEGER PRESENT                          
         LLC   RF,INTEGER                                                       
         AHI   RF,1                                                             
         STC   RF,INTEGER                                                       
         B     PWDV3C                                                           
*                                                                               
PWDV3B4  OI    FLAG,B'00001000'    SET SPECIAL CHARACTER PRESENT                
         LLC   RF,SPECIAL                                                       
         AHI   RF,1                                                             
         STC   RF,SPECIAL                                                       
         B     PWDV3C                                                           
*                                                                               
PWDV3B5  OI    FLAG,B'00010000'    SET NATIONAL CHARACTER PRESENT               
         B     PWDV3C                                                           
*                                                                               
PWDV3B6  OI    FLAG,B'01000000'    SET SPACE CHARACTER PRESENT                  
*                                                                               
PWDV3C   LA    R1,1(R1)            BUMP TO NEXT PASSWORD CHR                    
         BCT   R0,PWDV3A                                                        
*                                                                               
PWDV3D   CLI   PWDRNUM,0           TEST FREE FORM PASSWORDS                     
         BNE   PWDV3E                                                           
         CLC   TXTUPR,=CL10'DDS'   DDS HAS NEVER BEEN A VALID PASSWORD          
         BE    PWDVE7                                                           
         B     PWDV3X                                                           
*                                                                               
PWDV3E   TM    FLAG,B'01000000'    SPACE CHARACTER PRESENT                      
         BO    PWDVE6                                                           
         TM    FLAG,B'00010000'    NATIONAL CHARACTER PRESENT                   
         BO    PWDVE5                                                           
*                                                                               
PWDV3F   CLC   TXTUPR,=CL10'DDS'   TEST BANNED PASSWORDS                        
         BE    PWDVE7                                                           
         CLC   TXTUPR,=CL10'PASSWORD'                                           
         BE    PWDVE7                                                           
*                                                                               
PWDV3G   CLI   PWDRLOWR,0          LOWER CASE CHRS REQUIRED                     
         BE    *+14                                                             
         CLC   LOWER,PWDRLOWR                                                   
         BL    PWDVE8                                                           
         CLI   PWDRUPPR,0          UPPER CASE CHRS REQUIRED                     
         BE    *+14                                                             
         CLC   UPPER,PWDRUPPR                                                   
         BL    PWDVE9                                                           
         CLI   PWDRINT,0           INTEGERS REQUIRED                            
         BE    *+14                                                             
         CLC   INTEGER,PWDRINT                                                  
         BL    PWDVE10                                                          
         CLI   PWDRSPCL,0          SPECIAL CHARACTERS REQUIRED                  
         BE    *+14                                                             
         CLC   SPECIAL,PWDRSPCL                                                 
         BL    PWDVE11                                                          
*                                                                               
PWDV3X   TM    PWDRFLAG,X'01'      EXIT IF PASSWORD IS CASE SENSITIVE           
         BO    PWDVX                                                            
         LH    R1,TXTLEX           OVERWRITE PASSWORD WITH UPPER VALUE          
         EX    R1,*+8                                                           
         B     PWDVX                                                            
         MVC   0(0,R3),TXTUPR                                                   
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO TRANSLATE PASSWORD TO UPPER CASE                         *         
***********************************************************************         
PWDUPPER LTR   R5,R5               A(SSB) MUST BE PASSED                        
         BZ    PWDVE1                                                           
         ICM   RF,15,SSBXLAT-SSBD(R5)                                           
         BZ    PWDVE1                                                           
         ICM   RE,15,SSBTKADR-SSBD(R5)                                          
         BZ    PWDVE1                                                           
         ICM   RE,15,TCBUTL-TCBD(RE)                                            
         BZ    PWDVE1                                                           
         LLC   R7,TCTRY-UTLD(RE)   EXTRACT COUNTRY CODE FROM UTL                
*                                                                               
PWDUPP1  LTR   R4,R4               TEST IF CTRY=N PASSED IN TEXT AREA           
         BZ    PWDUPP2                                                          
         CLC   0(5,R4),=C'CTRY='                                                
         BNE   PWDUPP2                                                          
         IC    R7,5(R4)                                                         
*                                                                               
PWDUPP2  CHI   R7,15               COUNTRY MUST BE VALID                        
         BNH   *+6                                                              
         SR    R7,R7                                                            
         STC   R7,CTRY             SAVE COUNTRY CODE                            
         SLL   R7,4                                                             
         L     RF,8(R7,RF)         RF=A(TRANSLATE TO UPPER CASE TABLE)          
         LH    R1,TXTLEX           R1=L'PASSWORD-1                              
         EX    R1,*+6                                                           
PWDUPPX  BR    R9                                                               
         TR    TXTUPR,0(RF)        CONVERT PASSWORD TO UPPER CASE               
         EJECT                                                                  
***********************************************************************         
* ERROR EXITS - NUMBERS CORRESPOND TO SRV/CTL SYSTEM ERROR MESSAGES   *         
***********************************************************************         
PWDVE1   MVI   8(R2),X'E1'         INVALID PARAMETER LIST                       
         B     PWDVX                                                            
PWDVE2   MVI   8(R2),X'E2'         INVALID PASSWORD RULE NUMBER                 
         B     PWDVX                                                            
PWDVE3   MVI   8(R2),X'E3'         INVALID PASSWORD LENGTH                      
         B     PWDVX                                                            
PWDVE4   MVI   8(R2),X'E4'         INVALID CHARACTER IN PASSWORD                
         B     PWDVX                                                            
PWDVE5   MVI   8(R2),X'E5'         NATIONAL CHARACTER IN PASSWORD               
         B     PWDVX                                                            
PWDVE6   MVI   8(R2),X'E6'         SPACE CHARACTER IN PASSWORD                  
         B     PWDVX                                                            
PWDVE7   MVI   8(R2),X'E7'         INVALID PASSWORD STRING                      
         B     PWDVX                                                            
PWDVE8   MVI   8(R2),X'E8'         MISSING LOWER CASE CHR                       
         B     PWDVX                                                            
PWDVE9   MVI   8(R2),X'E9'         MISSING UPPER CASE CHR                       
         B     PWDVX                                                            
PWDVE10  MVI   8(R2),X'EA'         MISSING INTEGER                              
         B     PWDVX                                                            
PWDVE11  MVI   8(R2),X'EB'         MISSING SPECIAL CHR                          
         B     PWDVX                                                            
*                                                                               
PWDVX    XMOD1 1                                                                
         EJECT                                                                  
***********************************************************************         
* VALID PASSWORD CHARACTERS                                           *         
*                                                                     *         
* 01 LOWER CASE a-z                                                   *         
* 02 UPPER CASE A-Z                                                   *         
* 03 INTEGERS   0-9                                                   *         
* 04 SPECIAL    4B=. 4D=( 4E=+ 50=& 5C=* 5D=) 60=- 61=/ 6C=% 6D=_ 6F=?*         
* 05 NATIONAL   4A= 4F=| 5A=! 5B=$ 5F=^ 6A=º 79=` 7B=# 7C=@ 7F=" 9F=þ*         
*               A1=~ C0={ D0=} E0=\                                   *         
* 06 SPACE      40=                                                   *         
***********************************************************************         
*                   0.1.2.3.4.5.6.7.8.9.A.B.C.D.E.F.                            
VALCHRS  DC    XL16'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF' 00-0F                     
         DC    XL16'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF' 10-1F                     
         DC    XL16'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF' 20-2F                     
         DC    XL16'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF' 30-3F                     
         DC    XL16'06FFFFFFFFFFFFFFFFFF0504FF040405' 40-4F .(+   |            
         DC    XL16'04FFFFFFFFFFFFFFFFFF05050404FF05' 50-5F &*)   !$^           
         DC    XL16'0404FFFFFFFFFFFFFFFF05FF0404FF04' 60-6F -/%_? º             
         DC    XL16'FFFFFFFFFFFFFFFFFF05FF0505FFFF05' 70-7F       `#@"          
         DC    XL16'FF010101010101010101FFFFFFFFFFFF' 80-8F                     
         DC    XL16'FF010101010101010101FFFFFFFFFF05' 90-9F       þ             
         DC    XL16'FF050101010101010101FFFFFFFFFFFF' A0-AF       ~             
         DC    XL16'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF' B0-BF                     
         DC    XL16'05020202020202020202FFFFFFFFFFFF' C0-CF       {             
         DC    XL16'05020202020202020202FFFFFFFFFFFF' D0-DF       }             
         DC    XL16'05FF0202020202020202FFFFFFFFFFFF' E0-EF       \             
         DC    XL16'03030303030303030303FFFFFFFFFFFF' F0-FF                     
                                                                                
SPACES   DC    CL10' '                                                          
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* PASSWORD RULES TABLE                                                *         
***********************************************************************         
PWDRULE  DC    0CL40                                                            
         DC    AL1(00),B'00000000',AL1(0,0,0,0),XL4'00'                         
         DC    CL30'Upper case,no validation      '                             
*                                                                               
         DC    AL1(01),B'00000000',AL1(0,0,0,0),XL4'00'                         
         DC    CL30'Upper case,no national chrs   '                             
*                                                                               
         DC    AL1(02),B'00000010',AL1(0,0,1,0),XL4'00'                         
         DC    CL30'Upper,1=A-Z                   '                             
*                                                                               
         DC    AL1(03),B'00000011',AL1(0,0,1,1),XL4'00'                         
         DC    CL30'Mixed,1=a-z,1=A-Z             '                             
*                                                                               
         DC    AL1(04),B'00000110',AL1(0,1,1,0),XL4'00'                         
         DC    CL30'Upper,1=A-Z,1=0-9             '                             
*                                                                               
         DC    AL1(05),B'00000111',AL1(0,1,1,1),XL4'00'                         
         DC    CL30'Mixed,1=a-z,1=A-Z,1=0-9       '                             
*                                                                               
         DC    AL1(06),B'00001110',AL1(1,1,1,0),XL4'00'                         
         DC    CL30'Upper,1=A-Z,1=0-9,1=special   '                             
*                                                                               
         DC    AL1(07),B'00001111',AL1(1,1,1,1),XL4'00'                         
         DC    CL30'Mixed,1=a-z,1=A-Z,1=0-9,1=spcl'                             
*                                                                               
         DC    AL1(08),B'00000110',AL1(0,2,1,0),XL4'00'                         
         DC    CL30'Upper,1=A-Z,2=0-9             '                             
*                                                                               
         DC    AL1(09),B'00000111',AL1(0,2,1,1),XL4'00'                         
         DC    CL30'Mixed,1=a-z,1=A-Z,2=0-9       '                             
*                                                                               
PWDRULEX DC    X'FF'                                                            
PWDMAX   EQU   ((PWDRULEX-PWDRULE)/L'PWDRULE)-1                                 
         EJECT                                                                  
***********************************************************************         
* DSECTS FOR PASSWORD RULES TABLE, WORKING STORAGE, AND INCLUDED      *         
***********************************************************************         
PWDRD    DSECT                     RULE TABLE DSECT                             
PWDRNUM  DS    XL1                 RULE NUMBER                                  
PWDRFLAG DS    BL1                 RULE FLAG X'01'=MIXED CASE                   
PWDRSPCL DS    AL1                 MINIMUM NUMBER OF SPECIAL CHRS               
PWDRINT  DS    AL1                 MINIMUM NUMBER OF INTEGERS                   
PWDRUPPR DS    AL1                 MINIMUM NUMBER OF UPPER CASE A-Z             
PWDRLOWR DS    AL1                 MINIMUM NUMBER OF LOWER CASE A-Z             
         DS    XL4                 N/D                                          
PWDRTEXT DS    CL30                RULE TEXT                                    
                                                                                
WORKD    DSECT                                                                  
SAVER1   DS    F                                                                
HALF     DS    H                                                                
ACTION   DS    X                                                                
CTRY     DS    X                                                                
TXTLEX   DS    H                                                                
TXTINP   DS    CL10                                                             
TXTUPR   DS    CL10                                                             
*                                                                               
PWDDATA  DS    0XL5                                                             
FLAG     DS    XL1                                                              
SPECIAL  DS    XL1                                                              
INTEGER  DS    XL1                                                              
UPPER    DS    XL1                                                              
LOWER    DS    XL1                                                              
*                                                                               
WORKX    EQU   *                                                                
                                                                                
*FASSB                                                                          
       ++INCLUDE FASSB                                                          
*FATCB                                                                          
       ++INCLUDE FATCB                                                          
*FAUTL                                                                          
       ++INCLUDE FAUTL                                                          
                                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'002FAPWDVAL  11/06/14'                                      
         END                                                                    
