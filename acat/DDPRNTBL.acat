*          DATA SET DDPRNTBL   AT LEVEL 008 AS OF 03/18/20                      
*PROCESS USING(WARN(15))                                                        
*CATALP PRNTBL                                                                  
         TITLE 'PRNTBL - MODULE TO PRINT/DUMP STORAGE'                          
**********************************************************************          
*        P1    XL1 L'TABLE-NAME , AL3 A(TABLE-NAME)                             
*        P2    A(TABLE)                                (R2)                     
*        P3    NUM OF ENTRIES IN TABLE / C'DUMP'       (R3)                     
*        P4    ENTRY LENGTH            / L'TABLE       (R4)                     
*              KEY LENGTH OR ZERO WHEN P5+1 = C'R'                              
*        P5    A(PRINT OPTIONS)                        (R5)                     
*              CL1  0=SKIP TO NEW PAGE                                          
*                   1=SKIP 1 LINE                                               
*                   2=SKIP 2 LINES                                              
*                   3=SKIP 3 LINES                                              
*              CL1  C=CHR PRINT                                                 
*                   H=HEX PRINT                                                 
*                   D=DUMP PRINT                                                
*                   R=REC/ELEMENT DUMP LIKE PFM  / C'DUMP' ONLY                 
*        P6    BYTE 0 = C'P' THEN BYTES 1-3 = A(PRINT) (R6)                     
**********************************************************************          
         EJECT                                                                  
         PRINT NOGEN                                                            
PRNTBL   CSECT                                                                  
*                                                                               
         WXTRN PRINT               V(PRINT) NEED NOT BE RESOLVED...             
*                                  ...IF A(PRINT) IS PROVIDED VIA P6            
*                                                                               
         NMOD1 WRKX-WRKD,**PTBL**,RR=R6                                         
         USING WRKD,RC                                                          
         USING PLISTD,R9           R9=A(PARAM LIST)                             
         LR    R9,R1                                                            
         L     RF,=V(PRINT)                                                     
         LTR   RF,RF               PRINT MUST BE INCLUDED                       
         BZ    PTBL00                                                           
         AR    R6,RF                                                            
         B     PTBL10                                                           
                                                                                
PTBL00   CLI   APRINT,C'P'         OR ADDRESS MUST BE PASSED                    
         BE    *+6                                                              
         DC    H'0'                                                             
                                                                                
         L     R6,APRINT                                                        
PTBL10   ST    R6,VPRINT           R6=A(PRINT MODULE)                           
         LA    RF,PRNIOA           BUILD PLIST TO V(PRINT)                      
         ST    RF,P1                                                            
         LA    RF,PRNTYP                                                        
         ST    RF,P2                                                            
                                                                                
         L     R2,AOPTN            SKIP PRINTER AS SPECIFIED BY P5              
         MVC   PSKIP(2),0(R2)                                                   
         OI    PSKIP,X'F0'         MAKE SURE IT IS A CHARACTER #                
         CLI   PSKIP,C'4'          CAN'T BE HIGHER THEN 3                       
         BL    *+8                                                              
         MVI   PSKIP,C'1'          DEFAULT                                      
*                                  'L' FOR LINE CONTROL                         
         MVC   PRNTYP,=C'BL01'     1 THRU 3 MEANS LINE SPACING                  
         MVC   PRNTYP+3(1),PSKIP   # OF LINES TO SKIP 1 TO 3                    
         CLI   PSKIP,C'0'                                                       
         BNE   *+10                                                             
         MVC   PRNTYP,=C'BC01'    'C' FOR CARRAGE CONTROL                       
         MVC   PRNIOD,SPACES                                                    
         GOTO1 (R6),P1                                                          
                                                                                
PTBL20   SR    R3,R3                                                            
         IC    R3,ATBLNAME         PRINT TABLE NAME SPECIFIED BY P1             
         SHI   R3,1                                                             
         BM    PTBL22              LENGTH ZERO - NO NAME REQUIRED               
         CHI   R3,131                                                           
         BH    PTBL22                                                           
         L     R2,ATBLNAME         GET ADDR OF TABLE NAME                       
         LA    R2,0(,R2)                                                        
         LTR   R2,R2                                                            
         BZ    PTBL22                                                           
         EXMVC R3,PRNIOD,0(R2)                                                  
         MVC   PRNTYP,=C'BL02'                                                  
         GOTO1 (R6),P1                                                          
         MVC   PRNIOD,SPACES                                                    
                                                                                
PTBL22   LM    R2,R5,ATBL          SET NUM OF LINES FOR DUMP OPTION             
         CLC   NENTRY,=C'DUMP'                                                  
         BNE   PTBL30                                                           
         CLI   PTYPE,C'R'                                                       
         BNE   PTBL28                                                           
                                                                                
PTBL24   LTR   RE,R4               R4 > 0 THEN KEY, R4 = 0 THEN ELEMENT         
         BNZ   PTBL27              PROCESS KEY PORTION                          
         CLI   0(R2),0             EOR                                          
         BE    PTBLXIT             FINISHED                                     
         IC    R4,1(,R2)           GET ELEMENT LENGTH INSTEAD                   
         LTR   RE,R4                                                            
         BZ    PTBLXIT             NO LENGTH, SO EXIT                           
                                                                                
PTBL27   MVI   CHAR,C'-'                                                        
         AR    R4,R2                                                            
         ST    R4,ANEXTEL                                                       
         B     PTBL29              DUMP ELEMENT                                 
                                                                                
PTBL28   MVI   PTYPE,C'D'                                                       
         LTR   RE,R4                                                            
         BNP   PTBLXIT             EXIT ON INV DUMP DATA LEN                    
                                                                                
PTBL29   SRDL  RE,5                DIVIDE BY 32                                 
         SRL   RF,27                                                            
         LTR   RF,RF                                                            
         BZ    *+8                                                              
         AHI   RE,1                RE=NUM OF LINES OF 32 CHRS TO DUMP           
         ST    RF,NUMCHRS          RF=NUM OF CHRS ON LAST LINE                  
         LR    R3,RE               SET NUM OF ENTRIES                           
         LA    R4,32               SET ENTRY LENGTH                             
         B     PTBL40                                                           
                                                                                
PTBL30   LTR   R3,R3                                                            
         BNP   PTBLXIT             EXIT ON INV NUM OF ENTRIES                   
         LTR   R4,R4                                                            
         BNP   PTBLXIT             EXIT ON INV ENTRY LEN                        
         CLI   PTYPE,C'C'                                                       
         BNE   PTBL32                                                           
         CHI   R4,132                                                           
         BH    PTBLXIT             EXIT ON INV ALPHA ENTRY LEN                  
         B     PTBL40                                                           
                                                                                
PTBL32   CLI   PTYPE,C'H'                                                       
         BNE   PTBL34                                                           
         CHI   R4,66                                                            
         BH    PTBLXIT             EXIT ON INV HEX ENTRY LEN                    
         B     PTBL40                                                           
                                                                                
PTBL34   CLI   PTYPE,C'D'                                                       
         BNE   PTBLXIT             EXIT ON INV OPTION                           
         CLC   NENTRY,=C'DUMP'                                                  
         BNE   PTBLXIT             EXIT ON INV OPTION                           
                                                                                
PTBL40   CLI   PTYPE,C'D'          SET HEX DISPLACEMENT FOR DUMP OPTION         
         BNE   PTBL42                                                           
         LR    RE,R2               R2 = LOCATION IN TABLE                       
         S     RE,ATBL                                                          
         STH   RE,HALF                                                          
         LA    R7,HALF             SOURCE                                       
         LA    R8,PRNIOD+2         DESTINATION                                  
         LHI   R0,2                LENGTH                                       
         BAS   RE,HEXLATE                                                       
         LA    R8,HEXLINE                                                       
                                                                                
PTBL42   CLI   PTYPE,C'R'          SET R8=A(HEX OUTPUT AREA) FOR H/D            
         BNE   PTBL44                                                           
         LR    RE,R2                                                            
         S     RE,ATBL                                                          
         STH   RE,HALF                                                          
         LA    R7,HALF                                                          
         LA    R8,PRNIOD+2                                                      
         LHI   R0,2                                                             
         BAS   RE,HEXLATE                                                       
         MVC   PRNIOD+6(1),CHAR    MOVE IN DASH IF START                        
         MVI   CHAR,C' '           CLEAR UNTIL NEXT START OF ELEMENT            
         LH    R7,HALF                                                          
         ST    R4,FULL                                                          
         CHI   R3,1                IS THIS THE LAST LINE ?                      
         BH    PTBL43                                                           
         OC    NUMCHRS,NUMCHRS     CASE- # OF CHARS IS EXACTLY ONE LINE         
         BZ    PTBL43                                                           
         MVC   FULL,NUMCHRS                                                     
                                                                                
PTBL43   A     R7,FULL             WHAT IS REMAINING OF LINE                    
         SHI   R7,1                                                             
         BP    *+6                                                              
         DC    H'00'                                                            
*                                                                               
         LHI   R0,2                                                             
         STH   R7,HALF                                                          
         LA    R7,HALF                                                          
         LA    R8,PRNIOD+7                                                      
         BAS   RE,HEXLATE                                                       
         LA    R8,HEXLINE                                                       
         B     PRTB47                                                           
                                                                                
PTBL44   CLI   PTYPE,C'H'          SET R8=A(HEX OUTPUT AREA) FOR H/D            
         BNE   PTBL46                                                           
         LA    R8,PRNIOD                                                        
         B     PRTB47                                                           
                                                                                
PTBL46   CLI   PTYPE,C'D'                                                       
         BNE   PTBL48                                                           
         LA    R8,HEXLINE                                                       
                                                                                
PRTB47   LR    R7,R2               DO HEX OUTPUT                                
         LR    R0,R4                                                            
         BAS   RE,HEXLATE                                                       
                                                                                
PTBL48   CLI   PTYPE,C'C'          SET R8=A(CHR OUTPUT AREA) FOR H/D            
         BNE   PTBL50                                                           
         LA    R8,PRNIOD                                                        
         B     PTBL55                                                           
                                                                                
PTBL50   CLI   PTYPE,C'D'                                                       
         BE    *+8                                                              
         CLI   PTYPE,C'R'                                                       
         BNE   PTBL80                                                           
         LA    R8,CHRLINE                                                       
                                                                                
PTBL55   LR    RF,R4                                                            
         BCTR  RF,R0                                                            
         EXMVC RF,0(R8),0(R2)      DO CHR OUTPUT                                
                                                                                
         CLI   PTYPE,C'D'                                                       
         BE    *+8                                                              
         CLI   PTYPE,C'R'                                                       
         BNE   PTBL80                                                           
         EX    RF,*+8              TRANSLATE CHRS FOR DUMP OPTION               
         B     *+10                                                             
         TR    0(0,R8),TRNTBL                                                   
                                                                                
         CHI   R3,1                                                             
         BNE   PTBL65                                                           
         L     R7,NUMCHRS                                                       
         LTR   R7,R7                                                            
         BZ    PTBL65                                                           
         LA    R8,CHRLINE(R7)      FIX LAST LINE CHRS                           
         MVI   0(R8),C' '                                                       
         LA    RF,30                                                            
         SR    RF,R7                                                            
         BM    PTBL60                                                           
         EXMVC RF,1(R8),0(R8)                                                   
                                                                                
PTBL60   SLL   R7,1                                                             
         LA    R8,HEXLINE(R7)      FIX LAST LINE HEX                            
         LA    RF,62                                                            
         SR    RF,R7                                                            
         MVI   0(R8),C' '                                                       
         EXMVC RF,1(R8),0(R8)                                                   
                                                                                
PTBL65   CLI   PTYPE,C'R'                                                       
         BNE   PTBL69                                                           
         MVC   PRNIOD+14(64),HEXLINE                                            
         MVC   PRNIOD+81(32),CHRLINE                                            
         MVC   PRNTYP,=C'BL01'     PRINT LINE                                   
         GOTO1 (R6),P1                                                          
         AR    R2,R4               BUMP TBL ADR                                 
         BCT   R3,PTBL42                                                        
                                                                                
         SR    R4,R4                                                            
         L     R2,ANEXTEL                                                       
         B     PTBL24              PROCESS ELEMENT NOW                          
*                                                                               
PTBL69   LHI   R0,31               REFORMAT IF ALL CHRS EQUAL IN LINE           
         LA    R7,HEXLINE                                                       
         CLC   0(2,R7),2(R7)                                                    
         BNE   PTBL70                                                           
         LA    R7,2(,R7)                                                        
         BCT   R0,*-14                                                          
                                                                                
         MVC   HEXLINE+8(8),=C'--SAME--'                                        
         MVI   HEXLINE+16,C' '                                                  
         MVC   HEXLINE+17(47),HEXLINE+16                                        
         MVI   CHRLINE+4,C' '                                                   
         MVC   CHRLINE+5(27),CHRLINE+4                                          
         CLC   HEXLINE(8),PRNIOD+8                                              
         BNE   PTBL70                                                           
         CLC   HEXLINE+8(8),PRNIOD+17                                           
         BE    PTBL90                                                           
                                                                                
PTBL70   LHI   R0,8                FORMAT DUMP PRINT LINE                       
         LA    R7,HEXLINE                                                       
         LA    R8,PRNIOD+8                                                      
                                                                                
PTBL72   MVC   0(8,R8),0(R7)                                                    
         LA    R8,9(,R8)                                                        
         LA    R7,8(,R7)                                                        
         CHI   R0,5                                                             
         BNE   *+8                                                              
         LA    R8,2(,R8)                                                        
         BCT   R0,PTBL72                                                        
                                                                                
         MVC   PRNIOD+086(16),CHRLINE                                           
         MVC   PRNIOD+104(16),CHRLINE+16                                        
                                                                                
PTBL80   MVC   PRNTYP,=C'BL01'     PRINT LINE                                   
         GOTO1 (R6),P1                                                          
                                                                                
PTBL90   AR    R2,R4               BUMP TBL ADR                                 
         BCT   R3,PTBL40                                                        
                                                                                
PTBLXIT  XMOD1 1                                                                
         EJECT                                                                  
HEXLATE  NTR1  ,                   BYTES AT R7 TO HEX AT R8 FOR R0 LEN          
         LR    R1,R0                                                            
         LR    R2,R8                                                            
HEXL1    UNPK  0(1,R8),0(1,R7)                                                  
         NI    0(R8),X'0F'                                                      
         MVN   1(1,R8),0(R7)                                                    
         NI    1(R8),X'0F'                                                      
         LA    R8,2(,R8)                                                        
         LA    R7,1(,R7)                                                        
         BCT   R0,HEXL1                                                         
                                                                                
         SLL   R1,1                                                             
         BCTR  R1,R0                                                            
         EX    R1,*+8                                                           
         B     HEXLX                                                            
         TR    0(0,R2),HEXLTBL                                                  
                                                                                
HEXLX    XIT1                                                                   
                                                                                
HEXLTBL  DC    C'0123456789ABCDEF'                                              
         EJECT                                                                  
SPACES   DC    CL132' '                                                         
         SPACE 2                                                                
TRNTBL   DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  X'00-0F'                    
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  X'10-1F'                    
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  X'20-2F'                    
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  X'30-3F'                    
         DC    X'404B4B4B4B4B4B4B4B4B4A4B4C4D4E4F'  X'40-4F'                    
         DC    X'504B4B4B4B4B4B4B4B4B5A5B5C5D5E5F'  X'50-5F'                    
         DC    X'60614B4B4B4B4B4B4B4B6A6B6C6D6E6F'  X'60-6F'                    
         DC    X'4B4B4B4B4B4B4B4B4B4B7A7B7C7D7E7F'  X'70-7F'                    
         DC    X'4B8182838485868788894B4B4B4B4B4B'  X'80-8F'                    
         DC    X'4B9192939495969798994B4B4B4B4B4B'  X'90-9F'                    
         DC    X'4B4BA2A3A4A5A6A7A8A94B4B4B4B4B4B'  X'A0-AF'                    
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  X'B0-BF'                    
         DC    X'4BC1C2C3C4C5C6C7C8C94B4B4B4B4B4B'  X'C0-CF'                    
         DC    X'4BD1D2D3D4D5D6D7D8D94B4B4B4B4B4B'  X'D0-DF'                    
         DC    X'4B4BE2E3E4E5E6E7E8E94B4B4B4B4B4B'  X'E0-EF'                    
         DC    X'F0F1F2F3F4F5F6F7F8F94B4B4B4B4B4B'  X'F0-FF'                    
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
PLISTD   DSECT                                                                  
ATBLNAME DS    A                                                                
ATBL     DS    A                                                                
NENTRY   DS    F                                                                
LENTRY   DS    F                                                                
AOPTN    DS    A                                                                
APRINT   DS    A                                                                
         SPACE 2                                                                
WRKD     DSECT                                                                  
DUB      DS    D                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    X                                                                
CHAR     DS    C                                                                
ANEXTEL  DS    A                                                                
P1       DS    A                                                                
P2       DS    A                                                                
VPRINT   DS    V                                                                
NUMCHRS  DS    F                                                                
HEXLINE  DS    8D                                                               
CHRLINE  DS    4D                                                               
PRNIOA   DS    CL1                                                              
PRNIOD   DS    CL132                                                            
         DS    CL3                                                              
PRNTYP   DS    CL4                                                              
PSKIP    DS    CL1                 NUMBER OF LINES TO SKIP                      
PTYPE    DS    CL1                 D=DUMP, H=HEX, C=CHAR, E=ELEMENT             
WRKX     DS    0C                                                               
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'008DDPRNTBL  03/18/20'                                      
         END                                                                    
