*          DATA SET ACLDPRGE   AT LEVEL 023 AS OF 04/02/19                      
*CATALP ACLDPRGE                                                                
LDPRGE   TITLE '- LOAD PURGE TEST'                                              
***********************************************************************         
* LEVEL CHANGE COMMENTS                                               *         
* ---------------------                                               *         
*VGUP 023 01APR19  <SPEC-33865> CHANGE TO DUMP & LOAD ROUTINE TO      *         
*                               STOP PURGE ORDER RECORD               *         
***********************************************************************         
***********************************************************************         
* PARAMS VIA R1                                                       *         
* AL3   A(RECORD)                                                     *         
* AL3   A(PEELDATE)                                                   *         
***********************************************************************         
         SPACE 1                                                                
LDPURGE  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,ACLDPRGE                                             
         USING WORKD,RC            RC=A(LOCAL W/S)                              
         ST    R1,SAVER1                                                        
         L     RF,4(R1)                                                         
         MVC   PEELDATE,0(RF)      SAVE PEELED DATE                             
         L     R2,0(R1)                                                         
         USING ACTRECD,R2          R2=A(RECORD)                                 
         CLI   ACTRFST,0           TEST ANY ELEMENTS ON RECORD                  
         BE    PRGEXIT             NO - PURGE THE RECORD                        
         GOTO1 VRECTYP,PARM,(C'D',ACTRECD)                                      
         MVC   RECTYPE,0(R1)       ESTABLISH RECORD TYPE                        
*                                                                               
         OC    TBADELDT,TBADELDT   TEST DELETE DATES ARE RESOLVED               
         BNZ   LDP02                                                            
         GOTO1 VDATCON,PARM,(5,0),(0,WORK)                                      
         GOTO1 VDATCON,PARM,(5,0),(2,TODAY)                                     
         MVC   WORK+6(6),WORK      SAVE TODAY IN WORK+6                         
         GOTO1 VADDAY,PARM,(C'M',WORK),WORK+12,-18                              
         MVC   WORK(6),WORK+12         FIRST DAY OF MONTH -18                   
         MVC   WORK+4(2),=C'01'                                                 
         GOTO1 VDATCON,PARM,(0,WORK),(2,TBADELDT) -18MONTHS (FIRSTDAY)          
         GOTO1 VDATCON,PARM,(0,WORK+6),(2,PBRDELDT) TODAY                       
         GOTO1 VADDAY,PARM,WORK+6,WORK,-10                                      
         GOTO1 VDATCON,PARM,(0,WORK),(1,BATDELDT)   TODAY -10 DAYS              
         GOTO1 VADDAY,PARM,WORK+6,WORK,-7                                       
         GOTO1 VDATCON,PARM,(0,WORK),(2,RAPDELDT)   TODAY -7  DAYS              
         GOTO1 VADDAY,PARM,(C'Y',WORK+6),WORK,-5                                
         GOTO1 VDATCON,PARM,(0,WORK),(1,ORDDELDT)   TODAY -5 YEARS              
*                                                                               
LDP02    CLI   RECTYPE,ACRTUNKN                                                 
         BE    LDP10                                                            
         CLI   RECTYPE,ACRTNBT     TEST NEW BATCH RECORDS                       
         BE    LDP50                                                            
         CLI   RECTYPE,ACRTBAT     TEST OLD BATCH RECORDS                       
         BE    LDP54                                                            
*        CLI   RECTYPE,ACRTORD     TEST ORDER RECORDS                           
*        BE    LDP55               COMMENT OUT CODE SPEC-33865                  
         CLI   RECTYPE,ACRTANC     TEST ACCOUNT NAME CHANGE POINTER             
         BE    LDP14                                                            
         CLI   RECTYPE,ACRTPBA     TEST NEW BILLING RECORDS                     
         BE    LDP70                                                            
         CLI   RECTYPE,ACRTTIM     TIME RECORD                                  
         BE    LDP03                                                            
         CLI   RECTYPE,ACRTTRNA                                                 
         BH    LDP10                                                            
         CLI   RECTYPE,ACRTACTL    TEST LOW ACCOUNT                             
         BL    LDP10                                                            
         BH    LDP03                                                            
         MVI   ACTPURGE,0          RESET PURGE FLAG                             
         TM    ACTRSTAT,ACTSDELT   TEST DELETE STATUS SET ON ACCOUNT            
         BZ    LDP60               NO                                           
*                                                                               
         BAS   RE,TESTDEL                                                       
         BNE   LDP60               KEEP THIS DELETE AROUND                      
         MVI   ACTPURGE,YES        YES - SET FLAG                               
         B     PRGEXIT             AND RETURN PURGE INDICATOR                   
*                                                                               
LDP03    CLI   ACTPURGE,YES        TEST PURGE SET                               
         BE    PRGEXIT             YES - PURGE THIS RECORD ELSE                 
         TM    ACTRSTAT,ACTSDELT   TEST DELETE STS SET ON THIS RECORD           
         BZ    *+12                                                             
         BAS   RE,TESTDEL                                                       
         BE    PRGEXIT             YES - RETURN PURGE INDICATOR                 
*                                                                               
         CLI   RECTYPE,ACRTCAC     TEST BUCKET RECORD                           
         BE    LDP20                                                            
         CLI   RECTYPE,ACRTTRN     TEST TRANSACTION                             
         BE    LDP30                                                            
         CLI   RECTYPE,ACRTTIM     TEST TIME                                    
         BE    LDP30                                                            
         CLI   RECTYPE,ACRTCHDH    TEST REAL CONTRA POINTER                     
         BNE   LDP60                                                            
         ICM   R0,15,NAMBUFFN      TEST ANY ACCOUNT NAME CHANGES                
         BZ    LDP60                                                            
         USING CHDRECD,R2                                                       
         GOTO1 VBINSRCH,PARM,CHDKCULC,NAMBUFF,(R0),A(NAMBUFFL),        *        
               A(L'NAMBKEY),(R0)                                                
         CLI   0(R1),0             TEST ACCOUNT FOUND IN CHANGE TABLE           
         BNE   LDP60                                                            
         L     R3,0(R1)                                                         
         USING NAMBUFFD,R3                                                      
         LA    R1,CHDRFST                                                       
         USING CACELD,R1                                                        
         CLI   CACEL,CACELQ                                                     
         BNE   LDP60                                                            
         SR    RF,RF                                                            
         IC    RF,CACLN                                                         
         SHI   RF,CACLN1Q                                                       
         CLM   RF,1,NAMBNAML       TEST LENGTH OF NAME CHANGED                  
         BNE   LDP04                                                            
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         CLC   CACNAME(0),NAMBNAM  TEST CONTRA NAME SAME AS ACCOUNT             
         BE    LDP60                                                            
*                                                                               
LDP04    GOTO1 VHELLO,PARM,(C'D',ACCMST),('CACELQ',CHDRECD),0,0,0               
         LA    R1,ELEMENT                                                       
         MVI   CACEL,1             SET LOW ELEMENT CODE                         
         MVC   CACCNT,CHDKCULC                                                  
         SR    RF,RF                                                            
         ICM   RF,1,NAMBNAML                                                    
         BZ    *+6                                                              
         BCTR  RF,0                                                             
         EXMVC RF,CACNAME,NAMBNAM                                               
         LA    RF,CACLN1Q+1(RF)                                                 
         STC   RF,CACLN                                                         
         GOTO1 VHELLO,PARM,(C'P',ACCMST),CHDRECD,ELEMENT,0,0                    
         CLI   12(R1),0                                                         
         BE    *+6                                                              
         DC    H'0'                DIE IF CAN'T ADD CONTRA HEADER               
         LA    R1,CHDRFST                                                       
         CLI   CACEL,1                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
         MVI   CACEL,CACELQ                                                     
         LA    RE,CHDRECD          NOW FIX PHYSICAL (IBM) RECORD LENGTH         
         SHI   RE,4                                                             
         SR    RF,RF                                                            
         ICM   RF,3,CHDRLEN                                                     
         LA    RF,4(,RF)                                                        
         SLL   RF,16                                                            
         STCM  RF,15,0(RE)                                                      
         B     LDP60                                                            
         DROP  R3                                                               
*                                                                               
         USING ACTRECD,R2                                                       
LDP10    TM    ACTRSTAT,ACTSDELT   TEST DELETE STS SET ON THIS RECORD           
         BZ    LDP60                                                            
         BAS   RE,TESTDEL                                                       
         BE    PRGEXIT             OK TO PURGE                                  
         B     LDP60               KEEP DELETE ON FILE                          
*                                                                               
         USING ANCRECD,R2                                                       
LDP14    ICM   R3,15,NAMBUFFN                                                   
         LA    R0,1(R3)                                                         
         C     R0,=A(NAMBUFFM)     TEST NUMBER OF ENTRIES EXCEEDED              
         BNH   *+6                                                              
         DC    H'0'                                                             
         STCM  R0,15,NAMBUFFN                                                   
         MHI   R3,NAMBUFFL                                                      
         LA    R3,NAMBUFF(R3)                                                   
         USING NAMBUFFD,R3                                                      
         MVC   NAMBKEY,ANCKCULA                                                 
         LA    R1,ANCRECD+(ACTRFST-ACTRECD)                                     
         SR    R0,R0                                                            
         USING NAMELD,R1                                                        
LDP16    CLI   NAMEL,0             TEST EOR                                     
         BE    PRGEXIT                                                          
         CLI   NAMEL,NAMELQ        LOCATE NAME ELEMENT                          
         BE    *+14                                                             
         IC    R0,NAMLN                                                         
         AR    R1,R0                                                            
         B     LDP16                                                            
         SR    RF,RF                                                            
         IC    RF,NAMLN                                                         
         SHI   RF,NAMLN1Q                                                       
         STC   RF,NAMBNAML         SET ACTUAL LENGTH OF ACCOUNT NAME            
         MVC   NAMBNAM,NAMEREC     SET ACCOUNT NAME                             
         B     PRGEXIT                                                          
         DROP  R1,R2,R3                                                         
         EJECT                                                                  
***********************************************************************         
* ADD SPARE BUCKET FOR NEW COST RECORDS                               *         
***********************************************************************         
         SPACE 1                                                                
         USING CHDRECD,R2                                                       
LDP20    CLI   CHDKBTYP,C' '       TEST SPECIAL BUCKET TYPE                     
         BNH   LDP60                                                            
         CLI   CHDKNULL,C' '       TEST NEW COST BUCKET                         
         BNH   LDP60                                                            
         SR    R1,R1                                                            
         SR    RE,RE                                                            
         LA    R3,CHDRFST                                                       
*                                                                               
         USING BUKELD,R3                                                        
LDP20A   CLI   BUKEL,0                                                          
         BE    LDP20F                                                           
         CLI   0(R3),BUKELQ        MAKE SURE IT IS A BUCKET ELEMENT             
         BNE   LDP20B                                                           
         LTR   RE,RE                                                            
         BNZ   *+6                                                              
         LR    RE,R3               FIRST ELEMENT                                
         LA    R1,1(,R1)           COUNT HOW MANY (MAX 108)                     
LDP20B   ZIC   RF,1(,R3)                                                        
         AR    R3,RF                                                            
         B     LDP20A                                                           
*                                  DELETE THE 1ST R1 # OF ELEMENTS              
LDP20F   SHI   R1,108              9 YEARS X 12 MONTHS = 108                    
         BNP   LDP20X              FINISHED                                     
         LR    R3,RE               RE-LOAD FIRST ELEMENT                        
LDP20G   CLI   0(R3),0             EOR ?                                        
         BE    LDP20R              YES                                          
         CLI   0(R3),BUKELQ        BUCKET ELEMENT ?                             
         BE    LDP20H              YES                                          
         ZIC   RF,1(,R3)           NO                                           
         AR    R3,RF                                                            
         B     LDP20G              GET NEXT                                     
*                                                                               
LDP20H   MVI   0(R3),X'FF'         MARK THE X'45' DELETED                       
         ZIC   RF,1(,R3)                                                        
         AR    R3,RF                                                            
         BCT   R1,LDP20G                                                        
*                                                                               
LDP20R   GOTO1 VHELLO,PARM,(C'D',ACCMST),(X'FF',CHDRECD),0,0,0                  
         LA    RE,CHDRECD          NOW FIX PHYSICAL (IBM) RECORD LENGTH         
         SHI   RE,4                                                             
         SR    RF,RF                                                            
         ICM   RF,3,CHDRLEN                                                     
         AHI   RF,4                                                             
         SLL   RF,16                                                            
         STCM  RF,15,0(RE)                                                      
*                                                                               
LDP20X   SR    RF,RF                                                            
         SR    R1,R1                                                            
         LA    R3,CHDRFST                                                       
*                                                                               
LDP21    CLI   BUKEL,0             END OF RECORD                                
         BE    LDP23                                                            
         CLI   BUKEL,BUKELQ        TEST BUCKET ELEMENT                          
         BNE   LDP22                                                            
         LTR   RF,RF               FIRST TIME                                   
         BNZ   *+10                                                             
         LR    RF,R3                                                            
         B     *+16                                                             
         CLC   BUKMOS,BUKMOS-BUKELD(RF)                                         
         BNH   *+6                                                              
         LR    RF,R3                                                            
         CP    BUKDR,=P'0'                                                      
         BNE   LDP22                                                            
         CP    BUKCR,=P'0'                                                      
         BE    LDP60               FOUND A SPARE (DON'T ADD ANOTHER)            
LDP22    IC    R1,BUKLN                                                         
         AR    R3,R1                                                            
         B     LDP21                                                            
*                                                                               
LDP23    LTR   RF,RF               NO BUCKET ELEMENTS                           
         BZ    LDP60               DON'T ADD ONE                                
         LA    R3,ELEMENT          BUILD A NEW ELEMENT                          
         MVI   BUKEL,BUKELQ                                                     
         MVI   BUKLN,BUKLNQ                                                     
         ZAP   BUKDR,=P'0'                                                      
         ZAP   BUKCR,=P'0'                                                      
         MVC   WORK(2),BUKMOS-BUKELD(RF) LATEST YEAR/MONTH                      
         MVI   WORK+2,1                                                         
         GOTO1 VDATCON,PARM,(X'31',WORK),(1,WORK+3),(2,0)                       
         MVC   BUKMOS,WORK+3                                                    
         GOTO1 VHELLO,PARM,(C'P',ACCMST),CHDRECD,ELEMENT,0,0                    
         CLI   12(R1),0                                                         
         BE    *+6                                                              
         DC    H'0'                DIE IF CAN'T ADD ELEMENT                     
         LA    RE,CHDRECD          NOW FIX PHYSICAL (IBM) RECORD LENGTH         
         SHI   RE,4                                                             
         SR    RF,RF                                                            
         ICM   RF,3,CHDRLEN                                                     
         LA    RF,4(,RF)                                                        
         SLL   RF,16                                                            
         STCM  RF,15,0(RE)                                                      
         B     LDP60                                                            
         DROP  R2,R3                                                            
         EJECT                                                                  
***********************************************************************         
* DELETE PEELED TRANSACTIONS                                          *         
***********************************************************************         
         SPACE 1                                                                
         USING TRNRECD,R2                                                       
LDP30    DS    0H                                                               
         OC    PEELDATE,PEELDATE   TEST ANY PEEL DATE                           
         BZ    LDP60               NONE, KEEP IT                                
         TM    TRNRSTA2,TRNSPEEL   TEST PEELED TRANSACTION                      
         BZ    LDP60                                                            
*                                                                               
         USING TRSELD,R3                                                        
         LA    R3,TRNRFST          LOCATE STATUS ELEMENT                        
         SR    R0,R0                                                            
LDP32    IC    R0,TRSLN                                                         
         AR    R3,R0                                                            
         CLI   TRSEL,0             END OF RECORD                                
         BE    LDP60                                                            
         CLI   TRSEL,TRSELQ        STATUS ELEMENT                               
         BNE   LDP32                                                            
         OC    TRSPDAT,TRSPDAT     HAS IT BEEN PEELED                           
         BZ    LDP60                                                            
         CLC   TRSPDAT,PEELDATE    TEST IF READY TO BE DROPPED                  
         BNH   PRGEXIT                                                          
         B     LDP60                                                            
         DROP  R2,R3                                                            
         EJECT                                                                  
***********************************************************************         
* DEAL WITH NEW BATCH RECORDS                                         *         
***********************************************************************         
         SPACE 1                                                                
         USING TBARECD,R2                                                       
LDP50    OC    TBAKTSEQ,TBAKTSEQ   TEST HEADER RECORD                           
         BNZ   LDP52                                                            
         MVI   TBAPURGE,0                                                       
         TM    TBARHSTA,TBAHSDEL   TEST DELETED                                 
         BO    *+14                                                             
         CLC   TBAHREDT,TBADELDT   TEST EFFECTIVE DATE AGAINST CONTROL          
         BH    LDP60                                                            
         MVI   TBAPURGE,1          LOWER - SET TO DELETE THIS BATCH             
*                                                                               
LDP52    CLI   TBAPURGE,0          TEST BATCH HEADER DELETED                    
         BE    LDP60                                                            
         B     PRGEXIT                                                          
         DROP  R2                                                               
         SPACE 2                                                                
***********************************************************************         
* DEAL WITH OLD BATCH RECORDS                                         *         
***********************************************************************         
         SPACE 1                                                                
         USING BATRECD,R2                                                       
LDP54    TM    BATRSTAT,BATSDELT   TEST DELETED                                 
         BO    PRGEXIT                                                          
         CLC   BATKDATE,BATDELDT   TEST BATCH DATE AGAINST CONTROL              
         BNL   LDP60                                                            
         B     PRGEXIT                                                          
         DROP  R2                                                               
         SPACE 2                                                                
***********************************************************************         
* DEAL WITH ORDER RECORDS                                             *         
***********************************************************************         
*&&DO                                                                           
         SPACE 1                                                                
         USING ORDRECD,R2                                                       
LDP55    TM    ORDRSTAT,ORDSFMCH+ORDSLDEL  MATCHED/LOGICALLY DELETED            
         BZ    LDP60                                                            
         LA    R3,ORDRFST          LOCATE ORDER ELEMENT                         
         USING ORDELD,R3                                                        
         SR    R0,R0                                                            
LDP56    CLI   ORDEL,0             END OF RECORD                                
         BE    LDP60                                                            
         CLI   ORDEL,ORDELQ        ORDER ELEMENT                                
         BE    *+14                                                             
         IC    R0,ORDLN                                                         
         AR    R3,R0                                                            
         B     LDP56                                                            
         CLC   ORDDATE,ORDDELDT    TEST AGAINST PURGE DATE                      
         BNL   LDP60                                                            
         B     PRGEXIT                                                          
         DROP  R2,R3                                                            
         EJECT                                                                  
*&&                                                                             
***********************************************************************         
* COMPANY RECORD - EXTRACT PASSIVE POINTER RULES                      *         
***********************************************************************         
         SPACE 1                                                                
         USING CPYRECD,R2                                                       
LDP60    CLI   RECTYPE,ACRTCPY     TEST COMPANY RECORD                          
         BNE   LDP60X                                                           
         XC    CPYVALS(CPYVALSL),CPYVALS                                        
         LA    R6,CPYRFST                                                       
         SR    R0,R0                                                            
         USING CPYELD,R6                                                        
LDP602   CLI   CPYEL,0             LOCATE COMPANY ELEMENT                       
         BE    LDP60X                                                           
         CLI   CPYEL,CPYELQ                                                     
         BE    *+14                                                             
         IC    R0,CPYLN                                                         
         AR    R6,R0                                                            
         B     LDP602                                                           
         CLI   CPYLN,CPYSTAT7-CPYELD                                            
         BL    *+10                                                             
         MVC   CPYSTA7,CPYSTAT7    SAVE COMPANY STATUS BYTE 7                   
*                                                                               
LDP60X   DS    0H                                                               
         DROP  R2,R6                                                            
         EJECT                                                                  
***********************************************************************         
* LEDGER RECORD - EXTRACT LEVEL LENGTHS                               *         
***********************************************************************         
         SPACE 1                                                                
         USING LDGRECD,R2                                                       
LDP62    CLI   RECTYPE,ACRTLDG     TEST FOR LEDGER RECORD                       
         BNE   LDP62X                                                           
         XC    LDGVALS(LDGVALSL),LDGVALS                                        
*                                                                               
         LA    R6,LDGRFST                                                       
         SR    R0,R0                                                            
         USING ACLELD,R6                                                        
LDP622   CLI   ACLEL,0             TEST END OF RECORD                           
         BE    LDP62X                                                           
         CLI   ACLEL,ACLELQ        TEST HEIRARCHY ELEMENT                       
         BE    *+14                                                             
         IC    R0,ACLLN                                                         
         AR    R6,R0                                                            
         B     LDP622                                                           
         MVC   LDGLVALN,ACLVALS                                                 
         MVC   LDGLVBLN,ACLVALS+L'ACLVALS                                       
         MVC   LDGLVCLN,ACLVALS+L'ACLVALS+L'ACLVALS                             
         MVC   LDGLVDLN,ACLVALS+L'ACLVALS+L'ACLVALS+L'ACLVALS                   
*                                                                               
LDP62X   DS    0H                                                               
         DROP  R2,R6                                                            
         EJECT                                                                  
***********************************************************************         
* PRODUCTION RECORD HANDLING                                          *         
***********************************************************************         
         SPACE 1                                                                
         USING ACTRECD,R2                                                       
LDP64    CLC   =C'SJ',ACTKUNT      TEST PRODUCTION LEDGER                       
         BNE   LDP64X                                                           
         CLI   RECTYPE,ACRTACTH    TEST CLIENT/PRODUCT LEVEL ACCOUNT            
         BNE   LDP6410                                                          
         SR    R1,R1                                                            
         ICM   R1,1,LDGLVALN                                                    
         BNZ   *+6                                                              
         DC    H'0'                                                             
         LA    R1,ACTKACT(R1)                                                   
         CLI   0(R1),C' '          TEST CLIENT OR PRODUCT RECORD                
         BNE   LDP644                                                           
*                                                                               
         LA    R1,ACTRFST          DEAL WITH CLIENT RECORD                      
         SR    R0,R0                                                            
         USING PPRELD,R1                                                        
         XC    CLIVALS(CLIVALSL),CLIVALS                                        
LDP642   CLI   PPREL,0             TEST END OF RECORD                           
         BE    LDP64X                                                           
         CLI   PPREL,PPRELQ        LOCATE CLIENT PROFILE ELEMENT                
         BE    *+14                                                             
         IC    R0,PPRLN                                                         
         AR    R1,R0                                                            
         B     LDP642                                                           
         MVC   CLIDEBAC,PPRRECVU                                                
         MVC   CLICSTAC,PPRCOSTU                                                
         MVC   CLIOFFC,PPRGAOFF                                                 
         B     LDP64X                                                           
*                                                                               
LDP644   LA    R1,ACTRFST          DEAL WITH PRODUCT RECORD                     
         SR    R0,R0                                                            
         USING PPRELD,R1                                                        
         XC    PROVALS(PROVALSL),PROVALS                                        
*                                                                               
LDP646   CLI   PPREL,PPRELQ        TEST PRODUCTION PROFILE ELEMENT              
         BE    LDP648                                                           
         CLI   PPREL,0             TEST END OF RECORD                           
         BE    *+14                                                             
         IC    R0,PPRLN                                                         
         AR    R1,R0                                                            
         B     LDP646                                                           
         TM    CPYSTA7,CPYSAGRP    TEST ACCOUNT GROUP PASSIVES REQUIRED         
         BZ    LDP64X                                                           
         LA    R1,ELEMENT                                                       
         XC    PPRELD(PPRLN1Q),PPRELD                                           
         MVI   PPREL,PPRELQ                                                     
         MVI   PPRLN,PPRLN1Q                                                    
         GOTO1 VHELLO,PARM,(C'P',ACCMST),ACTRECD,ELEMENT,0                      
         CLI   12(R1),0                                                         
         BNE   LDP64X                                                           
         L     R1,16(R1)           R1=A(ELEMENT ON RECORD)                      
         SR    RE,RE                                                            
         ICM   RE,3,ACTRLEN                                                     
         LA    RE,4(RE)                                                         
         SLL   RE,16                                                            
         LA    RF,ACTRECD                                                       
         SH    RF,=H'4'                                                         
         STCM  RE,15,0(RF)                                                      
*                                                                               
LDP648   MVC   PROOFFC,PPRGAOFF                                                 
         TM    CPYSTA7,CPYSAGRP    TEST ACCOUNT GROUP PASSIVES REQUIRED         
         BZ    LDP64X                                                           
         OC    PPRRECV,PPRRECV     REFRESH PRODUCT RECEIVABLE ACCOUNT           
         BNZ   *+16                                                             
         MVC   PPRRECVC,ACTKCPY                                                 
         MVC   PPRRECVU(L'CLIDEBAC),CLIDEBAC                                    
         OC    PPRCOST,PPRCOST     REFRESH PRODUCT COSTING ACCOUNT              
         BNZ   *+16                                                             
         MVC   PPRCOSTC,ACTKCPY                                                 
         MVC   PPRCOSTU(L'CLICSTAC),CLICSTAC                                    
         B     LDP64X                                                           
*                                                                               
LDP6410  CLI   RECTYPE,ACRTACTL    TEST JOB LEVEL ACCOUNT                       
         BNE   LDP6414                                                          
         LA    R1,ACTRFST          DEAL WITH PRODUCT RECORD                     
         SR    R0,R0                                                            
         USING PPRELD,R1                                                        
         XC    JOBVALS(JOBVALSL),JOBVALS                                        
*                                                                               
LDP6412  CLI   PPREL,0             TEST END OF RECORD                           
         BE    LDP64X                                                           
         CLI   PPREL,PPRELQ        TEST PRODUCTION PROFILE ELEMENT              
         BE    *+14                                                             
         IC    R0,PPRLN                                                         
         AR    R1,R0                                                            
         B     LDP6412                                                          
         MVC   JOBOFFC,PPRGAOFF                                                 
         B     LDP64X                                                           
*                                                                               
LDP6414  CLI   RECTYPE,ACRTTRN                                                  
         BE    *+12                                                             
         CLI   RECTYPE,ACRTTRNA                                                 
         BNE   LDP64X                                                           
         USING TRNRECD,R2                                                       
         LA    R1,TRNRFST                                                       
         SR    R0,R0                                                            
         USING TRNELD,R1                                                        
         TM    TRNSTAT,TRNSDR                                                   
         BZ    LDP64X                                                           
*                                                                               
         USING PTAELD,R1                                                        
LDP6416  IC    R0,PTALN                                                         
         AR    R1,R0                                                            
         CLI   PTAEL,0                                                          
         BE    LDP64X                                                           
         CLI   PTAEL,PTAELQ                                                     
         BNE   LDP6416                                                          
         CLI   PTATYPE,PTATRAL                                                  
         BNE   LDP6416                                                          
         TM    PTASTAT1,PTASPEND                                                
         BNZ   LDP6416                                                          
         CLC   PTAOFFC,SPACES                                                   
         BH    LDP6416                                                          
         MVC   PTAOFFC,JOBOFFC                                                  
         CLC   PTAOFFC,SPACES                                                   
         BH    LDP6416                                                          
         MVC   PTAOFFC,PROOFFC                                                  
         CLC   PTAOFFC,SPACES                                                   
         BH    LDP6416                                                          
         MVC   PTAOFFC,CLIOFFC                                                  
         B     LDP6416                                                          
*                                                                               
LDP64X   DS    0H                                                               
         B     EXIT                                                             
         DROP  R1,R2                                                            
         EJECT                                                                  
***********************************************************************         
* DEAL WITH NEW BILL RECORDS                                          *         
***********************************************************************         
         SPACE 1                                                                
         USING PBRRECD,R2                                                       
LDP70    CLI   PBRKSUB,PBRKACTQ                                                 
         BNE   EXIT                                                             
         CLI   PBRKPARA,0          TEST HEADER RECORD                           
         BNE   LDP704                                                           
         MVI   PBRPURGE,0                                                       
         TM    PBRRSTAT,PBRSDELT   TEST BILL HEADER DELETED                     
         BZ    *+12                                                             
         MVI   PBRPURGE,1                                                       
         B     PRGEXIT                                                          
*                                                                               
         LA    R1,PBRRFST                                                       
         USING BLHELD,R1                                                        
         SR    R0,R0                                                            
LDP702   CLI   BLHEL,0             LOCATE BILL HEADER ELEMENT                   
         BE    LDP704                                                           
         CLI   BLHEL,BLHELQ                                                     
         BE    *+14                                                             
         IC    R0,BLHLN                                                         
         AR    R1,R0                                                            
         B     LDP702                                                           
         CLC   PBRDELDT,BLHEXPD    TEST TODAY VS. BILL EXPIRY DATE              
         BL    EXIT                                                             
         MVI   PBRPURGE,1          LOWER - SET TO DELETE THIS BILL              
*                                                                               
LDP704   CLI   PBRPURGE,0          TEST BILL HEADER DELETED                     
         BE    EXIT                                                             
         B     PRGEXIT                                                          
         DROP  R1,R2                                                            
         EJECT                                                                  
***********************************************************************         
* SUB-ROUTINE TO TEST WHETHER A DELETED RECORD CAN BE PURGED          *         
* ON EXIT, CC=EQ FOR YES, CC=NEQ FOR NO                               *         
* DELETED RECORDS WITH RECORD ACTIVITY POINTERS SHOULD BE KEPT FOR    *         
* ONE DUMP AND LOAD CYCLE FOR PRESTO MONITORING                       *         
***********************************************************************         
         SPACE 1                                                                
         USING ACTRECD,R2                                                       
         USING PTRELD,R1                                                        
TESTDEL  NTR1  ,                                                                
         LA    R1,ACTRFST          R1=A(FIRST ELEMENT)                          
TESTDEL2 CLI   PTREL,0             TEST FOR EOR                                 
         BE    TESTDELY            YES-OK TO DELETE                             
         CLI   PTREL,PTRELQ        TEST FOR PASSIVE POINTER ELEMENT             
         BNE   TESTDEL4            NO                                           
         CLI   PTRTYPE,PTRTRAP     TEST FOR RECORD ACTIVITY POINTER             
         BE    TESTDEL6            YES                                          
*                                                                               
TESTDEL4 LLC   R0,PTRLN                                                         
         AR    R1,R0               BUMP TO NEXT ELEMENT                         
         B     TESTDEL2                                                         
*                                                                               
         USING RAPRECD,R4                                                       
TESTDEL6 LA    R4,PTRCODE                                                       
         DROP  R1,R2                                                            
                                                                                
***********************************************************************         
* THIS ENSURES THAT THE RECORDS WILL BE ON THE TAPE WE SEND TO        *         
*   IRON MOUNTAIN, LAST DAY OF THE MONTH                              *         
***********************************************************************         
         CLI   RECTYPE,ACRTACTL    TEST LOW ACCOUNT                             
         BNE   TESTDEL8                                                         
         GOTO1 VDATCON,PARM,(X'32',RAPKDATE),(2,RAPMYEND),(1,0)                 
         CLC   TODAY,RAPMYEND      ONLY PURGE ONCE WE GET TO NEXT MONTH         
         BNH   TESTDELN            NO-KEEP IT AROUND                            
                                                                                
TESTDEL8 CLC   RAPKDATE,RAPDELDT   TEST ACTIVITY PRIOR TO CUTOFF                
         BL    TESTDELY            YES-OK TO PURGE                              
         B     TESTDELN            NO-KEEP IT AROUND                            
*                                                                               
TESTDELY CLI   *+1,0               SET CC=EQU                                   
         B     TESTDELX                                                         
*                                                                               
TESTDELN CLI   *,0                 SET CC=NEQ                                   
*                                                                               
TESTDELX B     EXIT                                                             
         DROP  R4                                                               
         EJECT                                                                  
PRGEXIT  L     R1,SAVER1                                                        
         MVI   0(R1),X'FF'         RETURN PURGE INDICATOR                       
*                                                                               
EXIT     XIT1  ,                                                                
         SPACE 1                                                                
         LTORG                                                                  
         SPACE 2                                                                
SPACES   DC    CL64' '                                                          
VRECTYP  DC    V(ACRECTYP)                                                      
VHELLO   DC    V(HELLO)                                                         
VBINSRCH DC    V(BINSRCH)                                                       
VDATCON  DC    V(DATCON)                                                        
VADDAY   DC    V(ADDAY)                                                         
ACCMST   DC    C'ACCMST '                                                       
PEELDATE DC    XL2'00'                                                          
*                                                                               
TODAY    DC    XL2'00'    TODAY                        COMPRESSED               
TBADELDT DC    XL2'00'    TODAY -13 MONTHS (FIRST DAY) COMPRESSED               
PBRDELDT DC    XL2'00'    TODAY                        COMPRESSED               
BATDELDT DC    XL3'00'    TODAY -10 DAYS               PACKED                   
RAPDELDT DC    XL2'00'    TODAY -7  DAYS               COMPRESSED               
RAPMYEND DC    XL2'00'    MONTH END DATE               COMPRESSED               
ORDDELDT DC    XL3'00'    TODAY -5  YEARS              PACKED                   
*                                                                               
TBAPURGE DC    X'00'                                                            
PBRPURGE DC    X'00'                                                            
ACTPURGE DC    X'00'                                                            
YES      EQU   X'FF'               PURGE ALL FOR THIS LOW ACCOUNT               
*                                                                               
CPYVALS  DS    0X                  ** COMPANY RECORD VALUES **                  
CPYSTA7  DC    X'00'               COMPANY STATUS BYTE 7                        
CPYVALSL EQU   *-CPYVALS                                                        
*                                                                               
LDGVALS  DS    0X                  ** LEDGER RECORD VALUES **                   
LDGLVALN DC    AL1(0)              LENGTH OF LEVEL A ACCOUNT                    
LDGLVBLN DC    AL1(0)              LENGTH OF LEVEL B ACCOUNT                    
LDGLVCLN DC    AL1(0)              LENGTH OF LEVEL C ACCOUNT                    
LDGLVDLN DC    AL1(0)              LENGTH OF LEVEL D ACCOUNT                    
LDGVALSL EQU   *-LDGVALS                                                        
*                                                                               
CLIVALS  DS    0X                  ** CLIENT RECORD VALUES **                   
CLIDEBAC DC    CL(L'ACTKULA)' '    CLIENT DEBTORS ACCOUNT                       
CLICSTAC DC    CL(L'ACTKULA)' '    CLIENT COSTING ACCOUNT                       
CLIOFFC  DS    CL(L'PPRGAOFF)      CLIENT OFFICE CODE                           
CLIVALSL EQU   *-CLIVALS                                                        
*                                                                               
PROVALS  DS    0X                  ** PRODUCT RECORD VALUES **                  
PROOFFC  DS    CL(L'PPRGAOFF)      PRODUCT OFFICE CODE                          
PROVALSL EQU   *-PROVALS                                                        
*                                                                               
JOBVALS  DS    0X                  ** JOB RECORD VALUES **                      
JOBOFFC  DS    CL(L'PPRGAOFF)      JOB OFFICE CODE                              
JOBVALSL EQU   *-JOBVALS                                                        
         SPACE 2                                                                
NAMBUFFN DC    A(0)                NUMBER OF ENTRIES IN NAMBUFF                 
NAMBUFF  DC    (NAMBUFFM)XL(NAMBUFFL)'00'                                       
         SPACE 1                                                                
WORKD    DSECT                     ** LOCAL WORKING STORAGE **                  
DUB1     DS    D                                                                
DUB2     DS    D                                                                
PARM     DS    6F                                                               
SAVER1   DS    F                                                                
WORK     DS    CL16                                                             
ELEMENT  DS    XL256                                                            
RECTYPE  DS    X                                                                
WORKX    EQU   *                                                                
         SPACE 1                                                                
NAMBUFFD DSECT                     ** ACCOUNT NAME CHANGE BUFFER **             
NAMBKEY  DS    XL(L'ANCKCULA)      ACCOUNT CODE                                 
NAMBNAML DS    XL(L'NAMEL)         ACTUAL LENGTH OF ACCOUNT NAME                
NAMBNAM  DS    XL(L'NAMEREC)       ACCOUNT NAME                                 
NAMBUFFL EQU   *-NAMBUFFD          LENGTH OF BUFFER ENTRY                       
NAMBUFFM EQU   20000               MAXIMUM NUMBER OF ENTRIES IN NAMBUFF         
         SPACE 1                                                                
* ACGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* ACRECEQUS                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACRECEQUS                                                      
         PRINT ON                                                               
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'023ACLDPRGE  04/02/19'                                      
         END                                                                    
