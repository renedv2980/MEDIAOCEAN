*          DATA SET PPCOMLIN   AT LEVEL 070 AS OF 02/01/10                      
*CATALP PPCOMLIN                                                                
*                                                                               
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
*                                                                               
         TITLE 'INSERTION ORDERS - PRINT COMMENT LINE'                          
*                                                                               
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
*                                                                               
* KWAN 10/17/05 DO NOT STRIP LEADING SPACES FOR COMMENTS                        
*                                                                               
* KWAN 07/22/05 NEW SUB-REPLY RECORD FOR LEGAL WARNING COMMENTS                 
*                                                                               
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
*                                                                               
COMLIN   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 COMLINDX-COMLIND,*COMLIN*,CLEAR=YES                              
*                                                                               
         LR    R7,RC                                                            
         USING COMLIND,R7          R7 = A(LOCAL STORAGE)                        
*                                                                               
         L     RC,0(R1)                                                         
         USING POLWRKD,RC                                                       
         USING T41FFFD,RA                                                       
         USING IOWORKD,R8                                                       
         L     R9,PPFILEC                                                       
         USING POLFILED,R9                                                      
*                                                                               
         MVI   COMWIOSW,0          INIT TYPE OF COMMENT                         
*                                                                               
         L     R2,4(R1)            R2 = A(COMMENT LINE)                         
         LA    R4,2(R2)                                                         
         SR    R5,R5                                                            
         IC    R5,1(R2)            R5 = ELEM LENGTH                             
         AHI   R5,-3                                                            
*                                                                               
         CLC   0(3,R4),=C'RC='                                                  
         BE    EXIT                DON'T PRINT RC= COMMENTS                     
         CLC   0(5,R4),=C'SHIP='   OR SHIP= COMMENTS                            
         BE    EXIT                                                             
         CLC   0(6,R4),=C'LABEL='                                               
         BE    EXIT                                                             
*                                                                               
         LR    R6,R4                                                            
         CLI   0(R4),C'+'                                                       
         BNE   *+8                                                              
         LA    R6,2(R6)                                                         
         SR    RF,RF                                                            
         CLC   0(3,R6),=C'DC='     CANCELLATION COMMENTS?                       
         BNE   *+16                                                             
         TM    ADBSW,AS_WEBIO      WEB IO?                                      
         BNZ   EXIT                HANDLED WHEN INSERTION IS PROCESSED          
         LA    RF,1                ONLY DC= COMMENTS ON CANCELLATION            
*                                                                               
         CLI   TSCHGSW-STATDIF,C'D'                                             
         BNE   *+6                                                              
         BCTR  RF,R0                                                            
         LTR   RF,RF                                                            
         BNZ   EXIT                                                             
*                                                                               
         CLI   0(R4),C'+'          CHECK SPACING CONTROL                        
         BNE   COML2                                                            
         MVC   SPACING,1(R4)                                                    
*                                                                               
         MVI   DUB+0,X'FF'         INDICATOR FOR SKIPPING LINES                 
         MVC   DUB+1(2),0(R4)                                                   
         LA    RE,DUB                                                           
         ST    RE,FULL                                                          
         BRAS  RE,RPYCOMLN         REPLY COMMENT LINE                           
*                                                                               
         NI    SPACING,X'0F'                                                    
         CLI   SPACING,3                                                        
         BNH   *+8                                                              
         MVI   SPACING,3                                                        
         CLI   SPACING,0                                                        
         BNE   *+8                                                              
         MVI   SPACING,1                                                        
         MVI   P,0                                                              
         GOTOR AIOPRNT,DMCB,(RC)                                                
*                                                                               
         AHI   R5,-2                                                            
         LA    R4,2(R4)                                                         
*                                                                               
COML2    DS    0H                                                               
         CLC   =C'DC=',0(R4)                                                    
         BNE   COML2A                                                           
         CLC   QPROG-QRECORD+SAVEQ,=C'73'                                       
         BNE   *+10                                                             
         MVC   P+2(3),=C'DC='                                                   
         AHI   R5,-3                                                            
         LA    R4,3(R4)                                                         
         B     COML4                                                            
*                                                                               
COML2A   DS    0H                                                               
         CLC   0(4,R4),=C'MAT='                                                 
         BNE   COML4                                                            
         MVI   COMWIOSW,C'M'                                                    
         CLC   QPROG-QRECORD+SAVEQ,=C'73'                                       
         BNE   COML2B                                                           
         MVC   P+01(4),=C'MAT='                                                 
         B     COML3                                                            
*                                                                               
COML2B   DS    0H                                                               
         CLI   MATSW,C'R'                                                       
         BE    EXIT                                                             
         CLI   MATSW,C'N'                                                       
         BE    COML3                                                            
*                                                                               
         GOTOR AMATCOM,DMCB,(RC)                                                
*                                                                               
         CLI   MATSW,C'N'                                                       
         BNE   EXIT                                                             
*                                                                               
COML3    DS    0H                                                               
         AHI   R5,-4                                                            
         LA    R4,4(R4)                                                         
*                                                                               
COML4    DS    0H                                                               
         MVC   P+05(L'P-5),SPACES                                               
         LTR   R5,R5                                                            
         BM    EXIT                NO LENGTH                                    
         EX    R5,*+8                                                           
         B     *+10                                                             
         MVC   P+05(0),0(R4)                                                    
         MVI   P,0                                                              
*                                                                               
         CLI   0(R2),X'40'         TEST FROM COMMENT REC                        
         BNE   COML5                                                            
         CLC   QPROG-QRECORD+SAVEQ,=C'73'                                       
         BNE   COML5                                                            
         MVC   P+77(04),=C'COM='                                                
         MVC   P+81(06),KEY+4      COMMENT NUMBER                               
*                                                                               
COML5    DS    0H                                                               
*                                                                               
         LA    RE,P+05                                                          
         ST    RE,FULL                                                          
         BRAS  RE,RPYCOMLN         REPLY COMMENT LINE                           
*                                                                               
         GOTOR AIOPRNT,DMCB,(RC)                                                
*                                                                               
EXIT     XIT1                                                                   
*                                                                               
         LTORG                                                                  
         DROP  RB                                                               
         EJECT                                                                  
*                                                                               
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
*                                                                               
RPYCOMLN NTR1  BASE=*,LABEL=*      REPLY COMMENT LINE                           
*                                                                               
         TM    ADBSW,AS_WEBIO      WEB IO?                                      
         BZ    RPYCML_X            NO                                           
*                                                                               
         TM    ADRPYREC,RPYHEADQ   HEADER SECTION REPLIED?                      
         BZ    RPYCML_X                                                         
         TM    ADRPYREC,RPYDETLQ   DETAIL SECTION REPLIED?                      
         BZ    RPYCML_X                                                         
*                                                                               
         CHI   R5,0                EMPTY ELEM?                                  
         BE    RPYCML_X                                                         
*                                                                               
         CLI   COMWIOSW,C'M'       MATERIAL COMMENT?                            
         BE    RPYCML_X            HANDLED AT INSERTION LEVEL                   
*                                                                               
         L     R3,AWRKREC                                                       
         USING LIOBD,R3            LINKIO INTERFACE BLOCK                       
*                                                                               
         CLC   LIOBPCV1,=X'03030002'                                            
         BL    RPYCML12                                                         
         TM    ADRPYSW3,PRCLWCMQ   PROCESSING LEGAL WARNING COMMENT?            
         BZ    RPYCML12                                                         
         TM    ADRPYSW3,RPYLWCMQ   SUB REC REPLY CODE REPLIED?                  
         BO    RPYCML54                                                         
         GOTOR ALINKIO,DMCB,('LIOAPUT',LIOBD),('LIOTMAP',E#LEGLWC)              
         OI    ADRPYSW3,RPYLWCMQ                                                
         B     RPYCML54                                                         
*                                                                               
RPYCML12 CLC   RUNXCOM#,SPACES     HAVE RUN COMMAND COMMENT NUMBER?             
         JNH   RPYCML48                                                         
         GOTOR ALINKIO,DMCB,('LIOAPUT',LIOBD),('LIOTSRU',M#DLCOM)               
         XC    DUB,DUB                                                          
         MVC   DUB+0(1),KEY+2      MEDIA CODE                                   
         MVC   DUB+1(L'RUNXCOM#),RUNXCOM#                                       
         GOTOR ALINKIO,(R1),('LIOAPUT',LIOBD),('LIOTRAW',001),         +        
               ('LD_CHARQ',DUB),(L'RUNXCOM#+1,0)                                
*                                                                               
         GOTOR ALINKIO,(R1),('LIOAPUT',LIOBD),('LIOTERU',0),0,0                 
*                                                                               
         OI    ADRPYSW3,RUNXCM#Q   RUN COMMAND COMMENT # SENT                   
         OI    ADRPYSW2,RPYVARCQ                                                
         J     RPYCML_X                                                         
*                                                                               
RPYCML48 TM    ADRPYSW2,RPYVARCQ   SUB REC REPLY CODE REPLIED?                  
         BO    RPYCML54                                                         
         GOTOR ALINKIO,DMCB,('LIOAPUT',LIOBD),('LIOTMAP',E#COMVAR)              
         OI    ADRPYSW2,RPYVARCQ                                                
*                                                                               
RPYCML54 L     R1,FULL             ADDRESS OF COMMENT                           
         CLI   0(R1),X'FF'         SKIPPING LINES?                              
         BNE   RPYCML64                                                         
         CLI   1(R1),C'+'                                                       
         BE    *+6                                                              
         DC    H'0'                INVALID FORMAT FOR SKIPPING LINES            
         SR    R4,R4                                                            
         PACK  DUB,2(1,R1)                                                      
         TP    DUB                                                              
         BNZ   *+8                                                              
         CVB   R4,DUB              +0 OR INVALID, PUT ONE LINE ANYWAY           
         MVI   TMPBYTE1,C' '                                                    
*                                                                               
RPYCML56 GOTOR ALINKIO,DMCB,('LIOAPUT',LIOBD),('LIOTRAW',D#COMMNT),    +        
               ('LD_CHARQ',TMPBYTE1),(1,0)                                      
         CHI   R4,0                                                             
         BE    RPYCML_X                                                         
         BCT   R4,RPYCML56                                                      
         B     RPYCML_X                                                         
*                                                                               
RPYCML64 LA    R4,2(R2)            POINT TO COMMENT LINE                        
         SR    RF,RF                                                            
         IC    RF,1(R2)                                                         
         SHI   RF,2                                                             
         CHI   RF,EIOCOMLQ         EXCEEDED MAX LENGTH?                         
         BNH   *+8                                                              
         LA    RF,EIOCOMLQ                                                      
*                                                                               
         GOTOR ALINKIO,DMCB,('LIOAPUT',LIOBD),('LIOTRAW',D#COMMNT),    +        
               ('LD_CHARQ',0(R4)),((RF),0)                                      
*                                                                               
RPYCML_X NI    ADRPYSW3,X'FF'-PRCLWCMQ                                          
         J     EXIT                                                             
*                                                                               
         LTORG                                                                  
         DROP  RB,R3                                                            
         EJECT                                                                  
*                                                                               
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
*                                                                               
COMLIND  DSECT                                                                  
*                                                                               
COMWIOSW DS    C                   C'M' = MATERIAL COMMENT                      
*                                                                               
TMPBYTE1 DS    C                                                                
TMPBYTE2 DS    C                                                                
TMPFULL1 DS    F                                                                
TMPFULL2 DS    F                                                                
*                                                                               
COMLINDX EQU   *                                                                
*                                                                               
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
*                                                                               
         PRINT OFF                                                              
*                                                                               
       ++INCLUDE PPINSWRK1                                                      
         EJECT                                                                  
*                                                                               
       ++INCLUDE POLFILE                                                        
         EJECT                                                                  
*                                                                               
       ++INCLUDE PPINSWRK2                                                      
         EJECT                                                                  
*                                                                               
         PRINT ON                                                               
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'070PPCOMLIN  02/01/10'                                      
         END                                                                    
