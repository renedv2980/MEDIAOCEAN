*          DATA SET ACTMSXFR   AT LEVEL 082 AS OF 12/11/09                      
*CATALP TMSXFR                                                                  
***********************************************************************         
* THIS MODULE IS INCLUDED IN ADDTRNS                                  *         
* IT MUST BE RELINKED IF YOU MAKE ANY CHANGES TO THIS MODULE          *         
***********************************************************************         
         TITLE 'TMS TRANSACTION TRANSFER MODULE'                                
TMSXFR   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 BCWORKX-BCWORKD,*TMSXFR*,RA,RR=R2,CLEAR=YES                      
         USING BCWORKD,RC                                                       
         LR    R9,R1               PARAMETER 1 =A(CONTROL BLOCK)                
         USING TMSXFRD,R9                                                       
         ST    R2,BCRELO                                                        
*                                                                               
         MVI   TMSRETRN,TMSOK                                                   
         MVI   BCSPACES,C' '                                                    
         MVC   BCSPACES+1(L'BCSPACES-1),BCSPACES                                
         LH    R1,=Y(ACTRFST-ACTRECD)                                           
         STH   R1,BCDISP                                                        
         USING COMFACSD,R8                                                      
         ICM   R8,15,TMSCFACS      A(COMFACS)                                   
         BNZ   *+6                                                              
         DC    H'0'                MISSING ADDRESS OF COMFACS                   
*                                                                               
         OC    TMS1RLNQ,TMS1RLNQ   LOOKUP 1R LEDGER STRUCTURE IF                
         BNZ   *+8                 NOT PASSED IN TMSXFR BLOCK                   
         BAS   RE,GETLDG                                                        
*                                                                               
         USING TRNRECD,R4                                                       
         ICM   R4,15,TMSADTRN      R4 = A(TRANSACTION RECORD)                   
         BNZ   *+6                                                              
         DC    H'0'                                                             
         GOTO1 INIT,(R4)           EXTRACT CLUSTER FROM RECORD                  
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   BCKCULA,TRNKCULA    C/U/L/A                                      
         LR    RE,R4                                                            
         AH    RE,BCDISP                                                        
         ST    RE,BCAD44EL         A(44 ELEMENT)                                
         MVC   BCYYMMDD,TRNDATE-TRNEL(RE)  TRANSACTION DATE                     
*                                                                               
TMSX5    LA    RE,TMSX10                                                        
         SR    RF,RF                                                            
         ICM   RF,1,TMSACTN                                                     
         BNZ   *+6                                                              
         DC    H'0'                INVALID ACTION CODE                          
         SLL   RF,2                                                             
         B     *(RF)                                                            
         B     UPDTSN              UPDATE TIMESHEET REVISION NUMBER             
         B     ADDTMS              ADD NEW ITEM TO TIMESHEET                    
*                                                                               
TMSX10   BE    ROUTE                                                            
         BL    ROUTL                                                            
         MVI   TMSRETRN,TMSIERR    *** ERROR - DURING UPDATE ***                
         B     ROUTH                                                            
*                                                                               
ROUTL    MVI   BCDUB,0             SET CC LOW                                   
         B     ROUTCC                                                           
ROUTH    MVI   BCDUB,2             SET CC HIGH                                  
         B     ROUTCC                                                           
ROUTE    MVI   BCDUB,1             SET CC EQUAL                                 
ROUTCC   CLI   BCDUB,1                                                          
*                                                                               
ROUTX    XIT1                      EXIT WITH CC SET                             
         EJECT                                                                  
***********************************************************************         
* GET 1R LEDGER STRUCTURE                                             *         
*                                                                     *         
* NTRY - TMSADTRN = A(TRANSACTION RECORD)                             *         
*                                                                     *         
* EXIT - TMS1RLNQ = LEDGER STRUCTURE (EX/ 1, 4, 7, 12)                *         
***********************************************************************         
         SPACE 1                                                                
GETLDG   NTR1                                                                   
         USING TRNRECD,R4                                                       
         ICM   R4,15,TMSADTRN      R4 = A(TRANSACTION RECORD)                   
         BNZ   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         USING ACTRECD,R6                                                       
         LA    R6,BCKEY            *** READ FOR LEDGER RECORD ***               
         XC    BCKEY,BCKEY                                                      
         MVC   ACTKEY,BCSPACES                                                  
         MVC   ACTKEY(3),TRNKCULA  CPY/1R                                       
         MVC   BCKEYSAV,BCKEY                                                   
         GOTO1 TMSDMGR,BCDMCB,DMRDHI,ACCDIR,BCKEY,BCKEY,0                       
         CLC   ACTKEY,BCKEYSAV                                                  
         BE    *+6                                                              
         DC    H'0'                MISSING 1R LEDGER RECORD                     
*                                                                               
         LA    R6,BCKEY                                                         
         LA    R3,ACTKDA                                                        
         GOTO1 TMSDMGR,BCDMCB,GETREC,ACCMST,(R3),BCIO1,BCWORK                   
         CLI   BCDMCB+8,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    R6,BCIO1                                                         
         USING ACLELD,R6                                                        
         MVI   BCELCODE,ACLELQ     X'16' - HIERARCHY ELEMENT                    
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   TMS1RLQ1,ACLVALS                                                 
         MVC   TMS1RLQ2,ACLVALS+(L'ACLVALS*1)                                   
         MVC   TMS1RLQ3,ACLVALS+(L'ACLVALS*2)                                   
         MVC   TMS1RLQ4,ACLVALS+(L'ACLVALS*3)                                   
         B     ROUTE                                                            
         EJECT                                                                  
***********************************************************************         
* INITIALIZE TIME CLUSTER AREA                                        *         
*                                                                     *         
* NTRY - R1 = A(TRANSACTION RECORD)                                   *         
*                                                                     *         
* EXIT - CC=NEQ   = ERROR - NO TIME CLUSTER FOUND                     *         
*        CC=EQU   = BCCLUSTR = EXTRACTED 8B TIME CLUSTER              *         
*                   BCCLUSTQ = LENGTH TIME CLUSTER                    *         
***********************************************************************         
         SPACE 1                                                                
INIT     NTR1                                                                   
         USING TIMELD,R6                                                        
         LR    R6,R1               R1 = A(TRANSACTION RECORD)                   
         LA    R3,BCCLUSTR                                                      
         XC    BCCLUSTQ,BCCLUSTQ                                                
*                                                                               
         LA    R0,BCCLUSTR                                                      
         LH    R1,=Y(L'BCCLUSTR)                                                
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         AH    R6,BCDISP           COPY 8B CLUSTER                              
INIT10   CLI   0(R6),0                                                          
         BE    INITX                                                            
         CLI   0(R6),TIMELQ        X'8B' ELEMENT                                
         BNE   INIT20                                                           
         SR    R1,R1                                                            
         IC    R1,1(R6)                                                         
         SH    R1,=H'1'                                                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),0(R6)                                                    
         LA    R3,1(R1,R3)                                                      
         SR    RE,RE                                                            
         ICM   RE,3,BCCLUSTQ       INCREMENT CLUSTER LENGTH                     
         LA    RE,1(R1,RE)                                                      
         STCM  RE,3,BCCLUSTQ                                                    
*                                                                               
INIT20   SR    R1,R1                                                            
         IC    R1,1(R6)                                                         
         AR    R6,R1                                                            
         B     INIT10                                                           
*                                                                               
INITX    OC    BCCLUSTQ,BCCLUSTQ                                                
         BZ    ROUTH               NO TIME ELEMENTS ON THIS RECORD              
         B     ROUTE                                                            
         EJECT                                                                  
***********************************************************************         
* UPDATE TIMESHEET/REVISION # ON 1R ACCOUNT RECORD                    *         
*                                                                     *         
* EXIT - CC=NEQ   = ERROR                                             *         
*        CC=EQU   = BCRVSN# = NEXT REVISION # TO INSERT INTO ITEM     *         
***********************************************************************         
         SPACE 1                                                                
UPDTSN   NTR1                                                                   
         USING TRNRECD,R4                                                       
         ICM   R4,15,TMSADTRN      R4 = A(TRANSACTION RECORD)                   
         BNZ   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         USING ACTRECD,R6                                                       
         LA    R6,BCKEY            *** READ FOR ACCOUNT RECORD ***              
         XC    BCKEY,BCKEY                                                      
         MVC   ACTKEY,BCSPACES                                                  
         MVC   ACTKCULA,TRNKCULA   1R ACCOUNT                                   
         MVC   BCKEYSAV,BCKEY                                                   
         GOTO1 TMSDMGR,BCDMCB,(X'80',DMRDHI),ACCDIR,BCKEY,BCKEY,0               
         CLC   ACTKEY,BCKEYSAV                                                  
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    R6,BCKEY                                                         
         LA    R3,ACTKDA                                                        
         GOTO1 TMSDMGR,BCDMCB,(X'80',GETREC),ACCMST,(R3),BCIO1,BCWORK           
         CLI   BCDMCB+8,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    R6,BCIO1                                                         
         USING RSTELD,R6                                                        
         MVI   BCELCODE,RSTELQ       X'30' ELEMENT                              
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         CLI   RSTLN,RSTLN2Q                                                    
         BNL   *+6                                                              
         DC    H'0'                CANT UPDATE SMALL ELEMENTS                   
*                                                                               
         SR    R1,R1                                                            
         ICM   R1,3,RSTSNUM                                                     
         LA    R1,1(R1)                                                         
         STCM  R1,3,BCRVSN#        NEXT TS #                                    
         LA    R1,1(R1)                                                         
         STCM  R1,3,RSTSNUM        SET NEXT TS #                                
*                                                                               
         USING ACTRECD,R6                                                       
         LA    R6,BCKEY                                                         
         LA    R3,ACTKDA                                                        
         GOTO1 TMSDMGR,BCDMCB,PUTREC,ACCMST,(R3),BCIO1,BCWORK                   
         CLI   BCDMCB+8,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
         B     ROUTE                                                            
         EJECT                                                                  
***********************************************************************         
* GET HIGHEST SEQUENCE NUMBER - NEXT NUMBER                           *         
*                                                                     *         
* NTRY - 1R ACCCOUNT (C/U/L/A)                                        *         
*                                                                     *         
* EXIT - BCSEQ#   = HIGHEST SEQUENCE NUMBER                           *         
***********************************************************************         
         SPACE 1                                                                
HISEQ#   NTR1                                                                   
         XC    BCSEQ#,BCSEQ#       SEQUENCE NUMBER                              
*                                                                               
         USING TSWRECD,R6          READ TIMESHEET WEEKLY PASSIVE PTR            
         LA    R6,BCKEY                                                         
         XC    BCKEY,BCKEY                                                      
         MVI   TSWKTYP,TSWKTYPQ    X'3E'                                        
         MVI   TSWKSUB,TSWKSUBQ    X'0F'                                        
         MVC   TSWKCPY,BCKCPY      COMPANY CODE                                 
         LA    RF,BCKACT                                                        
         SR    R1,R1                                                            
         IC    R1,TMS1RLQ3                                                      
         SH    R1,=H'1'                                                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   TSWKODS(0),0(RF)    ISOLATE OFF/DPT/SUB                          
         OC    TSWKODS,BCSPACES                                                 
         LA    RF,1(R1,RF)                                                      
         IC    R1,TMS1RLQ4                                                      
         SR    RE,RE                                                            
         IC    RE,TMS1RLQ3                                                      
         SR    R1,RE                                                            
         SH    R1,=H'1'                                                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   TSWKPER(0),0(RF)    ISOLATE PERSON CODE                          
         OC    TSWKPER,BCSPACES                                                 
         ICM   R1,7,BCYYMMDD                                                    
         LNR   R1,R1                                                            
         STCM  R1,7,TSWKEND        WEEK ENDING DATE                             
         MVC   BCCOMMND,DMRDHI                                                  
         B     HISEQ15                                                          
*                                                                               
         USING TSWRECD,R6                                                       
HISEQ10  LA    R6,BCKEY                                                         
         SR    R1,R1                                                            
         IC    R1,TSWKSBR          BUMP SUBREFERENCE NUMBER                     
         LA    R1,1(R1)                                                         
         STC   R1,TSWKSBR                                                       
         MVC   BCCOMMND,DMRSEQ                                                  
*                                                                               
HISEQ15  MVC   BCKEYSAV,BCKEY                                                   
         GOTO1 TMSDMGR,BCDMCB,BCCOMMND,ACCDIR,BCKEY,BCKEY,0                     
         CLC   TSWKEY(TSWKODS+L'TSWKODS-TSWKEY),BCKEYSAV                        
         BNE   ROUTE                                                            
*                                                                               
         USING TSWRECD,R6                                                       
         LA    R6,BCKEY                                                         
         LA    R3,TSWKDA                                                        
         GOTO1 TMSDMGR,BCDMCB,GETREC,ACCMST,(R3),BCIO1,BCWORK                   
         CLI   BCDMCB+8,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         USING TIMELD,R6                                                        
         LA    R6,BCIO1                                                         
         AH    R6,BCDISP                                                        
HISEQ20  CLI   0(R6),0                                                          
         BE    HISEQ10                                                          
         CLI   0(R6),TIMELQ        X'8B' ELEMNT                                 
         BNE   *+12                                                             
         CLI   TIMETYP,TIMEINP     INPUT DETAIL ELEMENT                         
         BE    HISEQ40                                                          
HISEQ30  SR    R1,R1                                                            
         IC    R1,1(R6)                                                         
         AR    R6,R1                                                            
         B     HISEQ20                                                          
*                                                                               
HISEQ40  CLC   BCSEQ#,TIMSEQ       FIND HIGHEST SEQUENCE NUMBER                 
         BH    HISEQ30                                                          
         MVC   BCSEQ#,TIMSEQ                                                    
         B     HISEQ30                                                          
         EJECT                                                                  
**********************************************************************          
* ADD 8B CLUSTER TO TMS RECORD                                       *          
**********************************************************************          
         SPACE 1                                                                
ADDTMS   NTR1                                                                   
         BAS   RE,UPDTSN           INCREMENT TS REVISION #                      
         BAS   RE,HISEQ#           GET HIGHEST SEQ/SBR NUMBER                   
*                                                                               
         SR    R1,R1               UP THE SEQUENCE #                            
         IC    R1,BCSEQ#                                                        
         LA    R1,1(0,R1)                                                       
         STC   R1,BCSEQ#                                                        
*                                                                               
         USING TIMELD,R6                                                        
         LA    R6,BCCLUSTR                                                      
         MVC   TIMSEQ,BCSEQ#       SEQUENCE NUMBER                              
         MVC   TIMLINE#,BCRVSN#    NEXT LINE NUMBER                             
         DROP  R6                                                               
*                                                                               
         LA    R0,BCIO1                                                         
         LH    R1,=Y(L'BCIO1)                                                   
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         USING TRNRECD,R4                                                       
         ICM   R4,15,TMSADTRN      R4 = A(TRANSACTION RECORD)                   
*                                                                               
         USING TIMRECD,R5                                                       
         LA    R5,BCKEY            R5 = ACTIVE TIME KEY                         
         XC    BCKEY,BCKEY                                                      
         MVC   TIMKEY,TRNKEY                                                    
         MVC   TIMKREF,=C'*TIME*'                                               
         MVI   TIMKSBR,0                                                        
*                                                                               
ADD50    MVC   BCKEYSAV,BCKEY                                                   
         GOTO1 TMSDMGR,BCDMCB,(X'80',DMRDHI),ACCDIR,BCKEY,BCKEY,0               
         CLC   TIMKEY(TIMKREF+L'TIMKREF-TIMKEY),BCKEYSAV                        
         BNE   ADD150                                                           
*                                                                               
         MVC   BCDA,TIMKDA                                                      
*                                                                               
         LA    R0,BCIO1            CLEAR BUFFER BEFORE READING INTO IT          
         LH    R1,=Y(L'BCIO1)                                                   
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         GOTO1 TMSDMGR,BCDMCB,(X'80',GETREC),ACCMST,BCDA,BCIO1,BCWORK           
         CLI   BCDMCB+8,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
         BAS   RE,ADDCLST          ADD CLUSTER TO END OF RECORD                 
         BE    ADD100                                                           
         XC    BCKEY,BCKEY                                                      
         MVC   TIMKEY,BCKEYSAV                                                  
         SR    R1,R1                                                            
         IC    R1,TIMKSBR                                                       
         LA    R1,1(R1)                                                         
         STC   R1,TIMKSBR                                                       
         B     ADD50                                                            
*                                                                               
ADD100   GOTO1 TMSDMGR,BCDMCB,PUTREC,ACCMST,BCDA,BCIO1,BCWORK                   
         CLI   BCDMCB+8,0                                                       
         BE    ADD200                                                           
         DC    H'0'                                                             
*                                                                               
ADD150   DS    0H                  *** ADD NEW TIME RECORD ***                  
         LA    R0,BCIO1                                                         
         LH    R1,=Y(L'BCIO1)                                                   
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         USING TIMRECD,R5                                                       
         LA    R5,BCIO1                                                         
         MVC   TIMKEY,BCKEYSAV                                                  
         MVI   TIMRRI1,TIMKRI1Q                                                 
         MVI   TIMRRI2,TIMKRI2Q                                                 
         MVI   TIMRRI3,TIMKRI3Q                                                 
         MVI   TIMRRI4,TIMKRI4Q                                                 
         MVC   TIMRLMOS,=X'FFFF'                                                
         XC    TIMRHMOS,TIMRHMOS                                                
         LH    R1,=Y(TIMRFST-TIMKEY)                                            
         LA    R1,1(R1)                                                         
         STCM  R1,3,TIMRLEN                                                     
         BAS   RE,ADDPID           ADD PIDEL                                    
         BAS   RE,ADDCLST          ADD CLUSTER TO END OF RECORD                 
         BE    *+6                                                              
         DC    H'0'                                                             
         GOTO1 TMSDMGR,BCDMCB,ADDREC,ACCMST,BCDA,BCIO1,BCWORK                   
         CLI   BCDMCB+8,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         USING TIMRECD,R5                                                       
         LA    R5,BCIO1            R5 = A(ACTIVE KEY)                           
         USING TSWRECD,R6                                                       
         LA    R6,BCPASPTR         R6 = A(PASSIVE POINTER KEY)                  
         XC    BCPASPTR,BCPASPTR                                                
         MVI   TSWKTYP,TSWKTYPQ    X'3E'                                        
         MVI   TSWKSUB,TSWKSUBQ    X'0F'                                        
         MVC   TSWKCPY,TIMKCPY     COMPANY CODE                                 
         LA    RF,TIMKACT                                                       
         SR    R1,R1                                                            
         IC    R1,TMS1RLQ3                                                      
         SH    R1,=H'1'                                                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   TSWKODS(0),0(RF)    ISOLATE OFF/DPT/SUB                          
         OC    TSWKODS,BCSPACES                                                 
         LA    RF,1(R1,RF)                                                      
         IC    R1,TMS1RLQ4                                                      
         SR    RE,RE                                                            
         IC    RE,TMS1RLQ3                                                      
         SR    R1,RE                                                            
         SH    R1,=H'1'                                                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   TSWKPER(0),0(RF)    ISOLATE PERSON CODE                          
         OC    TSWKPER,BCSPACES                                                 
         ICM   R1,7,TIMKPEDT                                                    
         LNR   R1,R1                                                            
         STCM  R1,7,TSWKEND        WEEK ENDING DATE                             
         MVC   TSWKULC,TIMKULC     CONTRA ACCOUNT                               
         MVC   TSWKCOFC,TIMKOFF    CLIENT OFFICE                                
         MVC   TSWKSBR,TIMKSBR     SUBREFERENCE                                 
         MVI   TSWKRI1,TIMKRI1Q                                                 
         MVI   TSWKRI2,TIMKRI2Q                                                 
         MVI   TSWKRI3,TIMKRI3Q                                                 
         MVI   TSWKRI4,TIMKRI4Q                                                 
         MVC   TSWKDA,BCDA                                                      
         GOTO1 TMSDMGR,BCDMCB,DMADD,ACCDIR,BCPASPTR,BCPASPTR,0                  
         CLI   BCDMCB+8,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         USING ACTRECD,R5                                                       
ADD200   TM    TMSINDS,TMSILIVE    TEST DRAFT TO LIVE                           
         BNO   ADDX                                                             
         LA    R5,BCKEY                                                         
         XC    BCKEY,BCKEY                                                      
         MVC   ACTKEY,TRNKEY                                                    
         GOTO1 TMSDMGR,BCDMCB,DMREAD,ACCDIR,BCKEY,BCKEY,0                       
         CLI   BCDMCB+8,0                                                       
         BE    *+6                                                              
         DC    H'0'                MUST BE THERE                                
*                                                                               
         OI    ACTKSTAT,ACTSDELT                                                
         MVC   BCDA,ACTKDA                                                      
         GOTO1 TMSDMGR,BCDMCB,DMWRT,ACCDIR,BCKEY,BCKEY                          
*                                                                               
         GOTO1 TMSDMGR,BCDMCB,(X'80',GETREC),ACCMST,BCDA,BCIO1,BCWORK           
         CLI   BCDMCB+8,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    R5,BCIO1                                                         
         OI    ACTRSTAT,ACTSDELT                                                
         GOTO1 TMSDMGR,BCDMCB,PUTREC,ACCMST,BCDA,BCIO1,BCWORK                   
         CLI   BCDMCB+8,0                                                       
         BE    ADDX                                                             
         DC    H'0'                                                             
*                                                                               
ADDX     B     ROUTE                                                            
         EJECT                                                                  
***********************************************************************         
* ADD ELEMENT CLUSTER TO RECORD IN AIOBC                              *         
*                                                                     *         
* EXIT - CC=EQU - CLUSTER ADDED CORRECTLY                             *         
*        CC=NEQ - CLUSTER DOESNT FIT ON THIS RECORD                   *         
***********************************************************************         
         SPACE 1                                                                
ADDCLST  NTR1                                                                   
         USING TIMRECD,R5                                                       
         LA    R5,BCIO1                                                         
         USING TIMELD,R6                                                        
         LA    R6,BCCLUSTR         R0=A(8B ELEM CLUSTER) SOURCE                 
         MVC   TIMMOA,TMSMOA                                                    
         CLC   TIMMOA,TIMRLMOS                                                  
         BH    *+10                                                             
         MVC   TIMRLMOS,TIMMOA                                                  
         CLC   TIMMOA,TIMRHMOS                                                  
         BL    *+10                                                             
         MVC   TIMRHMOS,TIMMOA                                                  
*                                                                               
         LA    RE,BCIO1                                                         
         SR    RF,RF                                                            
         ICM   RF,3,ACCORLEN(RE)   RF=CURRENT LENGTH OF RECORD                  
         AH    RF,BCCLUSTQ         +LENGTH OF ELEMENT CLUSTER                   
         CH    RF,=Y(L'BCIO1-50)   IF NEW RECORD > 1950 BYTES                   
         BH    ROUTH               THEN DONT ADD IT TO THIS RECORD              
*                                                                               
         LA    RE,BCIO1            *** ATTATCH ELEMENT CLUSTER ***              
         SR    RF,RF                                                            
         ICM   RF,3,ACCORLEN(RE)                                                
         SH    RF,=H'1'                                                         
         AR    RE,RF               RE=A(AREA TO ATTATCH CLUSTER)                
         LA    R0,BCCLUSTR         R0=A(8B ELEM CLUSTER) SOURCE                 
         LH    R1,BCCLUSTQ         R1=L'8B ELEMENT CLUSTER                      
         LH    RF,BCCLUSTQ         RF=L'8B ELEMENT CLUSTER                      
         MVCL  RE,R0               CLUSTER -> END OF RECORD                     
*                                                                               
         LA    RE,BCIO1            *** UPDATE RECORD LENGTH ***                 
         SR    RF,RF                                                            
         ICM   RF,3,ACCORLEN(RE)                                                
         AH    RF,BCCLUSTQ                                                      
         STCM  RF,3,ACCORLEN(RE)                                                
         B     ROUTE                                                            
         EJECT                                                                  
***********************************************************************         
* READ PERSON RECORD AND GET PIDEL. ATTACH PIDEL TO NEW TIME RECORD   *         
***********************************************************************         
         SPACE 1                                                                
         USING PERRECD,R6                                                       
ADDPID   NTR1                                                                   
         LA    R6,BCKEY                                                         
         MVC   BCKEY,BCSPACES                                                   
         MVI   PERKTYP,PERKTYPQ    X'0F'                                        
         MVC   PERKCPY,BCKCPY      COMPANY CODE                                 
*                                                                               
         LA    RF,BCKACT                                                        
         SR    R1,R1                                                            
         IC    R1,TMS1RLQ3                                                      
         LA    RF,0(R1,RF)         POINT TO PERSON CODE                         
         IC    R1,TMS1RLQ4                                                      
         SR    RE,RE                                                            
         IC    RE,TMS1RLQ3                                                      
         SR    R1,RE                                                            
         SH    R1,=H'1'                                                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   PERKCODE(0),0(RF)   ISOLATE PERSON CODE                          
         OC    PERKCODE,BCSPACES                                                
*                                                                               
         MVC   BCKEYSAV,BCKEY                                                   
         GOTO1 TMSDMGR,BCDMCB,DMRDHI,ACCDIR,BCKEY,BCKEY,0                       
         CLC   PERKEY,BCKEYSAV                                                  
         BE    *+6                                                              
         DC    H'0'                MISSING PERSON RECORD                        
*                                                                               
         USING ACTRECD,R6                                                       
         LA    R6,BCKEY                                                         
         LA    R3,ACTKDA                                                        
         GOTO1 TMSDMGR,BCDMCB,GETREC,ACCMST,(R3),BCIO2,BCWORK                   
         CLI   BCDMCB+8,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    R6,BCIO2                                                         
         USING PIDELD,R6                                                        
         MVI   BCELCODE,PIDELQ     X'D8' - PERSON ID ELEMENT                    
         BAS   RE,GETEL                                                         
         BNE   ADDPIDX                                                          
*                                                                               
         LA    RE,BCIO1            *** ATTATCH ELEMENT CLUSTER ***              
         SR    RF,RF                                                            
         ICM   RF,3,ACCORLEN(RE)                                                
         SH    RF,=H'1'                                                         
         AR    RE,RF               RE=A(AREA TO ATTATCH CLUSTER)                
*                                                                               
         SR    R1,R1                                                            
         IC    R1,PIDLN                                                         
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,RE),PIDEL       SAVE THE ELEMENT                             
         LA    R1,1(R1)            BUMP UP LENGTH                               
*                                                                               
         LA    RE,BCIO1            *** UPDATE RECORD LENGTH ***                 
         SR    RF,RF                                                            
         ICM   RF,3,ACCORLEN(RE)                                                
         AR    RF,R1                                                            
         STCM  RF,3,ACCORLEN(RE)                                                
*                                                                               
ADDPIDX  B     ROUTE                                                            
         EJECT                                                                  
***********************************************************************         
* GETEL                                                               *         
***********************************************************************         
         SPACE 1                                                                
         GETEL R6,BCDISP,BCELCODE                                               
         SPACE 3                                                                
***********************************************************************         
* LITERALS                                                            *         
***********************************************************************         
         SPACE 1                                                                
ACCDIR   DC    CL8'ACCDIR'                                                      
ACCMST   DC    CL8'ACCMST'                                                      
DMRDHI   DC    CL8'DMRDHI'                                                      
DMREAD   DC    CL8'DMREAD'                                                      
DMRSEQ   DC    CL8'DMRSEQ'                                                      
GETREC   DC    CL8'GETREC'                                                      
PUTREC   DC    CL8'PUTREC'                                                      
ADDREC   DC    CL8'ADDREC'                                                      
DMADD    DC    CL8'DMADD'                                                       
DMWRT    DC    CL8'DMWRT'                                                       
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* WORKING STORAGE                                                     *         
***********************************************************************         
         SPACE 1                                                                
BCWORKD  DSECT                                                                  
BCDUB    DS    D                                                                
BCWORK   DS    16D                                                              
BCWORK2  DS    16D                                                              
BCDMCB   DS    6F                                                               
BCRELO   DS    A                                                                
BCADTRNS DS    A                   A(TRANSACTION RECORD)                        
BCAD44EL DS    A                   A(44 ELEMENT)                                
BCDA     DS    F                   TEMPORARY DISK ADDRESS AREA                  
         DS    4A                  N/D                                          
BCHALF   DS    H                                                                
BCDISP   DS    H                   DISPLACEMENT TO FIRST ELEMENT                
BCCOMMND DS    CL8                                                              
BCBYTE   DS    XL1                                                              
*                                                                               
BCKCULA  DS    0XL15               C/U/L/ACCOUNT CODE                           
BCKCPY   DS    XL1                 COMPANY CODE                                 
BCKULA   DS    0CL14                                                            
BCKUNT   DS    CL1                 UNIT                                         
BCKLDG   DS    CL1                 LEDGER                                       
BCKACT   DS    CL12                ACCOUNT CODE                                 
BCYYMMDD DS    PL3                 TRANSACTION DATE                             
BCELCODE DS    XL1                                                              
BCRVSN#  DS    XL2                 REVISION NUMBER                              
BCSEQ#   DS    XL1                 SEQUENCE NUMBER                              
BCSBR#   DS    XL1                 SUBREFERENCE NUMBER                          
BCELEM   DS    CL255                                                            
BCKEY    DS    CL56                                                             
BCKEYSAV DS    CL56                                                             
BCPASPTR DS    CL56                                                             
BCSPACES DS    CL132                                                            
BCCLUSTQ DS    H                   CLUSTER LENGTH                               
BCCLUSTR DS    CL500               CLUSTER BUFFER AREA                          
BCIO1    DS    CL2000              IO AREA - 2K                                 
BCIO2    DS    CL2000              IO AREA - 2K                                 
BCWORKX  EQU   *                                                                
         EJECT                                                                  
***********************************************************************         
* ++INCLUDES                                                          *         
***********************************************************************         
         SPACE 1                                                                
       ++INCLUDE ACTMSXFRD                                                      
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
       ++INCLUDE ACGENFILE                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'082ACTMSXFR  12/11/09'                                      
         END                                                                    
