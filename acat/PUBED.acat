*          DATA SET PUBED      AT LEVEL 011 AS OF 11/30/06                      
*CATALP PUBED                                                                   
         TITLE 'PUBED - FORMAT PACKED PUB CODE FOR DISPLAY'                     
*                                                                               
*                                                                               
*        THIS MODULE WILL FORMAT A PACKED PUB CODE                              
*        (4 BYTES PUB-PWOS,1 BYTE ZONE-PWOS,1 ALPHA EDITION)                    
*        TO NNNNNNNN-NN-AAA                                                     
*                                                                               
*        PARAMS                                                                 
*                 1   BYTE    0  NOT USED                                       
*                           1-3  A(INPUT)                                       
*                 2           0  LENGTH OF OUTPUT - SET BY PUBED                
*                           1-3  A(OUTPUT)                                      
*                                                                               
*                                                                               
PUBED    CSECT                                                                  
         NMOD1 3,**PBED**                                                       
         USING PUBEDWD,RC                                                       
         LM    R2,R3,0(R1)         R2=INPUT                                     
*                                  R3=OUTPUT                                    
         EJECT                                                                  
*                                  PUB CODE                                     
         UNPK  WORK(9),0(5,R2)                                                  
         MVC   0(8,R3),WORK                                                     
         LA    R6,8                SET LENGTH                                   
PB3      DS    0H                                                               
         CLI   0(R3),C'0'                                                       
         BNE   PB3B                                                             
         MVC   0(7,R3),1(R3)                                                    
         MVI   7(R3),C' '                                                       
         BCTR  R6,R0                                                            
         B     PB3                                                              
PB3B     DS    0H                                                               
         LA    R5,0(R3,R6)                                                      
         CLI   4(R2),0             NO ZONE                                      
         BE    PB4                                                              
         MVI   0(R5),C','                                                       
         BAS   RE,EDZON                                                         
         MVC   1(2,R5),WORK                                                     
         LA    R5,3(R5)                                                         
         LA    R6,3(R6)                                                         
*                                  EDITION                                      
PB4      CLI   5(R2),0             NO EDIT                                      
         BE    EXIT                                                             
         LA    R7,EDTABLE                                                       
         SPACE 2                                                                
PB6      DS    0H                                                               
         CLC   0(3,R7),=C'END'                                                  
         BE    EXIT                                                             
         CLC   0(1,R7),5(R2)                                                    
         BE    PB7                                                              
         LA    R7,4(R7)                                                         
         B     PB6                                                              
*                                                                               
PB7      DS    0H                                                               
         MVI   0(R5),C','                                                       
         MVC   1(3,R5),1(R7)                                                    
         LA    R6,4(R6)                                                         
         SPACE 3                                                                
EXIT     EQU   *                                                                
         STC   R6,4(R1)            SET OUTPUT LENGTH                            
         XMOD1 1                                                                
         EJECT                                                                  
EDZON    DS    0H                                                               
*                                  'HEXOUT'                                     
         SR    RF,RF                                                            
         MVC   BYTE,4(R2)                                                       
         NI    BYTE,X'0F'                                                       
         IC    RF,BYTE                                                          
         LA    RF,183(RF)          A-F                                          
         CLI   BYTE,10                                                          
         BNL   *+8                                                              
         LA    RF,57(RF)           0-9                                          
         STC   RF,WORK+1                                                        
         IC    RF,4(R2)                                                         
         SRL   RF,4                                                             
         LA    RF,183(RF)          A-F                                          
         CLI   4(R2),160                                                        
         BNL   *+8                                                              
         LA    RF,57(RF)           0-9                                          
         STC   RF,WORK                                                          
         BR    RE                                                               
         EJECT                                                                  
*          DATA SET PUBEDIT    AT LEVEL 020 AS OF 01/04/89                      
EDTABLE  DS    0C                                                               
         DC    CL4'AM  '   MORNING                                              
         DC    CL4'BE  '   EVENING                                              
         DC    CL4'CD  '   DAILY                                                
         DC    CL4'DME '   MORN/EVE                                             
         DC    CL4'ESAM'   SAT MORN                                             
         DC    CL4'FSAE'   SAT EVE                                              
         DC    CL4'GSD '   SAT DAILY                                            
         DC    CL4'HSME'   SAT M/E                                              
         DC    CL4'ISU '   SUNDAY                                               
         DC    CL4'JOM '   MORN COMBO                                           
         DC    CL4'KOE '   EVE COMBO                                            
         DC    CL4'LOD '   DAILY COMBO                                          
         DC    CL4'MOME'   M/E COMBO                                            
         DC    CL4'NPR '   PRINTED                                              
         DC    CL4'PP  '   PAINTED                                              
         DC    CL4'RR  '   ROTARY                                               
         DC    CL4'SS  '   SUNDAY                                               
         DC    CL4'TT  '   TRANSIT                                              
         DC    CL4'UMON'   MONTHLY                                              
         DC    CL4'WW  '   WEEKLY                                               
         DC    CL4'XPM '   PERMANENT                                            
         DC    CL4'END '           EOL                                          
         SPACE 3                                                                
PUBEDWD  DSECT                                                                  
BYTE     DS    CL1                                                              
WORK     DS    2D                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'011PUBED     11/30/06'                                      
         END                                                                    
