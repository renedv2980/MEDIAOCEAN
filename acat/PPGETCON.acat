*          DATA SET PPGETCON   AT LEVEL 028 AS OF 12/18/02                      
*CATALP PPGETCON                                                                
*               PPGETCON - FIND CONTRACT FOR BUY RECORD'                        
         TITLE 'PPGETCON - FIND CONTRACT FOR BUY RECORD - HISTORY'              
***********************************************************************         
*                                                                     *         
*        HISTORY OF CHANGES                                           *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
* BOBY    10/29/02 BIG BANG FOR THIS MODULE                                     
*                                                                               
         TITLE 'PPGETCON - FIND CONTRACT FOR BUY RECORD - PARMS'                
***********************************************************************         
*                                                                     *         
*        FIND CONTRACT FOR A BUY RECORD                               *         
*                                                                     *         
*        FOR OFF-LINE USE ONLY                                        *         
*                                                                     *         
*        PARAMETER LIST                                               *         
*                                                                     *         
*        PARM0  - A(GETCONCB) - A(CONTROL BLOCK)                      *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
         TITLE 'PPGETCON - FIND CONTRACT FOR BUY RECORD - INIT'                 
***********************************************************************         
*                                                                     *         
*        INITIALIZATION                                               *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
PPGETCON CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 GETCONWL,GETCON,CLEAR=YES                                        
*                                                                               
         USING GETCONWD,RC         RC = A(LOCAL WORKING STORAGE)                
*                                                                               
         L     RA,0(R1)            ESTABLISH GETCOND CONTROL BLOCK              
         USING GETCOND,RA                                                       
*                                                                               
         L     RF,GCNVUTL          GET SE NUMBER                                
         MVC   GCNSE,4(RF)                                                      
*                                                                               
         L     RF,GCNCOMFA         GET DATAMGR ADDRESS                          
         MVC   VDATAMGR,CDATAMGR-COMFACSD(RF)                                   
*                                                                               
         L     R3,=A(IOA1)         LOCAL AREA FOR READING                       
         ST    R3,WRKCONA          CONTRACTS                                    
*                                                                               
         OC    GCNCONA,GCNCONA     IF NO CONTRACT RECORD PASSED                 
         BNZ   *+10                                                             
         MVC   GCNCONA,WRKCONA        RETURN LOCAL A(IOAREA)                    
*                                                                               
         LA    R3,WRKIOA2          LOCAL AREA FOR READING                       
         ST    R3,WRKCLTA             CLIENT RECORDS                            
*                                                                               
         TITLE 'PPGETCON - FIND CONTRACT FOR BUY RECORD - FNDCON'               
***********************************************************************         
*                                                                     *         
*        FIND CONTRACT                                                *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
         DS    0D                  ALIGNMENT                                    
FNDCON   DS    0H                                                               
*                                                                               
         ICM   R2,15,GCNBUYA       ESTABLISH BUYREC                             
         BZ    FCNNBUY               ERROR IF NO BUY RECORD                     
         USING PBUYREC,R2                                                       
*                                                                               
*        CHECK IF NEW CLIENT INFO NEEDED.                                       
*                                                                               
         SR    R4,R4               INIT CLTREC POINTER                          
*                                                                               
         CLC   GCNAGY,PBUYKAGY     OKAY IF AGENCY UNCHANGED                     
         BNE   *+10                                                             
         CLC   GCNMED,PBUYKMED     OKAY IF MEDIA UNCHANGED                      
         BNE   *+10                                                             
         CLC   GCNCLT,PBUYKCLT     OKAY IF CLIENT UNCHANGED                     
         BE    FCNCLTOK                                                         
*                                                                               
*        READ CLIENT HEADER                                                     
*                                                                               
FCNCLGT  DS    0H                                                               
*                                                                               
         XC    GCNCURR(GCNCURRL),GCNCURR     INIT CURRENT VALUES                
*                                                                               
         XC    KEY,KEY             INIT KEY AREA                                
         LA    R4,KEY                                                           
         USING PCLTKEY,R4          ESTABLISH CLIENT KEY                         
*                                                                               
         MVC   PCLTKAGY,PBUYKAGY   SET AGENCY                                   
         MVC   PCLTKMED,PBUYKMED   SET MEDIA                                    
         MVI   PCLTKRCD,PCLTKIDQ   SET RECORD ID                                
         MVC   PCLTKCLT,PBUYKCLT   SET CLIENT CODE                              
*                                                                               
         ICM   RF,15,GCNCLTA       POINT TO PASSED CLIENT RECORD                
         BZ    FCNCLGT1            NONE GIVEN                                   
*                                                                               
         CLC   PCLTKEY,0(RF)       IF WE HAVE CLIENT IN CORE                    
         BNE   *+10                                                             
         LR    R4,RF                  POINT TO PASSED CLIENT RECORD             
         B     FCNCLGTX                                                         
*                                                                               
FCNCLGT1 DS    0H                                                               
*                                                                               
         MVC   KEYSAVE,KEY         SAVE STARTING KEY                            
*                                                                               
*        READ POINTER FOR CLIENT RECORD                                         
*                                                                               
         GOTO1 VDATAMGR,DMCB,DMRDHI,PRTDIR,KEY,KEY                              
*                                                                               
         CLC   PCLTKEY,KEYSAVE      MUST FIND CLIENT                            
         BNE   FCNNCLT                                                          
*                                                                               
*        GET CLIENT RECORD                                                      
*                                                                               
         GOTO1 VDATAMGR,DMCB,GETREC,PRTFILE,KEY+27,WRKCLTA,DMWORK               
*                                                                               
         L     R4,WRKCLTA          POINT TO FOUND CLIENT RECORD                 
*                                                                               
FCNCLGTX DS    0H                                                               
*                                                                               
         MVC   GCNAGY,PCLTKAGY     SAVE AGENCY                                  
         MVC   GCNMED,PCLTKMED     SAVE MEDIA                                   
         MVC   GCNCLT,PCLTKCLT     SAVE CLIENT                                  
*                                                                               
         CLI   PCLTPROF+5,C'2'     IF THIS IS A SLAVE CLIENT                    
         BNE   *+10                                                             
         MVC   GCNMSCLT,PCLTPROF+6    SAVE MASTER CLIENT                        
*                                                                               
*        CHECK IF THIS IS AN AOR SITUATION                                      
*                                                                               
         XC    GCNAOR(GCNAORL),GCNAOR INIT AOR VALUES                           
*                                                                               
         MVI   ELCODE,X'15'        CHECK FOR AOR ELEMENT                        
         LA    R6,PCLTREC                                                       
*                                                                               
         BRAS  RE,GETEL            LOOK FOR AOR ELEMENT                         
         BNZ   FCNAORX             NO AOR ELEMENT                               
*                                                                               
*        SAVE AOR INFORMATION                                                   
*                                                                               
         USING PCLTADVE,R6         ESTABLISH AOR ELEMENT                        
*                                                                               
         MVC   GCNARAGY,PCLTAOR    SAVE AGENCY POWER CODE                       
         MVC   GCNARADV,PCLTADV    SAVE ADVERTISER                              
         MVC   GCNARCLT,PCLTADVC   SAVE CLIENT CODE                             
         MVC   GCNARSE,PCLTAORS    SAVE AOR SE CODE                             
*                                                                               
*        OPEN AOR FILES                                                         
*                                                                               
         CLC   GCNSE,GCNARSE       SKIP IF WE ARE AOR                           
         BE    FCNOPENX                                                         
*                                                                               
         L     RF,GCNVUTL          POINT TO UTL                                 
*                                                                               
         CLC   GCNARSE,4(RF)       SKIP OPEN IF CURRENTLY ON SYSTEM             
         BE    FCNOPENX                                                         
*                                                                               
         MVC   4(1,RF),GCNARSE     SET NEW SYSTEM NUMBER                        
*                                                                               
         GOTO1 VDATAMGR,DMCB,DMOPEN,=CL8'PRINT',PRTFILES,DMWORK                 
*                                                                               
         L     RF,GCNVUTL          POINT TO UTL                                 
         MVC   4(1,RF),GCNSE       RESET OLD SYSTEM NUMBER                      
*                                                                               
FCNOPENX DS    0H                                                               
*                                                                               
FCNAORX  DS    0H                                                               
*                                                                               
FCNCLTOK DS    0H                                                               
*                                                                               
         MVC   GCNPUB,PBUYKPUB     SET CURRENT PUB CODE                         
*                                                                               
*        FIND AOR PUB CODE                                                      
*                                                                               
         OC    GCNAOR(GCNAORL),GCNAOR IF AOR SITUATION                          
         BZ    *+8                                                              
         BRAS  RE,GTAORPUB               FIND AOR PUB NUMBER                    
*                                                                               
*        READ CORRECT CONTRACT RECORD                                           
*                                                                               
         XC    KEY,KEY             INIT KEY                                     
         LA    R3,KEY              ESTABLISH CONTRACT KEY                       
         USING PCONREC,R3                                                       
*                                                                               
         OC    GCNAOR(GCNAORL),GCNAOR IF AOR SITUATION                          
         BZ    FCNCNARN                                                         
*                                                                               
*        SET TO READ CONTRACT FROM AOR'S FILES                                  
*                                                                               
         L     RF,GCNVUTL          SET TO AOR AGENCY                            
         MVC   4(1,RF),GCNARSE                                                  
*                                                                               
         MVC   PCONKAGY,GCNARAGY   SET AGENCY                                   
         MVC   PCONKMED,GCNARMED   SET MEDIA                                    
         MVI   PCONKRCD,PCONKIDQ   SET RECORD ID                                
         MVC   PCONKCLT,GCNARCLT   SET CLIENT                                   
         MVC   PCONKPUB(6),GCNARPUB   SET PUB                                   
*                                                                               
         B     FCNCNRD             GO READ CONTRACT RECORD                      
*                                                                               
FCNCNARN DS    0H                                                               
*                                                                               
*        READ CONTRACT FROM THIS AGENCY'S FILES                                 
*                                                                               
         MVC   PCONKAGY,GCNAGY     SET AGENCY                                   
         MVC   PCONKMED,GCNMED     SET MEDIA                                    
         MVI   PCONKRCD,PCONKIDQ   SET RECORD ID                                
         MVC   PCONKCLT,GCNCLT     SET CLIENT                                   
         MVC   PCONKPUB(6),GCNPUB     SET PUB                                   
*                                                                               
         OC    GCNMSCLT,GCNMSCLT   IF MASTER CLIENT EXISTS                      
         BZ    *+10                                                             
         MVC   PCONKCLT,GCNMSCLT      USE IT FOR CONTRACT                       
*                                                                               
*        READ FOR CONTRACT POINTER                                              
*                                                                               
FCNCNRD  DS    0H                                                               
*                                                                               
         MVC   KEYSAVE,KEY         SAVE STARTING KEY                            
*                                                                               
         GOTO1 VDATAMGR,DMCB,DMRDHI,PRTDIR,KEY,KEY                              
*                                                                               
FCNRDLP  DS    0H                                                               
*                                                                               
         CLC   PCONKEY(PCONNUM-PCONKEY),KEYSAVE  CHECK IF CLOSE                 
         BNE   FCNRDDN                                                          
*                                                                               
*        READ CONTRACT RECORD                                                   
*                                                                               
         GOTO1 VDATAMGR,DMCB,GETREC,PRTFILE,KEY+27,GCNCONA,DMWORK               
*                                                                               
         MVI   ELCODE,X'10'        SET CONTRACT DESC ELM ID                     
         L     R6,GCNCONA          START OF RECORD                              
*                                                                               
         BRAS  RE,GETEL            FIND DESCRIPTION ELEMENT                     
         BNZ   FCNRDCN             MUST FIND IT                                 
*                                                                               
         USING PCONDESC,R6         ESTABLISH DESCRIPTION ELEMENT                
*                                                                               
         CLC   PBUYKDAT,PCONSDT    BUY MUST BE IN PERIOD                        
         BL    FCNRDCN                                                          
*                                                                               
         CLC   PBUYKDAT,PCONEND                                                 
         BH    FCNRDCN                                                          
*                                                                               
         B     FCNRDFD             CONTRACT FOUND                               
*                                                                               
FCNRDCN  DS    0H                                                               
         GOTO1 VDATAMGR,DMCB,DMRSEQ,PRTDIR,KEY,KEY  NEXT CONTRACT               
         B     FCNRDLP                                                          
*                                                                               
FCNRDDN  DS    0H                  NO CONTRACT FOUND                            
         XC    PCONKEY,PCONKEY     ERASE CONTRACT                               
         L     R3,GCNCONA          POINT TO CONTRACT RECORD AREA                
         XC    0(256,R3),0(R3)     CLEAR OUT RECORD                             
*                                                                               
FCNRDFD  DS    0H                  HAVE CONTRACT                                
*                                                                               
         CLI   GCNGETCU,C'Y'       DO WE NEED TO GET CU VALUE                   
         BNE   FCNGTCUX                                                         
*                                                                               
*        FIND CU VALUE                                                          
*                                                                               
         GOTO1 =V(PPGETCU),DMCB,GCNBUYA,GCNCONA,VDATAMGR                        
*                                                                               
         MVC   GCNCU,4(R1)         SAVE RETURNED VALUE                          
*                                                                               
FCNGTCUX DS    0H                                                               
*                                                                               
         OC    0(25,R3),0(R3)      IF CONTRACT IS MISSING                       
         BNZ   *+10                                                             
         XC    GCNCONA,GCNCONA        CLEAR A(CONREC)                           
*                                                                               
         B     FCNCONX                                                          
*                                                                               
FCNNBUY  DS    0H                                                               
         MVI   GCNERR,GCNNBUYE     NO BUY FOUND                                 
         B     FCNCONX                                                          
*                                                                               
FCNNCLT  DS    0H                                                               
         MVI   GCNERR,GCNNCLTE     CLIENT NOT FOUND                             
         B     FCNCONX                                                          
*                                                                               
FCNCONX  DS    0H                                                               
*                                                                               
GETCONX  DS    0H                                                               
*                                  DATAMGR COMMANDS                             
         L     RF,GCNVUTL          RESET TO CALLING AGENCY                      
         MVC   4(1,RF),GCNSE                                                    
*                                                                               
         XIT1                                                                   
*                                                                               
         GETEL R6,33,ELCODE                                                     
*                                                                               
         LTORG                                                                  
*                                                                               
         DROP                                                                   
*                                                                               
DMRDHI   DC    CL8'DMRDHI'                                                      
DMRSEQ   DC    CL8'DMRSEQ'                                                      
GETREC   DC    CL8'GETREC'                                                      
DMOPEN   DC    CL8'DMOPEN'                                                      
*                                                                               
PRTFILES DS    0C                  LIST OF PRINT FILES                          
         DC    CL8' PRTDIR'                                                     
         DC    CL8' PRTFIL'                                                     
         DC    CL8' PUBDIR'                                                     
         DC    CL8' PUBFIL'                                                     
         DC    CL8'X'              END OF LIST                                  
PRTDIR   DC    CL8'PRTDIR'                                                      
PRTFILE  DC    CL8'PRTFIL'                                                      
PUBDIR   DC    CL8'PUBDIR'                                                      
PUBFILE  DC    CL8'PUBFIL'                                                      
         DC    CL8'X'              END OF LIST                                  
*                                                                               
         TITLE 'T00A4C - GETCON - GET AOR PUB ID - GTAORPUB'                    
***********************************************************************         
*                                                                     *         
*        GIVEN AGENCY'S PUB ID FIND AOR'S PUB ID                      *         
*           ROUTINE USES PUB EQUIVALENCE PASSIVE POINTERS             *         
*                                                                     *         
*NTRY    GCNPUB  = AGENCY'S PUB ID                                    *         
*                                                                     *         
*EXIT    GCNARPUB= AOR'S    PUB ID                                    *         
*        GCNPUB  = AGENCY'S PUB ID                                    *         
*        CC      EQ   EXACT MATCH ON PUB,ZONE,EDITION                 *         
*                LE   MATCH ON BASE PUB ONLY                          *         
*                GT   NO MATCH                                        *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
GTAORPUB NTR1  WORK=(R9,500),BASE=*,LABEL=*                                     
*                                                                               
         USING GETCONWD,RC         RC = A(LOCAL WORKING STORAGE)                
         USING GETCOND,RA          CONTROL BLOCK                                
*                                                                               
         SR    R0,R0               INIT ERROR INDICATOR                         
*                                                                               
         MVC   GTIOKEY,KEY         SAVE CURRENT IOKEY                           
         MVC   GTIOKEYS,KEYSAVE    SAVE CURRENT IOKEYSAV                        
*                                                                               
         XC    GCNARPUB,GCNARPUB   INIT AOR'S PUB ID                            
*                                                                               
         CLC   GCNARAGY,GCNAGY     IF THIS IS AOR AGENCY                        
         BNE   *+14                                                             
         MVC   GCNARPUB,GCNPUB        COPY PUB ID AND EXIT                      
         B     GTAORPBX                                                         
*                                                                               
         XC    KEY,KEY                                                          
         LA    R2,KEY              ESTABLISH KEYAREA AS PUBFILE KEY             
         USING PUBREC,R2                                                        
*                                                                               
         MVI   PUBAID,PUBAIDQ      SET PUB ADV PTR ID                           
         MVC   PUBAMED,GCNMED      SET MEDIA                                    
         MVC   PUBAAGY,GCNMED      SET AGY                                      
         MVC   PUBAADV,GCNARADV    SET ADVERTISER'S CODE                        
         MVC   PUBAAOR,GCNARAGY    SET AOR'S AGY CODE                           
         MVC   PUBAAPUB,GCNPUB     SET AGENCY'S PUB ID                          
*                                                                               
         GOTO1 VDATAMGR,DMCB,=CL8'DMRDHI',=CL8'PUBDIR',KEY,KEY                  
         BE    *+6                                                              
         DC    H'0'                NO ERRORS TOLERATED                          
*                                                                               
         CLC   KEY(PUBAVPUB-PUBKEY),KEYSAVE     IF EXACT MATCH                  
         BNE   *+14                                                             
         MVC   GCNARPUB,PUBAVPUB   SAVE AOR'S PUB ID                            
         B     GTAORPBX                                                         
*                                                                               
         CLC   KEY(PUBAVPUB-2-PUBKEY),KEYSAVE  IF BASE PUB MATCH                
         BNE   *+18                                                             
         L     R0,=F'-1'           SET INDICATOR                                
         MVC   GCNARPUB(4),PUBAVPUB SAVE AOR'S BASE PUB ID                      
         B     GTAORPBX                                                         
******                                                                          
******   TM    GCNARCTL,X'01'      IF PUBLINKS NOT REQUIRED                     
******   BO    *+14                                                             
******   MVC   GCNARPUB,GCNPUB        USE AGY'S NUMBER                          
******   B     GTAORPBX                                                         
*                                                                               
         LA    R0,1                INDICATE ERROR                               
*                                                                               
         B     GTAORPBX            NEQ CC SET                                   
*                                                                               
GTAORPBX DS    0H                                                               
*                                                                               
         MVC   KEY,GTIOKEY         RESTORE SAVED KEYS                           
         MVC   KEYSAVE,GTIOKEYS                                                 
*                                                                               
         LTR   R0,R0               SET CC                                       
*                                                                               
         XIT1                                                                   
*                                                                               
         LTORG                                                                  
*                                                                               
GTIOKEY  DS    XL32                KEY SAVE AREA                                
GTIOKEYS DS    XL32                KEYSAVE SAVE AREA                            
*                                                                               
         DROP                                                                   
*                                                                               
IOA1     DS    XL4096              I/O AREA 1                                   
*                                                                               
         TITLE 'PPGETCON - FIND CONTRACT FOR BUY RECORD - GETCONWD'             
***********************************************************************         
*                                                                     *         
*        WORKING STORAGE FOR PPGETCON                                 *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
GETCONWD DSECT                                                                  
*                                                                               
WRKCBA   DS    A                   A(CONTROL BLOCK)                             
VDATAMGR DS    A                   V(DATAMGER)                                  
WRKCONA  DS    A                   A(CONTRACT RECORD)                           
WRKCLTA  DS    A                   A(CLIENT   RECORD)                           
*                                                                               
DMCB     DS    6A                  DATAMGR CONTROL BLOCK                        
DMWORK   DS    CL64                DATAMGR WORKAREA                             
*                                                                               
KEY      DS    XL32                KEY                                          
KEYSAVE  DS    XL32                KEYSAVE                                      
*                                                                               
ELCODE   DS    XL1                 GETEL WORK AREA                              
DUB      DS    D                   WORKAREA                                     
*                                                                               
         DS    0D                  ALIGNMENT                                    
WRKIOA2  DS    XL4096              LOCAL I/O AREA2                              
*                                                                               
GETCONWL EQU   *-GETCONWD          LENGTH OF WORKING STORAGE                    
*                                                                               
         EJECT                                                                  
       ++INCLUDE GETADVCD                                                       
       ++INCLUDE PPGETCOND                                                      
* DDCOREQUS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOREQUS                                                      
         PRINT ON                                                               
* PRGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE PRGENFILE                                                      
         PRINT ON                                                               
* DDCOMFACS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'028PPGETCON  12/18/02'                                      
         END                                                                    
