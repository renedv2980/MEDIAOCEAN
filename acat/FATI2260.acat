*          DATA SET FATI2260   AT LEVEL 003 AS OF 05/01/02                      
*CATALP FATI2260                                                                
         TITLE 'TI2260 - 2260 TERMINAL INPUT TRANSLATOR'                        
TI2260   CSECT                                                                  
         NMOD1 1,**TI2260                                                       
         USING WORK,12                                                          
         L     R2,0(R1)            A(TIA)                                       
         LH    4,4(2)              MESSAGE LENGTH                               
         LA    5,64(2)   FIRST INPUT CHARACTER                                  
         LR    3,5       FIRST OUTPUT CHARACTER                                 
         LTR   4,4                 ANY DATA ?                                   
         BC    8,EXIT    BRANCH IF NO                                           
         CLI   0(5),X'7A'          TST FOR COLON                                
         BNE   *+16                NO                                           
         LA    5,1(5)              YES, SKIP OVR IT                             
         BCTR  4,0                 DECREMENT LENGTH                             
         LTR   4,4                 LENGTH ZERO NOW                              
         BC    8,EXIT                                                           
         L     14,=V(SSB)                                                       
         TM    1(14),X'02'                                                      
         BC    8,NOPRINT                                                        
         ST    5,DYPAR1                                                         
         STH   4,DYPAR2+2                                                       
         MVI   DYPAR2,C'R'                                                      
         LA    1,DYPAR1                                                         
         L     15,=V(DYDUMP)                                                    
         BASR  14,15                                                            
NOPRINT  MVI   BLANKSW,0                                                        
TESTCHAR CLI   0(5),X'7B'          POUND SIGN ?                                 
         BC    8,POUND                                                          
         CLI   0(5),X'15'          END OF LINE ?                                
         BC    8,EOL                                                            
         CLI   BLANKSW,0                                                        
         BC    8,MOVECHAR          NO NON-BLANK CHARACTERS YET                  
         CLI   0(5),X'40'                                                       
         BC    8,ANYMORE           GO SEE IF ANYTHING BUT BLANKS LEFT           
         CLI   0(5),X'00'                                                       
         BC    8,ANYMORE                                                        
         CLI   0(5),X'6D'                                                       
         BC    8,ANYMORE                                                        
MOVECHAR MVC   0(1,3),0(5)         MOVE CHARACTER                               
         OI    0(R3),C' '          'OR' WITH BLANK FOR USCAN FEATURE            
         MVI   BLANKSW,1                                                        
NEXTCHAR LA    3,1(3)                                                           
         LA    5,1(5)                                                           
         BCT   4,TESTCHAR                                                       
*                                  NO MORE DATA. COMPUTE MESSAGE LENGTH         
NOMORE   LA    5,64(2)                                                          
         SR    3,5                                                              
         STH   3,4(2)                                                           
         STH   3,DYPAR2+2                                                       
         L     14,=V(SSB)                                                       
         TM    1(14),X'02'                                                      
         BC    8,EXIT                                                           
         BASR  14,15                                                            
EXIT     XMOD  1                                                                
         SPACE 2                                                                
*                                  POUND ENCOUNTERED                            
*                                  READ TO EOL                                  
POUND    MVI   0(3),X'22'                                                       
         MVI   BLANKSW,0                                                        
         LA    3,1(3)                                                           
NEXTP    CLI   0(5),X'15'          EOL ?                                        
         BC    8,NEXTCHAR+4        YES. BRANCH BUT DONT BUMP 3 AGAIN            
         LA    5,1(5)                                                           
         BCT   4,NEXTP                                                          
         B     NOMORE                                                           
         SPACE 2                                                                
*                                  EOL ENCOUNTERED.                             
EOL      MVI   0(3),X'22'                                                       
         MVI   BLANKSW,0                                                        
         B     NEXTCHAR                                                         
*                                 READ TO STOP CHARACTER FOR ALL BLANKS         
ANYMORE  LR    9,5                                                              
         LR    10,4                                                             
ANYMORE2 CLI   0(9),X'7B'                                                       
         BC    8,ANYMOREX                                                       
         CLI   0(9),X'15'                                                       
         BC    8,ANYMOREX                                                       
         CLI   0(9),0                                                           
         BC    8,ANYMORE3                                                       
         CLI   0(9),X'6D'                                                       
         BC    8,ANYMORE3                                                       
         CLI   0(9),X'40'                                                       
         BC    7,MOVECHAR                                                       
ANYMORE3 LA    9,1(9)                                                           
         BCT   10,ANYMORE2                                                      
         B     NOMORE                                                           
ANYMOREX LR    4,10                REMAINING STRING TO EOL IS BLANK             
         LR    5,9                                                              
         B     TESTCHAR                                                         
         SPACE 2                                                                
         LTORG                                                                  
DYPAR1   DS    F                                                                
DYPAR2   DS    F                                                                
WORK     DSECT                                                                  
BLANKSW  DS    CL1                                                              
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003FATI2260  05/01/02'                                      
         END                                                                    
