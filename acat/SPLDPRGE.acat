*          DATA SET SPLDPRGE   AT LEVEL 022 AS OF 11/02/16                      
*CATALP SPLDPRGE                                                                
         TITLE 'LDPURGE - SPOT - LOAD PURGE TEST'                               
*                                                                               
* P1     XL1   X'00'    RETURN  X'00'=NOPURGE  OR  X'FF'=PURGE                  
*        AL3   A(RECORD)                                                        
* P2     AL4   A(PEELDATE)                                                      
* P3     CL1   PASS VALUE       C'Y' = YOU ASKED ME TO RETURN                   
*              RETURN VALUE     C'R' = RETURN BACK TO ME                        
*        AL3   A(LAST DSKADR)                                                   
* P4     AL4   A(FILE DEFN)                                                     
* P5     XL1   X'10     RECORD IS AN INDIRECT POINTER                           
*                                                                               
*        UPSI  00000001 PURGE X'80' DELETES                                     
*        UPSI  00000010 PURGE X'C3' CLOSEOUTS                                   
*        UPSI  00000100 PURGE TEST AGY RECORDS                                  
*        UPSI  00001000 PURGE BUY ACTVITY RECS WITH NO EL'S, AND                
*                       DEL EL'S OLDER THAN 3 MOS.                              
         PRINT NOGEN                                                            
LDPURGE  CSECT                                                                  
         NMOD1 0,SPLDPRGE                                                       
         SPACE 2                                                                
PU       BC    0,PU2                                                            
         OI    PU+1,X'F0'          SET BRANCH                                   
         LR    R0,R1               SAVE R1                                      
         L     R1,=V(UPSIVAL)                                                   
         MVC   UPSI,0(R1)                                                       
         LR    R1,R0               RESTORE R1                                   
         SPACE 2                                                                
PU2      L     R3,0(R1)            R3 = A(RECORD)                               
*                                                                               
PU3      TM    16(R1),X'10'        TEST INDIRECT POINTER PASSED                 
         BZ    PU4                                                              
         TM    13(R3),X'80'        TEST IF DELETED INDIRECT POINTER             
         BZ    XIT                                                              
         MVI   0(R1),X'FF'         PURGE INDIRECT POINTER                       
         B     XIT                                                              
PU4      EQU   *                                                                
*                                                                               
         TM    UPSI,X'08'          PURGE BUY ACTV RECS?                         
         BZ    *+14                                                             
         CLC   =X'0A2E',0(R3)      TEST BUY ACTIVITY RECORD                     
         BE    ACTV                                                             
         CLC   0(2,R3),=X'0D3A'    TEST AUTOPAY RECORD                          
         BE    APY                                                              
         SPACE 2                                                                
PU10     TM    UPSI,X'07'          ANY PURGES                                   
         BC    8,XIT               NO                                           
         TM    15(R3),X'C0'        ANY DELETE INDS ON                           
         BC    7,ACAN              YES. A CANDIDATE                             
* NO DELETE INDICATORS. TEST FOR TEST AGENCY RECORD PURGE OPTION                
PU20     TM    UPSI,X'04'                                                       
         BC    1,TAG               YES. GO TEST FOR TEST AG                     
         SPACE 2                                                                
XIT      XMOD1 1                                                                
         EJECT                                                                  
* THIS IS A CANDIDATE FOR A PURGE                                               
ACAN     TM    UPSI,X'03'          DELETE OR CLOSEOUT PURGE                     
         BC    8,PU20              NO, GO TEST FOR TEST AG                      
         BC    1,BOTH              DELETE BOTH X'80' AND X'C0'                  
         SPACE 2                                                                
* DELETE ONE OR THE OTHER                                                       
         TM    UPSI,X'01'                                                       
         BC    1,O80               ONLY X'80'                                   
         TM    UPSI,X'02'                                                       
         BC    1,OC0               ONLY X'C0'                                   
         B     PU20                                                             
         SPACE 2                                                                
* DELETE ONLY X'80'                                                             
O80      TM    15(R3),X'C0'                                                     
         BO    PU20                BOTH ON, NO DELETE                           
         TM    15(R3),X'80'                                                     
         BZ    PU20                                                             
* FOLLOWING TEST FOR BUYS ON COKE AGENCIES TO MAKE SURE THAT                    
* BUYLINE NUMBER IS NEVER RE-USED  MH 10/03/95                                  
         TM    15(R3),X'20'                                                     
         BO    PU20                                                             
O801     MVI   0(R1),255                                                        
         B     XIT                                                              
         SPACE 2                                                                
* DELETE ONLY X'C0'                                                             
OC0      TM    15(R3),X'C0'                                                     
         BC    1,O801                                                           
         B     PU20                                                             
         SPACE 2                                                                
* DELETE BOTH                                                                   
BOTH     TM    15(R3),X'C0'                                                     
         BC    1,O801                                                           
         TM    15(R3),X'80'                                                     
         BC    1,O801                                                           
         B     PU20                                                             
         SPACE 2                                                                
* TEST AGENCY PURGE                                                             
TAG      CLC   20(2,R3),TSTAG                                                   
         BC    8,O801                                                           
         B     XIT                                                              
         EJECT                                                                  
ACTV     DS    0H                                                               
         LR    R7,R1               SAVER1                                       
         LR    R6,R3                                                            
         MVI   ELCODE,X'05'                                                     
         BAS   RE,GETEL                                                         
         BNE   NOACTV                                                           
         USING TBADTAEL,R6                                                      
*                                                                               
* GET TODAY'S DATE, SUBTRACT 3 MONTHS                                           
         OC    ACTVDATE,ACTVDATE                                                
         BNZ   ACTV2                                                            
         GOTO1 =V(DATCON),DMCB,(5,WORK),(0,WORK)                                
         GOTO1 =V(ADDAY),DMCB,(C'M',WORK),WORK,-3                               
         GOTO1 =V(DATCON),DMCB,(0,WORK),(2,ACTVDATE)                            
*                                                                               
ACTV2    CLC   TBADTAWK,ACTVDATE   LESS THAN CUTOFF?                            
         BNL   *+8                                                              
         MVI   0(R6),X'FF'                                                      
         BAS   RE,NEXTEL                                                        
         BE    ACTV2                                                            
*                                                                               
* REMOVE ALL X'FF' ELEMS, SEE IF ANY 05'S REMAIN                                
         GOTO1 =V(HELLO),DMCB,(C'D',=C'SPTFIL'),(X'FF',(R3)),0,0                
         LR    R6,R3                                                            
         MVI   ELCODE,X'05'                                                     
         BAS   RE,GETEL                                                         
         BNE   NOACTV                                                           
         B     XIT                                                              
*                                                                               
NOACTV   DS    0H                                                               
         LR    R1,R7               RESTORER1                                    
         MVI   0(R1),X'FF'         PURGE REC, NO ACTIVITY                       
         B     XIT                                                              
         DROP  R6                                                               
         EJECT                                                                  
*                                                                               
APY      DS    0H                  DELETE AUTOPAY RECS > 2 WEEKS OLD            
         OC    APYDATE,APYDATE                                                  
         BNZ   APY2                                                             
         LR    R7,R1               SAVE R1                                      
         GOTO1 =V(DATCON),DMCB,(5,WORK),(0,WORK)       TODAY'S DATE             
         GOTO1 =V(ADDAY),DMCB,WORK,WORK,-14            MINUS 14 DAYS            
         GOTO1 =V(DATCON),DMCB,(0,WORK),(2,APYDATE)                             
         XC    APYDATE,=X'FFFF'                        COMPLEMENT DATE          
         LR    R1,R7               RESTORE R1                                   
*                                                                               
* BEALS SUGGESTS REMEMBERING DATES ARE COMPLEMENTED !!!                         
* SO BRANCHES LOOK BACKWARDS                                                    
*                                                                               
APY2     CLC   2(2,R3),APYDATE     KEEP RECS < CUTOFF DATE                      
         BL    PU10                                                             
         MVI   0(R1),X'FF'         PURGE RECS > CUTOFF                          
         B     XIT                                                              
*                                                                               
         EJECT                                                                  
         GETEL R6,24,ELCODE                                                     
ELCODE   DS    X                                                                
DMCB     DS    6F                                                               
UPSI     DC    X'00'                                                            
TSTAG    DC    X'EEEE'             CHANGED FROM T1 11/08/04 ABEA                
WORK     DS    CL24                                                             
ACTVDATE DC    XL2'00'                                                          
APYDATE  DC    XL2'00'                                                          
         LTORG                                                                  
         EJECT                                                                  
       ++INCLUDE SPTRTBAE                                                       
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'022SPLDPRGE  11/02/16'                                      
         END                                                                    
