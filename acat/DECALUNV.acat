*          DATA SET DECALUNV   AT LEVEL 020 AS OF 05/05/05                      
*CATALP DECALUNV                                                                
DECALUNV CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,DECALUNV                                                       
         USING DECALUNV+4096,R8                                                 
         LA    R8,2048(RB)                                                      
         LA    R8,2048(R8)                                                      
         ST    R1,PARAMS           SAVE THE PARAMETERS                          
         USING CALVPHD,R1                                                       
         MVC   NTIBASE,VPHDEMS     SET DEMOGRAPHIC BASE                         
         BAS   RE,GENDISP          FIND DEMDISP DISPLACEMENTS                   
         LA    RE,FORMS                                                         
         ST    RE,CURRFORM                                                      
         BAS   RE,DOFORM           CREATE/ADD CATAGORIES & SLOT UNIVS           
         LA    R1,UNIVS            PT TO UNIV LIST                              
UNV20    SR    R3,R3                                                            
         ICM   R3,3,2(R1)          DISPLACEMENT INTO INTACCS                    
         A     R3,NTIBASE          ADD TO DEMO START ADDRESS                    
         MVC   0(4,R3),4(R1)       MOVE IN UNIV DEMO                            
         LA    R1,L'HARDSP(R1)                                                  
         CLI   0(R1),X'FF'                                                      
         BNE   UNV20                                                            
         XMOD1 1                                                                
         EJECT                                                                  
**********************************************************************          
*DOFORM - SET UP FORMULAE FROM TABLES AND PROCESS THEM                          
**********************************************************************          
DOFORM   NTR1                                                                   
DOFORM1  LA    R9,WORK                                                          
         L     RE,CURRFORM         SET TO CURRENT FORMULA                       
         ZIC   R1,0(RE)            LOOP CONTROL                                 
         XC    WORK,WORK                                                        
         LR    RF,RE               PROCESS FORMULA                              
         MVC   3(1,R9),1(RE)       FUNCTION TO PERFORM                          
         LA    R9,4(R9)            NEXT WORK BUCKET                             
         LA    RF,2(RF)            NEXT FORMULA DISP VALUE                      
*                                                                               
DOFORM5  SR    R3,R3                                                            
         ICM   R3,3,0(RF)                                                       
         LA    R3,B+4(R3)          DISPLACEMENT TO BUCKET                       
         ST    R3,0(R9)            SAVE THE ACTUAL ADDRESS                      
         LA    RF,2(RF)            SET TO DO NEXT                               
         LA    R9,4(R9)                                                         
         BCT   R1,DOFORM5          DO NEXT FIELD                                
*                                                                               
         XC    0(4,R9),0(R9)       SET TERMINATOR                               
         ZIC   R1,0(RE)            NUMBER OF ENTRIES                            
         LA    R1,1(R1)            PLUS HEADER                                  
         SLL   R1,1                * 2                                          
         AR    RE,R1               + CURRENT ADDR = NEXT FORM                   
         ST    RE,CURRFORM                                                      
         L     R1,WORK             SET THE FUNCTION                             
         SLL   R1,2                *4                                           
         L     RF,FUNC(R1)                                                      
         BASR  RE,RF               DO THE FORMULA                               
         L     RE,CURRFORM                                                      
         CLI   0(RE),X'FF'         CONTINUE LOOP UNTIL END                      
         BNE   DOFORM1             OF FORMULAS                                  
         XIT1                                                                   
**********************************************************************          
* MATHMATICAL FUNCTIONS                                                         
**********************************************************************          
FUNC     DC    A(0)                                                             
         DC    A(FADD)                                                          
         DC    A(FSUB)                                                          
         DC    A(FVPH)                                                          
         DC    A(FVPH2)                                                         
         SPACE 3                                                                
FADD     SR    R1,R1               ADD A LIST OF NUMBERS                        
         LA    R2,WORK+8                                                        
         L     R3,0(R2)                                                         
         A     R1,0(R3)                                                         
         LA    R2,4(R2)                                                         
         L     R3,0(R2)                                                         
         LTR   R3,R3                                                            
         BNZ   *-14                                                             
         L     R3,WORK+4                                                        
         ST    R1,0(R3)                                                         
         BR    RE                                                               
         SPACE 2                                                                
FSUB     DS    0H                  SUBTRACT A LIST OF NUMBERS                   
         LA    R2,WORK+8                                                        
         L     R3,0(R2)                                                         
         L     R1,0(R3)                                                         
         B     *+8                                                              
         S     R1,0(R3)                                                         
         LA    R2,4(R2)                                                         
         L     R3,0(R2)                                                         
         LTR   R3,R3                                                            
         BNZ   *-14                                                             
         L     R3,WORK+4                                                        
         ST    R1,0(R3)                                                         
         BR    RE                                                               
         SPACE 1                                                                
FVPH     DS    0H                  CALCULATE VPH VALUES                         
         SR    R2,R2                                                            
         L     R1,WORK+8           IMPRESSIONS                                  
         L     R3,0(R1)                                                         
         M     R2,=F'2000'                                                      
         L     R1,WORK+12          POINT TO DIVISION FIELD: A(HOMES)            
*                                                                               
         L     RF,0(R1)                                                         
         LTR   RF,RF               KILL DIVIDE IF ZERO                          
         BNZ   *+10                                                             
         SR    R3,R3                                                            
         B     *+8                                                              
         D     R2,0(R1)            VPH = IMPS * 1000 / HOMES                    
         A     R3,=F'1'                                                         
         SRA   R3,1                                                             
         L     R2,WORK+4           FIND OUTPUT AREA                             
         ST    R3,0(R2)            AND SAVE VPH                                 
         BR    RE                                                               
         EJECT                                                                  
*                                                                               
FVPH2    DS    0H        CALCULATE VPH AND ADD TO ANOTHER VPH                   
         SR    R2,R2                                                            
         L     R1,WORK+12          IMPRESSIONS                                  
         L     R3,0(R1)                                                         
         M     R2,=F'2000'                                                      
         L     R1,WORK+16          POINT TO DIVISION FIELD                      
         L     RF,0(R1)                                                         
         LTR   RF,RF               KILL DIVIDE IF ZERO                          
         BNZ   *+10                                                             
         SR    R3,R3                                                            
         B     *+8                                                              
         D     R2,0(R1)            VPH = IMPS * 1000 / HOMES                    
         A     R3,=F'1'                                                         
         SRA   R3,1                HAVE VPH                                     
         L     R1,WORK+8           GET THE OTHER ONE                            
         A     R3,0(R1)            AND SUM THEM                                 
         L     R1,WORK+4                                                        
         ST    R3,0(R1)                                                         
         BR    RE                                                               
         EJECT                                                                  
**********************************************************************          
*GENDISP -     READ IN DEMDISP AND COMPARE TO HARD CODED DEMDISP TABLE          
*        ONLY BUILD DEMDISP ENTRIES WE'RE INTERESTED IN PROCESSING.             
*        SAVE THE INTACCS DISPLACEMENT IN OUR BUFFER                            
*        UNIVERSES WILL COME IN ON THE 'Y'S AND OUTPUT ON THE 'U'S              
**********************************************************************          
GENDISP  NTR1                                                                   
         ICM   RE,15,=V(DEMDISP)   GET V(DEMDISP) IF LINKED IN                  
         BNZ   *+18                                                             
         L     RE,=V(COMFACS)      V(COMFACS) FROM DEMCNV                       
         ICM   RE,15,CT00AD0-COMFACSD(RE)  V(DEMDISP)                           
         BNZ   *+6                                                              
         DC    H'0'                V(DEMDISP) IS UNRESOLVED!?!                  
         LA    RE,16(RE)           BYPASS DEMADDR HEADER                        
FNDDSP   CLC   0(3,RE),=C'PNN'     FIND NTI HEADER                              
         BE    SETDISP                                                          
         SR    RF,RF               GET LENGTH THIS ENTRY                        
         ICM   RF,7,7(RE)                                                       
         LA    RE,1(RF,RE)         BUMP TO NEXT ONE                             
         B     FNDDSP                                                           
         SPACE 1                                                                
SETDISP  L     R2,NTIBASE          PT TO INTACCS DEMO LIST                      
         LA    RE,10(RE)           BYPASS DETAIL HEADER                         
         LA    RF,0                                                             
         LA    R3,HARDSP           HARD CODED DEMO LIST                         
BLDDSP   CLI   0(RE),X'FF'         BUILD THE SELECTED DISP. TAB                 
         BE    GENDISPX                                                         
         CLI   2(RE),HIELQ         FST DEMS HAVE ELCD >5F IGNORE THEM!          
         BH    GENDISPX            TREAT SAME AS END OF TABLE                   
         CLI   0(R3),X'FF'         END OF DEMDISP THAT WE NEED                  
         BE    GENDISPX                                                         
         CLI   0(RE),C'Y'          WE ONLY CARE ABOUT 'Y'-DEMOS                 
         BE    BLD10                                                            
         CLI   0(RE),C'U'          AND 'U' DEMOS                                
         BNE   BLDDSP1             BUMP TO DEMDISP ENTRIES WE WANT              
****                                                                            
         CLI   1(RE),16            BYPASS THIS STUPID 'U'-CABLE                 
         BE    BLDDSP1                                                          
****                                                                            
BLD10    CLC   0(2,RE),0(R3)       TEST THAT DEMDISP AND HARD TBL               
         BE    *+6                 ARE IN SYNC                                  
         DC    H'0'                NOT IN SYNC!!!!!!                            
         STCM  RF,3,2(R3)            SAVE DEMDISP DISP IN OUR TABLE             
         MVC   4(4,R3),0(R2)       MOVE IN DEMOS                                
         LA    R3,L'HARDSP(R3)     NEXT HARD CODED DEMDIP ENTRY                 
BLDDSP1  LA    RE,5(RE)                                                         
         LA    RF,4(RF)                                                         
         LA    R2,4(R2)            BUMP INTACCS                                 
         B     BLDDSP                                                           
*                                                                               
GENDISPX XIT1                                                                   
         EJECT                                                                  
*_____________________________________________________________________          
         LTORG                                                                  
*_____________________________________________________________________          
         EJECT                                                                  
HOLIDAY  DC    X'00'                                                            
FIRSTSW  DC    X'00'                                                            
SVDPT    DC    X'00'                                                            
*              IN TAB MINIMUMS FOR 87/88                                        
ITMINS   DC    AL1(145,145,105,92,85,81,79,77,75,74,73)                         
SAVER2   DS    F                                                                
PARAMS   DS    F                   A(INPUT PARAMETERS)                          
NTIBASE  DS    F                   START OF DEMOGRAPHICS                        
CURRFORM DS    F                                                                
ADPTDEMS DS    F                                                                
DMCB     DS    6F                                                               
WORK     DS    CL40                                                             
NUMDISPS DC    A(0)                                                             
HIELQ    EQU   X'5F'         DEMDISP ELEMENTS > 5F SHOULD BE IGNORED            
         EJECT                                                                  
* NTI FILE - EFFECTIVE 30/87                                                    
* THE FIELDS ARE ORGANIZED FOR MINIMUM FILE SPACE THEY ARE NOT                  
* HAPHAZARD OR RANDOM. KEEP THIS IN MIND IF CHANGES ARE MADE.                   
*                Y = RAW IMPRESSIONS    U = UNIVERSES                           
B        DS    0F                  BASE FOR DATA                                
HARDSP   DS    0CL8                                                             
YW25     DC    C'Y',AL1(197),AL2(0),AL4(0)     W2-5                             
YW68     DC    C'Y',AL1(198),AL2(0),AL4(0)     W6-8                             
YW911    DC    C'Y',AL1(199),AL2(0),AL4(0)     W9-11                            
YW1214   DC    C'Y',AL1(024),AL2(0),AL4(0)     W1214                            
YW1517   DC    C'Y',AL1(026),AL2(0),AL4(0)     W1517                            
YW1820   DC    C'Y',AL1(068),AL2(0),AL4(0)     W1820                            
YW2124   DC    C'Y',AL1(069),AL2(0),AL4(0)     W2124                            
YW2529   DC    C'Y',AL1(196),AL2(0),AL4(0)     W2529                            
YW3034   DC    C'Y',AL1(207),AL2(0),AL4(0)     W3034                            
YW3539   DC    C'Y',AL1(208),AL2(0),AL4(0)     W3539                            
YW4044   DC    C'Y',AL1(209),AL2(0),AL4(0)     W4044                            
YW4549   DC    C'Y',AL1(203),AL2(0),AL4(0)     W4549                            
YW5054   DC    C'Y',AL1(055),AL2(0),AL4(0)     W5054                            
YW5564   DC    C'Y',AL1(058),AL2(0),AL4(0)     W5564                            
YW65P    DC    C'Y',AL1(060),AL2(0),AL4(0)     W65+                             
YM25     DC    C'Y',AL1(217),AL2(0),AL4(0)     M2-5                             
YM68     DC    C'Y',AL1(218),AL2(0),AL4(0)     M6-8                             
YM911    DC    C'Y',AL1(219),AL2(0),AL4(0)     M9-11                            
YM1214   DC    C'Y',AL1(074),AL2(0),AL4(0)     M1214                            
YM1517   DC    C'Y',AL1(076),AL2(0),AL4(0)     M1517                            
YM1820   DC    C'Y',AL1(112),AL2(0),AL4(0)     M1820                            
YM2124   DC    C'Y',AL1(113),AL2(0),AL4(0)     M2124                            
YM2529   DC    C'Y',AL1(227),AL2(0),AL4(0)     M2529                            
YM3034   DC    C'Y',AL1(228),AL2(0),AL4(0)     M3034                            
YM3539   DC    C'Y',AL1(229),AL2(0),AL4(0)     M3539                            
YM4044   DC    C'Y',AL1(195),AL2(0),AL4(0)     M4044                            
YM4549   DC    C'Y',AL1(213),AL2(0),AL4(0)     M4549                            
YM5054   DC    C'Y',AL1(105),AL2(0),AL4(0)     M5054                            
YM5564   DC    C'Y',AL1(108),AL2(0),AL4(0)     M5564                            
YM65P    DC    C'Y',AL1(110),AL2(0),AL4(0)     M65+                             
YWMOMS   DC    C'Y',AL1(120),AL2(0),AL4(0)     WMOMS                            
YWW1820  DC    C'Y',AL1(230),AL2(0),AL4(0)     WW1820                           
YWW2124  DC    C'Y',AL1(231),AL2(0),AL4(0)     WW2124                           
YWW2534  DC    C'Y',AL1(232),AL2(0),AL4(0)     WW2534                           
YWW3544  DC    C'Y',AL1(233),AL2(0),AL4(0)     WW3544                           
YWW4549  DC    C'Y',AL1(234),AL2(0),AL4(0)     WW4549                           
YWW5054  DC    C'Y',AL1(235),AL2(0),AL4(0)     WW5054                           
YWW55P   DC    C'Y',AL1(236),AL2(0),AL4(0)     WW55+                            
YHOMES   DC    C'Y',AL1(000),AL2(0),AL4(0)     HOMES                            
*                                                                               
UNIVS    DS    0C          UNIVERSE BUCKETS BEGIN HERE                          
UTOT     DC    C'U',AL1(127),AL2(0),AL4(0)     UTOT                             
UW1834   DC    C'U',AL1(041),AL2(0),AL4(0)     UW1834                           
UW1849   DC    C'U',AL1(042),AL2(0),AL4(0)     UW1849                           
UW2554   DC    C'U',AL1(048),AL2(0),AL4(0)     UW2554                           
UW5564   DC    C'U',AL1(058),AL2(0),AL4(0)     UW5564                           
UW55P    DC    C'U',AL1(059),AL2(0),AL4(0)     UW55+                            
UWOMEN   DC    C'U',AL1(045),AL2(0),AL4(0)     UWOMEN                           
UM1834   DC    C'U',AL1(091),AL2(0),AL4(0)     UM1834                           
UM1849   DC    C'U',AL1(092),AL2(0),AL4(0)     UM1849                           
UM2554   DC    C'U',AL1(098),AL2(0),AL4(0)     UM2554                           
UM5564   DC    C'U',AL1(108),AL2(0),AL4(0)     UM5564                           
UM55P    DC    C'U',AL1(109),AL2(0),AL4(0)     UM55+                            
UMEN     DC    C'U',AL1(095),AL2(0),AL4(0)     UMEN                             
ULOH     DC    C'U',AL1(064),AL2(0),AL4(0)     ULOH                             
UWW      DC    C'U',AL1(065),AL2(0),AL4(0)     UWRK WOM                         
UTNBY    DC    C'U',AL1(075),AL2(0),AL4(0)     UTN BOYS                         
UTNGL    DC    C'U',AL1(025),AL2(0),AL4(0)     UTN GIRL                         
UTEENS   DC    C'U',AL1(125),AL2(0),AL4(0)     UTEENS                           
UCH25    DC    C'U',AL1(121),AL2(0),AL4(0)     UCH25                            
UCH611   DC    C'U',AL1(123),AL2(0),AL4(0)     UCH611                           
UCHILD   DC    C'U',AL1(122),AL2(0),AL4(0)     UCHILD                           
UHOMES   DC    C'U',AL1(001),AL2(0),AL4(0)     UHOMES                           
UW3564   DC    C'U',AL1(053),AL2(0),AL4(0)     UW3564                           
UM3564   DC    C'U',AL1(103),AL2(0),AL4(0)     UM3564                           
UW21P    DC    C'U',AL1(067),AL2(0),AL4(0)     UW21+                            
UW2149   DC    C'U',AL1(071),AL2(0),AL4(0)     UW2149                           
UW2549   DC    C'U',AL1(047),AL2(0),AL4(0)     UW2549                           
UW611    DC    C'U',AL1(021),AL2(0),AL4(0)     UW6-11                           
UW211    DC    C'U',AL1(119),AL2(0),AL4(0)     UW2-11                           
UW1524   DC    C'U',AL1(034),AL2(0),AL4(0)     UW1524                           
UM21P    DC    C'U',AL1(118),AL2(0),AL4(0)     UM21+                            
UM2149   DC    C'U',AL1(115),AL2(0),AL4(0)     UM2149                           
UM2549   DC    C'U',AL1(097),AL2(0),AL4(0)     UM2549                           
UM611    DC    C'U',AL1(022),AL2(0),AL4(0)     UM6-11                           
UM211    DC    C'U',AL1(020),AL2(0),AL4(0)     UM2-11                           
UM1524   DC    C'U',AL1(084),AL2(0),AL4(0)     UM1524                           
USPARE   DC    C'U',AL1(000),AL2(0),AL4(0)     SPARE                            
UWW1849  DC    C'U',AL1(237),AL2(0),AL4(0)     UWW1849                          
UWW2554  DC    C'U',AL1(238),AL2(0),AL4(0)     UWW2554                          
UV911    DC    C'U',AL1(162),AL2(0),AL4(0)     UV9-11                           
UMOMS    DC    C'U',AL1(120),AL2(0),AL4(0)     UMOMS                            
UHWC18   DC    C'U',AL1(242),AL2(0),AL4(0)     UHWC18                           
UHWC12   DC    C'U',AL1(241),AL2(0),AL4(0)     UHWC12                           
UHWC6    DC    C'U',AL1(240),AL2(0),AL4(0)     UHWC6                            
         DC    X'FF'                                                            
         EJECT                                                                  
*---------------------------------------------------------------------          
*FORMS- TABLE OF FORMULAS TO CALCULATE UNIV DEMO CATAGORIES                     
*---------------------------------------------------------------------          
ADD      EQU   1                                                                
SUB      EQU   2                                                                
VPH      EQU   3                                                                
VPH2     EQU   4                                                                
FORMS    DS    0C                                                               
         DC    AL1(3,ADD)                                                       
         DC    AL2(UM611-B),AL2(YM68-B),AL2(YM911-B)                            
         DC    AL1(3,ADD)                                                       
         DC    AL2(UW611-B),AL2(YW68-B),AL2(YW911-B)                            
         DC    AL1(3,ADD)                                                       
         DC    AL2(UM211-B),AL2(YM25-B),AL2(UM611-B)                            
         DC    AL1(3,ADD)                                                       
         DC    AL2(UW211-B),AL2(YW25-B),AL2(UW611-B)                            
         DC    AL1(5,ADD)                                                       
         DC    AL2(UW1834-B),AL2(YW1820-B),AL2(YW2124-B)                        
         DC    AL2(YW2529-B),AL2(YW3034-B)                                      
         DC    AL1(5,ADD)                                                       
         DC    AL2(UM1834-B),AL2(YM1820-B),AL2(YM2124-B)                        
         DC    AL2(YM2529-B),AL2(YM3034-B)                                      
         DC    AL1(3,ADD)                                                       
         DC    AL2(UM55P-B),AL2(YM5564-B),AL2(YM65P-B)                          
         DC    AL1(3,ADD)                                                       
         DC    AL2(UW55P-B),AL2(YW5564-B),AL2(YW65P-B)                          
         DC    AL1(6,ADD)                                                       
         DC    AL2(UM2549-B),AL2(YM2529-B),AL2(YM3034-B)                        
         DC    AL2(YM3539-B),AL2(YM4044-B),AL2(YM4549-B)                        
         DC    AL1(6,ADD)                                                       
         DC    AL2(UW2549-B),AL2(YW2529-B),AL2(YW3034-B)                        
         DC    AL2(YW3539-B),AL2(YW4044-B),AL2(YW4549-B)                        
         DC    AL1(3,ADD)                                                       
         DC    AL2(UW2149-B),AL2(UW2549-B),AL2(YW2124-B)                        
         DC    AL1(3,ADD)                                                       
         DC    AL2(UM2149-B),AL2(UM2549-B),AL2(YM2124-B)                        
         DC    AL1(3,ADD)                                                       
         DC    AL2(UM2554-B),AL2(UM2549-B),AL2(YM5054-B)                        
         DC    AL1(3,ADD)                                                       
         DC    AL2(UW2554-B),AL2(UW2549-B),AL2(YW5054-B)                        
         DC    AL1(5,ADD)                                                       
         DC    AL2(UM1849-B),AL2(UM1834-B),AL2(YM3539-B)                        
         DC    AL2(YM4044-B),AL2(YM4549-B)                                      
         DC    AL1(5,ADD)                                                       
         DC    AL2(UW1849-B),AL2(UW1834-B),AL2(YW3539-B)                        
         DC    AL2(YW4044-B),AL2(YW4549-B)                                      
         DC    AL1(3,SUB)                                                       
         DC    AL2(UM2149-B),AL2(UM1849-B),AL2(YM1820-B)                        
         DC    AL1(3,SUB)                                                       
         DC    AL2(UW2149-B),AL2(UW1849-B),AL2(YW1820-B)                        
         DC    AL1(2,ADD)                                                       
         DC    AL2(UM5564-B),AL2(YM5564-B)                                      
         DC    AL1(2,ADD)                                                       
         DC    AL2(UW5564-B),AL2(YW5564-B)                                      
         DC    AL1(4,ADD)                                                       
         DC    AL2(UMEN-B),AL2(UM1849-B),AL2(YM5054-B)                          
         DC    AL2(UM55P-B)                                                     
         DC    AL1(4,ADD)                                                       
         DC    AL2(UWOMEN-B),AL2(UW1849-B),AL2(YW5054-B)                        
         DC    AL2(UW55P-B)                                                     
         DC    AL1(3,SUB)                                                       
         DC    AL2(UM21P-B),AL2(UMEN-B),AL2(YM1820-B)                           
         DC    AL1(3,SUB)                                                       
         DC    AL2(UW21P-B),AL2(UWOMEN-B),AL2(YW1820-B)                         
         DC    AL1(6,ADD)                                                       
         DC    AL2(UM3564-B),AL2(YM3539-B),AL2(YM4044-B)                        
         DC    AL2(YM4549-B),AL2(YM5054-B),AL2(YM5564-B)                        
         DC    AL1(6,ADD)                                                       
         DC    AL2(UW3564-B),AL2(YW3539-B),AL2(YW4044-B)                        
         DC    AL2(YW4549-B),AL2(YW5054-B),AL2(YW5564-B)                        
         DC    AL1(4,ADD)                                                       
         DC    AL2(UW1524-B),AL2(YW1517-B),AL2(YW1820-B)                        
         DC    AL2(YW2124-B)                                                    
         DC    AL1(4,ADD)                                                       
         DC    AL2(UM1524-B),AL2(YM1517-B),AL2(YM1820-B)                        
         DC    AL2(YM2124-B)                                                    
         DC    AL1(6,ADD)                                                       
         DC    AL2(UWW1849-B),AL2(YWW1820-B),AL2(YWW2124-B)                     
         DC    AL2(YWW2534-B),AL2(YWW3544-B),AL2(YWW4549-B)                     
         DC    AL1(5,ADD)                                                       
         DC    AL2(UWW2554-B),AL2(YWW2534-B),AL2(YWW3544-B)                     
         DC    AL2(YWW4549-B),AL2(YWW5054-B)                                    
         DC    AL1(4,ADD)                                                       
         DC    AL2(UWW-B),AL2(UWW1849-B),AL2(YWW5054-B)                         
         DC    AL2(YWW55P-B)                                                    
         DC    AL1(3,ADD)                                                       
         DC    AL2(UCH25-B),AL2(YM25-B),AL2(YW25-B)                             
         DC    AL1(3,ADD)                                                       
         DC    AL2(UCH611-B),AL2(UW611-B),AL2(UM611-B)                          
         DC    AL1(3,ADD)                                                       
         DC    AL2(UCHILD-B),AL2(UCH25-B),AL2(UCH611-B)                         
         DC    AL1(3,ADD)                                                       
         DC    AL2(UV911-B),AL2(YM911-B),AL2(YW911-B)                           
         DC    AL1(2,ADD)                                                       
         DC    AL2(UHOMES-B),AL2(YHOMES-B)                                      
         DC    AL1(3,ADD)                                                       
         DC    AL2(UTNBY-B),AL2(YM1214-B),AL2(YM1517-B)                         
         DC    AL1(3,ADD)                                                       
         DC    AL2(UTNGL-B),AL2(YW1214-B),AL2(YW1517-B)                         
         DC    AL1(3,ADD)                                                       
         DC    AL2(UTEENS-B),AL2(UTNGL-B),AL2(UTNBY-B)                          
         DC    AL1(2,ADD)                                                       
         DC    AL2(UMOMS-B),AL2(YWMOMS-B)                                       
         DC    AL1(7,ADD)                                                       
         DC    AL2(UTOT-B),AL2(UWOMEN-B),AL2(UMEN-B)                            
         DC    AL2(UTNGL-B),AL2(UTNBY-B),AL2(UM211-B)                           
         DC    AL2(UW211-B)                                                     
         DC    X'FF'                                                            
         EJECT                                                                  
DSPTB    DS    2500C                                                            
       ++INCLUDE DECALVPHD                                                      
         EJECT                                                                  
       ++INCLUDE DDCOMFACS                                                      
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'020DECALUNV  05/05/05'                                      
         END                                                                    
