*          DATA SET SPWGTLIST  AT LEVEL 007 AS OF 05/01/02                      
*CATALP WGTLIST                                                                 
         TITLE 'WGTLIST - BUILD WEIGHTED PRODUCT ALLOCATION LIST'               
WGTLIST  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 10,WGTLIST                                                       
         USING WLWKD,RC                                                         
*                                                                               
* PARAM 1      A(INPUT LIST)   1 BYTE PRD, 1 BYTE WGT.  X'00' = EOL             
* PARAM 2      A(OUTPUT)       1 BYTE PRDS - WEIGHTED LIST                      
* PARAM 3      V(XSORT)                                                         
* PARAM 4      LENGTH OF OUTPUT LIST  (=SUM OF WGTS)                            
*                                                                               
         MVC   PARMS,0(R1)                                                      
         ST    R1,SAVR1                                                         
*                                  COUNT ENTRIES AND SUM WGTS                   
         SR    R3,R3                                                            
         SR    R4,R4                                                            
         L     R2,AIN                                                           
WL4      DS    0H                                                               
         CLI   0(R2),0             EOL                                          
         BE    WL5                                                              
         ZIC   R0,1(R2)                                                         
         AR    R4,R0               R4 = WGT SUM                                 
         LA    R3,1(R3)            R3 = COUNT                                   
         LA    R2,2(R2)                                                         
         B     WL4                                                              
*                                                                               
WL5      DS    0H                                                               
         ST    R4,WGTSUM                                                        
         LTR   R3,R3               TEST ANY                                     
         BZ    WLX                                                              
*                                  SORT DESCENDING ON WGT                       
         GOTO1 XSORT,DMCB,(C'D',AIN),(R3),2,1,1                                 
*                                  CLEAR OUTPUT AREA                            
         L     RE,AOUT                                                          
         LA    RF,1(R4)            LENGTH TO CLEAR = WGT SUM +1                 
         XCEF                                                                   
*                                                                               
         L     RF,AOUT                                                          
         AR    RF,R4                                                            
         BCTR  RF,R0                                                            
         ST    RF,ALAST            A(LAST ENTRY)                                
         EJECT                                                                  
*                                  DO ALLOCATION                                
         L     R2,AIN              START OF INPUT                               
WL8      DS    0H                                                               
         CLI   0(R2),0             EOL                                          
         BE    WL60                                                             
*                                                                               
         ZIC   RF,1(R2)                                                         
         ST    RF,BWGT             SAVE WGT                                     
         LTR   RF,RF                                                            
         BZ    WL20                SKIP IF NO WEIGHT                            
         LR    R1,R4               DIVIDEND IS REMAINING SPOTS                  
         M     R0,=F'100'                                                       
         BAS   RE,DIV              R1 = SKIP RATIO (2 DEC)                      
         ST    R1,SKIPRAT                                                       
*                                                                               
*                                                                               
         L     R3,AOUT             START OF OUTPUT LIST                         
         BCTR  R3,R0                                                            
         BAS   RE,FNDSLOT          FIND FIRST AVAILABLE SLOT                    
         SR    R7,R7               COUNT OF USES                                
         LA    R5,1                INTEGER SLOT COUNT                           
         LA    R6,100              2 DEC. SLOT COUNT                            
WL12     DS    0H                                                               
         MVC   0(1,R3),0(R2)       SET PRD CODE                                 
         LA    R7,1(R7)            BUMP USE COUNT                               
         C     R7,BWGT             IF = WGT                                     
         BNL   WL20                DONE WITH PRD                                
*                                                                               
         LR    R1,R6                                                            
         A     R1,SKIPRAT                                                       
         SR    R0,R0                                                            
         LA    RF,100                                                           
         BAS   RE,DIV                                                           
         LR    RF,R1                                                            
         SR    R1,R5               R1 = BCT FOR SLOTS TO COUNT DOWN             
         LR    R5,RF               R5 - NEW INTEGER                             
         A     R6,SKIPRAT          R6 - NEW DECIMAL                             
         BAS   RE,FNDSLOT                                                       
         BCT   R1,*-4                                                           
         B     WL12                                                             
*                                                                               
WL20     DS    0H                                                               
         S     R4,BWGT             NEW NO. SLOTS                                
         LA    R2,2(R2)            NEXT PRD                                     
         B     WL8                                                              
*                                                                               
WL60     DS    0H                                                               
WLX      DS    0H                                                               
         L     R1,SAVR1                                                         
         MVC   12(4,R1),WGTSUM     LENGTH OF OUTPUT LIST                        
         XIT1                                                                   
         SPACE 3                                                                
FNDSLOT  DS    0H                                                               
         C     R3,ALAST                                                         
         BNH   *+6                                                              
         DC    H'0'           SHOULD NOT RUN OUT OF ROOM                        
         LA    R3,1(R3)                                                         
         CLI   0(R3),0                                                          
         BER   RE             CC= 0  OK                                         
         B     FNDSLOT                                                          
         SPACE 3                                                                
DIV      DS    0H                                                               
         SLDA  R0,1                                                             
         DR    R0,RF                                                            
         LTR   R1,R1                                                            
         BNP   *+8                                                              
         A     R1,=F'1'                                                         
         SRA   R1,1                                                             
         BR    RE                                                               
*                                                                               
         SPACE 3                                                                
         LTORG                                                                  
         SPACE 3                                                                
WLWKD    DSECT                                                                  
*                                                                               
PARMS    DS    0XL24                                                            
AIN      DS    A                                                                
AOUT     DS    A                                                                
XSORT    DS    V                                                                
         DS    3F                                                               
*                                                                               
DMCB     DS    6F                                                               
ALAST    DS    A                                                                
BWGT     DS    F                                                                
WGTSUM   DS    F                                                                
SKIPRAT  DS    F                                                                
SAVR1    DS    F                                                                
         SPACE 3                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'007SPWGTLIST 05/01/02'                                      
         END                                                                    
