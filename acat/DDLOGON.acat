*          DATA SET DDLOGON    AT LEVEL 005 AS OF 10/27/20                      
*CATALP LOGON                                                                   
         TITLE 'LOGON - MODULE TO PRINT START AND END LOGOS'                    
         PRINT NOGEN                                                            
         WXTRN BOXAREA                                                          
         WXTRN DMGRCNT                                                          
         WXTRN MODLCNT                                                          
LOGO     CSECT                                                                  
         NMOD1 WORKX-WORKD,**LOGO**                                             
         USING WORKD,RC                                                         
         L     R9,0(R1)            R9=A(LOGO INFO BLOCK)                        
         USING LOGOD,R9                                                         
         TM    LOGOFLGS,LOGOFNO    EXIT IF SUPRESSING LOGOS                     
         BO    XIT                                                              
*                                                                               
         L     R8,=V(MASTC)        R8=A(MASTC CONTROL BLOCK)                    
         USING MASTD,R8                                                         
         LT    R7,=V(BOXAREA)      R7=A(BOXAREA)                                
         BZ    *+8                                                              
         USING BOXD,R7                                                          
         MVI   BOXOFF,C'Y'                                                      
         XC    LOGOSGTD,LOGOSGTD                                                
         L     R3,LOGOPAGE         R3=NUMBER OF PAGES TO PRINT                  
         STC   R3,NUMLOGOS                                                      
         MVI   SPACES,C' '                                                      
         MVC   SPACES+1(131),SPACES                                             
         MVC   P,SPACES                                                         
         MVC   WORD,SPACES                                                      
*                                                                               
         CLI   LOGOTYPE,C'M'       SKIP TO NEW PAGE IF START OR END             
         BNE   LOGOA                                                            
         CLI   LOGOSGCN,C'E'       SKIP TO NEW PAGE IF END OF SEGMENT           
         BNE   LOGOB                                                            
*                                                                               
LOGOA    GOTO1 LOGOPRNT,PARA,PFILL,=C'BC01'                                     
         CLI   MCREPTS,2                                                        
         BNE   LOGOB                                                            
         GOTO1 =V(PRINT2)          REPEAT FOR PRINT2                            
*                                                                               
LOGOB    CLI   LOGOTYPE,C'S'                                                    
         BE    START                                                            
         CLI   LOGOTYPE,C'M'                                                    
         BE    MIDDLE                                                           
         B     END                                                              
         EJECT                                                                  
***********************************************************************         
* CONTROL OF START LOGOS                                              *         
* THE LOCATION AND VALUE OF TEXT STRING C'FROM -' MUST NOT BE CHANGED *         
* IT MUST BE UPPER CASE. IT LOOKED FOR BY SCRIPTS TO FIND LOGO PAGES  *         
***********************************************************************         
START    OI    LOGOFLGS,LOGOFSS    SET WE HAVE START LOGOS                      
         CLI   NUMLOGOS,1          TEST ONLY ONE LOGO PAGE                      
         BNE   START0                                                           
         CLI   LOGOSTRP,C'Y'       TEST IF WANT DIVIDER STRIPES                 
         BNE   START0                                                           
         MVC   P(10),STRIPY        ONE TOP DIVIDER FOR SINGLE LOGO              
         MVC   P+10(100),P                                                      
*                                                                               
START0   MVI   LINE,2              NUMBER OF LINES BEFORE "FROM" LINE           
         BAS   RE,PRINT                                                         
         CLI   LOGODDSA,C'N'                                                    
         BE    START1                                                           
*&&US                                                                           
START0D  LTR   R8,R8               TALENT DOES NOT HAVE A MASTC                 
         BZ    START0DX                                                         
         CLI   MCOVSYS,7           SET COMPANY NAME FOR TALENT                  
         BNE   START0DX                                                         
         MVC   P+3(24),=C'FROM - Talent Partners  '                             
         B     START1                                                           
START0DX EQU   *                                                                
*&&                                                                             
START0E  MVC   P+3(24),=C'FROM - Mediaocean       '                             
         CLI   LOGODDSA,C'1'                                                    
         BNE   START1                                                           
         MVC   P+3(24),=C'FROM - EIX              '                             
*                                                                               
START1   BAS   RE,PRINT            FROM MEDIAOCEAN                              
         LA    R5,3                                                             
*&&UK*&& LA    R4,UKTAB                                                         
*&&US*&& LA    R4,USTAB                                                         
         CLI   LOGODDSA,C'N'                                                    
         BNE   *+8                                                              
         LA    R4,NOTAB                                                         
*                                                                               
START2   MVC   P+10(20),0(R4)      FROM ADDRESS (THREE LINES)                   
         BAS   RE,PRINT                                                         
         LA    R4,20(R4)                                                        
         BCT   R5,START2                                                        
*                                                                               
START3   MVI   LINE,2                                                           
         BAS   RE,PRINT                                                         
         MVC   P+35(4),=C'TO -'                                                 
         MVC   P+40(33),LOGONAME   TO NAME AND TO ADDRESS (TWO LINES)           
         MVI   LINE,1                                                           
         BAS   RE,PRINT                                                         
         MVC   P+40(33),LOGOADD                                                 
         MVI   LINE,1                                                           
         BAS   RE,PRINT                                                         
         MVC   P+40(33),LOGOADD2                                                
         MVI   LINE,1                                                           
         BAS   RE,PRINT                                                         
         BAS   RE,PRINT                                                         
*                                                                               
         MVC   P+40(33),LOGOADD3   ATTENTION LINE                               
         CLC   LOGOADD3,SPACES                                                  
         BE    *+10                                                             
         MVC   P+33(6),=C'ATTN -'                                               
         MVI   LINE,2                                                           
         BAS   RE,PRINT                                                         
         MVI   LINE,5                                                           
         BAS   RE,PRINT                                                         
*                                                                               
         MVC   WORD(7),LOGO1       BIG LOGOS                                    
         BAS   RE,BIG                                                           
         MVC   WORD(7),LOGO2                                                    
         BAS   RE,BIG                                                           
         MVC   WORD(8),LOGOJOB                                                  
         CLC   WORD(8),SPACES                                                   
         BNE   START4                                                           
         BAS   RE,GETJOB           GET JOB NAME                                 
*                                                                               
START4   CLI   LOGOEND,C'S'                                                     
         BNE   *+10                                                             
         MVC   WORD(8),=CL8'SUMMARY'                                            
         BAS   RE,BIG                                                           
         MVC   WORD(2),LOGOINFO                                                 
         CLC   WORD(2),SPACES                                                   
         BE    START4A                                                          
         TM    WORD,X'80'                                                       
         BO    START4A                                                          
         EDIT  (2,LOGOINFO),(5,WORD),ALIGN=LEFT                                 
START4A  BAS   RE,BIG                                                           
         BAS   RE,GETTIME                                                       
         ST    R1,LOGOSTRT                                                      
         B     ENDALL                                                           
         EJECT                                                                  
***********************************************************************         
* CONTROL OF MIDDLE LOGOS                                             *         
***********************************************************************         
MIDDLE   OI    LOGOFLGS,LOGOFSM    SET WE HAVE MIDDLE LOGOS                     
         MVI   LINE,7                                                           
         BAS   RE,PRINT                                                         
         MVC   WORD(3),=C'END'                                                  
         CLI   LOGOSGCN,C'S'                                                    
         BNE   *+10                                                             
         MVC   WORD(5),=C'START'                                                
         BAS   RE,BIG                                                           
*                                                                               
         MVC   WORD(7),=C'SEGMENT'                                              
         EDIT  (4,LOGOSEG),(3,WORD+8),ALIGN=LEFT                                
         BAS   RE,BIG                                                           
*                                                                               
         MVC   WORD(7),LOGO1                                                    
         BAS   RE,BIG                                                           
         MVC   WORD(7),LOGO2                                                    
         BAS   RE,BIG                                                           
         MVC   WORD(8),LOGOJOB                                                  
         CLC   WORD,SPACES                                                      
         BNE   MIDL2                                                            
         BAS   RE,GETJOB                                                        
*                                                                               
MIDL2    BAS   RE,BIG                                                           
         B     ENDALL                                                           
         EJECT                                                                  
***********************************************************************         
* CONTROL OF END LOGOS                                                *         
***********************************************************************         
END      OI    LOGOFLGS,LOGOFSE    SET WE HAVE END LOGOS                        
         MVI   LINE,2                                                           
         BAS   RE,PRINT                                                         
         MVC   WORD(3),=C'END'                                                  
         CLI   LOGOEND,C'P'                                                     
         BE    ENDA                                                             
         LTR   R8,R8                                                            
         BZ    ENDB                                                             
         CP    MCNDUMPS,=P'0'                                                   
         BNE   ENDA                                                             
         CLI   MCSTOP,C'R'         TEST STOP ON E-O-R                           
         BNE   ENDB                                                             
         MVC   WORD+4(4),=C'PART'                                               
         B     ENDD                                                             
*                                                                               
ENDA     MVC   WORD+4(4),=C'DUMP'                                               
         B     ENDD                                                             
*                                                                               
ENDB     CLC   LOGOSEG,=F'2'                                                    
         BL    ENDD                                                             
         MVC   WORD+4(4),=C'LAST'                                               
*                                                                               
ENDD     BAS   RE,BIG                                                           
         MVC   WORD(7),LOGO1                                                    
         BAS   RE,BIG                                                           
         MVC   WORD(7),LOGO2                                                    
         BAS   RE,BIG                                                           
         MVC   WORD(8),LOGOJOB                                                  
         CLI   LOGOEND,C'S'        SUMMARY FEATURE                              
         BNE   *+10                                                             
         MVC   WORD(8),=CL8'SUMMARY'                                            
         CLC   WORD,SPACES         LOOK UP JOB NAME FOR USER                    
         BNE   END2                                                             
         BAS   RE,GETJOB                                                        
*                                                                               
END2     BAS   RE,BIG                                                           
         MVI   LINE,2                                                           
         BAS   RE,PRINT                                                         
         CLI   LOGOEND,C'S'                                                     
         BNE   END4                                                             
         MVI   LINE,7                                                           
         BAS   RE,PRINT                                                         
         MVC   P+40(21),=C'NUMBER OF PACKAGES = '                               
         EDIT  (P4,LOGOREQS),(6,P+61),ALIGN=LEFT                                
         CP    LOGOREQS,=P'0'                                                   
         BNZ   *+10                                                             
         MVC   P(132),SPACES                                                    
         MVI   LINE,6                                                           
         BAS   RE,PRINT                                                         
         B     ENDALL                                                           
         EJECT                                                                  
***********************************************************************         
* REPORT OF JOB DETAILS IN END LOGOS                                  *         
***********************************************************************         
END4     EQU   *                                                                
*&&UK                                                                           
         MVI   LINE,13                                                          
         BAS   RE,PRINT                                                         
         B     ENDALL                                                           
*&&                                                                             
         MVC   P+10(85),REPHEADA                                                
         MVI   LINE,1                                                           
         BAS   RE,PRINT                                                         
         MVC   P+10(85),REPHEADB                                                
         MVI   LINE,1              WAS 2 BUT ONE LINE TOO MANY                  
         BAS   RE,PRINT                                                         
         C     R3,LOGOPAGE                                                      
         BNE   END8                                                             
         EJECT                                                                  
***********************************************************************         
* REPORT OF JOB DETAILS IN END LOGOS                                  *         
***********************************************************************         
         EDIT  (P4,LOGOREQS),(4,DA+48),ALIGN=LEFT                               
         MVC   DB+48(4),DA+48                                                   
         EDIT  (4,LOGORUNP),(6,DB+64),ALIGN=LEFT                                
         EDIT  (4,LOGORUNL),(7,DC+64),ALIGN=LEFT                                
         CLC   LOGOSEG,=F'1'                                                    
         BL    END6                                                             
         EDIT  (4,LOGOSEG),(4,DA+64),ALIGN=LEFT                                 
*                                                                               
END6     EXTRACT DUB2,FIELDS=TIOT  JOB NAME                                     
         L     R1,DUB2                                                          
         MVC   DD+9(8),0(R1)                                                    
         L     R1,LOGOSTRT         GET START TIME                               
         BAS   RE,PRTIME                                                        
         MVC   DB+9(8),WORK                                                     
         BAS   RE,GETTIME                                                       
         LR    R2,R1                                                            
         BAS   RE,PRTIME                                                        
         MVC   DC+9(8),WORK                                                     
         S     R2,LOGOSTRT                                                      
         BNM   END6A                                                            
         A     R2,=A(24*3600*100)  WENT PAST MIDNITE SO ADD 24 HOURS            
*                                                                               
END6A    LR    R1,R2                                                            
         BAS   RE,PRTIME                                                        
         MVC   DB+86(8),WORK                                                    
         EXTRACT ASIDFLD,'S',FIELDS=(ASID)                                      
         L     R2,ASIDFLD                                                       
         LOCASCB ASID=(R2)                                                      
         USING ASCB,R1                                                          
         L     R1,ASCBEJST         GET ELAPSED JOB STEP TIMING (SEC)            
         DROP  R1                                                               
         BAS   RE,PRTIMEA                                                       
         MVC   DA+86(8),WORK       MOVE IN EDITED TIME                          
         LTR   R8,R8                                                            
         BZ    END7                                                             
         MVC   DA+9(8),MCDATE      PUT DATE IN HEADING                          
         EDIT  (B4,MCACTIOS),(7,DD+86),ALIGN=LEFT                               
*                                                                               
END7     LTR   R8,R8               DETAILS FROM MASTC                           
         BZ    END8                                                             
         CP    MCNREQS,=P'0'                                                    
         BNZ   *+10                                                             
         ZAP   MCNREQS,MCREQNO                                                  
         EDIT  (P3,MCNREQS),(4,DA+48),ALIGN=LEFT                                
         EDIT  (P3,MCREQNO),(4,DB+48),ALIGN=LEFT                                
         ZAP   DUB,MCNREQS                                                      
         SP    DUB,MCREQNO                                                      
         CVB   R2,DUB                                                           
         EDIT  (R2),(4,DC+48),ALIGN=LEFT,ZERO=NOBLANK                           
         EDIT  (P3,MCNDUMPS),(4,DD+48),ALIGN=LEFT,ZERO=NOBLANK                  
*                                                                               
END8     ICM   RE,15,=V(DMGRCNT)   ADDRESS OF COUNTER                           
         BNZ   END8A                                                            
         ICM   RE,15,=V(MODLCNT)   ADDRESS OF COUNTER                           
         BZ    END8C                                                            
END8A    L     R0,0(RE)            GET NUMBER OF DMGR CALLS                     
         EDIT  (R0),(8,DC+86),ALIGN=LEFT                                        
*                                                                               
END8C    MVC   P+10(100),DA                                                     
         MVI   LINE,1                                                           
         BAS   RE,PRINT                                                         
         MVC   P+10(100),DB                                                     
         MVI   LINE,1                                                           
         BAS   RE,PRINT                                                         
         MVC   P+10(100),DC                                                     
         MVI   LINE,1                                                           
         BAS   RE,PRINT                                                         
         MVC   P+10(100),DD                                                     
         MVI   LINE,2                                                           
         BAS   RE,PRINT                                                         
         MVC   P+10(80),LOGOBRK                                                 
         MVI   LINE,1                                                           
         BAS   RE,PRINT                                                         
         MVC   P+10(80),LOGOBRK2                                                
         MVI   LINE,2                                                           
         BAS   RE,PRINT                                                         
         MVC   P+13(80),LOGOINFO                                                
         CLC   LOGOINFO(2),SPACES                                               
         BE    END8D                                                            
         TM    LOGOINFO,X'80'                                                   
         BO    END8D                                                            
         EDIT  (2,LOGOINFO),(5,P+10),ALIGN=LEFT                                 
END8D    MVI   LINE,1                                                           
         BAS   RE,PRINT                                                         
         MVC   P+10(80),LOGOSHP2                                                
         MVI   LINE,1                                                           
         BAS   RE,PRINT                                                         
*                                                                               
ENDALL   CLI   LOGOSTRP,C'Y'                                                    
         BE    END9                                                             
         BCT   R3,LOGOA            BACK TO REPEAT THIS PAGE AGAIN               
         B     END12                                                            
*                                                                               
END9     MVI   LINE,2                                                           
         CP    LOGOLINE,=P'68'                                                  
         BNE   *+8                                                              
         MVI   LINE,1              WAS 4 BUT ONE LINE TOO MANY                  
         LA    R4,6                VISUAL DIVIDERS                              
END9P    CLI   NUMLOGOS,1          TEST IF ONLY ONE LOGO PAGE                   
         BNE   END9Q                                                            
         LA    R4,3                YES-ONLY HALF OF DIVIDER LINES               
         BAS   RE,PRINT                                                         
         B     END10                                                            
END9Q    BAS   RE,PRINT            MULTIPLE LOGO PAGES                          
         CHI   R3,1                                                             
         BE    END12               NO DIVIDERS IF LAST LOGO PAGE                
*                                                                               
END10    MVC   P(10),STRIPY        SET STRIPE REPEAT PATTERN                    
         MVC   P+10(100),P                                                      
         BAS   RE,PRINT                                                         
         BCT   R4,END10                                                         
         CLI   NUMLOGOS,1          DONE IF ONLY ONE LOGO PAGE                   
         BE    END12                                                            
         BAS   RE,PRINT                                                         
         BCT   R3,LOGOB            BACK TO REPEAT THIS PAGE AGAIN               
*                                                                               
END12    CLI   LOGOSGCN,C'S'                                                    
         BNE   END14                                                            
         CLI   LOGOTYPE,C'M'                                                    
         BNE   END14                                                            
         MVC   P,SPACES                                                         
         GOTO1 LOGOPRNT,PARA,PFILL,=C'BC01'                                     
         CLI   MCREPTS,2                                                        
         BNE   END14                                                            
         GOTO1 =V(PRINT2)          REPEAT FOR PRINT2                            
*                                                                               
END14    XC    LOGOSGTD,LOGOSGTD                                                
         LT    R7,=V(BOXAREA)                                                   
         BZ    XIT                                                              
         MVI   BOXOFF,0                                                         
         B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
* ROUTINES TO EXTRACT JOB NAME AND CURRENT TIME                       *         
***********************************************************************         
GETJOB   ST    RE,PARA                                                          
         LA    R7,DUB                                                           
         EXTRACT (R7),FIELDS=TIOT                                               
         L     RE,DUB                                                           
         MVC   WORD(8),0(RE)                                                    
         L     RE,PARA                                                          
         BR    RE                                                               
*                                                                               
GETTIME  ST    RE,PARA                                                          
         TIME  BIN                                                              
         LR    R1,R0                                                            
         L     RE,PARA                                                          
         BR    RE                                                               
*                                                                               
PRTIME   SR    R0,R0                                                            
         D     R0,=F'100'                                                       
         SR    R0,R0               LEAVES SECONDS IN R1                         
PRTIMEA  SR    R0,R0                                                            
         D     R0,=F'3600'         GIVES HOURS IN R1                            
         CVD   R1,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  WORK(2),DUB                                                      
         MVI   WORK+2,C'.'                                                      
         SRDL  R0,32                                                            
         D     R0,=F'60'           GIVES MINUTES IN R1/SECS IN R0               
         CVD   R1,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  WORK+3(2),DUB                                                    
         MVI   WORK+5,C'.'                                                      
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  WORK+6(2),DUB                                                    
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO HANDLE BIG PRINTING                                      *         
***********************************************************************         
BIG      NTR1                                                                   
         LA    R2,BIGWORD                                                       
         LA    R3,10                                                            
*                                                                               
BIG2     MVC   0(110,R2),SPACES     CLEAR WORK AREA                             
         LA    R2,110(R2)                                                       
         BCT   R3,BIG2                                                          
         CLC   WORD,SPACES                                                      
         BE    BIG12                                                            
         LA    R3,BIGWORD          CENTER THE BIG PRINTING                      
         LA    R4,WORD+10                                                       
*                                                                               
BIG4     CLI   0(R4),C' '                                                       
         BNE   BIG6                                                             
         LA    R3,5(R3)                                                         
         BCT   R4,BIG4                                                          
*                                                                               
BIG6     LA    R2,WORD                                                          
         LA    R5,11                                                            
*                                                                               
BIG8     CLI   0(R2),C' '          GROW                                         
         BE    BIG10                                                            
         GOTO1 LOGOXPND,PARA,(R2),(102,(R3))                                    
*                                                                               
BIG10    LA    R2,1(R2)                                                         
         LA    R3,10(R3)                                                        
         BCT   R5,BIG8                                                          
*                                                                               
BIG12    LA    R2,BIGWORD          NOW PRINT EM                                 
         LA    R3,10                                                            
*                                                                               
BIG14    MVC   P(110),0(R2)                                                     
         BAS   RE,PRINT                                                         
         LA    R2,110(R2)                                                       
         BCT   R3,BIG14                                                         
         MVC   WORD,SPACES                                                      
         B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO PRINT                                                    *         
***********************************************************************         
PRINT    NTR1                                                                   
         MVC   CONTROL(2),=C'BL'                                                
         LLC   R2,LINE                                                          
         CVD   R2,DUB                                                           
         UNPK  CONTROL+2(2),DUB                                                 
         OI    CONTROL+3,X'F0'                                                  
         GOTO1 LOGOPRNT,PARA,PFILL,CONTROL                                      
         CLI   MCREPTS,2                                                        
         BNE   PRINT9                                                           
         GOTO1 =V(PRINT2)          REPEAT FOR PRINT2                            
PRINT9   MVI   LINE,1                                                           
         MVC   P,SPACES                                                         
*                                                                               
XIT      XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
* TABLES AND CONSTANTS                                                *         
***********************************************************************         
         LTORG                                                                  
*                                                                               
STRIPY   DS    0CL10                                                            
*&&UK*&& DC    CL10'*|*|*|*   '                                                 
*&&US*&& DC    CL10'XXXXX     '                                                 
*                                                                               
NOTAB    DC    CL20' '                                                          
         DC    CL20' '                                                          
         DC    CL20' '                                                          
*                                                                               
USTAB    DC    CL20'45 West 18th Street'                                        
         DC    CL20'New York'                                                   
         DC    CL20'NY 10011'                                                   
*                                                                               
UKTAB    DC    CL20'110 Southwark Street'                                       
         DC    CL20'London'                                                     
         DC    CL20'SE1 0TA'                                                    
*                                                                               
REPHEADA DC    C'DETAILS OF JOB      JES3            REQUESTS'                  
         DC    C'            PRINTING          ACCOUNTING    '                  
REPHEADB DC    C'--------------      -----           --------'                  
         DC    C'            --------          ----------    '                  
DA       DC    C'DATE  =             CLASS =         SCHEDULED ='               
         DC    C' 0       SEGS. = 1         CPU TIME =                '         
DB       DC    C'START =             COPY  =         PROCESSED ='               
         DC    C' 0       PAGES =           ELAPSED  =                '         
DC       DC    C'END   =             PRI   =         REMAINING ='               
         DC    C' 0       LINES =           DM COUNT =                '         
DD       DC    C'MVS   =             FORM  =         PARTIALS  ='               
         DC    C' 0                         START IO =                '         
*                                                                               
DUB2     DC    D'0'                                                             
ASIDFLD  DC    F'0'                                                             
         EJECT                                                                  
***********************************************************************         
* DSECT FOR LOGO WORKING STORAGE                                      *         
***********************************************************************         
WORKD    DSECT                                                                  
DUB      DS    D                                                                
WORK     DS    CL32                                                             
PARA     DS    6F                                                               
NUMLOGOS DS    X                                                                
PFILL    DS    CL1                                                              
P        DS    CL132                                                            
SPACES   DS    CL132                                                            
CONTROL  DS    CL4                                                              
LINE     DS    X                                                                
WORD     DS    CL11                                                             
BIGWORD  DS    1100C                                                            
WORKX    EQU   *                                                                
         EJECT                                                                  
***********************************************************************         
* OTHER SYSTEM DSECTS                                                 *         
***********************************************************************         
* DDMASTD                                                                       
       ++INCLUDE DDMASTD                                                        
* DDLOGOD                                                                       
       ++INCLUDE DDLOGOD                                                        
* DDBIGBOX                                                                      
       ++INCLUDE DDBIGBOX                                                       
* IHAASCB                                                                       
         PRINT NOGEN                                                            
         IHAASCB                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'005DDLOGON   10/27/20'                                      
         END                                                                    
