*          DATA SET REPRTCOV   AT LEVEL 087 AS OF 01/23/04                      
*CATALP REPRTCOV                                                                
***********************************************************************         
*                                                                     *         
*         REPRTCOV --- GENERATE PRINTED CONTRCT COVERSHEET OUTPUT     *         
*                                                                     *         
* ------------------------------------------------------------------- *         
* UPDATE HISTORY:                                                     *         
*                                                                     *         
* 27APR98 (RHV) --- ORIGINATION DATE                                  *         
*                                                                     *         
*                                                                     *         
***********************************************************************         
         TITLE 'REPRTCOV - FORMAT CONTRACT COVERSHEET'                          
***********************************************************************         
*  INPUT:                                                                       
*     PARAMETER 1 = BYTE 0 = PRINTED LINE MAX WIDTH  (68<LINE<132)              
*                   BYTES 1-3 = A(RCONREC)                                      
*                                                                               
*               2 = BYTE 0 - SVHEADCD                                           
*                   BYTE 1-3 A(BLOCK):                                          
*                   BLOCK= A(SPOOLD)                                            
*                          A(DATAMGR)                                           
*                          A(REPFACS)                                           
*                          A(IO AREA OF MAX REC SIZE (4K))                      
*                          A(SPOOL)                                             
*                          A(GEND OF CALLING MODULE)                            
*                          A(REGENSTC)                                          
*                          A(GETTXT)                                            
*                                                                               
***********************************************************************         
REPRTCOV CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,*PRTCOV*                                             
         USING WORKD,RC                                                         
*                                                                               
         ICM   R9,7,1(R1)         RESOLVE PARAMETER LIST ADDRESSES              
         USING RCONREC,R9                                                       
         MVC   COVWIDE,0(R1)                                                    
         MVC   HEADCD,4(R1)                                                     
         ICM   RF,7,5(R1)          A(INPUT) BLOCK                               
         ICM   R8,15,0(RF)                                                      
         USING SPOOLD,R8                                                        
         MVC   DATAMGR,4(RF)                                                    
         MVC   VREPFACS,8(RF)                                                   
         MVC   AIO,12(RF)                                                       
         MVC   SPOOL,16(RF)                                                     
         MVC   AGEND,20(RF)                                                     
         MVC   VGENSTC,24(RF)                                                   
         MVC   VGETTXT,28(RF)                                                   
         MVC   DATADISP,=H'34'                                                  
*                                                                               
         CLI   COVWIDE,68                                                       
         BNL   *+6                                                              
         DC    H'0'                LINE TOO NARROW                              
         CLI   COVWIDE,132                                                      
         BNH   *+6                                                              
         DC    H'0'                LINE TOO WIDE                                
*                                                                               
         LA    R6,RCONREC                                                       
         CLI   RCONREC,X'0C'                                                    
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         MVI   ELCODE,X'1F'                                                     
         BAS   RE,GETEL                                                         
         BNE   COVER010                                                         
         USING RCONXEL,R6                                                       
         TM    RCONCONF,X'40'      CONFIRMED NOW?                               
         BO    COVER020            YES - GEN COVERSHEET                         
         DROP  R6                                                               
*                                                                               
COVER010 DS    0H                                                               
         LA    R6,RCONREC                                                       
         MVI   ELCODE,X'20'                                                     
         BAS   RE,GETEL                                                         
         BNE   COVER020                                                         
         USING RCONSEND,R6                                                      
         TM    RCONSENF,X'40'      LAST SENT BY STATION?                        
         BO    COVERX              YES - SKIP COVERSHEET                        
         DROP  R6                                                               
*                                                                               
COVER020 DS    0H                                                               
         LA    R6,RCONREC                                                       
         MVI   ELCODE,X'A6'                                                     
         BAS   RE,GETEL                                                         
         BNE   COVERX                                                           
*                                                                               
         USING RCONCVEL,R6                                                      
         XC    KEY,KEY                                                          
         LA    R2,KEY                                                           
         USING RCOVREC,R2                                                       
         GOTOX (RFCONLOW,VREPFACS),DMCB,RCONREC  GET LOWEST K NUMBER            
         MVI   KEY,X'49'                                                        
         MVC   RCOVKREP,RCONKREP                                                
         MVC   RCOVKNAM,RCONCVNM                                                
         NI    COVERFLG,X'FF'-X'20'                                             
COVER030 DS    0H                                                               
         MVC   KEYSAVE,KEY                                                      
         GOTO1 DATAMGR,DMCB,=C'DMRDHI',=C'REPDIR',KEYSAVE,KEY                   
         CLC   KEY(L'RCONKEY),KEYSAVE                                           
         BE    COVER045                                                         
*** SPECIAL CODING FOR ALL/MG CONVERSION                                        
         TM    COVERFLG,X'20'                                                   
         BO    COVER040            DONE THIS ALREADY                            
         OI    COVERFLG,X'20'                                                   
*                                                                               
         CLC   RCONKREP(2),=C'MG'  ONLY FOR REP MG                              
         BNE   COVER040                                                         
         MVC   RCOVKREP,RCONKREP                                                
         MVC   RCOVKNAM,RCONCVNM                                                
         MVI   RCOVKNAM+4,1       ADD 1 MILLION                                 
         CLC   RCOVKNAM+4(4),RCONKCON                                           
         BNE   COVER040                                                         
         B     COVER030            LOOP BACK                                    
COVER040 DS    0H                                                               
         DC    H'0'                                                             
COVER045 DS    0H                                                               
*** SPECIAL CODING FOR ALL/MG CONVERSION                                        
         GOTO1 DATAMGR,DMCB,=C'GETREC',=C'REPFILE',KEY+28,AIO,DMWORK,0          
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
         L     R2,AIO                                                           
         DROP  R6                                                               
*                                                                               
         MVI   WPCOVFLG,0          INIT FLAGS                                   
         TM    RCOVFLAG,RCOVFLAF                                                
         BZ    *+8                                                              
         OI    WPCOVFLG,X'80'      AUTO FORMAT ON                               
*                                                                               
         ZIC   R3,COVWIDE                                                       
         BCTR  R3,0                                                             
         EX    R3,*+8                                                           
         B     *+10                                                             
         MVC   P(0),=132C'*' SECTION HEADER                                     
         LA    R3,1(R3)                                                         
         SH    R3,=Y(L'COVHEAD)                                                 
         SRL   R3,1                                                             
         LA    R4,P                                                             
         AR    R4,R3                                                            
         MVC   0(L'COVHEAD,R4),COVHEAD                                          
         MVI   P2,0                                                             
         BAS   RE,GOSPOOL                                                       
*                                                                               
         LA    R6,RCOVEL1          POINT TO 1ST ELEM IN RCOVREC                 
         DROP  R2                                                               
*                                                                               
COVER050 DS    0H                                                               
         BAS   RE,GETCOV           GET NEXT COVER TEXT ELEM                     
         LTR   R6,R6               HAVE ANOTHER ELEMENT?                        
         BZ    COVER200            NO -WRAP IT UP                               
*                                                                               
         CLC   =C'SC=',2(R6)       SFM CMT?                                     
         BE    *+14                                                             
         CLC   =C'C=',2(R6)        STD CMT?                                     
         BNE   COVER054                                                         
         BAS   RE,STDCMT                                                        
         B     COVER050                                                         
*                                                                               
COVER054 DS    0H                                                               
         CLI   1(R6),2             PRINTING BLANK LINE?                         
         BH    COVER060            NO, NEXT CASE                                
         TM    WPCOVFLG,X'80'      AUTO FORMAT ON                               
         BZ    COVER055                                                         
         CLC   P,SPACES            ANYTHING ON LINE?                            
         BE    COVER055                                                         
         BAS   RE,GOSPOOL                                                       
COVER055 DS    0H                                                               
         BAS   RE,GOSPOOL                                                       
         B     COVER050            NEXT ELEMENT                                 
*                                                                               
COVER060 DS    0H                                                               
         ZIC   R3,1(R6)                                                         
         SH    R3,=H'2'            LENGTH FOR CURRENT DATA                      
         LA    R5,2(R6)            A(CURRENT DATA)                              
         MVC   FULL,0(R5)                                                       
         OC    FULL,SPACES         KEEP THE 1ST 4 CHAR IN CAPS                  
*                                                                               
         CH    R3,=H'2'            CHECK FOR '$P' PAGE BREAK                    
         BNE   COVER070                                                         
         CLC   =C'$P',FULL                                                      
         BNE   COVER070                                                         
         MVI   FORCEHED,C'Y'                                                    
         BAS   RE,GOSPOOL                                                       
         B     COVER050                                                         
*                                                                               
COVER070 DS    0H                  CHECK FOR '$ON' CONTROL                      
         CH    R3,=H'3'                                                         
         BNE   COVER080                                                         
         CLC   =C'$ON',FULL                                                     
         BNE   COVER080                                                         
         OI    WPCOVFLG,X'80'      AUTO FORMAT ON                               
         B     COVER050                                                         
*                                                                               
COVER080 DS    0H                  CHECK FOR '$OFF' CONTROL                     
         CH    R3,=H'4'                                                         
         BNE   COVER090                                                         
         CLC   =C'$OFF',FULL                                                    
         BNE   COVER090                                                         
         NI    WPCOVFLG,X'FF'-X'80' AUTO FORMAT OFF                             
         CLC   P,SPACES            ANYTHING ALREADY ON LINE?                    
         BE    COVER090            NO - OK                                      
         BAS   RE,GOSPOOL                                                       
         B     COVER050                                                         
*                                                                               
COVER090 DS    0H                  HANDLE LINE TEXT                             
         TM    WPCOVFLG,X'80'      AUTO FORMAT ON?                              
         BZ    COVER150            NO                                           
*                                                                               
COVER100 DS    0H                  FIGURE OUT HOW MUCH SPACE LEFT               
         LA    RF,P                START OF PRINT LINE                          
         ZIC   R2,COVWIDE                                                       
         AR    R2,RF               END OF PRINT LINE                            
         SR    R4,R4               CLEAR COUNTER                                
         BCTR  R2,0                BACK UP 1 CHAR                               
         CLI   0(R2),C' '          SPACE ?                                      
         BNE   *+18                                                             
         LA    R4,1(R4)                                                         
         CR    R2,RF               EMPTY LINE?                                  
         BE    COVER105            GO MOVE TEXT TO LINE                         
         B     *-20                                                             
*                                                                               
         CH    R4,=H'2'            AT LEAST 2 SPACES ON LINE                    
         BL    COVER120            NO - FLUSH LINE & GET NEW ONE                
***>     LTR   R4,R4               AT LEAST 1 SPACE ON LINE?                    
***>     BZ    COVER120            NO - FLUSH LINE & GET NEW ONE                
*                                                                               
         BCTR  R4,0                LEAVE A SPACE BEFORE INSERTION               
         LA    R2,2(R2)            A(INSERTION POINT)                           
*                                                                               
         CR    R3,R4               TEXT LEN VS SPACE LEFT                       
         BH    COVER110            CAN'T FIT ALL TEXT                           
*                                                                               
COVER105 DS    0H                                                               
         BCTR  R3,0                                                             
         EX    R3,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),0(R5)       TEXT TO LINE                                 
         B     COVER050            NEXT ELEMENT                                 
*                                                                               
COVER110 DS    0H                                                               
         LA    R5,0(R4,R5)         MAX DISPL INTO DATA WE CAN TAKE              
         LA    RE,2(R6)                                                         
         CLI   0(R5),C' '                                                       
         BE    *+18                                                             
         BCTR  R4,0                                                             
         BCTR  R5,0                                                             
         CR    R5,RE                                                            
         BNH   COVER120            DON'T HAVE ANYTHING THAT FITS                
         B     *-18                                                             
*                                                                               
         BCTR  R4,0                DO PARTIAL MOVE TO PRINT LINE                
         EX    R4,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),2(R6)                                                    
         BAS   RE,GOSPOOL                                                       
*                                                                               
         LA    R5,1(R5)            START OF REMAINDER OF TEXT                   
         LA    R4,2(R4)            ALLOW FOR PREV EX AND SPACE                  
         SR    R3,R4               LENGTH OF REMAINING TEXT                     
         BCTR  R3,0                                                             
         EX    R3,*+8                                                           
         B     *+10                                                             
         MVC   P(0),0(R5)          MOVE REST OF TEXT TO PRINT LINE              
         B     COVER050            GET NEXT ELEM                                
*                                                                               
COVER120 DS    0H                  NEED NEW LINE                                
         BAS   RE,GOSPOOL                                                       
         BCTR  R3,0                                                             
         EX    R3,*+8                                                           
         B     *+10                                                             
         MVC   P(0),0(R5)          START NEW LINE                               
         B     COVER050            GET NEXT ELEM                                
                                                                                
*                                                                               
COVER150 DS    0H                  HANDLE NON-AUTOFORMAT LINE                   
         BCTR  R3,0                                                             
         EX    R3,*+8                                                           
         B     *+10                                                             
         MVC   P(0),0(R5)                                                       
         BAS   RE,GOSPOOL                                                       
         B     COVER050            NEXT ELEMENT                                 
*                                                                               
COVER200 DS    0H                  CLOSE COVERSHEET SECTION                     
         TM    WPCOVFLG,X'80'      AUTO FORMAT ON?                              
         BZ    COVER210                                                         
         CLC   P,SPACES            ANYTHING LEFT ON LINE?                       
         BE    COVER210                                                         
         BAS   RE,GOSPOOL                                                       
COVER210 DS    0H                                                               
         ZIC   RF,COVWIDE                                                       
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   P(0),=132C'*'                                                    
         BAS   RE,GOSPOOL                                                       
         MVI   FORCEHED,C'Y'                                                    
         MVC   PAGE,=H'1'                                                       
         MVC   RCSUBPRG,HEADCD     RESTORE ORIGINAL CODE                        
COVERX   XIT1                                                                   
*                                                                               
COVHEAD  DC    C' CONTRACT COVERSHEET '                                         
*                                                                               
***********************************************************************         
* GETCOV - POINTS R6 TO NEXT ELEMENT IN RECORD SET R6=0 WHEN END OF REC         
***********************************************************************         
GETCOV   DS    0H                                                               
         ST    RE,FULL             SAVE RE FOR RETURN                           
*                                                                               
         MVI   ELCODE,3                                                         
         BAS   RE,NEXTEL           NEXT TEXT ELEM                               
         BE    GETCOVX             GOT IT, ALL DONE                             
*                                                                               
         L     R1,AIO                                                           
         USING RCOVREC,R1                                                       
         MVC   KEY(27),RCOVKEY     GET NEXT COVER RECORD IN SET                 
         ZIC   RE,RCOVKSEQ                                                      
         LA    RE,1(RE)                                                         
         STC   RE,KEY+26                                                        
         MVC   KEYSAVE,KEY                                                      
         GOTO1 DATAMGR,DMCB,=C'DMRDHI',=C'REPDIR',KEYSAVE,KEY                   
         CLC   KEY(27),KEYSAVE     ANOTHER RECORD IN SET?                       
         BE    GETCOV30            YES - PROCESS IT                             
         SR    R6,R6               NO - RETURN R6=0                             
         B     GETCOVX             RETURN                                       
*                                                                               
GETCOV30 DS    0H                                                               
         GOTO1 DATAMGR,DMCB,=C'GETREC',=C'REPFILE',KEY+28,AIO,DMWORK,0          
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
         L     R1,AIO                                                           
         LA    R6,RCOVEL1          1ST ELEM IN REC                              
         CLI   0(R6),3                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
GETCOVX  DS    0H                                                               
         L     RE,FULL                                                          
         BR    RE                                                               
         DROP  R1                                                               
         B     COVERX                                                           
*                                                                               
* EXPLODE STANDARD COMMENTS                                                     
*                                                                               
STDCMT   NTR1                                                                   
         CLC   P,SPACES            ANYTHING ON LINE?                            
         BE    *+8                                                              
         BAS   RE,GOSPOOL          YES, FLUSH IT                                
*                                                                               
         ZIC   R1,1(R6)                                                         
         AHI   R1,-3                                                            
         EX    R1,*+4                                                           
         MVC   P(0),2(R6)          DEFAULT PRINT ACTUAL TEXT                    
*                                                                               
         LA    R3,CMTIO                                                         
         GOTO1 VGENSTC,DMCB,(2,(R6)),(R3),DATAMGR,RCONREC,VGETTXT               
         BNZ   OCM090              COMMENT NOT FOUND                            
         CLI   0(R3),0             IF NULL, PRINT FREE FORM COMMENT             
         BE    OCM090                                                           
*                                                                               
OCM070   DS    0H                                                               
         CLI   0(R3),X'FF'         DONE                                         
         BNE   OCM075                                                           
         ZIC   R1,1(R6)                                                         
         AR    R6,R1               NEXT ELEM                                    
         B     OCMX                                                             
OCM075   DS    0H                                                               
         ZIC   R1,0(R3)                                                         
         AHI   R1,-2                                                            
         EX    R1,*+4                                                           
         MVC   P(0),1(R3)                                                       
         BAS   RE,GOSPOOL                                                       
         LA    R3,2(R1,R3)         NEXT LINE                                    
         LA    RF,CMTIO                                                         
         AHI   RF,L'CMTIO                                                       
         CR    R3,RF               BOUNDARY CHECK                               
         BH    OCMX                OUT OF BOUNDS - GET OUT                      
         B     OCM070              NEXT LINE                                    
*                                                                               
OCM090   DS    0H                                                               
         BAS   RE,GOSPOOL                                                       
OCMX     XIT1                                                                   
**********************************************************************          
* GOSPOOL - CALL SPOOL                                                          
**********************************************************************          
GOSPOOL  NTR1                                                                   
***>     GOTO1 SPOOL,DMCB,SPOOLD                                                
         LA    R1,DMCB                                                          
         LA    RE,SPOOLD                                                        
         ST    RE,0(R1)                                                         
         L     RF,SPOOL                                                         
         L     RC,AGEND            POINT TO CALLER'S WORKIG STORAGE             
         BASR  RE,RF                                                            
         B     COVERX                                                           
         EJECT                                                                  
*                                                                               
         GETEL R6,DATADISP,ELCODE                                               
         EJECT                                                                  
WORKD    DSECT                                                                  
DMWORK   DS    12D                                                              
KEY      DS    CL32                                                             
KEYSAVE  DS    CL32                                                             
DMCB     DS    6F                                                               
DUB      DS    D                                                                
FULL     DS    F                                                                
DATAMGR  DS    A                                                                
VREPFACS DS    A                                                                
AIO      DS    A                                                                
SPOOL    DS    A                                                                
VGENSTC  DS    A                                                                
VGETTXT  DS    A                                                                
AGEND    DS    F                                                                
HEADCD   DS    X                                                                
DATADISP DS    H                                                                
ELCODE   DS    X                                                                
WPCOVFLG DS    X                                                                
COVWIDE  DS    X                                                                
WORK     DS    XL132                                                            
ELEM     DS    XL256                                                            
COVERFLG DS    X                                                                
CMTIO    DS    CL4096              COMMENT I/O AREA                             
WORKX    EQU   *                                                                
*                                                                               
RCOND    DSECT                                                                  
       ++INCLUDE REGENCON                                                       
       ++INCLUDE REGENCOV                                                       
       ++INCLUDE REPFACSQ                                                       
       ++INCLUDE DDSPOOLD                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'087REPRTCOV  01/23/04'                                      
         END                                                                    
