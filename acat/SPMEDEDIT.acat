*          DATA SET SPMEDEDIT  AT LEVEL 121 AS OF 10/22/19                      
*CATALP MEDEDIT                                                                 
         TITLE 'ROUTINE TO EDIT A MEDIA SUMMARY LINE'                           
VMDEDIT  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 51,**EDIT**                                                      
         USING EDITD,RC                                                         
         L     RA,0(R1)                                                         
         LA    R9,2048(RA)                                                      
         LA    R9,2048(R9)                                                      
         USING SPWORKD,RA,R9                                                    
         MVC   P,SPACES                                                         
         L     R8,MEDBUFF                                                       
         USING MEDBLOCK,R8                                                      
         ST    R1,SAVER1                                                        
         MVI   WAVESW,C'N'         WEIGHTED AVERAGE SWITCH                      
         MVI   SLDET,0                                                          
         MVI   OPTION,C'R'                                                      
         CLI   SPOTPROF,C'D'       CHANGE TO D IF DOLLAR EQUIVALENCING          
         BNE   *+8                                                              
         MVI   OPTION,C'D'                                                      
         MVC   WEIGHT,4(R1)        SET WEIGHT                                   
         OC    MEDADDWT,MEDADDWT                                                
         BZ    *+10                                                             
         MVC   WEIGHT,SPWEIGHT                                                  
         EJECT                                                                  
*              FIRST BUILD THE BRAND RATING/IMP LIST                            
                                                                                
ME1      SR    R2,R2                                                            
         IC    R2,MEDBRAND                                                      
         CLI   MEDBRAND,X'FF'                                                   
         BNE   *+8                                                              
         LA    R2,220                                                           
         BCTR  R2,0                                                             
         MH    R2,PRDBUFLN                                                      
         A     R2,PRDBUFF          KEY INTO BRAND BUFFER                        
         LA    R2,28(R2)                                                        
         LA    R3,LIST                                                          
         LA    R4,14                                                            
         XC    LIST,LIST                                                        
         EJECT                                                                  
ME2      OC    0(3,R2),0(R2)       TEST MORE DEMOS                              
         JZ    ME10                NO - DONE                                    
*                                                                               
         CLI   0(R2),0             TEST NONT-DEMO                               
         BNE   *+12                IF BYTE 0 NEQ 0,THEN NORMALIZED              
         TM    RQOPT2,RQOPT2_NONTDEM   TEST NONT DEMOS NORMALIZED               
         JZ    ME3                     NO                                       
         CLI   0(R2),C'R'                                                       
         JE    *+12                                                             
         CLI   0(R2),C'E'                                                       
         JNE   *+8                                                              
         MVI   0(R3),C'R'                                                       
         J     ME8                                                              
*                                                                               
ME3      CLI   2(R2),0             TEST NONT DEMO                               
         JNE   ME4                 NO                                           
         LLC   R0,1(R2)            GET NONT DEMO INDEX                          
         BCTR  R0,0                                                             
         SLL   R0,3                X 8                                          
         L     RE,VNONTNMS                                                      
         AR    RE,R0                                                            
         CLI   0(RE),C'R'                                                       
         JE    *+12                                                             
         CLI   0(RE),C'E'                                                       
         JNE   *+8                                                              
         MVI   0(R3),C'R'                                                       
         J     ME8                                                              
*                                                                               
ME4      CLI   1(R2),C'E'          CHECK FOR EXTENDED                           
         JE    *+12                                                             
         CLI   1(R2),C'R'                                                       
         JNE   *+8                                                              
         MVI   0(R3),C'R'                                                       
*                                                                               
         CLC   0(2,R2),=X'00C1'    CHECK FOR WGTD OR USER DEMO                  
         BNL   *+8                                                              
         BAS   RE,GETDNAM          GET DEMO NAME FOR USER OR WGTD               
*                                                                               
ME8      LA    R2,3(R2)                                                         
         LA    R3,1(R3)                                                         
         BCT   R4,ME2                                                           
*                                                                               
ME10     LA    R3,LIST             PASS USER A(LIST)                            
         L     R1,SAVER1                                                        
         ST    R3,0(R1)                                                         
                                                                                
* NOW CONTROL THE REPORTING                                                     
                                                                                
         L     R2,BUFFIO                                                        
         L     R3,MEDTABLE                                                      
         L     R4,BUFFBUFF                                                      
         USING BUFFALOD,R4                                                      
         L     R5,BUFFLKEY         THE REPORT NUMBER IS LAST BYTE               
         AR    R5,R2               OF BUFFALO KEY - FIND IT                     
         BCTR  R5,0                                                             
*                                                                               
         SR    R6,R6               THIS INDEXES US INTO REPORT TABLE            
         IC    R6,0(R5)                                                         
         BCTR  R6,0                                                             
         SLL   R6,2                                                             
         AR    R6,R3                                                            
         L     R3,0(R6)                                                         
         TM    0(R6),X'80'         CHECK ITS ACTIVE                             
         JO    XIT                                                              
*                                                                               
         L     R1,SAVER1           R3 NOW HAS A(ROW DEF)                        
         MVC   0(1,R1),8(R3)       PASS USER BACK PRINT CHARACTER               
         L     R3,0(R3)            R3 NOW HAS A(PRINT DEF)                      
         SPACE 2                                                                
ME12     CLI   0(R3),X'FF'                                                      
         JE    XIT                                                              
         TM    7(R3),X'80'         IS THIS ELEMENT ACTIVE                       
         BO    ME14                                                             
         ST    R3,AELEMNT                                                       
         BAS   RE,SETPARAS         YES SO SET UP PARAMETERS                     
         SR    R5,R5               FIRST BYTE IS EDIT ROUTINE NUMBER            
         IC    R5,0(R3)                                                         
         BCTR  R5,0                                                             
         SLL   R5,2                                                             
         BAS   RE,BRANCHER         OFF WE GO TO SPECIFIC ROUTINE                
         SPACE 2                                                                
ME14     LA    R3,8(R3)                                                         
         B     ME12                                                             
         EJECT                                                                  
*              ROUTINE TO SET UP PARAMETER LIST FOR EDITORS                     
                                                                                
SETPARAS NTR1  LABEL=NO                                                         
         XC    EDPARAS(24),EDPARAS                                              
         SR    R5,R5                                                            
         IC    R5,6(R3)            THIS BYTE HAS PRINT COLUMN NUMBER            
         BCTR  R5,0                                                             
         LA    R5,P(R5)                                                         
         ST    R5,EDPARAS                                                       
         LA    R3,1(R3)                                                         
         LA    R5,EDPARAS+4                                                     
         LA    R6,5                                                             
         SPACE 2                                                                
SP2      SR    R7,R7                                                            
         IC    R7,0(R3)            COLUMN NUMBER OR KEY BYTE                    
         LTR   R7,R7                                                            
         BZ    SP6                                                              
         SLL   R7,25                                                            
         SRL   R7,25                                                            
         TM    0(R3),X'80'                                                      
         BO    SP4                                                              
         BCTR  R7,0                COLUMN NUMBER                                
         SLL   R7,2                                                             
         A     R7,BUFFLKEY         BUMP PAST KEY                                
         AR    R7,R2                                                            
         ST    R7,0(R5)                                                         
         B     SP6                                                              
         SPACE 2                                                                
SP4      BCTR  R7,0                KEY BYTE                                     
         AR    R7,R2                                                            
         ST    R7,0(R5)                                                         
         SPACE 2                                                                
SP6      LA    R3,1(R3)                                                         
         LA    R5,4(R5)                                                         
         BCT   R6,SP2                                                           
         J     XIT                                                              
         EJECT                                                                  
*              BRANCH CONTROL                                                   
                                                                                
BRANCHER NTR1  LABEL=NO                                                         
         LR    RF,R5                                                            
         LM    R2,R7,EDPARAS                                                    
         B     BRANCH(RF)                                                       
         SPACE 2                                                                
XIT      XIT1                                                                   
                                                                                
*              ADDRESS LIST FOR ROUTINES                                        
                                                                                
BRANCH   DS    0F                                                               
         B     ME20                BRAND                                        
         B     ME24                MARKET                                       
         B     ME28                DAYPART                                      
         B     ME40                NUMBER                                       
         B     ME44                NUMBER3                                      
         B     ME48                NUMBER14                                     
         B     ME52                MONTHLY                                      
         B     ME54                CPM                                          
         B     ME56                AVERAGE                                      
         B     ME60                PERCENT                                      
         B     ME62                IMPS                                         
         B     ME64                IMPS3                                        
         J     XIT                 DEMONAME                                     
         B     ME66                WEEK                                         
         B     ME68                MONTH                                        
         B     ME70                LITERAL                                      
         B     ME74                WAVE                                         
         EJECT                                                                  
*              SPECIFIC PRINT ROUTINES - BRAND & MARKET                         
                                                                                
ME20     SR    R4,R4               BRAND                                        
         IC    R4,0(R3)                                                         
         LTR   R4,R4                                                            
         JZ    XIT                                                              
         CH    R4,=H'255'                                                       
         BNE   *+8                                                              
         LA    R4,220                                                           
         BCTR  R4,0                                                             
         MH    R4,PRDBUFLN                                                      
         A     R4,PRDBUFF                                                       
         MVC   0(3,R2),1(R4)       BBB - X(24) (PRIMARY)                        
         MVI   4(R2),C'-'                                                       
         MVC   6(24,R2),4(R4)                                                   
         MVC   31(9,R2),=C'(UNKNOWN)'                                           
ME20NDEM L     RE,ADBLOCK          GET NEW FORMAT DEMO NAMES                    
         USING DBLOCK,RE                                                        
         XC    0(256,RE),0(RE)                                                  
         MVC   DBCOMFCS,ACOMFACS                                                
         MVC   DBFILE,=C'TP '                                                   
         MVI   DBSELMED,C'T'                                                    
         DROP  RE                                                               
         L     RE,ADEST                                                         
         USING ESTHDR,RE                                                        
         GOTO1 DEMOCON,MYPARA,(1,28(R4)),(2,32(R2)),(C'S',ADBLOCK),    X        
               EUSRNMS                                                          
         DROP  RE                                                               
         XIT                                                                    
         SPACE 2                                                                
ME22     GOTO1 SQUASHER,MYPARA,(R2),41                                          
         J     XIT                                                              
         SPACE 2                                                                
ME24     L     R3,ADMARKET                                                      
         USING MKTREC,R3                                                        
         MVC   0(4,R2),MKTKMKT     MMMM - X(24) (WT)                            
         MVI   5(R2),C'-'                                                       
         MVC   7(24,R2),MKTNAME                                                 
         PRINT GEN                                                              
*---->   EDIT  (C4,MKTWT),(6,33(R2)),2,BRACKET=YES                              
         MVI   CURTAB+3,2                                                       
         XC    DUB,DUB                                                          
         PACK  DUB(3),MKTWT(4)                                                  
         CURED (P3,DUB+5),(6,33(R2)),CURTAB,DMCB=YDMCB,BRACKET=YES              
         PRINT NOGEN                                                            
         GOTO1 SQUASHER,MYPARA,(R2),41                                          
         J     XIT                                                              
         EJECT                                                                  
*              DAYPART (SPOTLENGTH) CODES                                       
                                                                                
ME28     CLI   1(R3),C''          NON PRINTING DETAIL                          
         BE    ME29                 BUT GOES IN TOTAL                           
         CLI   5(R3),C''                                                       
         BE    ME29                                                             
*                                                                               
         CLI   0(R3),X'FE'         ORIGINATION                                  
         BNE   ME28A                                                            
         MVC   1(9,R2),=C'* ORIG  *'                                            
         CLI   8(R3),X'FF'                                                      
         BNE   ME28D                                                            
         J     XIT                                                              
ME28A    CLI   0(R3),X'FD'         SPILL LINE                                   
         BNE   ME28B                                                            
         MVC   1(9,R2),=C'* SPILL *'                                            
         CLI   RCLANG,4            TEST FOR FROGGY SPILL                        
         BNE   *+10                                                             
         MVC   1(11,R2),=C'DEBORDEMENT'                                         
         CLI   8(R3),X'FF'                                                      
         BNE   ME28D                                                            
         J     XIT                                                              
ME28B    CLI   0(R3),X'FF'         OVERALL TOTAL                                
         BNE   ME30                                                             
         CLI   8(R3),X'FF'         ANY SPOT LENGTH                              
         BNE   ME28C                YES - SHOW IT                               
         MVC   1(9,R2),=C'* TOTAL *'                                            
         J     XIT                                                              
ME28C    CLI   8(R3),1             SUPPRESS SPECIAL 1 SEC. SPOT LENGTH          
         BE    ME29                                                             
         MVC   1(6,R2),=C'TOTAL-'                                               
ME28D    EDIT  (1,8(R3)),(3,8(R2)),ALIGN=LEFT                                   
         CLI   10(R2),C' '                                                      
         JE    XIT                                                              
         MVC   7(3,R2),8(R2)                                                    
         MVI   10(R2),C' '                                                      
         J     XIT                                                              
         SPACE 2                                                                
ME29     L     R1,SAVER1           SPECIAL DETAIL SUPPRESS                      
         MVI   0(R1),0             USER DOESN'T NEED TO PRINT                   
         MVC   P(132),SPACES                                                    
         J     XIT                                                              
         SPACE 2                                                                
ME30     CLI   4(R3),X'FF'         GROUP TOTAL                                  
         BNE   ME34                                                             
         MVC   0(3,R2),1(R3)                                                    
         MVC   3(7,R2),=C'-TOTAL*'                                              
         CLI   MEDDPCNT,1          IF THERE'S ONLY BEEN 1 DAYPART,              
         BNE   ME32                                                             
         L     R1,SAVER1                                                        
         MVI   0(R1),0             USER DOESN'T NEED TO PRINT                   
         MVC   P(132),SPACES                                                    
         SPACE 2                                                                
ME32     MVI   MEDDPCNT,0                                                       
         J     XIT                                                              
         SPACE 2                                                                
ME34     CLI   8(R3),X'FF'         DAYPART TOTAL                                
         BNE   ME38                                                             
         MVC   7(3,R2),=C'TOT'                                                  
         CLI   MEDSLCNT,0          IF SL IS SUPPRESSED                          
         BNE   ME35                                                             
         MVC   4(3,R2),5(R3)       SHOW DAYPART                                 
         MVC   7(3,R2),SPACES                                                   
         CLI   MEDDPCNT,0                                                       
         BH    ME35                                                             
         MVC   0(3,R2),1(R3)       AND GROUP WHERE RELEVANT                     
         MVI   3(R2),C'-'                                                       
         CLC   0(3,R2),4(R2)                                                    
         BNE   ME35                                                             
         MVC   0(4,R2),SPACES                                                   
         SPACE 2                                                                
ME35     SR    R4,R4               BUMP DP COUNT                                
         IC    R4,MEDDPCNT                                                      
         LA    R4,1(R4)                                                         
         STC   R4,MEDDPCNT                                                      
         CLI   MEDSLCNT,1          IF THERE'S ONLY BEEN 1 SPOTLENGTH            
         BNE   ME36                FOR THIS DAYPART,                            
         L     R1,SAVER1                                                        
         MVI   0(R1),0             USER DOES'NT NEED TO PRINT                   
         MVC   P(132),SPACES                                                    
         SPACE 2                                                                
ME36     MVI   MEDSLCNT,0                                                       
         J     XIT                                                              
         SPACE 2                                                                
ME38     SR    R4,R4               DETAIL ITEM                                  
         IC    R4,MEDSLCNT                                                      
         LA    R4,1(R4)            BUMP SL COUNT                                
         STC   R4,MEDSLCNT                                                      
         MVI   SLDET,1             SET SPOT LENGTH DETAIL SWITCH                
         MVI   7(R2),C'-'          EDIT SECONDS                                 
         EDIT  (1,8(R3)),(3,8(R2)),ALIGN=LEFT                                   
         CLI   10(R2),C' '                                                      
         BE    ME39                                                             
         MVC   7(3,R2),8(R2)                                                    
         MVI   10(R2),C' '                                                      
ME39     CLI   MEDSLCNT,1                                                       
         JH    XIT                                                              
         MVC   4(3,R2),5(R3)       FIRST FOR DAYPART - SO SHOW IT               
         CLI   MEDDPCNT,0                                                       
         JH    XIT                                                              
         MVC   0(3,R2),1(R3)       FIRST FOR GROUP - SHOW THAT AS WELL          
         MVI   3(R2),C'-'                                                       
         CLC   0(3,R2),4(R2)       IF GROUP AND DAYPART CODES EQUATE            
         JNE   XIT                                                              
         MVC   0(4,R2),SPACES      TAKE OUT DAYPART CODE AND DASH               
         J     XIT                                                              
         EJECT                                                                  
*              NUMBER ROUTINES                                                  
                                                                                
ME40     EDIT  (4,0(R3)),(10,AREA)                                              
         LA    R4,AREA                                                          
         LA    R5,10                                                            
         SPACE 2                                                                
ME42     CLI   0(R2),C' '                                                       
         BNE   *+10                                                             
         MVC   0(1,R2),0(R4)                                                    
         LA    R2,1(R2)                                                         
         LA    R4,1(R4)                                                         
         BCT   R5,ME42                                                          
         J     XIT                                                              
         SPACE 2                                                                
ME44     LA    R4,3                NUMBER 3                                     
         SPACE 2                                                                
ME46     EDIT  (4,0(R3)),(8,0(R2))                                              
         LA    R2,8(R2)                                                         
         LA    R3,4(R3)                                                         
         BCT   R4,ME46                                                          
         J     XIT                                                              
         SPACE 2                                                                
ME48     LA    R4,14               NUMBER 14                                    
         SPACE 2                                                                
ME50     EDIT  (4,0(R3)),(6,0(R2))                                              
         LA    R2,6(R2)                                                         
         LA    R3,4(R3)                                                         
         BCT   R4,ME50                                                          
         J     XIT                                                              
         EJECT                                                                  
*              ROUTINES FOR MONTHLY                                             
                                                                                
ME52     L     R3,0(R3)                                                         
         L     R4,WEIGHT                                                        
         LA    R5,LIST                                                          
         BRAS  RE,POINTS           GOAL POINTS                                  
         LA    R2,7(R2)                                                         
         SPACE 2                                                                
         L     R3,EDPARAS+8        GOAL DOLLARS                                 
         CVDX  XDUB,0(R3)                                                       
         CP    XDUB,=P'0'                                                       
         BL    *+10                                                             
         AP    XDUB,=P'50'                                                      
         EDIT  (P8,XDUB),(10,CRUNCH)                                            
         MVC   0(8,R2),CRUNCH                                                   
         LA    R2,8(R2)                                                         
         SPACE 2                                                                
         L     R3,EDPARAS+12       ACHIEVED POINTS                              
         L     R3,0(R3)                                                         
         L     R4,WEIGHT                                                        
         LA    R5,LIST                                                          
         BRAS  RE,POINTS                                                        
         LA    R2,7(R2)                                                         
         SPACE 2                                                                
         L     R3,EDPARAS+16       ACHIEVED DOLLARS                             
         CVDX  XDUB,0(R3)                                                       
         CP    XDUB,=P'0'                                                       
         BL    *+10                                                             
         AP    XDUB,=P'50'                                                      
         EDIT  (P8,XDUB),(10,CRUNCH)                                            
         MVC   0(8,R2),CRUNCH                                                   
         LA    R2,9(R2)                                                         
         SPACE 2                                                                
         L     R3,EDPARAS+4        POINTS PERCENT                               
         L     R4,EDPARAS+12                                                    
         L     R1,0(R3)                                                         
         L     R3,0(R4)                                                         
         LR    R4,R1                                                            
         BRAS  RE,PERCENT                                                       
         LA    R2,5(R2)                                                         
         SPACE 2                                                                
         L     R3,EDPARAS+8        DOLLARS PERCENT                              
         L     R4,EDPARAS+16                                                    
         L     R1,0(R3)                                                         
         L     R3,0(R4)                                                         
         LR    R4,R1                                                            
         BRAS  RE,PERCENT                                                       
         J     XIT                                                              
         EJECT                                                                  
*              ROUTINES FOR CPM                                                 
                                                                                
ME54     L     R3,0(R3)            POINTS                                       
         L     R4,WEIGHT                                                        
         LA    R5,LIST                                                          
         BRAS  RE,POINTS                                                        
         LA    R2,7(R2)                                                         
         SPACE 2                                                                
         L     R3,EDPARAS+8        DOLLARS                                      
         CVDX  XDUB,0(R3)                                                       
         CP    XDUB,=P'0'                                                       
         BL    *+10                                                             
         AP    XDUB,=P'50'                                                      
         EDIT  (P8,XDUB),(11,CRUNCH)                                            
         MVC   0(9,R2),CRUNCH                                                   
         CP    XDUB,=P'0'                                                       
         BNL   *+14                                                             
         MVC   0(8,R2),CRUNCH+1                                                 
         MVI   8(R2),C'-'                                                       
         LA    R2,9(R2)                                                         
         SPACE 2                                                                
         LM    R3,R4,EDPARAS+4     CPP/CPM                                      
         L     R5,WEIGHT                                                        
         LA    R6,LIST                                                          
         BRAS  RE,CPP                                                           
         J     XIT                                                              
         EJECT                                                                  
*              ROUTINES FOR AVERAGE                                             
                                                                                
ME56     LA    R2,4(R2)       DO POINTS PER SPOT FIRST                          
         L     R6,0(R4)                                                         
         SRDA  R6,31                                                            
         OC    0(4,R3),0(R3)                                                    
         JZ    XIT                                                              
         D     R6,0(R3)                                                         
         AH    R7,=H'1'                                                         
         SRA   R7,1                                                             
         LR    R3,R7                                                            
         L     R4,WEIGHT                                                        
         LA    R5,LIST                                                          
         BRAS  RE,POINTS                                                        
         CLC   0(3,R2),SPACES                                                   
         BE    ME58                                                             
         MVC   AREA(5),0(R2)                                                    
         MVC   0(2,R2),SPACES                                                   
         MVC   2(5,R2),AREA                                                     
         SPACE 2                                                                
ME58     SH    R2,=H'4'            NOW BACK UP FOR SPOTS                        
         L     R3,EDPARAS+4                                                     
         EDIT  (4,0(R3)),(6,0(R2))                                              
         J     XIT                                                              
         EJECT                                                                  
*              ROUTINES FOR PERCENT                                             
                                                                                
ME60     L     R3,0(R3)                                                         
         L     R4,0(R4)                                                         
         BRAS  RE,PERCENT                                                       
         J     XIT                                                              
         EJECT                                                                  
*              ROUTINES FOR IMPS AND IMPS 3                                     
                                                                                
ME62     L     R3,EDPARAS+8        IMPS                                         
         L     R4,EDPARAS+4                                                     
         LA    R5,LIST+1                                                        
         BAS   RE,IMPS                                                          
         J     XIT                                                              
         SPACE 2                                                                
ME64     L     R3,EDPARAS+8        IMPS * 3                                     
         L     R4,EDPARAS+4                                                     
         LA    R5,LIST+1                                                        
         BAS   RE,IMPS                                                          
         LA    R2,15(R2)                                                        
         L     R3,EDPARAS+12                                                    
         LA    R5,LIST+2                                                        
         BAS   RE,IMPS                                                          
         LA    R2,15(R2)                                                        
         L     R3,EDPARAS+16                                                    
         LA    R5,LIST+3                                                        
         BAS   RE,IMPS                                                          
         J     XIT                                                              
         EJECT                                                                  
*==================================================================             
*FIND A USER OR WEIGHTED DEMO NAME IN ESTHDR AND SET DEMO TYPE CODE             
*==================================================================             
                                                                                
GETDNAM  NTR1  LABEL=NO                                                         
         L     RE,ADEST                                                         
         USING ESTHDR,RE                                                        
*                                                                               
         LLC   RF,2(R2)            GET DEMO NUMBER                              
         CLI   1(R2),63            TEST WGHTD DEMO                              
         BNE   *+8                                                              
         LA    RF,5                                                             
         BCTR  RF,0                                                             
         MHI   RF,7                                                             
         LA    RF,EUSRNMS(RF)      SET TO NAME                                  
*                                                                               
         CLI   0(RF),C'E'          WEIGHT EXTENDED OR REGULAR RATINGS           
         BE    *+8                                                              
         CLI   0(RF),C'R'                                                       
         BNE   *+8                                                              
         MVI   0(R3),C'R'                                                       
         XIT1                                                                   
         DROP  RE                                                               
*                                                                               
IMPS     NTR1  LABEL=NO                                                         
         LA    R2,7(R2)            CPP/CPM FIRST                                
         LR    R6,R5                                                            
         L     R5,WEIGHT                                                        
         BRAS  RE,CPP                                                           
         SPACE 2                                                                
         AHI   R2,-7               NOW SHOW POINTS/IMPS                         
         L     R3,0(R3)                                                         
         L     R4,WEIGHT                                                        
         LR    R5,R6                                                            
         BRAS  RE,POINTS                                                        
         J     XIT                                                              
         EJECT                                                                  
*              ROUTINES FOR WEEK MONTH AND LITERALS                             
                                                                                
ME66     GOTO1 DATCON,MYPARA,(2,0(R3)),(4,0(R2))                                
         J     XIT                                                              
         SPACE 2                                                                
ME68     GOTO1 DATCON,MYPARA,(2,2(R3)),(4,AREA)                                 
         MVC   0(3,R2),AREA                                                     
         J     XIT                                                              
         SPACE 2                                                                
ME70     L     R3,AELEMNT                                                       
         SR    R4,R4                                                            
         IC    R4,1(R3)                                                         
         LH    R5,2(R3)                                                         
         SLL   R5,16                                                            
         SRL   R5,16                                                            
         A     R5,MEDTABLE                                                      
         BCTR  R4,0                                                             
         EX    R4,ME72                                                          
         J     XIT                                                              
         SPACE 2                                                                
ME72     MVC   0(0,R2),0(R5)                                                    
         SPACE 2                                                                
****** WAVE ROUTINE *********                                                   
ME74     CLI   LIST,C'R'           USE UNWEIGHTED SPOTS IF NOT AN R             
         BNE   ME56                                                             
         LR    R3,R5               SET POINTER TO WEIGHTED SPOTS                
         MVI   WAVESW,C'Y'         DISABLE WEIGHTING                            
         BAS   RE,ME76             FORCE AN ENTER AND RETURN HERE               
         MVI   WAVESW,C'N'         RESTORE WEIGHTING                            
         J     XIT                                                              
*                                                                               
ME76     NTR1  LABEL=NO            ENTRY TO ME56                                
         B     ME56                                                             
         LTORG                                                                  
         EJECT                                                                  
*              ROUTINE TO EDIT POINTS/IMPS                                      
                                                                                
POINTS   NTR1  BASE=*,LABEL=*                                                   
*                                  R2=A(7-BYTE OUTPUT)                          
*                                  R3=POINTS(1 DEC) OR IMPS(00)                 
*                                  R4=ADJUSTMENT FACTOR                         
*                                  R5=A(LIST INDICATOR R=RATING)                
         CLI   SPOTPROF+1,C'N'     SUPPRESS WEIGHTING                           
         BNE   *+8                                                              
         LA    R4,1                                                             
         CLI   SPOTPROF+1,C'D'     IMPRESSION WEIGHTING                         
         BE    *+12                                                             
         CLI   0(R5),C'R'                                                       
         BNE   IMPRN                                                            
         OC    MEDADDWT,MEDADDWT   NEW WEIGHTING SCHEME ACTIVE                  
         BZ    POINTS1              NO                                          
         LTR   R4,R4                WEIGHT = 0                                  
         BNZ   POINTS1               YES - SUPPRESS POINTS                      
         SR    R3,R3                                                            
         B     POINTS2                                                          
POINTS1  DS    0H                                                               
         LTR   R4,R4                                                            
         BZ    POINTS2                                                          
         CLI   WAVESW,C'Y'         WEIGHTED AVERAGES                            
         BNE   *+8                                                              
         LA    R4,1                DON'T UNWEIGHT IT                            
         LTR   R3,R3                                                            
         BM    POINTS3                                                          
         LR    R7,R3               ADJUST POINTS                                
         M     R6,=F'2'                                                         
         DR    R6,R4                                                            
         AHI   R7,1                                                             
         SRA   R7,1                                                             
         LR    R3,R7                                                            
         CLI   0(R5),C'R'                                                       
         BNE   IMPRN                                                            
         SPACE 2                                                                
POINTS2  LTR   R3,R3                                                            
         JZ    XIT                                                              
         TM    RQOPTS,RQOPTS_2DEC  REPORT SUPPORT 2-DEC RATINGS                 
         BZ    POINTS2A            NO                                           
         LR    RF,R3               ROUND TO 1 DEC                               
         M     RE,=F'2'                                                         
         D     RE,=F'10'                                                        
         AHI   RF,1                                                             
         SRL   RF,1                                                             
         LR    R3,RF                                                            
*                                                                               
POINTS2A MVI   CURTAB+3,1                                                       
         CURED (R3),(7,0(R2)),CURTAB,DMCB=YDMCB                                 
         CLI   0(R2),C' '          SHOW 1 DECIMAL PLACE IF ROOM                 
         JE    XIT                                                              
         LR    RF,R3               OTHERWISE ROUND AND SHOW POINTS              
         M     RE,=F'2'                                                         
         D     RE,=F'10'                                                        
         AHI   RF,1                                                             
         SRL   RF,1                                                             
         EDIT  (RF),(7,0(R2))                                                   
         J     XIT                                                              
         SPACE 2                                                                
POINTS3  SR    R6,R6               BIG POINTS                                   
         LR    R7,R3                                                            
         SLL   R7,2                ELIMINATE FLAGS                              
         SRL   R7,2                                                             
         CVD   R7,YDMCB            ALLOW BIG NUMBERS                            
         ZAP   YWORK(16),YDMCB        FOR DIVISION                              
         MP    YWORK(16),=P'1000'                                               
         CVD   R4,YDUB                                                          
         DP    YWORK(16),YDUB                                                   
         ZAP   YDUB,YWORK(8)                                                    
         AP    YDUB,=P'5'                                                       
         ZAP   YWORK(16),YDUB                                                   
         DP    YWORK(16),=PL8'10'                                               
         ZAP   YDUB,YWORK(8)                                                    
         SR    R7,R7                                                            
         CP    YDUB,=P'21000000'                                                
         BNL   *+8                                                              
         CVB   R7,YDUB                                                          
         LR    R3,R7                                                            
         CLI   0(R5),C'R'                                                       
         BE    POINTS2                                                          
*                                                                               
IMPRN    TM    RQOPTS,RQOPTS_2DECIMP  SUPPORT FOR 2-DEC IMPS                    
         JZ    IMPRN2                                                           
         LR    RF,R3               ROUND TO 1 DEC                               
         M     RE,=F'2'                                                         
         D     RE,=F'10'                                                        
         AHI   RF,1                                                             
         SRL   RF,1                                                             
         LR    R3,RF                                                            
* TRY TO SHOW 1 DEC PLACE                                                       
IMPRN2   MVI   CURTAB+3,1                                                       
         CURED (R3),(7,0(R2)),CURTAB,DMCB=YDMCB                                 
         CLI   0(R2),C' '          SHOW 1 DECIMAL PLACE IF ROOM                 
         JE    XIT                                                              
*                                                                               
         LR    RF,R3               OTHERWISE ROUND AND SHOW POINTS              
         M     RE,=F'2'                                                         
         D     RE,=F'10'                                                        
         AHI   RF,1                                                             
         SRL   RF,1                                                             
         LR    R3,RF                                                            
                                                                                
         C     R3,=F'99999999'                                                  
         BH    IMPRN4                                                           
         EDIT  (R3),(8,AREA)                                                    
         MVC   0(7,R2),AREA+1                                                   
         J     XIT                                                              
*                                                                               
IMPRN4   EDIT  (R3),(10,AREA)                                                   
         MVC   130(9,R2),AREA+1    BIG NUMBER PUT ON NEXT LINE                  
         J     XIT                                                              
         LTORG                                                                  
         EJECT                                                                  
*              ROUTINE TO COMPUTE AND EDIT CPP/CPM                              
                                                                                
CPP      NTR1  BASE=*,LABEL=*                                                   
*                                  R2=A(8-BYTE OUTPUT)                          
*                                  R3=A(8-BYTE AREA) F'DEM' F'DEMEQV'           
*                                  R4=A(8-BYTE AREA) F'$'   F'$EQU'             
*                                  R5=ADJUSTMENT FACTOR                         
*                                  R6=A(LIST INDICATOR R=RATING)                
         CLI   SPOTPROF+1,C'N'     SUPPRESS WEIGHTING                           
         BNE   *+8                                                              
         LA    R5,1                                                             
         CLI   SPOTPROF+4,C'N'     EQUIVALENCE DETAIL LINES                     
         BNE   CPP1                 YES                                         
         CLI   SLDET,1              NO                                          
         BNE   CPP1                                                             
         MVC   4(4,R3),0(R3)        IF DETAIL SET EQV=ACT                       
         MVC   4(4,R4),0(R4)                                                    
CPP1     DS    0H                                                               
         OC    0(4,R3),0(R3)                                                    
         JZ    XIT                                                              
         OC    0(4,R4),0(R4)                                                    
         JZ    XIT                                                              
         MVC   DEMS,0(R3)                                                       
         MVC   RTGSW,0(R6)         SAVE RATING INDICATOR                        
         TM    DEMS,X'C0'          PROTECT AGAINST NEGATIVES                    
         BNO   *+10                                                             
         XC    DEMS,DEMS                                                        
         CLI   SPOTPROF+1,C'D'                                                  
         BE    *+12                                                             
         CLI   RTGSW,C'R'                                                       
         BNE   CPP2                                                             
         OC    MEDADDWT,MEDADDWT                                                
         BZ    CPP1A                                                            
         LTR   R5,R5                                                            
         BNZ   CPP1A                                                            
         XC    DEMS(8),DEMS                                                     
         B     CPP2                                                             
CPP1A    DS    0H                                                               
         LTR   R5,R5                                                            
         BZ    CPP2                                                             
         SR    R6,R6               UNWEIGHT DEMOS                               
         L     R7,DEMS                                                          
         TM    DEMS,X'C0'                                                       
         BNM   CPP1A1                                                           
         SLL   R7,2                                                             
         SRL   R7,2                                                             
         M     R6,=F'200'                                                       
         B     CPP1A2                                                           
CPP1A1   SLDA  R6,1                                                             
CPP1A2   DR    R6,R5                                                            
         AHI   R7,1                                                             
         SRA   R7,1                                                             
         ST    R7,DEMS                                                          
         SPACE 1                                                                
         SR    R6,R6                                                            
         L     R7,DEMS+4                                                        
         TM    DEMS+4,X'C0'                                                     
         BNM   CPP1A3                                                           
         SLL   R7,2                                                             
         SRL   R7,2                                                             
         M     R6,=F'200'                                                       
         B     CPP1A4                                                           
CPP1A3   SLDA  R6,1                                                             
CPP1A4   DR    R6,R5                                                            
         AH    R7,=H'1'                                                         
         SRA   R7,1                                                             
         ST    R7,DEMS+4                                                        
         SPACE 2                                                                
CPP2     CLI   OPTION,C'D'         EQUIVALENCED POINTS ROUTINE                  
         BE    CPP4                                                             
         OC    DEMS+4(4),DEMS+4                                                 
         JZ    XIT                                                              
         CLC   DEMS(4),DEMS+4                                                   
         BE    *+8                                                              
         MVI   7(R2),C'+'          SHOW A PLUS IF EQUIVALENCED                  
         ZAP   YDUB,=P'100'                                                     
         L     R7,0(R4)            DIVIDE DOLLARS BY EQUIV POINTS               
         TM    0(R4),X'C0'                                                      
         BNM   CPP3                                                             
         ZAP   YDUB,=P'10000'                                                   
         SLL   R7,2                ADJUST BIG DOLLARS                           
         SRL   R7,2                                                             
CPP3     CVD   R7,YDMCB            ADJUST TO PROPER PRECISION                   
         ZAP   YWORK(16),YDMCB(8)                                               
         MP    YWORK(16),YDUB                                                   
         L     R7,DEMS+4           GET THE DEMOS                                
         CVD   R7,YDUB                                                          
*                                                                               
         CLI   RTGSW,C'R'          TEST FOR RATING                              
         BNE   CPP3A                                                            
         TM    RQOPTS,RQOPTS_2DEC  REPORT SUPPORT 2-DEC                         
         BZ    CPP3B               NO                                           
         MP    YWORK,=P'10'        ADJUST DOLLARS                               
         B     CPP3B                                                            
*                                                                               
CPP3A    TM    RQOPTS,RQOPTS_2DECIMP  REPORT SUPPORT 2-DEC                      
         BZ    CPP3B               NO                                           
         MP    YWORK,=P'10'                                                     
*                                                                               
CPP3B    DP    YWORK(16),YDUB      CALC CPP                                     
         ZAP   YDUB,YWORK(8)                                                    
         AP    YDUB,=P'5'          AND ROUND                                    
         ZAP   YWORK(16),YDUB                                                   
         DP    YWORK(16),=PL8'10'                                               
         ZAP   YDUB,YWORK(8)                                                    
         SR    R7,R7               STOP BIG NUMBERS                             
         CP    YDUB,=P'21000000'                                                
         BNL   CPP6                                                             
         CP    YDUB,=P'0'          AND NEGATIVE ONES !                          
         BNH   CPP6                                                             
         CVB   R7,YDUB             SEND CPP IN R7                               
         B     CPP6                                                             
         SPACE 2                                                                
CPP4     OC    4(4,R4),4(R4)       EQUIVALENCED DOLLARS ROUTINE                 
         JZ    XIT                                                              
         OC    DEMS(4),DEMS                                                     
         JZ    XIT                                                              
         CLC   0(4,R4),4(R4)                                                    
         BE    *+8                                                              
         MVI   7(R2),C'+'                                                       
         ICM   R7,15,4(R4)         DIVIDE EQUIV. DOLLARS BY POINTS              
         BM    CPP4A               MAKE SURE NOT NEGATIVE                       
         C     R7,=F'210000000'    NUMBER TOO LARGE FOR * 10                    
         BH    CPP4A               DIVIDE THE POINTS                            
         M     R6,=F'10'                                                        
         TM    4(R4),X'C0'                                                      
         BNM   CPP5                                                             
         SPACE 2                                                                
         MVC   FULL,DEMS           SAVE ORIGINAL VALUE                          
         L     R7,FULL             ADJUST DEMO PRECISION                        
         SR    R6,R6                                                            
         D     R6,=F'10'                                                        
         ST    R7,DEMS                                                          
         LTR   R7,R7               EXIT IF DEMOS TOO SMALL                      
         BNZ   *+14                                                             
         MVC   DEMS,FULL                                                        
         J     XIT                                                              
         SPACE 2                                                                
         L     R7,4(R4)            ADJUST BIG DOLLARS                           
         SLL   R7,2                                                             
         SRL   R7,2                                                             
         LA    R6,100                                                           
         MR    R6,R6                                                            
         B     CPP5                                                             
         SPACE 2                                                                
CPP4A    MVC   FULL,DEMS           SAVE ORIGINAL VALUE                          
         L     R7,FULL             ADJUST DEMO PRECISION                        
         SR    R6,R6                                                            
         D     R6,=F'10'                                                        
         ST    R7,DEMS                                                          
         LTR   R7,R7               EXIT IF DEMOS TO SMALL                       
         BNZ   *+14                                                             
         MVC   DEMS,FULL                                                        
         J     XIT                                                              
         L     R7,4(R4)            RESET THE DOLLARS                            
         SR    R6,R6                                                            
                                                                                
CPP5     CVD   R7,YDMCB            ADJUST TO PROPER PRECISION                   
         ZAP   YWORK(16),YDMCB(8)                                               
         MP    YWORK(16),=P'10'                                                 
*                                                                               
         CLI   RTGSW,C'R'          TEST FOR RATING                              
         BNE   CPP5A                                                            
         TM    RQOPTS,RQOPTS_2DEC  REPORT SUPPORT 2 DEC                         
         BZ    CPP5B               NO                                           
         MP    YWORK,=P'10'        ADJUST DOLLARS                               
         B     CPP5B                                                            
*                                                                               
CPP5A    TM    RQOPTS,RQOPTS_2DECIMP  REPORT SUPPORT 2-DEC IMPS                 
         BZ    CPP5B                                                            
         MP    YWORK,=P'10'        ADJUST DOLLARS                               
*                                                                               
CPP5B    L     R7,DEMS             GET THE DEMOS                                
         CVD   R7,YDUB                                                          
         DP    YWORK(16),YDUB      CALC CPP                                     
         ZAP   YDUB,YWORK(8)                                                    
         AP    YDUB,=P'5'          AND ROUND                                    
         ZAP   YWORK(16),YDUB                                                   
         DP    YWORK(16),=PL8'10'                                               
         ZAP   YDUB,YWORK(8)                                                    
         SR    R7,R7               STOP BIG NUMBERS                             
         CP    YDUB,=P'21000000'                                                
         BNL   *+8                                                              
         CVB   R7,YDUB             SEND CPP IN R7                               
*                                                                               
         TM    4(R4),X'C0'         ARE WE DOING BIG NUMBERS                     
         BNM   *+10                                                             
         MVC   DEMS,FULL           RESTORE ORIGINAL VALUE                       
         SPACE 2                                                                
CPP6     DS    0H                                                               
*---->   EDIT  (R7),(7,0(R2)),2                                                 
         MVI   CURTAB+3,2                                                       
         CURED (R7),(7,0(R2)),CURTAB,DMCB=YDMCB                                 
         LTR   R7,R7                                                            
         BNZ   *+14                                                             
         MVC   0(8,R2),SPACES                                                   
         J     XIT                                                              
         CLI   0(R2),C' '          SHOW 2 PLACES IF SPACE                       
         JE    XIT                                                              
         LA    R7,50(R7)           OTHERWISE ROUND TO NEAREST DOLLAR            
         EDIT  (R7),(9,AREA)                                                    
         MVC   0(7,R2),AREA        AND SHOW THOSE                               
         J     XIT                                                              
         LTORG                                                                  
         EJECT                                                                  
*              ROUTINE TO EDIT PERCENT                                          
                                                                                
PERCENT  NTR1  BASE=*,LABEL=*                                                   
*                                  R2=A(4-BYTE OUTPUT)                          
*                                  R3=OPERAND1                                  
*                                  R4=OPERAND2                                  
         LTR   R3,R3                                                            
         JZ    XIT                                                              
         LTR   R4,R4                                                            
         JZ    XIT                                                              
         SPACE 2                                                                
PERCENT1 STM   R3,R4,XDUB                                                       
         TM    XDUB,X'C0'          CHECK                                        
         BM    PERCENT2                                                         
         TM    XDUB+4,X'C0'                                                     
         BNM   PERCENT4                                                         
         SPACE 2                                                                
PERCENT2 LR    R1,R3               AND ADJUST FOR BIG NUMBERS                   
         SLDL  R0,34                                                            
         SRDL  R0,34                                                            
         LR    R3,R1                                                            
         TM    XDUB,X'C0'                                                       
         BM    *+14                                                             
         A     R1,=F'50'                                                        
         D     R0,=F'100'                                                       
         LR    R3,R1                                                            
         LR    R1,R4                                                            
         SLDL  R0,34                                                            
         SRDL  R0,34                                                            
         LR    R4,R1                                                            
         TM    XDUB+4,X'C0'                                                     
         BM    *+14                                                             
         A     R1,=F'50'                                                        
         D     R0,=F'100'                                                       
         LR    R4,R1                                                            
         SPACE 2                                                                
PERCENT4 LR    R5,R3                                                            
         LR    R3,R4                                                            
         CVD   R5,YDMCB            ADJUST TO PROPER PRECISION                   
         ZAP   YWORK(16),YDMCB(8)                                               
         MP    YWORK(16),=PL3'1000'                                             
         CVD   R3,YDUB                                                          
         DP    YWORK(16),YDUB      CALC CPP                                     
         ZAP   YDUB,YWORK(8)                                                    
         AP    YDUB,=P'5'          AND ROUND                                    
         ZAP   YWORK(16),YDUB                                                   
         DP    YWORK(16),=PL8'10'                                               
         ZAP   YDUB,YWORK(8)                                                    
         CP    YDUB,=P'10000'                                                   
         BNL   PERCENT5                                                         
         CVB   R5,YDUB             SEND CPP IN R7                               
         EDIT  (R5),(4,0(R2))                                                   
         CLI   0(R2),C' '                                                       
         JE    XIT                                                              
PERCENT5 MVC   0(4,R2),=C'HIGH'                                                 
         J     XIT                                                              
         LTORG                                                                  
         EJECT                                                                  
*              DSECT FOR MODULE                                                 
                                                                                
EDITD    DSECT                                                                  
XDUB     DS    D                                                                
YDUB     DS    D                                                                
YWORK    DS    CL16                                                             
YDMCB    DS    6D                                                               
MYPARA   DS    6F                                                               
SAVER1   DS    F                                                                
LIST     DS    CL14                                                             
EDPARAS  DS    6F                                                               
AREA     DS    CL100                                                            
OPTION   DS    CL1                                                              
DEMS     DS    D                                                                
WEIGHT   DS    F                                                                
AELEMNT  DS    A                                                                
CRUNCH   DS    CL16                                                             
SLDET    DS    C                                                                
WAVESW   DS    C                   WEIGHTED AVERAGE SWITCH                      
RTGSW    DS    C                   FLAG FOR RATING                              
         PRINT OFF                                                              
       ++INCLUDE DDBUFFALOD                                                     
       ++INCLUDE SPREPWORKD                                                     
       ++INCLUDE SPREPMODES                                                     
       ++INCLUDE SPMEDBLOCK                                                     
       ++INCLUDE SPGENMKT                                                       
       ++INCLUDE SPGENEST                                                       
       ++INCLUDE DEDBLOCK                                                       
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'121SPMEDEDIT 10/22/19'                                      
         END                                                                    
