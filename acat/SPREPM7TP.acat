*          DATA SET SPREPM7TP  AT LEVEL 105 AS OF 03/19/15                      
*$PANAPT01S$ *******  FIRST LINE TO DELETE  *****************                   
*                                                           *                   
*  ATTN: THE LEVEL STAMPS ARE *LEGITIMATELY* OUT-OF-SYNC!   *                   
*        DELETE THIS COMMENT BLOCK ON THE NEXT PROMOTION!   *                   
*                                                           *                   
*  THIS SOURCE MODULE IS AT A HIGHER LEVEL NUMBER THAN ITS  *                   
*  CORRESPONDING LOAD OR OBJECT MODULE, BECAUSE THE MEMBER  *                   
*  WAS PROMOTED USING THE SRCE LIBCODE VIA MR# 045233.      *                   
*                                                           *                   
*  THIS COMMENT BLOCK WAS INSERTED *PROGRAMMATICALLY* BY    *                   
*  PANAPT, TO EXPLAIN WHY THE LEVEL STAMPS ARE OUT-OF-SYNC. *                   
*  BEFORE THIS MEMBER CAN BE PROMOTED AGAIN, THIS ENTIRE    *                   
*  COMMENT BLOCK *MUST* BE MANUALLY DELETED.                *                   
*                                                           *                   
*$PANAPT01X$ *******  LAST LINE TO DELETE  ******************                   
*CATALP SPM7TP                                                                  
         TITLE 'SPM7TP - TAPE INTERFACES FOR SPA2 REPORT'                       
SPM7TP   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,SPM7TP,R9                                                      
         L     RA,0(R1)                                                         
         LA    RC,2048(RA)                                                      
         LA    RC,2048(RC)                                                      
         USING SPWORKD,RA,RC                                                    
*                                                                               
         MVC   AIEEQU,4(R1)                                                     
         MVC   ANEWDNAM,8(R1)                                                   
         MVC   SVABUFLO,12(R1)     SAVE BUFFALO RECORD                          
         L     R3,ADMARKET                                                      
         MVC   SVAMKT,2(R3)                                                     
*                                                                               
         CLI   0(R1),C'I'          INITIALIZE WORK AREA,OPEN TAPE               
         BE    INITIAL                                                          
         CLI   0(R1),C'A'          ACCUM TOTALS TO OUTPUT BUFFER                
         BE    ACCUM                                                            
         CLI   0(R1),C'C'          CLOSE TAPE                                   
         BE    CLOSE                                                            
EXIT     XIT1                                                                   
         EJECT                                                                  
INITIAL  DS    0H                                                               
*                                                                               
         OPEN  (JWTAPE,(OUTPUT))                                                
*                                                                               
         XC    FDMED(143),FDMED                                                 
*                                                                               
INITEX   B     EXIT                                                             
         SPACE 4                                                                
ACCUM    DS    0H                                                               
*                                                                               
         L     R6,SVABUFLO                                                      
         USING BUFFRECD,R6                                                      
*                                                                               
         CLC   BFCODE,=XL2'0001'                                                
         BNE   EXIT                                                             
*                                                                               
         BAS   RE,BLDCOM           BUILD COMMON RECORD INFORMATION              
*                                                                               
*        GOTO1 =V(PRNTBL),DMCB,=C'BUFFALO',(R6),C'DUMP',88,=C'1D'               
*                                                                               
         PUT   JWTAPE,TAPREC                                                    
*                                                                               
         CLC   QUESTOR(7),=C'DUMPIT*'                                           
         BNE   NODUMP                                                           
         GOTO1 =V(PRNTBL),DMCB,=C'TAPREC',TAPREC,C'DUMP',143,=C'1D'             
*                                                                               
NODUMP   XC    FDMED(143),FDMED                                                 
         B     EXIT                                                             
         SPACE 4                                                                
CLOSE    DS    0H                                                               
*                                                                               
         CLOSE JWTAPE                                                           
*                                                                               
         B     EXIT                                                             
         EJECT                                                                  
* BLDCOM WRITES THE COMMON DATA TO THE TAPE BUFFER                              
BLDCOM   NTR1                                                                   
*                                                                               
         MVC   FDMED,QMED                                                       
         MVC   FDCLT,CLT                                                        
         MVC   FDMKT,SVAMKT                                                     
         GOTO1 DATCON,DMCB,QSTART,(3,DUB)                                       
         GOTO1 (RF),(R1),(X'23',DUB),FDSTART                                    
*                                                                               
         GOTO1 (RF),(R1),QEND,(3,DUB+3)                                         
         GOTO1 (RF),(R1),(X'23',DUB+3),FDEND                                    
*                                                                               
         GOTO1 (RF),(R1),,(X'20',WORK) GET YYYYMMDD                             
         MVC   FDYEAR,WORK                                                      
         MVC   FDHIST(1),QUESTOR                                                
*                                                                               
*        TM    FDHIST,X'F0'        PROTECT AGAINST FORGETFUL REQUESTOR          
*        BO    BLDCOM1             ON BIG REQUESTS                              
         CLC   QCLT,=C'ALL'                                                     
         BNE   BLDCOM1                                                          
*        CLI   QCOMPARE,C'I'                                                    
*        BNE   BLDCOM1                                                          
         MVI   FDHIST,C'5'         FORCE TO A 5 IF CLIENT ALL                   
*                                                                               
BLDCOM1  MVC   FDDPTM(1),QDPTMENU                                               
         MVC   FDPRD(3),BFPROD                                                  
* GET PRIME DEMO NAME USING ROUTINES LOCATED IN SPREPM702                       
         GOTO1 AIEEQU,DMCB,(1,BFPRIME),DUB                                      
         GOTO1 ANEWDNAM,DMCB,(1,DUB),WORK                                       
         MVC   FDPDEMO,WORK                                                     
* GET SECONDARY DEMO NAME USING ROUTINES LOCATED IN SPREPM702                   
         L     R3,MEDBUFF                                                       
         USING MEDBLOCK,R3                                                      
         ZIC   RE,BFPRCD                                                        
         BCTR  RE,0                                                             
         MH    RE,PRDBUFLN                                                      
         A     RE,PRDBUFF                                                       
         LA    R8,31(RE)                                                        
         GOTO1 ANEWDNAM,DMCB,(1,(R8)),WORK                                      
         MVC   FDPSCND,WORK                                                     
         DROP  R3                                                               
*                                                                               
* FIND QUARTER                                                                  
         CLC   QEND+2(2),=CL2'06'                                               
         BH    BLDCM060                                                         
*                                                                               
         CLC   QEND+2(2),=CL2'03'                                               
         BH    BLDCM030                                                         
         MVI   FDQUART,C'1'                                                     
         B     BLDCM100                                                         
*                                                                               
BLDCM030 MVI   FDQUART,C'2'                                                     
         B     BLDCM100                                                         
*                                                                               
BLDCM060 CLC   QEND+2(2),=CL2'09'                                               
         BH    BLDCM090                                                         
         MVI   FDQUART,C'3'                                                     
         B     BLDCM100                                                         
*                                                                               
BLDCM090 MVI   FDQUART,C'4'                                                     
* CALCULATE THE TYPE CODES                                                      
BLDCM100 MVC   HLDTYPE(3),=C'GPA'                                               
         CLI   QCOMPARE,C'H'                                                    
         BE    BLDCM120                                                         
         MVI   HLDTYPE+2,C'I'                                                   
         CLI   QCOMPARE,C'J'                                                    
         BE    BLDCM110                                                         
         CLI   QCOMPARE,C'K'                                                    
         BNE   BLDCM120                                                         
BLDCM110 MVI   HLDTYPE+1,C'L'                                                   
*                                                                               
* ACCUM DAYPART AREA                                                            
*                                                                               
BLDCM120 LA    R5,FDELEM                                                        
*                                                                               
         MVC   0(3,R5),BFSUBDAY+1                                               
         EDIT  (1,BFSPTLEN),(3,3(R5)),FILL=0                                    
         EDIT  (4,BFBSPTS),(5,6(R5)),FILL=0                                     
*                                                                               
* MOVE TYPE INFORMATION TO TAPE BUFFER                                          
*                                                                               
         MVC   11(1,R5),HLDTYPE                                                 
         GOTO1 CKWEIGHT,DMCB,BFGDEM1E                                           
*        GOTO1 ROUNDIT,DMCB,FULL,10                                             
         EDIT  (R3),(6,12(R5)),FILL=0                                           
         GOTO1 CKWEIGHT,DMCB,BFGDEM1                                            
*        GOTO1 ROUNDIT,DMCB,FULL,10                                             
         EDIT  (R3),(6,18(R5)),FILL=0                                           
         GOTO1 ROUNDIT,DMCB,BFGDOLR,100                                         
         EDIT  (R3),(9,24(R5)),FILL=0                                           
*                                                                               
         MVC   33(1,R5),HLDTYPE+1                                               
         GOTO1 CKWEIGHT,DMCB,BFLDEM1E                                           
*        GOTO1 ROUNDIT,DMCB,FULL,10                                             
         EDIT  (R3),(6,34(R5)),FILL=0                                           
         GOTO1 CKWEIGHT,DMCB,BFLDEM1                                            
*        GOTO1 ROUNDIT,DMCB,FULL,10                                             
         EDIT  (R3),(6,40(R5)),FILL=0                                           
         GOTO1 ROUNDIT,DMCB,BFLDOLR,100                                         
         EDIT  (R3),(9,46(R5)),FILL=0                                           
*                                                                               
         MVC   55(1,R5),HLDTYPE+2                                               
         GOTO1 CKWEIGHT,DMCB,BFBDEM1E                                           
*        GOTO1 ROUNDIT,DMCB,FULL,10                                             
         EDIT  (R3),(6,56(R5)),FILL=0                                           
         GOTO1 CKWEIGHT,DMCB,BFBDEM1                                            
*        GOTO1 ROUNDIT,DMCB,FULL,10                                             
         EDIT  (R3),(6,62(R5)),FILL=0                                           
         GOTO1 ROUNDIT,DMCB,BFBDOLR,100                                         
         EDIT  (R3),(9,68(R5)),FILL=0                                           
*                                                                               
         MVI   77(R5),C'S'                                                      
         GOTO1 CKWT2,DMCB,BFBDEM2E                                              
*        GOTO1 ROUNDIT,DMCB,FULL,10                                             
         EDIT  (R3),(6,78(R5)),FILL=0                                           
         GOTO1 CKWT2,DMCB,BFBDEM2                                               
*        GOTO1 ROUNDIT,DMCB,FULL,10                                             
         EDIT  (R3),(6,84(R5)),FILL=0                                           
         GOTO1 ROUNDIT,DMCB,BFBDOLR,100                                         
         EDIT  (R3),(9,90(R5)),FILL=0                                           
*                                                                               
         B     EXIT                                                             
         EJECT                                                                  
*                                                                               
* ROUNDIT ROUNDS THE FIELD ANSWER RETURNED IN R3                                
ROUNDIT  L     R3,0(R1)                                                         
         L     R2,0(R3)                                                         
         L     R4,4(R1)                                                         
         SR    R3,R3                                                            
         SRDA  R2,31                                                            
         DR    R2,R4                                                            
         LTR   R3,R3                                                            
         BM    *+8                                                              
         AHI   R3,1                                                             
         SRA   R3,1                                                             
         BR    RE                                                               
         EJECT                                                                  
*                                                                               
* CKWEIGHT WEIGHTS THE DEMO VALUE IF THE PRIMARY DEMO IS RATING                 
CKWEIGHT L     R3,0(R1)                                                         
         L     R2,0(R3)            R2 = DEMO VALUE                              
         SRDA  R2,32                                                            
*                                                                               
         CLI   FDPDEMO,C'R'                                                     
         BNE   CKWEX                                                            
         CLC   SPWEIGHT,=F'0'                                                   
         BE    CKWEX                                                            
*                                                                               
         D     R2,SPWEIGHT                                                      
*                                                                               
CKWEX    ST    R3,FULL                                                          
         BR    RE                                                               
         EJECT                                                                  
*                                                                               
* CKWT2 WEIGHTS THE DEMO VALUE IF THE SECONDARY DEMO IS RATING                  
CKWT2    L     R3,0(R1)                                                         
         L     R2,0(R3)            R2 = DEMO VALUE                              
         SRDA  R2,32                                                            
*                                                                               
         CLI   FDPSCND,C'R'                                                     
         BNE   CKW2EX                                                           
         CLC   SPWEIGHT,=F'0'                                                   
         BE    CKWEX                                                            
*                                                                               
         D     R2,SPWEIGHT                                                      
*                                                                               
CKW2EX   ST    R3,FULL                                                          
         BR    RE                                                               
         SPACE 2                                                                
         EJECT                                                                  
SVABUFLO DS    A                                                                
SVAMKT   DS    CL4                                                              
HLDTYPE  DS    F                                                                
*                                                                               
AIEEQU   DS    A(0)                                                             
ANEWDNAM DS    A(0)                                                             
*                                                                               
         DS    0D                                                               
EKREC    DS    CL490                                                            
         SPACE 2                                                                
*                                                                               
*-- OUTPUT TAPE LAYOUT                                                          
*                                                                               
TAPREC   DS    0CL143              ***** FORD INTERFACE RECORD *****            
*                                                                               
FDMED    DS    CL1       A           MEDIA CODE                                 
FDCLT    DS    CL3       AAA         CLIENT CODE                                
FDPRD    DS    CL3       AAA         PRODUCT CODE                               
FDPDEMO  DS    CL7       AAAAAAA     PRIMARY DEMO                               
FDPSCND  DS    CL7       AAAAAAA     SECONDARY DEMO                             
FDMKT    DS    CL4       NNNN        MARKET CODE                                
FDHIST   DS    CL1       N           HISTORY OR TRACKING INDICATOR              
FDDPTM   DS    CL1       N           DAYPART MENU                               
FDYEAR   DS    CL4       NNNN        YEAR                                       
FDQUART  DS    CL1       N           QUARTER                                    
FDSTART  DS    CL6       YYMMDD      EST START DATE                             
FDEND    DS    CL6       YYMMDD      EST END   DATE                             
*                                                                               
FDELEM   DS    CL99                                                             
* THE FOLLOWING DAYPART CODES OCCUR OCCUR 50 TIMES                              
*   DAYPART  DS   CL3 AAA            DAYPART CODE                               
*   SPOT LEN DS   CL3 NNN            SPOT LENGTH                                
*   #SPOTS   DS   CL5 NNNNN          # SPOTS                                    
* THE FOLLOWING TYPE CODES OCCUR 4 TIMES FOR EACH DAYPART CODE                  
*   TYPE     DS   CL1 A              TYPE CODE G,P,A,I,S                        
*   TOETPTS  DS   CL6 NNNNN.N        TOTAL EQUATED POINTS                       
*   TOTPTS   DS   CL6 NNNNN.N        TOTAL POINTS                               
*   TOTCST   DS   CL9 NNNNNNNNN      TOTAL COST                                 
*                                                                               
JWTAPE   DCB   DDNAME=JWTAPE,BLKSIZE=1430,LRECL=143,RECFM=FB,          *        
               MACRF=PM,DSORG=PS                                                
*                                                                               
         EJECT                                                                  
BUFFRECD DSECT                                                                  
* SPA202 BUFFALO RECORD DSECT *                                                 
         SPACE 1                                                                
BUFFKEY  DS    0D                                                               
BFHALF   DS    CL1                                                              
BFUN1    DS    CL1                                                              
BFWORK   DS    CL2                                                              
BFPRIME  DS    CL1                                                              
BFUN2    DS    CL1                                                              
BFPROD   DS    CL3                                                              
BFPRCD   DS    CL1                                                              
BFDAYPT  DS    CL4                                                              
BFSUBDAY DS    CL4                                                              
BFSPTLEN DS    CL1                                                              
BFCODE   DS    CL2                                                              
*                                                                               
BFGDEM1  DS    CL4                                                              
BFGDEM1E DS    CL4                                                              
BFGDOLR  DS    CL4                                                              
BFGDLRE  DS    CL4                                                              
*                                                                               
BFLDEM1  DS    CL4                                                              
BFLDEM1E DS    CL4                                                              
BFLDOLR  DS    CL4                                                              
BFLDLRE  DS    CL4                                                              
BFLSPTS  DS    CL4                                                              
*                                                                               
BFBDEM1  DS    CL4                                                              
BFBDEM1E DS    CL4                                                              
BFBDOLR  DS    CL4                                                              
BFBDLRE  DS    CL4                                                              
BFBSPTS  DS    CL4                                                              
BFBDEM2  DS    CL4                                                              
BFBDEM2E DS    CL4                                                              
         EJECT                                                                  
         PRINT OFF                                                              
       ++INCLUDE SPREPWORKD                                                     
       ++INCLUDE SPMEDBLOCK                                                     
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'105SPREPM7TP 03/19/15'                                      
         END                                                                    
