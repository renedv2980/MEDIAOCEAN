*          DATA SET MXAGYX     AT LEVEL 020 AS OF 05/01/02                      
*CATALP MXAGYX                                                                  
         TITLE 'MXAGYX - MEDIA EXTRACT - CONVERT AGENCY RECORD TO SQL'          
**********************************************************************          
* AL4 A(EXTRACT RECORD)                                              *          
* AL4 A(SQL BUFFER)                                                  *          
**********************************************************************          
         SPACE 1                                                                
         PRINT NOGEN                                                            
MXAGYX   CSECT                                                                  
         NMOD1 WORKX-WORKD,*MXAGYX*,RR=RA                                       
         USING WORKD,RC                                                         
         ST    R1,APARM                                                         
         ST    RA,RELO                                                          
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         XC    0(4,R3),0(R3)                                                    
         LA    R3,4(R3)            BUMP PAST LENGTH IN BUFFER                   
         USING MXAGYD,R2           R2=A(EXTRACT RECORD)                         
         MVI   SEPARATE,DEFSEP     SET DEFAULT FIELD SEPARATOR CHAR             
         MVI   XFLAG,0             INITIALISE CONVERT INPUT FLAG                
         CLI   MXAGYACT,C'L'       TEST IF IN LOAD MODE                         
         BNE   *+12                                                             
         MVI   SEPARATE,LOADSEP    SET LOAD MODE FIELD SEPARATOR CHAR           
         OI    XFLAG,LOADMODE      SET CONVERT FLAG TO LOAD MODE                
*                                                                               
AXTYP    NI    XFLAG,REQUIRED      CONVERT RECORD TYPE - REQUIRED               
         GOTO1 XMXCNVX,DMCB,(L'MXAGYTYP,MXAGYTYP),(XFLAG,(R3)),(1,CHAR)         
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
AXSTA    NI    XFLAG,REQUIRED      CONVERT RECORD STATUS - REQUIRED             
         GOTO1 XMXCNVX,DMCB,(L'MXAGYSTA,MXAGYSTA),(XFLAG,(R3)),(1,CHAR)         
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
AXACT    NI    XFLAG,REQUIRED      CONVERT RECORD ACTION - REQUIRED             
         GOTO1 XMXCNVX,DMCB,(L'MXAGYACT,MXAGYACT),(XFLAG,(R3)),(1,CHAR)         
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
AXDTM    NI    XFLAG,REQUIRED      CONVERT ACTION DATE TIME - REQUIRED          
         LA    R0,L'MXAGYDAT+L'MXAGYTIM+1                                       
         GOTO1 XMXCNVX,DMCB,((R0),MXAGYDAT),(XFLAG,(R3)),(1,DTIM)               
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
AXATY    NI    XFLAG,REQUIRED      CONVERT AGENCY - REQUIRED                    
         GOTO1 XMXCNVX,DMCB,(L'MXAGYATY,MXAGYATY),(XFLAG,(R3)),(1,CHAR)         
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
AXCDE    NI    XFLAG,REQUIRED      CONVERT AGENCY CODE - REQUIRED               
         GOTO1 XMXCNVX,DMCB,(L'MXAGYCDE,MXAGYCDE),(XFLAG,(R3)),(1,TINY)         
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
AXALF    OI    XFLAG,OPTIONAL      CONVERT AGENCY ALPHA ID - OPTIONAL           
         GOTO1 XMXCNVX,DMCB,(L'MXAGYALF,MXAGYALF),(XFLAG,(R3)),(1,CHAR)         
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
AXIPA    OI    XFLAG,OPTIONAL      CONVERT IPA CODE - OPTIONAL                  
         GOTO1 XMXCNVX,DMCB,(L'MXAGYIPA,MXAGYIPA),(XFLAG,(R3)),(1,CHAR)         
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
AXSHN    OI    XFLAG,OPTIONAL      CONVERT SHORT NAME - OPTIONAL                
         GOTO1 XMXCNVX,DMCB,(L'MXAGYSHN,MXAGYSHN),(XFLAG,(R3)),(1,CHAR)         
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
AXCUR    OI    XFLAG,OPTIONAL      CONVERT CURRENCY CODE - OPTIONAL             
         GOTO1 XMXCNVX,DMCB,(L'MXAGYCUR,MXAGYCUR),(XFLAG,(R3)),(1,CHAR)         
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
AXCTR    OI    XFLAG,OPTIONAL      CONVERT COUNTRY - OPTIONAL                   
         GOTO1 XMXCNVX,DMCB,(L'MXAGYCTR,MXAGYCTR),(XFLAG,(R3)),(1,TINY)         
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
AXNAM    OI    XFLAG,OPTIONAL      CONVERT AGENCY NAME - OPTIONAL               
         GOTO1 XMXCNVX,DMCB,(L'MXAGYNAM,MXAGYNAM),(XFLAG,(R3)),(1,CHAR)         
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
AXBUFLEN L     RF,ASQLBUFF         STORE BUFFER DATA LENGTH                     
         SR    R3,RF               IN TOP 2 BYTES                               
         SLL   R3,16                                                            
         STCM  R3,15,0(RF)                                                      
         B     EXIT                                                             
*                                                                               
EXIT     XMOD1 1                                                                
*                                                                               
PUTSEP   SR    RF,RF               PUT OUT SEPARATOR CHARACTER                  
         ICM   RF,1,4(R1)          GET RETURN LENGTH                            
         LA    R3,0(RF,R3)         BUMP DATA BUFFER POINTER                     
         MVC   0(1,R3),SEPARATE                                                 
         LA    R3,1(R3)                                                         
         BR    RE                                                               
         EJECT                                                                  
         LTORG                                                                  
         SPACE 1                                                                
XMXCNVX  DC    V(MXCNVX)                                                        
         SPACE 1                                                                
CHAR     DC    CL8'CHAR    '                                                    
DTIM     DC    CL8'DATETIME'                                                    
TINY     DC    CL8'TINYINT '                                                    
         EJECT                                                                  
WORKD    DSECT                                                                  
PARM     DS    0XL16               PARAMETER LIST                               
AXREC    DS    A                   A(EXTRACT RECORD)                            
ASQLBUFF DS    A                   A(SQL BUFFER)                                
         DS    A                                                                
         DS    A                                                                
*                                                                               
DUB      DS    D                                                                
APARM    DS    A                   A(PARAMETER LIST)                            
RELO     DS    A                                                                
DMCB     DS    6F                                                               
SEPARATE DS    CL1                 FIELD SEPARATOR CHARACTER                    
DEFSEP   EQU   C' '                                                             
LOADSEP  EQU   C';'                                                             
XFLAG    DS    XL1                 CONVERT INPUT CONTROL FLAGS                  
LOADMODE EQU   X'80'                 LODE MODE                                  
OPTIONAL EQU   X'40'                 OPTIONAL FIELD                             
REQUIRED EQU   X'FF'-X'40'           REQUIRED FIELD                             
*                                                                               
WORKX    EQU   *                                                                
         EJECT                                                                  
       ++INCLUDE MXAGYD                                                         
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'020MXAGYX    05/01/02'                                      
         END                                                                    
