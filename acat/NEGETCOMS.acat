*          DATA SET NEGETCOMS  AT LEVEL 039 AS OF 05/01/02                      
*CATALP NETCOM                                                                  
         TITLE 'T31XXX - NETWORK COMMENT READING MODULE'                        
NETCOM   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 40,**NCOM**                                                      
         USING MYD,RC                                                           
         MVC   USERRD,4(RD)                                                     
         L     R9,0(R1)                                                         
         USING NCOMBLKD,R9                                                      
         L     RA,NCBNETB                                                       
         USING NETBLOCK,RA                                                      
         SPACE 2                                                                
* FILL KEY FROM NCOMBLKD                                                        
         LA    R4,REQKEY                                                        
         USING NCOMKEY,R4                                                       
         XC    REQKEY,REQKEY                                                    
         MVI   REQKEY,X'FF'                                                     
         MVC   REQKEY+1(L'REQKEY-1),REQKEY                                      
         MVI   NCOMKTYP,X'0C'                                                   
         MVC   NCOMKAM,NCBAM                                                    
         MVC   NCOMKID,NCBID                                                    
         MVC   NCOMKIDA,NCBID+1                                                 
         MVC   NCOMKIDB,NCBID+2                                                 
         MVC   NCOMKCLT,NCBCLT                                                  
         MVC   NCOMKPRD,NCBPRD                                                  
         MVC   NCOMKEST,NCBEST                                                  
         MVC   NCOMKNET,NCBNTWK                                                 
         MVC   NCOMKDPT,NCBDPT                                                  
         MVC   NCOMKPKG,NCBPKG                                                  
         MVC   REQKEYA,REQKEY      REQKEYA HAS REQ INCLUDING *                  
*                                  REQKEY  HAS REQ WITH * SET TO 0              
         SPACE                                                                  
         CLI   NCOMKID,C'*'        ID GROUP                                     
         BNE   *+6                                                              
         DC    H'0'           **** IF THIS IS ALLOWED SET FILTER=Y              
         CLI   NCOMKIDA,C'*'       ID SUB-GROUP                                 
         BNE   *+8                 LAST KEY FIELD/DO NOT TURN ON                
         MVI   NCOMKIDA,0          FILTER(SEQREAD) FLAG                         
         SPACE                                                                  
         LA    R3,14               TEST IF ANY ALL FIELDS                       
         LA    R2,NCOMKCLT                                                      
         MVI   FILTER,0                                                         
GC2      CLI   0(R2),C'*'                                                       
         BNE   GC2A                                                             
         MVI   0(R2),0             YES/SET * TO ZERO                            
         MVI   FILTER,C'Y'             SET FILTER FLAG                          
GC2A     LA    R2,1(R2)                                                         
         BCT   R3,GC2                                                           
         DROP  R4                                                               
         B     GC3                                                              
         EJECT                                                                  
         SPACE                                                                  
*******************************************                                     
*     COMMENT CODES                       *                                     
* NN1=TOP OF PAGE COMMENT                 *                                     
* NN2=BOTTOM OF PAGE COMMENT              *                                     
* NN3=FIRST PAGE ONLY-TOP COMMENT         *                                     
* NN4=FIRST PAGE ONLY-BOTTOM COMMENT      *                                     
* NN5=LAST PAGE ONLY COMMENT (BOTTOM)     *                                     
*                                         *                                     
*******************************************                                     
         SPACE 2                                                                
*                                                                               
GC3      DS    0H                                                               
         CLI   NCBFPGE,C'Y'       IS IT FIRST PAGE                              
         BNE   GC5                                                              
         BAS   RE,FRSTPG                                                        
         B     GC10                                                             
GC5      CLI   NCBLPGE,C'Y'        IS IT LAST PAGE                              
         BNE   GC10                                                             
         BAS   RE,LASTPG                                                        
         SPACE                                                                  
GC10     LA    R4,KEY                                                           
         USING NCOMKEY,R4                                                       
         CLI   NCOMKIDB,C'2'        IS IT FIRST/LAST ONLY                       
         BH    EXIT                   IF YES/ EXIT                              
         CLI   NCOMKIDB,C'*'      ELSE SET TOP/BOT COM ID NUMBER                
         BNE   GC12                                                             
         MVI   NCOMKIDB,C'2'                                                    
         CLI   NCBTOP,C'Y'                                                      
         BNE   GC12                                                             
         MVI   NCOMKIDB,C'1'                                                    
GC12     MVC   REQKEY+4,NCOMKIDB   SET ID COM PRINT                             
         B     GC15                                                             
         SPACE                                                                  
*                                                                               
EXIT     XIT                                                                    
         EJECT                                                                  
************************************                                            
* DECISION ON DEFAULT              *                                            
         SPACE                                                                  
GC15     DS    0H                                                               
         CLI   NCBKDFL,C'Y'                                                     
         BE    DEC10               DEF=Y                                        
         B     DEC20               DEF=0                                        
**************************************************                              
         SPACE                                                                  
*                                                                               
*                                                                               
DEC10    DS    0H                  DEF=Y                                        
         BAS   RE,HIGH                                                          
         CLC   KEY(2),KEYSAVE      AGY/MED                                      
         BNE   DEC15X                                                           
         CLC   KEY(20),KEYSAVE                                                  
         BNE   DEC14                                                            
DEC12    BAS   RE,GETREC                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
         BAS   RE,GOHOOK                                                        
         CLI   FILTER,C'Y'         IF FILTER IS ON/MEANS                        
         BNE   DEC15X              THERE COULD BE MULT RECS                     
         BAS   RE,SEQ                                                           
DEC14    BAS   RE,DFLRTNA                                                       
         BNZ   DEC12                                                            
DEC15X   B     EXIT                                                             
         SPACE 2                                                                
*                                                                               
DEC20    DS    0H                 DEF=0                                         
         BAS   RE,HIGH                                                          
         CLC   KEY(2),KEYSAVE                                                   
         BNE   DEC25X                                                           
         CLC   KEY(20),KEYSAVE                                                  
         BNE   DEC24                                                            
DEC22    BAS   RE,GETREC                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
         BAS   RE,GOHOOK                                                        
DEC24    CLI   FILTER,C'Y'         IF FILTER=Y/COULD BE                         
         BNE   DEC25X              MULTIPLE RECS                                
         BAS   RE,DFLRTNB                                                       
         BNZ   DEC22                                                            
DEC25X   B     EXIT                                                             
         EJECT                                                                  
***********************************************                                 
* THIS DEFAULT ROUTINE STARTS AT TOP OF KEY   *                                 
* AND MOVES IN X'FF' FIELD BY FIELD           *                                 
* KEY RETURNED FROM READ IS COMPARED TO REQKEY*                                 
* AND FF MOVED IN WHERE NOT EQUAL             *                                 
*                                             *                                 
* AFTER THE FIRST KEY IS FOUND, WE CHECK TO   *                                 
* SEE IF THERE IS AN ALL FIELD. IF YES, THE   *                                 
* THE SEQUENTIAL FLAG IS SET TO READ SEQ      *                                 
* FOR THE REMAINING RECORDS.                  *                                 
* INPUT  REQKEY                               *                                 
* OUTPUT KEY                                  *                                 
*                                             *                                 
*  CONDITION CODE=0 IF NO KEY FOUND           *                                 
***********************************************                                 
         SPACE                                                                  
DFLRTNA  NTR1                                                                   
         LA    R4,KEY                                                           
         USING NCOMKEY,R4                                                       
         SPACE                                                                  
DF10     CLC   KEY(2),REQKEY       AGY/MED                                      
         BNE   DFX                                                              
         SPACE                                                                  
         CLI   REQKEYA+2,C'*'      IS IT ALL FILTER                             
         BE    DF10A                                                            
         CLC   NCOMKID,REQKEY+2   ID COM GROUP                                  
         BE    DF10A                                                            
         B     DFX                                                              
         SPACE                                                                  
DF10A    CLC   NCOMKIDB,REQKEY+4  ID COM PRINT(TOP/BOT)                         
         BE    DF10B                                                            
         B     DFX                                                              
         SPACE                                                                  
DF10B    DS    0H                                                               
         CLI   REQKEYA+5,C'*'       IS IT ALL FILTER                            
         BE    DF13                YES/SKIP CHK                                 
         CLC   NCOMKCLT,REQKEY+5   CLIENT                                       
         BE    DF13                                                             
         CLI   FILTER,C'Y'                                                      
         BE    DFREC                                                            
         CLC   NCOMKCLT,=20X'FF'                                                
         BE    DF13                                                             
         CLC   NCOMKCLT,REQKEY+5                                                
         BNL   DF12                                                             
         MVC   NCOMKCLT,REQKEY+5                                                
         B     *+10                                                             
DF12     MVC   NCOMKCLT,=20X'FF'                                                
         MVC   NCOMKPRD(NCOMKEND-NCOMKPRD),REQKEY+7                             
         B     DFREC                                                            
         SPACE                                                                  
DF13     CLI   REQKEYA+7,C'*'       IS IT ALL FILTER                            
         BE    DF16                YES/SKIP CHK                                 
         CLC   NCOMKPRD,REQKEY+7   PRODUCT                                      
         BE    DF16                                                             
         CLI   FILTER,C'Y'                                                      
         BE    DFREC                                                            
         CLC   NCOMKPRD,=20X'FF'                                                
         BE    DF16                                                             
         CLC   NCOMKPRD,REQKEY+7                                                
         BNL   DF14                                                             
         MVC   NCOMKPRD,REQKEY+7                                                
         B     *+10                                                             
DF14     MVC   NCOMKPRD,=20X'FF'                                                
         MVC   NCOMKEST(NCOMKEND-NCOMKEST),REQKEY+8                             
         B     DFREC                                                            
         SPACE                                                                  
DF16     CLI   REQKEYA+8,C'*'       IS IT ALL FILTER                            
         BE    DF22                YES/SKIP CHK                                 
         CLC   NCOMKEST,REQKEY+8   ESTIMATE                                     
         BE    DF22                                                             
         CLI   FILTER,C'Y'                                                      
         BE    DFREC                                                            
         CLC   NCOMKEST,=20X'FF'                                                
         BE    DF22                                                             
         CLC   NCOMKEST,REQKEY+8                                                
         BNL   DF18                                                             
         MVC   NCOMKEST,REQKEY+8                                                
         B     *+10                                                             
DF18     MVC   NCOMKEST,=20X'FF'                                                
         MVC   NCOMKNET(NCOMKEND-NCOMKNET),REQKEY+9                             
         B     DFREC                                                            
         SPACE                                                                  
DF22     CLI   REQKEYA+9,C'*'       IS IT ALL FILTER                            
         BE    DF25                YES/SKIP CHK                                 
         CLC   NCOMKNET,REQKEY+9   NETWORK                                      
         BE    DF25                                                             
         CLI   FILTER,C'Y'                                                      
         BE    DFREC                                                            
         CLC   NCOMKNET,=20X'FF'                                                
         BE    DF25                                                             
         CLC   NCOMKNET,REQKEY+9                                                
         BNL   DF24                                                             
         MVC   NCOMKNET,REQKEY+9                                                
         B     *+10                                                             
DF24     MVC   NCOMKNET,=20X'FF'                                                
         MVC   NCOMKDPT(NCOMKEND-NCOMKDPT),REQKEY+13                            
         B     DFREC                                                            
         SPACE                                                                  
DF25     CLI   REQKEYA+13,C'*'       IS IT ALL FILTER                           
         BE    DF28                 YES/SKIP CHK                                
         CLC   NCOMKDPT,REQKEY+13   DAYPART                                     
         BE    DF28                                                             
         CLI   FILTER,C'Y'                                                      
         BE    DFREC                                                            
         CLC   NCOMKDPT,=20X'FF'                                                
         BE    DF28                                                             
         CLC   NCOMKDPT,REQKEY+13                                               
         BNL   DF27                                                             
         MVC   NCOMKDPT,REQKEY+13                                               
         B     *+10                                                             
DF27     MVC   NCOMKDPT,=20X'FF'                                                
         MVC   NCOMKPKG(NCOMKEND-NCOMKPKG),REQKEY+14                            
         B     DFREC                                                            
         SPACE                                                                  
DF28     CLI   REQKEYA+14,C'*'       IS IT ALL FILTER                           
         BE    DF32                 YES/SKIP CHK                                
         CLC   NCOMKPKG,REQKEY+14   PACKAGE                                     
         BE    DF32                                                             
         CLC   NCOMKPKG,=20X'FF'                                                
         BE    DF32                                                             
         CLI   FILTER,C'Y'                                                      
         BE    DFREC                                                            
         CLC   NCOMKPKG,REQKEY+14                                               
         BNL   DF30                                                             
         MVC   NCOMKDPT,REQKEY+14                                               
         B     *+10                                                             
DF30     MVC   NCOMKPKG,=20X'FF'                                                
         MVC   NCOMKIDB,REQKEY+15                                               
         B     DFREC                                                            
         SPACE                                                                  
DF32     CLI   REQKEYA+15,C'*'     IS IT ALL FILTER                             
         BE    GOTIT             ID COM SUB-GROUP                               
         CLI   NCOMKIDA,X'FF'                                                   
         BE    GOTIT                                                            
         CLC   NCOMKIDA,REQKEY+15                                               
         BE    GOTIT                                                            
         BNL   DF34                                                             
         MVC   NCOMKIDA,REQKEY+15                                               
         B     DFREC                                                            
DF34     MVI   NCOMKIDA,X'FF'                                                   
         B     DFREC                                                            
*                                                                               
DFREC    DS    0H                                                               
         CLI   FILTER,C'Y'         IS IT READ SEQUENTIAL                        
         BE    DFR5                                                             
         BAS   RE,HIGH                                                          
         B     *+8                                                              
DFR5     BAS   RE,SEQ                                                           
         BZ    DFX                                                              
         CLC   KEY,KEYSAVE                                                      
         BE    GOTIT                                                            
         B     DF10                                                             
         SPACE                                                                  
*                                                                               
GOTIT    DS    0H                                                               
         LA    R1,1                                                             
         B     *+6                                                              
DFX      SR    R1,R1               CC=ZERO/MEANS NO KEY FOUND                   
         LTR   R1,R1                                                            
         B     EXIT                                                             
         DROP  R4                                                               
         EJECT                                                                  
***********************************************                                 
* THIS ROUTINE DOES NOT GIVE DEFAULT KEY      *                                 
*                                             *                                 
* IT IS FOR GETTING A SPECIFIC REC            *                                 
* OR FOR A FILTER.                            *                                 
*                                             *                                 
* INPUT  REQKEY                               *                                 
* OUTPUT KEY                                  *                                 
*                                             *                                 
*  CONDITION CODE=0 IF NO KEY FOUND           *                                 
***********************************************                                 
         SPACE                                                                  
DFLRTNB  NTR1                                                                   
         LA    R4,KEY                                                           
         USING NCOMKEY,R4                                                       
         SPACE                                                                  
DB10     CLC   KEY(2),REQKEY       AGY/MED                                      
         BNE   DBX                                                              
         SPACE                                                                  
         CLI   REQKEYA+2,C'*'      IS IT ALL FILTER                             
         BE    DB10A                                                            
         CLC   NCOMKID,REQKEY+2     ID COM GROUP                                
         BNE   DBX                                                              
         SPACE                                                                  
DB10A    CLC   NCOMKIDB,REQKEYA+4   ID COM PRINT(TOP/BOT)                       
         BNE   DBX                                                              
         SPACE                                                                  
DB10B    DS    0H                                                               
         CLI   REQKEYA+5,C'*'       IS IT ALL FILTER                            
         BE    DB13                YES/SKIP CHK                                 
         CLC   NCOMKCLT,REQKEY+5   CLIENT                                       
         BNE   DBREC                                                            
         SPACE                                                                  
DB13     CLI   REQKEYA+7,C'*'       IS IT ALL FILTER                            
         BE    DB16                YES/SKIP CHK                                 
         CLC   NCOMKPRD,REQKEY+7   PRODUCT                                      
         BNE   DBREC                                                            
         SPACE                                                                  
DB16     CLI   REQKEYA+8,C'*'       IS IT ALL FILTER                            
         BE    DB22                YES/SKIP CHK                                 
         CLC   NCOMKEST,REQKEY+8   ESTIMATE                                     
         BNE   DBREC                                                            
         SPACE                                                                  
DB22     CLI   REQKEYA+9,C'*'       IS IT ALL FILTER                            
         BE    DB25                YES/SKIP CHK                                 
         CLC   NCOMKNET,REQKEY+9   NETWORK                                      
         BNE   DBREC                                                            
         SPACE                                                                  
DB25     CLI   REQKEYA+13,C'*'       IS IT ALL FILTER                           
         BE    DB28                 YES/SKIP CHK                                
         CLC   NCOMKDPT,REQKEY+13   DAYPART                                     
         BNE   DBREC                                                            
         SPACE                                                                  
DB28     CLI   REQKEYA+14,C'*'       IS IT ALL FILTER                           
         BE    DB32                 YES/SKIP CHK                                
         CLC   NCOMKPKG,REQKEY+14   PACKAGE                                     
         BNE   DBREC                                                            
         SPACE                                                                  
DB32     CLI   REQKEYA+15,C'*'     IS IT ALL FILTER                             
         BE    GOTITB                                                           
         CLC   NCOMKIDB,REQKEY+15    ID COM SUB-GROUP                           
         BE    GOTITB                                                           
         B     DBREC                                                            
*                                                                               
DBREC    DS    0H                                                               
         BAS   RE,SEQ                                                           
         BZ    DBX                                                              
         CLC   KEY,KEYSAVE                                                      
         BE    GOTITB                                                           
         B     DB10                                                             
         SPACE                                                                  
*                                                                               
GOTITB   DS    0H                                                               
         LA    R1,1                                                             
         B     *+6                                                              
DBX      SR    R1,R1               CC=ZERO/MEANS NO KEY FOUND                   
         LTR   R1,R1                                                            
         B     EXIT                                                             
         DROP  R4                                                               
         EJECT                                                                  
FRSTPG   DS    0H                                                               
         NTR1                                                                   
         LA    R4,KEY                                                           
         USING NCOMKEY,R4                                                       
         MVC   KEYSV,KEY           SAVE ORIGINAL KEY                            
         CLI   NCOMKID+2,C'*'                                                   
         BE    FR10                                                             
         CLI   NCOMKID+2,C'3'                                                   
         BE    FR20                                                             
         CLI   NCOMKID+2,C'4'                                                   
         BE    FR20                                                             
         B     FRX                                                              
FR10     MVI   NCOMKID+2,C'3'      1ST PAGE TOP                                 
         CLI   NCBTOP,C'Y'                                                      
         BE    FR20                                                             
         CLI   NCBBOT,C'Y'         1ST PAGE BOT                                 
         BNE   FRX                                                              
         MVI   NCOMKID+2,C'4'                                                   
FR20     BAS   RE,HIGH                                                          
         CLC   KEY(20),KEYSAVE                                                  
         BNE   FRX                                                              
         BAS   RE,GETREC                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
         BAS   RE,GOHOOK                                                        
FRX      MVC   KEY,KEYSV           RESTORE ORIGINAL KEY                         
         B     EXIT                                                             
         DROP  R4                                                               
         SPACE 2                                                                
*                                                                               
LASTPG   DS    0H                                                               
         NTR1                                                                   
         LA    R4,KEY                                                           
         USING NCOMKEY,R4                                                       
         MVC   KEYSV,KEY           SAVE ORIGINAL KEY                            
         CLI   NCOMKID+2,C'*'                                                   
         BE    LS10                                                             
         CLI   NCOMKID+2,C'5'            LAST PAGE BOTTOM                       
         BNE   LSX                                                              
LS10     MVI   NCOMKID+2,C'5'                                                   
         BAS   RE,HIGH                                                          
         CLC   KEY(20),KEYSAVE                                                  
         BNE   LSX                                                              
         BAS   RE,GOHOOK                                                        
LSX      MVC   KEY,KEYSV           RESTORE ORIGINAL KEY                         
         B     EXIT                                                             
         EJECT                                                                  
*              DATAMGR INTERFACE                                                
         SPACE 3                                                                
HIGH     NTR1                                                                   
         MVC   KEYSAVE,KEY                                                      
         MVC   COMMAND,=CL8'DMRDHI'          HANDLE COMMANDS                    
         B     DIRALL                                                           
         SPACE 1                                                                
SEQ      NTR1                                                                   
         MVC   KEYSAVE,KEY                                                      
         MVC   COMMAND,=CL8'DMRSEQ'                                             
         B     DIRALL                                                           
         SPACE 1                                                                
READ     NTR1                                                                   
         MVC   COMMAND,=CL8'DMREAD'                                             
         SPACE 1                                                                
DIRALL   MVC   FILE(8),=C'UNTDIR  '         DIRECTORIES                         
         LA    R2,WORK                                                          
         GOTO1 NBDM,DMCB,COMMAND,FILE,KEY,(R2),0                                
         BAS   RE,DMCHECK                                                       
         MVC   KEY,0(R2)                                                        
         B     YES                                                              
         SPACE 1                                                                
GETREC   NTR1                                                                   
         LA    R3,KEY+21                                                        
         MVC   FILE(8),=C'UNTFILE '     FILE                                    
         MVC   NBDTADSP,=H'27'                                                  
         SPACE 1                                                                
GETREC2  L     R2,NCBAIO                                                        
         GOTO1 NBDM,DMCB,(X'00',=C'GETREC'),FILE,(R3),(R2),DMWORK               
         BAS   RE,DMCHECK                                                       
         B     XIT                                                              
         SPACE 1                                                                
DMCHECK  CLI   8(R1),0                                                          
         BER   RE                                                               
         TM    8(R1),X'90'                                                      
         BM    NO                                                               
         DC    H'0'                                                             
         SPACE 1                                                                
         GETEL (R6),NBDTADSP,ELCODE                                             
         SPACE 1                                                                
NO       SR    R1,R1                                                            
         B     *+8                                                              
         SPACE 1                                                                
YES      LA    R1,1                                                             
         LTR   R1,R1                                                            
         SPACE 1                                                                
XIT      XIT1                                                                   
         EJECT                                                                  
         SPACE 1                                                                
GOHOOK   NTR1                                                                   
         L     RF,NCBAHOOK                                                      
         L     RE,USERRD           USERS RD                                     
         LM    R0,RC,20(RE)        RESTORE USERS R0-RC                          
         BASR  RE,RF               RF CONTAINED A(HOOK ROUTINE)                 
         XIT1                                                                   
         SPACE 1                                                                
RELO     DS    A                                                                
         EJECT                                                                  
*              DSECTS ETC                                                       
         SPACE 3                                                                
*                                                                               
MYD      DSECT                                                                  
DUB      DS    D                                                                
WORK     DS    CL64                                                             
PARAS    DS    0F                                                               
DMCB     DS    6F                                                               
USERRD   DS    F                                                                
KEY      DS    CL27                                                             
KEYSAVE  DS    CL27                                                             
KEYSV    DS    CL27                                                             
REQKEY   DS    CL27                                                             
REQKEYA  DS    CL27                                                             
ELCODE   DS    CL1                                                              
FILTER   DS    CL1                                                              
COMMAND  DS    CL8                                                              
FILE     DS    CL8                                                              
DMWORK   DS    CL96                                                             
         SPACE 3                                                                
         EJECT                                                                  
       ++INCLUDE NETBLOCKD                                                      
         EJECT                                                                  
       ++INCLUDE NECOMBLOK                                                      
         EJECT                                                                  
       ++INCLUDE NEGENCOM                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'039NEGETCOMS 05/01/02'                                      
         END                                                                    
