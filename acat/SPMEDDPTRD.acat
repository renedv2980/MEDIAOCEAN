*          DATA SET SPMEDDPTRD AT LEVEL 029 AS OF 08/20/01                      
*CATALP DPTRD                                                                   
         TITLE 'DAYPART READ MODULE'                                            
DPTRD    CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 DPTRDWX-DPTRDW,**DPRD**                                          
         USING DPTRDD,RC                                                        
         LR    R2,R1                                                            
         USING PARAMS,R2                                                        
         XC    KEY,KEY                                                          
         XC    KEYSAVE,KEYSAVE                                                  
         L     RE,PRMATAB                                                       
         LA    RF,LNDPT                                                         
         XCEF                                                                   
         LA    R3,KEY                                                           
         USING RECD,R3                                                          
         MVI   DPTKTYPE,X'08'                                                   
         MVC   DPTKAGY,PRMAGY                                                   
         MVC   DPTKMED,PRMMED                                                   
         MVC   KEYSAVE,KEY                                                      
         L     RF,=V(DATAMGR)                                                   
         LTR   RF,RF                                                            
         BNZ   *+8                                                              
         L     RF,PRMVDMGR                                                      
         ST    RF,MYDMGR                                                        
         GOTO1 (RF),DMCB,=C'DMRDHI',=C'SPTDIR',KEYSAVE,KEY                      
         CLC   KEY(5),KEYSAVE      CHECK IF OLD EXISTS                          
         BE    GETDPT              YES - GET THE DAYPART                        
         CLC   KEY(4),KEYSAVE      ANYTHING FOR THIS AGY/MED                    
         BNE   GETDFLT              NO - GET DEFAULT                            
         LA    R3,KEYSAVE          NO  - SETUP FOR NEW                          
         MVC   DPTKMENU,PRMMENU                                                 
         GOTO1 (RF)                AND GET IT                                   
         CLC   KEY(5),KEYSAVE                                                   
         BE    GETDPT                                                           
GETDFLT  LA    R3,KEYSAVE          RETURN HERE FOR DEFAULT MENU                 
         MVC   DPTKAGY,=C'00'                                                   
         MVC   KEY,KEYSAVE                                                      
         L     RF,MYDMGR                                                        
         GOTO1 (RF),DMCB,=C'DMRDHI',=C'SPTDIR',KEYSAVE,KEY                      
         CLC   KEY(4),KEYSAVE                                                   
         BNE   SETDFLT                                                          
         CLI   KEY+4,0                                                          
         BE    GETDPT                                                           
         MVC   DPTKMENU,PRMMENU                                                 
         GOTO1 (RF)                                                             
         CLC   KEY(5),KEYSAVE                                                   
         BNE   SETDFLT                                                          
*                                                                               
GETDPT   GOTO1 (RF),DMCB,=C'GETREC',=C'SPTFIL',KEY+14,REC,DMWORK                
         LA    R3,REC                                                           
         LA    R4,DPTEL                                                         
         USING DPTEL,R4                                                         
         CLI   DPTKMENU,0                                                       
         BE    OLD                                                              
         L     RF,PRMATAB                                                       
         MVC   0(LNDPT,RF),DPTCODES                                             
*                                                                               
         CLI   PRMATAB,C'P'        NEED PLANNING DAYPART ELEMENT                
         BNE   EXIT                 NO - JUST EXIT                              
         L     RE,PRMATAB           YES- CLEAR THE AREA                         
         LA    RE,LNDPT(RE)                                                     
         LA    RF,LNDPT                                                         
         XCEF                                                                   
*                                                                               
PLANEL   ZIC   RE,1(R4)            FIND PLANNING DAYPART ELEMENT                
         LTR   RE,RE               STOP THE LOOP                                
         BZ    EXIT                                                             
         AR    R4,RE                                                            
         LA    RF,REC                                                           
         ICM   RE,3,DPTLEN                                                      
         AR    RF,RE                                                            
         CR    RF,R4                                                            
         BNH   EXIT                                                             
         CLI   0(R4),X'02'         ELEMENT FOR PLANNING                         
         BNE   PLANEL                                                           
         L     RF,PRMATAB                                                       
         MVC   LNDPT(LNDPT,RF),DPTCODES                                         
         B     EXIT                                                             
*                                                                               
OLD      OI    PRMMENU,X'F0'       FORCE NUMERIC                                
         CLC   2(1,R4),PRMMENU     PROCESS OLD STYLE RECORDS                    
         BE    OLDOK                                                            
         ZIC   RE,1(R4)                                                         
         AR    R4,RE                                                            
         CLI   0(R4),0                                                          
         BNE   OLD                                                              
         LA    R3,KEY                                                           
         CLC   DPTKAGY,=C'00'      HAS DEFAULT BEEN PROCESSED                   
         BNE   GETDFLT               NO - DO IT NOW                             
         B     SETDFLT                                                          
OLDOK    ZIC   R9,1(R4)                                                         
         LA    RE,3(R4)            POINT TO DAYPART DEFINITION                  
         L     RF,PRMATAB                                                       
         SH    R9,=H'4'                                                         
         EX    R9,*+8              MOVE DATA TO OUTPUT                          
         B     EXIT                                                             
         MVC   0(0,RF),0(RE)       **EXECUTED**                                 
         SPACE 2                                                                
SETDFLT  L     RF,PRMATAB          NOTHING FOUND - SET A DEFAULT                
         MVI   PRM3,X'FF'                                                       
         MVC   0(5,RF),ZZZDPT                                                   
EXIT     XMOD1 1                                                                
         SPACE 1                                                                
LNDPT    EQU   182                                                              
ZZZDPT   DC    C'Z'                DEFAULT DAYPART                              
         DC    X'0F'                                                            
         DC    C'ZZZ'                                                           
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
DPTRDD   DSECT                                                                  
DPTRDW   DS    0C                                                               
MYDMGR   DS    F                                                                
DMCB     DS    6F                                                               
KEY      DS    CL20                                                             
KEYSAVE  DS    CL20                                                             
DMWORK   DS    CL64                                                             
REC      DS    CL1600                                                           
DPTRDWX  DS    0C                                                               
         EJECT                                                                  
RECD     DSECT                                                                  
       ++INCLUDE SPGENDAYPT                                                     
         EJECT                                                                  
PARAMS   DSECT                                                                  
PRM1     DS    0CL4                PARAMETER 1                                  
PRMAGY   DS    CL2                   AGENCY                                     
PRMMED   DS    C                     MEDIA                                      
PRMMENU  DS    C                     MENU                                       
PRM2     DS    0CL4                PARAMETER 2                                  
*                                  BYTE 0 = C'P' FOR PLANNING DAYPARTS          
PRMATAB  DS    CL4                   ADDRESS OF TABLE                           
PRM3     DS    0CL4                PARAMETER 3                                  
PRMVDMGR DS    CL4                   VDATAMGR                                   
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'029SPMEDDPTRD08/20/01'                                      
         END                                                                    
