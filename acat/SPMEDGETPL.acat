*          DATA SET SPMEDGETPL AT LEVEL 026 AS OF 05/01/02                      
*CATALP MEDGETPL                                                                
         TITLE 'MEDGETPL - EXTRACT DATA FROM PEPSI LOCKIN RECORDS'              
*                                                                               
* PARAMETERS                       REGISTER USAGE                               
*  1 = A(WORKRC)                   RA-RC     A(WORKC)                           
*  2 = XL1(STAGE),XL1(0),H(BOOK)   R2        PEPSI RECORD                       
*                                  R3        MEDBLOCK                           
*                                  R4        VARIABLE DSECTS                    
*                                  R5        DISPLACEMENT TO NEXT ENTRY         
*                                                                               
MEDGETPL CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,MEDGETPL                                                       
         L     RA,0(R1)                                                         
         LA    RC,2048(RA)                                                      
         LA    RC,2048(RC)                                                      
         USING SPWORKD,RA,RC                                                    
         MVC   STGCODE,4(R1)                                                    
         MVC   BOOK,6(R1)                                                       
         L     R2,ADGOAL                                                        
         USING PEPREC,R2                                                        
         L     R3,MEDBUFF                                                       
         USING MEDBLOCK,R3                                                      
         L     R5,MEDAFRST                                                      
         MVC   MLSTART,0(R5)       SET OVERALL START AND END DATES              
         L     R5,MEDALAST                                                      
         MVC   MLEND,2(R5)                                                      
         LA    R6,MEDTOTAL                                                      
         L     R5,MEDAFRST                                                      
*                                                                               
CLRBUFF  L     RE,4(R5)                                                         
         LTR   RE,RE                                                            
         BZ    CLRBUFF2                                                         
         L     RF,MEDLCHNK                                                      
         XCEF                                                                   
CLRBUFF2 LA    R5,12(R5)                                                        
         CR    R5,R6                                                            
         BH    CLRBUFF3                                                         
         B     CLRBUFF                                                          
CLRBUFF3 DS    0H                                                               
         EJECT                                                                  
* GET MEDBLOCK KEY INFORMATION                                                  
*                                                                               
         MVC   MEDSPTLN,PEPKLEN                                                 
         GOTO1 MEDEQUIV,MLPARAM,(RA)                                            
         MVC   MLEQU,MEDEQFAC                                                   
         OC    MLEQU,MLEQU                                                      
         BNZ   *+10                                                             
         MVC   MLEQU,=H'1000'                                                   
         LA    R7,PEPELEMS                                                      
         BAS   R9,NEXTEL2                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         USING PEPELEMS,R7                                                      
         LA    R4,MEDDATA                                                       
         USING MEDGLD,R4                                                        
         L     R6,MEDAFRST         ADDR OF FIRST DATE                           
*                                                                               
         CLC   PEPKDTE,MLSTART                                                  
         BNL   PL25                                                             
         CLI   MEDNYBEF,C'Y'                                                    
         BNE   PLEXIT                                                           
         CLC   PEPKDTE,MEDBFORE                                                 
         BL    PLEXIT                                                           
         LA    R6,MEDBFORE                                                      
         B     PL40                                                             
*                                                                               
PL25     CLC   PEPKDTE,MLEND                                                    
         BNH   PL28                                                             
         CLI   MEDNYAFT,C'Y'                                                    
         BNE   PLEXIT                                                           
         CLC   PEPKDTE,MEDAFTER+2                                               
         BH    PLEXIT                                                           
         LA    R6,MEDAFTER                                                      
         B     PL40                                                             
*                                                                               
PL28     CLC   PEPKDTE,2(R6)                                                    
         BL    PL40                                                             
PL30     LA    R6,12(R6)           CHECK NEXT DATE                              
         CLI   0(R6),0                                                          
         BE    PL30                                                             
         B     PL28                                                             
*                                                                               
PL40     L     R4,4(R6)            ADDR OF DATA CHUNK                           
         XC    MLFULL,MLFULL                                                    
         MVC   MLFULL+2(2),PEPSPOTS                                             
         L     RF,MEDBYSPT                                                      
         A     RF,MLFULL                                                        
         ST    RF,MEDBYSPT                                                      
         MVC   MLFULL,PEPDOL                                                    
         L     RF,MLFULL                                                        
         A     RF,MEDBYD                                                        
         ST    RF,MEDBYD                                                        
         BAS   R9,EQUIVDL                                                       
         A     RF,MEDBYDEQ                                                      
         ST    RF,MEDBYDEQ                                                      
         ZIC   R1,PEPDLEN          FIGURE NO. OF DEMOS FROM ELEMENT LEN         
         SH    R1,=H'12'                                                        
         SR    R0,R0                                                            
         D     R0,=F'5'                                                         
         LA    R7,PEPDEMS          DEMOS IN RECORD                              
         LA    R8,MEDBY1           DATA CHUNK                                   
*        DEMOS FROM PRDLIST        DEMOS FROM PRDBUFF                           
         LA    R6,220                                                           
         CLI   BPRD,X'FF'                                                       
         BE    PL48                                                             
         ZIC   R6,BPRD                                                          
PL48     DS    0H                                                               
         BCTR  R6,R0                                                            
         MH    R6,PRDBUFLN                                                      
         A     R6,PRDBUFF                                                       
         LA    R6,28(R6)                                                        
         ST    R6,MLFULL                                                        
*                                                                               
PL50     L     R6,MLFULL           A(DEMO LIST)                                 
PL52     OC    0(3,R6),0(R6)                                                    
         BZ    PL56                                                             
         CLC   0(2,R7),1(R6)                                                    
         BE    PL54                MATCH                                        
         LA    R6,3(R6)                                                         
         LA    R8,8(R8)                                                         
         B     PL52                                                             
PL54     ZICM  RF,2(R7),3                                                       
         A     RF,0(R8)                                                         
         ST    RF,0(R8)                                                         
         BAS   R9,EQUIVL                                                        
         A     RF,4(R8)                                                         
         ST    RF,4(R8)                                                         
         LA    R8,MEDBY1                                                        
PL56     LA    R7,5(R7)                                                         
         BCT   R1,PL50             ANY MORE DEMOS IN ELEMENT                    
         B     DOTOTAL                                                          
         EJECT                                                                  
*                                                                               
* DO TOTALS                                                                     
DOTOTAL  L     R6,MEDAFRST                                                      
DOTOTAL1 C     R6,MEDALAST                                                      
         BE    PLEXIT                                                           
         L     RE,4(R6)                                                         
         L     RF,8(R6)                                                         
         LTR   RF,RF                                                            
         BZ    DOTOTAL5                                                         
         LA    R4,13                                                            
         LA    RE,64(RE)                                                        
         LA    RF,64(RF)                                                        
DOTOTAL3 L     R7,0(RE)                                                         
         A     R7,0(RF)                                                         
         ST    R7,0(RF)                                                         
         LA    RE,4(RE)                                                         
         LA    RF,4(RF)                                                         
         BCT   R4,DOTOTAL3                                                      
DOTOTAL5 LA    R6,12(R6)                                                        
         C     R6,MEDALAST                                                      
         BNL   PLEXIT                                                           
         CLI   2(R6),0                                                          
         BE    DOTOTAL5                                                         
         B     DOTOTAL1                                                         
*                                                                               
PLEXIT   XMOD1 1                                                                
         LTORG                                                                  
         SPACE 2                                                                
EQUIVL   SR    RE,RE                                                            
         MH    RF,MLEQU                                                         
         SLDA  RE,1                                                             
         D     RE,=F'1000'                                                      
         A     RF,=F'1'                                                         
         SRL   RF,1                                                             
         BR    R9                                                               
EQUIVDL  SR    RE,RE                                                            
         M     RE,=F'1000'                                                      
         SLDA  RE,1                                                             
         LH    R1,MLEQU                                                         
         DR    RE,R1                                                            
         A     RF,=F'1'                                                         
         SRL   RF,1                                                             
         BR    R9                                                               
         EJECT                                                                  
NEXTEL   ZIC   RE,1(R7)                                                         
         LTR   RE,RE                                                            
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R7,RE                                                            
NEXTEL2  CLI   0(R7),0                                                          
         BE    NEXTELX                                                          
         CLI   0(R7),X'10'                                                      
         BNE   NEXTEL                                                           
         CLC   3(1,R7),STGCODE                                                  
         BNE   NEXTEL                                                           
         CLC   4(2,R7),BOOK                                                     
         BNE   NEXTEL                                                           
         BR    R9                                                               
NEXTELX  LTR   R9,R9                                                            
         BR    R9                                                               
         EJECT                                                                  
MLFULL   DS    F                                                                
MLDUB    DS    D                                                                
MLPARAM  DS    6F                                                               
MLSTART  DS    H                                                                
MLEND    DS    H                                                                
MLADDR   DS    6F                                                               
MLEQU    DS    H                                                                
MLHALF   DS    H                                                                
MLSECOND DS    C                                                                
STGCODE  DS    C                                                                
BOOK     DS    CL2                                                              
         PRINT OFF                                                              
         EJECT                                                                  
       ++INCLUDE SPREPWORKD                                                     
         EJECT                                                                  
       ++INCLUDE SPMEDBLOCK                                                     
         EJECT                                                                  
       ++INCLUDE SPGENPEPSI                                                     
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'026SPMEDGETPL05/01/02'                                      
         END                                                                    
