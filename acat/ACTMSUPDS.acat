*          DATA SET ACTMSUPDS  AT LEVEL 086 AS OF 12/16/98                      
*CATALP TMSUPD                                                                  
***********************************************************************         
* UPDATE TIME TOTAL RECORDS.                                          *         
*                                                                     *         
* THIS MODULE SHOULD BE INCLUDED IN:                                  *         
*    ACGEN6E                                                          *         
*    ACCAP30                                                          *         
*    ACAU02                                                           *         
*                                                                     *         
* PARAM 1  0  X'80'  UPDATE TIME TOTAL RECORD                         *         
*             X'40'  ADD RECORD TO BUFFER                             *         
*         1-3 A(TRANSACTION OR TMS RECORD)                            *         
*       2  =  A(COMFACS)                                              *         
*       3  =  A(TMS UPDATE BLOCK)                                     *         
***********************************************************************         
         TITLE 'TIME UPDATE PROGRAM'                                            
TMSUPD   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 LWSX-LWS,*TMSU*,R9,CLEAR=YES                                     
         USING LWSD,RC                                                          
         MVC   PARMS,0(R1)                                                      
         L     R8,ACOMFAC                                                       
         USING COMFACSD,R8                                                      
         L     R7,ATMSUBLK                                                      
         USING TMSUD,R7                                                         
*                                                                               
         LA    RF,IO1                                                           
         ST    RF,AIO1                                                          
         LA    RF,IO2                                                           
         ST    RF,AIO2                                                          
*                                                                               
         EJECT                                                                  
***********************************************************************         
* FILTER TRANSACTIONS AND TIME RECORDS                                *         
***********************************************************************         
FLTK     TM    ARECORD,PMTRANS     TRANSACTION?                                 
         BO    FLTK2                                                            
         TM    ARECORD,PMUPDTRC    UPDATE RECORD?                               
         BZ    XIT                                                              
         L     RF,ARECORD          USE PERSON PASSED                            
         MVC   PERACC,0(RF)                                                     
         MVC   COMPANY,PERACC                                                   
         BAS   RE,BLDR             NO, WE'RE ALL DONE.  BUILD & UPDATE          
         B     XIT                                                              
*                                                                               
FLTK2    L     R2,ARECORD                                                       
         USING TRNRECD,R2                                                       
         CLC   TRNKUNT(2),PERSUL   PERSON LEDGER ONLY                           
         BNE   XIT                                                              
         CLC   TRNKREF,SPACES      TEST TRANSACTION OR TIME RECORD              
         BNH   XIT                                                              
         MVC   PERACC,TRNKEY                                                    
         MVC   COMPANY,PERACC                                                   
         LA    RF,TIMRSTA-TIMRECD(R2)                                           
         CLC   TRNKREF,=C'*TIME*'  TIME RECORD?                                 
         BNE   FLTK3                                                            
         USING TIMRECD,R2                                                       
         CLI   TIMKRI1-TIMKSTA(RF),TIMKRI1Q                                     
         BNE   FLTK3                                                            
         CLI   TIMKRI2-TIMKSTA(RF),TIMKRI2Q                                     
         BNE   FLTK3                                                            
         CLI   TIMKRI3-TIMKSTA(RF),TIMKRI3Q                                     
         BNE   FLTK3                                                            
         CLI   TIMKRI4-TIMKSTA(RF),TIMKRI4Q                                     
         BNE   FLTK3                                                            
         B     FLTKTIM                                                          
*                                                                               
         USING TRNRECD,R2                                                       
FLTK3    TM    TRNKSTAT-TRNKSTA(RF),TRNSDRFT  SKIP DRAFT TRANSACTIONS           
         BO    XIT                                                              
         CLI   TRNKSTYP-TRNKSTA(RF),27        TYPE 27, 34, 41 AND 49            
         BE    FLTKTRN                                                          
         CLI   TRNKSTYP-TRNKSTA(RF),34                                          
         BE    FLTKTRN                                                          
         CLI   TRNKSTYP-TRNKSTA(RF),41                                          
         BE    FLTKTRN                                                          
         CLI   TRNKSTYP-TRNKSTA(RF),49                                          
         BE    FLTKTRN                                                          
         B     XIT                 NOT A TIME TRANSACTION                       
                                                                                
FLTKTRN  OI    TYPSW,TYPTRN        OLD TIME TRANSACTION                         
         B     PTRN                PROCESS TRANSACTION                          
FLTKTIM  OI    TYPSW,TYPTIM        NEW TIME RECORD                              
         B     PTIM                                                             
*                                                                               
         B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
* GET HOURS FROM TRANSACTION RECORDS                                 *          
***********************************************************************         
         USING TRNRECD,R2                                                       
PTRN     LA    R4,TRNRFST                                                       
         USING TRNELD,R4                                                        
         CLI   0(R4),TRNELQ        TRANSACTION ELEMENT                          
         BNE   XIT                                                              
         SR    R0,R0                                                            
         SR    R1,R1                                                            
         SR    R3,R3                                                            
         SR    R5,R5                                                            
         LR    RF,R4                                                            
         MVC   TEMPDATE,TRNDATE                                                 
         MVC   TEMPOFF,TRNKOFF                                                  
*                                                                               
         BAS   RE,GETOFFC          GET THE OFFICE FROM 1R ACCT                  
PTRN5    BAS   RE,CLNDR            GET PERIOD END DATE FOR TRANSACTION          
*                                                                               
PTRN7    IC    R0,1(RF)            GET HOURS/STATUS ELEMENTS                    
         AR    RF,R0                                                            
         CLI   0(RF),0             TEST EOR                                     
         BE    PTRN9                                                            
         CLI   0(RF),TRSELQ        STATUS ELEMENT                               
         BNE   *+6                                                              
         LR    R3,RF                                                            
         CLI   0(RF),TIMELQ        TIME ELEMENT                                 
         BNE   *+6                                                              
         LR    R1,RF                                                            
         CLI   0(RF),SCIELQ        SUBSIDIARY CASH ELEMENT                      
         BNE   PTRN7                                                            
         CLI   SCITYPE-SCIELD(RF),SCITHOUR    TEST HOURS ELEMENT                
         BNE   PTRN7                                                            
         LR    R5,RF                                                            
         B     PTRN7                                                            
*                                                                               
PTRN9    LA    R6,TS               PERIOD TOTAL HOURS                           
         USING TSD,R6                                                           
         LTR   R3,R3               TEST STATUS ELEMENT                          
         BZ    XIT                                                              
         LTR   R5,R5               TEST HOURS ELEMENT                           
         BZ    XIT                                                              
         USING TRSELD,R3                                                        
         USING SCIELD,R5                                                        
*                                                                               
         XC    TS,TS                                                            
         TM    TRSSTAT2,TRSSTADJ   TEST ADJUSTED HOURS                          
         BO    PTRN9X                                                           
         CLI   TRNRSTYP,34         SEPERATE CHECK FOR TYPE 34                   
         BNE   PTRN9Z                                                           
         LTR   R1,R1                                                            
         BZ    PTRN9Z                                                           
         USING TIMELD,R1                                                        
         TM    TIMIND,TIMIADJ                                                   
         BZ    PTRN9Z                                                           
PTRN9X   OI    TSTYPE,TSADJSTD     MARK AS SUCH                                 
PTRN9Z   MVC   TSNMOA,ENDMOA                                                    
         MVC   TSSMOA,STRMOA                                                    
         MVC   TSPEDT,TEMPDATE     PERIOD END                                   
         MVC   TSMOA,TRSPMOS       MOA                                          
         ZAP   TSTHRS,SCIAMNT                                                   
         ZAP   TSCLTH,=P'0'                                                     
*                                                                               
PTRN11   CLC   TRNKULC(2),COSTUL   CLIENT TIME?                                 
         BNE   *+10                                                             
         ZAP   TSCLTH,SCIAMNT                                                   
         BAS   RE,ADDT                                                          
*                                                                               
PTRN15   TM    ARECORD,PMUPDTRC    UPDATE RECORD SET TOO?                       
         BZ    XIT                                                              
         BAS   RE,BLDR                                                          
         B     XIT                                                              
*                                                                               
         DROP  R1                                                               
         EJECT                                                                  
***********************************************************************         
* GET HOURS FROM TIME RECORDS                                         *         
***********************************************************************         
         USING TIMRECD,R2                                                       
         USING TIMELD,R4                                                        
PTIM     LA    R4,TIMRFST                                                       
         SR    R0,R0                                                            
*                                                                               
PTIM5    CLI   0(R4),TIMELQ        TIME DETAIL ELEMENT                          
         BNE   PTIM9               SKIP IT                                      
         CLI   TIMETYP,TIMEINP     INPUT DETAILS                                
         BNE   PTIM9                                                            
         LA    R6,TS               PERIOD TOTAL HOURS                           
         USING TSD,R6                                                           
         NI    TSTYPE,X'FF'-TSADJSTD                                            
         TM    TIMIND,TIMIADJ      ADJUSTED HOURS?                              
         BZ    *+8                                                              
         OI    TSTYPE,TSADJSTD     MARK AS SUCH                                 
         MVC   TSPEDT,TIMKPEDT     PERIOD END DATE                              
         MVC   TSMOA,TIMMOA        MOA                                          
         ZAP   TSTHRS,TIMHRS                                                    
         ZAP   TSCLTH,=P'0'                                                     
*                                                                               
PTIM7    CLC   TIMKULC(2),COSTUL                                                
         BNE   *+10                                                             
         ZAP   TSCLTH,TIMHRS                                                    
         BAS   RE,ADDT                                                          
*                                                                               
PTIM9    IC    R0,1(R4)            GET NEXT TIME ELEMENT                        
         AR    R4,R0                                                            
         CLI   0(R4),0             TEST EOR                                     
         BNE   PTIM5                                                            
         TM    ARECORD,PMUPDTRC    UPDATE RECORD?                               
         BZ    XIT                                                              
         BAS   RE,BLDR                                                          
         B     XIT                 GET ANOTHER RECORD                           
         DROP  R2,R4,R6                                                         
         EJECT                                                                  
***********************************************************************         
* GET OFFICE FROM 1R ACCOUNT                                          *         
*       READS THE 1R LEDGER TO SEE LENGTH OF OFFICE                   *         
*       SAVES THE OFFICE IN TEMPOFF                                   *         
***********************************************************************         
         USING LDGRECD,R2                                                       
         USING TRNRECD,R3                                                       
         USING ACLELD,R4                                                        
GETOFFC  NTR1                                                                   
         LR    R3,R2                                                            
         LA    R2,DKEY                                                          
         MVC   LDGKEY,SPACES                                                    
         MVC   LDGKCPY(LDGKEND),TRNKCPY                                         
         BAS   RE,DMHGH                                                         
         CLC   LDGKEY(LDGKEND),DIR                                              
         BNE   XIT                                                              
         L     R2,AIO2                                                          
         BAS   RE,DMGETR                                                        
         LA    R4,LDGRFST                                                       
         SR    R1,R1                                                            
GETOFFC3 CLI   0(R4),0                                                          
         BE    XIT                                                              
         CLI   ACLEL,ACLELQ                                                     
         BE    GETOFFC5                                                         
         IC    R1,ACLLN                                                         
         AR    R4,R1                                                            
         B     GETOFFC3                                                         
*                                                                               
GETOFFC5 MVC   TEMPOFF,SPACES                                                   
         IC    R1,ACLVLEN                                                       
         BCTR  R1,0                                                             
         EX    R1,*+4                                                           
         MVC   TEMPOFF(0),TRNKACT                                               
         B     XIT                                                              
*                                                                               
         DROP  R2,R3,R4                                                         
         EJECT                                                                  
***********************************************************************         
* READ CALENDAR RECORD TO GET PERIOD END DATE                         *         
*        INPUT: TEMPDATE = TRANSACTION DATE                           *         
*       OUTPUT: TEMPDATE = PERIOD END DATE                            *         
***********************************************************************         
         USING CASRECD,R2                                                       
CLNDR    NTR1                                                                   
         LA    R2,DKEY                                                          
         XC    CASKEY,CASKEY                                                    
         MVI   CASPTYP,CASPTYPQ    X'3E'                                        
         MVI   CASPSUB,CASPSUBQ    X'0C'                                        
         MVC   CASPCPY,COMPANY     COMPANY                                      
         MVC   CASPEDTE,TEMPDATE   POSSIBLE MOA                                 
         MVC   SVKEY,DKEY                                                       
         BAS   RE,DMHGH                                                         
         B     *+8                                                              
CLNDR10  BAS   RE,DMSEQ                                                         
         CLC   DKEY(CASPEDTE-CASKEY),DIR     UP TO COMPANY MUST MATCH           
         BNE   CLNDRWKS                                                         
         CLC   DIR+CASPOFC-CASKEY(L'CASPOFC),SPACES                             
         BNE   CLNDR10                                                          
         MVC   DKEY,DIR                                                         
         MVC   SVKEY,DKEY                                                       
*                                                                               
CLNDR20  MVC   CASPOFC,TEMPOFF                                                  
         BAS   RE,DMHGH                                                         
         CLC   DIR(CASPSTA-CASKEY),DKEY                                         
         BE    CLNDR30             OFFICES THE SAME?                            
CLNDR23  MVC   DKEY,SVKEY                                                       
         BAS   RE,DMHGH                                                         
CLNDR25  CLC   DIR(CASPSTA-CASKEY),SVKEY                                        
         BE    CLNDR30                                                          
         BAS   RE,DMSEQ                                                         
         B     CLNDR25                                                          
*                                                                               
CLNDR30  L     R2,AIO2                                                          
         BAS   RE,DMGETR           GET THE CALENDAR RECORD                      
         LA    R4,CASRFST                                                       
         SR    R0,R0                                                            
         MVC   ENDMOA,CASKEMOA     SAVE START AND END MOA                       
         MVC   STRMOA,CASKSMOA                                                  
*                                                                               
         USING TMPELD,R4                                                        
CLNDR50  CLI   0(R4),TMPELQ        X'88' ELEMENT                                
         BE    CLNDR90                                                          
         CLI   0(R4),0                                                          
         BE    XIT                 EOR                                          
CLNDR70  IC    R0,1(R4)                                                         
         AR    R4,R0                                                            
         B     CLNDR50                                                          
*                                                                               
CLNDR90  CLC   TMPEND,TEMPDATE   TEST BEFORE START                              
         BL    CLNDR70                                                          
         MVC   TEMPDATE,TMPEND                                                  
         B     XIT                                                              
*                                                                               
CLNDRWKS DS    0H                  CALCULATE SATURDAY FOR DATE                  
         GOTO1 CDATCON,DMCB,(1,TEMPDATE),(0,TODAY)                              
         MVC   WORK(6),TODAY                                                    
         MVC   WORK+6(6),WORK                                                   
*                                  GET SATURDAY START                           
CLNDRWK5 GOTO1 CGETDAY,DMCB,WORK+6,WORK+12                                      
         CLC   WORK+12(3),=C'SAT'                                               
         BE    CLNDRWK7                                                         
         MVC   WORK(6),WORK+6                                                   
         GOTO1 CADDAY,DMCB,WORK,WORK+6,F'1'                                     
         B     CLNDRWK5                                                         
*                                                                               
CLNDRWK7 GOTO1 CDATCON,DMCB,(0,WORK+6),(1,TEMPDATE)                             
         MVC   ENDMOA,TEMPDATE                                                  
         MVC   STRMOA,TEMPDATE                                                  
         MVI   ENDMOA+1,X'12'                                                   
         MVI   STRMOA+1,1                                                       
         B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
* ADD ITEM TO TABLE                                                   *         
***********************************************************************         
         USING TSD,R6                                                           
NEW0     USING TSD,R3                                                           
*                                                                               
ADDT     NTR1  ,                                                                
         LA    R3,TSTAB                                                         
         ICM   R0,15,NUMTAB        NUMBER IN TABLE                              
         BZ    ADDT9                                                            
*                                                                               
ADDT3    CLC   0(TSKLNQ,R6),0(R3)  MATCH ITEM TO TABLE                          
         BE    ADDT5                                                            
         LA    R3,TSLNQ(R3)                                                     
         BCT   R0,ADDT3                                                         
         B     ADDT9                                                            
*                                                                               
ADDT5    AP    NEW0.TSTHRS,TSTHRS  ADD TOTAL HOURS                              
         AP    NEW0.TSCLTH,TSCLTH  CLIENT HOURS                                 
         B     XIT                                                              
*                                                                               
ADDT9    MVC   0(TSLNQ,R3),0(R6)   SAVE NEW ITEM                                
         L     R0,NUMTAB                                                        
         AH    R0,=H'1'                                                         
         ST    R0,NUMTAB                                                        
         CH    R0,=Y(TSNMX)                                                     
         BNH   XIT                                                              
         BAS   RE,BLDR             TABLE IS FULL, PUT IT OUT TO RECORD          
         B     XIT                                                              
*                                                                               
         DROP  NEW0                                                             
         DROP  R6                                                               
         EJECT                                                                  
***********************************************************************         
* BUILD NEW TIME RECORDS AND UPDATE / ADD THEM                        *         
***********************************************************************         
         USING TSD,R6                                                           
BLDR     NTR1  ,                                                                
         ICM   R2,15,NUMTAB        SORT BY DATE                                 
         BZ    XIT                                                              
         LA    R3,TSLNQ                                                         
         LA    RF,TSKLNQ                                                        
         LA    R6,TSTAB                                                         
         GOTO1 CXSORT,DMCB,(0,(R6)),(R2),(R3),(RF),0                            
*                                                                               
BLDR3    L     R0,AIO2             CLEAR IO2                                    
         LA    R1,RLNMX                                                         
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         L     R3,AIO2             OUTPUT IO                                    
         USING TTHRECD,R3                                                       
         MVI   TTHKTYP,TTHKTYPQ    RECORD TYPE                                  
         MVI   TTHKSUB,TTHKSUBQ                                                 
         MVC   TTHKCULA,PERACC     ACCOUNT CODE                                 
         MVI   TTHKTIME,TTHKTTOT   TYPE OF TIME                                 
         MVC   TTHKEMOA,TSNMOA                                                  
         MVC   TTHKSMOA,TSSMOA                                                  
         MVC   TTHRLEN,=Y(TTHRFST-TTHRECD)                                      
*                                                                               
BLDR5    CLC   TTHKEMOA,TSNMOA                                                  
         BNE   BLDR10                                                           
         CLC   TTHKSMOA,TSSMOA                                                  
         BNE   BLDR10                                                           
*                                                                               
BLDR6    LA    R4,ELEMENT                                                       
         USING PTHELD,R4                                                        
         MVI   PTHEL,PTHELQ                                                     
         TM    TSTYPE,TSADJSTD     ADJUSTED HOURS                               
         BZ    *+8                                                              
         MVI   PTHEL,PTHELQA                                                    
         MVI   PTHLN,PTHLNQ                                                     
         MVC   PTHEDT,TSPEDT                                                    
         MVC   PTHMOA,TSMOA                                                     
         ZAP   PTHCHRS,TSCLTH                                                   
         ZAP   PTHTHRS,TSTHRS                                                   
         LA    R5,PTHMOA                                                        
*                                                                               
BLDR7    LA    R6,TSLNQ(R6)        BUMP TO NEXT ELEMENT                         
         CLC   PTHEDT,TSPEDT       CHECK IF PERIOD THE SAME                     
         BNE   BLDR9                                                            
         CLI   PTHEL,PTHELQ        REGULAR X'8D' ELEMENT                        
         BNE   BLDR7C              NO, HAS TO BE X'8E' ADJUSTED                 
         TM    TSTYPE,TSADJSTD     NEW ONE IS ADJUSTED?                         
         BO    BLDR9               YES, SEND THIS ELEMENT OUT                   
         B     BLDR8                                                            
*                                                                               
BLDR7C   TM    TSTYPE,TSADJSTD     ADJUSTED ELEMENT, IS NEW ONE ADJSTD?         
         BZ    BLDR9               NO, SEND THIS ELEMENT OUT                    
*                                                                               
BLDR8    BCTR  R2,0                                                             
         LTR   R2,R2                                                            
         BZ    BLDR10                                                           
         USING PTHMOA,R5                                                        
         LA    R5,L'PTHSLNQ(R5)                                                 
         MVC   PTHMOA,TSMOA        MORE SUBELEMENT FOR MOAS                     
         ZAP   PTHCHRS,TSCLTH                                                   
         ZAP   PTHTHRS,TSTHRS                                                   
         SR    RF,RF               BUMP UP LENGTH OF ELEMENT                    
         IC    RF,PTHLN                                                         
         LA    RF,L'PTHSLNQ(RF)                                                 
         STC   RF,PTHLN                                                         
         B     BLDR7                                                            
*                                                                               
BLDR9    L     RF,AIO2                                                          
         LA    R4,ELEMENT                                                       
         BAS   RE,ADDL             ADD THE NEW ELEMENT                          
         XC    ELEMENT,ELEMENT                                                  
         LTR   R2,R2                                                            
         BZ    BLDR10                                                           
         BCT   R2,BLDR5                                                         
*                                                                               
BLDR10   OC    ELEMENT,ELEMENT     ANYTHING LEFT                                
         BZ    BLDR11                                                           
         L     RF,AIO2                                                          
         LA    R4,ELEMENT                                                       
         BAS   RE,ADDL             ADD THE NEW ELEMENT                          
         XC    ELEMENT,ELEMENT                                                  
*                                                                               
BLDR11   CLI   TTHRFST,0           TEST ANYTHING ADDED                          
         BE    *+8                 NO, DON'T BOTHER UPDATING                    
         BAS   RE,UPDR                                                          
*                                                                               
BLDR20   XC    NUMTAB,NUMTAB       DONE, CLEAR TABLE                            
         XC    TSTAB(256),TSTAB                                                 
         B     XIT                                                              
         DROP  R5                                                               
         EJECT                                                                  
***********************************************************************         
* UPDATE TIME RECORD                                                  *         
***********************************************************************         
         USING TTHRECD,R3                                                       
UPDR     NTR1  ,                                                                
         L     R3,AIO2             R3=NEW RECORD                                
         MVC   DKEY,TTHKEY                                                      
         BAS   RE,DMRD             TEST RECORD ON FILE                          
         CLC   DKEY(L'TTHKEY),DIR                                               
         BE    UPDR3                                                            
         LR    R2,R3                                                            
         BAS   RE,DMADDR           ADD NEW RECORD                               
         B     XIT                                                              
*                                                                               
UPDR3    L     R2,AIO1                                                          
         MVC   SVDA,DA                                                          
         BAS   RE,DMGETRU          GET OLD RECORD, READ FOR UPDATE              
         BAS   RE,MRGE             MERGE ELEMENTS, NEW RECORD IN AIO2           
*                                                                               
         L     R2,AIO2                                                          
         BAS   RE,DMPUTR                                                        
         B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
* MERGE TIME CHANGES WITH EXITING RECORD                              *         
***********************************************************************         
         USING TTHRECD,R3                                                       
MRGE     NTR1  ,                                                                
         L     R3,AIO1                                                          
         LA    R2,TTHRFST                                                       
         SR    R0,R0                                                            
*                                                                               
MRGE3    CLI   0(R2),PTHELQ        PERIOD ELEMENT                               
         BE    *+12                                                             
         CLI   0(R2),PTHELQA       ADJUSTED PERIOD ELEMENT                      
         BNE   MRGE9                                                            
         LR    R4,R2                                                            
         USING PTHELD,R4                                                        
NEW1     USING PTHELD,R5                                                        
*                                                                               
         GOTO1 CHELLO,LPARM,(C'G',ACCMST),(PTHEL,AIO2),(3,2(R4)),0              
         CLI   LP4,0               TEST ELEMENT FOUND                           
         BNE   MRGE7               ADD TO NEW RECORD                            
         L     R5,LP4              MERGE ELEMENT DATA                           
         BAS   RE,APPENDEL                                                      
         B     MRGE9                                                            
*                                                                               
MRGE7    BAS   RE,ADDL             ADD ELEMENT TO NEW RECORD                    
*                                                                               
MRGE9    IC    R0,1(R2)                                                         
         AR    R2,R0                                                            
         CLI   0(R2),0                                                          
         BNE   MRGE3                                                            
         B     XIT                                                              
         EJECT                                                                  
**********************************************************************          
* APPEND NEW MOA TO ELEMENT IF NECESSARY                             *          
**********************************************************************          
APPENDEL NTR1                                                                   
         LR    R1,R5               SAVE FOR LATER                               
         XC    ELEMENT,ELEMENT                                                  
         MVC   ELEMENT(PTHBLNQ),NEW1.PTHEL                                      
         ZIC   R0,NEW1.PTHLN                                                    
         SH    R0,=Y(PTHBLNQ)                                                   
         ZIC   RE,PTHLN                                                         
         SH    RE,=Y(PTHBLNQ)                                                   
         LA    RF,ELEMENT                                                       
         LA    RF,PTHBLNQ(RF)                                                   
*                                                                               
APEL     USING PTHMOA,RF                                                        
APPEL03  CLC   NEW1.PTHMOA,PTHMOA                                               
         BNE   APPEL05                                                          
         MVC   0(L'PTHSLNQ,RF),NEW1.PTHMOA                                      
         AP    APEL.PTHTHRS,PTHTHRS                                             
         AP    APEL.PTHCHRS,PTHCHRS                                             
         LA    R5,L'PTHSLNQ(R5)                                                 
         SH    R0,=Y(L'PTHSLNQ)                                                 
         B     APPEL10                                                          
*                                                                               
APPEL05  BH    APPEL07                                                          
         MVC   0(L'PTHSLNQ,RF),NEW1.PTHMOA                                      
         LA    RF,L'PTHSLNQ(RF)                                                 
         LA    R5,L'PTHSLNQ(R5)                                                 
         SH    R0,=Y(L'PTHSLNQ)                                                 
         BZ    APPEL60                                                          
         B     APPEL03                                                          
*                                                                               
APPEL07  MVC   0(L'PTHSLNQ,RF),PTHMOA        APPEND TO ELEMENT                  
*                                                                               
APPEL10  LA    RF,L'PTHSLNQ(RF)                                                 
         LA    R4,L'PTHSLNQ(R4)                                                 
         SH    RE,=Y(L'PTHSLNQ)                                                 
         BZ    APPEL50                                                          
         LTR   R0,R0                                                            
         BZ    APPEL60                                                          
         B     APPEL03                                                          
*                                                                               
APPEL50  LTR   R0,R0                                                            
         BZ    APPEL70                                                          
         MVC   0(L'PTHSLNQ,RF),NEW1.PTHMOA                                      
         LA    R5,L'PTHSLNQ(R5)                                                 
         LA    RF,L'PTHSLNQ(RF)                                                 
         SH    R0,=Y(L'PTHSLNQ)                                                 
         BZ    APPEL70                                                          
         B     APPEL50                                                          
*                                                                               
APPEL60  LTR   RE,RE                                                            
         BZ    APPEL70                                                          
         MVC   0(L'PTHSLNQ,RF),PTHMOA                                           
         LA    R4,L'PTHSLNQ(R4)                                                 
         LA    RF,L'PTHSLNQ(RF)                                                 
         SH    RE,=Y(L'PTHSLNQ)                                                 
         BZ    APPEL70                                                          
         B     APPEL60                                                          
*                                                                               
APPEL70  MVI   0(RF),0                                                          
         LA    RE,ELEMENT                                                       
         SR    RF,RE               CALCULATE LENGTH OF ELEMENT                  
         STC   RF,ELEMENT+1                                                     
         LR    R5,R1                                                            
         CLC   NEW1.PTHLN,ELEMENT+1                                             
         BNE   APPEL80                                                          
         BCTR  RF,0                                                             
         EX    RF,*+4                                                           
         MVC   NEW1.PTHEL(0),ELEMENT                                            
         B     APPEL90                                                          
*                                                                               
APPEL80  MVI   NEW1.PTHEL,X'FF'                                                 
         GOTO1 CHELLO,LPARM,(C'D',ACCMST),(X'FF',AIO2),0,0                      
         CLI   LP4,0                                                            
         BE    *+6                                                              
         DC    H'0'                                                             
         L     RF,AIO2                                                          
         LA    R4,ELEMENT                                                       
         BAS   RE,ADDL                                                          
*                                                                               
APPEL90  B     XIT                                                              
         DROP  NEW1                                                             
         DROP  APEL                                                             
         DROP  R3,R4                                                            
         EJECT                                                                  
***********************************************************************         
* ADD TIME ELEMENT TO RECORD                                          *         
***********************************************************************         
         USING TTHRECD,R3                                                       
ADDL     NTR1  ,                                                                
         L     R3,AIO2                                                          
ADDL3    SR    RE,RE               TEST RECORD LENGTH                           
         ICM   RE,3,TTHRLEN                                                     
         SR    R0,R0                                                            
         IC    R0,0(R4)                                                         
         AR    RE,R0                                                            
         CH    RE,=Y(RECLNMX)                                                   
         BL    ADDL5                                                            
         MVI   TTHRFST,X'FF'       DELETE OLDEST                                
         GOTO1 CHELLO,LPARM,(C'D',ACCMST),(X'FF',(R3)),0,0                      
         CLI   LP4,0                                                            
         BE    ADDL3                                                            
         DC    H'0'                                                             
*                                                                               
ADDL5    GOTO1 CHELLO,LPARM,(C'P',ACCMST),(R3),(R4),0                           
         CLI   LP4,0                                                            
         BE    *+6                                                              
         DC    H'0'                                                             
         B     XIT                                                              
*                                                                               
XIT      XIT1  ,                                                                
         EJECT                                                                  
***********************************************************************         
* DATA MANAGER ROUTINES                                               *         
***********************************************************************         
DMRD     LR    R0,RE                                                            
         GOTO1 CDATAMGR,DMCB,(X'08',DMREAD),ACCDIR,DKEY,DIR                     
         MVC   DA,DIR+(ACCKDA-ACCRECD)                                          
         B     DMERR                                                            
*                                                                               
DMHGH    LR    R0,RE                                                            
         GOTO1 CDATAMGR,DMCB,DMRDHI,ACCDIR,DKEY,DIR                             
         MVC   DA,DIR+(ACCKDA-ACCRECD)                                          
         B     DMERR                                                            
*                                                                               
DMSEQ    LR    R0,RE                                                            
         GOTO1 CDATAMGR,DMCB,DMRSEQ,ACCDIR,DKEY,DIR                             
         MVC   DA,DIR+(ACCKDA-ACCRECD)                                          
         B     DMERR                                                            
*                                                                               
DMGETR   LR    R0,RE                                                            
         GOTO1 CDATAMGR,DMCB,GETREC,ACCMST,DA,(R2),DMWORK                       
         B     DMERR                                                            
*                                                                               
DMGETRU  LR    R0,RE                                                            
         GOTO1 CDATAMGR,DMCB,(X'80',GETREC),ACCMST,DA,(R2),DMWORK               
         B     DMERR                                                            
*                                                                               
DMWRTR   LR    R0,RE                                                            
         GOTO1 CDATAMGR,DMCB,DMWRT,ACCDIR,DIR,DIR                               
         B     DMERR                                                            
*                                                                               
DMADDR   LR    R0,RE                                                            
         GOTO1 CDATAMGR,DMCB,ADDREC,ACCMST,DKEY,(R2),DMWORK                     
         B     DMERR                                                            
*                                                                               
DMPUTR   LR    R0,RE                                                            
         GOTO1 CDATAMGR,DMCB,PUTREC,ACCMST,DA,(R2),DMWORK                       
*                                                                               
DMERR    MVC   BYTE,8(R1)                                                       
         NI    BYTE,X'FF'-(X'10'+X'02') IGNORE RNF/RECORD DELETED               
         CLI   BYTE,0                                                           
         BE    *+6                                                              
         DC    H'0'                                                             
         LR    RE,R0                                                            
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* CONSTANTS                                                           *         
***********************************************************************         
*                                                                               
ACCFIL   DC    CL8'ACCOUNT '                                                    
ACCDIR   DC    CL8'ACCDIR  '                                                    
ACCMST   DC    CL8'ACCMST  '                                                    
GETREC   DC    CL8'GETREC  '                                                    
DMREAD   DC    CL8'DMREAD  '                                                    
DMRSEQ   DC    CL8'DMRSEQ  '                                                    
DMRDHI   DC    CL8'DMRDHI  '                                                    
DMADD    DC    CL8'DMADD   '                                                    
DMWRT    DC    CL8'DMWRT   '                                                    
ADDREC   DC    CL8'ADDREC  '                                                    
PUTREC   DC    CL8'PUTREC  '                                                    
*                                                                               
PERSUL   DC    C'1R'                                                            
COSTUL   DC    C'1C'                                                            
NONCUL   DC    C'1N'                                                            
*                                                                               
SPACES   DC    CL42' '                                                          
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* WORKING STORAGE                                                     *         
***********************************************************************         
LWSD     DSECT                                                                  
LWS      DS    0X                                                               
*                                                                               
PARMS    DS    0XL16                                                            
ARECORD  DS    AL4                                                              
ACOMFAC  DS    AL4                                                              
ATMSUBLK DS    AL4                                                              
A1ROFFC  DS    AL4                                                              
PMUPDTRC EQU   X'80'               UPDATE TIME TOTAL RECORD                     
PMTRANS  EQU   X'40'               TRANSACTION PASSED                           
*                                                                               
DMCB     DS    6F                                                               
DUB      DS    D                                                                
BYTE     DS    XL1                                                              
WORK     DS    XL64                                                             
DMWORK   DS    XL96                                                             
*                                                                               
FWKDTE   DS    PL3                 FIRST WEEK                                   
TODAY    DS    CL6                 TODAY DATE                                   
TODAYC   DS    XL2                 TODAY COMPRESSED                             
PL8      DS    PL8                                                              
*                                                                               
COMMAND  DS    CL8                 USED IN CDATAMGR IO                          
DKEY     DS    CL(L'ACCKEY)        DIRECTORY KEY                                
DIR      DS    CL64                DIRECTORY RECORD                             
DA       DS    F                                                                
SVDA     DS    F                                                                
*                                                                               
TYPSW    DS    XL1                 TYPE OF TIME RECORD                          
TYPTRN   EQU   X'80'               TRANSACTION                                  
TYPTIM   EQU   X'40'               TIME RECORD                                  
*                                                                               
ELEMENT  DS    XL255                                                            
*                                                                               
COMPANY  DS    CL1                 COMPANY CODE                                 
CMPSW    DS    XL1                 COMPANY OPTIONS                              
CMPNC    EQU   X'80'               ON NEW COST                                  
CMPTMS   EQU   X'40'               ON TMS                                       
PERACC   DS    CL15                ACCOUNT CODE                                 
LVLS1R   DS    XL4                 LEVELS OF 1R                                 
OFC      DS    CL2                 OFFICE CODE                                  
*                                                                               
LPARM    DS    0F                  PARM LIST FOR CHELLO                         
LP1      DS    F                   ACTION, A(FILE)                              
LP2      DS    F                   ELEMENT CODE, A(RECORD)                      
LP3      DS    F                   ARG. LEN, A(ELEMENT)                         
LP4      DS    F                   COMPLETION CODE, NEW LENGTH                  
LP5      DS    F                                                                
*                                                                               
TEMPDATE DS    XL3                 TEMPORARY DATE                               
TEMPOFF  DS    CL2                 TEMPORARY OFFICE                             
ENDMOA   DS    PL2                 END MOA                                      
STRMOA   DS    PL2                 STR MOA                                      
SVKEY    DS    CL(L'ACCKEY)        TEMPORARY KEY                                
AIO1     DS    A                   IO AREA #1                                   
AIO2     DS    A                   IO AREA #2                                   
*                                                                               
TS       DS    XL(TSLNQ)                                                        
TSNMX    EQU   9                                                                
         EJECT                                                                  
***********************************************************************         
* BUFFERS/TABLES/ETC.                                                 *         
***********************************************************************         
RECLNMX  EQU   1900                                                             
RLNMX    EQU   2100+24+4                                                        
         DC    C'**IO1***'                                                      
         DC    F'0'                      IOAREA #1                              
IO1      DC    (RLNMX)X'00'                                                     
*                                                                               
         DC    C'**IO2***'                                                      
         DC    F'0'                      IOAREA #2                              
IO2      DC    (RLNMX)X'00'                                                     
*                                                                               
LWSX     EQU   *                                                                
         EJECT                                                                  
***********************************************************************         
* DSECT FOR TMS UPDATE BLOCK                                          *         
***********************************************************************         
TMSUD    DSECT                                                                  
NUMTAB   DS    F                   NUMBER IN TABLE                              
TSTAB    DS    ((TSNMX+1)*TSLNQ)C                                               
TSTABX   DS    0H                                                               
         EJECT                                                                  
***********************************************************************         
* DSECT FOR TIME SUMMARY ENTRY                                        *         
***********************************************************************         
TSD      DSECT                                                                  
TSNMOA   DS    PL2                 ENDING MOA                                   
TSSMOA   DS    PL2                 START MOA                                    
TSTYPE   DS    XL1                 TYPE                                         
TSADJSTD EQU   X'80'               ADJUSTED TIME                                
TSPEDT   DS    XL3                 PERIOD END DATE                              
TSMOA    DS    XL2                 MONTH                                        
TSKLNQ   EQU   *-TSD                                                            
TSTHRS   DS    PL5                 FIRST HOURS                                  
TSCLTH   DS    PL5                 TOTAL HOURS                                  
TSLNQ    EQU   *-TSD                                                            
         EJECT                                                                  
* ACGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
         PRINT ON                                                               
* DDCOMFACS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'086ACTMSUPDS 12/16/98'                                      
         END                                                                    
