*          DATA SET DMDEMVER   AT LEVEL 004 AS OF 01/20/18                      
*CATALP DEMVER                                                                  
***********************************************************************         
* SUBROUTINE TO APPLY DEMO VSAM "VERSION CONTROL" OFFLINE.            *         
*                                                                     *         
* CALLED BY DDRUNNER AND DDMASTER AT START OF NEW REQUEST.            *         
*                                                                     *         
* WILL SET VERSION CONTROL FLAGS IN SSODMST2 ACCORDING TO TABLE IN    *         
* DMGR DATASPACE.                                                     *         
***********************************************************************         
         EJECT                                                                  
DEMVER   TITLE 'SET VSAM DEMO VERSION CONTROL OFFLINE'                          
         PRINT NOGEN                                                            
                                                                                
***********************************************************************         
* NO PARAMETERS REQUIRED UNLESS CALLED BY RUNNER                      *         
*                                                                     *         
* PARAMETERS WHEN CALLED BY RUNNER:                                   *         
*                                                                     *         
* P1     A(DDLINK CONTROL BLOCK (LP_D IN DDLINKD))                    *         
*                                                                     *         
***********************************************************************         
DEMVER   CSECT                                                                  
         NMOD1 WORKL,*DEMVER*,RA,CLEAR=YES                                      
         USING WORKD,RC                                                         
                                                                                
         MVC   ALINKD,0(R1)        IF RUNNER                                    
                                                                                
         L     R3,VSSB                                                          
         USING SSBOFFD,R3                                                       
         L     R4,VMASTC                                                        
         USING MASTD,R4                                                         
                                                                                
         MVC   OLDVER,SSODMST2     SAVE CURRENT VERSION CONTROL                 
         NI    OLDVER,255-SSODMSCH CLEAR 'CHANGED' FLAG                         
         MVI   NEWVER,0            CLEAR NEW VERSION CONTROL                    
                                                                                
         OC    SSOALET,SSOALET     DO WE HAVE DATAMGR DATASPACE ALET?           
         JNZ   DEMVER05            YES: NO NEED TO GET IT AGAIN                 
         ICM   RF,15,=V(LOCKSPC)   NO: MUST GET ALET (FIRST TIME)               
         JZ    PROGEXIT            CAN'T DO ANYTHING IF NO LOCKSPC              
         XC    DUB,DUB                                                          
         OI    DUB,X'20'           SET ENQUIRY ONLY                             
         LA    R1,DUB              R1 = A(LOCKSPC PARAMETER LIST)               
         BASR  RE,RF               LOCKSPC TO SET DMGR DATASPACE ALET           
         OC    SSOALET,SSOALET     NOW DO WE HAVE ALET?                         
         JZ    PROGEXIT            NO: CAN'T DO ANYTHING WITHOUT IT             
                                                                                
DEMVER05 LAM   AR2,AR2,SSOALET     DATAMGR DATASPACE ALET                       
         SAC   512                                                              
         SR    R2,R2                                                            
         L     R2,DHASSBG-DMDHDR(,R2)                                           
         USING FASSBG,R2                                                        
         LH    R0,SSGDMVST         GET COUNT OF VERSION CONTROL ENTRIES         
         LTR   R0,R0                                                            
         JNZ   DEMVER08            VERIFY AND SET VERSIONS                      
         CLI   SSGDMDRA,C'V'       DEMVSMA                                      
         BNE   *+8                                                              
         OI    NEWVER,SSODMSA      LET PROGRAM KNOW IT IS VSAM                  
         CLI   SSGDMDRN,C'V'       DEMVSMN                                      
         BNE   *+8                                                              
         OI    NEWVER,SSODMSN      LET PROGRAM KNOW IT IS VSAM                  
         CLI   SSGDMDRR,C'V'       DEMVSMR                                      
         BNE   *+8                                                              
         OI    NEWVER,SSODMSR      LET PROGRAM KNOW IT IS VSAM                  
         CLI   SSGDMNTI,C'V'       NTIVSM                                       
         BNE   *+8                                                              
         OI    NEWVER,SSODMSNT     LET PROGRAM KNOW IT IS VSAM                  
         CLI   SSGDMPAV,C'V'       PAVVSM                                       
         BNE   *+8                                                              
         OI    NEWVER,SSODMSPV     LET PROGRAM KNOW IT IS VSAM                  
         J     DEMVERX                                                          
*                                                                               
DEMVER08 LA    R2,SSGDMVST+2       FIRST ENTRY                                  
         USING DMVSVSNT,R2                                                      
*                                                                               
DEMVER10 OC    DMVSVTAL,DMVSVTAL   SPECIFIC ALPHA ID?                           
         JZ    DEMVER12                                                         
         CLC   DMVSVTAL,MCUSER     YES, OURS?                                   
         JNE   DEMVER50            NO, NEXT                                     
DEMVER12 CLI   DMVSVTTY,0          FURTHER QUALIFIERS?                          
         JE    DEMVER60            NO, SO THIS MATCHES                          
         TM    SSOFLAG3,SSO3RUNR   AM I A RUNNER?                               
         JZ    DEMVER30            NO, BATCH                                    
                                                                                
DEMVER20 CLI   DMVSVTTY,DMVSVTRQ   RUNNER ENTRY?                                
         JNE   DEMVER50            NO, NEXT                                     
         CLI   DMVSVTFI,0          SPECIFIC FACPAK ID?                          
         JE    DEMVER22                                                         
         CLC   DMVSVTFI,MCFACPAK   YES, OURS?                                   
         JNE   DEMVER50            NO, NEXT                                     
DEMVER22 CLI   DMVSVTSV,0          SPECIFIC SERVER?                             
         JE    DEMVER60            NO, NO FURTHER QUALIFIERS SO MATCHES         
         L     RF,ALINKD                                                        
         CLC   DMVSVTSV,LP_SVRTY-LP_D(RF) YES, OURS?                            
         JE    DEMVER60            YES, SO MATCHES                              
         J     DEMVER50            NO, NEXT                                     
                                                                                
DEMVER30 CLI   DMVSVTTY,DMVSVTBQ   BATCH ENTRY?                                 
         JNE   DEMVER50            NO, NEXT                                     
         CLI   DMVSVTFI,0          SPECIFIC FACPAK ID?                          
         JE    DEMVER32                                                         
         CLC   DMVSVTFI,MCFACPAK   YES, OURS?                                   
         JNE   DEMVER50            NO, NEXT                                     
DEMVER32 CLI   DMVSVTSY,0          SPECIFIC SYSTEM?                             
         JE    DEMVER60            NO, NO FURTHER QUALIFIERS SO MATCHES         
         CLC   DMVSVTSY,MCOVSYS    YES, OURS?                                   
         JNE   DEMVER50            NO, NEXT                                     
         OC    DMVSVTRP,DMVSVTRP   SPECIFIC PROGRAM?                            
         JZ    DEMVER60            NO, SO THIS MATCHES                          
         CLC   DMVSVTRP,MCPROG     YES, OURS?                                   
         JE    DEMVER60            YES, SO THIS MATCHES                         
                                                                                
DEMVER50 LA    R2,DMVSVTLQ(,R2)    NEXT ENTRY                                   
         JCT   R0,DEMVER10                                                      
         J     DEMVERX                                                          
                                                                                
DEMVER60 MVC   NEWVER,DMVSVTFL     COPY VERSION CONTROL SETTINGS                
         DROP  R2                                                               
                                                                                
DEMVERX  SAC   0                                                                
         LAM   AR2,AR2,ZERO                                                     
         CLC   OLDVER,NEWVER       DID IT CHANGE?                               
         JE    PROGEXIT            NO, EXIT                                     
         MVC   SSODMST2,NEWVER     APPLY NEW VERSION CONTROL                    
         OI    SSODMST2,SSODMSCH   AND SET 'CHANGED' FLAG                       
         DROP  R3                                                               
                                                                                
PROGEXIT XIT1                                                                   
         EJECT                                                                  
VSSB     DC    V(SSB)                                                           
VMASTC   DC    V(MASTC)                                                         
ZERO     DC    F'0'                                                             
         LTORG                                                                  
         EJECT                                                                  
WORKD    DSECT                                                                  
DUB      DS    D                                                                
ALINKD   DS    F                                                                
NEWVER   DS    X                                                                
OLDVER   DS    X                                                                
WORKL    EQU   *-WORKD                                                          
***********************************************************************         
* OTHER INCLUDED DSECTS                                               *         
***********************************************************************         
*FASSBOFF                                                                       
SSBOFFD  DSECT                                                                  
         PRINT OFF                                                              
       ++INCLUDE FASSBOFF                                                       
         PRINT ON                                                               
*DMDSHDR                                                                        
         PRINT OFF                                                              
       ++INCLUDE DMDSHDR                                                        
         PRINT ON                                                               
*DDLINKD                                                                        
         PRINT OFF                                                              
       ++INCLUDE DDLINKD                                                        
         PRINT ON                                                               
*DDMASTD                                                                        
         PRINT OFF                                                              
       ++INCLUDE DDMASTD                                                        
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'004DMDEMVER  01/20/18'                                      
         END                                                                    
