*          DATA SET EDREQSORT  AT LEVEL 008 AS OF 04/19/19                      
*CATALP REQSORT                                                                 
REQSORT  TITLE '- RESOLVE REQUEST DETAILS AND SORT'                             
REQSORT  CSECT                                                                  
         PRINT NOGEN                                                            
         ENTRY SSB                                                              
         ENTRY UTL                                                              
         NBASE 0,*REQSORT,R9,R8,WORK=A(REQWRK)                                  
         L     RA,=V(CPRINT)                                                    
         USING DPRINT,RA                                                        
         L     R7,=A(REQIN)                                                     
         USING EODREQD,R7                                                       
         MVC   TITLE+06(12),=C'REQUEST SORT'                                    
*                                                                               
SRTINIT  L     R4,=A(SRTCRD1)          INITIALISE FOR SORTER                    
         L     R5,=A(SRTCRD2)                                                   
         GOTO1 =V(SORTER),PARA,(R4),(R5)                                        
         EJECT                                                                  
***********************************************************************         
*READ PARAMETER CARDS. OPEN EODSORT AND CORRECT SET OF ACCOUNT FILES  *         
***********************************************************************         
INIT     L     RF,=A(CARDIN)       READ PARAMETER CARDS                         
         BASR  RE,RF                                                            
         BNE   EOJ                                                              
*                                                                               
         OPEN  (EODSORT,(INPUT))                                                
         GOTO1 =V(DATAMGR),DMCB,OPEN,FILES,OPENLIST                             
*&&US*&& MVC   DDS,=H'90'                                                       
*&&UK*&& MVC   DDS,=H'01'                                                       
         GOTOR BUILDOUT                                                         
         GOTOR BUILDEST                                                         
         EJECT                                                                  
***********************************************************************         
*INPUT ROUTINES                                                       *         
***********************************************************************         
INPUT    CLI   STACKSW,C'Y'        ANY DATA IN STACK                            
         BE    INPUT8              YES-RELEASE IT TO SORT                       
*                                                                               
INPUT0   L     R1,=A(EODSORT)      READ INPUT EODSORT RECORD                    
         L     R2,=A(REQINRL)      VARIABLE LENGTH INPUT BUFFER                 
         GET   (1),(R2)                                                         
*                                  DATA READ INTO BUFFER AT R7                  
         MVI   ANYRECS,C'Y'                                                     
         MVC   RDEST,QDEST         SAVE REQUEST DESTINATION ID                  
         MVC   ROUTTYPE,QOUTTYPE   SAVE REQUEST OUTPUT TYPE                     
*                                                                               
         LH    RF,0(R2)            GET BUFFER LENGTH                            
         SH    RF,=Y(L'QREQINFO)   RFHDR DATA L'QREQINFO BYTES FROM END         
         AR    RF,R2               POINT TO RFHDR DATA IN BUFFER                
         USING QREQINFO,RF                                                      
         MVC   RSYSID,QQHSYSID     SAVE REQUEST FACPAK ID                       
         MVC   RFLAG1,QQHFLAG1     SAVE REQUEST FLAG1                           
         MVC   RCTRL,QQHCTRL       SAVE REQUEST CONTROL FLAG                    
*                                                                               
         XC    SECSORT(L'SECSORT),SECSORT                                       
         MVC   SECPID(2),QQHSAGYP  SET REQUESTOR PID                            
         MVC   SECPID+2(2),QQHPSWD                                              
*                                                                               
         XC    RPQOSID(5),RPQOSID                                               
         TM    QQHFLAG1,QQHFOSI    TEST OVERNIGHT SOON                          
         BNO   INPUT0A                                                          
         CLI   QQHOSID,C' '        TEST OVERNIGHT SOON                          
         BNH   INPUT0A                                                          
         MVC   RPQOSID(5),QQHOSID  SAVE OVERNIGHT SOON REQUEST ID               
         MVI   SECTYPE,SECTONS     SET SORT TYPE O/N SOON                       
         MVC   SECOSID(5),QQHOSID                                               
         B     INPUT1              MAKE IT DIRECT                               
*                                                                               
INPUT0A  CLI   QQHSECF,QQHFPIN     TEST PIN PROTECTED                           
         BNE   INPUT0B                                                          
         MVI   SECTYPE,SECTPIN     SET USER INPUT PIN                           
         MVC   SECFLD,QQHSECD                                                   
         B     INPUT1              MAKE IT DIRECT                               
*                                                                               
INPUT0B  CLI   QQHSECF,QQHFPID     TEST PID PROTECTED                           
         BNE   INPUT1                                                           
         MVI   SECTYPE,SECTPID                                                  
         MVC   SECFLD,QQHSECD      SET USER INPUT PID                           
         CLC   SECPID,SECFLD       TEST PID THE SAME AS THE REQUESTOR           
         BE    INPUT1                                                           
         MVC   SECPID,SECFLD       SET REQUESTOR PID TO SECURED PID             
*                                                                               
INPUT1   MVC   C(L'QREQINFO),QREQINFO   TEMPORARY SAVE DATA                     
         MVC   QREQINFO(80),SPACES      CLEAR 80 BYTE AREA                      
         MVC   QREQINFO(6),=C'RFHDR='   ADD COMMENT TEXT                        
         MVC   QREQINFO+6(L'QREQINFO),C AND RESTORE DATA                        
         LH    RF,0(R2)                 INCREASE BUFFER LENGTH                  
         LA    RF,80-L'QREQINFO(RF)                                             
         STH   RF,0(R2)                                                         
         DROP  RF                                                               
*                                                                               
         CLI   ITRACE,C'Y'         TEST IF TRACE OF INPUT RECS WANTED           
         BNE   INPUT2                                                           
         L     RF,=A(ITRC1)                                                     
         BASR  RE,RF                                                            
*                                                                               
INPUT2   GOTOR FIX                 FIX REQUEST HEADER FROM PROFILES             
         MVI   STACKSW,C'Y'        SET FLAG TO RESOLVE OUTPUT                   
         GOTOR SETSTACK            SAVE JOB INFO IN STACK RECORDS               
         MVI   STACKSW,C'N'        RESET FLAG                                   
         MVI   STAKDIR,0                                                        
*                                                                               
INPUT2A  CLC   QOUTTYPE,=C'DIRECT' IGNORE IF ALREADY DIRECT                     
         BE    INPUT2G                                                          
         BRAS  RE,ANYDIR           IGNORE IF WILL BE SET TO DIRECT              
         BE    INPUT2G                                                          
*                                                                               
INPUT2B  CLC   STAKORUT(3),=C'TOR' NEW SPECIAL IDI OVER-RIDE                    
         BNE   INPUT2C                                                          
         MVC   STAKOOLD,QOUTTYPE   SAVE OLD OUTPUT TYPE                         
         MVC   QOUTTYPE,STAKORUT   SET OUTPUT TYPE TO IDI OUTPUT                
         MVC   ROUTTYPE,QOUTTYPE   SAVE REQUEST OUTPUT TYPE                     
         B     INPUT2E             PRINT @ TORONTO DDS                          
*                                                                               
INPUT2C  CLI   NPDOPT,C'Y'         TEST IF NO PRINT DATE IS ACTIVE              
         BNE   INPUT2F                                                          
         CLC   TODPAK,STAKNPD      TEST TODAY WITH NO PRINT DATE                
         BL    INPUT2F                                                          
         CLI   STAKPDD,C'Y'        TEST REPORT STILL PRINT@DDS                  
         BE    INPUT2D                                                          
         MVC   STAKOOLD,QOUTTYPE   SAVE OLD OUTPUT TYPE                         
         MVC   QOUTTYPE,=C'DIRECT' SET OUTPUT TYPE TO DIRECT                    
         MVC   ROUTTYPE,QOUTTYPE   SAVE REQUEST OUTPUT TYPE                     
         MVI   QCLASS,C'Q'         SET CLASS TO DIRECT                          
         B     INPUT2G                                                          
*                                                                               
INPUT2D  MVI   STAK@DDS,C'Y'       SET TO PRINT AT DDS                          
         CLC   DDSOUTYP,SPACES     TEST SPECIAL @DDS OUTPUT TYPE                
         BNH   INPUT2F                                                          
         MVC   STAKOOLD,QOUTTYPE   SAVE OLD OUTPUT TYPE                         
         MVC   QOUTTYPE,DDSOUTYP   SET NEW OUTPUT TYPE                          
         MVC   ROUTTYPE,QOUTTYPE                                                
*                                                                               
INPUT2E  MVI   STACKSW,C'Y'                                                     
         GOTOR RESOUT              RESOLVE NEW CLASS & PRIORITY                 
         MVI   STACKSW,C'N'                                                     
*                                                                               
INPUT2F  CLI   STAKPTYP,C'B'       PRINT AT DDS WITH DIRECT PQ COPY             
         BNE   INPUT3                                                           
         OI    STAKDIR,X'80'       SET BOTH FLAG                                
*                                                                               
INPUT2G  OI    STAKDIR,X'01'       SET DIRECT FLAG                              
*                                                                               
INPUT3   OC    RPQOSID,RPQOSID     SEPARATE OVERNIGHT SOON REQUESTS             
         BZ    INPUT3B                                                          
         ICM   RF,3,OVSNCNT                                                     
         LA    RF,1(RF)                                                         
         STCM  RF,3,OVSNCNT        BUMP OVSN COUNT                              
         STCM  RF,1,QSPLTR                                                      
         CHI   RF,255                                                           
         BNH   *+16                                                             
         CLI   QSUB2,0                                                          
         BNE   *+8                                                              
         STCM  RF,2,QSUB2                                                       
         B     INPUT5                                                           
*                                                                               
INPUT3B  CLC   QOUTTYPE,=C'DOWN  ' SEPARATE DOWN LOAD REQUESTS                  
         BE    *+14                                                             
         CLC   QOUTTYPE,=C'DIRDL '                                              
         BNE   INPUT3C                                                          
         MVI   STAKARC,C' '        CLEAR ARCHIVE STACK VARIABLES                
         XC    STAKADT,STAKADT                                                  
         MVI   STAKARCS,C' '                                                    
         SR    RF,RF                                                            
         ICM   RF,3,DOWNCNT                                                     
         LA    RF,1(RF)                                                         
         STCM  RF,3,DOWNCNT        BUMP DOWN COUNT                              
         STCM  RF,1,QSPLTR                                                      
         CHI   RF,255                                                           
         BNH   *+16                                                             
         CLI   QSUB2,0                                                          
         BNE   *+8                                                              
         STCM  RF,2,QSUB2                                                       
         B     INPUT5                                                           
*                                                                               
INPUT3C  CLC   QOUTTYPE,=C'SQL   ' SEPARATE SQL REQUESTS                        
         BE    *+14                                                             
         CLC   QOUTTYPE,=C'DIRSQL'                                              
         BNE   INPUT3D                                                          
         MVI   STAKARC,C' '        CLEAR ARCHIVE STACK VARIABLES                
         XC    STAKADT,STAKADT                                                  
         MVI   STAKARCS,C' '                                                    
         SR    RF,RF                                                            
         ICM   RF,3,SQLCNT                                                      
         LA    RF,1(RF)                                                         
         STCM  RF,3,SQLCNT         BUMP SQL REQUESTS                            
         STCM  RF,1,QSPLTR                                                      
         CHI   RF,255                                                           
         BNH   *+16                                                             
         CLI   QSUB2,0                                                          
         BNE   *+8                                                              
         STCM  RF,2,QSUB2                                                       
         B     INPUT5                                                           
*                                                                               
INPUT3D  CLI   QOUTTYPE,C'@'       THESE ARE SQL REQUESTS TOO                   
         BNE   INPUT4                                                           
         MVI   STAKARC,C' '        CLEAR ARCHIVE STACK VARIABLES                
         XC    STAKADT,STAKADT                                                  
         MVI   STAKARCS,C' '                                                    
         SR    RF,RF                                                            
         ICM   RF,3,SQL@CNT                                                     
         LA    RF,1(RF)                                                         
         STCM  RF,3,SQL@CNT        BUMP @SQL REQUESTS                           
         STCM  RF,1,QSPLTR                                                      
         CHI   RF,255                                                           
         BNH   *+16                                                             
         CLI   QSUB2,0                                                          
         BNE   *+8                                                              
         STCM  RF,2,QSUB2                                                       
         B     INPUT5                                                           
*                                                                               
INPUT4   CLI   STAKARCS,C'A'       SEPARATE ARCHIVE REQUESTS                    
         BE    INPUT4A                                                          
         CLI   STAKARCS,C'E'                                                    
         BE    INPUT4E                                                          
         B     INPUT5                                                           
INPUT4A  CLI   ARCASEP,C'Y'        TEST IF WANT SEPARATE ARCA JOBS              
         BE    INPUT4J                                                          
         B     INPUT5                                                           
INPUT4E  CLI   ARCESEP,C'Y'        TEST IF WANT SEPARATE ARCE JOBS              
         BE    INPUT4J                                                          
         B     INPUT5                                                           
INPUT4J  L     RF,=A(SKIPARC)      TEST PROGRAMS THAT HAVE MULTIPLES            
         BASR  RE,RF                                                            
         BE    INPUT5                                                           
         SR    RF,RF                                                            
         ICM   RF,3,ARCCNT                                                      
         LA    RF,1(RF)                                                         
         STCM  RF,3,ARCCNT         BUMP ARCHIVE REQUESTS                        
         STCM  RF,1,QSPLTR                                                      
         CHI   RF,255                                                           
         BNH   *+16                                                             
         CLI   QSUB2,0                                                          
         BNE   *+8                                                              
         STCM  RF,2,QSUB2                                                       
         B     INPUT5                                                           
*                                                                               
INPUT5   EQU   *                                                                
*&&US                                                                           
         CLI   RCTRL,X'01'         TEST COMSCORE REQUEST                        
         BNE   *+8                                                              
         NI    QPRIPO,X'BF'        OUTPUT CODE TO LOWER CASE FOR SORT           
*&&                                                                             
INPUT5A  OC    SECSORT,SECSORT     TEST SECURITY DATA                           
         BZ    INPUT6                                                           
         OC    QSORTFLD,QSORTFLD   TEST ALREADY HAVE SPECIAL SORT DATA          
         BNZ   INPUT6                                                           
         L     RF,=A(SKIPSEC)      TEST SKIP SECURITY SORT FOR PROGRAM          
         BASR  RE,RF                                                            
         BE    INPUT6                                                           
         MVC   QSORTFLD(L'SECSORT),SECSORT                                      
*                                                                               
INPUT6   CLC   QSHIPUNT,SPACES     SET SHIPPING UNIT IF NOT SET                 
         BH    INPUT7                                                           
         MVC   QSHIPUNT,THISUNIT   DESTINATION LEVEL UNIT                       
         CLC   OVERUNIT,SPACES                                                  
         BE    *+10                                                             
         MVC   QSHIPUNT,OVERUNIT   OVERRIDDEN BY PROFILE                        
*                                                                               
INPUT7   CLC   LASTINJB(22),0(R7)  TEST KEY FOR NEW JOB                         
         BNE   INPUT7J                                                          
*&&US                                                                           
         MVC   BYTE,QPRIPO         LOWER CASE FOR COMSCORE REQUEST              
         NC    BYTE,LASTINJB+(QPRIPO-QKEY)                                      
         TM    BYTE,X'40'                                                       
         BZ    INPUT7J             IF NOT ONE INDICATES CHANGE OF JOB           
*&&                                                                             
         CLC   LASTINJB+QSPLTR-QKEY(1),QSPLTR-QKEY(R7)                          
         BE    *+8                                                              
*                                                                               
INPUT7J  MVI   STACKSW,C'Y'                                                     
         MVC   LASTINJB,0(R7)      SAVE LAST JOB KEY                            
         CLI   ITRACE,C'Y'                                                      
         BNE   INPUT9                                                           
         L     RF,=A(ITRC2)                                                     
         BASR  RE,RF                                                            
         B     INPUT9                                                           
*                                                                               
INPUT8   L     R1,ASTACK           RELEASE RECORDS TO SORT                      
         MVC   QINTKEY(L'STAKRECD),0(R1)                                        
         LA    R1,L'STAKRECD(R1)                                                
         ST    R1,ASTACK                                                        
         CLC   0(3,R1),=C'END'                                                  
         BNE   INPUT9                                                           
         MVI   STACKSW,C'N'                                                     
         LA    RF,4+(QINTKEY-QRECORD)+L'STAKRECD                                
         STH   RF,0(R2)                                                         
         B     INPUT9                                                           
*                                                                               
INPUT9   CLC   0(2,R2),=Y(4+L'QKEY+160)   MUST BE AT LEAST THIS LONG            
         BNL   INPUTX                                                           
         MVC   P(28),=C'ERROR: SHORT RECORD BYPASSED'                           
         GOTOR BOTH                                                             
         B     INPUT                                                            
*                                                                               
INPUTX   GOTO1 =V(SORTER),PARA,=C'PUT',(R2) PUT V/L RECORD TO SORT              
         AP    INPRECS,=P'1'                                                    
         B     INPUT                                                            
*                                                                               
EOF      DS    0H                                                               
         CLOSE (EODSORT)                                                        
         CLI   ANYRECS,C'N'                                                     
         BE    EOJ                                                              
         B     OUTPUT                                                           
         EJECT                                                                  
***********************************************************************         
*ROUTINE TO BUILD STACK FOR PASSING TO SORT                           *         
***********************************************************************         
SETSTACK NTR1                                                                   
         MVI   STAKREC1,C' '                                                    
         MVC   STAKREC1+1(L'STAKREC1-1),STAKREC1                                
         XC    STAKADT,STAKADT                                                  
         MVI   STAKPRO1,C' '                                                    
         MVC   STAKPRO1+1(L'STAKPRO1-1),STAKPRO1                                
         MVI   STAKPAC1,C' '                                                    
         MVC   STAKPAC1+1(L'STAKPAC1-1),STAKPAC1                                
         MVI   STAKSHI1,C' '                                                    
         MVC   STAKSHI1+1(L'STAKSHI1-1),STAKSHI1                                
         XC    STAKATTN,STAKATTN                                                
         LA    R1,STAKRECD                                                      
         ST    R1,ASTACK                                                        
         XC    STAKTIME,STAKTIME                                                
         MVC   OVERUNIT,SPACES                                                  
         MVI   STAKPDD,C'N'        SET PRINT@DDS DEFAULTS                       
         MVC   STAKNPD,NPDPAK                                                   
         MVI   STAK@DDS,C'N'                                                    
         MVC   DDSOUTYP,SPACES     SPECIAL PRINT@DDS OUTPUT TYPE                
*                                                                               
         L     R2,=A(SYSTAB)       LOOK UP SYSTEM                               
SS2      CLI   0(R2),C' '                                                       
         BE    SS4                                                              
         CLC   0(1,R2),QSYSTEM                                                  
         BE    SS4                                                              
         LA    R2,L'SYSTAB(R2)                                                  
         B     SS2                                                              
SS4      DS    0H                                                               
*&&US                                                                           
         CLI   QSYSTEM,C'S'        TEST IF SPOT SYSTEM                          
         BNE   SS5                                                              
         MVC   DUB(5),=C'SE=0X'    SET UP SE=0X TO GET SYSTEM BY SE NUM         
         MVC   DUB+4(1),QSUBSYS                                                 
         GOTO1 =V(DMDDNAME),PARA,=C'DDNAME',DUB                                 
         L     RF,8(R1)                                                         
         CLI   8(R1),0                                                          
         BNE   SS5                                                              
         CLI   DDNASYN3-DDNAD(RF),C'N' TEST SPOT IS A NET SYSTEM                
         BNE   SS5                                                              
         L     R2,=A(SYSNET)       SWITCH TO NET                                
*&&                                                                             
SS5      MVC   STAKSYS,3(R2)       SET SYSTEM CODE                              
         MVC   STAKDUPS,11(R2)     SET SYSTEM DUPLICATE LETTERS                 
         MVC   STAKMVS(1),0(R2)    PART FILL MVS AND JES JOB NAMES              
         MVC   STAKMVS+1(2),QPROGRAM                                            
         MVC   STAKJES+4(1),0(R2)                                               
         MVC   STAKJES+5(2),QPROGRAM                                            
         GOTOR RESDEST             CHECK FOR OUTPUT ROUTE                       
         GOTOR RESOUT                                                           
         CLC   STAKORUT(3),=C'RJE'                                              
         BNE   SS6                                                              
         MVI   STAKDISP,C'H'                                                    
*                                                                               
SS6      L     R4,=A(PROGBUFF)     MASTER PROGRAM PROFILE                       
         MVI   PROFTYPE,PROFTMST                                                
         GOTOR SS10                                                             
         L     R4,=A(OVERBUFF)     OFFICE PROGRAM PROFILE                       
         MVI   PROFTYPE,PROFTOFC                                                
         GOTOR SS10                                                             
         TM    PROFFLAG,PROFXTRA   TEST IF WE READ EXTRA PROFILE                
         BZ    XIT                                                              
         L     R4,=A(XTRABUFF)                                                  
         MVI   PROFTYPE,PROFTXTR   EXTRA PROGRAM PROFILE                        
         GOTOR SS10                                                             
         B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
*CHECK PROFILE RECORD FOR POSSIBLE STACK VALUES                       *         
***********************************************************************         
SS10     NTR1                                                                   
         MVI   ELCODE,0                                                         
         GOTOR GETEL                                                            
         B     SS14                                                             
*                                                                               
SS12     GOTOR NEXTEL                                                           
*                                                                               
SS14     BNE   XIT                                                              
         CLI   0(R4),CTDSCELQ      PROGRAM NAME                                 
         BNE   SS16                                                             
         CLI   PROFTYPE,PROFTXTR   IGNORE IF EXTRA PROFILE                      
         BE    SS16                                                             
         USING CTDSCD,R4                                                        
         LLC   R3,CTDSCLEN                                                      
         AHI   R3,-3                                                            
         EX    R3,*+8                                                           
         B     SS12                                                             
         MVC   STAKNAME(0),CTDSC                                                
*                                                                               
SS16     CLI   0(R4),CTTIMELQ      TIME/REQUEST                                 
         BNE   SS18                                                             
         CLI   PROFTYPE,PROFTXTR   IGNORE IF EXTRA PROFILE                      
         BE    SS18                                                             
         USING CTTIMED,R4                                                       
         MVC   STAKTIME,CTTIME                                                  
         B     SS12                                                             
*                                                                               
SS18     LA    R2,STAKPROC         LOOK FOR COMMENTS                            
         CLI   PROFTYPE,PROFTXTR   IGNORE IF EXTRA PROFILE                      
         BE    SS22                                                             
         CLI   0(R4),CTPRCELQ                                                   
         BE    SS20                                                             
         LA    R2,STAKPACK                                                      
         CLI   0(R4),CTPAKELQ                                                   
         BE    SS20                                                             
         LA    R2,STAKSHIP                                                      
         CLI   0(R4),CTSHPELQ                                                   
         BNE   SS22                                                             
*                                                                               
SS20     GOTOR APPLY               CHECK IF THEY QUALIFY                        
         BNE   SS22                                                             
         USING CTPRCD,R4                                                        
         LLC   R3,CTPRCLEN                                                      
         AHI   R3,-7                                                            
         CHI   R3,151                                                           
         BL    *+8                                                              
         LHI   R3,151                                                           
         EX    R3,*+8                                                           
         B     *+10                                                             
         MVC   4(0,R2),CTPRCINS                                                 
         CLI   0(R4),CTSHPELQ      FOR SHIPPING INSTRUCTIONS                    
         BNE   SS12                                                             
         MVC   OVERUNIT,CTPRCINS   USE THE FIRST TWO CHARACTERS                 
         B     SS12                AS THE SHIPPING UNIT CODE.                   
*                                                                               
SS22     CLI   0(R4),CTRCLELQ      READER CLASS                                 
         BNE   SS23                                                             
         CLI   PROFTYPE,PROFTXTR   IGNORE IF EXTRA PROFILE                      
         BE    SS23                                                             
         GOTOR APPLY                                                            
         BNE   SS12                                                             
         USING CTRCLD,R4                                                        
         MVC   STAKCLAS,CTRCLASS                                                
         B     SS12                                                             
*                                                                               
SS23     CLI   0(R4),CTPQCELQ      PQ RETAIN CLASS                              
         BNE   SS23A                                                            
         CLI   PROFTYPE,PROFTXTR   IGNORE IF EXTRA PROFILE                      
         BE    SS23A                                                            
         GOTOR APPLY                                                            
         BNE   SS12                                                             
         USING CTPQCD,R4                                                        
         MVC   STAKRETC,CTPQCLAS                                                
         B     SS12                                                             
SS23A    CLI   0(R4),CTSDSELQ      PQ REPORT DESCRIPTION                        
         BNE   SS23B                                                            
         CLI   PROFTYPE,PROFTXTR   IGNORE IF EXTRA PROFILE                      
         BE    SS23B                                                            
         GOTOR APPLY                                                            
         BNE   SS12                                                             
         USING CTSDSD,R4                                                        
         MVC   STAKPQDS,CTSDSTXT                                                
         B     SS12                                                             
SS23B    CLI   0(R4),CTFRMELQ      PQ REPORT FORMS CODE                         
         BNE   SS24                                                             
         CLI   PROFTYPE,PROFTXTR   IGNORE IF EXTRA PROFILE                      
         BE    SS24                                                             
         GOTOR APPLY                                                            
         BNE   SS12                                                             
         USING CTFRMD,R4                                                        
         MVC   STAKPQFM(2),CTFRMCOD                                             
         B     SS12                                                             
*                                                                               
SS24     CLI   0(R4),CTJCLELQ      SPECIAL JCL                                  
         BNE   SS25                                                             
         CLI   PROFTYPE,PROFTOFC   IGNORE IF IN QDEST OVERRIDE                  
         BNE   *+12                                                             
         TM    PROFFLAG,PROFDEST+PROFOVER                                       
         BO    SS25                                                             
         GOTOR APPLY                                                            
         BNE   SS12                                                             
         USING CTJCLD,R4                                                        
         MVC   STAKJCL,CTJCLEX                                                  
         B     SS12                                                             
*                                                                               
SS25     CLI   0(R4),CTSQLELQ      SQL TRANSFORM CODE                           
         BNE   SS26                                                             
         CLI   PROFTYPE,PROFTXTR   IGNORE IF EXTRA PROFILE                      
         BE    SS26                                                             
         GOTOR APPLY                                                            
         BNE   SS12                                                             
         USING CTSQLD,R4                                                        
         MVC   STAKSQL,CTSQLCOD                                                 
         B     SS12                                                             
*                                                                               
SS26     CLI   0(R4),CTARTELQ      ARCHIVE REPORT TYPE                          
         BNE   SS27                                                             
         CLI   PROFTYPE,PROFTXTR   IGNORE IF EXTRA PROFILE                      
         BE    SS27                                                             
         GOTOR APPLY                                                            
         BNE   SS12                                                             
         USING CTARTD,R4                                                        
         MVC   STAKART,CTARTCOD                                                 
         B     SS12                                                             
*                                                                               
SS27     CLI   0(R4),CTARCELQ      ARCHIVE REPORT CLASS                         
         BNE   SS27A                                                            
         CLI   PROFTYPE,PROFTXTR   IGNORE IF EXTRA PROFILE                      
         BE    SS27A                                                            
         GOTOR APPLY                                                            
         BNE   SS12                                                             
         USING CTARCD,R4                                                        
         MVC   STAKARC,CTARCCOD                                                 
         B     SS12                                                             
*                                                                               
SS27A    CLI   0(R4),CTARSELQ      ARCHIVE REPORT STATUS                        
         BNE   SS27B                                                            
         CLI   PROFTYPE,PROFTXTR   IGNORE IF EXTRA PROFILE                      
         BE    SS27B                                                            
         GOTOR APPLY                                                            
         BNE   SS12                                                             
         USING CTARSD,R4                                                        
         MVC   STAKARCS,CTARSTAO   ARCHIVE STATUS OVERNIGHT                     
         B     SS12                                                             
*                                                                               
SS27B    CLI   0(R4),CTADTELQ      ARCHIVE REPORT DOCUMENT TYPE                 
         BNE   SS27C                                                            
         CLI   PROFTYPE,PROFTXTR   IGNORE IF EXTRA PROFILE                      
         BE    SS27C                                                            
         GOTOR APPLY                                                            
         BNE   SS12                                                             
         USING CTADTD,R4                                                        
         MVC   STAKADT,CTADTCOD    ARCHIVE DOCUMENT TYPE                        
         B     SS12                                                             
*                                                                               
SS27C    CLI   0(R4),CTPADELQ      PRINT@DDS ELEMENT                            
         BNE   SS27D                                                            
         CLI   PROFTYPE,PROFTXTR   IGNORE IF EXTRA PROFILE                      
         BE    SS27D                                                            
         GOTOR APPLY                                                            
         BNE   SS12                                                             
         USING CTPADD,R4                                                        
         MVC   STAKPDD,CTPADFLG    PRINT@DDS FLAG                               
         MVC   STAKPQB,CTPADPQB    PRINT QUEUE BURST FLAG                       
         CLI   CTPADLEN,14                                                      
         BL    SS12                                                             
         MVC   STAKOOLD,DDSOUTYP   SAVE OLD OUTPUT TYPE                         
         MVC   DDSOUTYP,CTPADOUT   SPECIAL "PRINT@DDS" OUTPUT TYPE              
         B     SS12                                                             
*                                                                               
SS27D    CLI   0(R4),CTPDDELQ      DATE@DDS ELEMENT                             
         BNE   SS28                                                             
         CLI   PROFTYPE,PROFTXTR   IGNORE IF EXTRA PROFILE                      
         BE    SS28                                                             
         GOTOR APPLY                                                            
         BNE   SS12                                                             
         USING CTPDDD,R4                                                        
*&&UK*&& B     *+10                                                             
*&&US*&& B     *+4                                                              
         MVC   STAKNPD,CTPDDATE    DATE WHEN PRINT@DDS DATE STARTS              
         B     SS12                                                             
*                                                                               
SS28     CLI   0(R4),CTPHSELQ      SPECIAL PHASES                               
         BNE   SS29                                                             
         CLI   PROFTYPE,PROFTOFC   IGNORE IF IN QDEST OVERRIDE                  
         BNE   *+12                                                             
         TM    PROFFLAG,PROFDEST+PROFOVER                                       
         BO    SS29                                                             
         GOTOR APPLY                                                            
         BNE   SS12                                                             
         USING CTPHSD,R4                                                        
         MVC   STAKPHS,CTPHS01                                                  
         B     SS12                                                             
*                                                                               
SS29     CLI   0(R4),CTACOELQ                                                   
         BNE   SS12                                                             
         CLI   PROFTYPE,PROFTXTR   IGNORE IF EXTRA PROFILE                      
         BE    SS12                                                             
         GOTOR APPLY                                                            
         BNE   SS12                                                             
         USING CTACOD,R4                                                        
         MVC   STAKATTN,CTACODE                                                 
         LA    R3,STAKATTN                                                      
         GOTOR SS30                                                             
         LA    R3,STAKATTN+1                                                    
         GOTOR SS30                                                             
         LA    R3,STAKATTN+2                                                    
         GOTOR SS30                                                             
         B     SS12                                                             
*                                                                               
SS30     NTR1                                                                   
         CLI   0(R3),C'&&'         SUBSTITUTION                                 
         BNE   XIT                                                              
*&&US*&& JIF   QSYSTEM,=,C'R',SS32                                              
         JIF   QSYSTEM,=,C'A',SS34                                              
         B     XIT                                                              
*&&US                                                                           
SS32     MVC   0(1,R3),QSUBUSER                                                 
         B     XIT                                                              
*&&                                                                             
SS34     MVI   0(R3),C'P'                                                       
         JIF   QSUBUSER,=,C'P',OR,C'Q',XIT                                      
         MVI   0(R3),C'S'                                                       
         JIF   QSUBUSER,=,C'S',OR,C'T',XIT                                      
         MVI   0(R3),C'V'                                                       
         JIF   QSUBUSER,=,C'V',OR,C'W',XIT                                      
         MVI   0(R3),C'X'                                                       
         JIF   QSUBUSER,=,C'X',OR,C'Y',XIT                                      
         CLC   QPROGRAM,=C'08'     JOURNAL G OR P                               
         BE    SS34A                                                            
         CLC   QPROGRAM,=C'DJ'     NEW JOURNAL                                  
         BE    SS34A                                                            
         CLC   QPROGRAM,=C'54'     CLEARANCES S OR P                            
         BNE   XIT                                                              
SS34A    MVC   0(1,R3),QSUB2                                                    
         B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
*ROUTINES TO COMPLETE REQUEST HEADERS                                 *         
***********************************************************************         
FIX      NTR1                                                                   
         GOTOR PROFIN              MAKE SURE PROFILES ARE IN CORE               
         OC    QDEST,QDEST                                                      
         BNZ   FIX0                                                             
         L     R2,=A(PROGBUFF)     ESTABLISH DESTINATION CODE                   
         MVI   PROFTYPE,PROFTMST                                                
         LA    R3,QDEST                                                         
         GOTOR DEST                                                             
         L     R2,=A(OVERBUFF)                                                  
         MVI   PROFTYPE,PROFTOFC                                                
         LA    R3,QDEST                                                         
         GOTOR DEST                                                             
         OC    QDEST,QDEST         IF ITS STILL NOT SPECIFIED                   
         BNZ   *+10                                                             
         MVC   QDEST,QORIGIN       USE ORIGIN CODE AS DEST                      
*                                                                               
FIX0     CLC   QUSER,SPACES        TEST IF QUSER SET BY REQXPLOD                
         BH    FIX1                                                             
         MVC   QUSER,QDATA+2                                                    
*                                                                               
FIX1     L     R4,=A(PROGBUFF)                                                  
         L     R5,=A(OVERBUFF)                                                  
         MVI   STAKCLAS,C' '                                                    
         MVC   PRIPERM(3),=C'9Y '  SET PROGRAM WRAP ORDER/TYPE/OUTPUT           
         MVC   PRIOVER(3),SPACES   SET DEFAULT OVERRIDE VALUES                  
         LR    R2,R4                                                            
         GOTOR PRIORITY            EXTRACT FROM PROGRAM PROFILE                 
         MVC   PRGPERM,PRIPERM     SAVE PROGRAM PERMANENT VALUES                
         MVC   PRGOVER,PRIOVER     SAVE PROGRAM OVERRIDE VALUES                 
         LR    R2,R5                                                            
         GOTOR PRIORITY            EXTRACT FROM USERID OVERRIDE                 
*                                                                               
         CLI   PRIOVER+0,C' '      APPLY PERMANENT AT USERID LEVEL              
         BNE   *+10                                                             
         MVC   PRIOVER+0(1),PRIPERM+0                                           
         CLI   PRIOVER+1,C' '                                                   
         BNE   *+10                                                             
         MVC   PRIOVER+1(1),PRIPERM+1                                           
         CLI   PRIOVER+2,C' '                                                   
         BNE   *+10                                                             
         MVC   PRIOVER+2(1),PRIPERM+2                                           
*                                                                               
         CLI   PRIOVER+0,C' '      APPLY OVERRIDE AT PROGRAM LEVEL              
         BNE   *+10                                                             
         MVC   PRIOVER+0(1),PRGOVER+0                                           
         CLI   PRIOVER+1,C' '                                                   
         BNE   *+10                                                             
         MVC   PRIOVER+1(1),PRGOVER+1                                           
         CLI   PRIOVER+2,C' '                                                   
         BNE   *+10                                                             
         MVC   PRIOVER+2(1),PRGOVER+2                                           
*                                                                               
         CLI   PRIOVER+0,C' '      APPLY PERMANENT AT PROGRAM LEVEL             
         BNE   *+10                                                             
         MVC   PRIOVER+0(1),PRGPERM+0                                           
         CLI   PRIOVER+1,C' '                                                   
         BNE   *+10                                                             
         MVC   PRIOVER+1(1),PRGPERM+1                                           
         CLI   PRIOVER+2,C' '                                                   
         BNE   *+10                                                             
         MVC   PRIOVER+2(1),PRGPERM+2                                           
*                                                                               
         CLI   PRIOVER,C' '        APPLY STANDARD IF STILL NO VALUE             
         BNE   *+10                                                             
         MVC   PRIOVER(1),=C'9Y '                                               
*                                                                               
         MVC   QPRISC,PRIOVER      SET SORT CODE                                
         MVC   QPRIPT,PRIOVER+1    SET PROGRAM TYPE                             
         MVC   QPRIPO,PRIOVER+2    SET PROGRAM OUTPUT TYPE                      
*                                                                               
FIX2     GOTOR RESUNIT             EXTRACT SHIPPING UNIT                        
         MVC   STAKORUT,SPACES                                                  
*                                                                               
FIX3     OC    QOUTTYPE,QOUTTYPE   ANYTHING IN OUTPUT TYPE FIELD                
         BZ    FIX3E               NO-GET IT FROM PROFILE                       
         CLI   QOUTTYPE,C'@'                                                    
         BE    FIX6                                                             
         CLC   QOUTTYPE,=C'SQL   ' TEST IF REQUESTED SQL OUTPUT                 
         BE    FIX3D               YES-GET FORMULA FROM PROFILE                 
         CLC   QOUTTYPE,=C'DIRSQL'                                              
         BE    FIX3D                                                            
         CLI   RSYSID,0            OK IF REQUEST WAS ADDED OFFLINE              
         BE    FIX4                                                             
         TM    RFLAG1,QQHFOUTT     OK IF NOT INPUT AT REQUEST ADD TIME          
         BZ    FIX4                                                             
FIX3A    CLI   QPRIPT,C'A'         TEST IF FILE MARKER (A-I)                    
         BL    FIX3B                                                            
         CLI   QPRIPT,C'I'                                                      
         BNH   FIX3C                                                            
FIX3B    CLI   QPRIPO,C'A'         TEST IF CONFIDENTIAL OUTPUT (A-I)            
         BL    FIX4                                                             
         CLI   QPRIPO,C'I'                                                      
         BH    FIX4                                                             
*                                                                               
FIX3C    B     FIX4                OVERRIDE OF FILEMARKER OUTPUT TYPE           
*                                                                               
FIX3D    LR    R2,R4               GET SQL FORMULA FROM PROG PROFILE            
         GOTOR SQLFRM                                                           
         LR    R2,R5                                                            
         GOTOR SQLFRM                                                           
         B     FIX6                                                             
*                                                                               
FIX3E    LR    R2,R4               GET OUTPUT TYPE FROM PROG PROFILE            
         GOTOR OUTYPE                                                           
         LR    R2,R5                                                            
         GOTOR OUTYPE                                                           
*                                                                               
FIX4     GOTOR RESOUT              RESOLVE OUTPUT DETAILS                       
         CLC   QOUTTYPE(3),=C'&&CON'                                            
         BE    FIX5                                                             
         CLC   QOUTTYPE(4),=C'&&4AS'                                            
         BE    FIX5                                                             
         CLC   QOUTTYPE(4),=C'&&INS'                                            
         BNE   FIX6                                                             
*                                                                               
FIX5     MVC   QOUTTYPE-1(1),QSUBUSER                                           
         CLI   QSUBUSER,C'O'                                                    
         BE    FIX6                                                             
         CLI   QSUBUSER,C'N'                                                    
         BE    FIX6                                                             
         MVI   QOUTTYPE-1,C'M'                                                  
*                                                                               
FIX6     XC    AFORM,AFORM                                                      
         LR    R2,R4                                                            
         GOTOR FORM                SORT FORMULA                                 
         LR    R2,R5                                                            
         GOTOR FORM                                                             
         GOTOR RESORT              APPLY SORT FORMULA                           
         B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
*ROUTINE TO DIG OUT DESTINATION CODE                                  *         
***********************************************************************         
DEST     NTR1                                                                   
         LR    R4,R2                                                            
         MVI   SACRED,C'N'                                                      
         MVI   ELCODE,CTDCOELQ                                                  
         GOTOR GETEL                                                            
         B     DEST4                                                            
*                                                                               
DEST2    GOTOR NEXTEL                                                           
*                                                                               
DEST4    BNE   XIT                                                              
         USING CTDCOD,R4                                                        
         GOTOR APPLY                                                            
         BNE   DEST2                                                            
         MVC   0(2,R3),CTDCNUM                                                  
         CLI   SACRED,C'Y'                                                      
         BE    XIT                                                              
         B     DEST2                                                            
         EJECT                                                                  
***********************************************************************         
*ROUTINE TO DIG OUT PRIORITY                                          *         
***********************************************************************         
PRIORITY NTR1                                                                   
         LR    R4,R2                                                            
         MVI   SACRED,C'N'                                                      
         MVI   ELCODE,CTPRIELQ                                                  
         GOTOR GETEL                                                            
         B     PRI4                                                             
*                                                                               
PRI2     GOTOR NEXTEL                                                           
*                                                                               
PRI4     BNE   XIT                                                              
         USING CTPRID,R4                                                        
         GOTOR APPLY                                                            
         BNE   PRI2                                                             
         MVC   PRIOVER(1),CTPRISC                                               
         MVI   PRIOVER+1,C' '                                                   
         MVI   PRIOVER+2,C' '                                                   
         CLI   CTPRILEN,8          TEST IF OLD STYLE ELEMENT                    
         BE    *+10                                                             
         MVC   PRIOVER(3),CTPRISC  NO-SET PRG WRAP/TYPE/OUTPUT                  
         CLI   CTPRITYP,C'P'                                                    
         BNE   *+10                                                             
         MVC   PRIPERM,PRIOVER     SAVE PERMANENT PROFILE VALUES                
         CLI   SACRED,C'Y'                                                      
         BE    XIT                                                              
         B     PRI2                                                             
         EJECT                                                                  
***********************************************************************         
*ROUTINE TO FIND OUTPUT TYPE                                          *         
***********************************************************************         
OUTYPE   NTR1                                                                   
         LR    R4,R2                                                            
         MVI   SACRED,C'N'                                                      
         MVI   ELCODE,CTOCOELQ                                                  
         GOTOR GETEL                                                            
         B     OUTYPE4                                                          
*                                                                               
OUTYPE2  GOTOR NEXTEL                                                           
*                                                                               
OUTYPE4  BNE   XIT                                                              
         USING CTOCOD,R4                                                        
         GOTOR APPLY                                                            
         BNE   OUTYPE2                                                          
         MVC   QOUTTYPE,CTOCODE                                                 
         MVC   ROUTTYPE,QOUTTYPE   SAVE REQUEST OUTPUT TYPE                     
         CLI   SACRED,C'Y'                                                      
         BE    XIT                                                              
         B     OUTYPE2                                                          
         EJECT                                                                  
***********************************************************************         
*ROUTINE TO FIND SQL TRANSFORM FORMULA                                *         
***********************************************************************         
SQLFRM   NTR1                                                                   
         LR    R4,R2                                                            
         MVI   SACRED,C'N'                                                      
         MVI   ELCODE,CTSQLELQ                                                  
         GOTOR GETEL                                                            
         B     SQLFRM4                                                          
*                                                                               
SQLFRM2  GOTOR NEXTEL                                                           
*                                                                               
SQLFRM4  BNE   XIT                                                              
         USING CTSQLD,R4                                                        
         GOTOR APPLY                                                            
         BNE   SQLFRM2                                                          
         MVI   QOUTTYPE,C'@'                                                    
         MVC   QOUTTYPE+1(5),CTSQLCOD                                           
         MVC   ROUTTYPE,QOUTTYPE   SAVE REQUEST OUTPUT TYPE                     
         CLI   SACRED,C'Y'                                                      
         BE    XIT                                                              
         B     SQLFRM2                                                          
         EJECT                                                                  
***********************************************************************         
*ROUTINE TO DIG OUT SORT FORMULA                                      *         
***********************************************************************         
FORM     NTR1                                                                   
         LR    R4,R2                                                            
         MVI   SACRED,C'N'                                                      
         MVI   ELCODE,CTSRTELQ                                                  
         GOTOR GETEL                                                            
         B     FORM4                                                            
*                                                                               
FORM2    GOTOR NEXTEL                                                           
*                                                                               
FORM4    BNE   XIT                                                              
         USING CTSRTD,R4                                                        
         GOTOR APPLY                                                            
         BNE   FORM2                                                            
         ST    R4,AFORM                                                         
         CLI   SACRED,C'Y'                                                      
         BE    XIT                                                              
         B     FORM2                                                            
         EJECT                                                                  
***********************************************************************         
*ROUTINE TO SEE IF THIS PROFILE APPLIES                               *         
***********************************************************************         
APPLY    NTR1                                                                   
         USING CTDCOD,R4                                                        
APPLY2   CLI   CTDCOTYP,C'S'       SACRED                                       
         BNE   APPLY4                                                           
         MVI   SACRED,C'Y'                                                      
         B     APPLYES                                                          
*                                                                               
APPLY4   CLI   CTDCOTYP,C'P'       PERMANENT                                    
         BE    APPLYES                                                          
         CLI   CTDCOTYP,C'T'       TEMPORARY                                    
         BNE   APPLY6                                                           
         CLC   TODPAK,CTDCODTA     SEE IF ITS EXPIRED                           
         BH    APPLYNO                                                          
         B     APPLYES                                                          
*                                                                               
APPLY6   CLC   CTDCODTA,DAYOFWK    DAY OF WEEK PROFILE - CHECK MATCH            
         BE    APPLYES                                                          
*                                                                               
APPLYNO  LA    R1,1                                                             
         LTR   R1,R1                                                            
         B     XIT                                                              
*                                                                               
APPLYES  SR    R1,R1                                                            
         LTR   R1,R1                                                            
         B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
*ROUTINE TO EXECUTE THE SORT FORMULA                                  *         
***********************************************************************         
RESORT   NTR1                                                                   
         L     R4,AFORM                                                         
         LTR   R4,R4                                                            
         BZ    XIT                                                              
         USING CTSRTD,R4                                                        
         LA    R2,CTSRTFRM                                                      
         LLC   R3,CTSRTLEN         NUMBER OF ELEMENTS (LEN-6/2)                 
         AHI   R3,-6                                                            
         SRL   R3,1                                                             
         SR    R5,R5               BYTES USED SO FAR                            
         LA    R4,QSORTFLD         POINT TO START OF SORT FIELD                 
*                                                                               
RESORT2  CLI   0(R4),0             FIND FIRST AVAILABLE SLOT                    
         BE    RESORT4                                                          
         LA    R4,1(R4)                                                         
         LA    R5,1(R5)                                                         
         B     RESORT2                                                          
*                                                                               
RESORT4  IC    R6,0(R2)            FIRST BYTE IS DISPLACEMENT                   
         SLL   R6,25                                                            
         SRL   R6,25                                                            
         LA    R6,QDATA(R6)        BUMP TO DATA ADDRESS                         
         LLC   R1,1(R2)            SECOND BYTE IS LENGTH                        
         AR    R5,R1                                                            
         LA    RF,L'QSORTFLD       GET LENGTH OF SORT AREA                      
         CR    R5,RF                                                            
         BH    XIT                                                              
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),0(R6)       DIG OUT A SORT FIELD                         
         TM    0(R2),X'80'         IS IT A DESCENDING FIELD                     
         BNO   RESORT6                                                          
         EX    R1,*+8                                                           
         B     *+10                                                             
         XC    0(0,R4),=10X'FF'    COMPLEMENT                                   
*                                                                               
RESORT6  LA    R2,2(R2)                                                         
         LA    R4,1(R1,R4)                                                      
         BCT   R3,RESORT4                                                       
         B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
*OUTPUT ROUTINES                                                      *         
***********************************************************************         
OUTPUT   GOTO1 =V(SORTER),PARA,=C'GET'                                          
         L     RE,4(R1)                                                         
         LTR   RE,RE               TEST FOR END OF FILE                         
         BZ    FINAL                                                            
*                                                                               
         L     R2,=A(REQINRL)                                                   
         LR    R0,R2                                                            
         LH    R1,0(RE)                                                         
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
         LH    RF,0(R2)            POINT TO DATA IN RFHDR= CARD                 
         AHI   RF,-80                                                           
         LA    RF,6(RF,R2)                                                      
         MVC   RSVQINFO,0(RF)                                                   
*                                                                               
         AP    OUTRECS,=P'1'                                                    
*                                                                               
OUTPUT0  CLC   QOUTTYPE,=C'DOWN  ' ONE DOWNLOAD REQUEST PER JOB                 
         BE    OUTPUT0A                                                         
         CLC   QOUTTYPE,=C'DIRDL '                                              
         BE    OUTPUT0A                                                         
         CLC   QOUTTYPE,=C'SQL   ' ONE SQL REQUEST PER JOB                      
         BE    OUTPUT0A                                                         
         CLC   QOUTTYPE,=C'DIRSQL'                                              
         BE    OUTPUT0A                                                         
         CLI   QOUTTYPE,C'@'                                                    
         BE    OUTPUT0A                                                         
         B     OUTPUT0B                                                         
*                                                                               
OUTPUT0A CLC   QINTKEY(3),=3X'FF'  BYPASS NEXT CHECK IF STACK RECORDS           
         BE    OUTPUT2                                                          
         B     OUTPUT0C                                                         
*                                                                               
OUTPUT0B CLC   QJOBKEY,LASTJOB     TEST FOR CHANGE OF JOB                       
         BE    OUTPUT2                                                          
*                                                                               
OUTPUT0C MVC   WORKIO(2),LENJOBH   WRITE OUT A JOB HEADER                       
         MVC   WORKIOD(04),=C'JOB='                                             
         MVC   WORKIO+8(L'QJOBKEY),QJOBKEY                                      
*&&US*&& MVC   RPRIPO,QPRIPO       SAVE QPRIPO AND SET TO UPPER                 
*&&US*&& OI    WORKIO+8+L'QJOBKEY-1,X'40'                                       
         LH    RF,0(R2)            POINT TO DATA IN RFHDR= CARD                 
         AHI   RF,-80                                                           
         LA    RF,6(RF,R2)                                                      
         TM    QQHFLAG1-QQHISYS(RF),QQHFWHY                                     
         BZ    OUTPUT0D                                                         
         MVC   WORKIO+8+L'QJOBKEY(L'QQHWHY),QQHWHY-QQHISYS(RF)                  
         LH    RE,WORKIO                                                        
         LA    RE,L'QQHWHY(RE)     SET LEN OF EXTENDED JOB HEADER               
         STH   RE,WORKIO                                                        
OUTPUT0D MVC   ID(4),=C'REQS'                                                   
         MVC   ID+5(2),QSYSTEM                                                  
         GOTO1 =V(WORKER),DMCB,=C'ADD',A(EODREQS),ID,WORKIO                     
*&&US*&& MVC   WORKIO+8+L'QJOBKEY-1(1),RPRIPO  RESTORE QPRIPO                   
         GOTOR WORKCHEK                                                         
*                                                                               
         LH    RF,0(R2)            POINT TO DATA IN RFHDR= CARD                 
         AHI   RF,-80                                                           
         LA    RF,6(RF,R2)                                                      
         XC    RPQOSID(5),RPQOSID                                               
         TM    QQHFLAG1-QQHISYS(RF),QQHFOSI                                     
         BNO   *+10                                                             
         MVC   RPQOSID(5),QQHOSID-QQHISYS(RF)                                   
         MVC   RRFPG,SPACES                                                     
         TM    QQHFLAG1-QQHISYS(RF),QQHFRFPG                                    
         BNO   *+10                                                             
         MVC   RRFPG,QQHRFPG-QQHISYS(RF)                                        
*                                                                               
         MVC   RSYSID,QQHSYSID-QQHISYS(RF)                                      
         MVC   RDATE,QQHPJDT-QQHISYS(RF)                                        
         MVC   RFLAG1,QQHFLAG1-QQHISYS(RF)                                      
         CLI   QQHSECF-QQHISYS(RF),QQHFPIN TEST PIN                             
         BE    OUTPUT0E                    YES-CLEAR FIELD                      
         CLI   QQHSECF-QQHISYS(RF),QQHFPID TEST PID                             
         BE    OUTPUT0E                    YES-CLEAR FIELD                      
         CLI   RPQOSID,C' '                                                     
         BH    *+10                                                             
OUTPUT0E XC    RPQOSID(5),RPQOSID                                               
         CLC   LASTJOB,SPACES                                                   
         BE    OUTPUT2                                                          
*                                                                               
OUTPUT1  LHI   RF,LENJOBR          ADD A SUMMARY TO THE JOB FILE                
         STCM  RF,3,WORKIO                                                      
         MVC   WORKIOD(L'QJOBKEY),LASTJOB                                       
*&&US*&& MVC   RPRIPO,WORKIOD+L'QJOBKEY-1  SAVE QPRIPO AND SET TO UPPER         
*&&US*&& OI    WORKIOD+L'QJOBKEY-1,X'40'                                        
         MVC   WORKIOD+QJOBREQS-QRECORD(4),REQCOUNT                             
         GOTOR TACKON                                                           
         ZAP   REQCOUNT,=P'0'                                                   
         MVC   JD(4),=C'JOBS'                                                   
         MVC   JD+5(2),LASTSYS                                                  
         GOTO1 =V(WORKER),DMCB,=C'ADD',A(EODJOBS),JD,WORKIO                     
*&&US*&& MVC   WORKIOD+L'QJOBKEY-1(1),RPRIPO  RESTORE QPRIPO                    
         GOTOR WORKCHEK                                                         
         AP    JOBRECS,=P'1'                                                    
         MVI   SETCTRL,0           INIT REQUEST HEADER CTRL FLAGS               
         CLI   EOFSW,C'Y'                                                       
         BE    FINAL2                                                           
*                                                                               
OUTPUT2  MVC   LASTJOB,QJOBKEY     SAVE INFO FROM LAST JOB                      
         MVC   LASTSYS,QSYSTEM                                                  
         MVC   LASTDATE,RDATE                                                   
         MVC   LASTFLAG,RFLAG1                                                  
         MVC   LRSYSID,RSYSID                                                   
         MVC   LRRFPG,RRFPG                                                     
         MVC   LRPQOSID(5),RPQOSID                                              
         CLC   QINTKEY(3),=3X'FF'  THE X'FF' RECORDS CONTAIN SOME               
         BE    SAVETACK            DESCRIPTIVE DATA FOR THIS JOB                
*                                                                               
OUTPUT3  EQU   *                   TEST IF WANT RFHDR CARDS                     
         CLI   RFHDR,C'N'          WAS RFHDR=N CARD INPUT                       
         BE    OUTPUT3X                                                         
         MVC   WORKIO(2),LENREQR   OUTPUT RFHDR=.... CARD                       
         LH    RF,0(R2)            GET BUFFER LENGTH                            
         AHI   RF,-80                                                           
         AR    RF,R2                                                            
         MVC   WORKIOD(80),0(RF)                                                
         MVI   WORKIOD+80,X'00'    TYPE 00 FOR HEADER CARD                      
         MVC   ID(4),=C'REQS'                                                   
         MVC   ID+5(2),QSYSTEM                                                  
OUTPUT3A LA    R3,WORKIOD+6                                                     
         USING QQHISYS,R3                                                       
         OC    QQHSYSID(4),QQHSYSID TEST FACPAK SYSID/SIN                       
         BNZ   OUTPUT3B                                                         
         MVC   C,SPACES            NO-SET DUMMY OFFLINE RFHDR=CARD              
         XC    C(20),C                                                          
         MVC   WORKIOD+6(72),C                                                  
         B     OUTPUT3G                                                         
*                                                                               
OUTPUT3B MVC   C,SPACES            SET BINARY AND CHR VALUES IN RFHDR           
         MVC   C(20),QQHITRM                                                    
         MVC   C+16(1),QQHCTRL     SET CTRL BYTE FROM REQUEST HEADER            
         CLI   QQHCTRL,X'00'                                                    
         BNE   *+12                                                             
         OI    SETCTRL,CTRLNO      SET NO VALUE IN CTRL BYTE                    
         B     OUTPUT3C                                                         
*&&US                                                                           
         CLI   QQHCTRL,X'01'       TEST COMSCORE REQUEST                        
         BNE   OUTPUT3C                                                         
         OI    SETCTRL,CTRLYES     SET VALUE IN CTRL BYTE                       
*&&                                                                             
OUTPUT3C MVI   C+20,C' '                                                        
         MVC   C+21(6),QQHSECI     SECURITY DATA                                
*                                                                               
OUTPUT3D TM    QQHFLAG1,QQHFWHY                                                 
         BZ    *+10                                                             
         MVC   C+28(22),QQHWHY                                                  
         MVC   WORKIOD+6(72),C                                                  
         DROP  R3                                                               
OUTPUT3G GOTO1 =V(WORKER),DMCB,=C'ADD',A(EODREQS),ID,WORKIO                     
         GOTOR WORKCHEK                                                         
         AP    RFHRECS,=P'1'       BUMP NUMBER OF RFHDR= CARDS                  
OUTPUT3X EQU   *                                                                
                                                                                
***********************************************************************         
*EXAMINE REQUEST CARDS IN VARIABLE LENGTH BUFFER                      *         
*GET DATA CARD AT +4 AS TRANSFER LENGTH                               *         
***********************************************************************         
OUTPUT4  LH    RF,0(R2)                                                         
         SH    RF,=Y(L'QKEY+80)                                                 
         CLC   QDATA2,SPACES       CHECK IF SINGLE CARD + SPACES                
         BNE   *+8                                                              
         AHI   RF,-80              IF SO REMOVE SPACES LENGTH                   
         STH   RF,WORKIO                                                        
         LA    R0,WORKIOD          BUILD VARIABLE LENGTH WORKER RECORD          
         LA    RE,QDATA1                                                        
         LR    R1,RF                                                            
         MVCL  R0,RE                                                            
         LA    RF,WORKIO           TACK QKEY ON TO END OF REQUESTS              
         AH    RF,WORKIO                                                        
         MVC   0(L'RSVQINFO,RF),RSVQINFO                                        
         LH    RF,WORKIO                                                        
         LA    RF,L'RSVQINFO(RF)                                                
         STH   RF,WORKIO                                                        
         MVC   ID(4),=C'REQS'                                                   
         MVC   ID+5(2),QSYSTEM     WRITE RECORD TO WORKER FILE                  
         GOTO1 =V(WORKER),DMCB,=C'ADD',A(EODREQS),ID,WORKIO                     
         GOTOR WORKCHEK                                                         
         AP    REQRECS,=P'1'       BUMP NUMBER OF REQUEST RECORDS               
*&&US*&& CLI   QPHASE,C'1'                                                      
*&&US*&& BH    *+10                                                             
         AP    REQCOUNT,=P'1'      BUMP NUMBER OF REQUESTS THIS JOB             
OUTPUT4X B     OUTPUT                                                           
         EJECT                                                                  
***********************************************************************         
*ROUTINES TO SAVE DESCRIPTIVE VALUES                                  *         
***********************************************************************         
SAVETACK CLI   QINTKEY+3,0                                                      
         BNE   *+10                                                             
         MVC   STAKREC1,QJOBMVS                                                 
         CLI   QINTKEY+3,1                                                      
         BNE   *+10                                                             
         MVC   STAKPRO1,QJOBMVS                                                 
         CLI   QINTKEY+3,2                                                      
         BNE   *+10                                                             
         MVC   STAKPAC1,QJOBMVS                                                 
         CLI   QINTKEY+3,3                                                      
         BNE   *+10                                                             
         MVC   STAKSHI1,QJOBMVS                                                 
         B     OUTPUT                                                           
         EJECT                                                                  
***********************************************************************         
*ROUTINES TO TACK ON DESCRIPTIVE VALUES                               *         
***********************************************************************         
TACKON   NTR1                                                                   
         LA    R7,WORKIOD                                                       
         MVC   QJOBDATE,TODAY      SET CREATION DATE OF JOB RECORD              
         TM    LASTFLAG,QQHFRFPG   TEST RFP GROUP REQUEST                       
         BZ    TACKON0                                                          
         OC    LASTDATE,LASTDATE                                                
         BZ    TACKON0                                                          
         GOTO1 =V(DATCON),DMCB,(8,LASTDATE),(10,QJOBDATE)                       
*                                                                               
TACKON0  MVC   QJOBOSID(5),LRPQOSID                                             
         MVC   QJOBFACI,LRSYSID                                                 
         MVC   QJOBRFPG,LRRFPG                                                  
         MVC   QJOBMVS,STAKMVS                                                  
         MVC   QJOBJES,STAKJES                                                  
         MVC   QJOBSYS,STAKSYS                                                  
         MVC   QJOBNAME,STAKNAME                                                
         MVC   QJOBCC,STAKCC                                                    
         MVC   QJOBCOPY,STAKCOPY                                                
         MVC   QJOBDISP,STAKDISP                                                
         MVC   QJOBORUT,STAKORUT                                                
         CLI   QOUTSPEC,C'P'                                                    
         BNE   *+8                                                              
         MVI   QJOBDISP,C'T'                                                    
         CLI   QOUTSPEC,C'C'                                                    
         BNE   *+8                                                              
         MVI   QJOBDISP,C'T'                                                    
         CLI   QOUTSPEC,C'L'                                                    
         BNE   *+8                                                              
         MVI   QJOBDISP,C'T'                                                    
         CLI   QOUTSPEC,C'S'                                                    
         BNE   *+8                                                              
         MVI   QJOBDISP,C'T'                                                    
         MVC   QJOBTIME,STAKTIME                                                
         MVC   QJOBPOWO,STAKPOWO                                                
         MVC   QJOBSEP,STAKSEP                                                  
         MVC   QJOBTPDT,STAKTAP                                                 
         MVC   QJOBPTYP,STAKPTYP                                                
         MVC   QJOBCHRS,STAKCHRS                                                
         OC    QJOBCHRS,SPACES                                                  
         MVC   QJOBCHR2,STAKCHR2                                                
         OC    QJOBCHR2,SPACES                                                  
         MVC   QJOBCHR3,STAKCHR3                                                
         OC    QJOBCHR3,SPACES                                                  
         MVC   QJOBCHR4,STAKCHR4                                                
         OC    QJOBCHR4,SPACES                                                  
         MVC   QJOBFCH,STAKFCH                                                  
         OC    QJOBFCH,SPACES                                                   
         MVC   QJOBFMN,STAKFMN                                                  
         MVC   QJOBFMD,STAKFMD                                                  
         OC    QJOBFMD,SPACES                                                   
         MVC   QJOBPGN,STAKPGN                                                  
         MVC   QJOBPGD,STAKPGD                                                  
         OC    QJOBPGD,SPACES                                                   
*&&US                                                                           
         CLI   QOUTSPEC,C'I'                                                    
         BNE   *+10                                                             
         MVC   QJOBTPDT,=CL6'INTER'                                             
         CLI   QOUTSPEC,C'P'                                                    
         BNE   *+10                                                             
         MVC   QJOBTPDT,=CL6'BM-COM'                                            
         CLI   QOUTSPEC,C'C'                                                    
         BNE   *+10                                                             
         MVC   QJOBTPDT,=CL6'JWT-CH'                                            
         CLI   QOUTSPEC,C'L'                                                    
         BNE   *+10                                                             
         MVC   QJOBTPDT,=CL6'JWT-LA'                                            
         CLI   QOUTSPEC,C'S'                                                    
         BNE   *+10                                                             
         MVC   QJOBTPDT,=CL6'JWT-SF'                                            
*&&                                                                             
         MVC   QJOBCLAS,STAKCLAS                                                
         MVC   QJOBRETC,STAKRETC                                                
         MVC   QJOBPQDS,STAKPQDS                                                
         MVC   QJOBPQFM,STAKPQFM                                                
         MVC   QJOBART,STAKART                                                  
*                                                                               
TACKON0A CLC   QOUTTYPE,=C'DOWN  ' DOWN/SQL REQUESTS NOT ARCHIVABLE             
         BE    TACKON0B                                                         
         CLC   QOUTTYPE,=C'DIRDL '                                              
         BE    TACKON0B                                                         
         CLC   QOUTTYPE,=C'SQL   '                                              
         BE    TACKON0B                                                         
         CLC   QOUTTYPE,=C'DIRSQL'                                              
         BE    TACKON0B                                                         
         CLI   QOUTTYPE,C'@'                                                    
         BNE   TACKON0C                                                         
TACKON0B MVI   STAKARC,C' '        CLEAR ARCHIVE STACK VARIABLES                
         XC    STAKADT,STAKADT                                                  
         MVI   STAKARCS,C' '                                                    
*                                                                               
TACKON0C MVC   QJOBARC,STAKARC                                                  
         MVC   QJOBADT,STAKADT                                                  
         MVC   QJOBARCS,STAKARCS                                                
         MVC   QJOBJCL,STAKJCL                                                  
         MVC   QJOBPHS,STAKPHS                                                  
         MVC   QDIRECT,STAKDIR                                                  
         MVC   QJOBPDD,STAKPDD                                                  
         CLI   QJOBPDD,C'N'        IF PRINT@DDS=N BURST OPTION KICKS IN         
         BNE   *+10                                                             
         MVC   QJOBPQB,STAKPQB                                                  
         MVC   QOUTTYPX,STAKOOLD                                                
         MVC   QJOB@DDS,STAK@DDS                                                
         MVC   QJOBPQC,STAKPQC                                                  
         MVC   QJOBCTRL,SETCTRL    SET FLAG TO SHOW REQ HDR CONTROL             
         CLI   QJOBCTRL,CTRLNO                                                  
         BNE   *+8                                                              
         MVI   QJOBCTRL,0          CLEAR IF NO REQS HAVE HDR CTRL VALUE         
         XC    QJOBSPR,QJOBSPR                                                  
*                                                                               
         MVC   QJOBPROC,STAKPROC+4                                              
         GOTOR SPECHECK                                                         
         MVC   QJOBPACK,STAKPACK+4                                              
         MVC   QJOBSHIP,STAKSHIP+4                                              
         CLI   QJOBSHIP+2,C' '                                                  
         BE    *+14                                                             
         MVI   QJOBSHIP+2,C' '                                                  
         MVC   QJOBSHIP+3(149),STAKSHIP+6                                       
         GOTO1 =V(SQUASHER),DMCB,QJOBSHIP+3,149                                 
         MVC   QJOBLOG1,SPACES                                                  
         MVC   QJOBLOG2,SPACES                                                  
         MVC   QJOBDNAM,SPACES                                                  
         MVC   QJOBDADD,SPACES                                                  
         MVC   QJOBDAD2,SPACES                                                  
         MVC   QJOBDAD3,SPACES                                                  
         MVC   QJOBONAM,SPACES                                                  
         MVC   QJOBOADD,SPACES                                                  
*                                                                               
         GOTOR ORIGIN              GET ORIGIN DETAILS                           
         L     R4,=A(ORIGBUFF)                                                  
         MVI   ELCODE,CTDSCELQ     FIND DESCRIPTION ELEMENET                    
         GOTOR GETEL                                                            
         USING CTDSCD,R4                                                        
         MVC   DUB,SPACES          EXTRACT USER ID ALPHA                        
         LLC   R3,CTDSCLEN                                                      
         AHI   R3,-2               R3=L'USERID ALPHA                            
         LR    RF,R3                                                            
         CHI   RF,8                                                             
         BL    *+8                                                              
         LA    RF,8                MOVE FIRST 8 BYTES OF ALPHA USERID           
         AHI   RF,-1                                                            
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   DUB(0),CTDSC                                                     
         MVC   QJOBOID,DUB         1-6 GO HERE                                  
         MVC   QJOBOID1,DUB+6      7-8 GO HERE IN EXTENSION FIELD               
*                                                                               
         L     R4,=A(ORIGBUFF)                                                  
         MVI   ELCODE,CTORGELQ                                                  
         GOTOR GETEL                                                            
         BNE   TACKON1                                                          
         USING CTORGD,R4                                                        
         MVC   QJOBONAM,CTORGNAM                                                
         MVC   QJOBOADD,CTORGADD                                                
*                                                                               
TACKON1  GOTOR DESTIN                                                           
         L     R4,=A(DESTBUFF)                                                  
         MVI   ELCODE,CTSHPELQ     CHECK FOR SHIPPING INSTRUCTIONS              
         GOTOR GETEL                                                            
         BNE   TACKON1B                                                         
         USING CTSHPD,R4                                                        
         CLC   QJOBSHIP(20),SPACES THERE ALREADY - DONT OVERRIDE                
         BNE   TACKON1B                                                         
         LLC   R3,CTSHPLEN                                                      
         MVC   QJOBSHIP(2),CTSHPINS                                             
         AHI   R3,-9                                                            
         BM    TACKON1B                                                         
         EX    R3,*+8                                                           
         B     *+10                                                             
         MVC   QJOBSHIP+3(0),CTSHPINS+2                                         
         GOTO1 =V(SQUASHER),DMCB,QJOBSHIP+3,149                                 
*&&US                                                                           
         CLC   QSHIPUNT,=C'AB'                                                  
         BNE   TACKON1B                                                         
         CLC   QJOBSHIP(2),=C'SG'                                               
         BNE   TACKON1B                                                         
         L     RE,=A(ABSPEC)                                                    
         MVC   QJOBSHIP(150),0(RE)                                              
*&&                                                                             
TACKON1B DS    0H                                                               
*&&US                                                                           
         CLC   QSHIPUNT,=C'XA'                                                  
         BNE   TACKON1D                                                         
         CLC   QJOBSHIP,=C'SG'                                                  
         BNE   TACKON1D                                                         
         L     RE,=A(XASPEC)                                                    
         MVC   QJOBSHIP(150),0(RE)                                              
*&&                                                                             
TACKON1D L     R4,=A(DESTBUFF)                                                  
         XC    SAVELFC,SAVELFC                                                  
         MVI   ELCODE,CTDSTELQ                                                  
         GOTOR GETEL                                                            
         BE    TACKON1E                                                         
         LA    R4,DUMDEST          NO DEST ELEMENT - USE DUMMY                  
         USING CTDSTD,R4                                                        
TACKON1E MVC   QJOBLOG1,CTDSTLG1                                                
         MVC   QJOBLOG2,CTDSTLG2                                                
         OC    QJOBLOG1,SPACES                                                  
         OC    QJOBLOG2,SPACES                                                  
         MVC   QJOBDNAM,CTDSTNAM                                                
         MVC   QJOBDADD,CTDSTADD                                                
         CLI   CTDSTLEN,166                                                     
         BL    TACKON2                                                          
         MVC   QJOBDAD2,CTDSTAD2                                                
         MVC   QJOBDAD3,CTDSTAD3                                                
*                                                                               
TACKON2  MVC   SAVELFC,CTDSTLFC    SAVE LASER FORMS CHECKS                      
         MVC   QJOBJES(4),CTDSTPOW SET JES JOB NAME FROM POWER CODE             
         LA    R2,QJOBJES                                                       
         LA    R3,4                                                             
TACKON4  CLI   0(R2),C' '          PAD OUT GAPS WITH X                          
         BNE   *+8                                                              
         MVI   0(R2),C'X'                                                       
         CLI   0(R2),0                                                          
         BNE   *+8                                                              
         MVI   0(R2),C'X'                                                       
         LA    R2,1(R2)                                                         
         BCT   R3,TACKON4                                                       
*                                                                               
         L     R4,=A(DESTBUFF)     EXPAND ATTENTION CODE                        
         MVI   ELCODE,CTATTELQ                                                  
         GOTOR GETEL                                                            
         B     TACKON4D                                                         
*                                                                               
TACKON4B GOTOR NEXTEL                                                           
*                                                                               
TACKON4D BNE   TACKON6                                                          
         USING CTATTND,R4                                                       
         LA    R2,STAKATTN                                                      
         LA    R3,CTATTTYP                                                      
         LA    R1,3                                                             
*                                                                               
TACKON4F CLC   0(1,R2),0(R3)                                                    
         BE    TACKON4H                                                         
         CLI   0(R3),C'*'          * IS THE WILD CARD FEATURE                   
         BNE   TACKON4B                                                         
*                                                                               
TACKON4H LA    R2,1(R2)                                                         
         LA    R3,1(R3)                                                         
         BCT   R1,TACKON4F                                                      
         MVC   QJOBDAD3,SPACES     FOUND A MATCH - SO REPLACE LINE 3            
         LLC   R3,CTATTLEN                                                      
         AHI   R3,-6                                                            
         EX    R3,*+8                                                           
         B     *+10                                                             
         MVC   QJOBDAD3(0),CTATTDET                                             
         B     TACKON4B                                                         
*                                                                               
TACKON6  DS    0H                  SPECIAL ADJUSTS FOR JOB UUUUSPP              
*&&UK                                                                           
         CLI   QJOBJES+4,C'E'      FIX PERSON MAILING LIST NUM                  
         BNE   TACKON7                                                          
         CLI   QJOBJES+6,C'0'      PROGRAM PP MUST BE FORMAT P0                 
         BNE   TACKON7                                                          
         CLI   QSUBUSER,1          QSUBUSER CONTAINS LIST NUM                   
         BL    TACKON7                                                          
         CLI   QSUBUSER,35                                                      
         BH    TACKON7                                                          
         LLC   RE,QSUBUSER                                                      
         LA    RE,PEMAILID(RE)     GET MAIL LIST NUM CHR                        
         MVC   QJOBJES+6(1),0(RE)  AND OVERWRITE LAST CHR OF PROGRAM            
         B     TACKON7                                                          
PEMAILID DC    C'0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ'                          
*&&                                                                             
TACKON7  GOTOR JESDUP              CHECK FOR DUPLICATE JES JOB NAME             
         CLC   QJOBJES+4(3),=C'A08'                                             
         BNE   *+10                                                             
         MVC   QJOBJES+7(1),QSUB2                                               
         CLC   QJOBJES+4(3),=C'ADJ'                                             
         BNE   *+10                                                             
         MVC   QJOBJES+7(1),QSUB2                                               
         MVC   QJOBMVS+3(1),QJOBJES+7                                           
         CLI   QJOBMVS+3,C' '                                                   
         BNE   *+8                                                              
         MVI   QJOBMVS+3,C'0'                                                   
         MVC   QJOBMVS+4(4),QJOBJES          ALSO COMPLETE MVS                  
         CLC   QJOBPOWO(3),=C'AAA' RESOLVE FNOS THAT NEED DEST. CODE            
         BNE   TACKON8                                                          
         MVC   QJOBPOWO(2),QJOBJES                                              
         MVI   QJOBPOWO+2,C'X'                                                  
         CLI   QJOBPOWO+3,C'$'     RESOLVE MEDIA CODE FOR CHECKS                
         BNE   TACKON8                                                          
         MVC   QJOBPOWO+2(1),QSUBUSER                                           
*&&US                                                                           
         CLC   QORIGIN,=H'162'     IF BDLA REQUESTED CHECKS                     
         BNE   *+10                                                             
         MVC   QJOBPOWO(2),=C'BW'  CHANGE CODE TO BW                            
*&&                                                                             
*&&US                                                                           
         OC    SAVELFC,SAVELFC     IF LASER FORM CHECK CODE DEFINED             
         BZ    *+10                                                             
         MVC   QJOBPOWO(2),SAVELFC USE THIS INSTEAD                             
*&&                                                                             
*&&UK                                                                           
         MVC   QJOBPOWO(3),QJOBJES CHEQUE FORMS CODE=AAAL (AGY(3)/LGGR)         
         MVC   QJOBPOWO+3(1),QSUBUSER                                           
         CLC   QJOBPROC+38(3),=C'LSR' FOR LASER CHQS                            
         BNE   TACKON8                                                          
         MVC   QJOBPQDS,SPACES        QJOBPOWO(3) BECOMES REPORT ID             
         MVC   QJOBPQDS(4),QJOBPOWO   AND PQ DESC IS (AGY(3)/LGGR)              
*&&                                                                             
TACKON8  DS    0H                                                               
*&&US                                                                           
         CLC   QJOBPOWO(4),=C'&&CON'                                            
         BNE   TACKON9                                                          
         MVC   QJOBPOWO(4),=C'5NEW'                                             
         CLI   QSUBUSER,C'N'                                                    
         BE    XIT                                                              
         MVC   QJOBPOWO(4),=C'5OUT'                                             
         CLI   QSUBUSER,C'O'                                                    
         BE    XIT                                                              
         MVC   QJOBPOWO(4),=C'5MAG'                                             
         B     XIT                                                              
*                                                                               
TACKON9  CLC   QJOBPOWO(4),=C'&&4AS'                                            
         BNE   TACKON10                                                         
         MVC   QJOBPOWO(4),=C'XNPR'                                             
         CLI   QSUBUSER,C'N'                                                    
         BE    XIT                                                              
         MVC   QJOBPOWO(4),=C'XODR'                                             
         CLI   QSUBUSER,C'O'                                                    
         BE    XIT                                                              
         MVC   QJOBPOWO(4),=C'XMZN'                                             
         B     XIT                                                              
*                                                                               
TACKON10 CLC   QJOBPOWO(4),=C'&&INS'                                            
         BNE   XIT                                                              
         MVC   QJOBPOWO(4),=C'5RED'                                             
         CLI   QSUBUSER,C'N'                                                    
         BE    XIT                                                              
         MVC   QJOBPOWO(4),=C'5OUT'                                             
         CLI   QSUBUSER,C'O'                                                    
         BE    XIT                                                              
         MVC   QJOBPOWO(4),=C'5PRP'                                             
*&&                                                                             
*&&UK                                                                           
         CLC   QJOBPOWO(4),=C'&&CRA'                                            
         BNE   XIT                                                              
         MVC   QJOBPOWO(3),QJOBJES USE ONLY 1ST THREE CHRS FOR BETA             
         MVI   QJOBPOWO+3,C' '                                                  
*&&                                                                             
         B     XIT                                                              
*                                                                               
DUMDEST  DC    0X                  DUMMY DEST ELEMENT                           
         DC    X'30'                                                            
         DC    AL1(100)                                                         
         DC    CL33'NO DESTINATION DEFINED'                                     
         DC    CL33'IDI RECORDS NEEDS TO BE SET UP'                             
         DC    CL7'NO LOGO'                                                     
         DC    CL7'NO LOGO'                                                     
         DC    CL4'XXXX'                                                        
         DC    14X'00'                                                          
         EJECT                                                                  
***********************************************************************         
*SPECIAL ROUTINES FOR ACCPAK CHECKS                                   *         
***********************************************************************         
SPECHECK NTR1                                                                   
         CLI   QSYSTEM,C'A'                                                     
         BNE   XIT                                                              
         CLC   QPROGRAM,=C'55'     CHECKS                                       
         BE    SCH1                                                             
*&&US*&& CLC   QPROGRAM,=C'56'     CHECKS                                       
*&&US*&& BE    SCH1                                                             
*&&UK*&& CLC   QPROGRAM,=C'DC'     DEUTCHE CHECKS                               
*&&UK*&& BE    SCH1                                                             
         B     XIT                                                              
*                                                                               
SCH1     LA    R6,WORK             FIND ACCOUNT SE NUMBER FOR THIS ID           
         USING CND,R6                                                           
         XC    CNLEN,CNLEN                                                      
         MVC   CNID,QORIGIN                                                     
         GOTO1 =V(CONFID),DMCB,(R6),DUB                                         
         OC    DUB(4),DUB                                                       
         BNZ   SCH1A                                                            
         MVC   P(30),=C'INVALID/MISSING USER ID RECORD'                         
         GOTOR BOTH                                                             
         MVC   P(18),=C'RUN IS BATTLING ON'                                     
         GOTOR BOTH                                                             
         B     XIT                                                              
SCH1A    L     RF,=A(UTL)                                                       
         MVC   4(1,RF),CNASE                                                    
         LLC   RE,CNASE                                                         
         L     RF,=A(OPENACCS)                                                  
         AR    RE,RF                                                            
         CLI   0(RE),0                                                          
         BNE   SCH1X                                                            
         MVC   0(1,RE),CNASE                                                    
         GOTO1 =V(DATAMGR),DMCB,OPEN,=C'ACCOUNT',EMUFLIST                       
*                                                                               
SCH1X    MVC   QJOBMVS+6(1),QSUBUSER                                            
         MVC   QJOBPROC+114(38),QJOBPROC+76                                     
         MVC   QJOBPROC+076(38),QJOBPROC+38                                     
         MVC   QJOBPROC+038(38),QJOBPROC+00                                     
         MVC   QJOBPROC(38),SPACES                                              
*                                                                               
         L     R2,=A(ACCBUFF)                                                   
         USING CHARECD,R2                                                       
*&&UK*&& MVC   CHAKEY,SPACES       BUILD CHECK AUTHORIZATION RECORD             
*&&US*&& XC    CHAKEY,CHAKEY       BUILD CHECK AUTHORIZATION RECORD             
         MVI   CHAKTYP,CHAKTYPQ    X'10' RECORD                                 
         MVC   CHAKCPY,CNACD       COMPANY                                      
         MVI   CHAKUNT,C'S'        UNIT                                         
         MVC   CHAKLDG,QSUBUSER    LEDGER                                       
         GOTOR ACCHIGH                                                          
*&&UK*&& BE    SCH2A               IF NOT FOUND THEN LOOK ON LDG REC            
*&&US*&& CLC   ACCSAVE(4),0(R2)                                                 
*&&US*&& BE    SCH2A               IF NOT FOUND THEN LOOK ON LDG REC            
*                                                                               
         DROP  R2                                                               
*&&US                                                                           
         L     R2,=A(ACCBUFF)                                                   
         USING LDGRECD,R2                                                       
         MVC   LDGKEY,SPACES       BUILD KEY FOR LEDGER RECORD                  
         MVC   LDGKCPY,CNACD       COMPANY                                      
         MVI   LDGKUNT,C'S'        UNIT                                         
         MVC   LDGKLDG,QSUBUSER    LEDGER                                       
         GOTOR ACCHIGH                                                          
         BNE   SCH6                                                             
*&&                                                                             
*&&UK                                                                           
         L     R2,=A(ACCBUFF)                                                   
         USING LCRRECD,R2                                                       
         XC    LCRKEY,LCRKEY       BUILD KEY FOR LEDGER CHEQUE RECORD           
         MVI   LCRKTYP,LCRKTYPQ                                                 
         MVI   LCRKSUB,LCRKSUBQ                                                 
         MVC   LCRKCPY,CNACD       COMPANY + UNIT + LEDGER                      
         MVI   LCRKUNIT,C'S'                                                    
         MVC   LCRKLEDG,QSUBUSER                                                
SCH2     GOTOR ACCHIGH                                                          
         BNE   SCH6                                                             
         DROP  R2                                                               
*&&                                                                             
SCH2A    LA    R4,ACCORFST(R2)     ACCFIL (OLD STYLE)                           
         SR    R5,R5                                                            
*                                                                               
SCH3     CLI   0(R4),0                                                          
*&&US*&& BE    SCH6                                                             
*&&UK                                                                           
         BNE   SCH3X                                                            
         CLI   0(R2),LCRKTYPQ      LCR RECORD?                                  
         BNE   SCH3X                                                            
         LLC   R1,LCRKSEQ-LCRRECD(R2)                                           
         AHI   R1,1                                                             
         STC   R1,LCRKSEQ-LCRRECD(R2)                                           
         B     SCH2                                                             
SCH3X    DS    0H                                                               
*&&                                                                             
         CLI   0(R4),OCNELQ        X'54' OFFICE CHECK RECORD                    
         BNE   SCH4                                                             
         USING OCNELD,R4                                                        
         CLC   OCNOFFID,QORIGIN                                                 
         BNE   SCH4                                                             
*&&US                                                                           
         CLC   QPROGRAM,=C'56'     LOOK FOR A56'S LASERWSP/LASERWSL             
         BNE   SCH3A                                                            
         CLI   OCNLN,OCNLN3Q       MUST HAVE LONG ELEMENT                       
         BL    SCH3A                                                            
         CLI   OCNLASR,OCNWSP      LASERWSP CHECK REGISTER                      
         BE    SCH16                                                            
         CLI   OCNLASR,OCNWSL      LASERWSL CHECK REGISTER                      
         BE    SCH17                                                            
*&&                                                                             
SCH3A    TM    OCNSTAT,OCNSPRTQ+OCNSLOCL PRINTING DIRECT OR LOCAL               
         BNZ   SCH15                                                            
         CLC   QPROGRAM,=C'56'     TEST IF 56 AND NOT DIRECT                    
         BE    XIT                                                              
         MVC   QJOBPROC(13),=C'CHECK NUMBER='                                   
         MVC   QJOBPROC+13(L'OCNAFT),OCNAFT                                     
*&&US                                                                           
         CLI   OCNLN,OCNLN3Q       LOOK FOR LASERWSP AND LASERWSL               
         BL    SCH8                                                             
         CLI   OCNLASR,OCNWSP                                                   
         BNE   *+14                                                             
         MVC   QJOBPROC+13(6),=CL6'WSP   '                                      
         B     SCH8                                                             
         CLI   OCNLASR,OCNWSL                                                   
         BNE   SCH8                                                             
         MVC   QJOBPROC+13(6),=CL6'WSL   '                                      
         B     SCH8                                                             
*&&                                                                             
SCH4     IC    R5,1(R4)                                                         
         AR    R4,R5                                                            
         B     SCH3                                                             
*                                                                               
SCH6     DS    0H                                                               
*&&US                                                                           
         L     R2,=A(ACCBUFF)                                                   
         GOTOR ACCSEQ                                                           
         CLC   ACCSAVE(4),0(R2)    SAME KEY C/U/L ?                             
         BE    SCH2A               IF NOT FOUND THEN LOOK ON LDG REC            
         DROP  R2                                                               
*&&                                                                             
         MVC   QJOBPROC(18),=C'USE ONE-PART PLAIN'                              
*                                                                               
SCH8     MVC   QJOBPROC+20(7),=C'SYSTEM='                                       
*                                                                               
         LA    R2,MEDTAB                                                        
         USING MEDTABD,R2                                                       
SCH10    CLC   MEDCODE,QSUBUSER                                                 
         BE    SCH12                                                            
         CLI   0(R2),C' '                                                       
         BE    SCH12                                                            
         AHI   R2,MEDTLNQ                                                       
         B     SCH10                                                            
*                                                                               
SCH12    MVC   QJOBPROC+27(L'MEDNAME-1),MEDNAME                                 
*&&US                                                                           
         TM    OCNSTAT,OCNSSHUT    TEST DDS SHUTTLE                             
         BZ    *+10                                                             
         MVC   QJOBPROC+38(7),=C'SHUTTLE'                                       
*&&                                                                             
         CLI   OCNLN,OCNLN3Q       IS IT LONG ELEMENT                           
         BL    SCH14               NO                                           
         SR    RF,RF                                                            
         ICM   RF,1,OCNLASR        IS IT LASER PRINTER TYPE                     
         BZ    SCH13                                                            
         BCTR  RF,0                YES-SO GET TYPE                              
         MHI   RF,L'LASERTYP                                                    
         LA    RE,LASERTYP(RF)                                                  
         MVC   QJOBPROC+38(L'LASERTYP),0(RE)                                    
*&&US                                                                           
         CLC   QJOBPROC+38(6),=C'LSRWSP' IS IT LASERWSP AND FLATFILE?           
         BE    *+14                                                             
         CLC   QJOBPROC+38(6),=C'LSRWSL' IS IT LASERWSL AND FLATFILE?           
         BNE   SCH13                                                            
         TM    OCNSTAT2,OCNSDFIL                                                
         BZ    SCH14                     THEN CHANGE TO LSRWFF                  
         MVC   QJOBPROC+38(L'LASERTYP),=C'LSRWFF '                              
         B     SCH14                                                            
*&&                                                                             
SCH13    DS    0H                                                               
*&&US                                                                           
         TM    OCNSTAT2,OCNSDFIL                                                
         BZ    *+10                                                             
         MVC   QJOBPROC+38(L'LASERTYP),=C'LSRDFF '                              
*&&                                                                             
SCH14    B     XIT                                                              
*                                                                               
SCH15    MVC   QJOBPROC(20),=CL20'PRINTING DIRECT'                              
         CLC   QPROGRAM,=C'56'     CHECK REGISTERS                              
         BNE   XIT                                                              
         MVI   QJOBPTYP,C'B'                                                    
         B     XIT                                                              
*&&US                                                                           
SCH16    MVI   QJOBPTYP,C'B'       A56 LSRWSP SETTINGS                          
         MVC   QJOBPROC(20),=CL20'PRINTING DIRECT'                              
         MVC   QJOBPROC+20(6),=CL6'WSP   '                                      
         B     XIT                                                              
*                                                                               
SCH17    MVI   QJOBPTYP,C'B'       A56 LSRWSL SETTINGS                          
         MVI   QJOBKEEP,C'Y'                                                    
         MVC   QJOBPROC+20(6),=CL6'WSL   '                                      
         B     XIT                                                              
*&&                                                                             
         DROP  R4                                                               
                                                                                
***********************************************************************         
*TYPE OF CHECKS GOING TO LASER QUEUE                                  *         
***********************************************************************         
LASERTYP DS    0CL7                ORDER MATTERS, SO DON'T CHANGE ORDER         
         DC    CL7'LSRRED'         #1                                           
         DC    CL7'LSRBLU'         #2                                           
         DC    CL7'LSRGRE'         #3 NOT USED IN US ANYMORE                    
         DC    CL7'LSRGOL'         #4 NOT USED IN US ANYMORE                    
         DC    CL7'LSRBRO'         #5 NOT USED IN US ANYMORE                    
         DC    CL7'LSRVIO'         #6 NOT USED IN US ANYMORE                    
*&&US*&& DC    CL7'LSRWSP'         #7                                           
*&&US*&& DC    CL7'LSRWSL'         #8                                           
*&&UK*&& DC    CL7'LSRMAG'         #7                                           
*&&UK*&& DC    CL7'LSRORA'         #8                                           
                                                                                
***********************************************************************         
*MEDIA TYPES BASED ON LEDGER                                          *         
***********************************************************************         
MEDTAB   DS    0CL12                                                            
*&&US                                                                           
         DC    CL12'SSPOT'                                                      
         DC    CL12'PPRINT'                                                     
         DC    CL12'QCAN.PRINT'                                                 
         DC    CL12'TCAN.SPOT'                                                  
         DC    CL12'UNETWORK'                                                   
         DC    CL12'VPRODUCTION'                                                
         DC    CL12'WCAN. PROD.'                                                
         DC    CL12'XEXPENSES'                                                  
         DC    CL12'YCAN. XPENS.'                                               
*&&                                                                             
*&&UK                                                                           
         DC    CL12'TARTIST'                                                    
         DC    CL12'VPRODUCTION'                                                
         DC    CL12'XEXPENSES'                                                  
         DC    CL12'FMEDLINE'                                                   
*&&                                                                             
         DC    CL12' UNKNOWN'                                                   
         EJECT                                                                  
***********************************************************************         
*FINAL ROUTINES, XIT AND GETEL                                        *         
***********************************************************************         
FINAL    MVI   EOFSW,C'Y'                                                       
         B     OUTPUT1             POP BACK FOR LAST SUMMARY RECORD             
FINAL2   BRAS  RE,CLOSE            CLOSE FILES AND PRINT TOTALS                 
         B     EOJ                                                              
*                                                                               
XIT      XIT1                                                                   
*                                                                               
EOJ      GOTO1 =V(SORTER),PARA,=C'END'                                          
         XBASE                                                                  
*                                                                               
         GETEL (R4),DATADISP,ELCODE                                             
         EJECT                                                                  
***********************************************************************         
*ROUTINE TO DIG OUT OUTPUT TYPE FROM DESTINATION BLOCK                *         
***********************************************************************         
RESDEST  NTR1                                                                   
         L     R2,=A(DESTBUFF)                                                  
         USING DESD,R2                                                          
*                                                                               
RD2      CLI   0(R2),X'FF'                                                      
         BE    XIT                                                              
         CLC   DESID,QDEST                                                      
         BE    RD4                                                              
         LA    R2,DESLNQ(R2)                                                    
         B     RD2                                                              
*                                                                               
RD4      OC    DESOUT,DESOUT                                                    
         BZ    XIT                                                              
         MVC   STAKORUT,DESOUT                                                  
         CLC   STAKJES+4(3),=C'A55'                                             
         BNE   XIT                                                              
         MVC   STAKORUT,SPACES                                                  
         B     XIT                                                              
         DROP  R2                                                               
*                                                                               
RESUNIT  NTR1                                                                   
         L     R2,=A(DESTBUFF)                                                  
         USING DESD,R2                                                          
         MVC   THISUNIT,SPACES                                                  
*                                                                               
RU2      CLI   0(R2),X'FF'                                                      
         BE    XIT                                                              
         CLC   DESID,QDEST                                                      
         BE    RU4                                                              
         LA    R2,DESLNQ(R2)                                                    
         B     RU2                                                              
*                                                                               
RU4      OC    DESSHP,DESSHP                                                    
         BZ    XIT                                                              
         MVC   THISUNIT,DESSHP                                                  
         B     XIT                                                              
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
*ROUTINE TO CHECK FOR DUPLICATE JES NAMES                             *         
***********************************************************************         
JESDUP   NTR1                                                                   
         L     R2,=A(DUPBUFF)                                                   
         L     R3,0(R2)            R3=NUM OF ENTRIES                            
         CLC   QSYSTEM,4(R2)                                                    
         BE    DUP2                                                             
         LA    RE,8(R2)            RE=A(FIRST ENTRY)                            
         LR    RF,R3                                                            
         MHI   RF,10               RF=LENGTH OF TABLE                           
         SR    R1,R1                                                            
         MVCL  RE,R0               CLEAR WHOLE TABLE AT NEW SYSTEM              
*                                                                               
DUP2     MVC   4(1,R2),QSYSTEM     SET SYSTEM NAME IN TABLE                     
         LA    R4,8(R2)            R4=A(NEXT ENTRY)                             
         MVI   QJOBDUPS,C' '                                                    
*                                                                               
DUP4     CLI   0(R4),0             TEST EMPTY SLOT                              
         BE    DUP6                                                             
         CLC   0(7,R4),QJOBJES     TEST JOB NAME ALREADY IN TABLE               
         BE    DUP8                                                             
         LA    R4,10(R4)                                                        
         BCT   R3,DUP4                                                          
         B     DUPX                                                             
*                                                                               
DUP6     MVC   0(7,R4),QJOBJES     FIRST ENTRY FOR JOB                          
         MVI   7(R4),C' '                                                       
         MVI   QJOBJES+7,C' '                                                   
         XC    QJOBDUPN,QJOBDUPN                                                
         B     DUPX                                                             
*                                                                               
DUP8     SR    RE,RE               SUBSEQUENT JOB ENTRY                         
         SR    RF,RF                                                            
         ICM   RF,3,8(R4)                                                       
         LA    RF,1(RF)            BUMP DUPLICATE JOB COUNT                     
         STCM  RF,3,8(R4)                                                       
         STCM  RF,3,QJOBDUPN                                                    
         D     RE,=F'36'           MAX 36 CHRS TO APPEND TO JOB NAME            
         LTR   RF,RF                                                            
         BZ    DUP14                                                            
         LTR   RE,RE               RE=INDEX INTO JOB LETERS TABLE               
         BNZ   DUP10                                                            
         LHI   RE,36                                                            
         AHI   RF,-1               RF=NUMBER OF TIMES REPEATED                  
         BZ    DUP14                                                            
*                                                                               
DUP10    CLC   STAKDUPS(2),SPACES  ANY SPECIAL ACTION FOR DUPLICATES            
         BNE   *+12                                                             
         L     RE,=F'36'           DEFAULT IS TO REPEAT LAST DUP CHR            
         B     DUP14                                                            
         CLC   STAKDUPS(2),=C'< '  TEST GO BACK TO BEGINING EACH TIME           
         BE    DUP14                                                            
         LA    R1,2                SET NUMBER OF SETS OF DUPS                   
         CLI   STAKDUPS+2,C' '                                                  
         BE    *+8                                                              
         LA    R1,3                                                             
         CLI   STAKDUPS+3,C' '                                                  
         BE    *+8                                                              
         LA    R1,4                                                             
         CLI   STAKDUPS+4,C' '                                                  
         BE    *+8                                                              
         LA    R1,5                                                             
         CLI   STAKDUPS+5,C' '                                                  
         BE    *+8                                                              
         LA    R1,6                                                             
         CLI   STAKDUPS,C'<'       TEST TO CYCLE DUPLICATES                     
         BE    DUP12                                                            
         CR    RF,R1                                                            
         BL    DUP12                                                            
         LA    R1,STAKDUPS-1(R1)   POINT TO ALTERNATE SYSTEM LETTER             
         MVC   QJOBDUPS,0(R1)                                                   
         L     RE,=F'36'           SET TO REPEAT LAST DUP CHR                   
         B     DUP14                                                            
*                                                                               
DUP12    CR    RF,R1               TEST HAVE USED ALL EXTRA SETS                
         BL    DUP12A              NO                                           
         LR    R0,RE               SAVE RE                                      
         SR    RE,RE                                                            
         DR    RE,R1               DIVIDE BY NUM OF SETS OF DUPS                
         LR    RF,RE                                                            
         LR    RE,R0               RESTORE RE                                   
DUP12A   LTR   RF,RF                                                            
         BZ    DUP14                                                            
         LA    R1,STAKDUPS(RF)     POINT TO ALTERNATE SYSTEM LETTER             
         MVC   QJOBDUPS,0(R1)                                                   
*                                                                               
DUP14    EQU   *                   RF=REMAINDER GIVES DUPLICATION CHR           
*&&UK                                                                           
DUPCR    CLC   QJOBJES+4(2),=C'LC' TEST MPL/CRAFT                               
         BE    DUPCR1                                                           
         CLC   QJOBJES+4(2),=C'LN'                                              
         BNE   DUPCRX                                                           
DUPCR1   CHI   RE,19               DONT USE "S" SUFFIX FOR CRAFT                
         BNE   DUPCRX                                                           
         LA    RE,1(RE)                                                         
         SR    R1,R1                                                            
         ICM   R1,3,8(R4)                                                       
         LA    R1,1(R1)                                                         
         STCM  R1,3,8(R4)                                                       
         STCM  R1,3,QJOBDUPN                                                    
DUPCRX   EQU   *                                                                
*&&                                                                             
         LA    RE,DUPLIST(RE)      GET DUPLICATE JOB CHR                        
         MVC   QJOBJES+7(1),0(RE)                                               
*                                                                               
DUPX     B     XIT                                                              
*                                                                               
DUPLIST  DC    C' ABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789$'                        
         EJECT                                                                  
***********************************************************************         
*ROUTINE TO RESOLVE OUTPUT CHARACTERISTICS                            *         
***********************************************************************         
RESOUT   NTR1                                                                   
         L     R2,=A(OUTBLOCK)                                                  
         USING CFDD,R2                                                          
RESOUT2  CLI   0(R2),X'FF'                                                      
         BE    RESOUT6                                                          
         CLC   CFDOID,ROUTTYPE     USE ORIGINAL SAVED OUTPUT TYPE CODE          
         BE    RESOUT4             FROM REQUEST JOB HEADER                      
         LA    R2,L'CFDATA(R2)                                                  
         B     RESOUT2                                                          
*                                                                               
RESOUT4  MVC   QCLASS,CFDOCLS                                                   
         CLI   QOUTPRI,C'1'        MAY BE SET BY REQXPLOD                       
         BL    *+12                                                             
         CLI   QOUTPRI,C'9'                                                     
         BNH   *+10                                                             
         MVC   QOUTPRI,CFDOPRI                                                  
         CLI   STACKSW,C'Y'                                                     
         BNE   XIT                                                              
         MVC   STAKPOWO,CFDOPOW                                                 
         MVC   STAKCC,CFDOCC                                                    
         MVC   STAKCOPY,CFDOCPY                                                 
         MVC   STAKDISP,CFDODIS                                                 
         MVC   STAKSEP,CFDOSEP                                                  
         MVC   STAKTAP,CFDOTAP                                                  
         MVC   STAKPTYP,CFDOTYP                                                 
         MVC   STAKCHRS,CFDOCHR                                                 
         MVC   STAKCHR2,CFDOCH2                                                 
         MVC   STAKCHR3,CFDOCH3                                                 
         MVC   STAKCHR4,CFDOCH4                                                 
         MVC   STAKFCH,CFDOFCH                                                  
         MVC   STAKFMN,CFDOFMN                                                  
         MVC   STAKFMD,CFDOFMD                                                  
         MVC   STAKPGN,CFDOPGN                                                  
         MVC   STAKPGD,CFDOPGD                                                  
         MVC   STAKPQC,CFDOPQC                                                  
         B     XIT                                                              
         DROP  R2                                                               
*                                                                               
RESOUT6  MVI   QCLASS,C'A'         SET SOME DEFAULTS                            
         MVI   STAKFMN,0                                                        
         MVI   STAKPGN,0                                                        
         CLI   QOUTPRI,C'1'        MAY BE SET BY REQXPLOD                       
         BL    *+12                                                             
         CLI   QOUTPRI,C'9'                                                     
         BNH   *+8                                                              
         MVI   QOUTPRI,C'5'                                                     
         B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
*ROUTINE TO CHECK FOR WORKER ERRORS                                   *         
***********************************************************************         
WORKCHEK ST    RE,SAVERE                                                        
         CLI   DMCB+8,0                                                         
         BE    WORKCHK1                                                         
         MVC   P(34),=C'DISK ERROR ON WORKER - JOB DUMPING'                     
         GOTOR BOTH                                                             
         DC    H'0'                                                             
WORKCHK1 CLI   OTRACE,C'Y'                                                      
         BNE   WORKCHKX                                                         
         L     RF,=A(OTRC)                                                      
         BASR  RE,RF                                                            
WORKCHKX L     RE,SAVERE                                                        
         BR    RE                                                               
*                                                                               
BOTH     NTR1                                                                   
         CLI   AUTOSENT,C'Y'                                                    
         BE    BOTH1                                                            
         MVI   AUTOSENT,C'Y'                                                    
*&&UK*&& WTO   'AUTONOTE*EU-FAC-TEAM:EOD REQSORT - SEE SYSPRINT'                
*&&US*&& WTO   'AUTONOTE*US-MF_FAC_NOTIFY:EOD REQSORT - SEE SYSPRINT'           
BOTH1    GOTO1 =V(LOGIO),PARA,1,(60,P)                                          
         GOTO1 =V(PRINTER)                                                      
         B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
*ROUTINE TO CHECK IF DESTINATION AND ORIGIN IS IN CORE                *         
***********************************************************************         
DESTIN   NTR1                                                                   
         L     R2,=A(KEY)          IS DESTINATION IN CORE                       
         USING CTIREC,R2                                                        
         XC    CTIKEY,CTIKEY                                                    
         MVI   CTIKTYP,C'I'                                                     
         MVC   CTIKID+8(2),QDEST                                                
         L     R3,=A(DESTBUFF)                                                  
         CLC   0(L'CTIKEY,R2),0(R3)                                             
         BE    XIT                                                              
         B     IDIN                                                             
*                                                                               
ORIGIN   NTR1                                                                   
         L     R2,=A(KEY)          IS ORIGIN IN CORE                            
         USING CTIREC,R2                                                        
         XC    CTIKEY,CTIKEY                                                    
         MVI   CTIKTYP,C'I'                                                     
         MVC   CTIKID+8(2),QORIGIN                                              
         L     R3,=A(ORIGBUFF)                                                  
         CLC   0(L'CTIKEY,R2),0(R3)                                             
         BE    XIT                                                              
*                                                                               
IDIN     GOTOR HIGH                                                             
         CLC   KEYSAVE,0(R2)                                                    
         BE    IDIN2                                                            
         MVC   P(20),=C'CANT FIND ID NUMBER '                                   
         MVC   DUB,KEYSAVE+23                                                   
         LH    R4,DUB                                                           
         EDIT  (R4),(5,P+20),ALIGN=LEFT                                         
         GOTOR BOTH                                                             
         MVC   P(08),=C'REQUEST='                                               
         MVC   P+8(80),QDATA                                                    
         GOTOR BOTH                                                             
         MVC   P(30),=C'PROCESSING REQUEST WITH ID=DDS '                        
         GOTOR BOTH                                                             
         MVC   CTIKID+8(2),DDS                                                  
         GOTOR READ                                                             
*                                                                               
IDIN2    LR    R0,R3               MOVE RECORD TO BUFFER                        
         LA    R1,2000                                                          
         LR    RE,R2                                                            
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
         B     XIT                                                              
         EJECT                                                                  
EMUFLIST DC    C'NACCDIR NACCMST NACCARC X'                                     
         EJECT                                                                  
***********************************************************************         
*ROUTINE TO READ CONTROL FILE RECORDS                                 *         
***********************************************************************         
READ     LA    RF,DMREAD                                                        
         B     CONALL                                                           
*                                                                               
SEQ      LA    RF,DMRSEQ                                                        
         B     CONALL                                                           
*                                                                               
HIGH     LA    RF,DMRDHI                                                        
*                                                                               
CONALL   NTR1                                                                   
         ST    RF,DMCB                                                          
         L     R2,=A(KEY)                                                       
         MVC   KEYSAVE,0(R2)                                                    
         L     RF,=A(UTL)                                                       
         MVI   4(RF),X'0A'                                                      
         GOTO1 =V(DATAMGR),DMCB,,CONFILE,(R2),(R2),(0,DMWORK)                   
         CLI   DMCB+8,0                                                         
         BE    XIT                                                              
         MVC   P(31),=C'DISK ERROR READING CONTROL FILE'                        
         GOTOR BOTH                                                             
         MVC   P(8),=C'COMMAND='                                                
         L     R3,DMCB                                                          
         MVC   P+8(8),0(R3)                                                     
         GOTOR BOTH                                                             
         MVC   P(4),=C'KEY='                                                    
         MVC   P+4(25),KEYSAVE                                                  
         GOTO1 =V(HEXOUT),PARA,KEYSAVE,P+30,25,=C'TOG'                          
         GOTOR BOTH                                                             
         MVC   P(11),=C'ERROR=X''HH'''                                          
         GOTO1 =V(HEXOUT),PARA,DMCB+8,P+8,1,=C'TOG'                             
         GOTOR BOTH                                                             
         MVC   P(18),=C'RUN IS BATTLING ON'                                     
         GOTOR BOTH                                                             
         B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
*ROUTINE TO READ ACCOUNT RECORDS                                      *         
***********************************************************************         
ACCHIGH  LA    RF,DMRDHI                                                        
         B     ACCALL                                                           
*                                                                               
ACCSEQ   LA    RF,DMRSEQ                                                        
         B     ACCALL                                                           
*                                                                               
ACCALL   NTR1                                                                   
         ST    RF,DMCB                                                          
         L     R2,=A(ACCBUFF)                                                   
         MVC   ACCSAVE,0(R2)                                                    
         GOTO1 =V(DATAMGR),DMCB,,ACCFILE,(R2),(R2),(0,DMWORK)                   
         CLI   DMCB+8,0                                                         
         BNE   XIT                                                              
         CLC   ACCSAVE,0(R2)                                                    
         B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
*INPUT PHASE STACK DEFINITION                                         *         
***********************************************************************         
STAKRECD DS    0CL256                                                           
         DC    X'FFFFFF00'                                                      
STAKREC1 DC    CL252' '                                                         
STAKRECX DS    0C                                                               
         ORG   STAKREC1                                                         
STAKMVS  DS    CL8                                                              
STAKJES  DS    CL8                                                              
STAKSYS  DS    CL8                                                              
STAKNAME DS    CL40                                                             
STAKCC   DS    CL4                                                              
STAKCOPY DS    CL1                                                              
STAKDISP DS    CL1                                                              
STAKTIME DS    CL4                                                              
STAKPOWO DS    CL4                                                              
STAKSEP  DS    CL1                                                              
STAKCLAS DS    CL1                                                              
STAKJCL  DS    CL10                                                             
STAKPHS  DS    CL4                                                              
STAKATTN DS    CL3                                                              
STAKTAP  DS    CL6                                                              
STAKORUT DS    CL6                                                              
STAKPTYP DS    CL1                                                              
STAKCHRS DS    CL4                                                              
STAKRETC DS    CL1                                                              
STAKCHR2 DS    CL4                                                              
STAKCHR3 DS    CL4                                                              
STAKCHR4 DS    CL4                                                              
STAKFCH  DS    CL1                                                              
STAKFMN  DS    XL1                                                              
STAKFMD  DS    CL6                                                              
STAKPGN  DS    XL1                                                              
STAKPGD  DS    CL6                                                              
STAKPQDS DS    CL11                                                             
STAKPQFM DS    CL5                                                              
STAKDATE DS    CL8                                                              
STAKSQL  DS    CL5                                                              
STAKART  DS    CL1                                                              
STAKARC  DS    CL1                                                              
STAKADT  DS    XL4                                                              
STAKDUPS DS    CL6                                                              
STAKPDD  DS    CL1                                                              
STAKPQB  DS    CL1                                                              
STAKARCS DS    CL1                                                              
STAKOOLD DS    CL6                                                              
STAKNPD  DS    PL3                                                              
STAK@DDS DS    CL1                                                              
STAKPQC  DS    CL1                                                              
STAKDIR  DS    XL1                                                              
         DS    CL54                SPARE                                        
         ORG   STAKRECX                                                         
*                                                                               
STAKPROC DS    0CL256                                                           
         DC    X'FFFFFF01'                                                      
STAKPRO1 DC    CL252' '                                                         
*                                                                               
STAKPACK DS    0CL256                                                           
         DC    X'FFFFFF02'                                                      
STAKPAC1 DC    CL252' '                                                         
*                                                                               
STAKSHIP DS    0CL256                                                           
         DC    X'FFFFFF03'                                                      
STAKSHI1 DC    CL252' '                                                         
*                                                                               
         DC    C'END '                                                          
*                                                                               
ASTACK   DC    A(0)                                                             
STACKSW  DC    C'N'                                                             
THISUNIT DC    C'  '                                                            
OVERUNIT DC    C'  '                                                            
RSYSID   DC    X'00'               REQUEST FACPAK SYSTEM ID                     
RFLAG1   DC    X'00'               REQUEST FLAG1                                
RCTRL    DC    X'00'               REQUEST CONTROL                              
RPRIPO   DC    X'00'               REQUEST PRIPO SAVE                           
RDATE    DC    XL3'00'             REQUEST HEADER DATE                          
RDEST    DC    XL2'00'             REQUEST DESTINATION ID                       
RPQOSID  DC    CL3' '              REQUEST OVERNIGHT SOON PQ REPORT ID          
RPQOSNO  DC    XL2'00'             REQUEST OVERNIGHT SOON PQ REPORT NO          
ROUTTYPE DC    CL6' '              REQUEST OUTPUT TYPE                          
RRFPG    DC    CL8' '              REQUEST RFP GROUP                            
LRPQOSID DC    XL3'00'                                                          
LRPQOSNO DC    XL2'00'                                                          
LRSYSID  DC    X'00'                                                            
LRRFPG   DC    CL8' '                                                           
RSVQINFO DS    XL(L'QREQINFO)      SAVED QINFO                                  
*                                                                               
SECSORT  DS    0XL9                SECURITY SORT                                
SECTYPE  DS    XL1                 DATA TYPE                                    
SECTPNO  EQU   0                   NO PID(NOTHING IN PID)                       
SECTPRQ  EQU   0                   PID OF REQUESTOR                             
SECTONS  EQU   1                   OVERNIGHT SOON                               
SECTPIN  EQU   2                   REQUESTOR ENTERD A PIN                       
SECTPID  EQU   3                   REQUESTOR ENTERD A PID                       
SECTGRP  EQU   4                   SECURITY GROUP                               
SECPID   DS    XL4                 REQUESTOR PID                                
SECFLD   DS    XL4                 SECURITY PIN OR PID                          
         ORG   SECPID                                                           
SECOSID  DS    CL3                 OVERNIGHT SOON REPORT ID                     
SECOSNO  DS    XL2                 OVERNIGHT SOON REPORT NUM                    
         DS    XL3                 N/D                                          
*                                                                               
DDSOUTYP DS    CL6                                                              
*                                                                               
DUMPLIST DS    0D                                                               
         DC    A(REQSORT)                                                       
         DC    V(DUMMY)                                                         
         ORG   *-4                                                              
         DC    X'80'                                                            
         ORG                                                                    
DUB      DS    D                                                                
DUB1     DS    D                                                                
WORK     DS    CL40                                                             
DMCB     DS    6F                                                               
PARA     DS    6F                                                               
DMWORK   DS    12D                                                              
SAVER1   DS    F                                                                
SAVERE   DS    F                                                                
*                                                                               
ANYRECS  DC    C'N'                                                             
AUTOSENT DC    C'N'                                                             
BYTE     DC    X'00'                                                            
BYTE1    DC    X'00'                                                            
*                                                                               
LENJOBH  DC    H'38'                                                            
LENJOBR  EQU   4+L'QJOBKEY+EODREQX-QJOBREQS                                     
LENREQR  DC    H'85'                                                            
*                                                                               
C        DS    CL80                                                             
ITRACE   DC    C'N'                                                             
ITRACETY DC    C' '                                                             
OTRACE   DC    C'N'                                                             
OTRACETY DC    C' '                                                             
RFHDR    DC    C'Y'                                                             
ELCODE   DC    X'00'                                                            
DATADISP DC    H'28'                                                            
DDS      DC    H'90'                                                            
KEYSAVE  DS    CL25                                                             
ACCSAVE  DS    CL42                                                             
*                                                                               
PROFFLAG DS    X                   PROFILE STATUS                               
PROFDEST EQU   X'80'               DESTINATION ID OVERRIDE                      
PROFNDST EQU   X'40'               NO DEST ID PROFILE AVAILABLE                 
PROFXTRA EQU   X'04'               EXTRA PROFILE PRESENT                        
PROFOVER EQU   X'02'               OVERRIDE PROFILE PRESENT                     
PROFMSTR EQU   X'01'               MASTER PROFILE PRESENT                       
*                                                                               
PROFTYPE DS    X                   PROFILE TYPE                                 
PROFTMST EQU   1                   MASTER                                       
PROFTOFC EQU   2                   OFFICE                                       
PROFTXTR EQU   3                   EXTRA                                        
*                                                                               
SETCTRL  DC    X'00'                                                            
CTRLNO   EQU   X'80'               NO VALUE IN REQ HDR CONTROL                  
CTRLYES  EQU   X'01'               VALUE IN REQ HDR CONTROL                     
*                                                                               
REQCOUNT DC    PL4'0'                                                           
INPRECS  DC    PL4'0'                                                           
OUTRECS  DC    PL4'0'                                                           
JOBRECS  DC    PL4'0'                                                           
REQRECS  DC    PL4'0'                                                           
RFHRECS  DC    PL4'0'                                                           
*                                                                               
AFORM    DC    A(0)                                                             
DOWNCNT  DC    XL2'00'                                                          
SQLCNT   DC    XL2'00'                                                          
SQL@CNT  DC    XL2'00'                                                          
ARCCNT   DC    XL2'00'                                                          
OVSNCNT  DC    XL2'00'                                                          
DMRDHI   DC    CL8'DMRDHI'                                                      
OPEN     DC    CL8'DMOPEN'                                                      
DMREAD   DC    CL8'DMREAD'                                                      
DMRSEQ   DC    CL8'DMRSEQ'                                                      
CONFILE  DC    CL8'CTFILE'                                                      
ACCFILE  DC    CL8'ACCOUNT'                                                     
FILES    DC    CL8'CONTROL'                                                     
OPENLIST DC    CL8'NCTFILE'                                                     
         DC    C'X'                                                             
TODAY    DC    CL8' '              C'DD/MM/YY' OR C'MM/DD/YY'                   
TODYMD   DC    XL3'00'                                                          
TODPAK   DC    PL3'00'             PACKED VERSION OF TODAY                      
DAYOFWK  DC    CL3' '                                                           
*&&UK                                                                           
NPDATE   DC    CL8'29/09/07'       NO PRINTING DATE FOR UK                      
NPDPAK   DC    PL3'0'                                                           
NPDOPT   DC    C'Y'                OPTION TO SET DIRECT IF NOT PRINTING         
NPDPDD   DC    C'N'                DEFAULT VALUE FOR PRINT@DDS                  
NPDPQB   DC    C'N'                DEFAULT VALUE FOR PQ BURST                   
ARCASEP  DC    C'N'                SEPARATE JOBS FOR ARCHIVABLE                 
ARCESEP  DC    C'N'                SEPARATE JOBS FOR ARCHIVE ELIGIBLE           
*&&                                                                             
*&&US                                                                           
NPDATE   DC    CL8'12/31/27'       NO PRINTING DATE FOR US                      
NPDPAK   DC    PL3'0'                                                           
NPDOPT   DC    C'Y'                OPTION TO SET DIRECT IF NOT PRINTING         
NPDPDD   DC    C'N'                DEFAULT VALUE FOR PRINT@DDS                  
NPDPQB   DC    C'N'                DEFAULT VALUE FOR PQ BURST                   
ARCASEP  DC    C'N'                SEPARATE JOBS FOR ARCHIVABLE                 
ARCESEP  DC    C'N'                SEPARATE JOBS FOR ARCHIVE ELIGIBLE           
*&&                                                                             
SACRED   DC    C'N'                                                             
PRGPERM  DC    CL3' '                                                           
PRGOVER  DC    CL3' '                                                           
PRIPERM  DC    CL3' '                                                           
PRIOVER  DC    CL3' '                                                           
SAVELFC  DC    XL2'00'                                                          
*                                                                               
ID       DC    CL8' '                                                           
         DC    XL8'00'                                                          
JD       DC    CL8' '                                                           
         DC    XL8'00'                                                          
LASTSYS  DS    CL2                                                              
LASTJOB  DC    CL30' '                                                          
LASTINJB DC    CL30' '                                                          
LASTDATE DC    XL3'00'                                                          
LASTFLAG DC    X'00'                                                            
EOFSW    DC    C'N'                                                             
*                                                                               
         LTORG                                                                  
*                                                                               
WORKIO   DC    F'0'                                                             
WORKIOD  DS    2000C               ENOUGH FOR HEADER AND 15 REQ CARDS           
*                                                                               
OPENACCS DS    256X'00'                                                         
         EJECT                                                                  
***********************************************************************         
*ROUTINE TO CHECK FOR PROGRAMS THAT MUST SKIP SECURITY SORT           *         
***********************************************************************         
SKIPSEC  NTR1  BASE=*                                                           
         LA    R1,SKISTAB                                                       
SKISEC1  CLC   QSYSTEM,0(R1)                                                    
         BNE   *+14                                                             
         CLC   QPROGRAM,1(R1)                                                   
         BE    SKISECY             SKIP THIS ONE                                
         LA    R1,3(R1)                                                         
         CLI   0(R1),X'FF'                                                      
         BNE   SKISEC1                                                          
SKISECN  CR    RB,RC               EXIT WITH CC=NEQ IF NOT IN TABLE             
         B     SKISECX                                                          
SKISECY  LTR   RB,RB               EXIT WITH CC=EQL IF IN TABLE                 
         B     SKISECX                                                          
SKISECX  XIT1                                                                   
*                                                                               
SKISTAB  DS    0CL3                                                             
*&&UK*&& DC    C'ADC'                                                           
*&&UK*&& DC    C'AD0'                                                           
*&&UK*&& DC    C'AHP'                                                           
*&&UK*&& DC    C'A12'                                                           
*&&UK*&& DC    C'A5P'                                                           
         DC    C'A55'                                                           
*&&UK*&& DC    C'MBA'                                                           
*&&UK*&& DC    C'M09'                                                           
*&&UK*&& DC    C'M12'                                                           
         DC    X'FF'                                                            
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*ROUTINE TO CHECK FOR PROGRAMS THAT PROCESS MULTIPLE ARCHIVE REQUESTS *         
***********************************************************************         
SKIPARC  NTR1  BASE=*                                                           
         LA    R1,SKIATAB                                                       
SKIARC1  CLC   QSYSTEM,0(R1)                                                    
         BNE   *+14                                                             
         CLC   QPROGRAM,1(R1)                                                   
         BE    SKIARCY             SKIP THIS ONE                                
         LA    R1,3(R1)                                                         
         CLI   0(R1),X'FF'                                                      
         BNE   SKIARC1                                                          
SKIARCN  CR    RB,RC               EXIT WITH CC=NEQ IF NOT IN TABLE             
         B     SKIARCX                                                          
SKIARCY  LTR   RB,RB               EXIT WITH CC=EQL IF IN TABLE                 
         B     SKIARCX                                                          
SKIARCX  XIT1                                                                   
*                                                                               
SKIATAB  DS    0CL3                                                             
*&&UK*&& DC    C'ADC'                                                           
*&&UK*&& DC    C'AD0'                                                           
*&&UK*&& DC    C'AHP'                                                           
*&&UK*&& DC    C'A12'                                                           
*&&UK*&& DC    C'A5P'                                                           
         DC    C'A55'                                                           
*&&UK*&& DC    C'MBA'                                                           
*&&UK*&& DC    C'M09'                                                           
*&&UK*&& DC    C'M12'                                                           
         DC    X'FF'                                                            
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*ROUTINE TO ENSURE PROFILES ARE IN CORE                               *         
***********************************************************************         
PROFIN   NTR1  BASE=*                                                           
         L     R2,=A(KEY)          IS THE PROGRAM RECORD IN CORE                
         USING CTPREC,R2                                                        
         XC    CTPKEY,CTPKEY                                                    
         MVI   CTPKTYP,C'P'                                                     
         MVC   CTPKSYS,QSYSTEM                                                  
*&&US*&& CLI   CTPKSYS,C'F'        SPOT TRAFFIC USES SPOT PROFILES              
*&&US*&& BNE   *+8                                                              
*&&US*&& MVI   CTPKSYS,C'S'                                                     
         MVC   CTPKPROG,QPROGRAM                                                
         MVI   PROFFLAG,0                                                       
*                                                                               
PROFIN1  L     R3,=A(PROGBUFF)     MASTER PROGRAM PROFILE IN PROGBUFF           
         CLC   0(L'CTPKEY,R2),0(R3)                                             
         BE    PROFIN1X                                                         
         MVC   0(L'CTPKEY,R3),0(R2)                                             
         MVI   28(R3),0            PRESET TO NONE                               
         GOTOR READ                NO-GO AND GET ONE AND SAVE                   
         CLI   DMCB+8,0                                                         
         BE    PROFIN1A                                                         
         MVC   P(36),=C'INVALID/MISSING PROGRAM PROFILE S/PP'                   
         MVC   P+32(1),QSYSTEM                                                  
         MVC   P+34(2),QPROGRAM                                                 
         GOTOR BOTH                                                             
         MVC   P(18),=C'RUN IS BATTLING ON'                                     
         GOTOR BOTH                                                             
         B     PROFIN2             MASTER PROFILE NOT FOUND                     
PROFIN1A LR    R0,R3                                                            
         LA    R1,1000                                                          
         LR    RE,R2                                                            
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
PROFIN1X OI    PROFFLAG,PROFMSTR   SET MASTER PROFILE PRESENT                   
*                                                                               
PROFIN2  MVC   CTPKORIG(2),QORIGIN SET TO READ OFFICE OVERRIDE                  
         OC    QDEST,QDEST         IF DESTINATION IS SPECIALLY                  
         BZ    PROFIN2A            REQUESTED THEN USE THAT ID                   
         CLC   QDEST,QORIGIN                                                    
         BE    PROFIN2A            IGNORE IF DESTINATION=ORIGIN                 
         MVC   CTPKORIG(2),QDEST                                                
         OI    PROFFLAG,PROFDEST   SET DESTID INPUT OVERRIDE                    
PROFIN2A L     R3,=A(OVERBUFF)                                                  
         CLC   0(L'CTPKEY,R2),0(R3)                                             
         BE    PROFIN2X                                                         
         MVC   0(L'CTPKEY,R3),0(R2)                                             
         MVI   28(R3),0            PRESET TO NONE                               
         GOTOR HIGH                                                             
         CLC   0(L'CTPKEY,R3),0(R2)                                             
         BE    PROFIN2C                                                         
PROFIN2B TM    PROFFLAG,PROFDEST      WERE WE READING QDEST OVERRIDE            
         BZ    PROFINX                NO-EXIT IF NO OFFICE OVERRIDE             
         NI    PROFFLAG,255-PROFDEST  TURN OFF DESTID INPUT OVERRIDE            
         TM    PROFFLAG,PROFNDST                                                
         BO    PROFINX             SHOULD NOT BE HERE TWICE                     
         OI    PROFFLAG,PROFNDST   SET NO QDEST PROFILE AVAILABLE               
         MVC   CTPKEY,0(R3)                                                     
         MVC   CTPKORIG(2),QORIGIN SET TO READ OFFICE OVERRIDE                  
         B     PROFIN2A                                                         
PROFIN2C LR    R0,R3                                                            
         LA    R1,1000                                                          
         LR    RE,R2                                                            
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
PROFIN2X OI    PROFFLAG,PROFOVER   SET OVERRIDE PROFILE PRESENT                 
*                                                                               
PROFIN3  TM    PROFFLAG,PROFDEST   WAS DESTINATION ID INPUT                     
         BZ    PROFINX                                                          
         MVC   CTPKORIG(2),QORIGIN YES-SET UP TO READ OFFICE PROFILE            
         L     R3,=A(XTRABUFF)                                                  
         CLC   0(L'CTPKEY,R2),0(R3)                                             
         BE    PROFIN3X                                                         
         MVC   0(L'CTPKEY,R3),0(R2)                                             
         MVI   28(R3),0            PRESET TO NONE                               
         GOTOR HIGH                                                             
         CLC   0(L'CTPKEY,R3),0(R2)                                             
         BNE   PROFINX             MISSED-SO NO OVERRIDE                        
         LR    R0,R3                                                            
         LA    R1,1000                                                          
         LR    RE,R2                                                            
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
PROFIN3X OI    PROFFLAG,PROFXTRA   SET XTRA PROFILE PRESENT                     
*                                                                               
PROFINX  XIT1                                                                   
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*READ PARAMETER CARDS                                                 *         
***********************************************************************         
CARDIN   NTR1  BASE=*                                                           
         MVI   BYTE,0              SET OK RETURN CODE                           
         GOTO1 =V(DATCON),PARA,(5,0),(10,TODAY)                                 
*                                                                               
CARD0    GOTO1 =V(CARDS),PARA,C,=C'RE00'                                        
         MVC   P(80),C                                                          
         GOTO1 =V(PRINTER)                                                      
         CLC   C(2),=C'/*'                                                      
         BE    CARDX                                                            
*                                                                               
CARD1    CLC   C(10),=C'ITRACE=YES'                                             
         BNE   CARD2                                                            
         MVC   TITLE+06+13(5),=C'TRACE'                                         
         MVI   ITRACE,C'Y'                                                      
         MVC   ITRACETY,C+10       H=HEX,F=FULL                                 
         B     CARD0                                                            
*                                                                               
CARD2    CLC   C(10),=C'OTRACE=YES'                                             
         BNE   CARD3                                                            
         MVC   TITLE+06+13(5),=C'TRACE'                                         
         MVI   OTRACE,C'Y'                                                      
         MVC   OTRACETY,C+10       H=HEX,F=FULL                                 
         B     CARD0                                                            
*                                                                               
CARD3    CLC   C(9),=C'TRACE=YES'                                               
         BNE   CARD4                                                            
         MVC   TITLE+06+13(5),=C'TRACE'                                         
         MVI   ITRACE,C'Y'                                                      
         MVI   OTRACE,C'Y'                                                      
         MVC   ITRACETY,C+9        H=HEX,F=FULL                                 
         MVC   OTRACETY,C+9                                                     
         B     CARD0                                                            
*                                                                               
CARD4    CLC   C(6),=C'DDSIO='     DDSIO=XXXXXXXX TO CHANGE DATAMGR             
         BNE   CARD5                                                            
         L     RF,=V(DDSIO)                                                     
         MVC   0(8,RF),C+6                                                      
         B     CARD0                                                            
*                                                                               
CARD5    CLC   C(6),=C'RFHDR='     RFHDR=YES OR NO                              
         BNE   CARD6                                                            
         MVC   RFHDR(1),C+6                                                     
         B     CARD0                                                            
*                                                                               
CARD6    CLC   C(3),=C'ACC'        ACC.=EMU                                     
         BNE   CARD7                                                            
         CLC   C+4(4),=C'=EMU'                                                  
         BNE   CARD7                                                            
         L     R4,=A(EMULIST)      FIND A FREE SLOT IN THE LIST                 
         LA    R5,L'EMULIST-1                                                   
CARD6A   CLI   0(R4),0                                                          
         BE    CARD6B                                                           
         LA    R4,1(R4)                                                         
         BCT   R5,CARD6A                                                        
         DC    H'0'                MAKE EMULIST LONGER                          
CARD6B   MVC   0(1,R4),C+3         SAVE ACC SYSTEM NUMBER                       
         B     CARD0                                                            
*                                                                               
CARD7    CLC   C(5),=C'DATE='      DATE=........ OVERRIDES TODAY'S DATE         
         BNE   CARD8                                                            
         ICM   RF,15,=V(DATVAL)                                                 
         BZ    CARDERR                                                          
         GOTO1 (RF),PARA,(0,C+5),DUB                                            
         OC    0(4,R1),0(R1)                                                    
         BZ    CARDERR                                                          
         MVC   TODAY,C+5           TODAY=C'DD/MM/YY' OR C'MM/DD/YY'             
         B     CARD0                                                            
*                                                                               
CARD8    CLC   C(7),=C'DSPACE='    DSPACE=X OVERRIDES DATASPACE                 
         BNE   CARD9                                                            
         L     RF,=A(SSB)                                                       
         MVC   SSODSPAC-SSOOFF(1,RF),C+7                                        
         B     CARD0                                                            
*                                                                               
CARD9    CLC   C(7),=C'NPDATE='    NPDATE=DATE SETS NO PRINTING DATE            
         BNE   CARD10                                                           
         L     RF,=V(DATVAL)       ROUTINE FOR NPDATE=                          
         LTR   RF,RF                                                            
         BZ    CARD10                                                           
         GOTO1 (RF),PARA,(0,C+7),DUB                                            
         OC    0(4,R1),0(R1)                                                    
         BZ    CARDERR                                                          
         MVC   NPDATE,C+7          OVERRIDE NO PRINT DATE                       
         B     CARD0                                                            
*                                                                               
CARD10   CLC   C(7),=C'NPDOPT='    NPDOPT=X SET NO PRINTING OPTION              
         BNE   CARD11                                                           
         MVC   NPDOPT,C+7                                                       
         B     CARD0                                                            
*                                                                               
CARD11   CLC   C(9),=C'STXITER=N'  STXITER=N TO NOP STXIT                       
         BE    *+14                                                             
         CLC   C(7),=C'DUMP=OS'    DUMP=OS DOES THE SAME                        
         BNE   CARD12                                                           
         L     RF,=V(STXITER)      NOP ROUTINE                                  
         MVC   0(2,RF),=X'07FE'                                                 
         B     CARD0                                                            
*                                                                               
CARD12   CLC   C(8),=C'ARCASEP='   ARCASEP=Y FOR SEPARATE ARCA JOBS             
         BNE   CARD13                                                           
         MVC   ARCASEP,C+8                                                      
         B     CARD0                                                            
*                                                                               
CARD13   CLC   C(8),=C'ARCESEP='   ARCESEP=Y FOR SEPARATE ARCE JOBS             
         BNE   CARD14                                                           
         MVC   ARCESEP,C+8                                                      
         B     CARD0                                                            
*                                                                               
CARD14   EQU   *                                                                
*                                                                               
CARDERR  MVC   P(36),=C'INVALID PARAMETER CARD - JOB ABORTED'                   
         GOTO1 =V(PRINTER)                                                      
         OI    BYTE,X'01'          SET RETURN CODE FOR INVALID CARD             
         B     CARDXX                                                           
*                                                                               
CARDX    GOTO1 =V(STXITER),PARA,DUMPLIST                                        
*                                                                               
         GOTO1 =V(DATCON),PARA,(4,TODAY),(3,TODYMD)                             
         GOTO1 (RF),(R1),(4,NPDATE),(1,NPDPAK)                                  
         GOTO1 (RF),(R1),(3,TODYMD),(1,TODPAK)                                  
         GOTO1 =V(GETDAY),(R1),DUB,DAYOFWK                                      
*                                                                               
CARDXX   CLI   BYTE,0              EXIT WITH CC EQL IF ALL OK                   
         XIT1                                                                   
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*ROUTINE TO BUILD OUTPUT TYPE BLOCK                                   *         
***********************************************************************         
BUILDOUT NTR1  BASE=*                                                           
         L     R2,=A(KEY)                                                       
         L     R3,=A(OUTBLOCK)                                                  
         USING CFDD,R3                                                          
         LHI   R5,OUTBLK#          R5=NUMBER OF ENTRIES IN OUTBLOCK             
         USING CTOREC,R2                                                        
         XC    CTOKEY,CTOKEY                                                    
         MVI   CTOKTYP,C'O'                                                     
         GOTOR HIGH                                                             
         B     BO4                                                              
*                                                                               
BO2      GOTOR SEQ                                                              
*                                                                               
BO4      CLI   CTOKTYP,C'O'                                                     
         BE    BO6                                                              
         MVI   0(R3),X'FF'                                                      
         B     BOXIT                                                            
*                                                                               
BO6      LR    R4,R2                                                            
         MVI   ELCODE,CTOUTELQ                                                  
         GOTOR GETEL                                                            
         BE    *+6                                                              
         DC    H'0'                                                             
         USING CTOUTD,R4                                                        
         XC    CFDATA(L'CFDATA),CFDATA                                          
         MVC   CFDOID,CTOKID                                                    
         MVC   CFDOCLS,CTOUTCLS                                                 
         MVC   CFDOPRI,CTOUTPRI                                                 
         MVC   CFDOPOW,CTOUTPOW                                                 
         MVC   CFDOCC,CTOUTCC                                                   
         MVC   CFDOCPY,CTOUTCPY                                                 
         MVC   CFDODIS,CTOUTDIS                                                 
         MVC   CFDOSEP,CTOUTSEP                                                 
         MVC   CFDOTAP,CTOUTTAP                                                 
         MVC   CFDOTYP,CTOUTTYP                                                 
         MVC   CFDOCHR,CTOUTCHR                                                 
         CLI   CTOUTLEN,32         TEST NEW EXTENDED ELEMENT                    
         BNH   BO7                                                              
         MVC   CFDOCH2,CTOUTCH2                                                 
         MVC   CFDOCH3,CTOUTCH3                                                 
         MVC   CFDOCH4,CTOUTCH4                                                 
         MVC   CFDOFCH,CTOUTFCH                                                 
         MVC   CFDOFMN,CTOUTFMN                                                 
         MVC   CFDOFMD,CTOUTFMD                                                 
         MVC   CFDOPGN,CTOUTPGN                                                 
         MVC   CFDOPGD,CTOUTPGD                                                 
         MVC   CFDOPQC,CTOUTPQC                                                 
BO7      LA    R3,L'CFDATA(R3)                                                  
         BCT   R5,BO2                                                           
         MVI   0(R3),X'FF'                                                      
         MVC   P(33),=C'OUTPUT TYPE BLOCK NEEDS EXPANDING'                      
         GOTOR BOTH                                                             
         MVC   P(28),=C'DONT PANIC - RUN IS GOING ON'                           
         GOTOR BOTH                                                             
*                                                                               
BOXIT    XIT1                                                                   
         DROP  R3                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*ROUTINE TO BUILD DESTINATION BLOCK                                   *         
***********************************************************************         
BUILDEST NTR1  BASE=*                                                           
         L     R2,=A(KEY)                                                       
         L     R3,=A(DESTBUFF)                                                  
         USING DESD,R3                                                          
         L     R5,=F'8000'         INCREASED FROM 6000                          
         USING CTIREC,R2                                                        
         XC    CTIKEY,CTIKEY                                                    
         MVI   CTIKTYP,C'I'                                                     
         GOTOR HIGH                                                             
         B     BD4                                                              
*                                                                               
BD2      GOTOR SEQ                                                              
*                                                                               
BD4      OC    CTIKID(8),CTIKID                                                 
         BZ    BD6                                                              
         MVI   0(R3),X'FF'                                                      
         B     BDXIT                                                            
*                                                                               
BD6      XC    0(DESLNQ,R3),0(R3)                                               
         MVI   ELCODE,CTOCOELQ     LOOK FOR OUTPUT TYPE ELEMENTS                
         LR    R4,R2                                                            
         GOTOR GETEL                                                            
         BNE   BD8                                                              
         USING CTOCOD,R4                                                        
         MVC   DESID,CTIKID+8                                                   
         MVC   DESOUT,CTOCODE                                                   
*                                                                               
BD8      MVI   ELCODE,CTSHPELQ     LOOK FOR SHIPPING UNIT                       
         LR    R4,R2                                                            
         GOTOR GETEL                                                            
         BNE   BD10                                                             
         MVC   DESID,CTIKID+8                                                   
         USING CTSHPD,R4                                                        
         MVC   DESSHP,CTSHPINS                                                  
*                                                                               
BD10     OC    0(DESLNQ,R3),0(R3)                                               
         BZ    BD2                                                              
         LA    R3,DESLNQ(R3)                                                    
         BCT   R5,BD2                                                           
         MVI   0(R3),X'FF'                                                      
*                                                                               
BDXIT    XIT1                                                                   
         DROP  R3                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*FIRST ROUTINE TO TRACE INPUT RECORDS                                 *         
***********************************************************************         
ITRC1    NTR1  BASE=*                                                           
         MVC   P(132),SPACES                                                    
         GOTO1 =V(PRINTER)                                                      
         MVC   P+00(EQULOK),0(R7)  ALPHA EOD JOB HDR                            
         MVC   P+32(4),=C'IN  '                                                 
         MVC   P+36(90),EQUSOC(R7) ALPHA EOD REQ CARD + A BIT OF 2              
         GOTO1 =V(PRINTER)                                                      
         MVC   P+00(EQULOK),0(R7)  ALPHA EOD JOB HDR                            
         MVC   P+32(4),=C'IN2 '                                                 
         MVC   P+36(90),EQUSOC+80(R7)                                           
         GOTO1 =V(PRINTER)                                                      
         MVC   P+00(EQULOK),0(R7)  ALPHA EOD JOB HDR                            
         MVC   P+32(4),=C'IN3 '                                                 
         MVC   P+36(90),EQUSOC+160(R7)                                          
         GOTO1 =V(PRINTER)                                                      
         GOTO1 =V(HEXOUT),PARA,(R2),P,4,=C'TOG'                                 
         GOTO1 =V(PRINTER)                                                      
*                                                                               
ITRC1H   CLI   ITRACETY,C'H'       TEST IF HEX REQUESTED                        
         BNE   ITRC1X                                                           
         L     RF,=A(REQINH1)                                                   
         GOTO1 =V(HEXOUT),PARA,(R7),(RF),322,=C'SEP'                            
         L     RF,=A(REQINH1)                                                   
         MVC   P+00(EQULOK),0(RF)  HEX EOD JOB HDR                              
         MVC   P+36(80),EQUSOC(RF) HEX EOD REQ CARD1                            
         CLC   EQUSOC+80(10,R7),SPACES                                          
         BE    *+10                                                             
         MVC   P+36+80(10),EQUSOC+80(RF) HEX REQ CARD2                          
         GOTO1 =V(PRINTER)                                                      
         L     RF,=A(REQINH2)                                                   
         MVC   P(EQULOK),0(RF)     HEX EOD JOB HDR                              
         MVC   P+36(80),EQUSOC(RF) HEX REQ REQ CARD1                            
         CLC   EQUSOC+80(10,R7),SPACES                                          
         BE    *+10                                                             
         MVC   P+36+80(10),EQUSOC+80(RF) HEX REQ CARD2                          
         GOTO1 =V(PRINTER)                                                      
*                                                                               
ITRC1X   XIT1                                                                   
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*SECOND ROUTINE TO TRACE INPUT RECORDS                                *         
***********************************************************************         
ITRC2    NTR1  BASE=*                                                           
         MVC   P+00(EQULOK),0(R7)  ALPHA EOD JOB HDR                            
         MVC   P+32(4),=C'OUT '                                                 
         MVC   P+36(80),EQUSOC(R7) ALPHA EOD REQ CARD                           
         GOTO1 =V(PRINTER)                                                      
         MVC   P+00(EQULOK),0(R7)  ALPHA EOD JOB HDR                            
         MVC   P+32(4),=C'OUT2'                                                 
         MVC   P+36(80),EQUSOC+80(R7)                                           
         GOTO1 =V(PRINTER)                                                      
         MVC   P+00(EQULOK),0(R7)  ALPHA EOD JOB HDR                            
         MVC   P+32(4),=C'OUT3'                                                 
         MVC   P+36(80),EQUSOC+160(R7)                                          
         GOTO1 =V(PRINTER)                                                      
         GOTO1 =V(HEXOUT),PARA,(R2),P,4,=C'TOG'                                 
         GOTO1 =V(PRINTER)                                                      
*                                                                               
ITRC2H   CLI   ITRACETY,C'H'                                                    
         BNE   ITRC2X                                                           
         L     RF,=A(REQINH1)                                                   
         GOTO1 =V(HEXOUT),PARA,(R7),(RF),322,=C'SEP'                            
         L     RF,=A(REQINH1)                                                   
         MVC   P+00(EQULOK),0(RF)  HEX EOD JOB HDR                              
         MVC   P+36(80),EQUSOC(RF) HEX EOD REQ CARD1                            
         CLC   EQUSOC+80(10,R7),SPACES                                          
         BE    *+10                                                             
         MVC   P+36+80(10),EQUSOC+80(RF) HEX REQ CARD2                          
         GOTO1 =V(PRINTER)                                                      
         L     RF,=A(REQINH2)                                                   
         MVC   P+00(EQULOK),0(RF)  HEX EOD JOB HDR                              
         MVC   P+36(80),EQUSOC(RF) HEX REQ REQ CARD1                            
         CLC   EQUSOC+80(10,R7),SPACES                                          
         BE    *+10                                                             
         MVC   P+36+80(10),EQUSOC+80(RF) HEX REQ CARD2                          
         GOTO1 =V(PRINTER)                                                      
*                                                                               
ITRC2X   XIT1                                                                   
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*ROUTINE TO TRACE OUTPUT RECORDS                                      *         
***********************************************************************         
OTRC     NTR1  BASE=*,WORK=(RC,30)                                              
         LR    R2,R1               R2=A(WORKER PARAMETER LIST)                  
         MVC   P,SPACES                                                         
         GOTO1 =V(PRINTER)                                                      
         L     RE,0(R2)                                                         
         MVC   P(3),0(RE)          WORKER ACTION                                
         L     RE,4(R2)                                                         
         MVC   P+4(8),22(RE)       WORKER FILE                                  
         L     RE,8(R2)                                                         
         MVC   P+13(8),0(RE)       WORKER INDEX                                 
         L     RE,12(R2)                                                        
         SR    R3,R3                                                            
         ICM   R3,3,0(RE)          R3=RECORD LENGTH                             
         CVD   R3,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  P+32(3),DUB         RECORD LENGTH                                
         LA    R4,4(RE)            R4=A(DATA)                                   
         AHI   R3,-4               R3=L'DATA                                    
         STM   R3,R4,DUB1          SAVE LEN AND ADR DATA                        
*                                                                               
OTRC1    LH    R0,LENREQR          SET R0=LEN OF REQUEST RECORD                 
         AHI   R0,-4                                                            
         CR    R3,R0               TEST DATA LENGTH GR MAX                      
         BNH   *+6                                                              
         LR    R3,R0               YES-SET MAX PRINTABLE LENGTH                 
         BCTR  R3,0                                                             
         EX    R3,*+8                                                           
         B     *+10                                                             
         MVC   P+36(0),0(R4)                                                    
         GOTO1 =V(PRINTER)                                                      
         CLI   OTRACETY,C'H'                                                    
         BNE   OTRC2                                                            
         LA    R3,1(R3)                                                         
         L     RE,4(R2)                                                         
         GOTO1 =V(HEXOUT),PARA,(R4),(RC),(R3),=C'SEP'                           
         BCTR  R3,0                                                             
         EX    R3,*+8                                                           
         B     *+10                                                             
         MVC   P+36(0),0(RC)                                                    
         GOTO1 =V(PRINTER)                                                      
         LA    RC,1(R3,RC)                                                      
         EX    R3,*+8                                                           
         B     *+10                                                             
         MVC   P+36(0),0(RC)                                                    
         BASR  RE,RF                                                            
*                                                                               
OTRC2    CLI   OTRACETY,C'F'       TEST FULL TRACE                              
         BNE   OTRCX                                                            
         L     R3,DUB1                                                          
         SR    R3,R0                                                            
         BNP   OTRCX                                                            
         L     R4,DUB1+4                                                        
         AR    R4,R0                                                            
         STM   R3,R4,DUB1                                                       
         B     OTRC1                                                            
*                                                                               
OTRCX    XIT1                                                                   
*                                                                               
         LTORG                                                                  
         DROP  RB                                                               
         EJECT                                                                  
***********************************************************************         
*ROUTINE TO CLOSE WORKER FILES AND PRINT TOTALS                       *         
***********************************************************************         
CLOSE    NTR1  BASE=*                                                           
         GOTO1 =V(WORKER),DMCB,=C'CLOSE',A(EODREQS)                             
         GOTO1 =V(WORKER),DMCB,=C'CLOSE',A(EODJOBS)                             
*                                                                               
         MVC   P,SPACES                                                         
         MVC   P(21),=C'       INPUT RECORDS   '                                
         UNPK  P(6),INPRECS                                                     
         OI    P+5,C'0'                                                         
         GOTO1 =V(LOGIO),PARA,1,(60,P)                                          
         GOTO1 =V(PRINTER)                                                      
         MVC   P(21),=C'       OUTPUT RECORDS '                                 
         UNPK  P(6),OUTRECS                                                     
         OI    P+5,C'0'                                                         
         GOTO1 =V(LOGIO),PARA,1,(60,P)                                          
         GOTO1 =V(PRINTER)                                                      
         MVC   P(21),=C'       JOB RECORDS    '                                 
         UNPK  P(6),JOBRECS                                                     
         OI    P+5,C'0'                                                         
         GOTO1 =V(LOGIO),PARA,1,(60,P)                                          
         GOTO1 =V(PRINTER)                                                      
         MVC   P(22),=C'       REQUEST RECORDS'                                 
         UNPK  P(6),REQRECS                                                     
         OI    P+5,C'0'                                                         
         GOTO1 =V(LOGIO),PARA,1,(60,P)                                          
         GOTO1 =V(PRINTER)                                                      
         MVC   P(22),=C'       RFHDR RECORDS  '                                 
         UNPK  P(6),RFHRECS                                                     
         OI    P+5,C'0'                                                         
         GOTO1 =V(LOGIO),PARA,1,(60,P)                                          
         GOTO1 =V(PRINTER)                                                      
*                                                                               
         CP    INPRECS,OUTRECS                                                  
         BE    CLOSEX                                                           
         MVC   P(24),=C'SORT ERROR - JOB DUMPING'                               
         GOTO1 =V(LOGIO),PARA,1,(60,P)                                          
         GOTO1 =V(PRINTER)                                                      
         MVC   P(24),=C'PLEASE RERUN END OF DAY '                               
         GOTO1 =V(LOGIO),PARA,1,(60,P)                                          
         GOTO1 =V(PRINTER)                                                      
         DC    H'0'                                                             
*                                                                               
CLOSEX   XIT1                                                                   
*                                                                               
         LTORG                                                                  
         DROP  RB                                                               
         EJECT                                                                  
***********************************************************************         
*ROUTINE TO SEE IF OUTPUT GOES DIRECT                                 *         
***********************************************************************         
ANYDIR   NTR1  BASE=*,LABEL=*                                                   
         MVI   IDIOTRCD,C' '                                                    
*&&UK                                                                           
ANYDIRA  CLI   QSYSTEM,C'A'        SPECIALS FOR ACCOUNT SYSTEM                  
         BNE   ANYDIRAX                                                         
         CLC   QPROGRAM,=C'55'     TEST CHECKS                                  
         BNE   ANYDIRAX                                                         
ANYDIRA2 CLC   STAKPRO1+38(3),=C'LSR'                                           
         BNE   ANYDIRAX                                                         
         B     ANYDIR10            WRAP CREATES DIRECT=CARD FOR THESE           
ANYDIRAX EQU   *                                                                
*&&                                                                             
*&&US                                                                           
ANYDIRA  CLI   QSYSTEM,C'A'        SPECIALS FOR ACCOUNT SYSTEM                  
         BNE   ANYDIRAX                                                         
         CLC   QPROGRAM,=C'55'     TEST CHECKS                                  
         BNE   ANYDIRAX                                                         
ANYDIRA1 CLC   STAKPRO1+38(7),=C'SHUTTLE'                                       
         B     ANYDIR10            WRAP CREATES DIRECT=CARD FOR THESE           
ANYDIRA2 CLC   STAKPROC+38(3),=C'LSR'                                           
         BNE   ANYDIRAX                                                         
         B     ANYDIR10            WRAP CREATES DIRECT=CARD FOR THESE           
ANYDIRAX EQU   *                                                                
*&&                                                                             
ANYDIR2  CLI   QCLASS,C'Q'         ANY OF THESE MEANS GOING DIRECT              
         BE    ANYDIR10                                                         
         CLI   STAKPTYP,C'Q'                                                    
         BE    ANYDIR10                                                         
*&&DO*&& CLI   STAKPTYP,C'B'                                                    
*&&DO*&& BE    ANYDIR10                                                         
         CLC   STAKPRO1(20),=CL20'PRINTING DIRECT'                              
         BE    ANYDIR10                                                         
         CLC   QOUTTYPE(3),=C'CDQ' CDROM VIA PQ                                 
         BE    ANYDIR10                                                         
         CLC   QOUTTYPE(2),=C'C$'  CDROM VIA PQ                                 
         BE    ANYDIR10                                                         
         CLI   QOUTTYPE,C'@'       SQL FORMULA                                  
         BE    ANYDIR10                                                         
         CLI   QOUTTYPE,C'/'       AFP REMOTE FORM CODE                         
         BE    ANYDIR10                                                         
         CLI   RPQOSID,C' '        OVERNIGHT SOON REPORT ID PRESENT             
         BH    ANYDIR10                                                         
*                                                                               
ANYDIR3  MVC   IDIOTOTY,STAKORUT   TEST FOR IDI OUTPUT TYPE OVERRIDE            
         MVC   IDIOTSYS,QSYSTEM                                                 
         MVC   IDIOTPRG,QPROGRAM                                                
         BAS   RE,IDIOT                                                         
         BNE   ANYDIRNO            EXIT IF NO IDI OVERRIDE                      
*                                                                               
ANYDIR10 DS    0H                  OUTPUT IS GOING DIRECT                       
ANYDIRYS CR    RB,RB               EXIT WITH CC EQL IF GOING DIRECT             
         B     ANYDIRX                                                          
ANYDIRNO LTR   RB,RB                                                            
ANYDIRX  J     XIT                                                              
*                                                                               
IDIOT    STM   RE,RF,IDIOTREF                                                   
         MVI   IDIOTRCD,C' '                                                    
IDIOT1   CLC   IDIOTOTY(2),=C'S1'  OUTPUT TYPE S1.... (MAX 4 SYS CHRS)          
         BNE   IDIOT2                                                           
         LA    RE,IDIOTOTY+2                                                    
         LA    RF,4                                                             
         B     IDIOT4                                                           
IDIOT2   CLI   IDIOTOTY,C'#'       OUTPUT TYPE #..... (MAX 5 SYS CHRS)          
         BNE   IDIOTX                                                           
         CLC   IDIOTOTY+1(5),SPACES                                             
         BE    IDIOT5                                                           
         LA    RE,IDIOTOTY+1                                                    
         LA    RF,5                                                             
IDIOT4   CLC   0(1,RE),IDIOTSYS    CHECK IF SYSTEM IN OUTPUT TYPE NAME          
         BE    IDIOT5                                                           
         LA    RE,1(RE)                                                         
         BCT   RF,IDIOT4                                                        
         B     IDIOTX                                                           
IDIOT5   LA    RF,IDIOTLST         CHECK FOR SYSTEM/PROGRAM EXCEPTIONS          
IDIOT5A  CLI   0(RF),C' '                                                       
         BE    IDIOT6                                                           
         CLC   0(3,RF),IDIOTSYS    IGNORE IF IN LIST                            
         BE    IDIOTX                                                           
         LA    RF,3(RF)                                                         
         B     IDIOT5A                                                          
*                                                                               
IDIOT6   EQU   *                                                                
*&&UK*&& CLI   STAKDISP,C'T'       IGNORE IDI ORIDE IF SPOOL TO TAPE            
*&&UK*&& BE    IDIOTX                                                           
*&&US                                                                           
         CLI   NPDOPT,C'Y'         TEST IF NO PRINT DATE IS ACTIVE              
         BNE   IDIOTQ                                                           
         CLC   TODPAK,STAKNPD      TEST TODAY WITH NO PRINT DATE                
         BL    IDIOTQ                                                           
         CLI   STAKPDD,C'Y'        TEST REPORT STILL PRINT@DDS                  
         BNE   IDIOTQ                                                           
         MVI   IDIOTRCD,C'@'       IGNORE IDI ORIDE IF PRINT@DDS                
         B     IDIOTX                                                           
*&&                                                                             
IDIOTQ   MVI   IDIOTRCD,C'Q'       SET IDI ORIDE FLAG                           
IDIOTX   CLI   IDIOTRCD,C'Q'       EXIT WITH CC EQL IF IDI OVERRIDE             
         L     RE,IDIOTREF                                                      
         BR    RE                                                               
*                                                                               
IDIOTREF DS    2F                                                               
IDIOTRCD DS    C                                                                
IDIOTOTY DS    CL6                                                              
IDIOTSYS DS    CL1                                                              
IDIOTPRG DS    CL2                                                              
*                                                                               
IDIOTLST DS    0C                                                               
*&&UK                                                                           
         DC    C'A07',C'A55',C'A77'                                             
         DC    C'M05',C'M31',C'M74',C'M75'                                      
*&&                                                                             
*&&US                                                                           
         DC    C'A55',C'AT5',C'A56'                                             
*&&                                                                             
IDIOTLSX DC    C' '                                                             
*                                                                               
         LTORG                                                                  
         DROP  RB                                                               
         EJECT                                                                  
         DS    0F                                                               
SRTCRD1  DC    CL80'SORT FIELDS=(5,256,A),FORMAT=BI,WORK=1'                     
SRTCRD2  DC    CL80'RECORD TYPE=V,LENGTH=(1380,,,,)'                            
                                                                                
***********************************************************************         
*SYSTEM TABLE CL1=ONE CHR ID,CL2=TWO CHR ID,CL8=FULL SYSTEN NAME      *         
*CL3=CODES FOR SYSTEM ID FOR DUPLICATE JOBS - 1ST CHR < TO CAUSE JOB  *         
*NAMES TO RESTART FROM A. PLUS TWO EXTRA ALTERNATE SYSTEM LETTERS.    *         
***********************************************************************         
SYSTAB   DS    0CL17                                                            
         DC    CL11'AACACCPAK  ',C' GHI  '                                      
         DC    CL11'BMBMBASE   ',C'      '                                      
         DC    CL11'CCTCONTROL ',C'      '                                      
         DC    CL11'EPEPERSON  ',C'      '                                      
*&&UK*&& DC    CL11'LMPMPL     ',C'<     '                                      
*&&US*&& DC    CL11'LMPMPL     ',C'      '                                      
*&&US*&& DC    CL11'DCPCPP     ',C'      '                                      
*&&US*&& DC    CL11'SSPSPOTPAK ',C' UVWXY'                                      
*&&US                                                                           
SYSNET   DC    CL11'NNENETPAK  ',C' OQKLM'                                      
*&&                                                                             
*&&US*&& DC    CL11'PPRPRINTPAK',C' Q    '                                      
*&&US*&& DC    CL11'RREREPPAK  ',C'      '                                      
*&&US*&& DC    CL11'TTATALENT  ',C'      '                                      
*&&US*&& DC    CL11'FTRSTRAFFIC',C'      '                                      
*&&UK*&& DC    CL11'MMEMEDLINE ',C' NOQRS'                                      
*&&UK*&& DC    CL11'FFEFEES    ',C'      '                                      
SYSTABX  DC    CL11' XXUNKNOWN ',C'      '                                      
*                                                                               
*&&US                                                                           
ABSPEC   DC    C'AB ALL BORO''S MESSENGER DELIVERS TO JWNY EXCEPT ON '          
         DC    C'WEEKENDS WHEN REPORTS ARE SENT BY AIRMAIL'                     
         DC    CL64' '                                                          
XASPEC   DC    CL150'XA EXPRESS MAIL P.O. TO ADDRESSEE'                         
*&&                                                                             
*                                                                               
EQULOK   EQU   30                  LENGTH OF KEY                                
EQULOL   EQU   66                  LENGTH OF AREA AFTER KEY                     
EQUSOC   EQU   96                  START OF REQUEST CARD                        
*                                                                               
EODREQS  DMWK  BUFFER=WKRQBUF                                                   
*                                                                               
EODJOBS  DMWK  BUFFER=WKRJBUF                                                   
*                                                                               
EODSORT  DCB   DDNAME=EODSORT,DSORG=PS,MACRF=GM,EODAD=EOF                       
         EJECT                                                                  
***********************************************************************         
*EXTERNAL BUFFERS                                                     *         
***********************************************************************         
*                                  EOD REQUEST RECORD FOR SORT IO               
         DS    0F                                                               
REQINRL  DS    H                   RECORD LENGTH                                
         DS    H                                                                
*                                  DATA + RFHDR EXTRACT AND EXTENSIONS          
REQIN    DS    XL(QRECLEN+L'QREQINFO+6+8)                                       
*                                                                               
*                                  TRACE DISPLAY BUFFERS                        
REQINH1  DS    CL256                                                            
REQINHX1 DS    CL66                                                             
REQINH2  DS    CL256                                                            
REQINHX2 DS    CL66                                                             
*                                                                               
EMULIST  DC    XL48'00'                                                         
*                                                                               
         DC    0D                                                               
         DC    CL8'SSB     '                                                    
SSB      DS    0X                                                               
         PRINT OFF                                                              
       ++INCLUDE FASSBOFF                                                       
         PRINT ON                                                               
         ORG   SSB                                                              
         DC    (SSOOFFX-SSOOFF)X'00'                                            
         ORG   SSOXTND                                                          
         DC    X'FF'               SET EXTENDED OFFLINE SSB                     
         DC    AL1(SSOSNRCV)       SET RECOVERY OFF                             
         ORG                                                                    
*                                                                               
         DC    0D                                                               
         DC    CL8'UTL     '                                                    
UTL      DC    F'0',X'0A',251X'00'                                              
*                                                                               
         DC    0D                                                               
         DC    CL8'KEY     '                                                    
KEY      DS    1000C                                                            
*                                                                               
         DC    0D                                                               
         DC    CL8'ACCBUFF '                                                    
ACCBUFF  DS    1000C                                                            
*                                                                               
         DC    0D                                                               
         DC    CL8'OUTBLOCK'                                                    
OUTBLOCK DS    4000CL80            INCREASED FROM 2500                          
OUTBLK#  EQU   (*-OUTBLOCK)/80                                                  
*                                                                               
         DC    0D                                                               
         DC    CL8'PROGBUFF'                                                    
PROGBUFF DS    1000C                                                            
*                                                                               
         DC    0D                                                               
         DC    CL8'OVERBUFF'                                                    
OVERBUFF DS    1000C                                                            
*                                                                               
         DC    0D                                                               
         DC    CL8'XTRABUFF'                                                    
XTRABUFF DS    1000C                                                            
*                                                                               
         DC    0D                                                               
         DC    CL8'ORIGBUFF'                                                    
ORIGBUFF DS    2000C                                                            
*                                                                               
         DC    0D                                                               
         DC    CL8'DUPBUFF '                                                    
DUPBUFF  DC    F'3000'             MAX NUMBER OF ENTRIES                        
         DC    F'0'                CUR NUMBER OF ENTRIES                        
         DS    3000CL10            CL8 JOB NAME, XL2 JOB DUP COUNT              
*                                                                               
         DC    0D                                                               
         DC    CL8'ATTNBUFF'                                                    
ATTNBUFF DC    F'40'                                                            
         DC    1800X'00'                                                        
*                                                                               
         DC    0D                                                               
         DC    CL8'WKRQBUFF'                                                    
WKRQBUF  DS    60000C                                                           
*                                                                               
         DC    0D                                                               
         DC    CL8'WKRJBUFF'                                                    
WKRJBUF  DS    60000C                                                           
*                                                                               
         DC    0D                                                               
         DC    CL8'DESTBUFF'                                                    
DESTBUFF DS    8000CL10            INCREASED FROM 6000                          
         DS    CL10                                                             
*                                                                               
         DC    0D                                                               
         DC    CL8'REQWRK  '                                                    
REQWRK   DS    2000D                                                            
*                                                                               
         DC    0D                                                               
         EJECT                                                                  
         EJECT                                                                  
***********************************************************************         
*DSECT TO MEDIA LETTERS AND NAMES                                     *         
***********************************************************************         
MEDTABD  DSECT                                                                  
MEDCODE  DS    CL1                                                              
MEDNAME  DS    CL11                                                             
MEDTLNQ  EQU   *-MEDTABD                                                        
                                                                                
***********************************************************************         
*DSECT TO COVER DESTINATION BUFFER                                    *         
***********************************************************************         
DESD     DSECT                                                                  
DESID    DS    XL2                 ID NUMBER                                    
DESOUT   DS    CL6                 OUTPUT TYPE                                  
DESSHP   DS    CL2                 SHIPPING INSTRUCTIONS                        
DESLNQ   EQU   *-DESD                                                           
                                                                                
***********************************************************************         
*DSECT TO COVER SAVED CONTROL FILE DATA                               *         
***********************************************************************         
CFDD     DSECT                                                                  
CFDATA   DS    0XL80                                                            
CFDOID   DS    CL6                                                              
CFDOCLS  DS    CL(L'CTOUTCLS)      OUTPUT CLASS                                 
CFDOPRI  DS    CL1                 OUTPUT PRIORITY                              
CFDOPOW  DS    CL(L'CTOUTPOW)      POWER CODE (USED IN FNO= )                   
CFDOCC   DS    CL(L'CTOUTCC)       CARRIAGE CONTROL TAPE                        
CFDOCPY  DS    CL(L'CTOUTCPY)      NUMBER OF COPIES (1-9) FOR POWER LST         
CFDODIS  DS    CL(L'CTOUTDIS)      OUTPUT DISPOSITION                           
CFDOSEP  DS    CL(L'CTOUTSEP)      SEPERATORS (N=NO)                            
CFDOTAP  DS    CL(L'CTOUTTAP)      OUTPUT TAPE DETAILS                          
CFDOTYP  DS    CL(L'CTOUTTYP)      OUTPUT TYPE - P=PRINT,Q=QUEUE,B=BOTH         
CFDOCHR  DS    CL(L'CTOUTCHR)      CHARS= VALUE 1                               
CFDOCH2  DS    CL(L'CTOUTCH2)      CHARS= VALUE 2                               
CFDOCH3  DS    CL(L'CTOUTCH3)      CHARS= VALUE 3                               
CFDOCH4  DS    CL(L'CTOUTCH4)      CHARS= VALUE 4                               
CFDOFCH  DS    CL(L'CTOUTFCH)      FICHE CLASS LETTER                           
CFDOFMN  DS    CL(L'CTOUTFMN)      NUMBER OF FORMDEFS                           
CFDOFMD  DS    CL(L'CTOUTFMD)      FORMDEF= VALUE                               
CFDOPGN  DS    CL(L'CTOUTPGN)      NUMBER OF PAGEDEFS                           
CFDOPGD  DS    CL(L'CTOUTPGD)      PAGEDEF= VALUE                               
CFDOPQC  DS    CL(L'CTOUTPQC)      PRINT QUEUE CLASS OVERRIDE                   
         DS    XL23                SPARE                                        
         ORG   CFDATA                                                           
         EJECT                                                                  
***********************************************************************         
*INCLUDED SYSTEM DSECTS                                               *         
***********************************************************************         
*EDREQD                                                                         
       ++INCLUDE EDREQD                                                         
         EJECT                                                                  
*DDCNTRL                                                                        
       ++INCLUDE DDCNTRL                                                        
         EJECT                                                                  
*DDDPRINT                                                                       
       ++INCLUDE DDDPRINT                                                       
         EJECT                                                                  
*DMDDNAMED                                                                      
DDNAD    DSECT                                                                  
       ++INCLUDE DMDDNAMED                                                      
         EJECT                                                                  
*DMDTFIS                                                                        
       ++INCLUDE DMDTFIS                                                        
         EJECT                                                                  
*CTGENFILE                                                                      
         PRINT OFF                                                              
       ++INCLUDE CTGENFILE                                                      
         PRINT ON                                                               
*ACGENFILE                                                                      
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
         PRINT ON                                                               
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'008EDREQSORT 04/19/19'                                      
         END                                                                    
