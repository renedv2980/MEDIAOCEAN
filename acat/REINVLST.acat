*          DATA SET REINVLST   AT LEVEL 052 AS OF 11/07/86                      
*CATALP INVLST                                                                  
         TITLE 'BUILD LIST OF INVENTORY NUMBERS AND WEIGHT'                     
*                                                                               
*              PARA 1    BYTE 1-3  A(FIELD HEADER)                              
*              PARA 2    BYTE 0    (OPTIONAL) MAX NUMBER OUTPUT LINES           
*                        BYTE 1-3  A(AN AVAILABLE WORK AREA)                    
*              PARA 3    BYTE 1-3  A(RC)                                        
*                                                                               
*                        INVNO = NO OF OUTPUT LINES                             
*                        INVNO = ZERO INDICATES ERROR                           
*                                                                               
*                        DEMEDIA   SET TO MEDIA AND                             
*                        DEMSTA    STATION OVERRIDE                             
*                                                                               
         EJECT                                                                  
INVLST   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,INVLST                                                         
         L     RC,8(R1)                                                         
         USING GENOLD,RC                                                        
         USING INVLD,R4                                                         
         SPACE 1                                                                
         MVI   INVNO,0                                                          
         MVI   FERN,2              PRESET ERROR MESSAGE NUMBER                  
         L     R4,INVLIST          OUTPUT AREA                                  
         XC    INVLREC,INVLREC                                                  
         MVC   INVLFLE,INVTYP                                                   
         SPACE 1                                                                
         LM    R2,R3,0(R1)         A(FIELD HEADER),  A(WORK AREA)               
         XR    R5,R5                                                            
         SPACE 1                                                                
         USING COMFACSD,R7                                                      
         L     R7,ACOMFACS                                                      
         GOTO1 CSCANNER,DMCB,(R2),((R5),(R3))                                   
         CLI   DMCB+4,0                                                         
         BE    INVERR                                                           
         SPACE 1                                                                
         XR    R7,R7                                                            
         IC    R7,DMCB+4           NUMBER OF INPUT FIELDS                       
         SPACE 1                                                                
PREFIX   OI    INVLTYP,X'08'                                                    
         CLI   12(R3),C'+'         ADD EXPRESSION                               
         BE    PREFIX2                                                          
         NI    INVLTYP,X'FF'-X'08' TURN OFF ADD BIT                             
         CLI   12(R3),C'/'         FOOTNOTE SUPPRESSION                         
         BNE   STATION                                                          
         MVI   TRFNOVER,C'Y'       YES-SET A SWITCH                             
         SPACE 1                                                                
PREFIX2  XR    R6,R6               REMOVE PREFIX CHARACTER                      
         IC    R6,0(R3)            AND DECREMENT BLOCK LENGTH                   
         BCTR  R6,0                                                             
         STC   R6,0(R3)                                                         
         BCTR  R6,0                                                             
         EX    R6,*+8                                                           
         B     *+10                                                             
         MVC   12(0,R3),13(R3)     SHIFT LEFT                                   
         LA    R6,13(R6,R3)                                                     
         MVI   0(R6),C' '          CLEAR LAST                                   
         SPACE 1                                                                
STATION  XR    R5,R5                                                            
         IC    R5,0(R3)            LENGTH OF DAY EXPRESSION                     
         GOTO1 VINVDAY,DMCB,((R5),12(R3)),DUB,DUB+1,VDAYVAL                     
         CLI   DUB,0                                                            
         BNE   DAYTIM2             FIRST IS A DAY                               
         SPACE 1                                                                
         GOTO1 VPAVSTA,DMCB,12(R3),DMCB+8    IS FIRST A STATION                 
         CLI   DMCB+4,X'FF'                                                     
         BE    NUM                 NOT A STATION                                
         MVC   DEMSTA,DMCB+8            SAVE STATION FOR GET DEMOS              
         MVC   DEMEDIA,DMCB+13           MEDIA                                  
         CH    R7,=H'2'                                                         
         BL    INVERR                                                           
         B     INVSKIP                                                          
         EJECT                                                                  
*              EDIT INVENTORY NUMBER                                            
*                                                                               
NUM      XR    R5,R5                                                            
         MVC   INVLFLE,INVTYP                                                   
         IC    R5,0(R3)                                                         
         CH    R5,=H'4'            INVENTORY NUMBER MUST BE 3 OR 4              
         BH    DAYTIM                                                           
         CH    R5,=H'3'                                                         
         BL    DAYTIM                                                           
         SPACE 1                                                                
         CLI   12(R3),C'0'         QTR  HOUR NUMBER MUST BE 00- 99              
         BL    DAYTIM                                                           
         CLI   12(R3),C'9'                                                      
         BH    DAYTIM                                                           
         CLI   13(R3),C'0'                                                      
         BL    DAYTIM                                                           
         CLI   13(R3),C'9'                                                      
         BH    DAYTIM                                                           
         SPACE 1                                                                
         PACK  DUB(8),12(2,R3)                                                  
         CVB   R0,DUB                                                           
         STC   R0,INVLNUMB            QTR HOUR NUMBER                           
         CLI   14(R3),C'D'         TYPICAL                                      
         BE    NUM2                                                             
         CLI   14(R3),C'E'         WEEKEND                                      
         BE    NUM2                                                             
         CLI   14(R3),C'0'                                                      
         BL    INVERR                                                           
         CLI   14(R3),C'9'                                                      
         BH    INVERR                                                           
         SPACE 1                                                                
NUM2     MVC   INVLNUMB+1(1),14(R3)      DAY CODE                               
         CLI   INVLFLE,C'I'                                                     
         BE    NUM4                                                             
         GOTO1 VINVDAY,DMCB,1,(0,14(R3)),(0,INVLNUMB+1)  PAV DAY CODE           
         SPACE 1                                                                
         CH    R5,=H'3'            PURE  3 CHARACTER INPUT                      
         BE    NUM6               IS OK                                         
         SR    RF,RF                                                            
         IC    RF,15(R3)           START WEEK                                   
         XR    RE,RE                                                            
         SLDL  RE,28               RE 4 HIGH ORDER                              
         SRL   RF,28               RF 4 LOW ORDER                               
         CH    RF,=H'7'                                                         
         BH    INVERR              0-7 , A-G                                    
         SLL   RF,1                                                             
         EX    RF,*+8              BITES 4-6                                    
         B     *+8                                                              
         OI    INVLNUMB+1,0                                                     
         SPACE 1                                                                
         CH    RE,=H'15'                                                        
         BNE   *+14                NOT NUMERIC                                  
         LTR   RF,RF                                                            
         BZ    *+16                INPUT ZERO                                   
         B     NUM6                                                             
         SPACE 1                                                                
         CH    RE,=H'12'                                                        
         BNE   INVERR              NOT A-G                                      
         OI    INVLNUMB+1,1        LOW ORDER ON FOR ZERO AND A-G                
         B     NUM6                                                             
         SPACE 1                                                                
NUM4     MVI   INVLNUMB+2,C'0'      DEFAULT LENGTH                              
         CH    R5,=H'3'                                                         
         BE    *+10                                                             
         MVC   INVLNUMB+2(1),15(R3) LENGTH OR SPECIAL INPUT CODE                
         SPACE 1                                                                
NUM6     OI    INVLTYP,X'80'         INPUT WAS AN INVENTORY NUMBER              
         MVI   INVLWT,1             WEIGHT                                      
         SPACE 1                                                                
         CLI   1(R3),0             NO WEIGHT                                    
         BE    DATE                                                             
         OC    8(4,R3),8(R3)                                                    
         BZ    INVERR              WEIGHT NOT NUMERIC                           
         CLC   8(4,R3),=F'99'                                                   
         BH    INVERR              99 SHOULD BE ENOUGH                          
         MVC   INVLWT,11(R3)      WEIGHT TO LIST                                
         OI    INVLTYP,X'04'       USER WEIGHTING OVERRIDE                      
         SPACE 1                                                                
DATE     CLI   INVLFLE,C'P'                                                     
         BE    INVNEXT                                                          
         CH    R7,=H'2'            DO I HAVE ONE LEFT                           
         BL    INVNEXT             NO, SO GET OUT                               
         LA    R6,32(R3)           NEXT SCAN AREA                               
*                                                                               
DATE1    GOTO1 VDATVAL,DMCB,(0,12(R6)),DUB                                      
         OC    DMCB,DMCB                                                        
         BZ    INVNEXT                                                          
         CLI   1(R6),0             MUST BE A DATE ENTRY                         
         BE    *+12                                                             
         MVI   FERN,INCFORM        INVALID ENTRY FORMULA                        
         B     INVERR                                                           
         SPACE 1                                                                
         LA    R3,32(R3)                                                        
         BCTR  R7,0                                                             
         GOTO1 VDATCON,DMCB,(0,DUB),(3,INVLDATE)                                
         B     INVNEXT                                                          
         EJECT                                                                  
*              EDIT A DAY/TIME EXPRESSION                                       
*                                                                               
DAYTIM   XR    R5,R5                                                            
         IC    R5,0(R3)            LENGTH OF DAY EXPRESSION                     
         GOTO1 VINVDAY,DMCB,((R5),12(R3)),DUB,DUB+1,VDAYVAL                     
         CLI   DUB,0                                                            
         BE    INVERR              NOT A VALID DAY                              
DAYTIM2  CLI   1(R3),0                                                          
         BNE   INVERR              DAY= IS INVALID                              
         SPACE 1                                                                
         CLI   INVLFLE,C'I'                                                     
         BE    DAYTIM6                                                          
         GOTO1 VDAYVAL,DMCB,((R5),12(R3)),INVLDAY,DUB                           
         OI    INVLTYP,X'60'                                                    
         SPACE 1                                                                
DAYTIM4  BCTR  R7,0                                                             
         LTR   R7,R7                                                            
         BZ    INVERR                                                           
         LA    R3,32(R3)      CHECK FOR MULTIPLE DAYS                           
         IC    R5,0(R3)                                                         
         GOTO1 VDAYVAL,DMCB,((R5),12(R3)),DUB,DUB+1                             
         CLI   DUB,0          NOT A DAY                                         
         BE    DAYTIM8                                                          
         OC    INVLDAY,DUB                                                      
         B     DAYTIM4                                                          
         SPACE 1                                                                
DAYTIM6  BCTR  R7,0                NOW I EXPECT TO FIND A TIME FIELD            
         LTR   R7,R7                                                            
         BZ    INVERR              BUT THERE IS NO MORE INPUT                   
         LA    R3,32(R3)           R3 TO TIME FIELD                             
         XR    R5,R5                                                            
         IC    R5,0(R3)            LENGTH OF THE TIME EXPRESSION                
         SPACE 1                                                                
DAYTIM8  GOTO1 VTIMVAL,DMCB,((R5),12(R3)),DUB+4                                 
         CLI   DMCB,X'FF'                                                       
         BE    INVERR              INVALID TIME                                 
         SPACE 1                                                                
         CLC   DUB+6(2),=H'2400'                                                
         BH    INVERR              DO NOT ACCEPT -CC, NONE, OR VARY             
         SPACE 1                                                                
         CLI   INVLFLE,C'P'                                                     
         BNE   *+14                                                             
         MVC   INVLSTIM(4),DUB+4       START/END TIME FOR PURE                  
         B     INVNEXT                                                          
         SPACE 1                                                                
         GOTO1 VHRTOQH,DMCB,DUB+4,FULL                                          
         XR    R5,R5                                                            
         IC    R5,FULL           START QTR HR CODE                              
         STC   R5,INVLNUMB            QTR HOUR CODE                             
         MVC   INVLNUMB+1(1),DUB        INV DAY CODE                            
         MVI   INVLNUMB+2,C'0'          LENGTH                                  
         SPACE 1                                                                
         MVI   INVLWT,1             WEIGHT                                      
         OI    INVLTYP,X'60'         FIRST AND LAST                             
         SPACE 1                                                                
         OC    DUB+6(2),DUB+6                                                   
         BZ    INVNEXT             NO END TIME                                  
         SPACE 1                                                                
         GOTO1 VHRTOQH,DMCB,DUB+6,FULL  END QTR HOUR                            
         XR    R9,R9                                                            
         IC    R9,FULL           END QTR CODE                                   
         SR    R9,R5               END MINUS START                              
         BCTR  R9,0                                                             
         LTR   R9,R9                                                            
         BNP   INVNEXT             THIS SHOULD NEVER HAPPEN                     
         NI    INVLTYP,X'4F'         TURN OFF LAST                              
         SPACE 1                                                                
DAYTIM10 MVC   10(10,R4),0(R4)       NEXT OUTPUT AREA                           
         LA    R4,10(R4)                                                        
         SPACE 1                                                                
         AH    R5,=H'1'           NEXT QTR HOUR                                 
         STC   R5,INVLNUMB                                                      
         NI    INVLTYP,X'0F'                                                    
         BCT   R9,DAYTIM10                                                      
         OI    INVLTYP,X'20'         LAST IN LIST                               
         EJECT                                                                  
INVNEXT  LA    R4,10(R4)            NEXT OUTPUT AREA                            
         XC    0(10,R4),0(R4)                                                   
         SPACE 1                                                                
INVSKIP  LA    R3,32(R3)           NEXT SCAN AREA                               
         BCT   R7,NUM                                                           
         SPACE 1                                                                
         L     R4,INVLIST            OUTPUT AREA                                
         XR    R5,R5                                                            
         OC    0(10,R4),0(R4)       LOOK FOR END                                
         BZ    *+16                                                             
         LA    R5,1(R5)                                                         
         LA    R4,10(R4)                                                        
         B     *-18                                                             
         SPACE 1                                                                
         STC   R5,INVNO            RETURN NUMBER OF ITEMS                       
EXXMOD   XMOD1 1                                                                
         SPACE 2                                                                
INVERR   MVI   INVNO,0                                                          
         B     EXXMOD                                                           
*                                                                               
         EJECT                                                                  
       ++INCLUDE RGENOLD                                                        
       ++INCLUDE RELFMWRK                                                       
         EJECT                                                                  
*              INVENTORY LIST DSECT                                             
INVLD    DSECT                                                                  
INVLREC  DS    0CL10                                                            
INVLFLE  DS    CL1                 P=PAV, I=INVENTORY                           
INVLTYP  DS    CL1                 X'80'  INVENTORY NUMBER                      
*                                  X'40'  FIRST IN DAY/TIME EXP.                
*                                  X'20'  LAST IN DAY/TIME  EXP.                
*                                  X'08'  ADD EXPRESSION                        
*                                  X'04'  WEIGHTING OVERRIDE                    
INVLWT   DS    CL1                 WEIGHT (BINARY)                              
INVLDATA DS    0CL6                                                             
INVLSTIM DS    CL2                 START TIME (PAV ONLY)                        
INVLETIM DS    CL2                 END TIME   (PAV ONLY)                        
INVLDAY  DS    CL1                 DAY        (PAV ONLY)                        
         DS    CL1                 SPARE                                        
         ORG   INVLDATA                                                         
INVLNUMB DS    CL3                 NUMBER                                       
INVLDATE DS    CL3                 START DATE (Y/M/D BINARY)                    
         DS    CL1                 SPARE                                        
         SPACE 2                                                                
       ++INCLUDE DDCOMFACS                                                      
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'052REINVLST  11/07/86'                                      
         END                                                                    
