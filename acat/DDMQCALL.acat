*          DATA SET DDMQCALL   AT LEVEL 003 AS OF 04/02/98                      
*CATALP MQCALL                                                                  
         TITLE 'MQCALL  - MQ SERIES CALL INTERFACE'                             
**********************************************************************          
* MQCALL - MQ SERIES CALL INTERFACE                                 *           
* PARAMETERS:                                                        *          
*                                                                    *          
* ENTRY:                                                             *          
* AL4 =A(MQCALLD CSECT)                                              *          
**********************************************************************          
         SPACE 1                                                                
         PRINT NOGEN                                                            
MQCALL   CSECT                                                                  
         NMOD1 WORKX-WORKD,**MQCL**,CLEAR=YES,RA,R9                             
         USING WORKD,RC                                                         
         ST    R1,APARM                                                         
         B     INIT                                                             
         SPACE 1                                                                
**********************************************************************          
* INITIALISE                                                         *          
**********************************************************************          
         SPACE 1                                                                
INIT     EQU   *                                                                
         MVC   PARM,0(R1)                                                       
         L     R8,0(R1)                                                         
         USING MQCALLD,R8                                                       
         BZ    ERROR01                                                          
         B     PROCESS                                                          
*                                                                               
EXIT     L     R1,APARM            EXIT WITH UPDATED PARAMETER LIST             
         MVC   0(L'PARM,R1),PARM                                                
         XMOD1 1                                                                
         EJECT                                                                  
**********************************************************************          
* MAIN PROCESS ROUTINE                                               *          
**********************************************************************          
         SPACE 1                                                                
PROCESS  EQU   *                                                                
*                                                                               
         OC    MQACTION,MQACTION                                                
         BZ    PROCINIT                                                         
         CLC   MQACTION,=CL8'CONNECT '                                          
         BE    PROCCONN                                                         
         CLC   MQACTION,=CL8'OPEN    '                                          
         BE    PROCOPEN                                                         
         CLC   MQACTION,=CL8'GET     '                                          
         BE    PROCGET                                                          
         CLC   MQACTION,=CL8'PUT     '                                          
         BE    PROCPUT                                                          
         CLC   MQACTION,=CL8'CLOSE   '                                          
         BE    PROCCLOS                                                         
         CLC   MQACTION,=CL8'DISCONN '                                          
         BE    PROCDISC                                                         
         CLC   MQACTION,=CL8'COMMIT  '                                          
         BE    PROCCMIT                                                         
         DC    H'0'                UNKNOWN ACTION CODE                          
**********************************************************************          
* INITIALISATION                                                     *          
**********************************************************************          
         SPACE 1                                                                
PROCINIT EQU   *                                                                
*                                                                               
         BAS   RE,INITCONN                                                      
         BAS   RE,INITOPEN                                                      
         BAS   RE,INITGET                                                       
         BAS   RE,INITPUT                                                       
         BAS   RE,INITCLOS                                                      
         BAS   RE,INITDISC                                                      
         BAS   RE,INITCMIT                                                      
*                                                                               
         B     RETURN                                                           
         EJECT                                                                  
**********************************************************************          
* PROCESS CONNECT ACTION CALL                                        *          
**********************************************************************          
         SPACE 1                                                                
PROCCONN EQU   *                                                                
         LA    RE,MQCALL_STRUCTURE                                              
         L     R0,=A(CONNECT_STRUCTURE)                                         
         LA    R1,L'MQCALL_STRUCTURE                                            
         LR    RF,R1                                                            
         MVCL  RE,R0                                                            
         LA    RE,MQCALL_STRUCTURE                                              
         LA    RE,20(RE)                                                        
         LA    RF,QMGRNAME                                                      
         ST    RF,0(RE)                                                         
         LA    RE,4(RE)                                                         
         LA    RF,HCONN                                                         
         ST    RF,0(RE)                                                         
         LA    RE,4(RE)                                                         
         LA    RF,MQCOMPCODE                                                    
         ST    RF,0(RE)                                                         
         LA    RE,4(RE)                                                         
         LA    RF,MQREASON                                                      
         ST    RF,0(RE)                                                         
         OI    0(RE),X'80'                                                      
         GOTO1 CALLMQ,MQCALL_STRUCTURE                                          
         B     RETURN                                                           
         EJECT                                                                  
**********************************************************************          
* PROCESS OPEN ACTION CALL                                           *          
**********************************************************************          
         SPACE 1                                                                
PROCOPEN EQU   *                                                                
         LA    RE,MQCALL_STRUCTURE                                              
         L     R0,=A(OPEN_STRUCTURE)                                            
         LA    R1,L'MQCALL_STRUCTURE                                            
         LR    RF,R1                                                            
         MVCL  RE,R0                                                            
         LA    RE,MQCALL_STRUCTURE                                              
         LA    RE,20(RE)                                                        
         LA    RF,HCONN                                                         
         ST    RF,0(RE)                                                         
         LA    RE,4(RE)                                                         
         LA    RF,OBJDESC                                                       
         ST    RF,0(RE)                                                         
         LA    RE,4(RE)                                                         
         LA    RF,MQOPTIONS                                                     
         ST    RF,0(RE)                                                         
         LA    RE,4(RE)                                                         
         LA    RF,HOBJ                                                          
         ST    RF,0(RE)                                                         
         LA    RE,4(RE)                                                         
         LA    RF,MQCOMPCODE                                                    
         ST    RF,0(RE)                                                         
         LA    RE,4(RE)                                                         
         LA    RF,MQREASON                                                      
         ST    RF,0(RE)                                                         
         OI    0(RE),X'80'                                                      
         GOTO1 CALLMQ,MQCALL_STRUCTURE                                          
         B     RETURN                                                           
         EJECT                                                                  
**********************************************************************          
* PROCESS GET ACTION CALL                                            *          
**********************************************************************          
         SPACE 1                                                                
PROCGET  EQU   *                                                                
         LA    RE,MQCALL_STRUCTURE                                              
         L     R0,=A(GET_STRUCTURE)                                             
         LA    R1,L'MQCALL_STRUCTURE                                            
         LR    RF,R1                                                            
         MVCL  RE,R0                                                            
         LA    RE,MQCALL_STRUCTURE                                              
         LA    RE,20(RE)                                                        
         LA    RF,HCONN                                                         
         ST    RF,0(RE)                                                         
         LA    RE,4(RE)                                                         
         LA    RF,HOBJ                                                          
         ST    RF,0(RE)                                                         
         LA    RE,4(RE)                                                         
         LA    RF,MSGDESC                                                       
         ST    RF,0(RE)                                                         
         LA    RE,4(RE)                                                         
         LA    RF,GETMSGOPTS                                                    
         ST    RF,0(RE)                                                         
         LA    RE,4(RE)                                                         
         LA    RF,MQBUFFERLEN                                                   
         ST    RF,0(RE)                                                         
         LA    RE,4(RE)                                                         
         ICM   RF,15,AMQBUFFER                                                  
         ST    RF,0(RE)                                                         
         LA    RE,4(RE)                                                         
         LA    RF,MQDATALEN                                                     
         ST    RF,0(RE)                                                         
         LA    RE,4(RE)                                                         
         LA    RF,MQCOMPCODE                                                    
         ST    RF,0(RE)                                                         
         LA    RE,4(RE)                                                         
         LA    RF,MQREASON                                                      
         ST    RF,0(RE)                                                         
         OI    0(RE),X'80'                                                      
         GOTO1 CALLMQ,MQCALL_STRUCTURE                                          
         B     RETURN                                                           
         EJECT                                                                  
**********************************************************************          
* PROCESS PUT ACTION CALL                                            *          
**********************************************************************          
         SPACE 1                                                                
PROCPUT  EQU   *                                                                
         LA    RE,MQCALL_STRUCTURE                                              
         L     R0,=A(PUT_STRUCTURE)                                             
         LA    R1,L'MQCALL_STRUCTURE                                            
         LR    RF,R1                                                            
         MVCL  RE,R0                                                            
         LA    RE,MQCALL_STRUCTURE                                              
         LA    RE,20(RE)                                                        
         LA    RF,HCONN                                                         
         ST    RF,0(RE)                                                         
         LA    RE,4(RE)                                                         
         LA    RF,HOBJ                                                          
         ST    RF,0(RE)                                                         
         LA    RE,4(RE)                                                         
         LA    RF,MSGDESC                                                       
         ST    RF,0(RE)                                                         
         LA    RE,4(RE)                                                         
         LA    RF,PUTMSGOPTS                                                    
         ST    RF,0(RE)                                                         
         LA    RE,4(RE)                                                         
         LA    RF,MQBUFFERLEN                                                   
         ST    RF,0(RE)                                                         
         LA    RE,4(RE)                                                         
         ICM   RF,15,AMQBUFFER                                                  
         ST    RF,0(RE)                                                         
         LA    RE,4(RE)                                                         
         LA    RF,MQCOMPCODE                                                    
         ST    RF,0(RE)                                                         
         LA    RE,4(RE)                                                         
         LA    RF,MQREASON                                                      
         ST    RF,0(RE)                                                         
         OI    0(RE),X'80'                                                      
         GOTO1 CALLMQ,MQCALL_STRUCTURE                                          
         B     RETURN                                                           
         EJECT                                                                  
**********************************************************************          
* PROCESS CLOSE ACTION CALL                                          *          
**********************************************************************          
         SPACE 1                                                                
PROCCLOS EQU   *                                                                
         LA    RE,MQCALL_STRUCTURE                                              
         L     R0,=A(CLOSE_STRUCTURE)                                           
         LA    R1,L'MQCALL_STRUCTURE                                            
         LR    RF,R1                                                            
         MVCL  RE,R0                                                            
         LA    RE,MQCALL_STRUCTURE                                              
         LA    RE,20(RE)                                                        
         LA    RF,HCONN                                                         
         ST    RF,0(RE)                                                         
         LA    RE,4(RE)                                                         
         LA    RF,HOBJ                                                          
         ST    RF,0(RE)                                                         
         LA    RE,4(RE)                                                         
         LA    RF,MQOPTIONS                                                     
         ST    RF,0(RE)                                                         
         LA    RE,4(RE)                                                         
         LA    RF,MQCOMPCODE                                                    
         ST    RF,0(RE)                                                         
         LA    RE,4(RE)                                                         
         LA    RF,MQREASON                                                      
         ST    RF,0(RE)                                                         
         OI    0(RE),X'80'                                                      
         GOTO1 CALLMQ,MQCALL_STRUCTURE                                          
         B     RETURN                                                           
         EJECT                                                                  
**********************************************************************          
* PROCESS DISCONNECT ACTION CALL                                     *          
**********************************************************************          
         SPACE 1                                                                
PROCDISC EQU   *                                                                
         LA    RE,MQCALL_STRUCTURE                                              
         L     R0,=A(DISCONNECT_STRUCTURE)                                      
         LA    R1,L'MQCALL_STRUCTURE                                            
         LR    RF,R1                                                            
         MVCL  RE,R0                                                            
         LA    RE,MQCALL_STRUCTURE                                              
         LA    RE,20(RE)                                                        
         LA    RF,HCONN                                                         
         ST    RF,0(RE)                                                         
         LA    RE,4(RE)                                                         
         LA    RF,MQCOMPCODE                                                    
         ST    RF,0(RE)                                                         
         LA    RE,4(RE)                                                         
         LA    RF,MQREASON                                                      
         ST    RF,0(RE)                                                         
         OI    0(RE),X'80'                                                      
         LA    R1,MQCALL_STRUCTURE                                              
         GOTO1 CALLMQ,MQCALL_STRUCTURE                                          
         B     RETURN                                                           
         EJECT                                                                  
**********************************************************************          
* PROCESS COMMIT ACTION CALL                                         *          
**********************************************************************          
         SPACE 1                                                                
PROCCMIT EQU   *                                                                
         LA    RE,MQCALL_STRUCTURE                                              
         L     R0,=A(COMMIT_STRUCTURE)                                          
         LA    R1,L'MQCALL_STRUCTURE                                            
         LR    RF,R1                                                            
         MVCL  RE,R0                                                            
         LA    RE,MQCALL_STRUCTURE                                              
         LA    RE,20(RE)                                                        
         LA    RF,HCONN                                                         
         ST    RF,0(RE)                                                         
         LA    RE,4(RE)                                                         
         LA    RF,MQCOMPCODE                                                    
         ST    RF,0(RE)                                                         
         LA    RE,4(RE)                                                         
         LA    RF,MQREASON                                                      
         ST    RF,0(RE)                                                         
         OI    0(RE),X'80'                                                      
         GOTO1 CALLMQ,MQCALL_STRUCTURE                                          
         B     RETURN                                                           
         EJECT                                                                  
**********************************************************************          
* RETURN TO CALLER                                                   *          
**********************************************************************          
         SPACE 1                                                                
RETURN   EQU   *                                                                
         B     EXIT                EXIT TO CALLER                               
ERROR01  EQU   *                                                                
         DC    H'0'                                                             
         EJECT                                                                  
**********************************************************************          
* INITIALISE CONNECT PARAMETER LIST                                  *          
**********************************************************************          
         SPACE 1                                                                
INITCONN NTR1                                                                   
         LOAD  EP=CSQBCONN                                                      
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
         L     RE,=A(CONNECT_STRUCTURE)                                         
         ST    R0,0(RE)                                                         
         LA    RE,4(RE)                                                         
         MVC   0(16,RE),=CL16'CONNECT'                                          
         XIT1                                                                   
         EJECT                                                                  
**********************************************************************          
* INITIALISE OPEN PARAMETER LIST                                     *          
**********************************************************************          
         SPACE 1                                                                
INITOPEN NTR1                                                                   
         LOAD  EP=CSQBOPEN                                                      
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
         L     RE,=A(OPEN_STRUCTURE)                                            
         ST    R0,0(RE)                                                         
         LA    RE,4(RE)                                                         
         MVC   0(16,RE),=CL16'OPEN'                                             
         XIT1                                                                   
         EJECT                                                                  
**********************************************************************          
* INITIALISE GET PARAMETER LIST                                      *          
**********************************************************************          
         SPACE 1                                                                
INITGET  NTR1                                                                   
         LOAD  EP=CSQBGET                                                       
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
         L     RE,=A(GET_STRUCTURE)                                             
         ST    R0,0(RE)                                                         
         LA    RE,4(RE)                                                         
         MVC   0(16,RE),=CL16'GET'                                              
         XIT1                                                                   
         EJECT                                                                  
**********************************************************************          
* INITIALISE PUT PARAMETER LIST                                      *          
**********************************************************************          
         SPACE 1                                                                
INITPUT  NTR1                                                                   
         LOAD  EP=CSQBPUT                                                       
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
         L     RE,=A(PUT_STRUCTURE)                                             
         ST    R0,0(RE)                                                         
         LA    RE,4(RE)                                                         
         MVC   0(16,RE),=CL16'PUT'                                              
         XIT1                                                                   
         EJECT                                                                  
**********************************************************************          
* INITIALISE CLOSE PARAMETER LIST                                    *          
**********************************************************************          
         SPACE 1                                                                
INITCLOS NTR1                                                                   
         LOAD  EP=CSQBCLOS                                                      
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
         L     RE,=A(CLOSE_STRUCTURE)                                           
         ST    R0,0(RE)                                                         
         LA    RE,4(RE)                                                         
         MVC   0(16,RE),=CL16'CLOSE'                                            
         XIT1                                                                   
         EJECT                                                                  
**********************************************************************          
* INITIALISE DISCONNECT PARAMETER LIST                               *          
**********************************************************************          
         SPACE 1                                                                
INITDISC NTR1                                                                   
         LOAD  EP=CSQBDISC                                                      
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
         L     RE,=A(DISCONNECT_STRUCTURE)                                      
         ST    R0,0(RE)                                                         
         LA    RE,4(RE)                                                         
         MVC   0(16,RE),=CL16'DISCONNECT'                                       
         XIT1                                                                   
         EJECT                                                                  
**********************************************************************          
* INITIALISE COMMIT PARAMETER LIST                                   *          
**********************************************************************          
         SPACE 1                                                                
INITCMIT NTR1                                                                   
         LOAD  EP=CSQBCOMM                                                      
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
         L     RE,=A(COMMIT_STRUCTURE)                                          
         ST    R0,0(RE)                                                         
         LA    RE,4(RE)                                                         
         MVC   0(16,RE),=CL16'COMMIT'                                           
         XIT1                                                                   
         EJECT                                                                  
         SPACE 1                                                                
*                                                                               
YES      SR    RC,RC                                                            
NO       LTR   RC,RC                                                            
         XIT1                                                                   
         EJECT                                                                  
CALLMQ   NTR1                                                                   
         LR    R2,R1                                                            
*                                                                               
* UPON ENTRY,  R2 POINTS TO PARAMETER STRUCTURE                                 
*              ASYNC = Y/N (ASYNCHRONOUS OR SYNCHRONOUS CALL)                   
* UPON RETURN, RTN_CODE CONTAINS THE APPC/MVS RETURN CODE                       
*                                                                               
         LA    RE,*+10                                                          
         O     RE,=X'80000000'                                                  
         BSM   0,RE                SWITCH TO 31-BIT MODE                        
*                                                                               
         L     RF,0(R2)            RF = A(APPC/MVS ROUTINE)                     
         LA    R3,20(R2)           R3 = A(PARAMETER LIST)                       
*                                                                               
         CALL  (15),MF=(E,(R3))                                                 
*                                                                               
         LA    RE,*+6                                                           
         BSM   0,RE                SWITCH BACK TO 24-BIT MODE                   
*                                                                               
         XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
* MQ SERIES DEFINITIONS                                               *         
***********************************************************************         
         SPACE 1                                                                
***********************************************************************         
*  MQ API CONSTANTS                                                   *         
***********************************************************************         
         CMQA LIST=NO                                                           
         SPACE 1                                                                
*                                                                               
         EJECT                                                                  
APPCECB  DC    F'0'                                                             
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
* PARAMETER LISTS FOR APPC/MVS CALLS                                            
*                                                                               
CONNECT_STRUCTURE              DS    A                                          
                               DS    CL16                                       
                               DS    4AL4                                       
*                                                                               
OPEN_STRUCTURE                 DS    A                                          
                               DS    CL16                                       
                               DS    6AL4                                       
*                                                                               
GET_STRUCTURE                  DS    A                                          
                               DS    CL16                                       
                               DS    9AL4                                       
*                                                                               
PUT_STRUCTURE                  DS    A                                          
                               DS    CL16                                       
                               DS    8AL4                                       
*                                                                               
CLOSE_STRUCTURE                DS    A                                          
                               DS    CL16                                       
                               DS    5AL4                                       
*                                                                               
DISCONNECT_STRUCTURE           DS    A                                          
                               DS    CL16                                       
                               DS    3AL4                                       
*                                                                               
COMMIT_STRUCTURE               DS    A                                          
                               DS    CL16                                       
                               DS    3AL4                                       
         EJECT                                                                  
         DS    0D                                                               
WORKD    DSECT                                                                  
*                                                                               
DMCB     DS    6F                                                               
RELO     DS    A                                                                
DUB      DS    D                                                                
APARM    DS    A                   A(PARAMETER LIST)                            
*                                                                               
PARM     DS    0XL4                PARAMETER LIST                               
MQCALLC  DS    XL4                 MQ CALL PARAMETER BLOCK                      
*                                                                               
BYTE     DS    XL1                                                              
DUMPLEN  DS    XL2                                                              
WORK     DS    CL256                                                            
*                                                                               
WORKX    EQU   *                                                                
         EJECT                                                                  
*                                                                               
       ++INCLUDE DDMQCALLD                                                      
         SPACE 2                                                                
       ++INCLUDE DDDPRINT                                                       
         SPACE 2                                                                
*                                                                               
         CSECT                                                                  
         DS    0D                                                               
PRNTBUFF DS    CL(5000)            APPC PRINT BUFFER                            
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003DDMQCALL  04/02/98'                                      
         END                                                                    
