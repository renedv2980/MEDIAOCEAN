*          DATA SET FALOCKETS  AT LEVEL 037 AS OF 06/28/00                      
*CATALP LOCKETA                                                                 
         TITLE 'FALOCKET - BUILD AND MAINTAIN LOCK TABLE IN DSPACE'             
LOCKET   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKL,*LOCKET*,RR=RE,CLEAR=YES                                   
         USING WORKD,RC                                                         
         ST    RE,RELO                                                          
         ST    RD,SAVERD                                                        
         LR    R9,R1                                                            
         USING LOCKETD,R9                                                       
         BRAS  RE,INIT             DO INITIALISATION                            
*                                                                               
LOCAL    USING UPDTABD,WRKKEY                                                   
         EJECT                                                                  
***********************************************************************         
* GO TO ROUTINE BY ACTION                                             *         
***********************************************************************         
         SPACE 1                                                                
         CLI   LKACTN,C'L'         ADD A LOCK ENTRY                             
         BE    LKLOCK                                                           
         CLI   LKACTN,C'T'         TEST A LOCK ENTRY                            
         BE    LKTEST                                                           
         CLI   LKACTN,C'U'         REMOVE A LOCK ENTRY                          
         BE    LKUNLK                                                           
         CLI   LKACTN,C'S'         INHIBIT LOCKING                              
         BE    LKSTOP                                                           
         CLI   LKACTN,C'F'         UNINHIBIT LOCKING                            
         BE    LKFREE                                                           
         CLI   LKACTN,C'A'         GET A(UPDTAB)                                
         BE    LKADDR                                                           
         CLI   LKACTN,C'B'         BUILD UPDTAB                                 
         BE    LKBUILD                                                          
         DC    H'0'                WHAT ARE YOU DOING?                          
         EJECT                                                                  
***********************************************************************         
* ADD A LOCK ENTRY                                                    *         
***********************************************************************         
         SPACE 1                                                                
LKLOCK   CLI   OFFLINE,C'Y'        DO NOTHING OFFLINE                           
         BE    XMOD                                                             
         BAS   RE,LKQBIT                                                        
         BL    ERR04               LOCKS INHIBITED                              
         BAS   RE,KEYPAD           BUILD WORK KEY                               
         BAS   RE,TABLOCK                                                       
         BNE   ERR02               CAN'T LOCK TABLE                             
         BAS   RE,CHKKEY                                                        
         BE    ERR01               KEY ALREADY LOCKED                           
         BAS   RE,CTSWIT                                                        
         BNE   ERR06               CAN'T SWITCH TO CONTROL SYSTEM               
*                                                                               
         BAS   RE,ADDKEY           ADD THIS KEY                                 
         BNE   ERR03               TABLE FULL                                   
*                                                                               
         BAS   RE,CTADD            ADD TO CTFILE                                
         BNE   ERRCT                                                            
         BAS   RE,SESWIT                                                        
         BNE   ERR06               CAN'T SWITCH BACK                            
         BAS   RE,TABFREE                                                       
         B     XMOD                                                             
         EJECT                                                                  
***********************************************************************         
* TEST A LOCK ENTRY                                                   *         
***********************************************************************         
         SPACE 1                                                                
LKTEST   CLI   OFFLINE,C'Y'        DO NOTHING OFFLINE                           
         BE    XMOD                                                             
         BAS   RE,KEYPAD           BUILD WORK KEY                               
         BAS   RE,CHKKEY           LOOK FOR KEY                                 
         BE    ERR01               KEY ALREADY LOCKED                           
         B     XMOD                                                             
         EJECT                                                                  
***********************************************************************         
* REMOVE A LOCK ENTRY                                                 *         
***********************************************************************         
         SPACE 1                                                                
LKUNLK   BAS   RE,KEYPAD           BUILD WORK KEY                               
         BAS   RE,CTSWIT                                                        
         BNE   ERR06               CAN'T SWITCH TO CONTROL                      
*                                                                               
         CLI   OFFLINE,C'Y'        TEST FOR OFFLINE                             
         BNE   LKUN02              NO                                           
         BAS   RE,CTDEL            DELETE CTFILE RECORD                         
         BNE   ERR05               OR RETURN KEY NOT FOUND                      
         B     LKUNX                                                            
*                                                                               
LKUN02   BAS   RE,TABLOCK                                                       
         BNE   ERR02               UNABLE TO LOCK TABLE                         
         BAS   RE,DELKEY                                                        
         BNE   ERR05               KEY NOT FOUND - CAN'T DELETE IT              
*                                                                               
         BAS   RE,CTDEL            DELETE THIS RECORD                           
         BAS   RE,TABFREE                                                       
*                                                                               
LKUNX    BAS   RE,SESWIT           SWITCH BACK TO NATIVE                        
         BNE   ERR06                                                            
         B     XMOD                                                             
         EJECT                                                                  
***********************************************************************         
* INHIBIT LOCKING                                                     *         
***********************************************************************         
         SPACE 1                                                                
LKSTOP   LAM   R0,RF,ARZERO                                                     
         LAM   R2,R2,ALET                                                       
         L     R2,ATAB                                                          
         USING LOCTABD,R2                                                       
         SAC   512                                                              
         OI    LOCFLAG,LOCSTOP                                                  
         SAC   0                                                                
         LAM   R2,R2,ARZERO                                                     
         B     XMOD                                                             
         EJECT                                                                  
***********************************************************************         
* UNINHIBIT LOCKING                                                   *         
***********************************************************************         
         SPACE 1                                                                
LKFREE   LAM   R0,RF,ARZERO                                                     
         LAM   R2,R2,ALET                                                       
         L     R2,ATAB                                                          
         USING LOCTABD,R2                                                       
         SAC   512                                                              
         NI    LOCFLAG,255-LOCSTOP                                              
         SAC   0                                                                
         LAM   R2,R2,ARZERO                                                     
         B     XMOD                                                             
         EJECT                                                                  
***********************************************************************         
* RETURN A(LOCKTAB)                                                   *         
***********************************************************************         
         SPACE 1                                                                
LKADDR   L     RF,ATAB             GET A(UPDTAB)                                
         STCM  RF,7,LKKEY                                                       
         B     XMOD                                                             
         EJECT                                                                  
***********************************************************************         
* BUILD LOCK TABLE FROM DISK                                          *         
***********************************************************************         
         SPACE 1                                                                
LKBUILD  BAS   RE,TABLOCK          LOCK TABLE                                   
         BNE   ERR02                                                            
         LA    R7,KEY                                                           
         USING CT8REC,R7           READ HI FOR TODAYS                           
         XC    CT8KEY,CT8KEY                                                    
         MVI   CT8KTYP,CT8KTYPQ                                                 
         MVC   CT8KDATE,LKDATE                                                  
*                                                                               
         GOTO1 ADMGR,DMCB,DMRDHI,CTFILE,KEY,IO                                  
*                                                                               
LKBL010  LA    R7,IO                                                            
         CLI   CT8KTYP,CT8KTYPQ                                                 
         BNE   LKBUILDX            EXIT WHEN ALL DONE                           
*                                                                               
         XC    WRKKEY,WRKKEY       FILL WORK KEY                                
         LA    R6,WRKKEY                                                        
         USING UPDTABD,R6                                                       
         MVI   UPDTABT,UPDTLQ                                                   
         MVC   LOCAL.UPDLDATE,CT8KDATE                                          
         MVC   LOCAL.UPDLTSE,CT8KSE                                             
         MVC   LOCAL.UPDLTAG,CT8KAG                                             
         MVC   LOCAL.UPDLTYP,CT8KTYPE                                           
         MVC   LOCAL.UPDLKEY,CT8KKEY                                            
         MVC   LOCAL.UPDLSTAT,CT8STAT                                           
         TM    LOCAL.UPDLSTAT,X'40' TEST UNLOCKED                               
         BO    LKBL020                                                          
         LA    R1,CT8DATA                                                       
         USING CTLKLD,R1                                                        
         MVC   LOCAL.UPDLTIME,CTLKLT1                                           
         MVC   LOCAL.UPDLUID,CTLKLUID                                           
         DROP  R1,R6                                                            
*                                                                               
         BAS   RE,CHKKEY           SEE IF KEY IS IN CORE                        
         BE    *+8                                                              
         BAS   RE,ADDKEY           IF NOT ADD IT                                
*                                                                               
LKBL020  GOTO1 ADMGR,DMCB,DMRSEQ,CTFILE,IO,IO                                   
         B     LKBL010                                                          
*                                                                               
LKBUILDX BAS   RE,TABFREE          UNLOCK LOCK TAB                              
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* PAD OUT AKEY TO PRODUCE INTERNAL WRKKEY                             *         
***********************************************************************         
         SPACE 1                                                                
KEYPAD   NTR1  ,                                                                
         L     R2,AKEY                                                          
         USING LKKEYD,R2                                                        
         XC    WRKKEY,WRKKEY                                                    
         MVI   LOCAL.UPDTABT,UPDTLQ      SET LOCKET ENTRY                       
         MVC   LOCAL.UPDLDATE,LKDATE     TODAYS DATE                            
         MVC   LOCAL.UPDLFAC,FAC         FACPAK                                 
         MVC   LOCAL.UPDLTSE,LKSENUM     SYSTEM SE NUMBER                       
         CLI   LOCKSE,0                                                         
         BE    *+10                                                             
         MVC   LOCAL.UPDLTSE,LOCKSE      CALLER CAN OVERRIDE                    
         CLI   LOCAL.UPDLTSE,0                                                  
         BNE   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         MVC   LOCAL.UPDLTAG,LOCKAGY                                            
         MVC   LOCAL.UPDLTYP,LOCKRTY                                            
         MVC   LOCAL.UPDLKEY,LOCKKEY                                            
         MVC   LOCAL.UPDLTIME,LKTIME                                            
         MVI   LOCAL.UPDLSTAT,0                                                 
         MVC   LOCAL.UPDLUID,LKLUID                                             
         B     EXITOK                                                           
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* ADD WRKKEY TO CONTROL FILE                                          *         
* EXIT: CC EQ  RECORD ADDED OK                                        *         
*          NE  DMGR ERROR - DUPLICATE KEY                             *         
***********************************************************************         
         SPACE 1                                                                
CTADD    NTR1  ,                                                                
         LA    R0,IO               CLEAR OUT IO AREA                            
         LHI   R1,IOL                                                           
         XR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         LA    R7,IO               BUILD LOCKET RECORD                          
         USING CT8REC,R7                                                        
         MVI   CT8KTYP,CT8KTYPQ                                                 
         MVC   CT8KDATE,LOCAL.UPDLDATE                                          
         MVC   CT8KSE,LOCAL.UPDLTSE                                             
         MVC   CT8KAG,LOCAL.UPDLTAG                                             
         MVC   CT8KTYPE,LOCAL.UPDLTYP                                           
         MVC   CT8KKEY,LOCAL.UPDLKEY                                            
         MVC   CT8KTIME,LOCAL.UPDLTIME                                          
         XC    CT8KTIME,EFFS       INVERT TIME                                  
*                                                                               
         LA    R1,CTLKLLNQ         FILL IN LEN                                  
         AHI   R1,28                                                            
         STCM  R1,3,CT8LEN                                                      
         MVC   CT8STAT,LOCAL.UPDLSTAT                                           
*                                                                               
         LA    R1,CT8DATA          FILL IN ELEMENT                              
         USING CTLKLD,R1                                                        
         MVI   CTLKLEL,CTLKLELQ                                                 
         MVI   CTLKLLEN,CTLKLLNQ                                                
         MVC   CTLKLT1,LOCAL.UPDLTIME                                           
         MVC   CTLKLUID,LOCAL.UPDLUID                                           
*                                                                               
         GOTO1 ADMGR,DMCB,DMADD,CTFILE,CT8KEY,CT8REC                            
         CLI   8(R1),X'20'                                                      
         BE    EXITL               DUPLICATE KEY ON ADD                         
         CLI   0(R1),0                                                          
         BE    EXITOK                                                           
         DC    H'0'                SOME OTHER DMGR ERROR                        
         EJECT                                                                  
***********************************************************************         
* DELETE WRKKEY FROM CONTROL FILE                                     *         
* EXIT: CC EQ  RECORD DELETED OK                                      *         
*          NE  DMGR ERROR                                             *         
***********************************************************************         
         SPACE 1                                                                
CTDEL    NTR1  ,                                                                
         LA    R7,KEY                                                           
         USING CT8REC,R7                                                        
         XC    CT8KEY,CT8KEY       CLEAR KEY                                    
         MVI   CT8KTYP,CT8KTYPQ    FILL IN KEY                                  
         MVC   CT8KDATE,LOCAL.UPDLDATE                                          
         MVC   CT8KSE,LOCAL.UPDLTSE                                             
         MVC   CT8KAG,LOCAL.UPDLTAG                                             
         MVC   CT8KTYPE,LOCAL.UPDLTYP                                           
         MVC   CT8KKEY,LOCAL.UPDLKEY                                            
         MVC   CT8KTIME,0                                                       
*                                                                               
         GOTO1 ADMGR,DMCB,(X'80',DMRDHI),CTFILE,KEY,IO                          
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    R7,IO               POINT TO IO                                  
         CLC   KEY(CT8KTIME-CT8KEY),CT8KEY                                      
         BNE   EXITL                                                            
*                                                                               
         LA    R1,CT8DATA          SET COMPLETION TIME                          
         USING CTLKLD,R1                                                        
         MVC   CTLKLT2,LKTIME                                                   
         OI    CT8STAT,X'40'       SET TO UNLOCKED                              
         GOTO1 ADMGR,DMCB,DMWRT,CTFILE,IO,IO                                    
         CLI   8(R1),0                                                          
         BE    EXITOK                                                           
         DC    H'0'                                                             
         EJECT                                                                  
***********************************************************************         
* LOCK TABLE IF POSSIBLE. EXIT CC=EQU IF OK                           *         
***********************************************************************         
         SPACE 1                                                                
TABLOCK  NTR1  ,                                                                
         LHI   R3,200              TRY 200 TIMES                                
*                                                                               
TLK02    LAM   R0,RF,ARZERO        I DON'T TRUST TICTOC HERE                    
         LAM   R2,R2,ALET                                                       
         L     R2,ATAB                                                          
         USING LOCTABD,R2                                                       
         SAC   512                                                              
         XR    R0,R0                                                            
         ICM   R1,15,=A(LOCTOKN)                                                
         CS    R0,R1,LOCLOCK       LOCK TABLE IF FREE                           
         BE    TLK06                                                            
*                                                                               
         SAC   0                                                                
         LAM   R2,R2,ARZERO                                                     
*                                                                               
         OC    ATICTOC,ATICTOC                                                  
         BZ    TLK04                                                            
*                                                                               
         LHI   RF,ONEMSTU          WAIT 1 MILLISECOND THEN TRY AGAIN            
         GOTO1 ATICTOC,DMCB,C'WAIT',(RF)                                        
         BCT   R3,TLK02                                                         
         B     EXITL                                                            
*                                                                               
TLK04    LHI   RF,10               WAIT 1/10 OF A SECOND                        
         ST    RF,FULL                                                          
         STIMER WAIT,BINTVL=FULL                                                
         BCT   R3,TLK02                                                         
         B     EXITL                                                            
*                                                                               
TLK06    MVI   LKFLAG,C'Y'         FLAG THAT I LOCKED IT                        
         SAC   0                                                                
         LAM   R2,R2,ARZERO                                                     
         B     EXITOK                                                           
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* FREE TABLE                                                          *         
***********************************************************************         
         SPACE 1                                                                
TABFREE  NTR1  ,                                                                
         LAM   R2,R2,ALET                                                       
         L     R2,ATAB                                                          
         USING LOCTABD,R2                                                       
         SAC   512                                                              
         XC    LOCLOCK,LOCLOCK                                                  
         SAC   0                                                                
         LAM   R2,R2,ARZERO                                                     
         DROP  R2                                                               
*                                                                               
         MVI   LKFLAG,C'N'                                                      
         B     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
* TEST IF LOCKING HAS BEEN STOPPED                                    *         
***********************************************************************         
         SPACE 1                                                                
LKQBIT   NTR1  ,                                                                
         LAM   R2,R2,ALET                                                       
         L     R2,ATAB                                                          
         USING LOCTABD,R2                                                       
         SAC   512                                                              
         TM    LOCFLAG,LOCSTOP                                                  
         IPM   R0                  PRESERVE CC                                  
         SAC   0                                                                
         LAM   R2,R2,ARZERO                                                     
         DROP  R2                                                               
*                                                                               
         SPM   R0                  RESTORE CC                                   
         BO    EXITL                                                            
         B     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
* CHECK FOR WORK KEY IN TABLE                                         *         
* EXIT: CC EQ  MATCHED A KEY                                          *         
***********************************************************************         
         SPACE 1                                                                
CHKKEY   NTR1  ,                                                                
         LAM   R0,RF,ARZERO                                                     
         LAM   R2,R2,ALET                                                       
         L     R2,ATAB                                                          
         SAC   512                                                              
         USING LOCTABD,R2                                                       
         LH    RE,LOCLEN                                                        
         L     RF,LOCEND                                                        
         LA    R2,LOCFST                                                        
         USING UPDTABD,R2                                                       
*                                                                               
CKL02    CLI   UPDTABT,UPDTLQ      CHECK IF LOCKET TYPE LOCK                    
         BNE   CKL12                                                            
         CLC   UPDLDATE,LOCAL.UPDLDATE                                          
         BNE   CKL12               DIFFERENT DATE                               
         CLC   UPDLTSE,LOCAL.UPDLTSE                                            
         BNE   CKL12               DIFFERENT SE                                 
*                                                                               
         XR    R1,R1                                                            
         ICM   R1,3,UPDLTAG        CHECK AGENCY - ZERO MATCHES ALL              
         BZ    CKL04                                                            
         ICM   R1,3,LOCAL.UPDLTAG                                               
         BZ    CKL04                                                            
         CLC   UPDLTAG,LOCAL.UPDLTAG                                            
         BE    CKL04                                                            
         B     CKL12                                                            
*                                                                               
CKL04    ICM   R1,3,UPDLTYP        CHECK RTYPE - ZERO MATCHES ALL               
         BZ    CKL06                                                            
         ICM   R1,3,LOCAL.UPDLTYP                                               
         BZ    CKL06                                                            
         CLC   UPDLTYP,LOCAL.UPDLTYP                                            
         BE    CKL06                                                            
         B     CKL12                                                            
*                                                                               
CKL06    LHI   R0,L'UPDLKEY        KEY TEST                                     
         LA    R3,LOCAL.UPDLKEY                                                 
         LA    R1,UPDLKEY                                                       
         CPYA  R1,R2                                                            
*                                                                               
CKL08    CLI   0(R1),0             ZERO IN KEY MATCHES ALL                      
         BE    CKL10                                                            
         CLI   0(R3),0                                                          
         BE    CKL10                                                            
         CLC   0(1,R1),0(R3)       COMPARE KEY BYTE                             
         BE    CKL10                                                            
         B     CKL12                                                            
*                                                                               
CKL10    AHI   R1,1                TEST NEXT BYTE                               
         AHI   R3,1                                                             
         BCT   R0,CKL08                                                         
         SAC   0                                                                
         LAM   R0,RF,ARZERO                                                     
         B     EXITOK                                                           
*                                                                               
CKL12    BXLE  R2,RE,CKL02                                                      
         SAC   0                                                                
         LAM   R0,RF,ARZERO                                                     
         B     EXITL                                                            
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* ADD WORK KEY TO TABLE                                               *         
* EXIT CC EQ  KEY ADDED OK                                            *         
*      CC NE  TABLE FULL                                              *         
***********************************************************************         
         SPACE 1                                                                
ADDKEY   NTR1  ,                                                                
         LAM   R0,RF,ARZERO                                                     
         LAM   R2,R2,ALET                                                       
         L     R2,ATAB                                                          
         SAC   512                                                              
         USING LOCTABD,R2                                                       
         LH    RE,LOCLEN                                                        
         L     RF,LOCEND                                                        
         LA    R2,LOCFST                                                        
         USING UPDTABD,R2                                                       
*                                                                               
ADK02    CLI   UPDTABT,0           LOOK FOR FREE ENTRY                          
         BE    ADK04                                                            
         BXLE  R2,RE,ADK02                                                      
         SAC   0                                                                
         LAM   R2,R2,ARZERO                                                     
         B     EXITL                                                            
*                                                                               
ADK04    MVC   UPDTABT(UPDTABL),WRKKEY                                          
         SAC   0                                                                
         LAM   R2,R2,ARZERO                                                     
         B     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
* DELETE KEY FROM TABLE                                               *         
* EXIT: CC EQ  KEY DELETED                                            *         
*       CC NE  KEY NOT FOUND                                          *         
***********************************************************************         
         SPACE 1                                                                
DELKEY   NTR1  ,                                                                
         LAM   R0,RF,ARZERO                                                     
         LAM   R2,R2,ALET                                                       
         L     R2,ATAB                                                          
         SAC   512                                                              
         USING LOCTABD,R2                                                       
         LH    RE,LOCLEN                                                        
         L     RF,LOCEND                                                        
         LA    R2,LOCFST                                                        
         USING UPDTABD,R2                                                       
*                                                                               
DLK02    CLC   UPDTABT(UPDKEYL),WRKKEY                                          
         BE    DLK04               MUST BE EXACT MATCH FOR DELETE               
         BXLE  R2,RE,DLK02                                                      
         SAC   0                                                                
         LAM   R2,R2,ARZERO                                                     
         B     EXITL                                                            
*                                                                               
DLK04    LA    R3,1(RF)                                                         
         SR    R3,R2               R3=L'DESTINATION                             
         LR    RF,R3                                                            
         SR    RF,RE               RF=L'SOURCE                                  
         AR    RE,R2               RE=FROM ADDRESS                              
         CPYA  RE,R2                                                            
         MVCL  R2,RE               MOVE & PAD LAST ENTRY WITH ZERO              
*                                                                               
         SAC   0                                                                
         LAM   R2,R2,ARZERO                                                     
         B     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
* ERROR EXITS                                                         *         
***********************************************************************         
         SPACE 1                                                                
ERR01    MVI   LKERR,X'01'         KEY IS ALREADY LOCKED                        
         B     ERRX                                                             
ERR02    MVI   LKERR,X'02'         CAN'T LOCK THE TABLE (IS BUSY)               
         B     ERRX                                                             
ERR03    MVI   LKERR,X'03'         LOCK TABLE FULL                              
         B     ERRX                                                             
ERR04    MVI   LKERR,X'04'         LOCKING INHIBITED                            
         B     ERRX                                                             
ERR05    MVI   LKERR,X'05'         KEY NOT FOUND FOR UNLOCK                     
         B     ERRX                                                             
ERR06    MVI   LKERR,X'06'         CONTROL NOT STARTED                          
         B     ERRX                                                             
*                                                                               
ERRX     CLI   LKFLAG,C'Y'         DID I LOCK THE TABLE                         
         BNE   *+8                                                              
         BAS   RE,TABFREE          YES SO FREE IT                               
         BAS   RE,SESWIT           GET BACK TO NATIVE SYSTEM                    
         B     XMOD                                                             
*                                                                               
ERRCT    CLI   LKFLAG,C'Y'         DID I LOCK THE TABLE                         
         BNE   *+8                                                              
         BAS   RE,TABFREE          YES SO FREE IT                               
         DC    H'0'                                                             
         SPACE 2                                                                
***********************************************************************         
* INITIALISATION                                                      *         
***********************************************************************         
         SPACE 1                                                                
INIT     NTR1  ,                                                                
         TIME                                                                   
         STCM  R0,14,LKTIME        GET DATE/TIME                                
         MVC   LKRTIME,LKTIME                                                   
         XC    LKRTIME,EFFS                                                     
         SRL   R1,4                                                             
         STCM  R1,7,LKDATE                                                      
*                                                                               
         XR    RF,RF                                                            
         ICM   RF,7,LKKEY                                                       
         ST    RF,AKEY             SET A(KEY)                                   
         XR    RF,RF               RF=A(COMFACS)                                
         ICM   RF,7,LKCOMFAC                                                    
         USING COMFACSD,RF         EXTRACT REQUIRED VALUES                      
         MVC   ADMGR,CDATAMGR                                                   
         MVC   ASWITCH,CSWITCH                                                  
         DROP  RF                                                               
*                                                                               
         ICM   RF,15,ASWITCH       NO SWITCH OFFLINE                            
         BZ    INIT02                                                           
         MVI   OFFLINE,C'N'        DEFAULT TO ONLINE                            
         GOTO1 (RF),DMCB,X'FEFFFFFF',0                                          
         L     RF,0(R1)                                                         
         USING SYSFACD,RF                                                       
         MVC   ASSB,VSSB           EXTRACT REQUIRED VALUES                      
         MVC   ASELIST,VSELIST                                                  
         MVC   ACALLOV,VCALLOV                                                  
         MVC   ATICTOC,VTICTOC                                                  
*                                                                               
         L     R2,ASSB                                                          
         USING SSBD,R2                                                          
         OI    SSBSTAT4,SSBDSLCK                                                
         MVC   ALET,SSBALET                                                     
         MVC   FAC,SSBSYSID                                                     
         ICM   R1,15,SSBTKADR      GET TASKADR IF ACTIVE                        
         BZ    INIT04                                                           
         ST    R1,ATASK                                                         
         USING TCBD,R1                                                          
         MVC   AUTL,TCBUTL-TCBD(R1)     A(UTL)                                  
         MVC   LKLUID,TCBSYM-TCBD(R1)   LUID                                    
         ICM   R1,15,AUTL                                                       
         BZ    *+10                                                             
         MVC   LKSENUM,TSYS-UTLD(R1)                                            
         B     INIT04                                                           
*                                                                               
INIT02   MVI   OFFLINE,C'Y'        SET OFFLINE                                  
         ICM   R2,15,=V(SSB)                                                    
         BZ    INIT03                                                           
         USING SSBD,R2                                                          
         CLI   SSOXTND,X'FF'       REQUIRE EXTENDED SSB                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         MVC   ALET,SSOALET                                                     
*                                                                               
INIT03   ICM   RF,15,=V(UTL)                                                    
         BZ    EXITOK                                                           
         ST    RF,AUTL                                                          
         MVC   LKSENUM,TSYS-UTLD(RF)                                            
         B     EXITOK                                                           
*                                                                               
INIT04   LAM   R0,RF,ARZERO                                                     
         LAM   R2,R2,ALET                                                       
         XR    R2,R2                                                            
         SAC   512                                                              
         USING DMDSHDR,R2                                                       
         MVC   ATAB,DHALOCT        SET A(LOCK TABLE)                            
*                                                                               
         L     R2,ATAB                                                          
         USING LOCTABD,R2                                                       
         LHI   RF,UPDTABL                                                       
         STH   RF,LOCLEN                                                        
         ICM   RF,15,=A(64*1024-1)                                              
         AR    RF,R2                                                            
         ST    RF,LOCEND                                                        
*                                                                               
         SAC   0                                                                
         LAM   R2,R2,ARZERO                                                     
         B     EXITOK                                                           
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* SWITCH TO CONTROL SYSTEM                                            *         
***********************************************************************         
         SPACE 1                                                                
CTSWIT   NTR1  ,                                                                
         MVC   SVSENUM,LKSENUM                                                  
         CLI   LKSENUM,0           HAVE AN SE NUMBER?                           
         BE    EXITOK                                                           
*                                                                               
         CLI   OFFLINE,C'Y'        OFFLINE?                                     
         BNE   CSW02               NO                                           
         ICM   R1,15,AUTL                                                       
         BZ    EXITOK                                                           
         MVI   TSYS-UTLD(R1),X'0A'                                              
         B     EXITOK                                                           
*                                                                               
CSW02    ICM   R0,15,EFFS                                                       
         ICM   R0,8,=X'0A'                                                      
         GOTO1 ASWITCH,DMCB,(R0),0                                              
         CLI   4(R1),0                                                          
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* SWITCH TO NATIVE SYSTEM                                             *         
***********************************************************************         
         SPACE 1                                                                
SESWIT   NTR1  ,                                                                
         CLI   SVSENUM,0           HAVE AN SE NUMBER SAVED?                     
         BE    EXITOK              NO                                           
*                                                                               
         CLI   OFFLINE,C'Y'        OFFLINE?                                     
         BNE   SSW02               NO                                           
         ICM   R1,15,AUTL                                                       
         BZ    EXITOK                                                           
         MVC   TSYS-UTLD(L'TSYS,R1),SVSENUM                                     
         B     EXITOK                                                           
*                                                                               
SSW02    ICM   R0,15,EFFS                                                       
         ICM   R0,8,SVSENUM                                                     
         GOTO1 ASWITCH,DMCB,(R0),0                                              
         CLI   4(R1),0                                                          
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* EXIT POINTS + HANDY ROUTINES                                        *         
***********************************************************************         
         SPACE 1                                                                
ON31     O     RE,=XL4'80000000'                                                
         BSM   0,RE                                                             
*                                                                               
OFF31    N     RE,=XL4'7FFFFFFF'                                                
         BSM   0,RE                                                             
*                                                                               
EXITOK   CR    RB,RB                                                            
         B     EXIT                                                             
*                                                                               
EXITL    CLI   *,255                                                            
         B     EXIT                                                             
*                                                                               
EXIT     XIT1  ,                                                                
*                                                                               
XMOD     SAC   0                                                                
         LAM   R0,RF,ARZERO                                                     
         BRAS  RE,OFF31                                                         
         L     RD,SAVERD                                                        
         CLI   LKERR,0             SET CC FOR RETURN                            
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
*        CONSTANTS & LTORG                                  *                   
***********************************************************************         
         SPACE 1                                                                
ONEMSTU  EQU   384                                                              
         CNOP  0,4                                                              
ARZERO   DC    16F'0'                                                           
EFFS     DC    X'FFFFFFFF'                                                      
         SPACE 1                                                                
DMREAD   DC    C'DMREAD  '                                                      
DMRSEQ   DC    C'DMRSEQ  '                                                      
DMRDHI   DC    C'DMRDHI  '                                                      
DMWRT    DC    C'DMWRT   '                                                      
DMADD    DC    C'DMADD   '                                                      
CTFILE   DC    C'CTFILE  '                                                      
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* WORKING STORAGE DSECT                                               *         
***********************************************************************         
         SPACE 1                                                                
WORKD    DSECT                                                                  
DUB      DS    D                                                                
SAVERD   DS    A                                                                
DMCB     DS    6F                                                               
ALET     DS    A                                                                
ATAB     DS    A                   A(LOCK TABLE IN DMGR DSPACE)                 
AKEY     DS    A                   A(LOCK KEY)                                  
*                                                                               
FULL     DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    X                                                                
FLAG     DS    X                                                                
WORK     DS    CL64                                                             
RELO     DS    A                                                                
SAVERE   DS    A                                                                
LKDATE   DS    XL3                 DATE NOW JULIAN                              
LKTIME   DS    XL3                 TIME NOW HH:MM:SS                            
LKRTIME  DS    XL3                 TIME NOW HH:MM:SS (COMPLIMENTED)             
LKSENUM  DS    X                   SENUM ONLINE OR OFF                          
LKLUID   DS    CL8                 LUID IF ONLINE                               
LKREF    DS    CL4                                                              
*                                                                               
ADMGR    DS    V                   ADDRESSES                                    
ACALLOV  DS    V                                                                
ASELIST  DS    V                                                                
ASSB     DS    V                                                                
ASWITCH  DS    V                                                                
ATASK    DS    V                                                                
AUTL     DS    V                                                                
ATICTOC  DS    V                                                                
*                                                                               
WRKKEY   DS    XL(UPDTABL)         WORK KEY                                     
UPDKEYL  EQU   UPDLTIME-UPDTLOCK   LEN FOR COMPARE                              
*                                                                               
KEY      DS    CL25                USED FOR RDHI                                
OFFLINE  DS    C                   SET TO Y IF OFFLINE                          
LKFLAG   DS    X                   SET TO Y IF TABLE WAS LOCKED                 
SVSENUM  DS    X                   SAVED SENUM BEFORE SWITCH                    
FAC      DS    X                   FACPAK ID                                    
*                                                                               
IO       DS    XL1024              I/O AREA FOR CTFILE                          
IOL      EQU   *-IO                                                             
WORKL    EQU   *-WORKD                                                          
         EJECT                                                                  
***********************************************************************         
* OTHER INCLUDED DSECTS                                               *         
***********************************************************************         
* DMDSHDR                                                                       
         PRINT OFF                                                              
       ++INCLUDE DMDSHDR                                                        
         PRINT ON                                                               
* DDCOMFACS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
* CTGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE CTGENFILE                                                      
         PRINT ON                                                               
* FAUPDTAB                                                                      
         PRINT OFF                                                              
       ++INCLUDE FAUPDTAB                                                       
         PRINT ON                                                               
* FALOCKETD                                                                     
         PRINT OFF                                                              
       ++INCLUDE FALOCKETD                                                      
         PRINT ON                                                               
* FATCB                                                                         
         PRINT OFF                                                              
       ++INCLUDE FATCB                                                          
         PRINT ON                                                               
* FAUTL                                                                         
         PRINT OFF                                                              
       ++INCLUDE FAUTL                                                          
         PRINT ON                                                               
* FASYSFAC                                                                      
         PRINT OFF                                                              
       ++INCLUDE FASYSFAC                                                       
         PRINT ON                                                               
* FASSB                                                                         
         PRINT OFF                                                              
       ++INCLUDE FASSB                                                          
         ORG   SSBCNTL                                                          
       ++INCLUDE FASSBOFF                                                       
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'037FALOCKETS 06/28/00'                                      
         END                                                                    
