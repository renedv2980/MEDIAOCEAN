*          DATA SET REDARTKO   AT LEVEL 007 AS OF 12/12/97                      
*CATALP REDARTKO                                                                
         TITLE 'AGENCY ORDER TAKEOVER CHOPPING ROUTINE'                         
***********************************************************************         
*                                                                     *         
* UPDATE HISTORY:                                                     *         
*                                                                     *         
* 28OCT97 (JRD) INITIAL RELEASE (CLONED FROM BUYBACK IN RESFM2F)      *         
* 12DEC97 (JRD) DON'T NEXTEL AFTER DELETING AN ELEMENT                *         
*                                                                     *         
* ------------------------------------------------------------------- *         
*                                                                     *         
* INPUT                                                               *         
*           P1  - BYTE 2-4   A(AGENCY BUY DETAIL)                     *         
*           P2  - BYTE 1-3   TKODATE (STATION JOIN DATE)              *         
*           P3  - BYTE 1-4   DAYINFO                                  *         
*                             - STARTDAY (FROM STARTEND IN REDAR21)   *         
*                             - ENDDAY  (FROM STARTEND IN REDAR21)    *         
*                             - ROTATDAY (FROM STARTEND IN REDAR21)   *         
*                             - ORBSTDAY (FROM BUYORBIT IN REDAR21)   *         
*           P4  - BYTE 2-4   A(PARMS)                                 *         
*                             - A(DATCON)                             *         
*                             - A(GETDAY)                             *         
*                             - A(ADDAY)                              *         
*                             - A(PERVERT)                            *         
*                                                                     *         
* OUTPUT                                                              *         
*           UPDATED AGENCY BUY DETAIL                                 *         
*   CC EQUAL     - SOME EFFECTIVE DATE/DETAILS KEPT                   *         
*   CC NOT EQUAL - NO EFFECTIVE DATE/DETAILS KEPT(DISCARD BUY)        *         
*                                                                     *         
*---------------------------------------------------------------------*         
*                                                                     *         
* NOTES:                                                              *         
*                                                                     *         
* 1. ROUTINE WILL BACKUP NON MONDAY TKODATES TO THE PREVIOUS MONDAY   *         
*                                                                     *         
***********************************************************************         
REDARTKO CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKLQ,*DARTKO*,R7,RR=R3                                         
         USING WORKD,RC                                                         
*                                                                               
         ST    R3,RELO                                                          
         MVC   ABUYDTL,0(R1)                                                    
         MVC   TKODATE,4(R1)                                                    
         MVC   DAYINFO,8(R1)                                                    
*                                                                               
         L     RE,12(R1)           A(PARMS)                                     
         MVC   DATCON,0(RE)                                                     
         MVC   GETDAY,4(RE)                                                     
         MVC   ADDAY,8(RE)                                                      
         MVC   PERVERT,12(RE)                                                   
*                                                                               
         GOTO1 DATCON,DMCB,(3,TKODATE),(0,WORK)                                 
         GOTO1 GETDAY,DMCB,WORK,DUB                                             
         ZIC   RF,DMCB             GET DAY OF WEEK                              
         BCTR  RF,0                MAKE DAY ZERO RELATIVE                       
         LTR   RF,RF               MONDAY?                                      
         BZ    CHOP0002            YES - LEAVE                                  
         LNR   RF,RF               NEGATE REGISTER                              
         GOTO1 ADDAY,DMCB,WORK,WORK,(RF)                                        
         GOTO1 DATCON,DMCB,(0,WORK),(3,TKODATE)                                 
*                                  BACK UP TO MONDAY                            
*                                                                               
CHOP0002 DS    0H                                                               
         L     R6,ABUYDTL          RESET A(X'41' RECORD)                        
         LA    R6,RDARELEM-RDARREC(R6)                                          
         USING RDARBDEL,R6         BUY DETAIL DESCRIP ELEMENT                   
*                                                                               
         MVI   DAILY,C'N'                                                       
         TM    RDARBDFL,X'80'      DAILY BUY?                                   
         BNO   *+8                                                              
         MVI   DAILY,C'Y'          YES - SET FLAG                               
*                                                                               
         MVI   ELCODE,X'02'        DETAIL ELEMENT                               
         BAS   RE,FIRSTEL                                                       
         USING RDARBUEL,R6                                                      
         BE    *+6                 NONE FOUND                                   
         DC    H'0'                                                             
         EJECT                                                                  
*------------------*                                                            
* CALC. START DATE *                                                            
*------------------*                                                            
CHOP0010 DS    0H                                                               
         GOTO1 DATCON,DMCB,(2,RDARBUSD),(0,WORK)                                
*                                  CONVERT START DATE TO EBCDIC                 
*                                                                               
         CLI   DAILY,C'Y'          DAILY BUY?                                   
         BNE   *+14                                                             
         MVC   EBCSTART,WORK       YES - USE START AS IS, THEN                  
         B     CHOP0060               SET END TO START                          
*                                                                               
         ZIC   RF,RDARBUWK         CALCULATE NUMBER OF DAYS                     
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         BCTR  RF,0                MAKE WEEKS ZERO RELATIVE                     
         MH    RF,=H'7'            MULTIPLY WEEKS BY 7                          
         GOTO1 ADDAY,DMCB,WORK,WORK+6,(RF)                                      
*                                                                               
         GOTO1 GETDAY,DMCB,WORK,WORK+12                                         
*                                  GET DAY OF START WEEK                        
         ZIC   RF,DMCB             GET DAY OF WEEK                              
         BCTR  RF,0                MAKE DAY ZERO RELATIVE                       
         LTR   RF,RF               MONDAY?                                      
         BZ    CHOP0030            YES - LEAVE                                  
         LNR   RF,RF               NEGATE REGISTER                              
         GOTO1 ADDAY,DMCB,WORK,WORK+12,(RF)                                     
*                                  BACK UP TO MONDAY                            
*                                                                               
* IN CASE OF OUT OF WEEK ROTATORS, COMPARE BUY START DAY AND ROTATION           
* START DAY TO GET THE CORRECT MONDAY DATE                                      
*                                                                               
         CLC   STARTDAY,ROTATDAY                                                
         BNL   CHOP0020                                                         
         MVC   WORK(6),WORK+12                                                  
         LA    RF,7                                                             
         GOTO1 ADDAY,DMCB,WORK,WORK+12,(RF)                                     
*                                                                               
CHOP0020 DS    0H                                                               
         MVC   WORK(6),WORK+12                                                  
*                                                                               
CHOP0030 DS    0H                                                               
         CLI   ORBSTDAY,0          ANY ORBIT START DAY?                         
         BE    CHOP0040            NO  - USE HEADER START DAY                   
*                                                                               
         ZIC   RF,ORBSTDAY         YES - USE IT                                 
         B     CHOP0050                                                         
*                                                                               
CHOP0040 DS    0H                                                               
         ZIC   RF,STARTDAY         ADD START DAY                                
*                                                                               
CHOP0050 DS    0H                                                               
         BCTR  RF,0                MAKE ZERO RELATIVE                           
         GOTO1 ADDAY,DMCB,WORK,EBCSTART,(RF)                                    
*                                  BUMP TO START DAY IN WEEK                    
CHOP0060 DS    0H                                                               
         GOTO1 DATCON,DMCB,(0,EBCSTART),(3,EFFSTART)                            
*                                  GET EFFECTIVE START DATE                     
         EJECT                                                                  
*----------------*                                                              
* CALC. END DATE *                                                              
*----------------*                                                              
         CLI   DAILY,C'Y'          DAILY BUY?                                   
         BNE   *+20                                                             
         MVC   EBCEND,EBCSTART                                                  
         MVC   EFFEND,EFFSTART                                                  
         B     CHOP0200            YES - SET END DATE = START DATE              
*                                                                               
*                                  GET DAY OF END   WEEK                        
         GOTO1 GETDAY,DMCB,WORK+6,WORK+12                                       
*                                                                               
         ZIC   RF,DMCB             GET DAY OF WEEK                              
         BCTR  RF,0                MAKE DAY ZERO RELATIVE                       
         LTR   RF,RF               MONDAY?                                      
         BZ    CHOP0130            YES - LEAVE                                  
         LNR   RF,RF               NEGATE REGISTER                              
         CLC   STARTDAY,ENDDAY     START/END ON SAME DAY?                       
*                                     (SINGLE-DAY BUY?)                         
         BE    CHOP0110            YES - DON'T BUMP TO NEXT WEEK                
         BL    CHOP0110            START < END DAY:  NOT AN                     
*                                     OOWR - DON'T BUMP                         
         LA    RF,7(RF)            BUMP IT INTO NEXT WEEK                       
*                                     OOWR - DON'T BUMP                         
CHOP0110 DS    0H                                                               
         GOTO1 ADDAY,DMCB,WORK+6,WORK+12,(RF)                                   
*                                  BACK UP TO MONDAY                            
*                                                                               
* IN CASE OF OUT OF WEEK ROTATORS, COMPARE BUY START DAY AND ROTATION           
* START DAY TO GET THE CORRECT MONDAY DATE                                      
*                                                                               
         CLC   STARTDAY,ROTATDAY                                                
         BNL   CHOP0120                                                         
         MVC   WORK+6(6),WORK+12                                                
         LA    RF,7                                                             
         GOTO1 ADDAY,DMCB,WORK+6,WORK+12,(RF)                                   
*                                                                               
CHOP0120 DS    0H                                                               
         MVC   WORK+6(6),WORK+12                                                
*                                                                               
CHOP0130 DS    0H                                                               
         ZIC   RF,ENDDAY           ADD END   DAY                                
         BCTR  RF,0                MAKE ZERO RELATIVE                           
         GOTO1 ADDAY,DMCB,WORK+6,EBCEND,(RF)                                    
*                                  BUMP TO END   DAY IN WEEK                    
         GOTO1 DATCON,DMCB,(0,EBCEND),(3,EFFEND)                                
*                                  GET EFFECTIVE END DATE                       
         EJECT                                                                  
*----------------*                                                              
* CHOP THE DATES *                                                              
*----------------*                                                              
CHOP0200 DS    0H                                                               
         MVI   EFFELTS,C'N'        SET 'NO EFFECTIVE DATE ELEMENTS'             
*                                                                               
*   PROCESS EACH EFFECTIVE DATE(START & END):                                   
*        1.  IF END BEFORE TKO DATE, DROP ELEMENT                               
*        2.  IF START NOT EARLIER THAN TKO DATE, USE AS-IS                      
*        3.  IF START EARLIER, USE TKO DATE, ADJUST ELEMENT                     
*                                                                               
*                                                                               
*   WHAT IMPACT DO OOWR HAVE ON THESE TESTS?                                    
*                                                                               
         CLC   TKODATE,EFFEND      TKO DATE VS EFF DATE END                     
         BH    CHOP0300            EFF DATE END PRIOR TKO DATE -                
*                                     DROP THE ELEMENT                          
*                                  EXAMPLE:  BEFFDATE = JUL1/97                 
*                                            EFF END  = JUN1/97                 
*                                                                               
         CLC   TKODATE,EFFSTART    TKO DATE VS EFF DATE START                   
         BH    CHOP0210            EFF DATE START PRIOR TKO DATE                
*                                     CUT BACK ELEMENT - REGEN BUCKETS          
*                                  EXAMPLE:  BEFFDATE = JUL1/97                 
*                                            EFF STRT = JAN1/97                 
*                                                                               
         MVI   EFFELTS,C'Y'        EFF DATE START =/AFTER TKO DATE              
         B     CHOP0400               LEAVE ALONE - NEXT ELEMENT                
*                                  EXAMPLE:  BEFFDATE = JUL1/97                 
*                                            EFF STRT = JUL1/97 (OR)            
*                                            EFF STRT = SEP1/97                 
*---------------------------------------------------------------------          
*   TKO DATE IS ALWAYS A MONDAY.  CHECK BUY START DATE.  IF NOT                 
*        MONDAY, ADJUST BEFFDATE INSERT INTO BUY TO PROVIDE                     
*        OUT-OF-WEEK ROTATOR SUPPORT.                                           
*                                                                               
CHOP0210 DS    0H                                                               
         GOTO1 GETDAY,DMCB,EBCSTART,DUB                                         
         CLI   DMCB,1              ORIGINAL START = MONDAY?                     
         BE    CHOP0220            YES - USE TKO DATE AS IS                     
*                                  NO  - OFFSET TO ORIG BUY START DAY           
         ZIC   RE,DMCB             GET DAY OF WEEK NUMBER                       
         BCTR  RE,0                SUBTRACT 1 DAY                               
         ST    RE,DMCB+8           SET ADDAY ADJUST VALUE                       
         GOTO1 DATCON,DMCB,(3,TKODATE),(0,WORK)                                 
*                                  CONVERT TKO DATE TO EBCDIC                   
         GOTO1 ADDAY,DMCB,WORK,WORK,,                                           
         GOTO1 DATCON,DMCB,(0,WORK),(3,EFFSTART)                                
*                                  CONVERT OOWR START TO BINARY                 
         CLC   EFFEND,EFFSTART                                                  
         BL    CHOP0300            END BEFORE START:  DROP BUYLINE              
*                                     OOWR IN SINGLE WEEK:  SETS                
*                                     START AFTER END DATE                      
         B     CHOP0230                                                         
*                                                                               
CHOP0220 DS    0H                                                               
         MVC   EFFSTART,TKODATE    RESET EFF START TO TKO DATE                  
*                                                                               
*------------------*                                                            
* INSERT NEW DATES *                                                            
*------------------*                                                            
CHOP0230 DS    0H                                                               
         GOTO1 DATCON,DMCB,(3,EFFSTART),(0,EBCSTART)                            
*                                                                               
         GOTO1 PERVERT,DMCB,EBCSTART,EBCEND                                     
*                                  CALCULATE DATE RANGE FIGURES                 
         ZICM  RF,DMCB+12,2        GET # WEEKS (DAYS/7)                         
         ZICM  RE,DMCB+10,2        CHECK REMAINDER DAYS/7                       
         LTR   RE,RE                                                            
         BZ    *+8                 NO REMAINDER                                 
         LA    RF,1(RF)            REMAINDER:  ADD 1 TO NUM WEEKS               
*                                                                               
         STC   RF,RDARBUWK         STORE # OF WEEKS                             
*                                                                               
******** SHOULD THE START DATE BE ADJUSTED BEFORE REINSERTING? ********         
*                                                                               
         GOTO1 DATCON,DMCB,(0,EBCSTART),(2,RDARBUSD)                            
*                                  STORE NEW START DATE                         
         MVI   EFFELTS,C'Y'        SET 'EFFECTIVE DATE ELEMENTS'                
         B     CHOP0400                                                         
         EJECT                                                                  
*----------------*                                                              
* DELETE ELEMENT *                                                              
*----------------*                                                              
CHOP0300 DS    0H                                                               
         L     RE,ABUYDTL          ADJUST RECORD LENGTH                         
         SR    R0,R0                                                            
         ICM   R0,3,RDARLEN-RDARREC(RE)                                         
         ZIC   R1,1(R6)                                                         
         AR    R1,R0                                                            
         STCM  R1,3,RDARLEN-RDARREC(RE)                                         
*                                                                               
         LR    RF,R6                                                            
CHOP0310 DS    0H                  FIND END OF RECORD                           
         ZIC   R0,1(RF)                                                         
         AR    RF,R0                                                            
         CLI   0(RF),0                                                          
         BNE   CHOP0310                                                         
*                                                                               
         SR    RF,R6               MOVE TO LENGTH                               
         LR    RE,R6               MOVE TO ADDRESS                              
*                                                                               
         ZIC   R0,1(R6)            GET LENGTH OF ELLEMENT TO BE DELETED         
         LNR   R1,R0               MAKE IT NEGATIVE                             
         AR    R1,RF               MOVE FROM LENGTH                             
         AR    R0,R6               MOVE FROM ADDRESS                            
*                                                                               
         MVCL  RE,R0               DELETE IT                                    
*                                                                               
         BAS   RE,FIRSTEL          DON'T NEXTEL                                 
         BE    CHOP0010            MORE ELEMENTS TO DO                          
         B     CHOP0500            ALL DONE EXIT                                
*                                                                               
         EJECT                                                                  
*--------------*                                                                
* NEXT ELEMENT *                                                                
*--------------*                                                                
CHOP0400 DS    0H                                                               
         BAS   RE,NEXTEL                                                        
         BE    CHOP0010            GO BACK FOR NEXT ELEMENT                     
*                                                                               
CHOP0500 DS    0H                                                               
         CLI   EFFELTS,C'Y'        SET CONDITION CODE AND EXIT                  
         XIT1                                                                   
         DROP  R6                                                               
         EJECT                                                                  
***********************************************************************         
         GETEL R6,=Y(RDARELEM-RDARREC),ELCODE                                   
***********************************************************************         
         LTORG                                                                  
***********************************************************************         
* APPLICATION STORAGE AREA                                                      
***********************************************************************         
WORKD    DSECT                                                                  
DUB      DS    D                                                                
RELO     DS    A                                                                
ABUYDTL  DS    A                                                                
*                                                                               
DATCON   DS    A                                                                
GETDAY   DS    A                                                                
ADDAY    DS    A                                                                
PERVERT  DS    A                                                                
*                                                                               
DAILY    DS    X                   DAILY BUY INDICATOR                          
TKODATE  DS    XL3                 DATE TO CHOP AGAINST                         
*                                                                               
DAYINFO  DS    0XL4                                                             
STARTDAY DS    X                                                                
ENDDAY   DS    X                                                                
ROTATDAY DS    X                                                                
ORBSTDAY DS    X                                                                
*                                                                               
DMCB     DS    6F                                                               
ELCODE   DS    X                                                                
EFFELTS  DS    X                                                                
NUMWEEKS DS    X                                                                
EBCSTART DS    CL6                 START DATE IN EBCDIC                         
EBCEND   DS    CL6                 END DATE IN EBCDIC                           
EFFSTART DS    XL3                 EFFECTIVE START DATE(YMD)                    
EFFEND   DS    XL3                 EFFECTIVE END DATE(YMD)                      
WORK     DS    XL256                                                            
WORKLQ   EQU   *-WORKD                                                          
         EJECT                                                                  
*REGENDAR                                                                       
         PRINT OFF                                                              
       ++INCLUDE REGENDAR                                                       
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'007REDARTKO  12/12/97'                                      
         END                                                                    
