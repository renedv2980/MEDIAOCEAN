*          DATA SET FAPROGBLD  AT LEVEL 016 AS OF 09/10/15                      
*CATALP PROGBLD                                                                 
         TITLE 'FAPROGBLD - BUILD PROGRAMS IN CORE FOR THIS SYSTEM'             
***********************************************************************         
* NTRY: P1    = A(START OF BUILD AREA)                                *         
*       P2    = A(END OF HOLE) OR 0 = NO LIMIT TO BUILD               *         
*       P3    = A(START OF PHASE LIST BLOCK IN XA)                    *         
*       P4    = L'PHASE LIST BLOCK IN XA                              *         
*       P5    = A(PHLIST FROM FAPARMDEF) OR ZERO                      *         
*                                                                     *         
* EXIT: P1    = A(START OF BUILD AREA)                                *         
*       P2    = A(END OF BUILD AREA)                                  *         
***********************************************************************         
         SPACE 1                                                                
PROGBLD  CSECT                                                                  
         NMOD1 WORKL,*PROGBLD,CLEAR=YES                                         
         USING WORKD,RC                                                         
D        USING LIBUFFD,LIBUFFR                                                  
C        USING LIBUFFD,LIBUFFC                                                  
DR       USING PROGSPCD,DPSREC                                                  
LC       USING PROGSPCD,PSREC                                                   
*                                                                               
         L     RA,VSYSFAC                                                       
         USING SYSFACD,RA                                                       
         L     R9,VSSB                                                          
         USING SSBD,R9                                                          
         ST    R1,SAVER1                                                        
         MVC   IPARMS,0(R1)                                                     
         MVC   ACORE,ICORE                                                      
*                                                                               
         LHI   R1,1                                                             
         BRAS  RE,PUTMESS                                                       
*                                                                               
         L     RF,ACORE            SET A(PROGRAMS BUILD AREA)                   
         STCM  RF,15,SSBAPGMS                                                   
*                                                                               
         AHI   RF,15               MARK PHASE LOAD AREAS                        
         SRL   RF,4                                                             
         SLL   RF,4                                                             
         MVC   0(16,RF),=CL16'*CORERES PHASES*'                                 
         AHI   RF,16                                                            
         ST    RF,ACORE            UPDATE CORE ADDRESS                          
*                                                                               
         ICM   R0,15,=AL4(DPDPGMS)                                              
         GOTO1 VLOCKSPC,DUB,(X'20',(R0)),WORK                                   
*                                                                               
         LAM   R0,RF,ARZERO                                                     
         L     RE,VSSB                                                          
         LAM   R2,R2,SSBPGMTA-SSBD(RE)                                          
         ICM   R2,15,WORK+(DSPTFRST-DMSPACED)                                   
         N     R2,=XL4'7FFFFFFF'                                                
         SAC   512                                                              
         MVC   LIBUFFR,0(R2)       GET DATASPACE ARREDIT PARMS                  
         SAC   0                                                                
         LAM   R2,R2,ARZERO                                                     
*                                                                               
         BRAS  RE,ON31             CLEAR OUT XA PHASE LIST                      
         L     R0,AXALIST                                                       
         L     R1,LXALIST                                                       
         XR    RF,RF                                                            
         MVCL  R0,RE                                                            
         BRAS  RE,OFF31                                                         
*                                                                               
         L     R1,AXALIST                                                       
         ST    R1,VPHLIST          SAVE A(START)                                
         AHI   R1,64               ALLOW FOR HEADER AND PARMS                   
         L     R0,LXALIST                                                       
         STCM  R0,15,SSBLXPH                                                    
         AHI   R0,-64              DITTO                                        
         STCM  R1,15,C.LIABUFF                                                  
         STCM  R0,15,C.LILBUFF                                                  
*                                                                               
         MVI   C.LIACTN,LIAINI                                                  
         MVC   C.LIKEYL,=AL2(PSKEYL)                                            
         MVC   C.LIRECLMX,=AL2(PROGSPCL)                                        
         GOTO1 VARREDIT,DMCB,C.LIBUFFD                                          
         CLI   C.LIRTN,LIROK                                                    
         BE    BLDP04                                                           
         LHI   R1,4                                                             
         BRAS  RE,PUTMESS                                                       
         ABEND 101,DUMP                                                         
*                                                                               
BLDP04   MVC   D.LIALET,SSBPGMTA                                                
         NI    D.LIFLAG1,255-LIF1INS                                            
         LA    RF,DPSREC                                                        
         ST    RF,D.LIAREC                                                      
*                                                                               
         XC    DPSREC,DPSREC       GET READY TO READ DATASPACE                  
         MVI   D.LIACTN,LIAHIGH    FIRST IN DATASPACE                           
         B     *+8                                                              
*                                                                               
BLDP06   MVI   D.LIACTN,LIASEQ     NEXT IN DATASPACE                            
         GOTO1 VARREDIT,DMCB,D.LIBUFFD                                          
         CLI   D.LIRTN,LIREOF                                                   
         BE    BLDP22                                                           
*                                                                               
         TM    DR.PSFLAG1,CTPHSCRQ IGNORE UNLESS CORERES                        
         BZ    BLDP06                                                           
*                                                                               
         MVC   PSREC,DPSREC        COPY RECORD ACROSS                           
         MVC   LC.PSADR,ACORE      SET CORE LOAD ADDRESS                        
         XC    LC.PSCHAIN,LC.PSCHAIN                                            
*                                                                               
         TM    DR.PSFLAG1,CTPHSDMQ DUMMY PHASE?                                 
         BZ    BLDP08              NO                                           
         ICM   R0,15,LC.PSADR      CLEAR STORAGE FOR DUMMY PHASE                
         ICM   R1,15,LC.PSLENH                                                  
         XR    RF,RF                                                            
         MVCL  R0,RE                                                            
         B     BLDP20                                                           
*                                                                               
BLDP08   OC    APARMS,APARMS       PHLIST PASSED?                               
         BZ    BLDP18              NO                                           
*                                                                               
         MVC   DUB,SPACES                                                       
         GOTO1 VHEXOUT,DMCB,DR.PSNAME,DUB,3,0                                   
         MVC   DUB+0(1),DR.PSLANG                                               
         MVC   DUB+6(1),DR.PSLVL                                                
         CLI   DUB+6,C' '                                                       
         BH    *+8                                                              
         MVI   DUB+6,C' '                                                       
*                                                                               
         L     RE,APARMS                                                        
         CLC   =CL4'ADDR',0(RE)                                                 
         BNE   *+8                                                              
         L     RE,8(RE)                                                         
         LHI   RF,L'PHLIST                                                      
BLDP10   CLI   0(RE),C' '          END OF LIST                                  
         BNH   BLDP18                                                           
         CLC   DUB,0(RE)                                                        
         BE    BLDP12                                                           
         BXH   RE,RF,BLDP10                                                     
*                                                                               
BLDP12   MVC   DUB,8(RE)           OVERRIDE NAME                                
         ICM   RF,15,LC.PSADR                                                   
         GOTO1 VLOADER,DMCB,DUB,(RF),(C'M',(RF))                                
         ICM   R0,15,4(R1)                                                      
         BNZ   BLDP16                                                           
*                                                                               
BLDP14   LHI   R1,3                PRINT WARNING ABOUT PHASE NOT FOUND          
         MVC   XTRAMESS,DUB                                                     
         BRAS  RE,PUTMESS                                                       
         B     BLDP18              LOAD FROM DATASPACE                          
*                                                                               
BLDP16   ICM   R0,15,0(R1)         PHASE LOADED FROM LOADLIB                    
         STCM  R0,15,LC.PSLEN      SET LENGTH                                   
         AHI   R0,63                                                            
         SRDL  R0,6                                                             
         SLL   R0,6                SET MOD 64                                   
         SRL   R1,32-6                                                          
         CHI   R1,32               MAKE SURE AT LEAST 32 BYTES OF HDR           
         BH    *+8                                                              
         AHI   R0,32               THERE ARE NOW...                             
         STCM  R0,15,LC.PSLENH                                                  
*                                                                               
         LHI   R1,5                PHASE LOADED FROM LOADLIB                    
         MVC   XTRAMESS,DUB                                                     
         BRAS  RE,PUTMESS                                                       
         B     BLDP20                                                           
*                                                                               
BLDP18   ICM   R1,15,LC.PSLEN                                                   
         ICM   R0,15,LC.PSLENH                                                  
         SR    R0,R1               GET AMOUNT OF SLACK IN DATASPACE             
         ICM   R1,15,LC.PSLENH                                                  
         CHI   R0,128              MAKE SURE AT LEAST 128 BYTES HERE            
         BNL   BLDP19                                                           
         ICM   R1,15,LC.PSLEN                                                   
         AHI   R1,128              MAKE SURE AT LEAST LEN+128                   
         AHI   R1,63                                                            
         SRL   R1,6                                                             
         SLL   R1,6                SET MOD 64                                   
         ST    R1,LC.PSLENH                                                     
*                                                                               
BLDP19   ICM   R0,15,LCORE                                                      
         BZ    BLDP19A                                                          
         ICM   RF,15,LC.PSLENH                                                  
         A     RF,ACORE                                                         
         CR    RF,R0                                                            
         BNH   BLDP19A                                                          
*                                                                               
         LHI   R1,6                                                             
         BRAS  RE,PUTMESS                                                       
         ABEND 101,DUMP                                                         
*                                                                               
BLDP19A  BRAS  RE,ON31                                                          
         LAM   R0,RF,ARZERO                                                     
         LAM   RE,RE,SSBPGMTA                                                   
         ICM   RE,15,DR.PSADR                                                   
         ICM   RF,15,DR.PSLEN                                                   
         ICM   R0,15,LC.PSADR                                                   
         ICM   R1,15,LC.PSLENH                                                  
         SAC   512                                                              
         MVCL  R0,RE               COPY PHASE FROM DATASPACE TO CORE            
         SAC   0                                                                
         LAM   RE,RE,ARZERO                                                     
         BRAS  RE,OFF31                                                         
*                                                                               
BLDP20   L     R0,ACORE            UPDATE CORE ADDRESS                          
         ICM   RF,15,LC.PSLENH                                                  
         AR    R0,RF                                                            
         AHI   R0,7                ROUND TO NEXT DOUBLEWORD                     
         SRL   R0,3                                                             
         SLL   R0,3                                                             
         ST    R0,ACORE                                                         
*                                                                               
         MVI   C.LIACTN,LIAADD     ADD RECORD TO BUFFER                         
         OI    C.LIFLAG1,LIF1INS                                                
         LA    RF,PSREC            GET A(RECORD)                                
         ST    RF,C.LIAREC                                                      
         MVC   C.LIRECL,=AL2(PROGSPCL)                                          
         GOTO1 VARREDIT,DMCB,C.LIBUFFD                                          
         CLI   C.LIRTN,0                                                        
         BE    BLDP06                                                           
         LHI   R1,4                                                             
         BRAS  RE,PUTMESS                                                       
         ABEND 102,DUMP                                                         
*                                                                               
BLDP22   MVI   C.LIACTN,LIAFIX     FIX ARRAY (TURN TO LINKED LIST)              
         GOTO1 VARREDIT,DMCB,C.LIBUFFD                                          
*                                                                               
         BRAS  RE,ON31             SAVE PARAMETER LIST                          
         L     RF,VPHLIST                                                       
         MVC   0(LIBUFFL,RF),C.LIBUFFD                                          
         BRAS  RE,OFF31                                                         
*                                                                               
         L     RF,ACORE            MARK END OF PHASE LOAD AREAS                 
         AHI   RF,15                                                            
         SRL   RF,4                                                             
         SLL   RF,4                                                             
         MVC   00(16,RF),=CL16'*END OF CORERES*'                                
         MVC   16(16,RF),=CL16'*END OF CORERES*'                                
         AHI   RF,32                                                            
         ST    RF,ACORE            UPDATE CORE ADDRESS                          
*                                                                               
         L     R1,SAVER1           RETURN A(END OF BUILD AREA)                  
         ST    RF,4(R1)                                                         
*                                                                               
         LA    R1,2                                                             
         BRAS  RE,PUTMESS                                                       
         B     EXITOK                                                           
         DROP  C,D,LC,DR                                                        
         EJECT                                                                  
***********************************************************************         
* PUT A MESSAGE TO THE OPERATOR CONSOLE AND OPTIONALLY GET A REPLY.   *         
* ON ENTRY R1=MESSAGE NUMBER AND XTRAMESS CAN OPTIONALLY CONTAIN 8    *         
* CHARACTERS TO BE INSERTED INTO MESSAGE.                             *         
***********************************************************************         
         SPACE 1                                                                
PUTMESS  NTR1  ,                                                                
         AHI   R1,-1                                                            
         BM    PUTMESS1            R1=ZERO MEANS R8=A(MESSAGE)                  
*                                                                               
         MHI   R1,L'MESSTAB                                                     
         LA    R8,MESSTAB(R1)      R8=A(MESSTAB ENTRY)                          
*                                                                               
PUTMESS1 GOTO1 ,PM1,VWCTYPE,MESSAGE,60                                          
*                                                                               
PUTMESS2 MVC   MESSAGE,SPACES      BUILD MESSAGE                                
         MVC   MESSAGE(8),=C'*FACPAK*'                                          
         MVC   MESSAGE+4(3),SSBSYSNA                                            
         MVC   MESSAGE+9(L'MESSTAB-1),1(R8)                                     
         LA    R1,MESSAGE+9                                                     
         LA    R0,L'MESSTAB-8                                                   
         CLC   0(8,R1),=8C'X'      REPLACE SPECIAL CHARACTERS                   
         BE    *+16                                                             
         LA    R1,1(R1)                                                         
         BCT   R0,*-14                                                          
         B     *+10                                                             
         MVC   0(8,R1),XTRAMESS                                                 
         LA    R2,DMCB                                                          
*                                                                               
         GOTO1 VDMOD000,PM1                                                     
         CLI   0(R8),C'X'                                                       
         BE    EXITOK                                                           
         TM    0(R8),X'F0'                                                      
         BO    *+12                                                             
         LA    R8,L'MESSTAB(R8)    BUMP MSG PTR FOR MULTIPLE MESSAGES           
         B     PUTMESS2                                                         
*                                                                               
         MVC   PM1(1),0(R8)        REQUEST A REPLY FROM THE OPERATOR            
         NI    PM1,X'0F'                                                        
         XR    R0,R0                                                            
         IC    R0,PM1                                                           
         GOTO1 (RF),(R1),VRCTYPE,REPLY,(R0)                                     
*                                                                               
         CLC   REPLY(4),=C'DUMP'   CHECK FOR DUMP REPLY                         
         BNE   EXITOK                                                           
         ABEND 103,DUMP                                                         
         EJECT                                                                  
***********************************************************************         
* EXIT POINTS AND OTHER HANDY ROUTINES                                *         
***********************************************************************         
         SPACE 1                                                                
ON31     O     RE,=XL4'80000000'                                                
         BSM   0,RE                                                             
*                                                                               
OFF31    N     RE,=XL4'7FFFFFFF'                                                
         BSM   0,RE                                                             
*                                                                               
EXITOK   CR    RB,RB                                                            
         B     EXIT                                                             
*                                                                               
EXITL    CLI   *,255                                                            
         B     EXIT                                                             
*                                                                               
EXIT     XIT1  ,                                                                
         ORG   *-2                                                              
         BSM   0,RE                                                             
         EJECT                                                                  
***********************************************************************         
* LITERALS AND CONSTANTS                                              *         
***********************************************************************         
         SPACE 1                                                                
ARZERO   DC    16F'0'                                                           
VSYSFAC  DC    V(SYSFAC)                                                        
VHEXOUT  DC    V(HEXOUT)                                                        
VLOADER  DC    V(LOADER)                                                        
SPACES   DC    CL16' '                                                          
*                                                                               
REPLY    DC    CL08' '                                                          
XTRAMESS DC    CL08' '                                                          
MESSAGE  DC    CL60' '                                                          
         EJECT                                                                  
***********************************************************************         
* LITERAL POOL                                                        *         
***********************************************************************         
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* MESSAGE TABLE ENTRIES                                               *         
***********************************************************************         
         SPACE 1                                                                
MESSTAB  DS    0CL50                                                            
MESS1    DC    CL50'XBeginning building phase lists in core'                    
MESS2    DC    CL50'XCompleted building phase lists in core'                    
MESS3    DC    CL50'X*WARNING* PHLIST Entry XXXXXXXX not found'                 
MESS4    DC    CL50'XProblem with ARREDIT - program abending'                   
MESS5    DC    CL50'XPHLIST Entry XXXXXXXX loaded from LOADLIB'                 
MESS6    DC    CL50'XPHASES WILL NOT FIT IN CORE AREA - MUST ABEND'             
         EJECT                                                                  
***********************************************************************         
* WORKING STORAGE DSECT                                               *         
***********************************************************************         
         SPACE 1                                                                
WORKD    DSECT                                                                  
DUB      DS    D                                                                
DUB1     DS    D                                                                
SAVER1   DS    A                                                                
SAVERD   DS    A                                                                
FULL     DS    F                                                                
*                                                                               
IPARMS   DS    0XL20                                                            
ICORE    DS    A                                                                
LCORE    DS    A                                                                
AXALIST  DS    A                                                                
LXALIST  DS    A                                                                
APARMS   DS    A                                                                
*                                                                               
DMCB     DS    0XL24                                                            
DM1      DS    F                                                                
DM2      DS    F                                                                
DM3      DS    F                                                                
DM4      DS    F                                                                
DM5      DS    F                                                                
DM6      DS    F                                                                
*                                                                               
PM1      DS    F                                                                
PM2      DS    F                                                                
PM3      DS    F                                                                
PM4      DS    F                                                                
*                                                                               
ACORE    DS    A                                                                
WORK     DS    XL80                                                             
*                                                                               
         DS    0F                                                               
PSREC    DS    XL(PROGSPCL)                                                     
         DS    0F                                                               
DPSREC   DS    XL(PROGSPCL)                                                     
*                                                                               
         DS    0F                                                               
LIBUFFR  DS    XL(LIBUFFL)                                                      
         DS    0F                                                               
LIBUFFC  DS    XL(LIBUFFL)                                                      
*                                                                               
WORKL    EQU   *-WORKD                                                          
         EJECT                                                                  
***********************************************************************         
* OTHER INCLUDED DSECTS                                               *         
***********************************************************************         
         SPACE 1                                                                
*DDARREDITD                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDARREDITD                                                     
         PRINT ON                                                               
*FADSECTS                                                                       
         PRINT OFF                                                              
       ++INCLUDE FADSECTS                                                       
         PRINT ON                                                               
*DMSPACED                                                                       
         PRINT OFF                                                              
       ++INCLUDE DMSPACED                                                       
         PRINT ON                                                               
*DMDSYSHDR                                                                      
         PRINT OFF                                                              
       ++INCLUDE DMDSYSHDR                                                      
         PRINT ON                                                               
*DMDSHDR                                                                        
         PRINT OFF                                                              
       ++INCLUDE DMDSHDR                                                        
         PRINT ON                                                               
*FAPGMSD                                                                        
         PRINT OFF                                                              
       ++INCLUDE FAPGMSD                                                        
         PRINT ON                                                               
*FAPGMSDEQU                                                                     
         PRINT  OFF                                                             
       ++INCLUDE FAPGMSDEQU                                                     
         PRINT  ON                                                              
*FAPARMDEF                                                                      
         PRINT  OFF                                                             
       ++INCLUDE FAPARMDEF                                                      
         PRINT  ON                                                              
*CTGENFILE                                                                      
         PRINT  OFF                                                             
       ++INCLUDE CTGENFILE                                                      
         PRINT  ON                                                              
*FAPROGSPCD                                                                     
         PRINT  OFF                                                             
       ++INCLUDE FAPROGSPCD                                                     
         PRINT  ON                                                              
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'016FAPROGBLD 09/10/15'                                      
         END                                                                    
