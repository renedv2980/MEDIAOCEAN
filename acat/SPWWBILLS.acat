*          DATA SET SPWWBILLS  AT LEVEL 043 AS OF 07/24/06                      
*CATALP SPB1WW                                                                  
         TITLE 'WW BILLING INTERFACE'                                           
SPB1WW   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,SPB1WW                                                         
*                                                                               
         L     RC,0(R1)            ADDRESS OF SPREPWORKD                        
         LA    R9,2048(RC)                                                      
         LA    R9,2048(R9)                                                      
         USING SPWORKD,RC,R9                                                    
         MVC   USERRD,4(RD)        SAVE LINK BACK TO USER                       
*                                                                               
         L     R8,4(R1)            ADDRESS OF SPWWB1BLK                         
         USING SPWWD,R8                                                         
*                                                                               
         LA    RA,2048(RB)                                                      
         LA    RA,2048(RA)                                                      
         USING SPB1WW+4096,RA                                                   
*                                                                               
         B     WW01                                                             
         SPACE 2                                                                
EXIT     XIT1                                                                   
         EJECT                                                                  
*                                                                               
WW01     MVI   LASTSW,C'N'         INIT FOR LAST PHYSICAL REC                   
*                                                                               
* GET BROADCAST START AND END DATES OF REQUESTED MONTH                          
*                                                                               
WW02     XC    DUB,DUB                                                          
         SR    R0,R0                                                            
         ICM   R0,1,SPWWYM                                                      
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'         SIGN BYTE                                    
         UNPK  THISDATE(2),DUB                                                  
*                                                                               
         XC    DUB,DUB                                                          
         SR    R0,R0                                                            
         ICM   R0,1,SPWWYM+1                                                    
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'         SIGN BYTE                                    
         UNPK  THISDATE+2(2),DUB                                                
*                                                                               
         MVC   THISDATE+4(2),=C'01'                                             
         GOTO1 GETBROAD,DMCB,(1,THISDATE),STDATE,GETDAY,ADDAY                   
*                                                                               
         XC    KEY,KEY                                                          
         LA    R1,KEY                                                           
         USING INPKEY,R1                                                        
*                                                                               
         MVI   INPKTYPE,INPKTYPQ   SET UP PASSIVE PTR                           
         MVI   INPKSTYP,INPKSTPQ                                                
         MVC   INPKAM,SPWWAGMD                                                  
         MVC   INPKCLT,SPWWCLT                                                  
         MVC   INPKPRD,SPWWPRD                                                  
         MVC   INPKCMP,SPWWEST                                                  
*                                                                               
         GOTO1 HIGH                                                             
         DROP  R1                                                               
         EJECT                                                                  
*                                                                               
WW05     MVI   LASTSW,C'N'                                                      
         MVC   PASSIVE,KEY         SAVE PASSIVE KEY                             
*                                                                               
         CLI   SPWWEST,0           WAS EST REQUESTED = 0                        
         BNE   WW07                                                             
*                                                                               
WW06     XR    R1,R1                                                            
         IC    R1,KEY+7                                                         
         LA    R1,ESTLST(R1)       ENTRY IN ESTLST                              
*                                                                               
         CLI   0(R1),0             CAN USE THIS EST                             
         BNE   WW06A               YES                                          
*                                                                               
         BAS   RE,GEST             ELSE GET NEXT EST                            
         CLI   VALEST,C'Y'         IF IT IS VALID EST                           
         BE    WW06                THEN CHECK IT                                
         B     WWX                 ELSE IT WAS NEXT PRD                         
*                                                                               
WW06A    CLC   KEY(6),KEYSAVE      SAME AM/CLT/PRD                              
         BNE   WWX                 LAST MKT & EST                               
         B     WW08                                                             
*                                                                               
WW07     CLC   KEY(7),KEYSAVE      COMPLETED REQUEST                            
         BNE   WWX                 LAST MKT & EST                               
*                                                                               
         USING INVTABD,R3                                                       
WW08     LA    R3,INVTAB                                                        
         XCEF  INVTAB,513          CLEAR TABLE                                  
         MVI   0(R3),X'FF'         END OF TABLE MARKER                          
*                                                                               
         L     R6,SPWWAIO                                                       
         ST    R6,AREC                                                          
*                                                                               
         GOTO1 GET                                                              
*                                                                               
         MVC   MASTKEY,0(R6)       SAVE MASTER KEY                              
*                                                                               
* GET MARKET CODE AND TAX RATE FROM STATION REC                                 
*                                                                               
         XC    KEY,KEY                                                          
         MVC   SVCLT,PASSIVE+3                                                  
         GOTO1 CLUNPK,DMCB,SVCLT,ZCLT                                           
*                                                                               
         XC    MYMKT(5),MYMKT      CLEAR MKT/STA                                
         MVC   MYSTA,PASSIVE+7     PACKED STA                                   
         MVC   SPWWSTA,MYSTA                                                    
*                                                                               
         GOTO1 MSUNPK,DMCB,MYMKSTA,ZMKT,ZSTA                                    
*                                                                               
         CLI   ZSTA+4,C' '         IF BLANK INSERT T                            
         BNE   WW09                                                             
         MVI   ZSTA+4,C'T'                                                      
*                                                                               
WW09     MVI   KEY,C'0'            REC IS INIT WITH C'0'                        
         MVC   KEY+1(16),KEY                                                    
         MVI   KEY,C'S'            STATION                                      
         MVC   KEY+1(1),QMED       MEDIA                                        
         MVC   KEY+2(5),ZSTA       STATION                                      
         MVC   KEY+7(2),QAGY       ALPHA AGY                                    
         MVC   KEY+9(3),ZCLT       CLIENT                                       
*                                                                               
         GOTO1 READSTA             READ STATION REC                             
*                                                                               
         L     R1,ADSTAT           ADDRESS OF STATION REC                       
         USING STAREC,R1                                                        
*                                                                               
         PACK  DUB,SMKT                                                         
         CVB   R0,DUB                                                           
         STH   R0,SPWWMKT          OUTPUT                                       
*                                                                               
         DROP  R1                                                               
         EJECT                                                                  
*                                                                               
* GET INVOICE RECORD                                                            
*                                                                               
         XC    KEY,KEY                                                          
         MVC   KEY(7),MASTKEY      READ FROM PASSIVE PTR                        
         GOTO1 HIGH                                                             
         B     WW15                                                             
*                                                                               
WW10     GOTO1 SEQ                                                              
*                                                                               
WW15     CLC   KEY(7),KEYSAVE          MUST BE THERE                            
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         CLC   KEY(13),MASTKEY         IS THIS THE LAST REC                     
         BNE   WW20                                                             
         MVI   LASTSW,C'Y'                                                      
*                                                                               
WW20     GOTO1 GET                                                              
         L     R6,SPWWAIO       1ST ELEMENT                                     
         LA    R6,24(R6)                                                        
*                                                                               
WW25     CLI   0(R6),0             END OF PHYSICAL RECORD                       
         BE    WW100                                                            
*                                                                               
         USING IHELEM,R6                                                        
         CLI   0(R6),IHCODEQ       INVOICE HEADER                               
         BNE   WW60                                                             
*                                                                               
* REACHED AN INVOICE HEADER ELEMENT                                             
*                                                                               
         CLC   IHMONTH(1),SPWWYM+1    CORRECT MONTH?                            
         BNE   WW70                   NO GET NEXT ELEM                          
*                                                                               
         CLC   IHYEAR(1),SPWWYM       CORRECT YEAR                              
         BNE   WW70                   NO                                        
         EJECT                                                                  
*                                                                               
* REACHED A REQUESTED INVOICE HEADER ELEMENT                                    
*                                                                               
         MVC   TINVID,IHID         MOVE ID TO TABLE                             
         MVC   TINVSEQ,IHSEQ       MOVE SEQUENCE NUMBER                         
*                                                                               
         MVI   TINVAPP,C'O'        ORDERED                                      
         OC    IHBUYER,IHBUYER                                                  
         BZ    WW55                                                             
         MVI   TINVAPP,C'P'        PAID                                         
*                                                                               
* COMPUTE TAX                                                                   
*                                                                               
WW55     XR    R1,R1               CLEAR FOR ARITHMETIC                         
         ICM   R1,3,IHTAX          TAX                                          
         M     R0,=F'170'          TAKE 85% OF TAX                              
         D     R0,=F'100'                                                       
         A     R1,=F'1'            ROUNDING                                     
         SRA   R1,1                                                             
*                                                                               
         A     R1,=F'100000'       ADD TO 85% OF TAX 100 FOR DIVIDE             
*                                                                               
         ICM   R5,15,IHAMNT         GROSS IN PENNIES                            
         M     R4,=F'200000'                                                    
*                                                                               
         DR    R4,R1                                                            
         A     R5,=F'1'            ROUND                                        
         SRA   R5,1                DIVIDE BY 2                                  
*                                                                               
         L     RF,TINVGRS          ACTUAL GROSS                                 
         AR    RF,R5                                                            
         ST    RF,TINVGRS                                                       
*                                                                               
         ICM   R0,15,IHAMNT                                                     
         SR    R0,R5               SUB ACTUAL GROSS FROM GRS + TAX              
*                                                                               
         L     RF,TINVTAX                                                       
         AR    RF,R0               ACTUAL TAX PAID                              
         ST    RF,TINVTAX                                                       
*                                                                               
         LR    R1,R5               ACTUAL GROSS                                 
         M     R0,=F'170'          TAKE 85% OF GROSS = NET                      
         D     R0,=F'100'                                                       
         A     R1,=F'1'            ROUND                                        
         SRA   R1,1                                                             
*                                                                               
         L     RF,TINVNET                                                       
         AR    RF,R1                                                            
         ST    RF,TINVNET                                                       
*                                                                               
         LA    R3,L'INVTAB(R3)     BUMP TABLE                                   
         MVI   0(R3),X'FF'         MARK END OF TABLE                            
*                                                                               
         B     WW70                GET NEXT ELEM                                
         DROP  R6                                                               
*                                                                               
         USING IEELEM,R6                                                        
WW60     CLI   0(R6),IECODEQ       ENTRY ELEM                                   
         BNE   WW70                NO GET NEXT ELEM                             
*                                                                               
         LA    R3,INVTAB                                                        
*                                                                               
* CHECK IF IN CORRECT BROADCAST MONTH                                           
*                                                                               
         GOTO1 DATCON,DMCB,(2,IEDATE),(0,THISDATE)                              
         CLC   THISDATE,STDATE                                                  
         BL    WW70                GET NEXT ELEM                                
*                                                                               
         CLC   THISDATE,ENDDATE                                                 
         BH    WW70                                                             
*                                                                               
WW65     CLI   0(R3),X'FF'                                                      
         BE    WW70                                                             
*                                                                               
         CLC   IESEQ,TINVSEQ       CORRECT SEQ NUM IN TABLE                     
         BNE   WW67                NO, BUMP TABLE                               
*                                                                               
* THIS IS THE CORRECT ENTRY IN THE TABLE                                        
*                                                                               
         L     R1,TSPOTS           COUNT NUMBER OF SPOTS                        
         LA    R1,1(R1)                                                         
         ST    R1,TSPOTS                                                        
         B     WW70                                                             
*                                                                               
WW67     LA    R3,L'INVTAB(R3)                                                  
         B     WW65                                                             
         DROP  R6                                                               
*                                                                               
WW70     SR    R0,R0               BUMP TO NEXT ELEM                            
         ICM   R0,1,1(R6)                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         AR    R6,R0                                                            
         B     WW25                                                             
*                                                                               
* END OF PHYSICAL RECORD                                                        
*                                                                               
WW100    CLI   LASTSW,C'N'                                                      
         BE    WW10                GET THE NEXT REC                             
         EJECT                                                                  
*                                                                               
* FINISHED LOGICAL RECORD - 1 MARKET - SEND INFO                                
*                                                                               
         LA    R3,INVTAB                                                        
*                                                                               
WW120    CLI   0(R3),X'FF'                                                      
         BE    WW150                                                            
*                                                                               
         MVC   SPWWDATA(1),TINVAPP                                              
         MVC   SPWWCMT(12),TINVID  INVOICE ID                                   
         MVC   SPWWGRS,TINVGRS     TOTAL GROSS OF INVOICE                       
         MVC   SPWWNET,TINVNET     TOTAL NET   OF INVOICE                       
         MVC   SPWWTAX,TINVTAX     TOTAL TAX   OF INVOICE                       
         MVC   SPWWSPTS,TSPOTS     TOTAL NUMBER OF SPOTS                        
*                                                                               
         BAS   RE,GO1                                                           
*                                                                               
         XC    SPWWCMT(31),SPWWCMT CLEAR AREA                                   
         LA    R3,L'INVTAB(R3)     BUMP TABLE                                   
         B     WW120                                                            
*                                                                               
* NEXT MARKET                                                                   
*                                                                               
WW150    MVC   KEY,PASSIVE         RESTORE PASSIVE KEY                          
         GOTO1 HIGH                REREAD LAST RECORD                           
         CLC   KEY(13),KEYSAVE                                                  
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         GOTO1 SEQ                 AND THEREBY GET NEXT REC                     
         B     WW05                                                             
*                                                                               
WWX      B    EXIT                                                              
         EJECT                                                                  
*                                                                               
GO1      NTR1                                                                   
         ICM   RF,15,SPWWHOOK                                                   
         BZ    GOX                                                              
*                                                                               
         L     RE,USERRD                                                        
         LM    R0,RC,20(RE)                                                     
         BASR  RE,RF                                                            
*                                                                               
GOX      XIT1                                                                   
*                                                                               
* IF EST NOT GIVEN, AND 1ST READ NOT APPLICABLE, GET NEXT                       
* ESTIMATE                                                                      
*                                                                               
GEST     NTR1                                                                   
         MVI   VALEST,C'Y'         VALID EST                                    
         MVC   WORK(L'KEY),KEY     SAVE KEY FROM PREV READ                      
         L     R6,ADEST            ADDRESS OF ESTIMATE HEADER                   
*                                                                               
         XC    KEY,KEY                                                          
         SR    R1,R1                                                            
         ICM   R1,1,7(R6)                                                       
         BNZ   GEST10                                                           
         DC    H'0'                                                             
*                                                                               
GEST10   LA    R1,1(R1)            SET EST NUM = EST MUM + 1                    
         MVC   KEY(7),0(R6)                                                     
         STCM  R1,1,KEY+7                                                       
         GOTO1 HIGH                READ NEXT  EST                               
         CLC   KEY(6),KEYSAVE      AM/CLT/PRD                                   
         BE    GEST20                                                           
         MVI   VALEST,C'N'                                                      
         B     GESTX                                                            
*                                                                               
GEST20   GOTO1 GETEST              READ THE RECORD                              
*                                                                               
GESTX    MVC   KEY,WORK            RESTORE LAST READ REC                        
         GOTO1 HIGH                                                             
         B     EXIT                                                             
*                                                                               
         EJECT                                                                  
         SPACE 2                                                                
         DS    0D                                                               
         DC    CL8'*STORAGE'                                                    
LASTSW   DS    CL1                 LAST PHYSICAL RECORD SWITCH                  
VALEST   DS    CL1                 VALID EST FOR REQUEST                        
NOEST    DS    CL1                 USE EST OR NOT                               
USERRD   DS    A                                                                
*                                                                               
MYMKSTA  DS    0XL5                                                             
MYMKT    DS    XL2                 B MARKET                                     
MYSTA    DS    XL3                 PACKED STATION                               
*                                                                               
ZMKT     DS    CL4                 EBCDIC MARKET                                
ZSTA     DS    CL5                 EBCDIC STATION                               
ZCLT     DS    CL3                 EBCDIC CLIENT                                
ZPRD     DS    CL3                 EBCDIC PRODUCT                               
*                                                                               
SVTAX    DS    CL2                 TAX RATE                                     
*                                                                               
STDATE   DS    CL6                 START BROADCAST DATE                         
ENDDATE  DS    CL6                 END BROADCAST DATE                           
*                                                                               
THISDATE DS    CL6                                                              
*                                                                               
PASSIVE  DS    XL18                                                             
MASTKEY  DS    XL13                                                             
         DS    0D                                                               
         DC    CL8'*TABLE**'                                                    
INVTAB   DS    16CL32              TABLE OF INVOICES                            
INVTABX  DS    CL1                 END OF TABLE MARKER                          
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
       ++INCLUDE SPREPWORKD                                                     
         EJECT                                                                  
       ++INCLUDE SPB1WWBLK                                                      
         EJECT                                                                  
*                                                                               
INVTABD  DSECT                                                                  
TINVID   DS    CL12                                                             
TINVSEQ  DS    XL1                                                              
TINVAPP  DS    CL1                                                              
TINVGRS  DS    F                                                                
TINVNET  DS    F                                                                
TINVTAX  DS    F                                                                
TSPOTS   DS    F                                                                
TTABX    EQU   *-TINVID                                                         
         EJECT                                                                  
       ++INCLUDE SPGENWBS                                                       
       ++INCLUDE SPGENCLT                                                       
       ++INCLUDE SPGENSTA                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'043SPWWBILLS 07/24/06'                                      
         END                                                                    
