*          DATA SET DELDBALC   AT LEVEL 013 AS OF 10/18/06                      
*CATALP DELDBALC                                                                
         TITLE 'LDBALCK - DEMOS - FILE BALANCE CHECK/PRINT'                     
*        PARAMS VIA R1                                                          
*        XL1   COUNT/PRINT  X'01'=COUNT  X'FF'=PRINT                            
*        AL3   A(RECORD)                                                        
*                                                                               
LDBALCK  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,DELDBALC                                             
         USING WORKD,RC                                                         
         LR    R2,R1               R2=A(PARAMETER LIST)                         
         CLI   0(R2),X'FF'         TEST PRINT                                   
         BE    LD10                                                             
*                                                                               
         XC    WORK,WORK                                                        
REC      USING ACCUMSD,WORK        BUILD BINSRCH RECORD IN WORK                 
*                                                                               
         L     R3,0(R2)                                                         
         USING MRKEY,R3            MPA FILE                                     
         CLC   MRCODE(2),=C'MP'                                                 
         BNE   LD2                                                              
*                                                                               
         MVC   REC.ACCMED,MRMEDIA  BUILD MPA BINSRCH RECORD IN WORK             
         MVC   REC.ACCSRC,MRSRC                                                 
         MVC   REC.ACCBOOK,MRMBOOK                                              
         B     LD5                                                              
         DROP  R3                                                               
*                                                                               
LD2      DS    0H                                                               
         USING DRKEY,R3            REGULAR DEMO FILES                           
         CLI   DRCODE,DRCODEQU     TEST DEMO RECORD                             
         BE    *+12                                                             
         CLI   DRCODE,C'D'         TEST RADIO DAYPART RECORD                    
         BNE   LDX                                                              
*                                                                               
         MVC   REC.ACCMED,DRMEDIA                                               
         MVC   REC.ACCSRC,DRSRC                                                 
         MVC   REC.ACCBOOK,DRBOOK                                               
         XC    REC.ACCBOOK,=X'FFFF'                                             
         MVC   REC.ACCBTYP,DRBTYP                                               
         DROP  R3                                                               
         DROP  REC                                                              
*                                                                               
LD5      DS    0H                                                               
         GOTO1 =V(BINSRCH),DMCB,(X'01',WORK),ACCUMS,ACCUMSN,ACCUMSL,   +        
               ACCKEYL,ACCUMSMX                                                 
         MVC   ACCUMSN,DMCB+8      SAVE NUMBER OF RECS IN TABLE                 
         SR    R4,R4                                                            
         ICM   R4,7,1(R1)                                                       
         BNZ   *+6                 ACCUMS TABLE IS FULL                         
         DC    H'0'                                                             
*                                                                               
         USING ACCUMSD,R4                                                       
         ICM   RF,15,ACCRECS       BUMP NUMBER OF RECS FOR THIS KEY             
         AHI   RF,1                                                             
         STCM  RF,15,ACCRECS                                                    
         DROP  R4                                                               
*                                                                               
         B     LDX                                                              
         EJECT                                                                  
LD10     DS    0H                                                               
         ICM   R4,15,ACCUMSN                                                    
         BZ    LDX                                                              
*                                                                               
         L     RA,=V(CPRINT)                                                    
         USING DPRINT,RA                                                        
         MVC   TITLE,SPACES                                                     
         MVC   TITLE(L'MYTITLE),MYTITLE                                         
         MVC   MID1,SPACES                                                      
         MVC   MID2,SPACES                                                      
         MVC   MID3,SPACES                                                      
         MVC   MID4,SPACES                                                      
         MVC   SUB1(L'MYSUB1),MYSUB1                                            
         MVC   SUB2(L'MYSUB2),MYSUB2                                            
         ZAP   LINE,=P'99'                                                      
*                                                                               
         LA    R5,ACCUMS                                                        
         USING ACCUMSD,R5                                                       
LD18     LA    R1,MEDSRC                                                        
LD20     CLI   0(R1),C'X'                                                       
         BE    LD22                                                             
         CLC   0(2,R1),ACCMED                                                   
         BE    LD22                                                             
         LA    R1,L'MEDSRC(R1)                                                  
         B     LD20                                                             
LD22     MVC   P+0(8),2(R1)                                                     
         MVC   P+9(8),10(R1)                                                    
         ZIC   R1,ACCBOOK+1                                                     
*                                                                               
         CLI   ACCMED,C'W'         WEEKLIES HAVE WEEK NUMBER                    
         BNE   LD23                                                             
         CVD   R1,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  P+19(2),DUB                                                      
         B     LD24                                                             
*                                                                               
LD23     MHI   R1,3                OTHERS HAVE A MONTH                          
         LA    R1,MONTHS-3(R1)                                                  
         MVC   P+18(3),0(R1)                                                    
*                                                                               
LD24     MVI   P+21,C'/'                                                        
         ZIC   R1,ACCBOOK                                                       
         CVD   R1,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  P+22(2),DUB                                                      
*                                                                               
         CLI   ACCBTYP,0           NOW CHECK FOR A BOOK TYPE                    
         BNE   *+8                                                              
         MVI   ACCBTYP,C' '                                                     
         MVC   P+26(1),ACCBTYP                                                  
         CLI   ACCBTYP,X'E0'                                                    
         BNE   *+10                                                             
         MVC   P+26(5),=C'SPILL'                                                
         ICM   R1,15,ACCRECS                                                    
         CVD   R1,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  P+33(7),DUB                                                      
         GOTO1 =V(PRINTER)                                                      
         LA    R5,ACCUMSL(R5)                                                   
         BCT   R4,LD18                                                          
*                                                                               
LDX      XIT1  ,                                                                
         EJECT                                                                  
         LTORG                                                                  
         SPACE 2                                                                
MYTITLE  DC    C'DEMFILE RECORD COUNTS BY BOOK'                                 
MYSUB1   DC    C'MEDIA    SOURCE   BOOK    BOOK   RECORDS'                      
MYSUB2   DC    C'-----    ------   ----    TYPE   -------'                      
         SPACE 1                                                                
MEDSRC   DS    0CL18                                                            
         DC    C'PA',C'USA/MPA ',C'ARBITRON'                                    
         DC    C'TA',C'USA/TP  ',C'ARBITRON'                                    
         DC    C'PN',C'USA/MPA ',C'NIELSEN '                                    
         DC    C'TN',C'USA/TP  ',C'NIELSEN '                                    
         DC    C'TS',C'USA/TP  ',C'SRC     '                                    
         DC    C'WN',C'USA/TP  ',C'NSI/WKLY'                                    
         DC    C'RA',C'US/RADIO',C'ARBITRON'                                    
         DC    C'RM',C'CAN/RAD ',C'BBM     '                                    
         DC    C'RN',C'US/RADIO',C'NIELSEN '                                    
         DC    C'CA',C'CAN/TP  ',C'BBM     '                                    
         DC    C'CN',C'CAN/TP  ',C'CSI     '                                    
         DC    C'DN',C'USA/DPT ',C'NIELSEN '                                    
         DC    C'XX',C'UNKNOWN ',C'UNKNOWN '                                    
         SPACE 1                                                                
ACCUMSN  DC    A(0)                DEFINED AS A-TYPE FOR BINSRCH                
ACCUMS   DS    (ACCUMSMX)XL(ACCUMSL)                                            
ACCUMSMX EQU   3000                MAXIMUM NUMBER OF ACCUMULATORS               
         SPACE 1                                                                
WORKD    DSECT                                                                  
DUB      DS    D                                                                
DMCB     DS    6F                                                               
WORK     DS    XL24                                                             
WORKX    EQU   *                                                                
         SPACE 1                                                                
* DDDPRINT                                                                      
         PRINT OFF                                                              
       ++INCLUDE DDDPRINT                                                       
         PRINT ON                                                               
         SPACE 1                                                                
* DEDEMFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE DEDEMFILE                                                      
       ++INCLUDE DEMPAFILE                                                      
         PRINT ON                                                               
         SPACE 1                                                                
ACCUMSD  DSECT                                                                  
ACCBOOK  DS    XL2                 BOOK                                         
ACCMED   DS    CL1                 MEDIA CODE                                   
ACCSRC   DS    CL1                 SOURCE CODE                                  
ACCBTYP  DS    CL1                 BOOK TYPE                                    
ACCKEYL  EQU   *-ACCUMSD                                                        
ACCRECS  DS    XL4                 NUMBER OF RECORDS READ                       
ACCUMSL  EQU   *-ACCUMSD                                                        
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'013DELDBALC  10/18/06'                                      
         END                                                                    
