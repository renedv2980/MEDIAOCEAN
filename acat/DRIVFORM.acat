*          DATA SET DRIVFORM   AT LEVEL 021 AS OF 11/21/07                      
*CATALP DRIVFORM                                                                
         TITLE 'DRIVFORM - FORMAT OUTPUT FOR DRIVER'                            
DRIVFORM CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,DRFORM**                                                       
         USING GLOBALD,RA                                                       
         L     RC,AOCOND                                                        
         USING DOUTD,RC                                                         
         EJECT                                                                  
*              CONTROL DRIVER FORMATTING                                        
         SPACE 3                                                                
         USING DRIND,R2            R2=A(ASSOCIATED IN)                          
         USING DROD,R3             R3=A(OUT)                                    
*                                  R4=A(INPUT FIELD)                            
*                                  R5=A(PRINT POSITION)                         
         OC    DROACOMP,DROACOMP   DO WE NEED TO COMPUTE THIS FIELD             
         BZ    FORM2                                                            
         OC    DRORADD,DRORADD     IF HAVE USER ROUTINE DONT RECOMPUTE          
         BNZ   FORMNUM2                                                         
         MVC   DMCB+4(4),DROACOMP                                               
         MVC   DMCB+8(4),ATHISREC                                               
         GOTO1 =V(DRIVCOMP),DMCB,(RA)                                           
         TM    DROOPTS,X'10'       IF ABSOLUTE VALUE                            
         BNO   FORM1                                                            
         CP    DUB,=P'0'           AND IF NEGATIVE NUMBER                       
         BNL   FORM1                                                            
         MP    DUB,=P'-1'          MAKE POSITIVE                                
FORM1    DS    0H                                                               
         BAS   RE,CHEKDOWN         MAY BE DOWNCASTING COMPUTES                  
         LTR   R5,R5               CHECK FOR P=NO                               
         BZ    XIT                                                              
         B     FORMNUM2                                                         
         SPACE 1                                                                
FORM2    LTR   R5,R5               REJECT IF P=NO                               
         BZ    XIT                                                              
         L     R2,DROIADD                                                       
         USING DRIND,R2                                                         
         LTR   R2,R2                                                            
         BZ    XIT                 (NO INPUT FIELD TO HANDLE)                   
         L     R4,GLAIFLD                                                       
         ZIC   R1,DRINLEN                                                       
         BCTR  R1,0                                                             
         BAS   RE,POSTCUM                                                       
         CLI   DRINTYPE,C'C'       PICK INPUT TYPE                              
         BE    CHAR                                                             
         CLI   DRINTYPE,C'D'                                                    
         BE    DATE                                                             
         CLI   DRINTYPE,C'X'                                                    
         BE    HEX                                                              
         B     FORMNUM                                                          
         EJECT                                                                  
*              DOWNCASTING ROUTINES                                             
         SPACE 3                                                                
CHEKDOWN NTR1                                                                   
         USING DROD,R3                                                          
         TM    DROOPTS,X'80'       IS DOWNCAST SET?                             
         BNO   XIT                                                              
         CLI   LINETYPE,C'D'                                                    
         BE    DOWNDET                                                          
         ZIC   R2,GLLEVEL          TOTAL                                        
         SLL   R2,3                                                             
         LA    R2,CDWNDUBS(R2)     DISPLACE TO DUB FOR CURRENT LEVEL            
         MVC   DUB,0(R2)                                                        
         ZAP   0(8,R2),=P'0'                                                    
         B     XIT                                                              
         SPACE 1                                                                
DOWNDET  LA    R0,12               DETAIL                                       
         LA    R2,CDWNDUBS                                                      
         SPACE 1                                                                
DOWNDET2 AP    0(8,R2),DUB                                                      
         LA    R2,8(R2)                                                         
         BCT   R0,DOWNDET2                                                      
         B     XIT                                                              
         EJECT                                                                  
*              CUME ROUTINES                                                    
         SPACE 3                                                                
POSTCUM  NTR1                                                                   
         TM    DROFORM,X'01'       MUST BE SET TO CUME                          
         BNO   XIT                                                              
         CLI   DRINTYPE,C'P'                                                    
         BE    POSTCUMP                                                         
         L     R1,CUMEBIN                                                       
         A     R1,0(R4)                                                         
         ST    R1,CUMEBIN                                                       
         ST    R1,0(R4)                                                         
         B     XIT                                                              
         SPACE 1                                                                
POSTCUMP AP    CUMEPACK,0(8,R4)                                                 
         ZAP   0(8,R4),CUMEPACK                                                 
         B     XIT                                                              
         EJECT                                                                  
*              CHARACTER ROUTINES                                               
         SPACE 3                                                                
CHAR     CLI   DROTYPE,C'C'        PICK OUTPUT TYPE                             
         BE    CTOC                                                             
         CLI   DROTYPE,C'X'                                                     
         BE    CTOX                                                             
         B     XIT                                                              
         SPACE 1                                                                
*                                  CHARACTER TO CHARACTER ROUTINES              
CTOC     CLC   DRINLEN,DROLEN      INPUT LENGTH NOT GREATER THAN                
         BNH   ODDOUT              OUTPUT LENGTH                                
         SPACE 1                                                                
*                                  INPUT IS GREATER THAN OUTPUT                 
CTOC2    TM    DROFORM,X'10'       IS CHOPPING ALLOWED                          
         BO    CTOC4                                                            
         ZIC   R1,DROLEN           NO SO USE OUTPUT LENGTH                      
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     XIT                                                              
         MVC   0(0,R5),0(R4)       AND TRUNCATE                                 
         SPACE 1                                                                
CTOC4    ST    R4,DMCB             CHOPPING HERE                                
         MVC   DMCB(1),DRINLEN                                                  
         ST    R5,DMCB+4                                                        
         MVC   DMCB+4(1),DROLEN                                                 
         MVC   DMCB+8(4),=F'20'                                                 
         MVI   DMCB+8,198                                                       
         GOTO1 CHOPPER,DMCB                                                     
         B     XIT                                                              
         SPACE 1                                                                
CTOX     ZIC   R1,DROLEN           CHARACTER TO HEX                             
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         XC    0(0,R5),0(R5)       CLEAR OUTPUT FIRST                           
         ST    R4,DMCB                                                          
         ST    R5,DMCB+4                                                        
         LA    R1,1(R1)                                                         
         SLL   R1,1                                                             
         ZIC   R0,DRINLEN          ENSURE THAT INPUT LENGTH IS NOT              
         CR    R0,R1               MORE THAN TWICE THE OUTPUT LENGTH            
         BL    *+6                                                              
         LR    R0,R1                                                            
         ST    R0,DMCB+8                                                        
         GOTO1 HEXIN,DMCB                                                       
         B     XIT                                                              
         EJECT                                                                  
*              DATE AND HEX ROUTINES                                            
         SPACE 3                                                                
DATE     STM   R4,R5,DMCB                                                       
         MVC   DMCB(1),DRINTYPE+1                                               
         MVC   DMCB+4(1),DROTYPE+1                                              
         GOTO1 DATCON,DMCB                                                      
         B     XIT                                                              
         SPACE 1                                                                
HEX      CLI   DROTYPE,C'C'                                                     
         BE    XTOC                                                             
         CLI   DROTYPE,C'X'                                                     
         BE    XTOX                                                             
         B     XIT                                                              
         SPACE 1                                                                
*                                  HEX TO HEX ROUTINES                          
XTOX     CLC   DRINLEN,DROLEN      CHECK INPUT NOT TOO LONG                     
         BH    ODDOUT                                                           
         ZIC   R1,DROLEN                                                        
         BCTR  R1,0                                                             
         B     ODDOUT                                                           
         SPACE 1                                                                
XTOC     ZIC   R1,DROLEN           HEX TO CHARACTER                             
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R5),SPACES      CLEAR OUTPUT FIRST                           
         ST    R4,DMCB                                                          
         ST    R5,DMCB+4                                                        
         LA    R1,1(R1)                                                         
         SRL   R1,1                                                             
         ZIC   R0,DRINLEN          ENSURE THAT INPUT LENGTH IS NOT              
         CR    R0,R1               MORE THAN HALF THE OUTPUT LENGTH             
         BL    *+6                                                              
         LR    R0,R1                                                            
         ST    R0,DMCB+8                                                        
         GOTO1 HEXOUT,DMCB,,,,=C'TOG'                                           
         B     XIT                                                              
         SPACE 1                                                                
ODDOUT   EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R5),0(R4)                                                    
         GOTO1 =V(DRIVALIN)                                                     
         B     XIT                                                              
         EJECT                                                                  
*              OUTPUT NUMERIC                                                   
         SPACE 3                                                                
FORMNUM  XC    EBLOCK,EBLOCK       GENERAL NUMERIC                              
         CLI   GLABCMOD,1          DONT PROCESS FOR ABC MID LINES               
         BE    XIT                                                              
         ST    R4,EBAIN            R4=A(INPUT)                                  
         MVC   EBTIN,DRINTYPE      PICK UP INPUT TYPE                           
         MVC   EBLIN,DRINFLEN      AND LENGTH                                   
         B     FORMNUM4                                                         
         SPACE 1                                                                
*                                  NUMERIC VALUE IS PACKED IN DUB               
FORMNUM2 XC    EBLOCK,EBLOCK                                                    
         LA    R1,DUB                                                           
         ST    R1,EBAIN                                                         
         MVI   EBTIN,C'P'                                                       
         MVI   EBLIN,8                                                          
         SPACE 1                                                                
FORMNUM4 LTR   R5,R5               FINAL CHECK FOR P=NO                         
         BZ    XIT                                                              
         ST    R5,EBAOUT           R5=A(OUTPUT)                                 
         MVC   EBLOUT,DROLEN       PASS OTHER OUTPUT VALUES                     
         MVC   EBDECS,DRODEC                                                    
         MVC   EBFILL,DROFILL                                                   
         MVC   EBFLOAT,DROFLOAT                                                 
         MVC   EBROUND,DRODIV                                                   
         MVC   EBOPT,DROEDIT                                                    
         MVC   EBTRIM,DROFORM                                                   
         MVC   EBALIGN,DROALIGN                                                 
         MVI   EBPWIDTH,198                                                     
         MVC   EBSCOUT,DROSCALE                                                 
         MVC   EBTRAIL,DROTRAIL                                                 
         BAS   RE,ADJEDIT                                                       
         TM    GLINDS3,GLNOOVER    ARE OVERFLOWS DISALLOWED?                    
         BZ    FORMNUM6                                                         
         CLI   EBTIN,C'B'          YES-APPLIES TO BINARY LENGTH 4 ONLY          
         BNE   FORMNUM6                                                         
         CLI   EBLIN,4                                                          
         BNE   FORMNUM6                                                         
         CLC   0(4,R4),=X'80000000'  HAS IT OVERFLOWED?                         
         BNE   FORMNUM6                                                         
         ZIC   R1,EBLOUT           YES-JUST PRINT *'S                           
         BCTR  R1,0                                                             
         BCTR  R1,0                                                             
         MVI   0(R5),C'*'                                                       
         EX    R1,*+8                                                           
         B     XIT                                                              
         MVC   1(0,R5),0(R5)                                                    
*                                                                               
FORMNUM6 DS    0H                                                               
         GOTO1 EDITOR,DMCB,EBLOCK                                               
         B     XIT                                                              
         EJECT                                                                  
*              LAST MINUTE ADJUST BEFORE EDITOR (FUDGES!)                       
         SPACE 3                                                                
ADJEDIT  NTR1                                                                   
         TM    GLINDS3,GLRNDALL   ROUND DETAILS/TOTALS OPTION                   
         BO    ADJED1                                                           
         TM    GLINDS,GLRNDOPT     ROUND OPTION                                 
         BNO   ADJED2                                                           
         TM    GLINDS,GLTOTLIN     ONLY APPLIES TO TOTALS                       
         BNO   ADJED2                                                           
         SPACE 1                                                                
ADJED1   TM    GLINDS3,GLNODCLN    DONT REDUCE OUTPUT LEN                       
         BO    ADJED1B                                                          
         ZIC   R1,EBLOUT           REDUCE OUTPUT LENGTH                         
         CH    R1,=H'12'                                                        
         BL    ADJED1B                                                          
         SH    R1,=H'3'            (NO POINT OR DECIMALS)                       
         STC   R1,EBLOUT                                                        
ADJED1B  MVI   EBROUND,2           FORCE ROUND BY 100                           
         MVI   EBDECS,0                  AND NO DECIMAL PLACES                  
         SPACE 1                                                                
ADJED2   B     XIT                                                              
         SPACE 1                                                                
XIT      XIT1                                                                   
         EJECT                                                                  
*              BLOCK FOR EDITOR                                                 
         SPACE 3                                                                
       ++INCLUDE DDEBLOCK                                                       
         EJECT                                                                  
*              LTORG                                                            
         SPACE 3                                                                
         LTORG                                                                  
         EJECT                                                                  
       ++INCLUDE DRIVOUTD                                                       
         SPACE 1                                                                
         PRINT OFF                                                              
       ++INCLUDE DRGLOBAL                                                       
       ++INCLUDE DRLOCAL                                                        
       ++INCLUDE DRINTRECD2                                                     
       ++INCLUDE DRIVETABLE                                                     
       ++INCLUDE DDBIGBOX                                                       
       ++INCLUDE DDBUFFALOD                                                     
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'021DRIVFORM  11/21/07'                                      
         END                                                                    
