*          DATA SET PRWRISTK   AT LEVEL 027 AS OF 04/19/05                      
*CATALP PRWRISTK                                                                
         TITLE 'PRWRISTK - STACK/MERGING LOGIC - ENTRY POINTS'                  
         ENTRY ISTACK                                                           
         ENTRY XSTACK                                                           
PRWRISTK CSECT                                                                  
         TITLE 'PRWRISTK - CHANGE LOG'                                          
         TITLE 'PRWRISTK - STACKING/MERGING LOGIC'                              
***********************************************************************         
*                                                                     *         
*        ROUTINES                                                     *         
*                                                                     *         
*        ISTACK    LOGIC INITIALIZATION - RESET ON CHANGE IN PUB      *         
*        XSTACK    LOGIC USED AT END OF EACH SORT RECORD              *         
*                    TO DETERMINE SORT POSITION                       *         
*                                                                     *         
*NTRY                                                                 *         
*                                                                     *         
*        R2==>  DRIVER INPUT AREA                                     *         
*        R8==>  SPOOL WORKAREA                                        *         
*        R9==>  PRINTWRITER WORKAREA                                  *         
*        RA==>  DRIVER GLOBAL AREA                                    *         
*        RC==>  GENCON WORKAREA                                       *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
         PRINT NOGEN                                                            
         TITLE 'PRWRISTK - STACKING - INPUT - ISTACK'                           
***********************************************************************         
*                                                                     *         
*        ROUTINE TO MONITOR INITIALIZATION OF STACKING FIELDS         *         
*                                                                     *         
*        GLARGS+1    C'M' - MERGING                                   *         
*                                                                     *         
*NTRY   R2 ==> XL2 - USED AS COUNTER TO SORT DATA                     *         
*                    ASSUMED TO BE LAST ROW SO CAN BE THOUGHT OF      *         
*                    AS A REPORT LINE NUMBER.                         *         
*                    RE-INITIALIZED ON CHANGE IN PUB FOR A START      *         
*                                                                     *         
*EXIT   R2 ==> XL2 - REPORT LINE NUMBER                               *         
*                                                                     *         
***********************************************************************         
         EJECT                                                                  
         DS    0D                                                               
ISTACK   NMOD1 0,**#ISTK                                                        
*                                                                               
         USING SPOOLD,R8           ESTABLISH SPOOL WORKAREA                     
         USING SYSD,R9             ESTABLISH PRINTWRITER WORKAREA               
         USING GLOBALD,RA          ESTABLISH DRIVER WORKAREA                    
*                                                                               
         LA    R7,PBLOCK           ESTABLISH PRINT BLOCK                        
         USING PBLOCK,R7                                                        
*                                                                               
         L     RC,GLAWORKD         RESET WORKING STORAGE POINTER                
         USING GEND,RC                                                          
*                                                                               
         CLI   PBMODE,PBPROCIV     IF READING INVOICES                          
         BNE   ISTKIVN                                                          
*                                                                               
         ICM   RF,15,PBIKYELA      POINT TO CURRENT KEY                         
         BZ    ISTACKX                NONE FOUND                                
*                                                                               
         USING PINVKEY,RF          ESTABLISH INVOICE KEY                        
*                                                                               
         CLC   PINVMAST,ISTKKEY    BREAK ON CHANGE IN RECORD                    
         BNE   ISTKINI                                                          
*                                                                               
         CLI   GLARGS+1,C'M'       SKIP IF NOT MERGING                          
         BNE   ISTKINIX                                                         
*                                                                               
         ICM   RF,15,PBIDTELA      POINT TO DETAIL ELEMENT                      
         BZ    ISTKINI             BREAK IF NONE                                
*                                                                               
         USING PIMDTLEL,RF         ESTABLISH INVOICE DETAIL ELEMENT             
*                                                                               
*****    OC    PIMBDATE,PIMBDATE   BREAK IF MATCHED                             
*****    BNZ   ISTKINI                                                          
*                                                                               
         CLC   PIMIDATE,PIMIDATE-PIMDTLEL+ISTKIDTL   BREAK ON INV DATE          
         BNE   ISTKINI                                                          
         CLC   PIMIEST,PIMIEST-PIMDTLEL+ISTKIDTL     OR ESTIMATE CHANGE         
         BNE   ISTKINI                                                          
*                                                                               
         B     ISTKINIX            ELSE SKIP                                    
*                                                                               
         DROP  RF                                                               
*                                                                               
ISTKIVN  DS    0H                                                               
*                                                                               
         CLI   PBMODE,PBPROCNV     IF READING NEW INVOICES                      
         BNE   ISTKNVN                                                          
*                                                                               
         ICM   RF,15,PBIKYELA      POINT TO CURRENT KEY                         
         BZ    ISTACKX                NONE FOUND                                
*                                                                               
         USING PNVKEY,RF           ESTABLISH NEW INVOICE KEY                    
*                                                                               
         CLC   PNVKEY(PNVKELMK-PNVKEY),ISTKKEY  BREAK ON CHANGE IN REC          
         BNE   ISTKINI                                                          
*                                                                               
         CLI   GLARGS+1,C'M'       SKIP IF NOT MERGING                          
         BNE   ISTKINIX                                                         
*                                                                               
         ICM   RF,15,PBIDTELA      POINT TO DETAIL ELEMENT                      
         BZ    ISTKINI             BREAK IF NONE                                
*                                                                               
         USING PNVDTLD,RF          ESTABLISH INVOICE DETAIL ELEMENT             
*                                                                               
         CLC   PNVDBYDT,PNVDBYDT-PNVDTLD+ISTKIDTL   BREAK ON BUY DATE           
         BNE   ISTKINI                                                          
         CLC   PNVDEST,PNVDEST-PNVDTLD+ISTKIDTL     OR ESTIMATE CHANGE          
         BNE   ISTKINI                                                          
*                                                                               
         B     ISTKINIX            ELSE SKIP                                    
*                                                                               
         DROP  RF                                                               
*                                                                               
ISTKNVN  DS    0H                                                               
*                                                                               
         L     R4,AIO1             ESTABLISH BUY RECORD                         
         USING PBUYRECD,R4                                                      
*                                                                               
         CLI   PBUYKRCD,PBUYKIDQ   SKIP IF NOT A BUY RECORD IN CORE             
         BNE   ISTKBUYN                                                         
*                                                                               
         LA    RF,IOKEYSVE         POINT TO CURRENT DIRECTORY KEY               
*                                                                               
         CLI   PBUYKRCD-PBUYKEY(RF),PBUYKIDQ    IF READING BY PRODUCT           
         BNE   *+18                                                             
         CLC   0(PBUYKDAT-PBUYKEY,RF),ISTKKEY      INIT ON PUB CHANGE           
         BNE   ISTKINI                                                          
         B     ISTKBUY1                                                         
*                                                                               
         CLI   PBUYPRCD-PBUYPKEY(RF),PBUYPIDQ    IF READING BY PUB              
         BNE   ISTKBUYN                                                         
*                                                                               
         CLC   0(PBUYPPRD-PBUYPKEY,RF),ISTKKEY      INIT ON PUB CHANGE          
         BNE   ISTKINI                                                          
*                                                                               
ISTKBUY1 DS    0H                                                               
*                                                                               
         CLI   GLARGS+1,C'M'       SKIP IF NOT MERGING                          
         BNE   ISTKINIX                                                         
*                                                                               
         CLC   ISTKKEY,0(RF)       ELSE ON CHANGE IN RECORD                     
         BNE   ISTKINI                                                          
*                                                                               
         TM    PBQMATSW,PBQNVPYQ   IF NVPAYABLE                                 
         BO    ISTKINI                ALWAYS INIT                               
*                                                                               
         B     ISTKINIX            ELSE SKIP                                    
*                                                                               
ISTKBUYN DS    0H                                                               
*                                                                               
         USING PISSREC,R4          ESTABLISH AS ISSUE RECORD                    
*                                                                               
         CLI   PISSKTYP,X'29'      SKIP IF NOT ISSUE RECORD IN CORE             
         BNE   ISTKISSN                                                         
*                                                                               
         CLC   PISSKEY(PISSKYR-PISSKEY),ISTKKEY  INIT ON PUB CHANGE             
         BNE   ISTKINI                                                          
*                                                                               
         CLI   GLARGS+1,C'M'       SKIP IF NOT MERGING                          
         BNE   ISTKINIX                                                         
*                                                                               
         CLC   ISTKKEY,PISSKEY     ELSE INIT ON CHANGE IN RECORD                
         BNE   ISTKINI                                                          
*                                                                               
         B     ISTKINIX            ELSE SKIP                                    
*                                                                               
ISTKISSN DS    0H                                                               
*                                  LOOK FOR BILL RECORD                         
*                                                                               
         USING PBILLRCD,R4         ESTABLISH AS BILL RECORD                     
*                                                                               
         CLI   PBILKRCD,PBILKIDQ   SKIP IF NOT BILL RECORD IN CORE              
         BNE   ISTKBLLN                                                         
*                                                                               
         CLC   PBILLKEY(PBILKPRD-PBILLKEY),ISTKKEY  INIT ON CLT CHANGE          
         BNE   ISTKINI                                                          
*                                                                               
         CLI   GLARGS+1,C'M'       SKIP IF NOT MERGING                          
         BNE   ISTKINIX                                                         
*                                                                               
         CLC   ISTKKEY,PBILLKEY    INIT ON CHANGE IN RECORD                     
         BNE   ISTKINI                                                          
*                                                                               
         B     ISTKINIX            ELSE SKIP                                    
*                                                                               
ISTKBLLN DS    0H                                                               
*                                  LOOK FOR CONTRACT RECORD                     
         L     R4,AIO2             POINT TO CONTRACT RECORD                     
         USING PCONREC,R4          ESTABLISH AS CONTRACT RECORD                 
*                                                                               
         CLI   PCONKRCD,PCONKIDQ   SKIP IF NOT CONTRACT RECORD IN CORE          
         BNE   ISTKCONN                                                         
*                                                                               
*******  CLC   PCONKEY(PCONNUM-PCONKEY),ISTKKEY  INIT ON PUB CHANGE             
         CLC   PCONKEY(PCONKPUB-PCONKEY),ISTKKEY INIT ON CLT CHANGE             
         BNE   ISTKINI                                                          
*                                                                               
         CLI   GLARGS+1,C'M'       SKIP IF NOT MERGING                          
         BNE   ISTKINIX                                                         
*                                                                               
         CLC   ISTKKEY,PCONKEY     ELSE INIT ON CHANGE IN RECORD                
         BNE   ISTKINI                                                          
*                                                                               
         B     ISTKINIX            ELSE SKIP                                    
*                                                                               
ISTKCONN DS    0H                                                               
*                                                                               
         B     ISTACKX             SKIP                                         
*                                  LOOK FOR CONTRACT RECORD                     
ISTKINI  DS    0H                                                               
*                                                                               
         ICM   RF,15,PBIDTELA      IF INVOICE DETAIL ELM AROUND                 
         BZ    *+10                                                             
         MVC   ISTKIDTL,0(RF)   SAVE IT                                         
*                                                                               
         CLI   PBQADVSW,C'Y'       SKIP IF DOING ADVERTISER REPORT              
         BE    ISTKINIX                                                         
*                                                                               
         USING PBUYKEY,R4          ASSUME BUY RECORD                            
*                                                                               
         XC    ISTKSAV,ISTKSAV     CLEAR WORKAREAS                              
*                                                                               
         MVC   ISTKKEY,IOKEYSVE    SAVE RECORD KEY                              
*                                                                               
         L     R1,AFLTTAB          POINT TO COLUMN FILTER AREA                  
         USING FLTTABD,R1          ESTABLISH FILTER TABLE                       
         LA    R0,FTBENTNM         MAX NUMBER OF COLUMNS                        
*                                                                               
         XC    FTBLINNM,FTBLINNM   INIT LINE NUMBER                             
         LA    R1,FLTAREAL(R1)     BUMP TO NEXT AREA                            
         BCT   R0,*-10                                                          
*                                                                               
         L     R4,PBASVBBF         ESTABLISH STACK VALUES BUFFER                
         USING STKVLBFD,R4                                                      
*                                                                               
         LA    RF,SVBFSTRT         INDICATE BUFFER IS EMPTY                     
         BCTR  RF,0                                                             
         ST    RF,SVBFENDA                                                      
*                                                                               
ISTKINIX DS    0H                                                               
*                                                                               
         MVI   STACKSW,STACKYQ+STCKINIQ  INDICATE STACKING BEING DONE           
*                                                                               
         CLI   GLARGS+1,C'M'       IF ACTUALLY MERGING                          
         BNE   *+8                                                              
         MVI   STACKSW,STCKMRGQ+STCKINIQ   SET INDICATOR                        
*                                                                               
         STCM  R2,15,STACKA        SAVE STACK KEYWORD ADDRESS                   
*                                                                               
ISTACKX  DS    0H                                                               
         XIT1                                                                   
*                                                                               
         LTORG                                                                  
*                                                                               
         DS    CL256               FOR NEW Z/OS PROCESSOR                       
ISTKSAV  DS    0D                  STACK ROUTINE SAVEAREA                       
ISTKKEY  DC    XL(L'PBUYKEY)'00'   KEY SAVEAREA                                 
ISTKIDTL DC    XL256'00'           INVOICE DETAIL SAVEAREA                      
ISTKSAVL EQU   *-ISTKSAV           SAVE AREA LENGTH                             
*                                                                               
         DROP                                                                   
*                                                                               
         TITLE 'PRWRISTK - STACKING - WRAP-UP - XSTACK'                         
***********************************************************************         
*                                                                     *         
*        ROUTINE TO SELECT LINE NUMBER TO BE USED FOR STACK           *         
*                                                                     *         
*NTRY   STACKA - ADDRESS OF LINE NUMBER IN SORT RECORD                *         
*       CURRENT KEYWORD HAS SIGNIFICANT DATA IN IT                    *         
*       R2   ==> CURRENT VALUE OF KEYWORD                             *         
*                                                                     *         
*EXIT   SET LINE NUMBER TO THAT OF CURRENT COLUMN                     *         
*                                                                     *         
***********************************************************************         
         EJECT                                                                  
         DS    0D                                                               
XSTACK   NMOD1 0,**#XSTK                                                        
*                                                                               
         USING SPOOLD,R8           ESTABLISH SPOOL WORKAREA                     
         USING SYSD,R9             ESTABLISH PRINTWRITER WORKAREA               
         USING GLOBALD,RA          ESTABLISH DRIVER WORKAREA                    
*                                                                               
         LA    R7,PBLOCK           ESTABLISH PRINT BLOCK                        
         USING PBLOCK,R7                                                        
*                                                                               
         L     RC,GLAWORKD         RESET WORKING STORAGE POINTER                
         USING GEND,RC                                                          
*                                                                               
         L     R1,GLADTENT         ESTABLISH INPUT DRIVETABLE ENTRY             
         USING DRIND,R1                                                         
*                                                                               
         CLI   DRINTYPE,C'P'       IF NUMERIC FIELD                             
         BE    *+8                                                              
         CLI   DRINTYPE,C'M'                                                    
         BE    *+8                                                              
         CLI   DRINTYPE,C'B'                                                    
         BNE   XSTACK1                                                          
*                                                                               
         L     R7,FLTFLTRA         POINT TO THIS LINE'S FILTER AREA             
         USING FLTTABD,R7          ESTABLISH FILTER TABLE ENTRY                 
*                                                                               
         MVC   FTBLINNM,=X'0001'      FORCE TO BE LINE 1                        
*                                                                               
         B     XSTMAJX                                                          
*                                                                               
XSTACK1  DS    0H                                                               
*                                                                               
*****    TM    STACKSW,STCKMRGQ    DONE IF NOT MERGING DATA                     
*****    BNO   XSTACKX                                                          
*                                                                               
*        CONTINUE IF THERE IS SIGNIFICANT DATA                                  
*                                                                               
         L     R1,GLADTENT         ESTABLISH DRIVE TABLE ENTRY                  
         USING DRIND,R1            AS AN INPUT RECORD                           
*                                                                               
         SR    R0,R0                                                            
         ICM   R0,1,DRINREP        GET REPETITION FACTOR                        
*                                                                               
         SR    RF,RF                                                            
         ICM   RF,1,DRINFLEN       GET FIELD LENGTH                             
         BCTR  RF,0                DECREMENT FOR EXECUTE                        
*                                                                               
         LR    RE,R2               COPY INPUT POINTER                           
*                                                                               
         EX    RF,*+20                                                          
         BNZ   XSTMRGFD                                                         
         LA    RE,1(RF,RE)                                                      
         BCT   R0,*-12                GET ALL REPETITIONS                       
         B     XSTACKX                NO SIGNIFICANT DATA                       
         OC    0(0,RE),0(RE)       ELSE COMPARE TO NULLS                        
*                                                                               
XSTMRGFD DS    0H                                                               
*                                                                               
         L     R7,FLTFLTRA         POINT TO THIS LINE'S FILTER AREA             
         USING FLTTABD,R7          ESTABLISH FILTER TABLE ENTRY                 
*                                                                               
         TM    GLARGS+5,X'02'      SKIP IF MAJOR FIELD                          
         BO    XSTMAJ                                                           
*                                                                               
         SR    RF,RF                                                            
         ICM   RF,3,FTBLINNM       BUMP LINE NUMBER FOR KEYWORD                 
         LA    RF,1(RF)                                                         
         STCM  RF,3,FTBLINNM                                                    
*                                                                               
         B     XSTMAJX                                                          
*                                                                               
*        SEARCH STACK VALUE BUFFER FOR MATCHING KEYWORD AND VALUE               
*            IF FOUND USE ITS LINE NUMBER                                       
*            ELSE CREATE NEW ENTRY IN TABLE WITH BUMPED LINE NUMBER             
*                                                                               
XSTMAJ   DS    0H                                                               
*                                                                               
         L     R6,PBASVBBF         ESTABLISH STACK VALUE BUFFER                 
         USING STKVLBFD,R6                                                      
*                                                                               
         SR    RF,RF                                                            
*                                                                               
         XC    XSTLINNM,XSTLINNM   INIT MAX LINE NUMBER SO FAR                  
*                                                                               
         LM    R3,R5,SVBFSTA       LOAD BXLE REGS FOR BUFFER                    
*                                                                               
         CR    R3,R5               SKIP IF BUFFER EMPTY                         
         BNL   XSTBUFDN                                                         
*                                                                               
XSTBUFLP DS    0H                                                               
*                                                                               
         USING SVTTABD,R3          ESTABLISH BUFFER ENTRY                       
*                                                                               
         CLI   PBQADVSW,C'Y'       IF DOING AOR                                 
         BNE   *+14                                                             
         CLC   SVTPUB,PAORPUB         MATCH AOR PUB NUMBER                      
         BNE   XSTBUFCN                                                         
*                                                                               
         CLC   SVTKYWD#,GLARGS+6   MATCH KEYWORD NUMBER                         
         BNE   XSTBUFCN                                                         
*                                                                               
         CLC   XSTLINNM,SVTLINNM   UPDATE MAX LINE NUMBER                       
         BNL   *+10                                                             
         MVC   XSTLINNM,SVTLINNM                                                
*                                                                               
         SR    RF,RF                                                            
         IC    RF,SVTLEN           GET VALUE LENGTH                             
         BCTR  RF,0                DECREMENT FOR EXECUTE                        
         EX    RF,*+8                                                           
         B     *+10                                                             
         CLC   SVTVALUE(0),0(R2)   MATCH VALUE                                  
         BE    XSTBUFFD            MATCH FOUND                                  
*                                                                               
XSTBUFCN DS    0H                                                               
*                                                                               
         IC    R4,SVTLEN           GET VALUE LENGTH                             
         LA    R4,SVTVALUE-SVTENTRY(R4)   TABLE ENTRY LENGTH                    
         BXLE  R3,R4,XSTBUFLP                                                   
*                                                                               
XSTBUFDN DS    0H                  NO MATCH CREATE NEW ENTRY                    
*                                                                               
         CLI   PBQADVSW,C'Y'       IF DOING AOR                                 
         BNE   *+10                                                             
         MVC   SVTPUB,PAORPUB         SAVE AOR PUB NUMBER                       
*                                                                               
         MVC   SVTKYWD#,GLARGS+6   SAVE KEYWORD NUMBER                          
*                                                                               
         ICM   RF,1,DRINLEN        INPUT LENGTH                                 
         STC   RF,SVTLEN           SAVE LENGTH                                  
         BZ    *+6                                                              
         BCTR  RF,0                DECREMENT FOR EXECUTE                        
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   SVTVALUE(0),0(R2)   SAVE INPUT VALUE                             
*                                                                               
         LA    R5,SVTVALUE(RF)     NEW END OF TABLE                             
         ST    R5,SVBFENDA                                                      
*                                                                               
         ICM   RF,3,XSTLINNM       BUMP MAX LINE NUMBER FOR KEYWORD             
         LA    RF,1(RF)                                                         
         STCM  RF,3,SVTLINNM                                                    
*                                                                               
XSTBUFFD DS    0H                                                               
*                                                                               
         MVC   FTBLINNM,SVTLINNM   SET LINE NUMBER                              
*                                                                               
XSTMAJX  DS    0H                                                               
*                                                                               
         ICM   RF,15,STACKA        POINT TO STACK KEYWORD                       
         BZ    XSTACKX             HAVEN'T FOUND STACK KYWD YET                 
*                                                                               
         CLC   0(L'FTBLINNM,RF),FTBLINNM   UPDATE LINE NUMBER                   
         BNL   *+10                                                             
         MVC   0(L'FTBLINNM,RF),FTBLINNM      IF NEW ONE HIGHER                 
*                                                                               
XSTACKX  DS    0H                                                               
         XIT1                                                                   
*                                                                               
         DS    CL256               FOR NEW Z/OS PROCESSOR                       
XSTLINNM DS    XL2                 MAX LINE NUMBER USED SO FAR FOR KYWD         
*                                                                               
         LTORG                                                                  
*                                                                               
         DROP  R1,R3,R6,R7                                                      
*                                                                               
         TITLE   'PRWRISTK DSECTS / STORAGE'                                    
* PRWRIWORKD                                                                    
         PRINT OFF                                                              
       ++INCLUDE PRWRIWORKD                                                     
         PRINT   ON                                                             
* DRGLOBAL                                                                      
         PRINT   OFF                                                            
       ++INCLUDE DRGLOBAL                                                       
         PRINT   ON                                                             
* INCLUDE DRIVETABLE                                                            
         PRINT   OFF                                                            
       ++INCLUDE DRIVETABLE                                                     
         PRINT   ON                                                             
* DRINTRECD2                                                                    
         PRINT   OFF                                                            
       ++INCLUDE DRINTRECD2                                                     
         PRINT   ON                                                             
* DDSPOOLD                                                                      
         PRINT   OFF                                                            
       ++INCLUDE DDSPOOLD                                                       
         PRINT   ON                                                             
* DDSPLWORKD                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDSPLWORKD                                                     
         PRINT   ON                                                             
* PRGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE PRGENFILE                                                      
         PRINT   ON                                                             
* PRGLOBEQUS                                                                    
         PRINT OFF                                                              
       ++INCLUDE PRGLOBEQUS                                                     
         PRINT   ON                                                             
* DDCOMFACSD                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACSD                                                     
         PRINT   ON                                                             
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'027PRWRISTK  04/19/05'                                      
         END                                                                    
