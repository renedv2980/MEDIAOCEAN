*          DATA SET SXLDCPTR   AT LEVEL 054 AS OF 07/10/17                      
*CATALP SXLDCPTR                                                                
         TITLE 'LDCPTR - XSPDIR/XSPFIL - CREATE DIRECTORY POINTERS'             
         PRINT NOGEN                                                            
*        PARA1 A(RECORD)                                                        
*        PARA2 A(AREA TO CREATE DIRECTORY POINTERS)                             
*              LAST POINTER FOLLOWED BY X'00'                                   
*                                                                               
LDCPTR   CSECT                                                                  
         NMOD1 WORKX-WORKD,XSLDCPTR                                             
         USING WORKD,RC                                                         
         LM    R2,R3,0(R1)         R2=A(PRIME DATA RECORD)                      
         USING RECORDD,R2                                                       
         ST    R3,NEXTDIR                                                       
*                                                                               
         L     RA,=V(CPRINT)                                                    
         USING DPRINT,RA                                                        
*                                                                               
         XC    KEY,KEY                                                          
         MVI   PSSVTAB,0                                                        
*                                                                               
LDC10D   USING RECORDD,KEY                                                      
LDC10    MVC   LDC10D.RECKEY,RECKEY                                             
         MVC   LDC10D.RECKYSTA,RECRCSTA                                         
         DROP  LDC10D                                                           
         BAS   RE,ADDKEY                                                        
*                                                                               
         CLC   RECKEY(2),=X'0E03'  IS IT AN NINV RECORD                         
         BNE   PTR20               NO, DONE                                     
         TM    RECRCSTA,X'80'                                                   
         BO    EXIT                                                             
         LA    R5,KEY                                                           
         USING SNVKEYD,R5                                                       
         LA    R4,RECRCEL          START OF 1ST ELEM                            
*                                                                               
PTR04    DS    0H                                                               
         CLI   0(R4),0             EOR                                          
         BE    PTR10                                                            
*                                                                               
         CLI   0(R4),SNVMMELQ      MATCHMAKER STATUS ELEM                       
         BE    PTR05                                                            
*                                                                               
PTR04B   DS    0H                                                               
         ZIC   R0,1(R4)                                                         
         LTR   R0,R0                                                            
         BNP   PTR10                                                            
         AR    R4,R0                                                            
         B     PTR04                                                            
*                                  BUILD PASSIVE POINTER FROM STATUS            
*                                  ELEMENT AND RECORD KEY                       
PTR05    DS    0H                                                               
         XC    KEY,KEY                                                          
         USING SNVMMELD,R4                                                      
*                                                                               
         OC    SNVMMNWK,SNVMMNWK   IF NETWORK IN ELEM                           
         BNZ   PTR06               THEN ADD NETWORK PASSIVE                     
*                                                                               
         MVI   SNVPTYP,SNVPTYPQ    SPOT PASSIVE                                 
         MVI   SNVPSUB,SNVPSUBQ                                                 
         MVC   SNVPAM,RECKEY+SNVKAM-SNVKEY                                      
         MVC   SNVPMKT,SNVMMMKT                                                 
         MVC   SNVPSTA,RECKEY+SNVKSTA-SNVKEY                                    
         MVC   SNVPMOS,RECKEY+SNVKMOS-SNVKEY  MONTH OF SERVICE                  
         MVC   SNVPCLT,RECKEY+SNVKCLT-SNVKEY                                    
         MVC   SNVPPRD,SNVMMRP1                                                 
         MVC   SNVPEST,SNVMMRE1                                                 
         MVC   SNVPEST2,SNVMMRE2                                                
         MVC   SNVPINV,RECKEY+SNVKINV-SNVKEY  INVOICE NUMBER                    
         MVC   SNVPPRD2,SNVMMRP2                                                
         CLI   SNVMMLEN,SNVMMLNQ   OLD LENGTH?                                  
         BE    PTR05A              YES, NO ERROR STAT ON THIS ELEM              
         MVC   BYTE,SNVMMERR       ERROR STATUS BYTE                            
         NI    BYTE,X'FF'-X'C0'    MAKE SURE X'80' & X'40' ARE OFF!             
         OC    SNVDSTAT+0(1),BYTE                                               
*                                                                               
PTR05A   MVC   SNVDSTAT+1(1),SNVMMMST         STATUS BYTE TO DIRECTORY          
         BAS   RE,ADDPSSV          ADD TO MY PASSIVE TABLE                      
         B     PTR04B              NEXT ELEM                                    
*                                                                               
*                                                                               
PTR06    DS    0H                  NETPAK PASSIVE                               
         MVI   SNVNTYP,SNVNTYPQ                                                 
         MVI   SNVNSUB,SNVNSUBQ                                                 
         MVC   SNVNAM,RECKEY+SNVKAM-SNVKEY                                      
         MVC   SNVNCLT,RECKEY+SNVKCLT-SNVKEY                                    
         MVC   SNVNNETW,SNVMMNWK                                                
         MVC   SNVNMOS,RECKEY+SNVKMOS-SNVKEY  MONTH OF SERVICE                  
         MVC   SNVNPRD,SNVMMRP1                                                 
         MVC   SNVNEST,SNVMMRE1                                                 
         MVC   SNVNEST2,SNVMMRE2                                                
         MVC   SNVNINV,RECKEY+SNVKINV-SNVKEY  INVOICE NUMBER                    
         MVC   SNVNPRD2,SNVMMRP2                                                
         CLI   SNVMMRPG,C' '       ANY PGROUP                                   
         BNH   *+10                                                             
         MVC   SNVNPGR,SNVMMRPG                                                 
         CLI   SNVMMLEN,SNVMMLNQ   OLD LENGTH?                                  
         BE    PTR06A              YES, NO ERROR STAT ON THIS ELEM              
         MVC   BYTE,SNVMMERR       ERROR STATUS BYTE                            
         NI    BYTE,X'FF'-X'C0'    MAKE SURE X'80' & X'40' ARE OFF!             
         OC    SNVDSTAT+0(1),BYTE                                               
*                                                                               
PTR06A   MVC   SNVDSTAT+1(1),SNVMMMST         STATUS BYTE TO DIRECTORY          
         BAS   RE,ADDPSSV          ADD TO MY PASSIVE TABLE                      
         B     PTR04B              NEXT ELEM                                    
*                                                                               
PTR10    DS    0H                                                               
         CLI   RECKEY+SNVKMINK-SNVKEY,X'FF'   IS IT MINIO MASTER                
         BNE   EXIT                NO, DONE WITH THIS RECORD                    
         B     PTRX                                                             
*                                                                               
*        PASSIVE KEY FOR SUPERDESK AUTH RECS                                    
*                                                                               
PTR20    DS    0H                                                               
         CLC   RECKEY(2),=X'0D39'  IS IT AN AUTH RECORD                         
         BNE   PTR30               NO, DONE                                     
         TM    RECRCSTA,X'80'                                                   
         BO    EXIT                                                             
         OC    RECKEY+AUTKMKT-AUTKEY(5),RECKEY+AUTKMKT-AUTKEY                   
         BNZ   EXIT                                                             
*                                                                               
         LA    R5,KEY                                                           
         USING AUTRECD,R5                                                       
         LA    R4,RECRCEL          START OF 1ST ELEM                            
*                                                                               
         CLI   0(R4),0             EOR                                          
         BE    EXIT                                                             
*                                                                               
         XC    KEY,KEY             ADD 0DB9 DUE DATE PASSIVE                    
         USING AUDEL,R4                                                         
         MVI   AUPKTYP,AUPKTYPQ                                                 
         MVI   AUPKSUB,AUPKSUBQ                                                 
         MVC   AUPKAM,RECKEY+AUTKAM-AUTKEY                                      
         MVC   AUPKDUE,AUDDUEDT    DUE DATE                                     
         MVC   AUPKCLT,RECKEY+AUTKCLT-AUTKEY                                    
         MVC   AUPKPRD,RECKEY+AUTKPRD-AUTKEY                                    
         MVC   AUPKPRD2,RECKEY+AUTKPRD2-AUTKEY                                  
         MVC   AUPKEST,RECKEY+AUTKEST-AUTKEY                                    
         MVC   AUPKAUN,RECKEY+AUTKAUN-AUTKEY                                    
         MVC   AUPKREV,RECKEY+AUTKREV-AUTKEY                                    
         BAS   RE,ADDPSSV          ADD TO MY PASSIVE TABLE                      
*                                                                               
         XC    KEY,KEY             ADD 0DC9 START DATE PASSIVE                  
         MVI   AUSKTYP,AUSKTYPQ                                                 
         MVI   AUSKSUB,AUSKSUBQ                                                 
         MVC   AUSKAM,RECKEY+AUTKAM-AUTKEY                                      
         MVC   AUSKSTDT,AUDFLST    FLIGHT START DATE                            
         MVC   AUSKCLT,RECKEY+AUTKCLT-AUTKEY                                    
         MVC   AUSKPRD,RECKEY+AUTKPRD-AUTKEY                                    
         MVC   AUSKPRD2,RECKEY+AUTKPRD2-AUTKEY                                  
         MVC   AUPKEST,RECKEY+AUTKEST-AUTKEY                                    
         MVC   AUPKAUN,RECKEY+AUTKAUN-AUTKEY                                    
         MVC   AUPKREV,RECKEY+AUTKREV-AUTKEY                                    
         BAS   RE,ADDPSSV          ADD TO MY PASSIVE TABLE                      
         B     PTRX                                                             
         DROP  R4,R5                                                            
*                                                                               
PTR30    DS    0H                                                               
         CLC   RECKEY(2),=X'0D73'  IS IT A STATION LOCKIN                       
         BNE   PTR40               NO, DONE                                     
         TM    RECRCSTA,X'80'                                                   
         BO    EXIT                                                             
         USING SLKRECD,RECKEY                                                   
*                                                                               
         CLI   SLKKPRD2,0          TEST PIGGYBACK                               
         BE    EXIT                                                             
*                                                                               
         XC    KEY,KEY                                                          
         MVC   KEY(32),RECKEY                                                   
         LA    RE,(SLKKPRD-SLKKEY)+KEY   POINT TO PRD1                          
         LA    RF,(SLKKPRD2-SLKKEY)+KEY  POINT TO PRD2                          
         IC    R0,0(RE)                  SAVE PRD1                              
         MVC   0(1,RE),0(RF)             SET PRD2 IN PRD1                       
         STC   R0,0(RF)                  SET PRD1 IN PRD2                       
*                                                                               
         LA    RE,(SLKKLEN-SLKKEY)+KEY   POINT TO SLN1                          
         LA    RF,(SLKKLEN2-SLKKEY)+KEY  POINT TO SLN2                          
         IC    R0,0(RE)                  SAVE SLN1                              
         MVC   0(1,RE),0(RF)             SET SLN2 IN SLN1                       
         STC   R0,0(RF)                  SET SLN1 IN SLN2                       
*                                                                               
         LA    RE,SLKKIND-SLKKEY+KEY     POINT TO IND                           
         OI    0(RE),SLKKIPSV            SET PASSIVE FLAG                       
         BAS   RE,ADDPSSV          ADD TO MY PASSIVE TABLE                      
         B     PTRX                                                             
*                                                                               
PTR40    DS    0H                                                               
         CLC   RECKEY(2),=X'0E0A'  IS IT A NETPAK UNIT BILLING RECORD?          
         BNE   PTR50               NO, DONE                                     
*                                                                               
         LA    R5,KEY                                                           
         USING NUBKEY,R5                                                        
*                                  BUILD X'0E06' PASSIVE POINTER                
REC      USING NUBK0KEY,RECKEY                                                  
         XC    KEY,KEY                                                          
         MVI   NUBKSYS,NUBKSYSQ                                                 
         MVI   NUBKSTYP,NUBKSTYQ                                                
         MVC   NUBKAM,REC.NUBK0AM                                               
         MVC   NUBKCLI,REC.NUBK0CLI                                             
         MVC   NUBKNET,REC.NUBK0NET                                             
         MVC   NUBKPROG,REC.NUBK0PRG                                            
         MVC   NUBKDATE,REC.NUBK0DAT                                            
         MVC   NUBKEST,REC.NUBK0EST                                             
         MVC   NUBKSUB,REC.NUBK0SUB                                             
         MVC   NUBKDPT,REC.NUBK0DPT                                             
         MVC   NUBKBDAT,REC.NUBK0BDT                                            
         MVC   NUBKSTAT,RECRCSTA   STATUS BYTES                                 
         BAS   RE,ADDPSSV          ADD TO MY PASSIVE TABLE                      
         DROP  REC                                                              
*                                                                               
         B     PTRX                                                             
*                                                                               
PTR50    DS    0H                                                               
         CLC   RECKEY(2),=X'0A61'   TEST NETPAK TRAFFIC PATTERN REC?            
         BNE   PTR60                NO, DONE                                    
*                                                                               
         OC    RECKEY+5(27),RECKEY+5  IS THIS A PATTERN SEQ REC                 
         BZ    PTR60                   YES, NO PASSIVE KEY                      
*                                                                               
         CLI   NPTXNET-NPTXKEY+RECKEY,C'$'  TEST PATTERN LIST REC               
         BNE   PTR54                                                            
         LA    RE,NPTXOR3G-NPTXKEY+RECKEY                                       
         OC    0(3,RE),0(RE)       TEST A 'SAVED' PATTERN                       
         BNZ   PTR54               YES - NO PASSIVES NEEDED                     
                                                                                
* NEED A PASSIVE POINTER FOR EACH 5B ELEMENT                                    
                                                                                
         LA    R6,RECRCEL          POINT TO FIRST ELEMENT                       
         MVI   ELCODE,X'5B'                                                     
*                                                                               
PTR52    BRAS  RE,NEXTEL                                                        
         BNE   PTR54                                                            
         LA    R5,KEY                                                           
         USING NPTXKEY,R5                                                       
         MVC   NPTXKEY,RECKEY      MOVE KEY TO RECORD                           
         MVC   NPTXNET,2(R6)       MOVE NETWORK TO KEY                          
         MVI   NPTXPSSV,C'$'       SET PASSIVE FLAG                             
*                                                                               
         BAS   RE,ADDPSSV          ADD TO MY PASSIVE TABLE                      
         B     PTR52                                                            
         DROP  R5                                                               
*                                                                               
PTR54    LA    R5,KEY                                                           
         USING NPTRECD,R5                                                       
*                                  BUILD X'0AE1' PASSIVE POINTER                
PTN      USING NPTXKEY,RECKEY                                                   
         XC    KEY,KEY                                                          
         MVC   NPTPXID,=X'0AE1'                                                 
         MVC   NPTPXAM,PTN.NPTXAM                                               
         MVC   NPTPXCLT,PTN.NPTXCLT                                             
         LA    R4,42(,R2)                                                       
         OC    NPTS3QNO-NPTDATA(3,R4),NPTS3QNO-NPTDATA(R4)                      
         BZ    PTR60                                                            
         MVC   NPTPXS3Q,NPTS3QNO-NPTDATA(R4)                                    
*                                                                               
         BAS   RE,ADDPSSV          ADD TO MY PASSIVE TABLE                      
         B     PTRX                                                             
*                                                                               
PTR60    CLC   RECKEY(2),=X'0D06'  CANAD ORDERS DEMO RECORD                     
         BNE   PTR70                NO, DONE                                    
*                                                                               
         LA    R5,KEY                                                           
         USING DDMRECD,R5                                                       
*                                  BUILD SPILL POINTERS                         
K        USING DDMKEY,RECKEY                                                    
         XC    KEY,KEY                                                          
         MVC   KEY(32),K.DDMKEY                                                 
                                                                                
* SEARCH FOR SPILL ELEMENTS                                                     
                                                                                
         LA    R6,RECRCEL          FIRST ELEMENT                                
         MVI   ELCODE,X'03'                                                     
         SR    R7,R7               ELEM COUNTER                                 
*                                                                               
PTR62    BRAS  RE,NEXTEL                                                        
         BNE   PTRX                                                             
         AHI   R7,1                ADD TO COUNTER                               
*                                                                               
         USING DDMDVELD,R6                                                      
         OC    DDMDVMKT,DDMDVMKT   TEST FOR SPILL MARKET                        
         BZ    PTR62               NO                                           
*                                                                               
         MVC   DDMKMKT,DDMDVMKT    MOVE SPILL MARKET TO KEY                     
         TM    DDMKAGMD,X'03'      TEST NETWORK                                 
         BNO   PTR64                                                            
         CHI   R7,1                TEST FIRST DEMO ELEMENT                      
         BE    *+8                 YES - IT IS NOT SPILL                        
PTR64    MVI   DDMKSPL,C'S'        SET SPILL FLAG                               
         BAS   RE,ADDPSSV          ADD TO MY PASSIVE TABLE                      
         B     PTR62                                                            
*                                                                               
PTR70    CLC   RECKEY(2),=X'0E0C'  WB FLIGHT RECORDS                            
         BNE   PTR80               NO, DONE                                     
*                                                                               
         LA    R5,KEY                                                           
         USING WFLIGHTD,R5                                                      
         LA    R4,RECRCEL          START OF 1ST ELEM                            
         CLI   0(R4),0             EOR                                          
         BE    EXIT                                                             
*                                                                               
         XC    KEY,KEY             ADD 0E8C PASSIVE                             
         USING WFIELEM,R4                                                       
         MVI   WP1TYP,WP1TYPQ                                                   
         MVI   WP1STYP,WP1STYPQ                                                 
         MVC   WP1AM,RECKEY+WFKAM-WFKEY                                         
         MVC   WP1CLT,RECKEY+WFKCLT-WFKEY                                       
         MVC   WP1PRD,WFIPRD       PRODUCT                                      
         MVC   WP1FTYPE,WFIFTYPE   FLIGHT TYPE                                  
         MVC   WP1DPT,WFIDPT       DAYPART                                      
         MVC   WP1STD,WFISTD       START DATE                                   
         MVC   WP1END,WFIEND       END DATE                                     
         MVC   WP1WFID,WFIWFID     WB FLIGHT ID                                 
         MVC   WP1IFLT,WFIIFLT     INTERNAL FLIGHT ID                           
         MVC   WFKCNTRL,RECRCSTA   STATUS BYTES                                 
         BAS   RE,ADDPSSV          ADD TO MY PASSIVE TABLE                      
*                                                                               
         XC    KEY,KEY             ADD 0DC9 START DATE PASSIVE                  
         MVI   WP2TYP,WP2TYPQ                                                   
         MVI   WP2STYP,WP2STYPQ                                                 
         MVC   WP2AM,RECKEY+WFKAM-WFKEY                                         
         MVC   WP2WFID,WFIWFID     WB FLIGHT ID                                 
         MVC   WP2IFLT,WFIIFLT     INTERNAL FLIGHT ID                           
         MVC   WP2CLT,RECKEY+WFKCLT-WFKEY                                       
         MVC   WP2PRD,WFIPRD       PRODUCT                                      
         MVC   WP2STD,WFISTD       START DATE                                   
         MVC   WP2END,WFIEND       END DATE                                     
         MVC   WP2FTYPE,WFIFTYPE   FLIGHT TYPE                                  
         MVC   WFKCNTRL,RECRCSTA   STATUS BYTES                                 
         BAS   RE,ADDPSSV          ADD TO MY PASSIVE TABLE                      
         B     PTRX                                                             
         DROP  R4,R5                                                            
*                                                                               
PTR80    CLC   RECKEY(2),=X'0E15'  EMAIL ADDRESS RECORD                         
         BNE   PTR90                NO                                          
         TM    RECRCSTA,X'80'                                                   
         BO    EXIT                                                             
*                                                                               
         LA    R5,KEY                                                           
         USING EMLPKEY,R5                                                       
         LA    R4,RECRCEL          START OF 1ST ELEM                            
         CLI   0(R4),0             EOR                                          
         BE    EXIT                                                             
         CLI   0(R4),X'10'         MAKE SURE EMAIL ADDRESS ELEM                 
         BNE   EXIT                                                             
*                                                                               
         XC    KEY,KEY             ADD 0E95 PASSIVE                             
         USING EMLELD,R4                                                        
         MVI   EMLPTYP,EMLPTYPQ                                                 
         MVI   EMLPSUB,EMLPSUBQ                                                 
         MVC   EMLPAGY,RECKEY+EMLKAGY-EMLKEY                                    
*                                                                               
         MVI   EMLPEML,C' '        PRESET KEY TO BLANKS                         
         MVC   EMLPEML+1(L'EMLPEML-1),EMLPEML                                   
         LLC   RE,EMLLEN                                                        
         AHI   RE,-2               SUB FOR ELCODE & LEN                         
*                                                                               
         CHI   RE,L'EMLPEML        MAKE SURE NOT LONGER THAN KEY FIELD          
         BNH   *+8                                                              
         LHI   RE,L'EMLPEML                                                     
         BCTR  RE,0                                                             
*                                                                               
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   EMLPEML(0),EMLADDR                                               
         OC    EMLPEML,=CL22' '                                                 
*                                                                               
         MVC   EMLPTOK,RECKEY+EMLKTOK-EMLKEY                                    
         MVC   EMLPSTAT,RECRCSTA   STATUS BYTES                                 
         BAS   RE,ADDPSSV          ADD TO MY PASSIVE TABLE                      
         B     PTRX                                                             
*                                                                               
PTR90    CLC   RECKEY(2),=X'0D0D'  CANAD MAPPING RECORD?                        
         BL    PTR100                                                           
         CLC   RECKEY(2),=X'0D0F'                                               
         BH    PTR100               NO                                          
*                                                                               
         TM    RECRCSTA,X'80'      RECORD MARKED DELETED?                       
         BO    EXIT                YES                                          
*                                                                               
         LA    R4,RECRCEL          START OF 1ST ELEM                            
PTR92    CLI   0(R4),0             EOR                                          
         BE    PTRX                YES, NO PASSIVE THEN                         
*                                                                               
         CLI   0(R4),DMPMKELQ      X'10' - TEXT ELEMENT?                        
         BE    PTR94               YES                                          
         LLC   R0,1(R4)                                                         
         AR    R4,R0                                                            
         B     PTR92                                                            
*                                                                               
REC      USING DMPKEY,RECKEY                                                    
PTR94    XC    KEY,KEY                                                          
         LA    R5,KEY                                                           
         USING DMPKEY,R5                                                        
*   COPY FROM ORIGINAL UPTO AND INCL A/M                                        
         MVC   DMPKEY(DMPPKADT-DMPKEY),REC.DMPKEY                               
         OI    DMPPKSTY,X'80'         MAKE IT A PASSIVE                         
         MVC   DMPPKADT,DMPMKADT-DMPMKELD(R4)    ACTIVITY DATE                  
         MVC   DMPPKTXT(DMPPKSPR-DMPPKTXT),REC.DMPKTEXT  TEXT + SEQ             
         MVC   DMPKSTAT(L'RECRCSTA),RECRCSTA      4 BYTES OF STATUS             
*                                                                               
         BAS   RE,ADDPSSV          ADD TO MY PASSIVE TABLE                      
         B     PTRX                                                             
         DROP  R5,REC                                                           
         EJECT                                                                  
PTR100   DS    0H                  ROOM FOR MORE PASSIVES HERE                  
         B     EXIT                                                             
*                                                                               
PTRX     BAS   RE,OUTPSSV          ELSE FLUSH PASSIVE TABLE                     
         XC    PSSVC,PSSVC         RESET COUNTERS, ETC.                         
         LA    RF,PSSVTAB                                                       
         ST    RF,NEXTPSSV                                                      
         MVI   0(RF),0                                                          
         B     EXIT                                                             
         EJECT                                                                  
********************************************************************            
*        ADDKEY - FILE  KEY TO DIRECTORY                                        
********************************************************************            
*                                                                               
ADDKEY   DS    0H                                                               
         L     R3,NEXTDIR                                                       
         MVC   0(L'KEY,R3),KEY                                                  
         LA    R3,L'KEY(R3)                                                     
         MVI   0(R3),0                                                          
         ST    R3,NEXTDIR                                                       
         BR    RE                                                               
*                                                                               
********************************************************************            
*        ADDPSSV - ADD PASSIVE TO TABLE                                         
********************************************************************            
*                                                                               
ADDPSSV  DS    0H                                                               
         L     RF,PSSVC                                                         
         CLC   PSSVC,=A(MAXPSSVS)                                               
         BL    ADP08                                                            
*                                                                               
         LR    R0,RE                                                            
         MVC   P(21),=C'**TOO MANY PASSIVES**'                                  
         GOTO1 VPRINT                                                           
         GOTO1 VHEXOUT,DMCB,RECKEY,P+22,40,=C'N'                                
         GOTO1 VPRINT                                                           
         LR    RE,R0                                                            
         B     ADP10                                                            
*                                                                               
ADP08    DS    0H                                                               
         L     RF,PSSVC                                                         
         LA    RF,1(RF)                                                         
         ST    RF,PSSVC                                                         
*                                                                               
         L     R3,NEXTPSSV                                                      
         MVC   0(L'KEY,R3),KEY                                                  
         LA    R3,L'KEY(R3)                                                     
         MVI   0(R3),0                                                          
         ST    R3,NEXTPSSV                                                      
*                                                                               
ADP10    DS    0H                                                               
         BR    RE                                                               
*                                                                               
********************************************************************            
*        OUTPSSV - FLUSH PASSSIVE TABLE                                         
********************************************************************            
*                                                                               
OUTPSSV  DS    0H                  FLUSH PASSIVE TABLE                          
         L     R3,NEXTDIR                                                       
         LA    R4,PSSVTAB                                                       
*                                                                               
OP4      DS    0H                                                               
         CLI   0(R4),0             END OF PASSIVE TABLE                         
         BE    OP10                                                             
         MVC   0(L'KEY,R3),0(R4)                                                
         LA    R3,L'KEY(R3)                                                     
         LA    R4,L'KEY(R4)                                                     
         MVI   0(R3),0                                                          
         B     OP4                                                              
*                                                                               
OP10     DS    0H                                                               
         BR    RE                                                               
*                                                                               
EXIT     XIT1                                                                   
*                                                                               
         GETEL R6,42,ELCODE                                                     
*                                                                               
         LTORG                                                                  
*                                                                               
NEXTDIR  DC    F'0'                                                             
NEXTPSSV DC    A(PSSVTAB)                                                       
PSSVC    DC    F'0'                COUNT OF PASSIVES                            
         DS    0D                                                               
*                                                                               
VPRINT   DC    V(PRINTER)                                                       
VHEXOUT  DC    V(HEXOUT)                                                        
*                                                                               
         DC    C'*PASSVS*'                                                      
MAXPSSVS EQU   100                 SHOULD BE PLENTY FOR ONE INVOICE             
PSSVTAB  DS    0X                                                               
         ORG   *+(L'KEY*MAXPSSVS)                                               
         DC    X'0000'                                                          
         LTORG                                                                  
         SPACE 2                                                                
WORKD    DSECT                                                                  
DUB      DS    D                                                                
DMCB     DS    6F                                                               
KEY      DS    XL40                                                             
KEYSAVE  DS    XL40                                                             
BYTE     DS    XL1                                                              
ELCODE   DS    XL1                                                              
WORKX    EQU   *                                                                
         EJECT                                                                  
***********************************************************************         
* GENERIC RECORD LAYOUT FOR XSPDIR/XSPFIL                                       
***********************************************************************         
RECORDD  DSECT                                                                  
RECKEY   DS    XL32                KEY                                          
RECKYSTA DS    XL4                 DIRECTORY STATUS BYTES                       
RECKYDA  DS    XL4                 DISK ADDRESS                                 
*                                                                               
         ORG   RECKYSTA                                                         
RECRCLEN DS    XL2                 RECORD LENGTH                                
RECRCSTA DS    XL4                 RECORD STATUS BYTES                          
RECRCLNK DS    XL4                 LINK FIELD                                   
*                                                                               
RECRCEL  DS    0X                  START OF FIRST ELEMENT                       
         EJECT                                                                  
         PRINT OFF                                                              
       ++INCLUDE SPGENAUTH                                                      
       ++INCLUDE SPGENSNV                                                       
       ++INCLUDE SPGENSLK                                                       
       ++INCLUDE NEGENUBILL                                                     
       ++INCLUDE SPTRNPAT                                                       
       ++INCLUDE SPGENCDORD                                                     
       ++INCLUDE SPGENWBFLT                                                     
       ++INCLUDE SPGENEMAIL                                                     
       ++INCLUDE DDDPRINT                                                       
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'054SXLDCPTR  07/10/17'                                      
         END                                                                    
