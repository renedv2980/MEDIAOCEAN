*          DATA SET DEGETMXM   AT LEVEL 019 AS OF 04/11/19                      
*PROCESS USING(WARN(15))                                                        
*CATALP DEGETMXM                                                                
         TITLE 'DEGETMXM - PROGRAM AVERAGES READ CONTROLLER'                    
***************************CHANGE LOG*******************************            
*      DATE       LVL    REASON                                                 
*                                                                               
********************************************************************            
* REGISTER USAGE: IMPORTANT!                                                    
* R3: USED TO CONTROL D-POINTER,T-POINTER TABLE.                                
********************************************************************            
GETMXM   RSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 MXMWORKL,**GETMXM,RA,CLEAR=YES                                   
         LR    R9,RC                                                            
         USING MXMWORKD,R9                                                      
         L     RC,0(R1)                                                         
         USING DEGETD,RC           RC=A(W/S)                                    
         USING COMFACSD,R8         R8=A(COMFACS)                                
         USING DBLOCKD,R6          R6=A(DBLOCK)                                 
         CLI   0(R1),0             TEST FIRST/NEXT TIME                         
         BNE   GETMXM40                                                         
************************************************************                    
* TEMPORARY CODE, PLEASE REMOVE!!!                                              
************************************************************                    
*        MVI   CAVGFLG,1                                                        
*        XC    DBSELDAY(5),DBSELDAY                                             
*        MVC   DBSELPRG,=X'4D6A'                                                
*        MVI   PDMSFLG,PDMSYES                                                  
************************************************************                    
* INITIALIZATION PORTION                                                        
************************************************************                    
         XC    DBFACTOR,DBFACTOR                                                
         XC    DBDIVSOR,DBDIVSOR                                                
         XC    WORKDAY,WORKDAY                                                  
         XC    WORKTIME,WORKTIME                                                
         LA    RE,PODTAB           D-POINTER BUFFER                             
         LA    RF,1200                                                          
         XCEF                                                                   
         MVC   PODTABX,=X'FFFF'                                                 
         LA    RE,PRGTAB           T-POINTER BUFFER                             
         LA    RF,1200                                                          
         XCEF                                                                   
         MVC   PRGTABX,=X'FFFF'                                                 
************************************************************                    
* DBEXTEND PORTION                                                              
************************************************************                    
         L     RE,DBEXTEND         CHECK FOR MULIPLE DAY/TIME                   
CHKNEXT  DS    0H                                                               
         LTR   RE,RE               NO MULTI D/T   - END IT                      
         BZ    DYTMNO                                                           
         USING DBXTLID,RE                                                       
         CLC   DBXTLID,=C'DYTM'    THIS IS THE ONE WE WANT                      
         BE    *+10                                                             
         CLC   DBXTLID,=C'ODYT'    OR THIS ONE                                  
         BE    *+12                                                             
         L     RE,DBXTLNXT                                                      
         B     CHKNEXT                                                          
*                                                                               
         MVC   DBSELDAY(5),DBXTLIST  SET FIRST DAY/TM                           
         ST    RE,ADTLIST                                                       
         MVI   DBXTLIDX,0                                                       
         DROP  RE                                                               
DYTMNO   DS    0H                                                               
*                                                                               
         L     RE,DBEXTEND         CHECK FOR CAVG INDICATOR                     
DYTMNO5  DS    0H                                                               
         LTR   RE,RE               NO MULTI D/T   - END IT                      
         BZ    CAVGNO                                                           
         USING DBXCAVD,RE                                                       
         CLC   DBXCAVID,=C'CAVG'   THIS IS THE ONE WE WANT                      
         BE    *+12                                                             
         L     RE,DBXCAVNX                                                      
         B     DYTMNO5                                                          
*                                                                               
         CLI   DBXCAV,DBXCVYQ                                                   
         BNE   *+8                                                              
         MVI   CAVGFLG,DBXCVYQ                                                  
         DROP  RE                                                               
CAVGNO   DS    0H                                                               
*                                                                               
         L     RE,DBEXTEND         CHECK FOR CAVG INDICATOR                     
CAVGNO5  DS    0H                                                               
         LTR   RE,RE               NO MULTI D/T   - END IT                      
         BZ    PDMSNO                                                           
         USING DBXPODM,RE                                                       
         CLC   DBXPMID,=C'PDMS'    THIS IS THE ONE WE WANT                      
         BE    *+12                                                             
         L     RE,DBXPMNX                                                       
         B     CAVGNO5                                                          
*                                                                               
         MVI   PDMSFLG,PDMSYES                                                  
         DROP  RE                                                               
PDMSNO   DS    0H                                                               
************************************************************                    
* ACCESS PORTION                                                                
************************************************************                    
         ZIC   R1,DBFUNCT          SAVE THE REQUESTED FUNCTION                  
         MVI   DBFUNCT,DBTSTACS    SET FOR VALIDATE                             
         GOTO1 ATSTACS             CALL TO VALIDATE ACCESS                      
         STC   R1,DBFUNCT          RESTORE ORIGINAL REQUEST                     
         CLI   DBERROR,0           EXIT IF NOT VALID                            
         BNE   GETX                                                             
*                                                                               
GETMXM1  OC    DBSELBK,DBSELBK     BOOK SET? WE ARE NOT DOING LATEST            
         BZ    GETMXMX             RETURN NOT FOUND                             
GETMXM2  DS    0H                                                               
         LA    R1,DBSELBK                                                       
         L     RF,AGETCTRL                                                      
         OC    ACONHDR,ACONHDR     TEST IF A(CONTROL TABLE) SET                 
         BNZ   *+6                                                              
         BASR  RE,RF               NO - GET AGENCY CONTROLS                     
*                                                                               
************************************************************                    
* DIRECTORY PORTION (MAJOR KEYS, PASSIVE KEYS)                                  
************************************************************                    
GETMXM8  DS    0H                                                               
         MVI   DBINTMED,C'N'                                                    
         CLI   DBSELSTA+4,C'C'                                                  
         BNE   *+8                                                              
         MVI   DBINTMED,C'C'                                                    
*                                                                               
         CLI   DBSELSTA+4,C'T'     DEFAULT BOOKTYPE FOR BROADCAST               
         BNE   *+16                IS 'A'                                       
         CLI   DBBTYPE,0                                                        
         BNE   *+8                                                              
         MVI   DBBTYPE,C'A'                                                     
*                                                                               
         CLI   DBFUNCT,DBVLNBK     VALIDATE NET/BOOK IGNORES DAY                
         BE    GETMXM25                                                         
*                                                                               
         GOTO1 AGETDQ                                                           
         OC    DBSELDAY(5),DBSELDAY                                             
         BNZ   GETMXM9                                                          
         OC    DBSELPRG,DBSELPRG                                                
         BNZ   GETMXM15                                                         
         MVI   DBERROR,EOF                                                      
         B     GETX                                                             
*                                                                               
GETMXM9  BAS   RE,ADJTIME          PRE 6AM REQUESTS                             
*                                                                               
GETMXM10 MVC   WORKDAY,DBSELDAY                                                 
         MVC   WORKTIME,DBSELTIM                                                
         NI    WORKDAY,X'7F'       TURN OFF X'80' BIT                           
         BAS   RE,TRNDAY           SET UP LOOP CONTROL BASED ON DAY             
         B     GETMXM17                                                         
*                                                                               
************************************************************                    
* T-POINTER PORTION:  PROGRAM MINUTES                                           
************************************************************                    
GETMXM15 MVI   PAVGFLG,PRGFLTY     ALL MINUTES FOR A PROGRAM                    
         BAS   RE,TRNPRG                                                        
         LA    R3,PRGTAB           RETRIEVE THE "T" POINTER                     
         USING PRGTABD,R3                                                       
GETMXM16 CLC   0(2,R3),=X'FFFF'                                                 
         BNE   *+12                                                             
         MVI   DBERROR,EOF                                                      
         B     GETMXM85                                                         
*                                                                               
         MVC   DAYPTR,PRGDAY       DAY                                          
         ZIC   RE,PRGTIME,2        START TIME                                   
         SRL   RE,4                                                             
         STCM  RE,3,WORKTIME                                                    
         ZIC   RE,PRGTIME+1,2      END TIME                                     
         SLL   RE,20                                                            
         SRL   RE,20                                                            
         STCM  RE,3,WORKTIME+2                                                  
         B     GETMXM25                                                         
         DROP  R3                                                               
************************************************************                    
* D-POINTER PORTION: COMMERCIAL MINUTES                                         
************************************************************                    
GETMXM17 CLI   CAVGFLG,DBXCVYQ     CAVG EXTENSION?                              
         BE    *+8                                                              
         CLI   PDMSFLG,PDMSYES     PDMS EXTENSION?                              
         BNE   GETMXM25                                                         
         BAS   RE,TRNPOD           RETRIEVES THE "D" POINTER                    
         LA    R3,PODTAB                                                        
         USING PODTABD,R3                                                       
GETMXM18 CLC   0(2,R3),=X'FFFF'                                                 
         BNE   *+12                                                             
         MVI   DBERROR,NOTFOUND    NOT FOUND FOR THE TIME RANGE                 
         B     GETMXM85            TRY NEXT DAY                                 
*                                                                               
         ZIC   RE,PODTIME,2        END TIME                                     
         SRL   RE,4                                                             
         STCM  RE,3,WORKTIME+2                                                  
         ZIC   RE,PODTIME+1,2      START                                        
         SLL   RE,20                                                            
         SRL   RE,20                                                            
         STCM  RE,3,WORKTIME                                                    
         DROP  R3                                                               
************************************************************                    
* M-POINTER PORTION: MXM RECORDS.                                               
*        -  DAYPTR NEEDS TO BE SET.                                             
************************************************************                    
GETMXM25 LA    R2,DBKEY            BUILD KEY OF DEMO RECORD                     
         USING MXMKEY,R2                                                        
         XC    MXMMAJOR,MXMMAJOR                                                
         MVI   MXMCODE,MXMCODEQ                                                 
         MVC   MXMMEDIA,DBINTMED                                                
         MVC   MXMSRC,DBACTSRC                                                  
         MVC   MXMBTYP,DBBTYPE                                                  
         MVC   MXMBOOK,DBSELBK                                                  
         CLI   DBSELSTA+4,C'C'     CABLE STORES NUMERIC CODE                    
         BNE   GETMXM28                                                         
         OC    DBCABNUM,DBCABNUM   MAKE SURE IT'S ASSIGNED                      
         BNZ   *+12                                                             
         MVI   DBERROR,EOF                                                      
         B     GETX                                                             
         MVC   MXMSTAT(4),DBCABNUM                                              
         MVI   MXMSTAT+4,C'C'                                                   
         B     *+10                                                             
GETMXM28 MVC   MXMSTAT,DBSELSTA                                                 
*                                                                               
         CLI   DBFUNCT,DBVLNBK     VALIDATE NET/BOOK IGNORES DAY                
         BE    *+10                                                             
         MVC   MXMDAY,DAYPTR                                                    
*                                                                               
         MVI   IOFLAG,DIR+HIGH+NTI READ HIGH/DIRECTORY                          
         GOTO1 AIO,IOFLAG                                                       
         BL    GETX                                                             
*                                                                               
         CLI   DBFUNCT,DBVLNBK                                                  
         BNE   GETMXM30                                                         
         CLC   DBKEY(MXMBOOK+4-MXMKEY),KEYSAVE                                  
         BE    GETX                                                             
         MVI   DBERROR,NOTFOUND                                                 
         B     GETX                                                             
*                                  CHECK DAY FOUND                              
GETMXM30 CLC   DBKEY,KEYSAVE                                                    
         BE    *+12                                                             
         MVI   DBERROR,NOTFOUND                                                 
         B     GETMXM80                                                         
         MVC   DBACTBK,MXMBOOK     BOOK                                         
*                                                                               
************************************************************                    
* FILE PORTION (MINOR KEYS)                                                     
*        -  WORKTIME NEEDS TO BE SET (START,END)                                
************************************************************                    
GETMXM40 XC    DBFACTOR,DBFACTOR                                                
         MVC   DBMINKEY,WORKTIME   WORKTIME=DBSELTIM OR MPDTIMES                
         LH    RE,DBMINKEY         SHOULD BE TRANSPARENT                        
         SLL   RE,4                                                             
         STH   RE,DBMINKEY                                                      
         MVI   IOFLAG,FILE+HIGH+NTI                                             
GETMXM50 GOTO1 AIO,IOFLAG                                                       
         L     R2,DBAREC                                                        
         TM    DBERROR,EOF                                                      
         BO    GETMXM80                                                         
*                                                                               
         ZICM  RE,MXMMINOR,2       WE STILL IN RANGE?                           
         SRL   RE,4                                                             
         STH   RE,TEMPMIN                                                       
         CLC   TEMPMIN,WORKTIME+2                                               
         BNH   *+8                                                              
         B     GETMXM80                                                         
************************************************************                    
* FILTER ON DURATION (DBSELDUR)                                                 
************************************************************                    
         BAS   RE,FILTDUR          FILTER LOGIC USING DBSELDUR                  
         BE    *+8                 GOOD                                         
         BNE   GETMXM70            BYPASS                                       
************************************************************                    
* FILTER ON PROGRAM (DBSELPRG)                                                  
************************************************************                    
         OC    DBSELPRG,DBSELPRG                                                
         BZ    *+16                                                             
         BAS   RE,FILTPRG          FILTER LOGIC USING DBSELPRG                  
         BE    *+8                 GOOD                                         
         BNE   GETMXM70            BYPASS                                       
*********************************************************************           
* THE CODE BELOW WAS COMMENTED OUT AFTER DATA WAS RELOADED. THE NEW             
* RECORDS NO LONGER INCLUDE THE '5D' ELEMENT.                                   
*                                                                               
* REMOVE ERRONEOUS '5D' ELEMENTS FROM MXM RECORDS.                              
*                                                                               
* ORIGINALLY, THE ZHOMES VALUE WAS ADDED TO THE MXM RECORDS                     
* IN THE '5D' ELEMENT. HOWEVER, NET TRANSACTIONAL USES THE SAME                 
* ELEMENT CODE ON THE ESTIMATION SIDE, AND '5D' SHOULD NEVER HAVE               
* BEEN USED ON THE DEMO RECORD.                                                 
*                                                                               
* THIS CODE REMOVES THE '5D' ELEMENT FROM THE MXM DEMO RECORD BEFORE            
* PASSING THE RECORD TO THE CALLER.                                             
*                                                                               
*********************************************************************           
*****    GOTO1 CHELLO,DMCB,(C'D',=C'PAVFIL  '),(X'5D',DBAREC),0,0               
*                                                                               
*********************************************************************           
* CODE TO ADD '5E' ELEMENT WHEN IT DOESN'T EXIST.                               
* THERE ARE SOME MINUTE BY MINUTE RECORDS WITH NO DEMO ELEMENTS (I.E.           
* ALL DEMO VALUES ARE ZERO). AS A RESULT, NO '5E' ELEMENT WAS ADDED.            
* THE ABSENCE OF THE '5E' ELEMENT IS CAUSING DEMEL/DEMOMATH TO GET              
* CONFUSED ABOUT THE FILE/MEDIA/SOURCE. THIS IS AN ATTEMPT TO FIX THIS          
* SCENARIO BY ADDING A "FAKE" 5E ELEMENT WHEN IT IS MISSING.                    
*********************************************************************           
         GOTO1 MXGETEL,DMCB,X'5E'  SEARCH FOR '5E' ELEMENT                      
         ZIC   R1,DMCB+12                                                       
         CLI   DMCB+12,0                                                        
         BE    GETMXM55            ELEMENT FOUND.                               
*                                                                               
         MVC   ELM5E(2),=X'5E07'   IF NOT FOUND,ADD ELEMENT TO THE END          
         MVC   ELM5E+2(3),=C'PNN'  F/M/S FOR BROADCAST AND SYNDICATION          
         CLI   DBSELSTA+4,C'C'                                                  
         BNE   *+10                                                             
         MVC   ELM5E+2(3),=C'CNN'  F/M/S FOR CABLE                              
         MVC   ELM5E+5(2),DBSELBK  CURRENT BOOK                                 
         GOTO1 CHELLO,DMCB,(C'P',=C'PAVFIL  '),DBAREC,ELM5E,=C'ADD=END'         
*********************************************************************           
* RETURN RECORD TO USER                                                         
*********************************************************************           
GETMXM55 MVI   DBMODE,DBMNEXT      TRY TO RETURN TO USER                        
         GOTO1 ATSTMRKT                                                         
         BNE   GETX                                                             
*                                                                               
         LA    R1,1                UPDATE FACTOR AND DIVISOR                    
         STH   R1,DBFACTOR                                                      
         AH    R1,DBDIVSOR                                                      
         STH   R1,DBDIVSOR                                                      
*                                                                               
         BAS   RE,GETX             ONLY RETURN POINT TO HOOK.(11/08)            
*                                                                               
         MVC   DBINTMED,SVMED                                                   
         MVC   DBSELDAY(5),SVDTM                                                
************************************************************                    
* LOOP CONTROL PORTION                                                          
*        - WORKTIME+2, DBSELTIM+2, D-PTR,T-PTR TABLE                            
*          ARE USED UNIQUELY BASED ON REQUEST.                                  
************************************************************                    
GETMXM70 CLC   TEMPMIN,WORKTIME+2                                               
         BH    GETMXM80            HIGH = DONE.  SHOULDN'T HIT THOUGH           
         BNE   *+12                LOW = GET NEXT                               
         TM    MXMMINOR+1,MXMMMORE EQUALS = DONE IF IT'S THE LAST ONE.          
         BO    *+12                HIGH = WON'T BE,WE TESTED AT MXM50           
         MVI   IOFLAG,FILE+SEQ+NTI                                              
         B     GETMXM50                                                         
*                                                                               
GETMXM80 CLI   PAVGFLG,PRGFLTY     ARE WE IN A T-PTR TABLE DRIVEN LOOP?         
         BNE   *+12                                                             
         LA    R3,PRGTABL(R3)                                                   
         B     GETMXM16                                                         
*                                                                               
         CLI   CAVGFLG,DBXCVYQ     ARE WE IN A D-PTR TABLE DRIVEN LOOP?         
         BNE   *+12                                                             
         LA    R3,PODTABL(R3)                                                   
         B     GETMXM18                                                         
*                                                                               
         CLC   WORKTIME+2(2),DBSELTIM+2  OR A DBSELTIM DRIVEN LOOP.             
         BNL   GETMXM85                                                         
         ZICM  RE,WORKTIME+2,2                                                  
         AHI   RE,1                                                             
         STCM  RE,3,WORKTIME                                                    
         MVC   WORKTIME+2(2),DBSELTIM+2                                         
         B     GETMXM25                                                         
*                                                                               
GETMXM85 CLI   WORKDAY,0           LAST DAY OF THE WEEK JUST FINISHED?          
         BE    GETMXM90            CHECK THE MULTIPLE DAY/TIME LOOP             
         BAS   RE,TRNDAY                                                        
         MVC   WORKTIME,DBSELTIM                                                
         B     GETMXM17                                                         
*                                                                               
GETMXM90 L     RE,DBEXTEND                                                      
GETMXM95 DS    0H                                                               
         LTR   RE,RE               NO MULTI D/T   - END IT                      
         BZ    GETMXMX                                                          
         USING DBXTLID,RE                                                       
         CLC   DBXTLID,=C'DYTM'    THIS IS THE ONE WE WANT                      
         BE    *+10                                                             
         CLC   DBXTLID,=C'ODYT'    THIS IS THE ONE WE WANT                      
         BE    *+12                                                             
         L     RE,DBXTLNXT                                                      
         B     GETMXM95                                                         
*                                                                               
         ZIC   RF,DBXTLIDX                                                      
         LA    RF,1(RF)                                                         
         STC   RF,DBXTLIDX                                                      
         MH    RF,=H'5'                                                         
         LA    RF,DBXTLIST(RF)                                                  
         CLI   0(RF),0                                                          
         BE    GETMXMX             END OF DAY TIME LIST                         
*                                                                               
         MVC   DBSELDAY(5),0(RF)                                                
         B     GETMXM8                                                          
*                                                                               
GETMXMX  MVI   DBERROR,EOF                                                      
         B     GETX                                                             
         DROP  RE                                                               
************************************************************                    
* EXIT PORTION: EXIT TO HOOK, REPORT ERROR, END PROCEDURE.                      
************************************************************                    
GETX     CLI   DBERROR,0           TEST IF ERROR SET                            
         BE    *+14                                                             
         XC    DBAQUART,DBAQUART   YES - CLEAR QTR HOUR ADDRESS                 
         MVI   DBMODE,DBMFRST      AND RESET SEQUENTIAL MODE                    
         MVC   DBLSTFNC,DBFUNCT    EQUATE LAST FUNCTION TO THIS                 
         L     RF,DBAREC           SET UP FOR PROGRAM                           
         AH    RF,DBDTADSP                                                      
         ST    RF,DBAQUART                                                      
         MVI   DBRECTYP,DBRECNTI                                                
*                                                                               
         OC    AUSERRTN,AUSERRTN   TEST IF USER HOOK SUPPLIED                   
         BZ    GETXX               NO - RETURN TO CALLER                        
         CLI   DBERROR,0           TEST IF ERROR SET                            
         BNE   GETXX               YES - RETURN TO CALLER                       
*                                                                               
         CLI   DBMODE,DBMNEXT      TEST IF SEQUENTIAL MODE                      
         BE    *+8                                                              
         LA    RE,GETXX            NO - SET RETURN TO EXIT                      
*                                  SAVE REGS & GO TO USER HOOK                  
         MVC   SVMED,DBINTMED                                                   
         MVC   SVDTM(5),DBSELDAY                                                
*                                  SAVE REGS & GO TO USER HOOK                  
         NTR1                                                                   
         L     RF,AUSERRTN                                                      
         L     RE,ROOTRD                                                        
         LR    R0,RE               R0=A(CALLER'S RD)                            
         LM    R1,RC,24(RE)        R1-RC=CALLERS REGISTERS                      
         BASR  RE,RF               GO TO USER HOOK                              
*                                                                               
GETXX    XIT1  ,                   EXIT TO CALLER OR DEMAND                     
         EJECT                                                                  
* LITERALS ETC.                                                                 
*                                                                               
         LTORG                                                                  
         DROP  R2                                                               
************************************************************                    
ADJTIME  NTR1  ,                   ADJUST REQUESTS < 6AM                        
         LA    R1,DBSELTIM                                                      
         LA    R0,2                                                             
AT05     CLC   0(2,R1),=AL2(600)                                                
         BNL   AT10                                                             
         ICM   RF,3,0(R1)                                                       
         AH    RF,=AL2(2400)                                                    
         STCM  RF,3,0(R1)                                                       
AT10     LA    R1,2(R1)                                                         
         BCT   R0,AT05                                                          
         XIT1                                                                   
************************************************************                    
TRNDAY   NTR1  ,                   TRANSLATE DAY OF WEEK NUMBER                 
         LA    RE,DAYTAB                                                        
TD05     ZIC   R1,0(RE)                                                         
         EX    R1,TD06                                                          
         BNZ   TD10                                                             
         B     *+8                                                              
TD06     TM    WORKDAY,0                                                        
         LA    RE,L'DAYTAB(RE)                                                  
         CLI   0(RE),X'FF'                                                      
         BNE   TD05                                                             
         BE    TDX                                                              
TD10     MVC   DAYPTR,1(RE)                                                     
         XC    WORKDAY,0(RE)                                                    
TDX      XIT1                                                                   
************************************************************                    
* TRNPRG: RETRIEVES THE "T" POINTERS USING INPUT.                               
*   ON RETURN:   PRGTAB WILL BE POPULATED WITH ALL DAY/TIME                     
*                OF A PARTICULAR PROGRAM (DBSELPRG) WITHIN                      
*                THE "BOOK!!! NOT DAY".                                         
*   PRGTAB:      XL1: DAY, XL3: START,END TIME                                  
************************************************************                    
TRNPRG   NTR1                                                                   
         LA    R2,DBKEY                                                         
         XC    DBKEY,DBKEY                                                      
         LA    R3,PRGTAB                                                        
         USING PRGTABD,R3                                                       
         USING MPTKEY,R2                                                        
         MVI   MPTCODE,MPTCODEQ                                                 
         MVC   MPTMEDIA,DBINTMED                                                
         MVC   MPTSRC,DBACTSRC                                                  
         MVC   MPTBTYP,DBBTYPE                                                  
         MVC   MPTBOOK,DBSELBK                                                  
         CLI   DBSELSTA+4,C'C'     CABLE STORES NUMERIC CODE                    
         BNE   TG20                                                             
         OC    DBCABNUM,DBCABNUM   MAKE SURE IT'S ASSIGNED                      
         BNZ   *+12                                                             
         MVI   DBERROR,EOF                                                      
         B     GETX                                                             
         MVC   MPTSTAT(4),DBCABNUM                                              
         MVI   MPTSTAT+4,C'C'                                                   
         B     *+10                                                             
TG20     MVC   MPTSTAT,DBSELSTA                                                 
         MVC   MPTSTYP,DBSTYPE                                                  
*                                                                               
         MVI   IOFLAG,DIR+HIGH+NTI READ HIGH/DIRECTORY                          
TG21     GOTO1 AIO,IOFLAG                                                       
         BL    GETX                                                             
         CLI   KEYSAVE+MPTSTYP-MPTCODE,0   STYPE SPECIFIED?                     
         BNE   TG25                                                             
         CLC   DBKEY(MPTSTAT-MPTCODE+1),KEYSAVE   RETURN EITHER                 
         BE    TG30                                                             
         BNE   TGX                                                              
TG25     CLC   DBKEY(MPTSTYP-MPTCODE+1),KEYSAVE   BREAKOUT?                     
         BNE   TGX                                                              
TG30     CLC   MPTPNUM,DBSELPRG                                                 
         BNE   TG60                                                             
         CLI   CAVGFLG,DBXCVYQ     ONLY LOOKING FOR COMMERICAN MIN              
         BNE   *+12                                                             
         CLI   MPTMTYP,MPTMTYPC                                                 
         BNE   TG60                                                             
         MVC   PRGDAY,MPTDAY       DAY AND TIME                                 
         MVC   PRGTIME,MPTTIMES                                                 
         LA    R3,PRGTABL(R3)                                                   
TG60     MVI   IOFLAG,DIR+SEQ+NTI  READ SEQUENTIAL                              
         B     TG21                                                             
*                                                                               
TGX      MVC   0(2,R3),=X'FFFF'                                                 
         XIT1                                                                   
         DROP  R2,R3                                                            
                                                                                
************************************************************                    
* TRNPOD: RETRIEVES THE "D" POINTERS USING INPUT.                               
*   ON RETURN:   PODTAB WILL BE POPULATED WITH ALL THE                          
*                PODS WITHIN A "DAY!!", ACROSS ALL PROGRAMS.                    
*                WITHIN THE TIME RANGE PROVIDED. DBSELTIM.                      
*                XL2: NTI NUMBER, XL3: START/END MINUTE                         
************************************************************                    
TRNPOD   NTR1                                                                   
         LA    R2,DBKEY                                                         
         XC    DBKEY,DBKEY                                                      
         LA    R3,PODTAB                                                        
         USING PODTABD,R3                                                       
         USING MPDKEY,R2                                                        
         MVI   MPDCODE,MPDCODEQ                                                 
         MVC   MPDMEDIA,DBINTMED                                                
*        MVC   MPDSRC,DBACTSRC                                                  
         MVI   MPDSRC,C'N'         WE ONLY HAVE LIVE T-PTR                      
         MVC   MPDBTYP,DBBTYPE                                                  
         MVC   MPDBOOK,DBSELBK                                                  
         CLI   DBSELSTA+4,C'C'     CABLE STORES NUMERIC CODE                    
         BNE   TP20                                                             
         OC    DBCABNUM,DBCABNUM   MAKE SURE IT'S ASSIGNED                      
         BNZ   *+12                                                             
         MVI   DBERROR,EOF                                                      
         B     GETX                                                             
         MVC   MPDSTAT(4),DBCABNUM                                              
         MVI   MPDSTAT+4,C'C'                                                   
         B     *+10                                                             
TP20     MVC   MPDSTAT,DBSELSTA                                                 
         MVC   MPDDAY,DAYPTR                                                    
         ZIC   RE,WORKTIME,2       FILL IN START TIME                           
         SLL   RE,4                                                             
         STCM  RE,3,MPDTIMES                                                    
*                                                                               
         MVI   IOFLAG,DIR+HIGH+NTI READ HIGH/DIRECTORY                          
TP25     GOTO1 AIO,IOFLAG                                                       
         BL    GETX                                                             
         CLC   DBKEY(MPDDAY-MPDCODE+1),KEYSAVE                                  
         BNE   TPX                 NOT FOUND FOR THE DAY                        
         ZICM  RE,MPDTIMES+1,2     COMPARE POD START TIME                       
         SLL   RE,20                                                            
         SRL   RE,20               TO                                           
         STCM  RE,3,TEMPMIN        SELECTED TIME RANGE                          
         CLI   PDMSFLG,PDMSYES     POD MINUTE REQUESTS?                         
         BE    TP30                                                             
         CLC   TEMPMIN,WORKTIME+2  START > RANGE?  DONE                         
         BH    TPX                                                              
         ZIC   RE,MPDTIMES,2                                                    
         SRL   RE,4                                                             
         STCM  RE,3,TEMPMIN                                                     
         CLC   TEMPMIN,WORKTIME+2  END > RANGE? RANGE = END                     
         BNH   TP40                                                             
         ZICM  RE,WORKTIME+2,2                                                  
         SLL   RE,12                                                            
         ZIC   R0,MPDTIMES+1,2                                                  
         SLL   R0,20                                                            
         SRL   R0,20                                                            
         OR    RE,R0                                                            
         STCM  RE,7,MPDTIMES                                                    
         B     TP40                                                             
*                                                                               
TP30     CLC   TEMPMIN,WORKTIME+2  POD START > REQUEST?                         
         BH    TPX                 NOT IN THIS POD                              
*                                                                               
TP40     OC    DBSELPRG,DBSELPRG   FILTER ON PROGRAM                            
         BZ    TP50                                                             
         CLC   MPDNTI,DBSELPRG                                                  
         BNE   TP60                                                             
TP50     MVC   PODNTI,MPDNTI                                                    
         MVC   PODTIME,MPDTIMES                                                 
         LA    R3,PODTABL(R3)                                                   
TP60     MVI   IOFLAG,DIR+SEQ+NTI  READ SEQUENTIAL                              
         B     TP25                                                             
*                                                                               
TPX      MVC   PODNTI,=X'FFFF'                                                  
         DROP  R2,R3                                                            
         XIT1                                                                   
************************************************************                    
FILTDUR  NTR1                                                                   
         L     R2,DBAREC                                                        
         USING MXMKEY,R2                                                        
*                                                                               
         XC    TEMPDUR,TEMPDUR                                                  
         MVC   TEMPDUR+1(1),DBSELDUR                                            
*                                                                               
FD10     GOTO1 MXGETEL,DMCB,NTCODEQU X'22'                                      
         ZIC   R1,DMCB+12                                                       
         CLI   DMCB+12,0                                                        
         BNE   FDX                                                              
         L     R1,DMCB+12                                                       
         USING NTELEM,R1                                                        
*                                                                               
         LA    RE,X'D0'                                                         
         CLI   DBSELDUR,X'FE'                                                   
         BNE   *+8                                                              
         LA    RE,X'20'                                                         
         CLI   DBSELDUR,X'FF'                                                   
         BNE   *+8                                                              
         LA    RE,X'00'                                                         
*                                                                               
         CLI   DBSELDUR,0                                                       
         BE    FD20                                                             
         CLI   DBSELDUR,240                                                     
         BH    FD20                                                             
         LA    RE,X'50'                                                         
         CLC   NTDUR2,TEMPDUR                                                   
         EX    RE,FDXBRAN1                                                      
         B     FDXTRUE                                                          
FD20     CLC   NTDUR2,=H'15'                                                    
         EX    RE,FDXBRAN1                                                      
*                                                                               
FDXBRAN1 BC    0,FDXFALSE                                                       
*                                                                               
FDXTRUE  CR    R1,R1                                                            
         B     FDX                                                              
FDXFALSE LA    R1,1(RE)                                                         
         CR    R1,RE                                                            
FDX      XIT1                                                                   
************************************************************                    
FILTPRG  NTR1                                                                   
         L     R2,DBAREC                                                        
         USING MXMKEY,R2                                                        
*                                                                               
FP10     GOTO1 MXGETEL,DMCB,PHTCODEQ    X'10'                                   
         ZIC   R1,DMCB+12                                                       
         CLI   DMCB+12,0           HELLO ERROR CODE                             
         BNE   FPX                                                              
         L     R1,DMCB+12          ADDRESS RETURNED FROM HELLO                  
         USING PHTELEM,R1                                                       
         CLC   DBSELPRG,PHTDDS     FOUND THE PROGRAM WE LOOKING FOR             
         BE    FPX                                                              
         TM    MXMMINOR+1,MXMMMORE FINAL RECORD OF THIS MINUTE                  
         BO    FPXFALSE                                                         
         MVI   IOFLAG,FILE+SEQ+NTI                                              
         GOTO1 AIO,IOFLAG                                                       
         B     FP10                                                             
*                                                                               
FPXTRUE  CR    R1,R1                                                            
         B     FPX                                                              
FPXFALSE LA    R1,1(RE)                                                         
         CR    R1,RE                                                            
FPX      XIT1                                                                   
         DROP  R2,R1                                                            
************************************************************                    
MXGETEL  NTR1                                                                   
         MVC   BYTE,DMCB+3                                                      
         GOTO1 CHELLO,DMCB,(C'G',=C'DEMFIL'),(BYTE,DBAREC),0                    
         XIT1                                                                   
************************************************************                    
*                                                                               
         SPACE 1                                                                
         DS    0H                                                               
EFFS     DC    X'FFFFFFFF'                                                      
ARZERO   DC    16F'0'                                                           
*                                                                               
DAYTAB   DS    0XL(2)                                                           
         DC    X'40',X'01'                                                      
         DC    X'20',X'02'                                                      
         DC    X'10',X'03'                                                      
         DC    X'08',X'04'                                                      
         DC    X'04',X'05'                                                      
         DC    X'02',X'06'                                                      
         DC    X'01',X'07'                                                      
         DC    X'FF'                                                            
         DROP  RA,RB                                                            
*                                                                               
         LTORG                                                                  
*********************************************************************           
         DROP  R6,R8,R9,RC                                                      
         EJECT                                                                  
*                                                                               
       ++INCLUDE DEGETD                                                         
         EJECT                                                                  
* ++INCLUDE FASSB                                                               
* ++INCLUDE FASYSFAC                                                            
* ++INCLUDE DEDBEXTRAD                                                          
* ++INCLUDE DDMASTD                                                             
         PRINT OFF                                                              
       ++INCLUDE FASSB                                                          
         EJECT                                                                  
       ++INCLUDE FASYSFAC                                                       
         EJECT                                                                  
       ++INCLUDE DEDBEXTRAD                                                     
         EJECT                                                                  
       ++INCLUDE DDMASTD                                                        
         SPACE 1                                                                
         PRINT ON                                                               
         TITLE 'DEGETMXM - PROGRAM AVERAGES READ CONTROLLER (LOCAL WORK+        
               AREA)'                                                           
***********************************************************************         
*====================== GETMXM'S LOCAL WORK AREA =====================*         
                                                                                
PODTABD  DSECT                                                                  
PODNTI   DS    XL2                                                              
PODTIME  DS    XL3                                                              
PODTABL  EQU   *-PODTABD                                                        
*                                                                               
PRGTABD  DSECT                                                                  
PRGDAY   DS    XL1                                                              
PRGTIME  DS    XL3                                                              
PRGTABL  EQU   *-PRGTABD                                                        
*                                                                               
MXMWORKD DSECT                                                                  
*                                                                               
WORKDAY  DS    XL1                                                              
WORKTIME DS    XL4                                                              
TEMPDUR  DS    XL2                 USED MORE OR LESS FOR COMPARES               
TEMPMIN  DS    H                   USED MORE OR LESS FOR COMPARES               
DAYPTR   DS    XL1                                                              
PODTAB   DS    XL1200                                                           
PODTABX  DS    XL2                                                              
PRGTAB   DS    XL1200                                                           
PRGTABX  DS    XL2                                                              
CAVGFLG  DS    XL1                                                              
PAVGFLG  DS    XL1                                                              
PDMSFLG  DS    XL1                                                              
PRGFLTY  EQU   1                                                                
PDMSYES  EQU   1                                                                
*                                                                               
SVMED    DS    XL1                 SAVE THE MEDIA                               
SVDTM    DS    XL5                 SAVE DBSELDAY AND TIME                       
*                                                                               
ELM5E    DS    XL7                 CREATE FAKE '5E' ELEMENT                     
*                                                                               
MXMWORKX EQU   *                                                                
MXMWORKL EQU   MXMWORKX-MXMWORKD                                                
*                                                                               
*                                                                               
***********************************************************************         
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'019DEGETMXM  04/11/19'                                      
         END                                                                    
