*          DATA SET MYGETNTIB  AT LEVEL 003 AS OF 05/01/02                      
*CATALP DEGETNTB                                                                
DEGETNTI TITLE '- NTI READ CONTROLLER'                                          
GETNTI   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 MYWORKX-MYWORK,**GETNTI,RA,CLEAR=YES                             
         LR    R9,RC               SAVE SPANNING WORK AREAS                     
         LR    R7,R1                                                            
         L     RC,0(R1)                                                         
         USING DEGETD,RC           RC=A(W/S)                                    
         USING DBLOCKD,R6                                                       
         USING COMFACSD,R8                                                      
         USING MYWORK,R9           R9=LOCAL W/S                                 
*                                                                               
         XC    ADEFINE,ADEFINE                                                  
         CLI   SPNETFLG,0                                                       
         BE    NOADFN              DON'T GET ADDR OF DEFINE                     
         LA    R1,DMCB                                                          
         L     RF,CCALLOV                                                       
         MVC   DMCB+4(4),=X'D9000A26'                                           
         GOTO1 (RF),(R1),0         GET A(DEFINE) FROM CALLOV                    
         MVC   ADEFINE,DMCB        SAVE AWAY FOR LATER USE                      
*                                                                               
NOADFN   LA    R4,SPANAD                                                        
         STCM  R4,15,DBSPANAD      WORK AREA FOR SPANNED RECORDS                
         L     R4,DBAREC           ORIG USERS DBAREC                            
         CLI   SPNETFLG,0          FOR REP, PURE#=PRG#                          
         BE    *+18                                                             
         CLI   DBFUNCT,DBGETPUR                                                 
         BNE   *+10                                                             
         MVC   DBSELPRG,DBSELPUR                                                
*                                                                               
         CLI   0(R1),0                                                          
         BNE   GOTUNIV                                                          
         CLI   DBSELSTA+4,C'H'                                                  
         BNE   GOTUNIV                                                          
         TM    SPNETFLG,SPNHTQ     SPOT-NHT CALL?                               
         BO    *+12                                                             
         CLI   DBSELSRC,C'N'       OR REG NET-NHT CALL                          
         BNE   GOTUNIV                                                          
*                                                                               
GETNTU   LA    RE,SPANAD                                                        
         LA    RE,4(RE)                                                         
         ST    RE,DBAREC                                                        
         XC    DBKEY,DBKEY                                                      
         MVC   DBKEY(8),=C'PNNUUUUH'                                            
         MVC   DBKEY+1(1),DBSELMED                                              
         MVC   DBKEY+10(2),DBSELBK                                              
         MVI   IOFLAG,DIR+HIGH                                                  
         GOTO1 AIO,IOFLAG                                                       
         BNE   GOTUFAIL                                                         
         CLC   DBKEY+10(2),DBSELBK                                              
         BNE   GOTUFAIL                                                         
         CLC   DBKEY(8),KEYSAVE                                                 
         BNE   GOTUFAIL                                                         
         MVC   SPANAD(4),=C'NTHU'                                               
         LA    RE,SPANAD+4                                                      
         MVC   DIRSAVE,DBKEY                                                    
         MVI   IOFLAG,FILE+READ                                                 
         GOTO1 AIO,IOFLAG                                                       
         BNE   GOTUFAIL                                                         
         XC    DBNDXDA,DBNDXDA                                                  
         XC    DBNDXDA2,DBNDXDA2                                                
         LA    RE,SPANAD+4                                                      
         USING PRKEY,RE                                                         
         LA    RE,PRFRSTEL                                                      
         SR    RF,RF                                                            
GETNHTU1 CLI   0(RE),0             TEMP FIX FOR ELEMENT CODES                   
         BE    GOTUNIV                                                          
         CLI   0(RE),X'41'                                                      
         BNE   *+8                                                              
         MVI   0(RE),X'4B'                                                      
         CLI   0(RE),X'42'                                                      
         BNE   *+8                                                              
         MVI   0(RE),X'4C'                                                      
         IC    RF,1(RE)                                                         
         AR    RE,RF                                                            
         B     GETNHTU1                                                         
         DROP  RE                                                               
GOTUFAIL XC    DBNDXDA,DBNDXDA                                                  
         XC    DBNDXDA2,DBNDXDA2                                                
*                                                                               
GOTUNIV  LR    R1,R7                                                            
         ST    R4,DBAREC           RESTORE ORIG/USERS DBAREC                    
         CLI   0(R1),0                                                          
         BNE   GETNTI4                                                          
*                                                                               
         ZIC   R1,DBFUNCT          SAVE THE REQUESTED FUNCTION                  
         MVI   DBFUNCT,DBTSTACS    SET FOR VALIDATE                             
         GOTO1 ATSTACS             CALL TO VALIDATE ACCESS                      
         STC   R1,DBFUNCT          RESTORE ORIGINAL REQUEST                     
         CLI   DBERROR,0           EXIT IF NOT VALID                            
         BNE   GETX                                                             
*                                                                               
         LA    R1,DBSELBK          SET THE CONTOL TABLE                         
         L     RF,AGETCTRL                                                      
         OC    ACONHDR,ACONHDR                                                  
         BNZ   *+6                                                              
         BASR  RE,RF                                                            
*                                                                               
         CLI   DBSELSTA+4,C'D'                                                  
         BE    *+8                                                              
         CLI   DBSELSTA+4,C'T'     ONLY FOR REG. NETWORKS                       
         BE    *+8                                                              
         CLI   DBSELSTA+4,C'S'     AND SYNDICATORS                              
         BNE   GETNTIA                                                          
         CLC   DBSELBK,=X'5724'    DON'T NEED BTYPE PRIOR TO 8736               
         BL    GETNTIA                                                          
         CLC   DBSELBK,=X'5731'    CHECK FOR OUT OF ORDER WEEKS                 
         BE    GETNTIA                                                          
         CLC   DBSELBK,=X'5732'                                                 
         BE    GETNTIA                                                          
         CLC   DBSELBK,=X'5733'                                                 
         BE    GETNTIA                                                          
         CLI   DBBTYPE,C'D'        KILL DIARY AFTER THESE DATES                 
         BNE   *+8                                                              
         MVI   DBBTYPE,X'FE'                                                    
         CLI   DBBTYPE,0           ALLOW USER TO SPECIFY                        
         BNE   GETNTIA                                                          
         MVI   DBBTYPE,C'A'        FORCE TO A (NO DIARY USE PM)                 
*                                                                               
GETNTIA  CLI   DBBTYPE,C'D'        DIARY ONLY                                   
         BNE   *+8                                                              
         MVI   DBBTYPE,0           CHANGE TO REGULAR BOOK                       
*                                                                               
         SR    R0,R0                                                            
         SR    R1,R1                                                            
         ICM   R1,12,DBSEL1WK      DBSEL1WK'S DAY REPLACES DBSELDAY             
         LTR   R1,R1               CHECK FOR SPECIFIC DAY                       
         BZ    GETNTI1                                                          
*                                                                               
         SLDL  R0,7                SHIFT YEAR                                   
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  WORK+0(2),DUB                                                    
         SR    R0,R0                                                            
         SLDL  R0,4                SHIFT MONTH                                  
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  WORK+2(2),DUB                                                    
         SR    R0,R0                                                            
         SLDL  R0,5                SHIFT DAY                                    
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  WORK+4(2),DUB                                                    
*                                                                               
         GOTO1 CGETDAY,DMCB,WORK,DUB                                            
         MVC   DBSELDAY,0(R1)      USE DAY RETURNED BY GETDAY                   
*                                                                               
GETNTI1  ICM   R3,15,DBAOPT        A(OPTIMIZATION AREA)                         
         BZ    GETNTI4                                                          
         OC    DBLOPT,DBLOPT       TEST OPT AREA LEN                            
         BNZ   *+14                                                             
         XC    DBAOPT,DBAOPT       NULLIFY OPTIMIZATION FOR MISSING LEN         
         B     GETNTI4                                                          
*                                                                               
         USING NOPTD,R3                                                         
         LR    R1,R3               SAVE A(OPT AREA) IN R1                       
         MVC   WORK(L'DBSELBK),DBSELBK                                          
         MVC   WORK+L'DBSELBK(L'DBSELSTA),DBSELSTA                              
         XC    2(2,R3),2(R3)       CLEAR DISP TO NEXT AVAILABLE NTRY            
         SR    R5,R5                                                            
         ICM   R5,3,0(R3)          LENGTH OF OPT USED SO FAR                    
         BZ    GETNTI2                                                          
         C     R5,=F'2000'         MAXIMUM REALLY USEFUL                        
         BH    GETNTI2                                                          
*                                                                               
         LA    R3,4(R3)            POINT PAST HEADER                            
         LA    R4,NOPTLEN          BXLE INCREMENT                               
         LA    R5,0(R1,R5)         POINT TO END OF AREA                         
         SR    R5,R4               BACK UP 1 NTRY AS BXLE LIMIT                 
         CLC   WORK(L'NOPTKEY),NOPTKEY                                          
         BE    GETNTI3             FOUND BOOK/NETWORK                           
         BXLE  R3,R4,*-10                                                       
         MVC   2(2,R1),0(R1)       DISP TO NXT NTRY IS LEN USED SO FAR          
         B     GETNTI4                                                          
         SPACE 1                                                                
GETNTI2  DS    0H                  FIRST OPT ENTRY LOGIC                        
         MVC   0(4,R3),=X'00040004'                                             
         LA    R3,4(R3)                                                         
         B     GETNTI4                                                          
         SPACE                                                                  
GETNTI3  DS    0H                                                               
         MVC   DBNDXDA2,NOPTDA     SET D/A FROM OPT ENTRY                       
         MVC   DBNDXDA,NOPTDA                                                   
         B     GETNTI4                                                          
         SPACE                                                                  
GETNTI4  DS    0H                                                               
         XC    DBFACTOR,DBFACTOR                                                
         CLI   DBLSTNXT,1          TEST FOR RENTRY-ALL PRGS FOR BK/STA          
         BE    GETNTI6                                                          
         OC    DBSELPRG,DBSELPRG   TEST FOR PROGRAM NUMBER                      
         BNZ   GETNTI5                                                          
         OC    DBSELDAY(5),DBSELDAY TEST FOR DAY/TIME                           
         BNZ   GETNTI10             FILTERING LOGIC                             
         OC    ADTLIST(4),ADTLIST   TEST FOR DAY/TIME LIST                      
         BNZ   GETNTI10             FILTERING LOGIC                             
         DROP  R3                                                               
         SPACE                                                                  
**********************************************************************          
*GETNTI5 -     READ THRU Q-RECDS FOR ALL PROGRAMS OR SPECIFIC ONE               
*        NO TIME/DAY FILTERING SO THE N-RECDS NOT USED HERE                     
**********************************************************************          
GETNTI5  DS    0H                                                               
         CLI   DBSELSTA+4,C'H'     FOR NHI NAD                                  
         BE    *+8                                                              
         CLI   DBSELSTA+4,C'M'     FOR SYNDICATION NAD                          
         BE    *+8                                                              
         CLI   DBSELSTA+4,C'S'     OR SYNDICATION NTI                           
         BNE   GETNTI5Q                                                         
         OC    DBSELPRG,DBSELPRG   PROGRAM REQUESTS ONLY                        
         BZ    GETNTI5Q                                                         
         LA    R2,DBKEY            EQUATE SYNDICATION STATIONS                  
         USING PKKEY,R2            USING PROGRAM NUMBER                         
         XC    PKKMAJOR,PKKMAJOR                                                
         MVI   PKCODE,PKCODEQU                                                  
         MVC   PKMEDIA,INTMED                                                   
         CLI   PKMEDIA,C'H'                                                     
         BNE   *+8                                                              
         MVI   PKMEDIA,C'N'                                                     
         CLI   DBMEDOVR,0          OVERRIDE MEDIA                               
         BE    *+10                                                             
         MVC   PKMEDIA,DBMEDOVR                                                 
         CLI   DBSELMED,C'W'                                                    
         BNE   *+8                                                              
         MVI   PKMEDIA,C'W'                                                     
         MVC   PKSRC,DBACTSRC                                                   
         CLI   SPNETFLG,0          SPOT->NET LK UP                              
         BE    *+8                                                              
         MVI   PKSRC,C'N'          ACTUAL SOURCE IS ALWAYS NIELSEN              
         MVC   PKBOOK,DBACTBK                                                   
         OC    DBACTBK,DBACTBK                                                  
         BNZ   *+10                                                             
         MVC   PKBOOK,DBSELBK                                                   
         MVC   PKSTAT,DBACTSTA                                                  
         OC    DBACTSTA,DBACTSTA                                                
         BNZ   *+10                                                             
         MVC   PKSTAT,DBSELSTA                                                  
         CLI   DBCABNUM,0          ONLY NEED TO CHECK FIRST BYTE                
         BE    *+10                                                             
         MVC   PKSTAT(4),DBCABNUM                                               
         MVC   PKBTYP,DBBTYPE      SET BOOK TYPE                                
         MVC   PKPNUM+1(2),DBSELPRG                                             
         MVI   IOFLAG,DIR+HIGH                                                  
         GOTO1 AIO,IOFLAG                                                       
         BNE   GETX                                                             
         CLC   PKPNUM+1(2),DBSELPRG                                             
         BNE   GETNTI5Q                                                         
         MVC   DBACTSTA,PKSTA                                                   
         OC    DBACTSTA(4),=C'    '                                             
         DROP  R2                                                               
         SPACE 1                                                                
GETNTI5Q LA    R2,DBKEY            BUILD 'Q' RECORD KEY                         
         USING PMKEY,R2                                                         
         XC    PMKMAJOR,PMKMAJOR                                                
         MVI   PMCODE,PMCODEQU                                                  
         MVC   PMMEDIA,INTMED                                                   
         CLI   PMMEDIA,C'H'                                                     
         BNE   *+8                                                              
         MVI   PMMEDIA,C'N'                                                     
         CLI   DBMEDOVR,0          OVERRIDE MEDIA                               
         BE    *+10                                                             
         MVC   PMMEDIA,DBMEDOVR                                                 
         CLI   DBSELMED,C'W'                                                    
         BNE   *+8                                                              
         MVI   PMMEDIA,C'W'                                                     
         MVC   PMSRC,DBACTSRC                                                   
         CLI   SPNETFLG,0          SPOT->NET LK UP                              
         BE    *+8                                                              
         MVI   PMSRC,C'N'          ACTUAL SOURCE IS ALWAYS NIELSEN              
         MVC   PMBOOK,DBACTBK                                                   
         OC    DBACTBK,DBACTBK                                                  
         BNZ   *+10                                                             
         MVC   PMBOOK,DBSELBK                                                   
         MVC   PMSTAT,DBACTSTA                                                  
         OC    DBACTSTA,DBACTSTA                                                
         BNZ   *+10                                                             
         MVC   PMSTAT,DBSELSTA                                                  
         CLI   DBCABNUM,0          ONLY NEED TO CHECK FIRST BYTE                
         BE    *+10                                                             
         MVC   PMSTAT(4),DBCABNUM                                               
         MVC   PMBTYP,DBBTYPE      SET BOOK TYPE                                
         TM    SPNETFLG,SPNHTQ+SPNADQ                                           
         BZ    *+8                 BYPASS FOR NAD OR NHT FILES                  
         MVI   PMBTYP,0            SET BOOK TYPE                                
         MVC   DBMINKEY,DBSELPRG                                                
         MVI   IOFLAG,FILE+READ                                                 
         OC    DBSELPRG,DBSELPRG   TEST FOR PROGRAM NUMBER                      
         BNZ   *+8                                                              
         MVI   IOFLAG,FILE+HIGH    SWITCH TO READ HIGH                          
         GOTO1 AIO,IOFLAG                                                       
         BE    GETNT15R                                                         
*                                                                               
         LA    R1,KEYSAVE                                                       
         CLI   PMSTAT-PMKEY+4(R1),C'T'       NTI                                
         BNE   GETNTI5S                                                         
         CLC   DBKEY(PMSTAT-PMKEY+4),KEYSAVE   LOOK UP DAILY'S?                 
         BE    GETNTI5S            NO, THERE'S ALREADY DATA FOR THE BK          
         BAS   RE,TSTDYL           TEST ACCESS TO DAILIES ALLOWED               
         BNE   GETNTI5S                                                         
         MVC   DBACTSTA,DBSELSTA                                                
         MVI   DBACTSTA+4,C'D'     LOOK UP DAILIES                              
         MVC   DBKEY,KEYSAVE                                                    
         MVC   PMSTAT,DBACTSTA                                                  
         B     GETNTI6C            READ DIR FOR DAILY'S KEY                     
*                                                                               
GETNTI5S CLI   PMSTAT+4,C'T'       NTI- REGULAR, NOT DAILIES                    
         BE    *+8                                                              
         CLI   PMSTAT+4,C'S'       NTI-SYN                                      
         BE    *+8                                                              
         CLI   PMSTAT+4,C'D'       NTI-DAILIES                                  
         BE    *+8                                                              
         CLI   PMSTAT+4,C'H'       NHTI                                         
         BE    *+8                                                              
         CLI   PMSTAT+4,C'N'       NAD                                          
         BE    *+12                                                             
         CLI   PMSTAT+4,C'M'       NAD-SYN                                      
         BNE   GETX                                                             
         MVC   DBKEY,KEYSAVE       RESTORE SEARCH KEY                           
         B     GETNTI6B                                                         
*                                                                               
GETNT15R MVC   DBNDXDA2,DBNDXDA    SAVE D/A                                     
         ICM   R7,15,DBAOPT        TEST FOR OPTIMIZATION                        
         BZ    GETNTI6             NO                                           
         SR    RE,RE                                                            
         ICM   RE,3,2(R7)          DISPLACMENT TO NEXT OPT ENTRY                
         BZ    GETNTI6             NONE-SO NO ENTRY TO BE ADDED                 
         LR    RF,R7               SAVE START OF AREA                           
         LA    RE,0(R7,RE)         RE NOW POINTS TO NEXT ENTRY                  
         LR    R1,RE               PUT A(NEXT OPT ENTRY) IN R1                  
         LA    RE,NOPTLEN(RE)      ADD LEN OF 1 ENTRY                           
         A     R7,DBLOPT           END OF OPT AREA                              
         CR    RE,R7               TEST FOR ENOUGH ROOM LEFT                    
         BH    GETNTI6             NO                                           
*                                                                               
         USING NOPTD,R1                                                         
         MVC   NOPTBK,PMBOOK                                                    
         MVC   NOPTSTA,PMSTAT                                                   
         MVC   NOPTDA,DBNDXDA                                                   
         SR    RE,RF               LEN USED SO FAR                              
         STCM  RE,3,0(RF)                                                       
         XC    2(2,RF),2(RF)       CLEAR DISP TO NEXT OPT ENTRY                 
         DROP  R1                                                               
         SPACE 1                                                                
GETNTI6  DS    0H                                                               
         CLI   DBLSTNXT,1          RE-ENTRY ON ALL PRG FOR BK/STA               
         BNE   GETNTI7                                                          
GETNTI6A MVI   IOFLAG,FILE+SEQ     RETURN HERE FOR SIMPLE SEQUNTIAL             
         GOTO1 AIO,IOFLAG                                                       
         BE    GETNTI7                                                          
*                                                                               
GETNTI6B DS    0H                                                               
         CLI   PMSTAT+4,C'D'       NTI-DAILIES?                                 
         BE    *+8                                                              
         CLI   PMSTAT+4,C'S'       EOF. FOR CERTAIN FILES LK UP SPCLS           
         BE    *+8                                                              
         CLI   PMSTAT+4,C'T'       NTI?                                         
         BE    *+8                                                              
         CLI   PMSTAT+4,C'H'       EOF. IF NHTI:                                
         BE    *+8                                                              
         CLI   PMSTAT+4,C'N'       EOF. IF NAD:                                 
         BE    *+12                                                             
         CLI   PMSTAT+4,C'M'       NAD-SYN?                                     
         BNE   GETX                FOR OTHER FILES, EOF                         
         XC    PMBTYP+1(PMDATA-PMBTYP+1),PMBTYP+1  CLEAR KEY FIELDS             
         MVI   DBERROR,EOF                                                      
         CLI   PMSTYP,X'00'        REGULAR PRMS?                                
         BNE   *+12                                                             
         MVI   PMSTYP,C'B'         YES, NOW READ FOR BREAKOUTS                  
         B     GETNTI6C                                                         
         CLI   PMSTYP,C'B'         FINISHED READING FOR BREAKOUTS?              
         BNE   GETX                IF 'S' OR ANYTHING ELSE -> EOF               
         MVI   PMSTYP,C'S'         NOW READ FOR SPECIALS                        
         B     GETNTI6C                                                         
*                                                                               
GETNTI6C DS    0H                                                               
         MVI   IOFLAG,DIR+HIGH     RDHIGH ON NEW KEY                            
         GOTO1 AIO,IOFLAG                                                       
         MVC   DBNDXDA2,DBNDXDA    SAVE D/A                                     
         L     R1,DBAREC                                                        
         CLC   PMKEY(PMBTYP+1-PMKEY),KEYSAVE                                    
         BE    *+14                NO BRKOUTS/SPCLS FOR THIS BOOK               
         MVC   DBKEY,KEYSAVE       RESTORE KEY USED IN CALL                     
         B     GETNTI6B                                                         
         MVI   IOFLAG,FILE+HIGH                                                 
         MVC   DBMINKEY,DBSELPRG                                                
         OC    DBSELPRG,DBSELPRG   READ FOR SPECIFIC RECD                       
         BZ    *+8                                                              
         MVI   IOFLAG,FILE+READ                                                 
         GOTO1 AIO,IOFLAG                                                       
         BNE   GETNTI6B            NO RECDS OR NO SPEC PRGM RECD                
         LTR   R1,R1                                                            
*                                                                               
GETNTI7  DS    0H                                                               
         OC    DBCABNUM,DBCABNUM                                                
         BZ    GETNTI7A                                                         
         L     RF,DBAREC   <-----  BYPASS NON PROGRAM RECORDS                   
         AH    RF,DBDTADSP <-----                                               
         CLI   0(RF),1     <-----                                               
         BNE   GETNTI6A    <-----                                               
         LA    RF,8(RF)    <-----                                               
         CLI   0(RF),X'0F' <-----                                               
         BNE   GETNTI6A    <-----                                               
         CLI   2(RF),0     <-----                                               
         BNE   GETNTI6A    <-----                                               
*                                                                               
GETNTI7A XC    DBAQUART,DBAQUART                                                
         BAS   RE,GETNTID                                                       
*                                                                               
         OC    DBSELPRG,DBSELPRG                                                
         BZ    *+14                                                             
         CLC   DBSELPRG,DBACTPRG                                                
         BNE   GETNTI6B                                                         
*                                                                               
         CLI   DBBTYPE,X'00'       NOT APPLICABLE TO DIARY                      
         BE    GETNTI7C                                                         
         CLC   PMBOOK,=X'5721'     OPTIONAL DATA STARTS 87/33                   
         BL    GETNTI7C                                                         
         CLI   DBSATLIT,C'Y'       INDIVIDUAL DAYS AUTHORIZED                   
         BNE   GETNTI7B            NO - BYPASS OPTION                           
         CLI   DBDAYOPT,C'A'       ALL AVG + DAYS                               
         BE    GETNTI7C            DON'T DO FILTERING                           
         CLI   DBDAYOPT,C'I'       INDIVIDUAL DAYS ONLY                         
         BNE   GETNTI7B            NO - DO POCKETPIECE LINES                    
         CLI   DUB+2,X'10'         M-F                                          
         BL    GETNTI6A            SKIP IT                                      
         CLI   DUB+2,X'80'         M-S OR VAR                                   
         BNL   GETNTI6A                                                         
         B     GETNTI7C            INDIV. DAY - USE IT                          
         SPACE 1                                                                
GETNTI7B CLI   DUB+2,X'00'         M-F                                          
         BE    *+12                                                             
         CLI   DUB+2,X'80'         M-S OR VAR                                   
         BL    *+14                                                             
         MVC   WORK+4(2),DBACTPRG  SAVE PROGRAM NUMBER                          
         B     GETNTI7C                                                         
         CLI   DUB+2,X'70'         TEST INDIV. DAYS (MON THRU SUN)              
         BH    *+14                (BOGUS TEST)                                 
         CLC   WORK+4(2),DBACTPRG                                               
         BE    GETNTI6A            BYPASS INDIV. DAY                            
         XC    WORK+4(2),WORK+4                                                 
*                                                                               
GETNTI7C MVC   DBACTDAY,DUB+2      RETURN DAY CODE FOR DEFINE                   
         ZIC   R1,DUB+3                                                         
         STH   R1,DBFACTOR                                                      
         AH    R1,DBDIVSOR                                                      
         STH   R1,DBDIVSOR                                                      
         MVI   DBMODE,DBMFRST                                                   
         MVI   DBRECTYP,DBRECNTI                                                
         MVI   DBMODE,DBMNEXT      SEQUENTIAL MODE                              
         MVI   DBLSTNXT,1          SET FOR RE-ENTRY(NO HOOK)                    
         MVC   WORKSV+10(2),DBINTFIL                                            
*                                                                               
         MVC   SVDQPTR,DBDQPTR                                                  
         MVI   RSTMB,0                                                          
         CLI   SPNETFLG,0          SPOT-NETWORK CALL?                           
         BE    USRHK                                                            
         MVI   DBDQPTR,1           FOR SPOT INDICATE ONLY ONE ENTRY             
         MVC   SVACTBK,DBACTBK                                                  
         MVC   SVACTSTA,DBACTSTA                                                
         L     RE,DBAREC           PT TO RECD TO PASS BACK                      
         MVC   DBACTBK,PMBOOK-PMKEY(RE)                                         
         MVC   DBACTSTA,PMSTAT-PMKEY(RE)                                        
         TM    SPNETFLG,SPNHTQ     SPOT-NHT CALL?                               
         BNO   USRHK                                                            
         BAS   RE,GETMB            GET MKT BRK FOR CALL                         
         BZ    *+8              <-- BYPASS USER HOOK                            
*                                                                               
USRHK    BAS   RE,GETX             HOOK TO USER FOR ALL PROGRAMS                
         MVC   DBDQPTR,SVDQPTR                                                  
         MVC   DBINTFIL(2),WORKSV+10                                            
         CLI   SPNETFLG,0          SPOT-> NHT OR NAD CALL?                      
         BE    *+16                                                             
         MVC   DBACTBK,SVACTBK     RESTORE ORIGINAL VALUE                       
         MVC   DBACTSTA,SVACTSTA   RESTORE ORIGINAL VALUE                       
         CLI   RSTMB,C'Y'          RESET RCD PTR AFTER MKTBRK LK UP             
         BNE   *+8                                                              
         BAS   RE,RSETMB                                                        
         OC    DBSELPRG,DBSELPRG   TEST FOR PROGRAM NUMBER READ                 
         BZ    GETNTI6                                                          
         OC    DBFACTOR,DBFACTOR   AND PROGRAM NOT FOUND                        
         BZ    GETNTI6B            READ 'B' & 'S' RECDS FOR PRGM                
         MVI   DBERROR,EOF         OTHERWISE END                                
         B     GETX                                                             
         EJECT                                                                  
**********************************************************************          
* DAY/TIME FILTERING USING PASSIVE 'N' RECORDS                                  
**********************************************************************          
*                                                                               
GETNTI10 DS    0H                                                               
         OC    AUSERRTN,AUSERRTN   TEST FOR USER HOOK                           
         BNZ   *+6                                                              
         DC    H'0'                BLOW UP FOR MISSING HOOK                     
         LA    R2,DBKEY                                                         
         USING PNKEY,R2                                                         
         XC    DBLSTDIR,DBLSTDIR   BUILD KEY AND READ FOR ANY                   
         MVI   PNCODE,PNCODEQU     RECORDS FOR BOOK/STATION                     
         MVC   PNMEDIA,INTMED                                                   
         CLI   PNMEDIA,C'H'                                                     
         BNE   *+8                                                              
         MVI   PNMEDIA,C'N'                                                     
         CLI   DBMEDOVR,0          OVERRIDE MEDIA                               
         BE    *+10                                                             
         MVC   PNMEDIA,DBMEDOVR                                                 
         CLI   DBSELMED,C'W'                                                    
         BNE   *+8                                                              
         MVI   PNMEDIA,C'W'                                                     
         MVC   PNSRC,DBACTSRC                                                   
         CLI   SPNETFLG,0          SPOT->NET LK UP                              
         BE    *+8                                                              
         MVI   PNSRC,C'N'          ACTUAL SOURCE IS ALWAYS NIELSEN              
         MVI   PNIND,PNINDEQU                                                   
         MVC   PNBOOK,DBACTBK                                                   
         OC    DBACTBK,DBACTBK                                                  
         BNZ   *+10                                                             
         MVC   PNBOOK,DBSELBK                                                   
         MVC   PNSTAT,DBACTSTA                                                  
         OC    DBACTSTA,DBACTSTA                                                
         BNZ   *+10                                                             
         MVC   PNSTAT,DBSELSTA                                                  
         CLI   DBCABNUM,0          ONLY NEED TO CHECK FIRST BYTE                
         BE    *+10                                                             
         MVC   PNSTAT(4),DBCABNUM                                               
         MVI   PNBTYP,0           INIT TO ZERO FOR NHT-SPOT CALL                
         TM    SPNETFLG,SPNHTQ+SPNADQ                                           
         BNZ   *+10                BYPASS FOR NAD OR NHT FILES                  
         MVC   PNBTYP,DBBTYPE      SET BOOK TYPE                                
         MVI   IOFLAG,DIR+HIGH                                                  
         BAS   RE,RDBUF                                                         
         BNE   GETX                                                             
*                                                                               
         CLC   PNKEY(PNDW-PNKEY),KEYSAVE   CHECK FOR HIT ON BOOK/STAT           
         BE    GETNT10A                                                         
*                                                                               
GETDAYL  DS    0H                                                               
         CLI   DBSTYPE,X'00'                                                    
         BNE   GETNTI18                                                         
         LA    R1,KEYSAVE                                                       
         CLI   PNSTAT+4-PNKEY(R1),C'T'       SEARCHING FOR NTI DATA?            
         BNE   GETNTI18            EOF. GET BRKS OR SPECIALS NEXT               
         BAS   RE,TSTDYL           TEST SECURITY                                
         BNE   GETNTI18            ACCESS DENIED                                
         MVC   DBACTSTA,DBSELSTA   TRY DAILY DATA                               
         MVI   DBACTSTA+4,C'D'                                                  
         B     GETNTI10                                                         
*                                                                               
GETNT10A MVC   DIRSAVE,PNKEY                                                    
         GOTO1 AGETDQ,0                                                         
         MVI   DBACTDAY,0          RE-CLEAR ACTUAL DAY                          
         LA    R5,DBDQTAB          R5 POINTS TO DAY/QH TABLE                    
         USING DBDQD,R5                                                         
         MVI   DBDQKDAY,X'10'      START AT MONDAY                              
         MVI   DBDQDAYW,7          COUNTER OF DAYS TO PROCESS                   
         MVC   DBACTSQH,DBDQSQH    REQUESTED SQH                                
         MVC   DBACTEQH,DBDQEQH    REQUESTED EQH                                
         CLI   DBBEST,C'L'         TEST FOR LOOKUP OPTION                       
         BNE   GETNTI11                                                         
         MVI   DBDQKDAY,0          FOR LOOKUP START AT M-F                      
         MVI   DBDQDAYW,9          9 DAYS TO PROCESS                            
         B     GETNTI12                                                         
GETNTI11 CLI   DBSELDAY,X'90'      ARE WE DEALING WITH VAR                      
         BNE   GETNTI12                                                         
         MVI   DBDQKDAY,X'90'      FOR LOOKUP START AT VAR                      
         MVI   DBDQDAYW,1          1 DAY TO LOOKUP                              
         SPACE                                                                  
GETNTI12 DS    0H                  FIND BIT SETTING FOR DAY CODE                
         L     RE,ADQPNNS          POINT AT FIRST ENTRY                         
         LA    RE,DQHDRLN(RE)                                                   
         USING DQDTAD,RE                                                        
         CLC   DBDQKDAY,DQKDAY     MATCH ON DAY CODE                            
         BE    *+12                                                             
         LA    RE,DQDTALN(RE)                                                   
         B     *-14                                                             
         MVC   WORK(1),DQDAY       EXTRACT BIT SETTING                          
         CLI   DBBEST,C'L'         TEST FOR LOOKUP OPTION                       
         BNE   *+14                                                             
         CLC   DQDAY,DBSELDAY      TEST FOR EXACT MATCH OF DAY                  
         BNE   GETNTI14            BUMP TO NEXT DAY                             
         SPACE                                                                  
         ZIC   R1,WORK             TEST FOR ANY MATCH AGAINST                   
         EX    R1,TSTOVER          DBSELDAY                                     
         BNZ   GETNTI15            READ FOR THE DAY CODE                        
         DROP  RE                                                               
         SPACE                                                                  
GETNTI14 DS    0H                  BUMP TO NEXT DAY                             
         ZIC   R1,DBDQKDAY                                                      
         LA    R1,X'10'(R1)                                                     
         STC   R1,DBDQKDAY                                                      
         ZIC   R1,DBDQDAYW         DECREMENT COUNTER                            
         SH    R1,=H'1'                                                         
         BZ    GETNTI18            EOF EXIT                                     
         STC   R1,DBDQDAYW         UPDATE COUNTER                               
         B     GETNTI12            PROCESS NEXT DAY                             
         SPACE                                                                  
TSTOVER  TM    DBSELDAY,0          EXECUTED                                     
         SPACE                                                                  
* FOR A GIVEN DAY, SEE IF ANY PROGRAMS FALL IN TIME RANGE                       
*                                                                               
GETNTI15 DS    0H                                                               
         MVC   PNDW,DBDQKDAY       SET DAY AND                                  
         MVC   PNSTIM,DBDQSQH      FIRST QUARTER HOUR                           
*  930324...930330                                                              
         CLI   PNSTAT+4,C'H'       HISPANIC SEARCH ALL                          
         BNE   GETNTI16                                                         
         ZIC   RE,DBDQSQH          BACK UP 4 HOURS MAX                          
         SH    RE,=H'16'           SO WE GET ALL PROGRAMS THAT                  
         BP    *+6                 CROSS THIS TIME PERIOD                       
         SR    RE,RE                                                            
         STC   RE,PNSTIM                                                        
*                                                                               
         SPACE                                                                  
GETNTI16 DS    0H                                                               
         XC    PNPNUM(PNKSTAT-PNPNUM),PNPNUM CLEAR REST OF KEY                  
GETNT16A MVI   IOFLAG,DIR+HIGH                                                  
         BAS   RE,RDBUF                                                         
*        GOTO1 AIO,IOFLAG                                                       
         BL    GETX                                                             
         BE    GETNTI17                                                         
         B     GETNTI18                                                         
         SPACE                                                                  
* TEST WHETHER RECORD CONFORMS TO DAY/TIME SPECS                                
*                                                                               
GETNTI17 DS    0H                                                               
         CLC   PNKEY(PNDW-PNKEY),KEYSAVE     SAME BOOK/STATION                  
         BNE   GETNTI18                      EOF EXIT                           
         CLI   PNBSIND,C'B'                                                     
         BNE   *+8                                                              
         OI    BSSW,BREAKOUT       BREAKOUT EXIST                               
         CLI   PNBSIND,C'S'                                                     
         BNE   *+8                                                              
         OI    BSSW,SPECIAL        SPECIAL EXISTS                               
*                                                                               
         CLC   PNBSIND,DBSTYPE     SAME AS REQUESTED?                           
         BNE   GETNTI19            READ NEXT N-RECD                             
         CLI   PNSTAT+4,C'H'       NHI IS MULTI-PROGRAM/WEEK                    
         BE    *+8                                                              
         CLI   PNSTAT+4,C'S'       SYND IS MULTI-PROGRAM                        
         BE    *+8                                                              
         CLI   PNSTAT+4,C'M'       NAD SYND IS MULTI-PROGRAM/WEEK               
         BE    *+8                                                              
         CLI   PNSTAT+4,C'T'       REGIONAL PROGRAMS OVERLLAP                   
         BE    *+8                  (ADDED 10/19/95)                            
         CLI   PNSTAT+4,C'N'       NAD IS MULTI-WEEK                            
         BE    *+20                                                             
         TM    PNDW,X'0F'          TEST LO-ORDER NIBBLE OF DAY/WEEK             
         BZ    *+12                TO ELIMINATE DUP POINTERS                    
         CLI   PNACTDUR,14         TEST FOR 15 MIN. OR LONGER                   
         BH    GETNTI19            READ NEXT RECORD                             
         MVC   WORK(1),PNDW        EXTRACT DAY FROM RECORD                      
         NI    WORK,X'F0'                                                       
         CLC   DBDQKDAY,WORK       TEST FOR SAME DAY                            
         BNE   GETNTI14            NO-BUMP TO NEXT ONE                          
*                                                                               
         CLI   PNBSIND,0           BREAKOUT AND SPECIALS ARE OKAY               
         BNE   *+8                                                              
         CLI   PNSTAT+4,C'H'       FOR NHT FILE ONLY                            
         BNE   *+12                                                             
         TM    PNDW,X'0F'          MAKE SURE WE HAVE AN ACTIVE WEEK             
         BZ    GETNTI19              ELSE THIS IS A WKLY RECD!!                 
*                                                                               
         CLI   PNSTAT+4,C'H'                                                    
         BE    *+8                                                              
         CLI   PNSTAT+4,C'M'                                                    
         BE    *+8                                                              
         CLI   PNSTAT+4,C'N'                                                    
         BNE   GETNT17A                                                         
         CLC   PNDW,KEYSAVE+(PNDW-PNKEY) MULTI-WEEK                             
         BE    GETNT17A            MUST RESET ON CHANGE OF WEEK                 
         MVC   PNSTIM,DBDQSQH      SET FIRST QH                                 
* 930324...930330                                                               
         CLI   PNSTAT+4,C'H'                                                    
         BNE   GETNTI16                                                         
         ZIC   RE,DBDQSQH          ALWAYS START 4 HOURS EARLY                   
         SH    RE,=H'16'                                                        
         BP    *+6                                                              
         SR    RE,RE                                                            
         STC   RE,PNSTIM                                                        
*                                                                               
         B     GETNTI16            AND READ HIGH                                
*                                                                               
GETNT17A CLI   DBBEST,C'L'         TEST FOR LOOKUP OPTION                       
         BNE   *+14                                                             
         CLC   PNDW,PNACTDAY       TEST IF E.G. M-F PROGRAM SHOULD              
         BNE   GETNTI19            BE TAKEN FOR MON.                            
*                                                                               
         LA    R1,X'D0'            DEFAULT IS PROGRAM OF OVER 15 MIN.           
         CLI   DBSELDUR,X'FE'      TEST SELECT PROGRAMS LE 15 MIN.              
         BNE   *+8                                                              
         LA    R1,X'20'            YES-SET REJECTION BRANCH MASK TO BH          
         CLI   DBSELDUR,X'FF'      TEST SELECT ALL PROGRAMS                     
         BNE   *+8                                                              
         LA    R1,X'00'            YES-NOOP REJECTION BRANCH                    
*                                                                               
         CLC   PNSTAT(5),=C'HUTDN' DON'T CHECK NAD DAYPART DURATIONS            
         BE    *+12                                                             
         CLI   PNACTDUR,15         TEST FOR 15 MINUTE PROGRAM                   
         EX    R1,NTIBRAN1         BRANCH TO REJECT USING MASK                  
         MVC   DIRSAVE,PNKEY       SAVE 'N' KEY                                 
* 930324                                                                        
         CLI   PNSTAT+4,C'H'                                                    
         BE    GETNT17AH                                                        
         CLC   PNSTIM,DBDQSQH      TEST REC SQH VS REQSTD SQH                   
         BL    GETNTI19            READ NEXT (CASE OF DW OF 31 AFTER 30         
*                                                                               
* 950901 STOP DAY READ 1/2 HOUR AFTER END QH                                    
*        ZIC   RE,PNSTIM       THIS DIDN'T WORK BECAUSE OF WEEK                 
*        ZIC   RF,DBDQEQH       BITS SET IN DAY. I'LL REVISIT WHEN              
*        SR    RE,RF            I HAVE MORE TIME                                
*        CH    RE,=H'2'                                                         
*        BH    GETNTI14                                                         
*                                                                               
GETNT17AH CLC   PNSTIM,DBDQEQH     TEST SQH AGAINST REQSTD EQH                  
         BH    GETNTI19            READ NEXT RECORD                             
*                                                                               
***********************************************************************         
* RECORD CHECKS OUT NOW.  HOWEVER, TO PREVENT LOOPS ON OLD            *         
* FILE, AN EXTRA CHECK IS MADE BELOW TO STOP READING THE SAME         *         
* PROGRAM RECORD IN SUCCESSION.                                       *         
***********************************************************************         
*                                                                               
         CLC   DBACTDAY,WORK       TEST IF DAY IS SAME AS LST RECD READ         
         BNE   GETNT17B                                                         
         CLC   DBACTPRG,PNPNUM     TEST IF PROG NUM IS SAME AS LST RECD         
         BNE   GETNT17B                                                         
         CLC   WORK+18(1),PNBSIND  SAME BRK-OUT/SPC IND VALUE?                  
         BE    GETNTI32                                                         
*                                                                               
GETNT17B MVC   DBACTDAY,WORK       RETURN DAY FOR DEFINE                        
         B     GETNTI20            RECORD CHECKS OUT                            
         SPACE                                                                  
GETNTI18 DS    0H                  EOF EXIT                                     
*        MVI   DBERROR,EOF                                                      
*        B     GETX                                                             
*                                                                               
GETBRKSP XC    DBNDXDA,DBNDXDA    CLEAR SAVED INDEX                             
         XC    DBNDXDA2,DBNDXDA2  CLEAR SAVED INDEX                             
         XC    DBACTBK,DBACTBK                                                  
         CLI   DBSELSTA+4,C'D'     NTI DAILIES                                  
         BE    *+8                                                              
         CLI   DBSELSTA+4,C'T'       CHECK FOR NETWORK                          
         BE    *+8                  FIX DUP POINTERS                            
         CLI   DBSELSTA+4,C'S'       CHECK FOR SYND OR                          
         BE    *+14                 FIX DUP POINTERS                            
         OC    DBCABNUM,DBCABNUM   NEW CABLE DATA                               
         BZ    *+18                                                             
         ICM   RE,15,DBSPANAD      YES                                          
         LA    RE,200(RE)          ONLY PROCESS PROGRAM 1 TIME                  
         XC    0(10,RE),0(RE)      CLEAR BUFFER                                 
*                                                                               
         CLI   DBSTYPE,X'00'      ALREADY READ REGULAR PRGMS?                   
         BNE   GETBRSP1                                                         
         MVI   DBSTYPE,C'B'        NOW READ FOR BREAKOUTS                       
         TM    BSSW,BREAKOUT       ANY BREAKOUTS PRESENT                        
         BZ    GETBRSP1                                                         
         B     GETNTI10            GO BACK TO READ N-RECS FROM TOP              
GETBRSP1 CLI   DBSTYPE,C'B'        ALREADY READ BRKOUTS?                        
         BNE   GETEOF              ELSE EXIT WITH END OF FILE                   
         MVI   DBSTYPE,C'S'        NOW READ SPECIALS                            
         TM    BSSW,SPECIAL        ANY SPECIALS PRESENT                         
         BZ    GETEOF                                                           
         B     GETNTI10            GO BACK TO READ N-RECS FROM TOP              
*                                                                               
GETEOF   MVI   DBERROR,EOF                                                      
         MVI   DBSTYPE,X'00'       RESET                                        
         B     GETX                                                             
*                                                                               
GETNTI19 DS    0H                  READ SEQUENTIAL                              
         MVI   IOFLAG,DIR+SEQ                                                   
         BAS   RE,RDBUF                                                         
*        GOTO1 AIO,IOFLAG                                                       
         BL    GETX                                                             
         BE    GETNTI17                                                         
         B     GETNTI18                                                         
         SPACE                                                                  
* READ Q-RECD (PRG RECD) POINTED AT BY N-RECD (PASSIVE DAY/TIME PTR)            
*                                                                               
GETNTI20 DS    0H                                                               
         LA    R2,DIRSAVE          MAKE SAVED 'N' KEY ADDRESSABLE               
         LA    R3,DBKEY                                                         
         USING PMKEY,R3                                                         
         XC    DBLSTDIR,DBLSTDIR                                                
         MVI   PMCODE,PMCODEQU                                                  
         MVC   PMMEDIA,INTMED                                                   
         CLI   PMMEDIA,C'H'                                                     
         BNE   *+8                                                              
         MVI   PMMEDIA,C'N'                                                     
         CLI   DBMEDOVR,0          OVERRIDE MEDIA                               
         BE    *+10                                                             
         MVC   PMMEDIA,DBMEDOVR                                                 
         CLI   DBSELMED,C'W'                                                    
         BNE   *+8                                                              
         MVI   PMMEDIA,C'W'                                                     
         MVC   PMSRC,DBACTSRC                                                   
         CLI   SPNETFLG,0          SPOT->NET LK UP                              
         BE    *+8                                                              
         MVI   PMSRC,C'N'          ACTUAL SOURCE IS ALWAYS NIELSEN              
         MVC   PMBOOK,PNBOOK                                                    
         MVC   PMSTAT,PNSTAT                                                    
         MVC   PMBTYP,PNBTYP       SET BOOK TYPE FROM POINTER                   
         MVC   DBMINKEY,PNPNUM     PROGRAM NUMBER                               
         MVC   DBNDXDA,DBNDXDA2    SAVED D/A IF ANY                             
         MVC   PMSTYP,PNBSIND      N-RECD BRKOUT/SPCL BIT                       
*                                                                               
         CLI   PMSTAT+4,C'T'       CHECK FOR NETWORK                            
         BE    *+8                  FIX DUP POINTERS                            
         CLI   PMSTAT+4,C'S'       CHECK FOR SYND OR                            
         BE    *+14                 FIX DUP POINTERS                            
         OC    DBCABNUM,DBCABNUM   NEW CABLE DATA                               
         BZ    GETNT20A                                                         
         CLI   DBSTYPE,0                                                        
         BNE   GETNT20A            DON'T OPTIMZ FOR BRKS & SPCLS                
         ICM   RE,15,DBSPANAD      YES                                          
         LA    RE,200(RE)          ONLY PROCESS PROGRAM 1 TIME                  
*                                                                               
OPTPNUM  OC    0(2,RE),0(RE)                                                    
         BZ    OPTPNUM2                                                         
         CLC   DBDQKDAY(1),2(RE)                                                
         BNE   *+10                                                             
         CLC   0(2,RE),PNPNUM                                                   
         BE    GETNT20B                                                         
         LA    RE,5(RE)                                                         
         B     OPTPNUM                                                          
OPTPNUM2 MVC   0(2,RE),PNPNUM                                                   
         MVC   2(1,RE),DBDQKDAY                                                 
         XC    3(5,RE),3(RE)       CLEAR NEXT SET OF BYTES                      
*                                                                               
GETNT20A MVI   IOFLAG,FILE+READ                                                 
         GOTO1 AIO,IOFLAG                                                       
         BL    GETX                                                             
         BE    GETNTI21            FOUND PROGRAM RECORD                         
*                                                                               
GETNT20B MVC   DBKEY,DIRSAVE       RESTORE 'N' KEY AND MAKE IT                  
         LA    R2,DBKEY            ADDRESSABLE.                                 
         SR    R1,R1               ATTEMPT TO RECOVER BY READING 'N'            
         CLC   PNPNUM,=X'FFFF'     PROTECT FROM LIMIT                           
         BNE   GETNT20C                                                         
         ZIC   R1,PNSTIM           JUST BUMP THE DAY                            
         LA    R1,1(R1)                                                         
         STC   R1,PNSTIM                                                        
         XC    PNPNUM,PNPNUM                                                    
GETNT20C ICM   R1,3,PNPNUM         REC AFTER PTR W MISSING PROGRAM.             
         LA    R1,1(R1)            BUMP THE PROGRAM NO BY 1 AND                 
         STCM  R1,3,PNPNUM         CLEAR REST OF KEY                            
         XC    PNACTDAY(PNKSTAT-PNACTDAY),PNACTDAY                              
         MVI   IOFLAG,DIR+HIGH                                                  
         BAS   RE,RDBUF                                                         
*        GOTO1 AIO,IOFLAG                                                       
         BNE   GETX                                                             
         B     GETNTI17            TEST NEXT KEY FOR QUALIFYING RECORD          
         SPACE 1                                                                
*                                                                               
GETNTI21 DS    0H                                                               
         GOTO1 ATSTMRKT                                                         
         BNE   GETX                                                             
         MVC   DBNDXDA2,DBNDXDA    SAVE D/A                                     
         ICM   R7,15,DBAOPT        TEST FOR OPTIMIZATION                        
         BZ    GETNTI22            NO                                           
         SR    RE,RE                                                            
         ICM   RE,3,2(R7)          DISPLACMENT TO NEXT OPT ENTRY                
         BZ    GETNTI22            NONE-SO NO ENTRY TO BE ADDED                 
         LR    RF,R7               SAVE OPT AREA START                          
         LA    RE,0(R7,RE)         RE NOW POINTS TO NEXT ENTRY                  
         LR    R1,RE               PUT A(NEXT OPT ENTRY) IN R1                  
         LA    RE,NOPTLEN(RE)      ADD LEN OF 1 ENTRY                           
         A     R7,DBLOPT           END OF OPT AREA                              
         CR    RE,R7               TEST FOR ENOUGH ROOM LEFT                    
         BH    GETNT21A            NO                                           
         USING NOPTD,R1                                                         
         MVC   NOPTBK,PMBOOK                                                    
         MVC   NOPTSTA,PMSTAT                                                   
         MVC   NOPTDA,DBNDXDA                                                   
         SR    RE,RF               LEN USED SO FAR                              
         STCM  RE,3,0(RF)                                                       
         XC    2(2,RF),2(RF)       CLEAR DISP TO NEXT ENTRY                     
         DROP  R1                                                               
         B     GETNTI22                                                         
GETNT21A L     RE,DBAOPT           JUST REST THE BUFFER                         
         L     RF,DBLOPT                                                        
         XCEF                                                                   
         SPACE                                                                  
GETNTI22 XC    DBAQUART,DBAQUART                                                
*        ******TEMP***********                                                  
         CLC   DBKEY(3),=C'QCN'                                                 
         BNE   GETNTI22A                                                        
         L     RF,DBAREC                                                        
         AH    RF,DBDTADSP                                                      
         LA    RF,8(RF)                                                         
         CLI   0(RF),4                                                          
         BNE   GETNTI22A                                                        
         MVI   IOFLAG,FILE+SEQ                                                  
         GOTO1 AIO,IOFLAG                                                       
*                                                                               
GETNTI22A BAS   RE,GETNTID                                                      
*                      ADDITIONAL AVERAGES START 8733                           
         CLC   DIRSAVE+(PNBOOK-PNKEY)(2),=X'5721'                               
         BL    GETNTI23                                                         
         CLI   DBBTYPE,C' '        N/A FOR DIARY SAMPLE                         
         BL    GETNTI23                                                         
         L     R1,ADQPNNS          CHECK FOR OVERLAP ON DAY                     
         LA    R1,DQHDRLN(R1)                                                   
         USING DQDTAD,R1                                                        
GETNT22A CLI   DQDAY,0             ??? TREAT AS AN OVERLAP                      
         BE    GETNTI23                                                         
         CLC   DIRSAVE+(PNDW-PNKEY)(1),DQKDAY KEY DAY = REC DAY                 
         BE    GETNT22B                                                         
         LA    R1,DQDTALN(R1)      NO - TRY NEXT                                
         B     GETNT22A                                                         
         USING NTELEM,RF                                                        
GETNT22B ZIC   RE,DQDAY            DAY IN THIS RECORD                           
         EX    RE,*+8                                                           
         B     *+8                                                              
         TM    NTDAY,0                                                          
         BZ    GETNT22C            NO - READ NEXT PROGRAM AVERAGE               
         DROP  R1,RF                                                            
*                                                                               
         CLI   DBSATLIT,C'Y'       AUTHORIZED FOR INDIV. DAY                    
         BNE   GETNTI23            NO - ACCEPT FIRST RECORD                     
* MUST GET CORRECT ONE IF OPTIONS ARE SET                                       
         CLI   DBDAYOPT,C'A'       ALL DAYS                                     
         BE    *+8                                                              
         CLI   DBDAYOPT,C'I'       INDIV DAYS                                   
         BNE   GETNTI23                                                         
         CLC   DBACTDAY,DUB+2      SELECT THIS DAY                              
         BE    GETNTI23            YES - PROCESS IT                             
GETNT22C MVI   IOFLAG,FILE+SEQ     OTHERWISE READ NEXT ONE                      
         GOTO1 AIO,IOFLAG                                                       
*        BNH   *+6                                                              
*        DC    H'0'                ERROR ****DAY NOT FOUND*****                 
         BNH   *+8                                                              
         B     GETNTI18            ERROR ****DAY NOT FOUND*****                 
         B     GETNTI21                                                         
*                                                                               
GETNTI23 MVC   DBACTSQC,DUB        CURRENT RECORD SQH                           
         MVC   DBACTEQC,DUB+6      CURRENT RECORD EQH                           
* 930324                                                                        
* HISPANIC HAS ONLY ONE POINTER FOR A DAY/PROGRAM                               
         CLI   PNSTAT+4,C'H'                                                    
         BNE   GETNT23A                                                         
         CLC   DUB+6(1),DBDQSQH                                                 
         BL    GETNTI25                                                         
         CLC   DUB(1),DBDQEQH                                                   
         BH    GETNTI25                                                         
         B     GETNT23B                                                         
*                                                                               
* POCKETPIECE HAS HALF HOUR POINTERS FOR DAY/PROGRAM                            
GETNT23A CLI   DBPRGDUR,C'Y'       INCLUDE FULL DURATION      950901            
         BE    GETNT23C             FOR ANY OVELAPPING PROGRAMS                 
*                                                                               
         CLC   DUB(1),DBDQSQH      TEST IF PROG STARTED BEFORE PERIOD           
         BL    GETNTI25            NOTE-HR PROG HAS 2 'N' RECS                  
*                                                                               
GETNT23B CLC   DUB+6(1),DBDQEQH    CALCULATE QUARTER HOUR OVERLAP               
         BL    *+10                WITH REQUESTED PERIOD                        
         MVC   DUB+6(1),DBDQEQH                                                 
*                                                                               
GETNT23C CLI   PNSTAT+4,C'H'       HISPANIC INCLUDES ONLY THE                   
         BNE   GETNT23D            ACTIVE PORTION                               
         CLC   DUB(1),DBDQSQH                                                   
         BNL   *+10                                                             
         MVC   DUB(1),DBDQSQH                                                   
*                                                                               
GETNT23D ZIC   R1,DUB+6                                                         
         ZIC   R0,DUB                                                           
         SR    R1,R0               EQH LESS SQH                                 
         MVI   DBRECTYP,DBRECNTI                                                
         MVI   DBMODE,DBMNEXT      SEQUENTIAL                                   
         LTR   R1,R1              PROTECT AGAINST ILLOGICAL CONDITIONS          
         BM    GETNTI25                                                         
         LA    R1,1(R1)            PLUS 1 FOR DURATION                          
         STH   R1,DBFACTOR                                                      
         AH    R1,DBDIVSOR                                                      
         STH   R1,DBDIVSOR                                                      
         OC    DBCABNUM,DBCABNUM                                                
         BNZ   GETCAB                                                           
*                                                                               
GTN24H   DS    0H                                                               
         MVI   RSTMB,0                                                          
         MVC   SVDQPTR,DBDQPTR                                                  
         CLI   SPNETFLG,0          SPOT-NETWORK CALL?                           
         BE    USERHOOK                                                         
         MVI   DBDQPTR,1           FOR SPOT INDICATE ONLY ONE ENTRY             
         MVC   SVACTBK,DBACTBK                                                  
         MVC   SVACTSTA,DBACTSTA                                                
         L     RE,DBAREC           PT TO RECD TO PASS TO USER                   
         MVC   DBACTBK,PMBOOK-PMKEY(RE)                                         
         MVC   DBACTSTA,PMSTAT-PMKEY(RE)                                        
         TM    SPNETFLG,SPNHTQ+SPNADQ    SPOT-> NHT OR NAD CALL?                
         BZ    *+12                                                             
         BAS   RE,GETMB            GET MKT BRK FOR CALL                         
         BZ    NOUSR            <-- BYPASS USER HOOK                            
*                                                                               
USERHOOK DS    0H                                                               
         MVC   WORKSV+10(2),DBINTFIL                                            
         BAS   RE,GETX             USER HOOK                                    
         MVC   DBINTFIL(2),WORKSV+10                                            
*                                                                               
NOUSR    MVC   DBDQPTR,SVDQPTR     RESTORE                                      
         CLI   SPNETFLG,0          SPOT-> NHT OR NAD CALL?                      
         BE    *+16                                                             
         MVC   DBACTBK,SVACTBK     RESTORE ORIGINAL VALUE                       
         MVC   DBACTSTA,SVACTSTA     RESTORE ORIGINAL VALUE                     
         CLI   RSTMB,C'Y'          RESET RCD PTR AFTER MKTBRK LK UP             
         BNE   *+8                                                              
         BAS   RE,RSETMB                                                        
*                                                                               
GETNTI25 DS    0H                                                               
         B     GETNTI32      <---- ALLOW DAY/TIME OVERLAPS                      
         CLI   PNSTAT+4,C'C'       CHECK FOR NHI FILE                           
         BE    GETNTI32                                                         
         CLI   PNSTAT+4,C'H'       CHECK FOR NHI FILE                           
         BE    GETNTI32                                                         
         CLI   PNSTAT+4,C'S'       CHECK FOR SYNDICATOR FILE                    
         BE    GETNTI32                                                         
         CLI   PNSTAT+4,C'M'       CHECK FOR NAD SYNDICATOR FILE                
         BE    GETNTI32                                                         
         CLI   PNSTAT+4,C'N'       CHECK FOR NAD FILE                           
         BE    GETNTI32                                                         
         MVI   IOFLAG,FILE+SEQ                                                  
         GOTO1 AIO,IOFLAG                                                       
         MVC   DBKEY,DIRSAVE       RESTORE 'N' KEY                              
         LA    R2,DBKEY            BUMP EQH OF LAST RECORD UNLESS IT            
         ZIC   R1,DBACTEQC         IS LESS THAN THE SQH OF LAST 'N'             
         CLM   R1,1,PNSTIM         KEY.  IF TRUE, BUMP THE 'N' SQH SO           
         BNH   GETNTI29            ALWAYS READING FORWARD (PROGRAMS             
*                                  WITH VARYING START TIMES).                   
         CLI   DBSELDAY,X'90'      ARE WE DEALING WITH VAR                      
         BE    GETNTI29                                                         
         B     GETNTI30                                                         
         SPACE 1                                                                
GETNTI27 DS    0H                  LOOP DEFENCE CODE                            
         B     GETNTI32   <------  ALLOW DAY/TIME OVERLAPS                      
         CLI   PNSTAT+4,C'C'       CHECK FOR NHI FILE                           
         BE    GETNTI32                                                         
         CLI   PNSTAT+4,C'H'       CHECK FOR NHI FILE                           
         BE    GETNTI32                                                         
         CLI   PNSTAT+4,C'S'       CHECK FOR SYNDICATOR FILE                    
         BE    GETNTI32                                                         
         CLI   PNSTAT+4,C'M'       CHECK FOR NAD SYNDICATOR FILE                
         BE    GETNTI32                                                         
         CLI   PNSTAT+4,C'N'       CHECK FOR NAD FILE                           
         BE    GETNTI32                                                         
         MVC   DBKEY,DIRSAVE       RESTORE 'N' KEY                              
         LA    R2,DBKEY                                                         
         SPACE 1                                                                
GETNTI29 ZIC   R1,PNSTIM           BUMP CURRENT START TIME                      
         SPACE 1                                                                
GETNTI30 DS    0H                                                               
         LA    R1,1(R1)                                                         
         STC   R1,PNSTIM                                                        
         B     GETNTI16            GO DO READ HIGH                              
         SPACE 1                                                                
GETNTI32 MVC   DBKEY,DIRSAVE       RESTORE THE KEY                              
         LA    R2,DBKEY                                                         
         SR    R1,R1                                                            
         CLI   PNSTAT+4,C'S'                                                    
         BE    *+8                                                              
         CLI   PNSTAT+4,C'T'                                                    
         BE    *+8                                                              
         CLI   PNSTAT+4,C'H'                                                    
         BE    *+8                                                              
         CLI   PNSTAT+4,C'M'                                                    
         BE    *+8                                                              
         CLI   PNSTAT+4,C'N'                                                    
         BNE   GETNTI33                                                         
         IC    R1,PNBSIND                                                       
         LA    R1,1(R1)                                                         
         STC   R1,PNBSIND                                                       
         B     GETNT16A                                                         
*                                                                               
GETNTI33 ICM   R1,3,PNPNUM                                                      
         LA    R1,1(R1)                                                         
         STCM  R1,3,PNPNUM                                                      
         MVI   PNACTDAY,0                                                       
         B     GETNT16A                                                         
         DROP  R3,R5                                                            
         SPACE 2                                                                
NTIBRAN1 BC    0,GETNTI19          EXECUTED                                     
         EJECT                                                                  
GETCAB   ICM   RF,15,DBSPANAD      SET UP FOR SPANNED RECORDS                   
         XC    0(94,RF),0(RF)                                                   
         MVC   0(3,RF),=C'CAB'                                                  
*                                                                               
         L     RF,DBAREC           SET UP FOR PROGRAM                           
         AH    RF,DBDTADSP                                                      
         CLI   0(RF),1             BYPASS MARKET ELEMENT                        
         BNE   GETNTI25                                                         
         ZIC   R0,1(RF)                                                         
         AR    RF,R0                                                            
         CLI   0(RF),X'0F'                                                      
         BNE   GETNTI25                                                         
         ST    RF,DBAQUART                                                      
GETCNUM  ZIC   R0,1(RF)            DON'T REPROCESS A PROGRAM                    
         AR    RF,R0                                                            
         CLI   0(RF),X'00'                                                      
         BE    GETNTI25                                                         
         CLI   0(RF),X'10'         FIND THE NTI PROG NUMBER                     
         BNE   GETCNUM                                                          
         USING PHTELEM,RF                                                       
         ICM   RE,15,DBSPANAD      FIND THE SPANNING TABLE                      
         LA    RE,200(RE)                                                       
GETCNUM2 OC    0(5,RE),0(RE)       OPEN SLOT - DATA NOT PROCESSED               
         BZ    GETCNUMX                                                         
         CLC   PHTNTI,0(RE)        MATCHED SLOT - BYPASS PROG                   
         BE    GETNTI25                                                         
         LA    RE,5(RE)                                                         
         B     GETCNUM2                                                         
GETCNUMX MVC   0(5,RE),PHTNTI                                                   
         DROP  RF                                                               
*                                                                               
         MVI   PVNORMAL,1          SET TO DO PROGRAM/TRACKAGE                   
         L     RF,DBAQUART                                                      
         BAS   RE,CABSPAN                                                       
*                                                                               
GETCAB1  IC    R0,1(RF)                                                         
         AR    RF,R0                                                            
         CLI   0(RF),X'0F'         FOUND TRACK/TEL BEFORE PROG DEMOS            
         BNE   *+12                                                             
         MVI   PVNORMAL,2          SET TO DO TELECAST                           
         B     *+12                                                             
         CLI   0(RF),X'5E'         FOUND PROG DEMOS IGNORE TELECASTS            
         BNE   GETCAB1                                                          
         L     RF,DBAQUART                                                      
*                                                                               
GETCAB6  ST    RF,DBAQUART                                                      
GETCAB7  BAS   RE,CABSPAN          SAVE SPANNING INFO                           
         L     RF,DBAQUART                                                      
         SR    R0,R0                                                            
*                                                                               
GETCAB7A IC    R0,1(RF)                                                         
         AR    RF,R0                                                            
         CLI   0(RF),X'0F'         IGNORE IF NO DEMS ON THIS LEVEL              
         BE    GETCAB7P                                                         
         CLI   0(RF),X'5E'         FOUND DEMOS ON THIS LEVEL                    
         BNE   GETCAB7A                                                         
         LR    RE,RF               SAVE LAST DEMO ELEMENT                       
         L     RF,DBAQUART         NOW CHECK FOR DUP DEMS                       
         CLI   0(RF),X'0F'         JUST TO BE SAFE                              
         BNE   GETCAB7E                                                         
         CLI   2(RF),0             ONLY ON PROGRAM LEVEL                        
         BNE   GETCAB7E                                                         
GETCAB7B ICM   R0,1,1(RE)          EOR - GET NEXT                               
         BZ    GETCAB7P                                                         
         AR    RE,R0                                                            
         CLI   0(RE),X'0F'                                                      
         BNE   GETCAB7B                                                         
         CLI   2(RE),1             NO TRACK - IT'S OK                           
         BNE   GETCAB7E                                                         
*                                                                               
         CLI   DBSELPTT,C'T'       ONLY WANT TRACKS                             
         BE    GETCAB7P             ZIP PAST PROGRAM                            
*                                                                               
GETCAB7C ICM   R0,1,1(RF)          JUST IN CASE                                 
         BZ    GETCAB7E                                                         
         AR    RF,R0                                                            
         CLI   0(RF),X'57'         UNIVERSE ON FIRST ONLY                       
         BE    GETCAB7P                                                         
         CLI   0(RF),X'5E'         DEMOS MATCH - BYPASS PROGRAM                 
         BE    GETCAB7P                                                         
         CLI   0(RF),X'30'         1ST TIME - POS TO 1ST DEMO ELEM              
         BL    GETCAB7C                                                         
GETCAB7D ICM   R0,1,1(RE)                                                       
         BZ    GETCAB7E            NO MATCH                                     
         AR    RE,R0                                                            
         CLI   0(RE),X'30'         1ST TIME - POS. TO 1ST DEMO ELEM             
         BL    GETCAB7D                                                         
         ZIC   R1,1(RF)            GET ELEMENT LENGTH                           
         BCTR  R1,0                                                             
         EX    R1,*+8              COMPARE ELEMENT DATA                         
         B     *+10                                                             
         CLC   0(0,RF),0(RE)       **EXECUTED**                                 
         BNE   *+8                                                              
         B     GETCAB7C                                                         
*                                                                               
GETCAB7E MVC   DMCB,DBAQUART                                                    
         BAS   RE,GETNTID                                                       
         L     RF,DMCB                                                          
         ST    RF,DBAQUART                                                      
         CLC   DUB+6(1),DBDQSQH                                                 
         BL    GETCAB8                                                          
         CLC   DUB(1),DBDQEQH                                                   
         BH    GETCAB8                                                          
         MVC   WORKSV+10(2),DBINTFIL                                            
         BAS   RE,GETX             USER HOOK                                    
         MVC   DBINTFIL(2),WORKSV+10                                            
         L     RF,DBAQUART                                                      
         CLI   DBSELPTT,C'P'       ONLY WANT PROGRAM                            
         BNE   *+12                                                             
         CLI   2(RF),0             AND HAD PROGRAM                              
         BE    GETNTI25            NEXT PROGRAM                                 
         B     GETCAB8                                                          
*                                                                               
GETCAB7P L     RF,DBAQUART                                                      
         BAS   RE,CABSPAN                                                       
GETCAB8  ZIC   R0,1(RF)                                                         
         AR    RF,R0                                                            
GETCAB8A CLI   0(RF),0                                                          
         BE    GETCAB9                                                          
GETCAB8B CLI   0(RF),X'0F'         SEGMENT HEADER                               
         BNE   GETCAB8                                                          
         CLI   2(RF),X'FF'         LAST SEGMENT - GET NEXT PROG                 
         BE    GETNTI25                                                         
         ST    RF,DBAQUART                                                      
         BAS   RE,CABSPAN                                                       
         L     RF,DBAQUART                                                      
         LR    RE,RF                                                            
GETCAB8C IC    R0,1(RE)                                                         
         AR    RE,R0                                                            
         CLI   0(RE),X'10'         TEST FOR BREAKOUT                            
         BH    GETCAB8D                                                         
         BNE   GETCAB8C                                                         
         USING PHTELEM,RE                                                       
         TM    PHTRSF,X'01'                                                     
         BNZ   GETCAB6             USE BREAKOUT TELECAST                        
         TM    PHTDTYP,X'04'                                                    
         BNZ   GETCAB6             USE SPECIAL TELECAST                         
         DROP  RE                                                               
GETCAB8D CLC   2(1,RF),PVNORMAL    DO I WANT THIS SEGMENT LEVEL                 
         BH    GETCAB8              NO - TRY NEXT                               
         B     GETCAB6                                                          
         SPACE 2                                                                
GETCAB9  MVI   IOFLAG,FILE+SEQ                                                  
         GOTO1 AIO,IOFLAG                                                       
         L     RF,DBAREC           SET UP FOR NEXT RECORD                       
         AH    RF,DBDTADSP                                                      
         B     GETCAB8A                                                         
         SPACE                                                                  
CABSPAN  NTR1                      DIG OUT AND FORMAT NAMES                     
         L     RF,DBAQUART                                                      
         ICM   R1,15,DBSPANAD                                                   
         LR    RE,RF                                                            
         CLI   0(RF),X'0F'                                                      
         BNE   XIT                                                              
         CLI   2(RF),1             SET FOR UNTITLED TRACKS                      
         BNE   CABSPAN2                                                         
         XC    34(30,R1),34(R1)                                                 
         MVC   34(15,R1),=C'UNTITLED TRACKS'                                    
*                                                                               
CABSPAN2 ZIC   R0,1(RE)                                                         
         AR    RE,R0                                                            
         CLI   0(RE),X'10'                                                      
         BE    CABSPAN7                                                         
         CLI   0(RE),X'21'                                                      
         BE    CABSPAN3                                                         
         CLI   0(RE),X'00'                                                      
         BE    XIT                                                              
         CLI   0(RE),X'0F'                                                      
         BE    XIT                                                              
         B     CABSPAN2                                                         
*                                                                               
CABSPAN3 MVC   3(1,R1),2(RF)                                                    
         CLI   2(RF),0                                                          
         BNE   CABSPAN4                                                         
         XC    4(90,R1),4(R1)                                                   
         ZIC   R4,1(RE)                                                         
         SH    R4,=H'3'                                                         
         EX    R4,CABM1                                                         
         EX    R4,CABM2                                                         
         EX    R4,CABM3                                                         
         B     XIT                                                              
CABSPAN4 CLI   2(RF),1                                                          
         BNE   CABSPAN5                                                         
         XC    34(30,R1),34(R1)                                                 
         ZIC   R4,1(RE)                                                         
         SH    R4,=H'3'                                                         
         EX    R4,CABM2                                                         
         B     XIT                                                              
CABSPAN5 CLI   2(RF),2                                                          
         BNE   XIT                                                              
         XC    64(30,R1),64(R1)                                                 
         ZIC   R4,1(RE)                                                         
         SH    R4,=H'3'                                                         
         EX    R4,CABM3                                                         
         B     XIT                                                              
*                                                                               
CABM1    MVC   4(0,R1),2(RE)                                                    
CABM2    MVC   34(0,R1),2(RE)                                                   
CABM3    MVC   64(0,R1),2(RE)                                                   
*                                                                               
         USING PHTCODE,RE          DIG OUT NTI/TRK/EPIS NUMBER                  
CABSPAN7 MVC   WORK(5),PHTNTI                                                   
         MVI   WORK+5,X'0F'                                                     
         UNPK  WORK+6(11),WORK(6)                                               
         ZIC   R0,2(RF)                                                         
         MH    R0,=H'10'                                                        
         AR    R1,R0                                                            
         MVC   94(10,R1),WORK+6                                                 
         ICM   R1,15,DBSPANAD                                                   
         B     CABSPAN2                                                         
         DROP  RE                                                               
*                                                                               
XIT      XIT1                                                                   
         EJECT                                                                  
* GET VALUES FROM AN NETWORK PROGRAM RECORD (-Q-).                              
*                                                                               
* ON EXIT DUB+0(1)=START QTR HOUR OF THIS RECORD                                
*         DUB+2(1)=DAY CODE FOR RECORD                                          
*         DUB+3(1)=DURATION IN QTR HOURS                                        
*         DUB+6(1)=END QTR HOUR OF RECORD                                       
*         DUB+7(1)=DATA TYPE (BIT0 1=OPTIONAL)                                  
*                                                                               
GETNTID  L     RF,DBAREC                                                        
         MVC   DBACTPRG,PMPNUM-PMKEY(RF)                                        
         MVC   WORK+18(1),PMSTYP-PMKEY(RF)                                      
*                                                                               
         ICM   RF,15,DBAQUART                                                   
         BNZ   *+16                                                             
         L     RF,DBAREC                                                        
         AH    RF,DBDTADSP                                                      
         ST    RF,DBAQUART         <-----INITIALIZE                             
         MVI   DUB+3,1             <-----INITIALIZE                             
         SR    R0,R0                                                            
         USING PHTELEM,RF                                                       
GETNTID2 CLI   0(RF),0             FIND 3 ELEMENTS                              
         BNE   *+6                                                              
         DC    H'0'                                                             
         CLI   PHTCODE,PHTCODEQ    FIND PROGRAM TYPE ELEMENT (X'10')            
         BNE   GETNTID3                                                         
         MVC   DUB+7(1),PHTDTYP    DATA TYPE (BIT 0 - 1=OPTIONAL)               
         B     GETNTID5                                                         
         USING PHELEM,RF                                                        
GETNTID3 CLI   PHCODE,PHCODEQ      FIND QTR HOUR ELEMENT (X'20')                
         BNE   GETNTID4                                                         
         ST    RF,DBAQUART                                                      
         MVC   DUB+3(1),PHDUR      QUARTER HOUR DURATION                        
         B     GETNTID5                                                         
         USING NTELEM,RF                                                        
GETNTID4 CLI   NTCODE,NTCODEQU     FIND RUN TIME ELEMENT (X'22')                
         BE    GETNTID6                                                         
GETNTID5 IC    R0,1(RF)                                                         
         AR    RF,R0                                                            
         B     GETNTID2                                                         
*                                                                               
GETNTID6 MVC   DUB(1),NTSQH                                                     
         MVC   DUB+6(1),NTEQH                                                   
         SR    R1,R1                                                            
         CLI   PNSTAT+4,C'H'                                                    
         BNE   GETNTD6A                                                         
         IC    R1,DUB+6                                                         
         BCTR  R1,0                                                             
         STC   R1,DUB+6                                                         
GETNTD6A MVI   DUB+2,X'90'         SET DAY CODE TO VAR                          
         TM    NTDAY,X'80'         TEST FOR INVALID DATE IN ELEM                
         BO    GETNTID8                                                         
         L     R1,ADQPNNS          FIND DAY CODE FOR BIT SETTING                
         LA    R1,DQHDRLN(R1)                                                   
         USING DQDTAD,R1                                                        
*                                                                               
GETNTID7 CLI   DQDAY,0             TEST FOR END OF TABLE                        
         BE    GETNTID8                                                         
         CLC   NTDAY,DQDAY         TEST FOR MATCH AGAINST TABLE                 
         BE    *+12                                                             
         LA    R1,DQDTALN(R1)                                                   
         B     GETNTID7                                                         
         MVC   DUB+2(1),DQKDAY     EXTRACT DAY CODE FOR BIT SETTING             
*                                                                               
GETNTID8 BR    RE                                                               
         DROP  R1,R2,RF                                                         
         EJECT                                                                  
GETX     CLI   DBERROR,0           TEST IF ERROR SET                            
         BE    *+14                                                             
         XC    DBAQUART,DBAQUART   YES - CLEAR QTR HOUR ADDRESS                 
         MVI   DBMODE,DBMFRST      AND RESET SEQUENTIAL MODE                    
         MVC   DBLSTFNC,DBFUNCT    EQUATE LAST FUNCTION TO THIS                 
         OC    AUSERRTN,AUSERRTN   TEST IF USER HOOK SUPPLIED                   
         BZ    GETXX               NO - RETURN TO CALLER                        
         CLI   DBERROR,0           TEST IF ERROR SET                            
         BNE   GETXX               YES - RETURN TO CALLER                       
         CLI   DBMODE,DBMNEXT      TEST IF SEQUENTIAL MODE                      
         BE    *+8                                                              
         LA    RE,GETXX            NO - SET RETURN TO EXIT                      
*                                  SAVE REGS & GO TO USER HOOK                  
         NTR1                                                                   
         L     RF,AUSERRTN                                                      
         L     RE,ROOTRD                                                        
         LR    R0,RE               R0=A(CALLER'S RD)                            
         LM    R1,RC,24(RE)        R1-RC=CALLERS REGISTERS                      
         BASR  RE,RF               GO TO USER HOOK                              
         XIT1  ,                   EXIT TO CALLER OR DEMAND                     
*                                                                               
GETXX    DS    0H                                                               
         ICM   RE,15,ADTLIST       ANY DAY/TIME LIST                            
         BZ    GETXXX                                                           
         USING DBXTLD,RE                                                        
         ZIC   R1,DBXTLIDX         BUMP THE INDEX                               
         LA    R1,1(R1)                                                         
         STC   R1,DBXTLIDX                                                      
         MH    R1,=H'5'                                                         
         AR    R1,RE                                                            
         CLI   DBXTLIST-DBXTLID(R1),0    EOL                                    
         BE    GETXXX                                                           
         MVI   DBERROR,0           RESET ERROR                                  
         MVI   DBLSTNXT,0          RESET FIRST TIME                             
         ICM   RE,15,DBSPANAD      REPROCESS PROGRAM FOR NEW DAY/TIM            
         BZ    GETNTI4                                                          
         LA    RE,200(RE)          ONLY PROCESS PROGRAM 1 TIME                  
         XC    0(15,RE),0(RE)                                                   
*                                                                               
         B     GETNTI4                                                          
         DROP  RE                                                               
         SPACE 2                                                                
GETXXX   XIT1  ,                   EXIT TO CALLER                               
         LTORG                                                                  
         EJECT                                                                  
***************************************************                             
*TSTDYL- TEST ACCESS TO DAILY DATA (DCON SECURITY)                              
*        IF GRANTED, CONDITION CODE NONZERO                                     
***************************************************                             
TSTDYL   NTR1                                                                   
         L     R2,ACONHDR          R2=A(CONTROL TABLE)                          
         USING CONHDRD,R2                                                       
         LA    R3,CONHDRD+CONHDRLN                                              
         USING CONDTAD,R3                                                       
         MVI   DEMOFLAG,0                                                       
         LA    R4,3                                                             
         SR    R5,R5                                                            
         ICM   R5,7,CONAET                                                      
         CR    R3,R5               NO LISTS?? --JUST DISALLOW                   
         BE    TSTDYLX                                                          
*                                                                               
TSTDYL5  CLI   CONINDIC,0          PROCESS VALID MARKET LIST                    
         BNE   TSTDYLX             NOT VALID                                    
         OI    DEMOFLAG,X'80'                                                   
         CLC   CONMRKT,=H'221'     DAILY MKT VALID?                             
         BE    *+10                                                             
         CLC   CONMRKT,=H'222'     DAILY MKT VALID?                             
         BNE   *+12                                                             
         OI    DEMOFLAG,X'40'                                                   
         B     *+8                                                              
         BXLE  R3,R4,TSTDYL5                                                    
*                                                                               
TSTDYLX  CLI   DEMOFLAG,X'C0'      TEST MARKET NUMBERS VALID                    
         XIT1                                                                   
         EJECT                                                                  
**********************************************************************          
*RDBUF - READS WHAT BUFFER???                                                   
**********************************************************************          
         PRINT GEN                                                              
RDBUF    NTR1  WORK=(R3,10)                                                     
         USING RDBUFD,R3                                                        
         PRINT NOGEN                                                            
         CLC   DBKEY(3),=C'NNN'    ONLY BUFFER DAY/TIME ITEMS                   
         BNE   RDBUF1                                                           
         CLC   DBLOPT,=F'18999'    NEED A MIN SIZED AREA                        
         BH    RDBUF2                                                           
RDBUF1   GOTO1 AIO,IOFLAG          JUST READ IF NOT THERE                       
         B     RDBUFX                                                           
         SPACE 1                                                                
RDBUF2   L     RE,DBAOPT                                                        
         A     RE,=F'3000'                                                      
         ST    RE,ARDBF                                                         
         LA    R4,4(RE)                                                         
         MVC   RDBKEY,DBKEY                                                     
         MVC   RDBFLAG,IOFLAG                                                   
         CLC   4(PNDW-PNKEY,RE),DBKEY BUFFER OK?                                
         BE    RDBUF8                                                           
         ST    R4,0(RE)                                                         
         XC    DBKEY,DBKEY                                                      
         MVC   DBKEY(PNDW-PNKEY),RDBKEY                                         
         MVI   IOFLAG,DIR+HIGH                                                  
         GOTO1 AIO,IOFLAG                                                       
         BNE   *+14                                                             
         CLC   DBKEY(PNDW-PNKEY),KEYSAVE                                        
         BE    RDBUF6                                                           
         MVC   DBKEY,RDBKEY                                                     
         MVC   IOFLAG,RDBFLAG                                                   
         B     RDBUF1                                                           
         SPACE 2                                                                
RDBUF4   MVI   IOFLAG,DIR+SEQ                                                   
         GOTO1 AIO,IOFLAG                                                       
RDBUF6   MVC   0(18,R4),DBKEY                                                   
         LA    R4,18(R4)                                                        
         CLC   DBKEY(PNDW-PNKEY),KEYSAVE                                        
         BE    RDBUF4                                                           
         MVC   0(18,R4),=18X'FF'                                                
         L     RE,ARDBF                                                         
         MVC   DBKEY,RDBKEY                                                     
         MVC   IOFLAG,RDBFLAG                                                   
         SPACE 2                                                                
RDBUF8   L     R4,ARDBF                                                         
         CLI   IOFLAG,SEQ+DIR                                                   
         BNE   RDBUF10                                                          
         L     R4,0(R4)            PICK UP LAST READ + 1                        
         MVC   DBKEY,0(R4)                                                      
         L     RE,ARDBF                                                         
RDBUF9   L     RF,DBAREC                                                        
         MVC   0(18,RF),0(R4)                                                   
         MVC   DBKEY(18),0(R4)                                                  
         LA    R4,18(R4)                                                        
         ST    R4,0(RE)                                                         
         MVI   DBERROR,0                                                        
         CLI   DBKEY,X'FF'                                                      
         BNE   *+8                                                              
         MVI   DBERROR,X'80'                                                    
         CLI   DBERROR,0                                                        
         B     RDBUFX                                                           
         SPACE 1                                                                
RDBUF10  L     R4,ARDBF            POINT TO THE BUFFER                          
         LA    R4,4(R4)            GET PAST SEQ LINK                            
         MVC   KEYSAVE,DBKEY                                                    
         CLI   IOFLAG,HIGH+DIR     MAKE SURE IT'S A READ HIGH                   
         BE    *+6                                                              
         DC    H'0'                                                             
RDBUF12  CLC   DBKEY(18),0(R4)                                                  
         BNH   *+12                                                             
         LA    R4,18(R4)                                                        
         B     RDBUF12                                                          
         L     RE,ARDBF                                                         
         B     RDBUF9                                                           
         SPACE 2                                                                
RDBUFX   XIT1                                                                   
         LTORG                                                                  
**********************************************************************          
*GETMB  - GET MARKET BREAK SECTION LEAD ELEM AS REQSTD IN DBBTYPE               
**********************************************************************          
GETMB    NTR1                                                                   
         XC    SVNDXDA,SVNDXDA                                                  
         L     R2,DBAREC                                                        
         USING PMKEY,R2                                                         
         MVC   FILKEY,0(R2)        SAVE ORIGINAL KEY                            
         XC    DBAQUART,DBAQUART                                                
         CLI   PMSTYP,0                                                         
         BE    GETMB2                                                           
         TM    SPNETFLG,SPNHTQ                                                  
         BNO   GETMB2                                                           
         BAS   RE,NHTWK            GET NHTI WEEKLY RECD FOR SP & BRKS           
*                                                                               
GETMB2   MVC   FILMB(1),DBBTYPE    DEFAULT IS TO INVALID                        
         CLI   FILMB,0                                                          
         BNE   *+12                                                             
         MVI   FILMB,1                                                          
         B     GETMB5                                                           
         TM    SPNETFLG,SPNHTQ     NHTI FILE BKTYPE EQUATES                     
         BNO   GETMB5                                                           
         LA    RE,NHTBRKS          TRANSLATE BKTYPE TO INTERNAL CODE            
GETMB3   CLI   0(RE),X'FF'           AND SAVE IN FILMB                          
         BE    GETMB5               -USE DDBTYPE STRAIGHT                       
         CLC   DBBTYPE,0(RE)                                                    
         BE    *+12                                                             
         LA    RE,L'NHTBRKS(RE)                                                 
         B     GETMB3                                                           
         MVC   FILMB(1),1(RE)                                                   
*                                                                               
GETMB5   L     R2,DBAREC                                                        
         LA    R1,PMDATA                                                        
         MVI   INBRK,C'N'                                                       
         SR    RE,RE                                                            
*                                                                               
GETMB10  CLI   0(R1),0             LOOP THRU LKING FOR X'23'                    
         BE    GETMB40             TO COMPARE ON MKT BREAK                      
         CLI   0(R1),X'23'         GET SECTION LEAD ELEM                        
         BNE   GETMB20                                                          
         MVI   0(R1),X'FF'         DELETE X'23' ELEM LATER                      
         CLC   FILMB(1),2(R1)      MATCH MKT BREAK TO SECTN LEAD                
         BNE   GETMB20                                                          
         ST    R1,DBAQUART         SAVE START OF MKT BRK ELEMS, FOR NOW         
         MVI   INBRK,C'Y'                                                       
*                                                                               
GETMB20  DS    0H                                                               
         CLI   INBRK,C'Y'          ARE WE WITHIN MKT BRK SECTION?               
         BNE   GETMB25                                                          
         CLI   0(R1),X'5E'         END OF GOOD SECTION                          
         BNE   GETMB30                                                          
         MVI   INBRK,C'N'                                                       
         B     GETMB30             DON'T MARK FOR DELETION                      
*                                                                               
GETMB25  CLI   0(R1),X'30'                                                      
         BL    *+8                 DON'T DELETE THE NAME/TIME ELEMS             
         MVI   0(R1),X'FF'         MARK OTHER MKT BRK ELEMS FOR DEL             
*                                                                               
GETMB30  IC    RE,1(R1)            BUMP TO NEXT ELEM IN RECD                    
         AR    R1,RE                                                            
         B     GETMB10                                                          
*                                                                               
GETMB40  CLC   DBAQUART,BZEROS     COMPARE TO BINARY ZEROS                      
         BNE   GETMB50                                                          
         LA    R2,DBKEY                                                         
         ZIC   R1,PMBTYP           BUMP BOOKTYPE FIELD IN KEY                   
         LA    R1,1(R1)                                                         
         STC   R1,PMBTYP                                                        
         XC    PMKSTAT(5),PMKSTAT                                               
         MVI   RSTMB,C'Y'          WILL NEED TO RESET FILE PTR LATER            
         MVI   IOFLAG,DIR+READ     READ/DIRECTORY                               
         GOTO1 AIO,IOFLAG                                                       
         BL    GETX                                                             
         BNE   GETMBNX             MKT BRK NOT AVAILABLE/NOT FOUND              
         MVI   IOFLAG,FILE+READ    READ/FILE                                    
         GOTO1 AIO,IOFLAG                                                       
         BE    GETMB5                                                           
         BNE   GETMBNX             MKT BRK NOT AVAILABLE/NOT FOUND              
*                                                                               
GETMB50  DS    0H                  DEL X'FF' ELEMS FROM RECD                    
         GOTO1 CHELLO,DMCB,(C'D',=C'PAVFIL  '),(X'FF',DBAREC),0,0               
         CLI   DMCB+12,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
         DROP  R2                                                               
*                                                                               
         ICM   R2,15,DBSPANAD      FIND MKT BRK UNIVS AND PUT ON RECD           
         LA    R2,4(R2)            BUMP PASS LABEL                              
         USING PRKEY,R2                                                         
         LA    R2,PRFRSTEL                                                      
         SR    RF,RF                                                            
         MVI   INBRK,C'N'                                                       
GETMB60  CLI   0(R2),0                                                          
         BE    GETMB80             NO UNIVS FOUND                               
         CLI   0(R2),X'23'                                                      
         BNE   GETMB62                                                          
         MVI   INBRK,C'N'          RE-SET DEFAULT                               
         CLC   FILMB,2(R2)         GET CORRECT MKT BREAK UNIVS                  
         BL    GETMB80             IT' NOT GOING TO BE THERE--EXIT              
         BH    GETMB65             KEEP LKG                                     
         MVI   INBRK,C'Y'          WE ARE W/IN THE CORRECT MKT BRK              
         B     GETMB65             BUMP TO NEXT ELEM                            
*                                                                               
GETMB62  CLI   INBRK,C'Y'          ARE WE W/IN THE BRK?                         
         BNE   GETMB65                                                          
         CLI   0(R2),X'4B'         YES: XFER 4B & 4C ELEMS ONLY                 
         BE    *+12                                                             
         CLI   0(R2),X'4C'                                                      
         BNE   GETMB65                                                          
         GOTO1 CHELLO,DMCB,(C'P',=C'PAVFIL  '),DBAREC,(R2),0                    
         CLI   0(R2),X'4C'                                                      
         BE    GETMB80             DONE XFERING                                 
         SR    RF,RF                                                            
*                                                                               
GETMB65  IC    RF,1(R2)            LK FOR MKT BRK MATCH OR 4B/4C                
         AR    R2,RF                                                            
         B     GETMB60                                                          
         DROP  R2                                                               
*                                                                               
GETMB80  L     R2,DBAREC           SET DBAQUART                                 
         USING PMKEY,R2                                                         
         LA    R1,PMDATA                                                        
         SR    RE,RE                                                            
GETMB85  CLI   0(R1),0                                                          
         BE    GETMBNX                                                          
         CLI   0(R1),X'20'                                                      
         BE    *+14                                                             
         IC    RE,1(R1)                                                         
         AR    R1,RE                                                            
         B     GETMB85                                                          
         ST    R1,DBAQUART                                                      
         CLI   PMKEY+1,C'W'                                                     
         BNE   GETMBX                                                           
         MVI   PMKEY+1,C'N'        RESET SO THEY THINK IT'S MNTHLY RECD         
         MVC   PMBOOK,DBSELBK                                                   
         MVC   PHDWKS-PHELEM(1,R1),WKBIT  SET WEEK ACTIVE BIT                   
         B     GETMBX                                                           
*                                                                               
GETMBNX  DS    0H                  MKT BREAK RECD NOT FOUND                     
         MVI   RSTMB,C'F'          FUDGE A RECD W/NO DEMOS                      
         BAS   RE,RSETMB                                                        
*                                                                               
GETMBX   OC    DBAQUART,DBAQUART   WAS A RECD FOUND?                            
         XIT1                                                                   
         DROP  R2                                                               
*                                                                               
         EJECT                                                                  
**********************************************************************          
*RSETMB - RESET POINTER TO '00' MKT BREAK WHERE WE WERE BEFORE GETMBK           
**********************************************************************          
RSETMB   NTR1                                                                   
         OC    SVNDXDA,SVNDXDA                                                  
         BZ    *+10                                                             
         MVC   DBNDXDA,SVNDXDA                                                  
         MVC   DBKEY,FILKEY                                                     
         LA    R2,DBKEY                                                         
         USING PMKEY,R2                                                         
         XC    PMKSTAT(5),PMKSTAT                                               
         MVI   IOFLAG,DIR+READ     READ/DIRECTORY                               
         GOTO1 AIO,IOFLAG                                                       
         BL    GETX                                                             
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   DBMINKEY(2),FILKEY+18                                            
         MVI   IOFLAG,FILE+READ    READ/FILE                                    
         GOTO1 AIO,IOFLAG                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         CLI   RSTMB,C'F'          MKTBRK NOT FOUND, RTN RECD W/NO DEMS         
         BNE   RSETMBX             FUDGE RECD W/JUST PRG INFO IN IT             
         L     R2,DBAREC                                                        
         LA    R1,PMDATA                                                        
         SR    RE,RE                                                            
         SR    RF,RF                                                            
*                                                                               
RSET20   CLI   0(R1),0             DELETE DEMO ELEMS SAVE PRG ELEMS             
         BE    RSET35                                                           
         CLI   0(R1),X'20'                                                      
         BNE   *+8                                                              
         ST    R1,DBAQUART                                                      
         CLI   0(R1),X'23'         DELETE LEAD ELEM TOO                         
         BE    RSET25                                                           
         CLI   0(R1),X'5E'         END OF SECTION --> EXIT                      
         BNE   RSET22                                                           
         LTR   RF,RF               DID WE GET ONE 5E ALREADY?                   
         BNZ   RSET25              YES, DELETE THE REST                         
         LA    RF,1                                                             
         B     RSET30              SAVE THIS ONE                                
RSET22   CLI   0(R1),X'30'                                                      
         BL    RSET30              SAVE HEADER ELEMS                            
RSET25   MVI   0(R1),X'FF'         DELETE ELEM                                  
*                                                                               
RSET30   IC    RE,1(R1)            BUMP TO NEXT ELEM IN RECD                    
         AR    R1,RE                                                            
         B     RSET20                                                           
*                                                                               
RSET35   GOTO1 CHELLO,DMCB,(C'D',=C'PAVFIL  '),(X'FF',DBAREC),0,0               
*                                                                               
         ICM   R2,15,DBSPANAD      PUT UNIV ON RECD                             
         LA    R2,4(R2)            BUMP PASS LABEL                              
         USING PRKEY,R2                                                         
         LA    R2,PRFRSTEL                                                      
         SR    RF,RF                                                            
         MVI   INBRK,C'N'                                                       
         CLI   FILMB,0                                                          
         BNE   *+8                                                              
         MVI   FILMB,X'01'         UNIV RECD TOT-SMPL IS X'01'                  
RSET60   CLI   0(R2),0                                                          
         BE    RSETMBX             NO UNIVS FOUND                               
         CLI   0(R2),X'23'                                                      
         BNE   RSET62                                                           
         MVI   INBRK,C'N'          RE-SET DEFAULT                               
         CLC   FILMB,2(R2)         GET CORRECT MKT BREAK UNIVS                  
         BL    RSETMBX             IT' NOT GOING TO BE THERE--EXIT              
         BH    RSET65              KEEP LKG                                     
         MVI   INBRK,C'Y'          WE ARE W/IN THE CORRECT MKT BRK              
         B     RSET65              BUMP TO NEXT ELEM                            
*                                                                               
RSET62   CLI   INBRK,C'Y'          ARE WE W/IN THE BRK?                         
         BNE   RSET65                                                           
         CLI   0(R2),X'4B'         YES: XFER 4B & 4C ELEMS ONLY                 
         BE    *+12                                                             
         CLI   0(R2),X'4C'                                                      
         BNE   RSET65                                                           
         GOTO1 CHELLO,DMCB,(C'P',=C'PAVFIL  '),DBAREC,(R2),0                    
         CLI   0(R2),X'4C'                                                      
         BE    RSETMBX             DONE XFERING                                 
         SR    RF,RF                                                            
*                                                                               
RSET65   IC    RF,1(R2)            LK FOR MKT BRK MATCH OR 4B/4C                
         AR    R2,RF                                                            
         B     RSET60                                                           
*                                                                               
RSETMBX  MVI   RSTMB,0                                                          
         XIT1                                                                   
         DROP  R2                                                               
         EJECT                                                                  
**********************************************************************          
*NHTWK - LOOK UP WEEKLY RECD                                                    
**********************************************************************          
NHTWK    NTR1                                                                   
         MVC   SVNDXDA,DBNDXDA                                                  
         OC    ADEFINE,ADEFINE                                                  
         BZ    NHTWKX              CAN'T DO ANYTHING W/OUT THE WKS              
         GOTO1 ADEFINE,DMCB,=C'NDMON',DBLOCKD,WEEKS                             
         LA    R2,DBKEY                                                         
         USING PMKEY,R2                                                         
         LA    R5,WEEKS+4          PT TO START WEEK                             
         MVI   WKBIT,X'08'         WK AIRED BIT FOR X'20' ELEM                  
*                                                                               
         USING PMKEY,R2                                                         
NHTWK10  CLI   0(R5),0             DONE WITH WEEKS?                             
         BE    NHTWKX              WKLY RECD NOT FOUND                          
         XC    DBNDXDA,DBNDXDA                                                  
         MVC   DBKEY,FILKEY                                                     
         MVI   PMKEY+1,C'W'        LOOK UP : QWN RECDS                          
         MVC   PMBOOK,0(R5)        MOVE IN WEEK BOOK                            
         XC    PMKSTAT(5),PMKSTAT                                               
         MVI   RSTMB,C'Y'          WILL NEED TO RESET FILE PTR LATER            
         MVI   IOFLAG,DIR+READ     READ/DIRECTORY                               
         GOTO1 AIO,IOFLAG                                                       
         BL    GETX                READ ERROR                                   
         CLC   DBKEY(PMKSTAT-PMKEY),KEYSAVE                                     
         BNE   NHTNXTWK            NOT FOUND FOR THIS WEEK                      
         MVI   IOFLAG,FILE+READ    READ/FILE                                    
         GOTO1 AIO,IOFLAG                                                       
         BE    NHTWKX              DONE, GOT THE RECD WE WANT! YIPPEE!          
         B     NHTNXTWK            PROGRAM NUMBER NOT FND IN THIS WK            
*                                                                               
NHTNXTWK DS    0H                                                               
         LA    R5,2(R5)            BUMP WEEK TABLE POINTER                      
         SR    R0,R0                                                            
         IC    R0,WKBIT            BUMP TO NEXT BIT FOR WK AIRED                
         SRL   R0,1                                                             
         STC   R0,WKBIT                                                         
         B     NHTWK10                                                          
*                                                                               
NHTWKX   B     XIT                                                              
*                                                                               
**********************************************************************          
*LOCAL WORK AREA - SMALL PART OF ALLOC AT TOP OF GETNTI                         
**********************************************************************          
         LTORG                                                                  
*                                                                               
NHTBRKS  DS    0CL2         WHEN SPTNHT ACTIVE, ALLOW CHAR BKTYPES              
         DC    C'1',X'01'          CONVERT CHAR TO NUMERIC                      
         DC    C'5',X'05'                                                       
         DC    C'6',X'06'                                                       
         DC    C'7',X'07'                                                       
         DC    C'H',X'05'                                                       
         DC    C'E',X'06'                                                       
         DC    C'B',X'07'                                                       
         DC    X'FFFFF'                                                         
*                                                                               
BZEROS   DC    XL20'00'                                                         
*                                                                               
MYWORK   DSECT                                                                  
SPANAD   DS    CL2000              STORE UNIVERSES                              
FILKEY   DS    XL23                ORIGINAL KEY                                 
INBRK    DS    C                   FLAG=N IF NOT IN DESIRED MKT BRK             
RSTMB    DS    C                   RESET POINTER TO MKBRK 1ST RECD              
FILMB    DS    C                   MKT BREAK NUMBER ON FILE                     
WKBIT    DS    X                   WEEK AIRED TO SLOT INTO B/S RECD             
WEEKS    DS    CL20                WEEKS FROM DEFINE                            
SVDQPTR  DS    X                   FOR SPOT DBDQPTR OVERRIDEN TO 1              
SVACTBK  DS    XL2                 BOOK ORIG IN DBACTBK FIELD                   
SVACTSTA DS    CL5                 PREV STATION IN ACTSTA FLD                   
SVNDXDA  DS    F                                                                
ADEFINE  DS    A                   ADDRESS OF DEFINE                            
BSSW     DS    C                                                                
BREAKOUT EQU   X'01'                                                            
SPECIAL  EQU   X'02'                                                            
MYWORKX  EQU   *                                                                
         SPACE 2                                                                
*                                                                               
RDBUFD   DSECT                                                                  
ARDBF    DS    F                                                                
RDBKEY   DS    XL23                                                             
RDBFLAG  DS    XL1                                                              
*                                                                               
* LITERALS ETC.                                                                 
*                                                                               
         EJECT                                                                  
       ++INCLUDE DEGETD            REMOVED 'B' TO ASM CLEAN                     
         SPACE 1                                                                
       ++INCLUDE DEDBEXTRAD                                                     
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003MYGETNTIB 05/01/02'                                      
         END                                                                    
