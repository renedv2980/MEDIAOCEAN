*          DATA SET DDLOCKKEY  AT LEVEL 028 AS OF 05/12/05                      
*CATALP LOCKKEY                                                                 
         TITLE 'DDLOCKKEY - LOGICAL DATASPACE LOCKS '                           
LOCKKEY  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,**LKKY**,CLEAR=YES                                   
         USING WORKD,RC                                                         
         SPACE 1                                                                
************************************************************                    
*     LOCK A LOGICAL KEY IN DATASPACE                      *                    
*                                                          *                    
*  P1 CL1         CALL TYPE                                *                    
*     C'L'        LOCK THIS KEY                            *                    
*     C'U'        FREE THIS KEY                            *                    
*     C'T'        TEST THIS KEY                            *                    
*                                                          *                    
*     AL3         A(LOCK KEY) 16 BYTES                     *                    
*                                                          *                    
*  P2 XL1         RETURN FLAGS (IF P1=08 OR 04)            *                    
*     X'00'       OK                                       *                    
*     X'80'       ERROR IN LOCKING / UNLOCKING                                  
*     X'40'       LOGICAL LOCKTAB FULL                     *                    
*     X'20'       KEY ALREADY LOCKED                       *                    
*                                                          *                    
************************************************************                    
         EJECT                                                                  
MAIN     LR    R3,R1                                                            
         USING PARMD,R3                                                         
         ST    RD,SAVERD           SAVE RETURN ADDRESS                          
*                                                                               
         ICM   RE,15,=V(SSB)       GET SSB                                      
         BNZ   *+6                                                              
         DC    H'0'                                                             
         OC    0(2,RE),0(RE)       TEST FOR OFFLINE                             
         BZ    MAIN020                                                          
*                                                                               
         L     RE,SSBTKADR-SSBD(RE)                                             
         L     RE,TCBUTL-TCBD(RE)                                               
         MVC   LOCKSYS,TSYS-UTLD(RE)                                            
         B     MAIN030                                                          
*                                                                               
MAIN020  ICM   RE,15,=V(UTL)       GET SYS FROM UTL OFFLINE                     
         BNZ   *+6                                                              
         DC    H'0'                                                             
         MVC   LOCKSYS,4(RE)                                                    
*                                                                               
MAIN030  XC    DUB,DUB             LOCK SYSTEM                                  
         MVC   DUB+3(1),LOCKSYS                                                 
         GOTO1 =V(LOCKSPC),DUB                                                  
*                                                                               
         L     RE,=V(SSB)          GET ALET AND OFFS                            
         MVC   ALET,SSBALET-SSBD(RE)                                            
         XC    OFFS,OFFS                                                        
*                                                                               
         L     R2,4(R1)                                                         
         USING DMSPACED,R2                                                      
*                                                                               
         MVC   TIMENOW,DSPTIME     GET TIME                                     
         MVC   ASYSTEM,DSPECB      GET A(SYSTEM BLOCK)                          
*                                                                               
         TM    DSPTYPE,DSPJOBQ     TEST OFFLINE JOB                             
         BNO   MAIN040                                                          
         MVC   LOCKID,DSPJOB       JOBNO                                        
         B     MAIN050                                                          
*                                                                               
MAIN040  L     RE,=V(SSB)                                                       
         MVC   LOCKID+0(1),SSBSYSID-SSBD(RE)                                    
         L     RE,SSBTKADR-SSBD(RE)                                             
         MVC   LOCKID+1(1),TCBTASK-TCBD(RE)                                     
*                                                                               
MAIN050  CLI   P1,C'L'             LOCK A KEY                                   
         BNE   MAIN060                                                          
         BAS   RE,LOCK01                                                        
         B     MAIN100                                                          
*                                                                               
MAIN060  CLI   P1,C'T'             TEST A KEY                                   
         BNE   MAIN070                                                          
         BAS   RE,TEST01                                                        
         B     MAIN100                                                          
*                                                                               
MAIN070  CLI   P1,C'U'             FREE A KEY                                   
         BNE   MAIN100                                                          
         BAS   RE,FREE01                                                        
         B     MAIN100                                                          
*                                                                               
MAINEXIT L     RD,SAVERD           RESTORE RD                                   
*                                                                               
MAIN100  XC    DUB,DUB             FREE SYSTEM                                  
         MVC   DUB+3(1),LOCKSYS                                                 
         OI    DUB,X'10'                                                        
         GOTO1 =V(LOCKSPC),DUB                                                  
         B     MAINX                                                            
*                                                                               
MAINX    CLI   P2,0                SET CC                                       
         XMOD1                                                                  
*                                                                               
XIT1     XIT1                                                                   
         EJECT                                                                  
*************************************************************                   
*        LOCK A KEY                                         *                   
*************************************************************                   
         SPACE 1                                                                
LOCK01   NTR1                                                                   
*                                                                               
         LAM   R2,R2,ALET          SET R2 FOR DSPACE                            
         SAC   512                                                              
         L     R2,ASYSTEM          GET SYSTEM ADDR                              
         USING DMSYSHDR,R2                                                      
         ST    R2,FULL                                                          
         L     R2,DSYALKEY         GET A LOCKEY TABLE                           
         DROP  R2                                                               
*                                                                               
         LA    R0,32                                                            
LOCK010  OC    0(2,R2),0(R2)       ANY ENTRY                                    
         BZ    LOCK020                                                          
         LA    R2,32(,R2)          NEXT                                         
         BCT   R0,LOCK010                                                       
         B     LKERRFUL            TABLE FULL                                   
*                                                                               
LOCK020  MVC   0(2,R2),LOCKID      SET ID                                       
         MVC   2(4,R2),TIMENOW     SET TIME                                     
         SR    R1,R1                                                            
         ICM   R1,7,P1+1                                                        
         BZ    LKERRERR                                                         
         MVC   16(16,R2),0(R1)     SET LOCK KEY                                 
*                                                                               
LOCKXX   SAC   0                                                                
         B     XIT1                                                             
         EJECT                                                                  
*************************************************************                   
*        TEST A KEY                                         *                   
*************************************************************                   
         SPACE 1                                                                
TEST01   NTR1                                                                   
*                                                                               
         LAM   R2,R2,ALET          SET R2 FOR DSPACE                            
         SAC   512                                                              
         L     R2,ASYSTEM          GET SYSTEM ADDR                              
         USING DMSYSHDR,R2                                                      
         ST    R2,FULL                                                          
         L     R2,DSYALKEY         GET A LOCKET TABLE                           
         DROP  R2                                                               
*                                                                               
         LA    R0,32                                                            
TEST010  OC    0(2,R2),0(R2)       ANY ENTRY                                    
         BZ    TEST020                                                          
         CLC   0(2,R2),LOCKID      IS IT MY ENTRY                               
         BE    TEST020                                                          
         SR    R1,R1                                                            
         ICM   R1,7,P1+1                                                        
         BAS   RE,KEYTEST          TEST LOGICAL LOCK                            
         BE    LKERRLOK            EXIT HERE IF LOCKED                          
*                                                                               
TEST020  LA    R2,32(,R2)          NEXT                                         
         BCT   R0,TEST010                                                       
         B     TESTXX                                                           
*                                                                               
TESTXX   SAC   0                                                                
         B     XIT1                                                             
         EJECT                                                                  
*************************************************************                   
*        FREE A KEY                                         *                   
*************************************************************                   
         SPACE 1                                                                
FREE01   NTR1                                                                   
*                                                                               
         LAM   R2,R2,ALET          SET R2 FOR DSPACE                            
         SAC   512                                                              
         L     R2,ASYSTEM          GET SYSTEM ADDR                              
         USING DMSYSHDR,R2                                                      
         ST    R2,FULL                                                          
         L     R2,DSYALKEY         GET A LOCKET TABLE                           
         DROP  R2                                                               
*                                                                               
         LA    R0,32                                                            
FREE010  OC    0(2,R2),0(R2)       ANY ENTRY                                    
         BZ    FREE020                                                          
         CLC   0(2,R2),LOCKID      IS IT MY ENTRY                               
         BNE   FREE020                                                          
         SR    R1,R1                                                            
         ICM   R1,7,P1+1           IF P1 IS ZERO                                
         BNZ   *+14                                                             
         XC    0(32,R2),0(R2)      CLEAR ALL MY ENTRIES                         
         B     FREE020                                                          
*                                                                               
         CLC   16(16,R2),0(R1)     MATCH ENTRY                                  
         BNE   FREE020                                                          
         XC    0(32,R2),0(R2)      CLEAR IT IF IT MATCHES                       
*                                                                               
FREE020  LA    R2,32(,R2)          NEXT                                         
         BCT   R0,TEST010                                                       
         B     TESTXX                                                           
*                                                                               
FREEXX   SAC   0                                                                
         B     XIT1                                                             
         EJECT                                                                  
*************************************************************                   
*        LOGICAL LOCK KEY TESTING                           *                   
*************************************************************                   
         SPACE 1                                                                
KEYTEST  ST    RE,SAVERE                                                        
*                                                                               
         CLC   0(16,R1),16(R2)     BASIC TEST                                   
*                                                                               
KEYTESTX BR    RE                                                               
         EJECT                                                                  
*************************************************************                   
*        ERROR EXITS                                        *                   
*************************************************************                   
         SPACE 1                                                                
LKERRERR MVC   P2(1),X'80'                                                      
         B     MAINEXIT                                                         
LKERRFUL MVC   P2(1),X'40'                                                      
         B     MAINEXIT                                                         
LKERRLOK MVC   P2(1),X'20'                                                      
         B     MAINEXIT                                                         
         EJECT                                                                  
*************************************************************                   
*        CONSTANTS & LTORG                                  *                   
*************************************************************                   
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
*************************************************************                   
*        WORKING STORAGE DSECT                              *                   
*************************************************************                   
         SPACE 1                                                                
WORKD    DSECT                                                                  
SAVERD   DS    F                                                                
SAVERE   DS    F                                                                
DUB      DS    D                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    X                                                                
FLAG     DS    X                                                                
WORK     DS    0CL64                                                            
*                                                                               
ALET     DS    AL4                                                              
OFFS     DS    AL4                                                              
LOCKSYS  DS    X                                                                
LOCKID   DS    XL2                 SYS/TSK OR JOBNO                             
TIMENOW  DS    A                   CURRENT TIME                                 
ASYSTEM  DS    A                   A(SYSTEM BLOCK)                              
WORKX    EQU   *                                                                
         EJECT                                                                  
*************************************************************                   
*        OTHER DSECTS                                       *                   
*************************************************************                   
         SPACE 1                                                                
PARMD    DSECT                                                                  
P1       DS    F                                                                
P2       DS    F                                                                
P3       DS    F                                                                
P4       DS    F                                                                
         SPACE 1                                                                
*FATCB                                                                          
*FAUTL                                                                          
       ++INCLUDE FATCB                                                          
       ++INCLUDE FAUTL                                                          
*FASSB                                                                          
       ++INCLUDE FASSB                                                          
         ORG   SSBCNTL                                                          
*FASSBOFF                                                                       
       ++INCLUDE FASSBOFF                                                       
         EJECT                                                                  
*DMSPACED                                                                       
         PRINT OFF                                                              
       ++INCLUDE DMSPACED                                                       
       ++INCLUDE DMDSYSHDR                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'028DDLOCKKEY 05/12/05'                                      
         END                                                                    
