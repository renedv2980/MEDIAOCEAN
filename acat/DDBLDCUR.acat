*          DATA SET DDBLDCUR   AT LEVEL 015 AS OF 04/19/01                      
*CATALP BLDCUR                                                                  
         TITLE 'BUILD A CURRENCY TABLE FOR ONE OR ALL CURRENCIES'               
BLDCUR   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,**BLDC**                                             
         USING WORKD,RC            RC=A(W/S)                                    
         LR    RA,R1               RA=A(PARM LIST)                              
         MVI   0(RA),0             RESET ERROR BYTE                             
         XC    CURCODE,CURCODE                                                  
         ICM   R1,15,0(RA)                                                      
         BZ    *+10                                                             
         MVC   CURCODE,0(R1)       SET REQUESTED CURRENCY CODE                  
         L     R2,4(RA)                                                         
         LA    R2,0(R2)            R2=A(OUTPUT TABLE/ENTRY)                     
         USING CURTABD,R2                                                       
         L     R4,8(RA)            R4=A(COMFACS)                                
         USING COMFACSD,R4                                                      
*                                                                               
         MVI   CTRYUK,C'Y'                                                      
*                                                                               
         MVI   ONOROFF,OFF         CHECK ON OR OFFLINE                          
         ICM   RE,15,=V(SSB)                                                    
         BZ    OFFLINE                                                          
         ICM   RF,3,0(RE)                                                       
         BZ    OFFLINE                                                          
         MVI   ONOROFF,ON                                                       
         B     ONLINE                                                           
*                                                                               
OFFLINE  L     RE,=V(UTL)                                                       
         MVC   SAVESYS,4(RE)                                                    
         MVI   4(RE),X'0A'         SWITCH TO CNTL                               
         L     RE,=V(MASTC)                                                     
         CLI   MCAGCTRY-MASTD(RE),7  LEAVE CTRYUK FLAG FOR NED                  
         BE    *+16                                                             
         CLI   MCAGCTRY-MASTD(RE),2  CTRY 0-1 ARE UK                            
         BL    *+8                                                              
         MVI   CTRYUK,C'N'                                                      
         MVC   AGCTRY,MCAGCTRY-MASTD(RE)                                        
         MVC   AGYCUR,MCAGCURR-MASTD(RE)                                        
         B     BLD0                                                             
*                                                                               
ONLINE   GOTO1 CSWITCH,DMCB,X'FFFFFFFF',0  GET (TCB) (UTL)                      
         L     RE,0(R1)            RE=A(UTL)                                    
         MVC   SAVESYS,4(RE)                                                    
         CLI   TAGCTRY-UTLD(RE),7  LEAVE CTRYUK FLAG FOR NED                    
         BE    *+16                                                             
         CLI   TAGCTRY-UTLD(RE),2  CTRY 0-1 ARE UK                              
         BL    *+8                                                              
         MVI   CTRYUK,C'N'                                                      
         MVC   AGYCUR,TAGCURR-UTLD(RE)                                          
         MVC   AGCTRY,TAGCTRY-UTLD(RE)                                          
*                                                                               
         GOTO1 CSWITCH,DMCB,X'0AFFFFFF',0  SWITCH TO CNTL                       
         CLI   4(R1),0                                                          
         BE    BLD0                                                             
         CLI   4(R1),2             SWITCH TO NO-OP SYS                          
         BE    *+6                                                              
         DC    H'0'                INVALID SYSTEM                               
         MVI   DMERROR,X'80'                                                    
         B     BLDERR              FAKE E-O-F IF SYSTEM NOT UP                  
         EJECT                                                                  
*                                  BUILD FIRST KEY                              
BLD0     LA    R3,KEY                                                           
         USING GCURD,R3            R3=A(KEY)                                    
         XC    GCKEY,GCKEY                                                      
         MVI   GCKREC,GCKRECQ                                                   
         MVC   GCKCURU,CURCODE                                                  
         MVC   CURSAVE,GCKEY                                                    
*                                                                               
         GOTO1 CDATAMGR,DMCB,(0,READHI),GENDIR,GCKEY,GCKEY                      
         MVC   DMERROR,8(R1)                                                    
         CLI   DMERROR,0           TEST FOR DATAMGR ERRORS                      
         BNE   BLDERR                                                           
         CLC   GCKEY(GCKCURR-GCKEY),CURSAVE                                     
         BE    *+12                                                             
         MVI   DMERROR,X'80'       SIMULATE E-O-F IF NO RECORD FOUND            
         B     BLDERR                                                           
         OC    CURCODE,CURCODE                                                  
         BZ    BLD2                                                             
*                                  SPECIAL CODE FOR SPECIFIC CALL               
         CLC   GCKEY(GCKCURX-GCKEY),CURSAVE                                     
         BE    BLD2                                                             
         MVI   DMERROR,X'10'       SIMULATE NOT FOUND                           
         B     BLDERR                                                           
*                                  READ GENFILE RECORD                          
BLD2     MVC   DA,GCDDA                                                         
         GOTO1 CDATAMGR,DMCB,(0,GETREC),GENFILE,DA,IO,DMWORK                    
         MVC   DMERROR,8(R1)                                                    
         CLI   DMERROR,0                                                        
         BNE   BLDERR                                                           
         LA    R3,IO               R3=A(CURRENCY RECORD)                        
*                                  BUILD A CURRENCY TABLE ENTRY                 
         XC    CURTABD(CURTABL),CURTABD                                         
         MVC   CURTCUR,GCKCURU                                                  
         MVC   CURTDECP,GCRDECP                                                 
         MVC   CURTPIND,GCRPFLN                                                 
         TM    GCRPFSP,X'01'       TEST CURPFIX IS A SUFFIX                     
         BO    *+12                                                             
         OI    CURTPIND,CURTPFIX   SET PREFIX/SUFFIX INDICATORS                 
         B     *+8                                                              
         OI    CURTPIND,CURTSFIX                                                
         MVC   CURTSYMB,GCRPFIX    SET PREFIX/SUFFIX SYMBOL                     
         CLI   CTRYUK,C'Y'                                                      
         BE    *+14                NON UK AGYS USE ISO CODES                    
         MVC   CURTSYMB,GCKCURU                                                 
         OI    CURTPIND,X'03'      ISO CODES ARE ALWAYS 3 CHR                   
*                                                                               
         CLC   AGYCUR,GCKCURU      TEST LOCAL CURRENCY                          
         BNE   BLD2B                                                            
         LA    R1,LOCALTAB                                                      
BLD2A    CLC   GCKCURU,0(R1)       FIND LOCAL ENTRY                             
         BE    *+16                                                             
         LA    R1,6(R1)                                                         
         CLI   0(R1),X'FF'         OR END OF TABLE                              
         BE    BLD3                                                             
         BNE   BLD2A                                                            
         MVC   CURTSYMB,3(R1)      SET LOCAL SYMBOL                             
         NI    CURTPIND,255-3                                                   
         CLI   5(R1),C' '                                                       
         BE    *+8                                                              
         OI    CURTPIND,X'03'      RESET LENGTH                                 
         CLI   4(R1),C' '                                                       
         BE    *+12                                                             
         OI    CURTPIND,X'02'                                                   
         B     *+8                                                              
         OI    CURTPIND,X'01'                                                   
*                                                                               
BLD2B    CLC   CURTSYMB,=C'EUR'    IS THIS THE EURO                             
         BNE   BLD3                                                             
         LA    R1,EUROTAB          IS IT A MEMBER COUNTRY                       
BLD2C    CLI   0(R1),X'FF'                                                      
         BE    BLD3                EOT                                          
         CLC   0(1,R1),AGCTRY                                                   
         BE    *+12                                                             
         LA    R1,1(R1)                                                         
         B     BLD2C                                                            
*                                                                               
         MVC   CURTSYMB,=C'E  '    SET TO E FOR EURO MEMBERS                    
         NI    CURTPIND,255-3                                                   
         OI    CURTPIND,X'01'                                                   
*                                  SET SHORT NAME                               
BLD3     LA    R1,CURTOTHR         POINT TO OTHER PORTION                       
         TM    4(RA),CURTSNAM                                                   
         BZ    BLD4                                                             
         MVC   0(L'CURTSHRT,R1),GCKCURR                                         
         OI    CURTPIND,CURTSNAM                                                
         LA    R1,L'CURTSHRT(R1)                                                
*                                  SET LONG CURRENCY NAME                       
BLD4     TM    4(RA),CURTLNAM                                                   
         BZ    BLD6                                                             
         MVI   0(R1),C' '          CLEAR LONG NAME TO SPACES                    
         MVC   1(L'CURTLNAM-1,R1),0(R1)                                         
         OI    CURTPIND,CURTLNAM                                                
         ZIC   RE,GCRLEN                                                        
         LA    RF,GCRLENQ+1                                                     
         SR    RE,RF                                                            
         BM    BLD4                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R1),GCRNAME                                                  
*                                                                               
BLD6     LA    R1,CURTABL                                                       
         TM    CURTPIND,CURTSNAM                                                
         BZ    *+8                                                              
         LA    R1,L'CURTSHRT(R1)                                                
         TM    CURTPIND,CURTLNAM                                                
         BZ    *+8                                                              
         LA    R1,L'CURTLONG(R1)                                                
         AR    R2,R1               R2=A(NEXT TABLE ENTRY)                       
         OC    CURCODE,CURCODE     EXIT IF SINGLE CURRENCY REQUEST              
         BZ    BLD8                RETURNING A(CURRENCY RECORD) IN P1           
         ST    R3,0(RA)                                                         
         B     BLDX                                                             
*                                  GET NEXT DIRECTORY RECORD                    
BLD8     GOTO1 CDATAMGR,DMCB,(0,READSQ),GENDIR,KEY,KEY                          
         MVC   DMERROR,8(R1)                                                    
         TM    DMERROR,X'7F'       TEST ERRORS                                  
         BNZ   BLDERR                                                           
         TM    DMERROR,X'80'       TEST E-O-F'S                                 
         BNZ   BLDX                                                             
         LA    R3,KEY              RESET KEY POINTER                            
         CLC   GCKEY(GCKCURU-GCKEY),CURSAVE                                     
         BE    BLD2                                                             
         B     BLDX                                                             
*                                  RETURN DATAMGR ERROR TO CALLER               
BLDERR   MVC   0(1,RA),DMERROR                                                  
*                                                                               
BLDX     OC    CURCODE,CURCODE                                                  
         BNZ   *+8                                                              
         MVI   CURTABD,0           SET CURRENCY TABLE TERMINATOR                
*                                                                               
         CLI   ONOROFF,ON                                                       
         BE    EXIT2                                                            
*                                                                               
EXIT1    L     RE,=V(UTL)          SWITCH BACK OFFLINE                          
         MVC   4(1,RE),SAVESYS                                                  
         B     EXIT                                                             
*                                                                               
EXIT2    MVC   DMCB(4),=X'FFFFFFFF'                                             
         MVC   DMCB(1),SAVESYS                                                  
         GOTO1 CSWITCH,DMCB,,0     SWITCH BACK TO SAVESYS                       
         CLI   4(R1),0                                                          
         BE    EXIT                                                             
         CLI   4(R1),2             SYSTEM GONE NO-OP                            
         BE    EXIT                                                             
         DC    H'0'                CAN'T SWITCH BACK                            
*                                                                               
EXIT     XIT1                                                                   
         EJECT                                                                  
         LTORG                                                                  
         SPACE 1                                                                
READHI   DC    C'DMRDHI  '                                                      
READSQ   DC    C'DMRSEQ  '                                                      
GETREC   DC    C'GETREC  '                                                      
GENDIR   DC    C'GENDIR  '                                                      
GENFILE  DC    C'GENFILE '                                                      
ON       EQU   X'00'               ONLINE                                       
OFF      EQU   X'FF'               OFFLINE                                      
         SPACE 1                                                                
LOCALTAB DS    0CL6                LOCAL OVERIDES                               
         DC    C'GBP',C'$  '                                                    
         DC    C'NLG',C'F. '                                                    
         DC    C'DEM',C'DM '                                                    
         DC    X'FF'                                                            
         SPACE 1                                                                
EUROTAB  DC    AL1(3,4,5,6,7,9)    EMU MEMBERS                                  
         DC    X'FF'                                                            
         EJECT                                                                  
WORKD    DSECT                                                                  
DMCB     DS    6F                                                               
CURCODE  DS    XL3                                                              
CURSAVE  DS    XL16                                                             
DMERROR  DS    X                                                                
ONOROFF  DS    X                                                                
SAVESYS  DS    X                                                                
AGCTRY   DS    X                                                                
CTRYUK   DS    X                                                                
AGYCUR   DS    CL3                                                              
DA       DS    XL4                                                              
DMWORK   DS    CL96                                                             
KEY      DS    CL64                                                             
IO       DS    CL256                                                            
WORKX    EQU   *                                                                
         SPACE 1                                                                
* DDCURTABD                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCURTABD                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* GEGENCUR                                                                      
         PRINT OFF                                                              
       ++INCLUDE GEGENCUR                                                       
         PRINT ON                                                               
         SPACE 1                                                                
* DDCOMFACS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
* DDMASTD                                                                       
         PRINT OFF                                                              
       ++INCLUDE DDMASTD                                                        
         PRINT ON                                                               
* FAUTL                                                                         
         PRINT OFF                                                              
       ++INCLUDE FAUTL                                                          
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'015DDBLDCUR  04/19/01'                                      
         END                                                                    
