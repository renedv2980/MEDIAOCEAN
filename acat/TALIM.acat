*          DATA SET TALIM      AT LEVEL 123 AS OF 07/27/15                      
*CATALP TALIM                                                                   
*                                                                               
*        P1 - A(TALIM PARAMETER BLOCK)                                          
*                                                                               
         TITLE 'TALIM - CALCULATE TAXES AT ALL LEVELS'                          
TALIM    CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,TALIM,R7                                                       
         L     RA,0(R1)            TALIM INTERFACE BLOCK                        
         USING TMDD,RA                                                          
         L     RC,TMRC             GENCON                                       
         USING GEND,RC                                                          
         L     R9,ASTARTSV         A(PROGRAM SAVED STORAGE)                     
         USING SYSWORKD,R9                                                      
         L     R8,ASPOOLD          A(SPOOL)                                     
         USING SPOOLD,R8                                                        
         EJECT                                                                  
*              MAIN CONTROL                                                     
         SPACE 1                                                                
         TM    TMSTAT3,TMSCRBR     CREDITING WITH SAVED BILLING RATES           
         BZ    M05                                                              
         GOTO1 DATCON,DMCB,(0,TMEFDTE0),(20,DATE20)                             
         GOTO1 DATCON,DMCB,(0,TMEFDTE0),(1,EFDATE1)                             
         BAS   RE,BLDLIM           GET MAX WAGES                                
         MVI   FIRST,C'Y'                                                       
         B     M10                                                              
*                                                                               
M05      CLI   FIRST,C'Y'          IF FIRST TIME IN                             
         BNE   M10                                                              
         GOTO1 DATCON,DMCB,(0,TMEFDTE0),(20,DATE20)                             
         GOTO1 DATCON,DMCB,(0,TMEFDTE0),(1,EFDATE1)                             
         BAS   RE,BLDLIM           GET MAX WAGES                                
         MVI   FIRST,C'N'                                                       
*                                                                               
M10      BRAS  RE,SETUNIT          FOR EVENTS, NEED TO SET UNIT                 
         BAS   RE,PROCESS          PROCESS                                      
         BE    YES                                                              
         B     NO                                                               
         EJECT                                                                  
*                                                                               
*        PROCESS THE INFO                                                       
*                                                                               
         SPACE 1                                                                
PROCESS  NTR1                                                                   
         TM    TMSTAT,TMSOHAND     IF THERE IS A HANDLING OVERRIDE              
         BZ    *+10                FOR TYPE 1 OR 2                              
         MVC   MYHAND,TMOHAND      SAVE PASSED HANDLING AMOUNT                  
*                                                                               
         XC    TMRTBLK1(TMRTLNQ1),TMRTBLK1   CLEAR RETURN VALUES                
         XC    TMRTBLK3(TMRTLNQ3),TMRTBLK3                                      
         XC    TMRTBLK4(TMRTLNQ4),TMRTBLK4                                      
         MVI   RATESTAT,0                          STAUS BYTE                   
         TM    TMSTAT,TMSPAMT      IF USER PASSED TAXABLE AMOUNTS               
         BO    *+10                   DON'T CLEAR THEM OUT                      
         XC    TMRTBLK2(TMRTLNQ2),TMRTBLK2                                      
         XC    AMOUNTS(AMTSLNQ),AMOUNTS            AMOUNTS                      
         MVC   GSTRATE,TGGSTRAT    SET GST RATE                                 
         TM    TMSTAT2,TMSGSTO     OR IF STATUS SET, USE OLD GST RATE           
         BNO   PROC3                                                            
         MVC   GSTRATE,=AL4(GSTOLDRT)   SET OLD GST RATE                        
*                                                                               
PROC3    L     R2,TMACURRA                                                      
         LTR   R2,R2               IF BILLING RATES ELEMENT                     
         JZ    PROC4               IS POPULATED                                 
*        L     R0,=A(3000)         MAKE A COPY OF IT FOR SAKE                   
*        GETMAIN R,LV=(0)          OF ADJUSTMENTS                               
         L     R1,TMACPYRA                                                      
         MVC   0(TARALNQ,R1),0(R2)                                              
*        ST    R1,TMACURRA                                                      
         USING TARAD,R1                                                         
         TM    TMSTAT3,TMSNOWC     IF NOT CALCULATING WC,                       
         BZ    *+10                                                             
         XC    TARATWC,TARATWC     CLEAR IT OUT                                 
*                                                                               
         CLI   TARAEL,TARAELQ      CHECK FOR BILLING RATES                      
         BNE   PROC4                                                            
         OI    RATESTAT,RTSTBRAT   SET BRATE STATUS                             
*                                                                               
         TM    TARASTA1,TARASBFI   WAGE BREAK AT FICA LEVEL?                    
         BNO   PROC3A                                                           
         OI    RATESTAT,RTSTFICA   SET FICA WAGE BREAK STATUS                   
*                                                                               
PROC3A   TM    TARASTA1,TARASBNL   NO WAGE BREAK?                               
         BNO   PROC4                                                            
         OI    RATESTAT,RTSTNOLM   SET NO WAGE BREAK LIMIT STATUS               
         DROP  R1                                                               
*                                                                               
PROC4    MVC   MYEMP,TMEMP                                                      
*                                                                               
         CLC   TMEMP,TGTPEMP       IF EMPLOYER NOT TALENT PARTNERS              
         BE    PROC5                                                            
         CLC   TMEMP,=C'PP '       OR PP                                        
         BE    PROC5                                                            
         OI    TMSTAT,TMSNSUR      NO SURCHARGE (MAY BE SET BY CALLER)          
*                                                                               
PROC5    BAS   RE,SETBSRT          SET WAGE BASES & RATES                       
*                                                                               
         TM    TMSTAT,TMSBSRT      ONLY GET WAGE BASE/RATES                     
         BO    YES                                                              
         TM    TMSTAT,TMSPAMT      IF USER PASSED TAXABLE AMOUNTS               
         BO    *+8                    CONTINUE                                  
         BAS   RE,GETTAXBL         ELSE FIND AMT TAXABLE AT EACH LEVEL          
*                                                                               
         TM    TMSTAT,TMSNTNH      IF USER DOESN'T WANT T&H CALCULATED          
         BO    *+8                    CONTINUE                                  
         BAS   RE,CALCTAX          CALCULATE AMT TAXABLE * RATE                 
*                                                                               
         BAS   RE,SETNYTD          SET NEW YTD IN RETURN BLOCK                  
         B     YES                                                              
         EJECT                                                                  
*        SET WAGE BASES & RATES                                                 
SETBSRT  NTR1                                                                   
         MVC   TMBFUI,FUIMAX       NEED TO SET ON EACH ENTRY TO TALIM           
         MVC   TMBFICA,FICMAX                                                   
         MVC   TMBMED,MEDMAX                                                    
         MVC   TMRFUI,FUIRATE                                                   
         MVC   TMRFICA,FICRATE                                                  
         MVC   TMRMED,MEDRATE                                                   
*                                                                               
         TM    TMSTAT2,TMSPCAN     P+ CANADIAN TAXES?                           
         BZ    SS10                                                             
         MVC   TMBCPP,CPPMAX                                                    
         MVC   TMBEI,EIMAX                                                      
         MVC   TMBQPIP,QPIPMAX                                                  
         MVC   TMRCPP,CPPRATE                                                   
         MVC   TMREI,EIRATE                                                     
         MVC   TMRQPIP,QPIPRATE                                                 
*                                                                               
SS10     LA    R4,LIMTAB           R4=A(SUI/SDI WAGE BASE TABLE)                
         USING LIMD,R4                                                          
*                                                                               
SS30     CLI   0(R4),X'FF'         IF END OF TABLE - GET GLOBAL EMP             
         BNE   SS40                                                             
         CLC   MYEMP,SPACES                                                     
         BNH   SSX                 IF NOT REQUESTING EMP OVERRIDE               
         MVC   MYEMP,SPACES        & REACHED END OF TABLE                       
         B     SS10                ELSE CLEAR OVERRIDE & START OVER             
*                                                                               
SS40     CLC   TMUNIT,LMUNIT       MATCH ON STATE CODE                          
         BNE   SS80                                                             
         CLC   LMEMP,MYEMP         MATCH ON EMPLOYER                            
         BNE   SS80                                                             
         TM    TMBTYPE,LMTYSUIW    IF SUI WAGE BASE NOT YET SET                 
         BO    SS50                                                             
         TM    LMTYPE,LMTYSUIW     IF SUI WAS ENTERED                           
         BNO   SS50                                                             
         MVC   TMBSUI,LMSUIW       SET WGBS                                     
         OI    TMBTYPE,LMTYSUIW                                                 
*                                                                               
SS50     TM    TMBTYPE,LMTYSDIW    IF SDI WAGE BASE NOT YET SET                 
         BO    SS60                                                             
         TM    LMTYPE,LMTYSDIW     & IF SDI WAS ENTERED                         
         BNO   SS60                                                             
         MVC   TMBSDI,LMSDIW       SET WGBS                                     
         OI    TMBTYPE,LMTYSDIW                                                 
*                                                                               
SS60     TM    TMBTYPE,LMTYSUIR    IF SUI RATE NOT YET SET                      
         BO    SS70                                                             
         TM    LMTYPE,LMTYSUIR     & IF SUI RATE WAS ENTERED                    
         BNO   SS70                                                             
         MVC   TMRSUI,LMSUIR       SET SUI RATE                                 
         OI    TMBTYPE,LMTYSUIR                                                 
*                                                                               
SS70     TM    TMBTYPE,LMTYSDIR    IF SDI RATE NOT YET SET                      
         BO    SS80                                                             
         TM    LMTYPE,LMTYSDIR     & IF SDI RATE WAS ENTERED                    
         BNO   SS80                                                             
         MVC   TMRSDI,LMSDIR       SET SDI RATE                                 
         OI    TMBTYPE,LMTYSDIR                                                 
*                                                                               
SS80     LA    R4,LIMNEXT                                                       
         B     SS30                                                             
*                                                                               
SSX      B     XIT                                                              
         EJECT                                                                  
*                                                                               
*        GET EACH LEVEL'S TAXABLE AMOUNT                                        
*                                                                               
GETTAXBL NTR1                                                                   
         TM    TMSTAT2,TMSPCAN     P+ CANADIAN TAXES?                           
         BZ    *+12                                                             
         BRAS  RE,CGETTXBL                                                      
         B     GTX                                                              
*                                                                               
         MVC   FEDYTD(YTDAMTS),TMYEARN    SET YTD AMOUNTS                       
         TM    TMSTATUS,TMSTCRDT   CREDIT INVOICE?                              
         BNO   GT05                                                             
         BRAS  RE,GETCRYTD         GET YTD OF INVOICE BEING CREDITED            
         BE    GT10                                                             
GT05     L     R1,TMYEARN          IF NEW YTD EARNINGS < 0                      
         A     R1,TMTXEARN         THEN TREAT YTD AMOUNTS                       
         BNM   GT10                AS = 0 TO CALCULATE TAXABLE AMOUNTS          
         XC    FEDYTD(YTDAMTS),FEDYTD                                           
*                                                                               
GT10     MVC   REMAIN,TMTXEARN                                                  
         XC    TAXED,TAXED         CLEAR AMOUNT TAXED THIS INVOICE              
*                                                                               
         OC    REMAIN,REMAIN                                                    
         BZ    GT30                                                             
         MVI   TMLEVEL,C'A'                                                     
         TM    REMAIN,X'80'        IF AMOUNT IS NEGATIVE                        
         BNO   *+8                                                              
         BAS   RE,NEGAMT           NEED TO BACK OUT EARNINGS FROM YTD           
         GOTO1 MAXTAX,DMCB,TMBFUI,FUIYTD,FUITAXBL                               
*                                                                               
         OC    REMAIN,REMAIN                                                    
         BZ    GT30                                                             
         MVI   TMLEVEL,C'B'                                                     
         L     R1,SUIYTD                                                        
         TM    RATESTAT,RTSTSUR    IF RATE SURCHARGE                            
         BNO   *+8                                                              
         L     R1,TMYEARN          USE TOTAL YTD EARNINGS                       
         A     R1,TAXED            ADD AMNT ALREADY TAXED AT FUI LEVEL          
         ST    R1,SUIYTD                                                        
         GOTO1 MAXTAX,DMCB,TMBSUI,SUIYTD,SUITAXBL                               
         BAS   RE,ASUITAX                                                       
*                                                                               
         OC    REMAIN,REMAIN                                                    
         BZ    GT30                                                             
         MVI   TMLEVEL,C'C'                                                     
         L     R1,FICYTD                                                        
         A     R1,TAXED            ADD AMNT ALREADY TAXED AT SUI LEVEL          
         ST    R1,FICYTD                                                        
         GOTO1 MAXTAX,DMCB,TMBFICA,FICYTD,FICTAXBL                              
*                                                                               
         OC    REMAIN,REMAIN                                                    
         BZ    GT30                                                             
         MVI   TMLEVEL,C'D'                                                     
         L     R1,MEDYTD                                                        
         A     R1,TAXED            ADD AMT ALREADY TAXED AT PREV LVLS           
         ST    R1,MEDYTD                                                        
         GOTO1 MAXTAX,DMCB,TMBMED,MEDYTD,MEDTAXBL                               
*                                                                               
**NO-OP**OC    REMAIN,REMAIN       NO-OP AS OF 12/2/97                          
*        BZ    GT30                                                             
*        MVI   TMLEVEL,C'E'                                                     
*        L     R2,REMAIN           A * AA = PAYROLL TAXES                       
*        TM    TMTXEARN,X'80'      IF THIS IS A NEGATIVE INVOICE                
*        BNO   *+6                                                              
*        LCR   R2,R2               RESTORE AMT TO (-) FOR CALCULATION           
**NO-OP**ST    R2,OMDTAXBL                                                      
*                                                                               
GT30     TM    RATESTAT,RTSTSUR    IF RATE SURCHARGE - DONE                     
         BO    GT50                                                             
         BRAS  RE,ADJAMT           ADJUST TAXABLE AMOUNTS                       
*                                                                               
GT50     BRAS  RE,SUISURCH         SUI SURCHARGE IF NEEDED                      
GTX      B     XIT                                                              
         EJECT                                                                  
*        FOR EVENTS, NEED TO ADJUST TAXABLE SUI AMOUNT                          
*                                                                               
ASUITAX  NTR1                                                                   
         TM    TMTXEARN,X'80'      IF THIS IS A NEGATIVE INVOICE                
         BZ    AST08                                                            
         BRAS  RE,ASUICXCR         ADJUST SUI FOR CXL OR CREDITS                
         B     XIT                                                              
                                                                                
AST08    CLI   LIMTAB,0            IF LIMIT TABLE IS NOT EMPTY                  
         BE    XIT                                                              
                                                                                
         BRAS  RE,GETCHK           GET CHECK RECORD                             
         BNE   XIT                                                              
                                                                                
         TM    TMSTAT2,TMOLDSUI    GET SUI THE OLD WAY?                         
         BNZ   AST09                                                            
         BRAS  RE,GETTATD          NEW WAY - GET TATD ELEMENTS                  
         B     XIT                                                              
                                                                                
AST09    L     R0,TMYSUI           R0=HIGHEST SUI WAGE BASE ENCOUNTERED         
                                                                                
         TM    TMSTAT2,TMEVTSUI    GOING TO CALC SUI SUR FOR EVENTS             
         BO    XIT                                                              
                                                                                
         USING TATUD,R4                                                         
         L     R4,AIO3                                                          
         MVI   ELCODE,TATUELQ      READ ALL TAX UNIT ELEMENTS                   
         BAS   RE,GETEL                                                         
         B     *+8                                                              
AST10    BAS   RE,NEXTEL                                                        
         BNE   AST50                                                            
         CLC   TATUUNIT,=C'FD '    SKIPPING FEDERAL                             
         BE    AST10                                                            
         CLC   TATUUNIT,=C'CN '    AND CANADIAN                                 
         BE    AST10                                                            
         CLI   TATUUNIT+2,C' '     AND CITY ELEMENTS                            
         BH    AST10                                                            
                                                                                
         USING LIMD,R2                                                          
         LA    R2,LIMTAB                                                        
AST20    CLI   0(R2),X'FF'                                                      
         BE    AST10                                                            
         CLC   LMUNIT,TATUUNIT     FIND STATE IN LIMIT TABLE                    
         BNE   AST30                                                            
         CLC   LMEMP,SPACES                                                     
         BE    AST40                                                            
AST30    LA    R2,LIMNEXT                                                       
         B     AST20                                                            
                                                                                
AST40    BRAS  RE,SETTAXAM         SET TAXABLE AMOUNT FOR STATE IN R3           
                                                                                
         ICM   RE,15,LMSUIW        R3 = THE HIGHER OF ...                       
         CR    R3,RE                    (1) THE STATE'S TAXABLE AMOUNT          
         BNH   *+6                          ON THIS CHECK                       
         LR    R3,RE               OR   (2) STATE'S SUI LIMIT                   
         DROP  R2                                                               
                                                                                
         CR    R0,R3               IF THIS IS THE HIGHEST SUI BASE              
         BNL   AST10               WE'VE ENCOUNTERED, RESET R0                  
         LR    R0,R3                                                            
         B     AST10                                                            
                                                                                
AST50    L     RE,FUITAXBL                                                      
         SR    R0,RE                                                            
         L     RE,TMYSUI                                                        
         SR    R0,RE               R0 = NEW SUI TAXABLE AMOUNT                  
                                                                                
         L     RE,SUITAXBL                                                      
         SR    RE,R0               RE = DIFFERENCE BETWEEN OLD AND NEW          
                                                                                
         ST    R0,SUITAXBL         SAVE NEW SUI TAXABLE YTD AMOUNT              
                                                                                
         L     RF,REMAIN                                                        
         AR    RF,RE                                                            
         ST    RF,REMAIN           AND ADJUST REMAINING AMOUNT                  
         B     XIT                                                              
         EJECT                                                                  
*                                                                               
*        ADJUST YTD AMOUNTS IF CHECK AMOUNT IS NEGATIVE                         
*        NEED TO BACK OUT THIS CHECK AMOUNT FROM YTD                            
*                                                                               
NEGAMT   NTR1                                                                   
         L     R1,FEDYTD           ADD NEGATIVE AMOUNT INTO YTD                 
         A     R1,REMAIN                                                        
         BNM   *+6                 BUT YTD CANNOT BE NEGATIVE                   
         XR    R1,R1                                                            
         ST    R1,FEDYTD           & USE THIS AMOUNT FOR CALCULATING            
         ST    R1,FUIYTD                                                        
*                                                                               
         L     R1,REMAIN           TAX AS IF POSITIVE AMOUNT                    
         LCR   R1,R1                                                            
         ST    R1,REMAIN                                                        
*                                                                               
         LA    R1,SUIYTD                                                        
         BRAS  RE,ADJYTD                                                        
*                                                                               
         LA    R1,FICYTD                                                        
         BRAS  RE,ADJYTD                                                        
*                                                                               
         LA    R1,MEDYTD                                                        
         BRAS  RE,ADJYTD                                                        
         B     XIT                                                              
         SPACE 2                                                                
*        CALCULATE TAXABLE AMOUNT * RATE REQUESTED                              
         SPACE 1                                                                
CALCTAX  NTR1                                                                   
         TM    TMSTAT,TMSBIL       IF BILLING                                   
         BNO   CTX05                                                            
         SPACE 1                                                                
         CLI   TMBILTYP,TABRTY18   IF BILLING TYPE 18                           
         BE    CTXAA                                                            
         CLI   TMBILTYP,TABRTY23   OR BILLING TYPE 23                           
         BNE   CTXAB                                                            
         SPACE 1                                                                
CTXAA    CLI   TMCURR,C'C'         IF THIS IS A CANADIAN $ INVOICE              
         BE    CTXB                SKIP I&R CHECK NOW                           
         SPACE 1                                                                
CTXAB    CLI   TMBILTYP,TABRTY6    IF BILLING TYPE 6                            
         BE    CTXA                                                             
         CLI   TMBILTYP,TABRTY8    OR BILLING TYPE 8                            
         BE    CTXA                                                             
         CLI   TMBILTYP,TABRTY18   OR BILLING TYPE 18                           
         BE    CTXA                                                             
         CLI   TMBILTYP,TABRTY21   OR BILLING TYPE 21                           
         BE    CTXA                                                             
         CLI   TMBILTYP,TABRTY23   OR BILLING TYPE 23                           
         BE    CTXA                                                             
         CLI   TMBILTYP,TABRTY25   OR BILLING TYPE 25                           
         BE    CTXA                                                             
         CLI   TMBILTYP,TABRTY26   OR BILLING TYPE 26                           
         BNE   CTXB                                                             
CTXA     BAS   RE,CHKSSN           DO NOT TAKE HANDLING ON I&R                  
         BNE   CTX70                                                            
         SPACE 1                                                                
CTXB     CLI   TMBILTYP,TABRTY22   IF BILLING TYPE 22                           
         BNE   CTXC                                                             
         BAS   RE,CHKAFM           DO NOT TAKE HANDLING ON H&W                  
         BNE   CTX70               TO AFM FUNDS                                 
         SPACE 1                                                                
CTXC     CLI   TMCURR,C'E'         IF THIS IS A EURO INVOICE                    
         BNE   CTXE                                                             
         BRAS  RE,EUROTAX          HANDLE IT EURO WAY                           
         B     CTX70                                                            
         SPACE 1                                                                
CTXE     CLI   TMCURR,C'C'         IF THIS IS A CANADIAN $ INVOICE              
         BNE   CTX02                                                            
         CLI   TMBILTYP,TABRTY18   IF BILLING TYPE 18                           
         BE    CTXF                                                             
         CLI   TMBILTYP,TABRTY23   OR BILLING TYPE 23                           
         BE    CTXF                TAKE I&R ON UNION CHECK                      
         BAS   RE,CHKSSN           AND NOT UNION CHECK                          
         BNE   CTX70                                                            
CTXF     BAS   RE,CANTAX           HANDLE IT CANADIAN WAY                       
         B     CTX70                                                            
         SPACE 1                                                                
CTX02    CLI   TMBILTYP,TABRTY99   IF BILLLING TYPE 99                          
         BNE   CTX04                                                            
         BAS   RE,CALCGSTU         ONLY TAX POSSIBLY TAKEN IS GST               
         B     CTX70                                                            
*                                                                               
CTX04    BAS   RE,RATEADJ          ADJUST CERTAIN TAX RATES                     
         BRAS  RE,TNHWGBS          TEST BILLING TYPE CALC BASED ON              
         BE    CTX05                    WAGE BASE                               
         BAS   RE,OTHTNH           CALC TNH FOR OTHER TYPES                     
         B     CTX70                                                            
*                                                                               
CTX05    TM    RATESTAT,RTSTNOLM   IF BRATE HAS NO LIMIT                        
         BZ    CTX07                                                            
         L     R6,TMTXEARN         CALCULATE NEW FUTA                           
         GOTOR CALCTAX2,DMCB,('CTAXFUTA',0),(R6),('CTAXBASE',0)                 
         B     CTX19                                                            
                                                                                
CTX07    TM    RATESTAT,RTSTFICA   IF FICA WAGE BREAK,                          
         BZ    CTX08                                                            
         L     R6,TMTXEARN         CALCULATE NEW FUTA                           
         GOTOR CALCTAX2,DMCB,('CTAXFUTA',0),(R6),0   USE FICA LIMIT             
         B     CTX19                                                            
*                                                                               
CTX08    LA    R2,FUITAXBL                                                      
         LA    R3,TMTAXES          AMOUNT * RATE                                
         LA    R4,TMBRATES         BILLING RATES                                
         LA    R5,4                                                             
         TM    TMSTAT,TMSBIL       IF BILLING USE BROKEN DOWN AMOUNTS           
         BO    CTX10                                                            
         LA    R2,TMTFUI           ELSE USE ACCUMULATED AMOUNTS                 
         LA    R4,TMWGRT           USE ALLTAX RATES                             
*                                                                               
CTX10    L     R6,0(R2)            AMOUNT TO TAX                                
*                                                                               
         TM    TMSTAT,TMSBIL       IF NOT BILLING, USE OLD CALCULATION          
         BO    CTX15                                                            
         GOTOR CALCP,DMCB,0(R4),(R6),TMXTOTAL                                   
         MVC   0(4,R3),0(R1)       RATE * AMOUNT                                
         B     CTX18                                                            
*                                                                               
*                                  USE WAGE BASE                                
CTX15    GOTOR CALCTAX2,DMCB,((R5),0),(R6),('CTAXBASE',0)                       
*                                                                               
CTX18    LA    R2,4(R2)            BUMP                                         
         LA    R3,4(R3)                                                         
         LA    R4,4(R4)                                                         
         BCT   R5,CTX10                                                         
*                                                                               
CTX19    TM    TMSTAT,TMSBIL       IF BILLING - DON'T DO SDI                    
         BO    CTX20                                                            
         L     R6,TMTSDI                                                        
         GOTOR CALCP,DMCB,TMRSDI,(R6),TMOSDI                                    
         B     CTX70                                                            
*                                                                               
CTX20    DS    0H                  NO-OP AS OF 12/2/97                          
**NO-OP**L     R6,OMDTAXBL         SET OVER MEDICARE TAXABLE AMOUNT             
*        GOTOR CALCP,DMCB,TMBROMED,(R6),TMXTOTAL                                
**NO-OP**MVC   TMXOMED,0(R1)       AMOUNT TAXED * RATE                          
*                                                                               
         CLI   TMBILTYP,TABRTY14   IF BILLLING TYPE 14                          
         BNE   CTX40                                                            
         TM    TGUSSTAT,SESSION    AND IT IS A SESSION                          
         BZ    CTX40                                                            
         BRAS  RE,CHKOVER          CHECK OVER MAX FOR WCTAX CHARGE              
*                                                                               
CTX40    MVC   WCTRATE,TMBRWCRP                                                 
         BRAS  RE,WCTAX            NON TAXABLE EARNINGS AT WC CORP RATE         
*                                                                               
         L     R6,TMNTEARN         ELSE ALL NON TAXABLE EARNINGS                
                                                                                
         CLI   TMBILTYP,TABRTY13   BILLING TYPE 13                              
         BNE   *+8                                                              
         A     R6,TMADVR           ADD ANY ADVANCED RECONCILIATION              
*                                                                               
         CLI   TMBILTYP,TABRTY24   BILLING TYPE 24                              
         BNE   CTX42                                                            
         TM    TGUSSTAT,SESSION    IF BILL TYPE 24 AND SESSION                  
         BZ    CTX42                                                            
****     GOTOR CALCP,DMCB,TMBRHAND,(R6),TMOCORP      USE HAND RATE              
         GOTOR CALCHAND,DMCB,('CHANDIC',0),(R6),0    USE REG HAND RATE          
         B     CTX44                                                            
*                                                                               
***42    GOTOR CALCP,DMCB,TMBRCORP,(R6),TMOCORP      USE CORP RATE              
CTX42    GOTOR CALCHAND,DMCB,('CHANDCC',0),(R6),0    USE REG HAND RATE          
*                                                    TYPE 24 USES REUSE         
*                                                                               
CTX44    CLI   TGUSEQU,UPNH        PNH TYPE?                                    
         BNE   CTX46                                                            
         CLI   TMBILTYP,TABRTY24   BILLING TYPE 24                              
         BNE   CTX46                                                            
CTX45    L     R6,TMPNH                                                         
         SR    R2,R2               SKIPS APPL GRT                               
         B     CTX48                                                            
*                                                                               
CTX46    L     R6,TMTXEARN         AND CALCULATE HANDLING                       
         GOTO1 =A(HNDRULES),DMCB,(R6)                                           
         L     R6,DMCB                                                          
         L     R2,FULL                                                          
*                                                                               
         CLI   TMBILTYP,TABRTY24   BILLING TYPE 24                              
         BNE   CTX50                                                            
         TM    TGUSSTAT,SESSION    IF BILL TYPE 24 AND SESSION                  
         BO    CTX50                                                            
***48    GOTOR CALCP,DMCB,TMBRCORP,(R6),TMOHAND      USE CORP RATE              
CTX48    GOTOR CALCHAND,DMCB,('CHANDCI',0),(R6),(R2) USE REUSE RATE             
         B     CTX52                                                            
*                                                                               
***50    GOTOR CALCP,DMCB,TMBRHAND,(R6),TMOHAND      USE HAND RATE              
CTX50    GOTOR CALCHAND,DMCB,('CHANDII',0),(R6),(R2) USE REG HAND RATE          
*                                                                               
CTX52    BAS   RE,CALCGSTU         AND CALCULATE US$ GST                        
                                                                                
CTX70    BAS   RE,CALCEXTX         CALCULATE EXTRA TAX IF NECESSARY             
                                                                                
CTX90    B     XIT                                                              
         EJECT                                                                  
*        ROUTINE TO CALC EXTRA TAXES IF NECESSARY                               
         SPACE 1                                                                
CALCEXTX NTR1                                                                   
         XC    XRATE,XRATE                                                      
         XC    XTAXTOT,XTAXTOT                                                  
         TM    TMSTAT2,TMSOTAX     IF TAX AMOUNT OVERRIDE                       
         BO    XIT                 YES, LEAVE                                   
                                                                                
         TM    TMSTAT2,TMEVTSUI    GOING TO CALC SUI SUR FOR EVENTS             
         BO    XIT                                                              
                                                                                
*&&DO                                                                           
         OC    TMACHK,TMACHK       IF WE HAVE A(CHECK RECORD)                   
         BZ    CXTX40              MUST BE P+                                   
                                                                                
         MVC   SYSFIL,=CL8'CHKFIL'                                              
         MVC   SYSDIR,=CL8'CHKDIR'                                              
         MVC   TGFULL,AIO                                                       
         MVC   AIO,AIO3                                                         
                                                                                
         USING TLDRD,R3                                                         
         LA    R3,KEY                                                           
         MVC   TLDRDA,TMACHK       READ CHECK RECORD                            
         GOTO1 GETREC                                                           
         DROP  R3                                                               
                                                                                
         MVC   SYSFIL,=CL8'TALFIL'                                              
         MVC   SYSDIR,=CL8'TALDIR'                                              
         MVC   AIO,TGFULL                                                       
                                                                                
         USING TATUD,R4                                                         
         L     R4,AIO3                                                          
         MVI   ELCODE,TATUELQ      READ ALL TAX UNIT ELEMENTS                   
         BAS   RE,GETEL                                                         
         B     *+8                                                              
CXTX10   BAS   RE,NEXTEL                                                        
         BNE   XIT                                                              
         DROP  R4                                                               
                                                                                
         USING LIMD,R2                                                          
         LA    R2,EXTAXTAB                                                      
CXTX20   CLI   0(R2),X'FF'                                                      
         BE    CXTX10                                                           
         CLC   TATUUNIT,0(R2)      FIND STATE IN EXTRA TAX TABLE                
         BE    CXTX25                                                           
         AHI   R2,4                                                             
         B     CXTX20                                                           
CXTX25                                                                          
*&&                                                                             
                                                                                
         USING EXTXTBD,RF                                                       
CXTX40   LA    RF,EXTAXTAB                                                      
CXTX45   CLI   0(RF),X'FF'         EOT                                          
         BE    XIT                                                              
         CLC   TMWUNIT(2),EXTXUNIT MATCH UNIT                                   
         BNE   CXTX48                                                           
         CLC   EXTXEFST,EFDATE1    FIND EFFECTIVE DATE RANGE                    
         BH    CXTX48                                                           
         CLC   EXTXEFED,EFDATE1                                                 
         BNL   CXTX50                                                           
CXTX48   AHI   RF,EXTXLNQ                                                       
         B     CXTX45                                                           
                                                                                
CXTX50   XR    RE,RE               SAVE RATE AROUND                             
         ICM   RE,3,EXTXRATE                                                    
         ST    RE,XRATE                                                         
                                                                                
         CLI   TMEMSFLG,3          CALCULATED ONCE BEFORE                       
         BNL   CXTX90              DON'T NEED IT STORED TWICE                   
                                                                                
         CLI   TMOFCDE,C'O'        IF OFFICE O                                  
         BE    CXTX55                                                           
         CLI   TMOFCDE,C'Q'        OR OFFICE Q                                  
         BNE   CXTX58                                                           
CXTX55   CLI   TMEMSFLG,1          AND CHLOE CALCULATION PASS                   
         BE    CXTX90              DON'T NEED IT STORED TWICE                   
                                                                                
         USING TXTD,RF                                                          
CXTX58   L     RF,TMXTDSCT                                                      
         LTR   RF,RF               THIS HAS TO BE SET                           
         BZ    XIT                                                              
         USING TXTABD,R5                                                        
         LA    R5,TXTXTTAB                                                      
CXTX60   CLC   TXTABUN,=X'0000'                                                 
         BE    CXTX70                                                           
         CLC   TXTABUN,TMWUNIT                                                  
         BE    CXTX80                                                           
         AHI   R5,TXTABLNQ                                                      
         B     CXTX60                                                           
                                                                                
CXTX70   MVC   TXTABUN,TMWUNIT                                                  
CXTX80   DS    0H                                                               
                                                                                
CXTX90   L     R2,TMNTEARN         EARNINGS                                     
         A     R2,TMTXEARN                                                      
         A     R2,TMPNH            + PNH                                        
         A     R2,TMHNW            + HNW                                        
******** A     R2,TMMDED           + MISC DED                                   
         A     R2,TMXTOTAL         + TAXES                                      
                                                                                
**NO-OP  CLI   TMBILTYP,TABRTY13   BILLING TYPE 13                              
**04/13  BE    CXTX95                                                           
**       CLI   TMBILTYP,TABRTY1    OR BILLING TYPE 1                            
**       BNE   *+8                                                              
**TX95   A     R2,TMADVR           ADD ANY ADVANCED RECONCILIATION              
                                                                                
         MVC   XSTAT,TMSTAT                                                     
         NI    TMSTAT,X'FF'-TMSBIL    CALCP WITH 5 DECIMAL PLACES               
                                                                                
         GOTOR CALCP,DMCB,XRATE,(R2),XTAXTOT                                    
         MVC   TMSTAT,XSTAT        RESTORE TMSTAT                               
                                                                                
         CLI   TMEMSFLG,3          CALCULATED ONCE BEFORE                       
         BNL   CXTX100             DON'T NEED IT STORED TWICE                   
                                                                                
         CLI   TMOFCDE,C'O'        IF OFFICE O                                  
         BE    CXTX94                                                           
         CLI   TMOFCDE,C'Q'        OR OFFICE Q                                  
         BNE   CXTX98                                                           
CXTX94   CLI   TMEMSFLG,1          AND CHLOE CALCULATION PASS                   
         BE    CXTX100             DON'T NEED IT STORED TWICE                   
                                                                                
CXTX98   ICM   RF,15,TXTABTAX      ACCUMULATE                                   
         A     RF,XTAXTOT                                                       
         STCM  RF,15,TXTABTAX                                                   
                                                                                
CXTX100  L     RF,TMXTOTAL         ACCUMULATE                                   
         L     RE,XTAXTOT                                                       
         AR    RF,RE                                                            
         ST    RF,TMXTOTAL                                                      
                                                                                
         B     XIT                                                              
                                                                                
XSTAT    DS    X                                                                
XRATE    DS    F                                                                
XTAXTOT  DS    F                                                                
                                                                                
         LTORG                                                                  
         EJECT                                                                  
*        ROUTINE TO TAKE TAXES FOR CANADIAN PAYMENTS                            
         SPACE 1                                                                
CANTAX   NTR1                                                                   
         L     R2,TMNTEARN         ELSE CALCULATE CAN HANDLING FEE ON           
         A     R2,TMTXEARN         ENTIRE AMOUNT                                
         CLI   TMBILTYP,TABRTY99   BILLING TYPE 99 - NO HANDLING                
         BE    CANTTX60                                                         
*                                                                               
         CLI   TMBILTYP,TABRTY24   BILLING TYPE 24                              
         BNE   CANTTX20                                                         
         TM    TGUSSTAT,SESSION    IF BILL TYPE 24 AND SESSION                  
         BO    CANTTX10                                                         
         GOTOR CALCHAND,DMCB,('CHANDCA',0),(R2),0   USE CORP RATE               
         B     CANTTX60                                                         
CANTTX10 GOTOR CALCHAND,DMCB,('CHANDIA',0),(R2),0   USE IND RATE                
         B     CANTTX60                                                         
*                                                                               
*                                                                               
***NO-OP GOTOR CALCP,DMCB,TMBRCORP,(R2),TMOCAN                                  
***      B     CANTTX60                                                         
***TTX10 GOTOR CALCP,DMCB,TMBRHAND,(R2),TMOCAN                                  
***2012  B     CANTTX60                                                         
*                                                                               
***TTX20 GOTOR CALCP,DMCB,TMBRCAN,(R2),TMOCAN                                   
CANTTX20 GOTOR CALCHAND,DMCB,('CHANDAA',0),(R2),0   USE CAN RATE                
         CLI   TMBILTYP,TABRTY18   BILLING TYPE 18                              
         BE    CANTTX40                                                         
         CLI   TMBILTYP,TABRTY23   OR BILLING TYPE 23                           
         BNE   CANTTX60                                                         
CANTTX40 BAS   RE,CHKSSN           IF I&R, JUST NEED HANDLING                   
         BNE   XIT                                                              
*                                                                               
CANTTX60 TM    TMSTAT2,TMSGSTU     AND TAKE GST IS REQUESTED                    
         BZ    XIT                                                              
         CLI   TGUSEQU,UPEN        AND USE IS NOT PENALTY                       
         BE    XIT                                                              
         A     R2,TMINR            ADD TO WAGES + MISC, INR                     
*        A     R2,TMOCAN           AND HANDLING                                 
         A     R2,TMCNTR           + TAXABLE REIMB. (P+)                        
         S     R2,TMREXP                                                        
         GOTOR CALCP,DMCB,GSTRATE,(R2),TMOGST                                   
         OC    TGPSTRAT,TGPSTRAT                                                
         BZ    CANTTX90                                                         
* QUEBEC - NO LONGER TAXING PST ON GST (GHOA: 3/22/2013)                        
*        TM    TGPRVST,TGPSTQUE       USE QUEBEC CALCULATION?                   
*        BZ    CANTTX80                                                         
*        A     R2,TMOGST                                                        
CANTTX80 GOTOR CALCP,DMCB,TGPSTRAT,(R2),TMOPST                                  
CANTTX90 B     XIT                                                              
         SPACE 2                                                                
*        ROUTINE TO TAKE GST ON US$ PAYMENTS IF NECESSSARY                      
         SPACE 1                                                                
CALCGSTU NTR1                                                                   
         BAS   RE,CHKSSN           IF NOT UNION CHECK                           
         BNE   XIT                                                              
         TM    TMSTAT2,TMSGSTU     AND TAKE GST IS REQUESTED                    
         BZ    XIT                                                              
         CLI   TGUSEQU,UPEN        AND USE IS NOT PENALTY                       
         BE    XIT                                                              
         L     R2,TMNTEARN         CALCULATE GST ON WAGES                       
         A     R2,TMTXEARN                                                      
         A     R2,TMINR            + INR                                        
         A     R2,TMCNTR           + TAXABLE REIMB. (P+)                        
         S     R2,TMREXP                                                        
         GOTOR CALCP,DMCB,GSTRATE,(R2),TMOGSTU                                  
         OC    TGPSTRAT,TGPSTRAT                                                
         BZ    XIT                                                              
* QUEBEC - NO LONGER TAXING PST ON GST (GHOA: 3/22/2013)                        
*        TM    TGPRVST,TGPSTQUE       USE QUEBEC CALCULATION?                   
*        BZ    CALCGST5                                                         
*        A     R2,TMOGSTU                                                       
CALCGST5 GOTOR CALCP,DMCB,TGPSTRAT,(R2),TMOPSTU                                 
         B     XIT                                                              
         EJECT                                                                  
*        CHECK IF SSN EXCLUDED FROM HANDLING OR GST CALC                        
*        THESE SSNS ARE I&R OR ACTRAWORKS CHECKS                                
*                                  XIT - CC EQU OKAY TO TAKE TAXES              
         SPACE 1                                                                
CHKSSN   NTR1                                                                   
         LA    R4,CANXTAB          R4=A(SSN TABLE)                              
*                                                                               
CHKSSN5  CLI   0(R4),X'FF'         END OF TABLE                                 
         BE    YES                                                              
         CLC   TMSSN,0(R4)         IF I&R OR ACTRAWRKS ACCT - DONE              
         BE    NO                                                               
         LA    R4,L'CANXTAB(R4)    BUMP TABLE                                   
         B     CHKSSN5                                                          
         EJECT                                                                  
*        CHECK IF SSN IS RECEIVING AFM H&W                                      
         SPACE 1                                                                
CHKAFM   NTR1                                                                   
         LA    R4,AFMTAB                                                        
CHKAFM10 CLI   0(R4),X'FF'                                                      
         BE    YES                                                              
         CLC   TMSSN,0(R4)                                                      
         BE    NO                                                               
         LA    R4,L'AFMTAB(R4)                                                  
         B     CHKAFM10                                                         
         EJECT                                                                  
*        ADJUST RATES FOR TAX & HANDLING                                        
         SPACE 1                                                                
RATEADJ  NTR1                                                                   
         LA    R2,RATETAB          SPECIAL RATE ADDITION TABLE                  
         USING RATETABD,R2                                                      
*                                                                               
RADJ10   CLI   0(R2),X'FF'         END OF TABLE                                 
         BE    RADJ40                                                           
         CLC   TMUNIT,RATUNIT      FOUND A STATE WITH SPECIAL RATE              
         BNE   RADJ15              ADDITION                                     
         CLI   RBILTYP,0           IF THIS IS FOR DEFAULT BILLING TYPES         
         BE    RADJ20                                                           
         CLC   RBILTYP,TMBILTYP    OR FOUND CORRECT BILLING TYPE                
         BE    RADJ20              CONTINUE                                     
*                                                                               
RADJ15   LA    R2,RATLNQ(R2)       ELSE BUMP TO NEXT STATE                      
         B     RADJ10                                                           
*                                                                               
RADJ20   CLI   TMBILTYP,TABRTY23   BILL TYPE 23, DON'T ADJUST HANDLING          
         BE    RADJ28                                                           
         L     R1,TMBRCORP         ADD SPECIAL RATE ADDITION TO REGULAR         
         AH    R1,RCRPRATE         CORPORATE RATE                               
         ST    R1,TMBRCORP                                                      
         L     R1,TMBRHAND                                                      
         AH    R1,RHNDRATE         AND HANDLING                                 
         ST    R1,TMBRHAND                                                      
*                                                                               
         CLI   TMBILTYP,TABRTY21   BILL TYPE 21 ADJUST SUTA                     
         BNE   RADJ28                                                           
         TM    TGUSSTAT,SESSION    IF SESSION PAYMENT,                          
         BNO   RADJ28              USE SUTA FIELD FOR HANDLING RATE             
         L     R1,TMBRSUI                                                       
         AH    R1,RHNDRATE                                                      
         ST    R1,TMBRSUI                                                       
*                                                                               
RADJ28   CLI   TMBILTYP,TABRTY1    BILL TYPES 1,2,16,20 USE FUTA FIELD          
         BE    RADJ30                                                           
         CLI   TMBILTYP,TABRTY2    ONLY                                         
         BE    RADJ30                                                           
         CLI   TMBILTYP,TABRTY16   ONLY                                         
         BE    RADJ30                                                           
         CLI   TMBILTYP,TABRTY20   ONLY                                         
         BE    RADJ30                                                           
         CLI   TMBILTYP,TABRTY23   ONLY                                         
         BNE   RADJ40                                                           
*                                                                               
RADJ30   L     R1,TMBRFUI                                                       
         AH    R1,RFUIRATE                                                      
         ST    R1,TMBRFUI                                                       
*                                                                               
         CLI   TMBILTYP,TABRTY23   BILLING TYPE 23                              
         BNE   RADJ40                                                           
         L     R1,TMBRWCRP         ALSO ADD TO WC RATE                          
         AH    R1,RFUIRATE                                                      
         ST    R1,TMBRWCRP                                                      
*                                                                               
RADJ40   TM    TMSTAT,TMSNSUR      TEST - NO SURCHARGE                          
         BO    RADJX                                                            
*                                                                               
*&&DO                                                                           
         LA    R2,SURTAB           SURCHARGE TABLE                              
         USING SURTABD,R2                                                       
*                                                                               
RADJ50   CLI   0(R2),X'FF'         END OF TABLE                                 
         BE    RADJ90                                                           
         CLC   SURYEAR,TMEFDTE0    IS THIS THE CORRECT YEAR                     
         BNE   RADJ55                                                           
         CLC   TMUNIT,SURUNIT      FOUND A STATE WITH A SURCHARGE               
         BE    RADJ60                                                           
*                                                                               
RADJ55   LA    R2,SURLNQ(R2)       BUMP TO NEXT STATE                           
         B     RADJ50                                                           
*                                                                               
RADJ60   CLC   TMYEARN,SURWGBS     IF YTD EARNINGS < SPECIAL WAGE BASE          
         BNL   RADJ90                                                           
         OI    RATESTAT,RTSTSUR    SET RATE CHANGED                             
         LA    R3,RTNTAB           CHECK WHAT ROUTINE TO GO TO                  
         USING RTNTABD,R3                                                       
*                                                                               
RADJ70   CLI   0(R3),X'FF'         END OF TABLE                                 
         BNE   *+6                                                              
         DC    H'0'                                                             
         CLC   TMBILTYP,RTNBTYPE   FIND CORRECT BILLING TYPE                    
         BE    RADJ80                                                           
         LA    R3,RTNLNQ(R3)       BUMP TO NEXT ENTRY                           
         B     RADJ70                                                           
*                                                                               
RADJ80   LH    RF,RTNDISP          DISPLACEMENT TO ROUTINE                      
         AR    RF,RB                                                            
         BASR  RE,RF                                                            
*&&                                                                             
RADJ90   CLI   TMBILTYP,TABRTY1    FOR TYPES 1,2,16 & 20                        
         BE    RADJ95                                                           
         CLI   TMBILTYP,TABRTY2                                                 
         BE    RADJ95                                                           
         CLI   TMBILTYP,TABRTY16                                                
         BE    RADJ95                                                           
         CLI   TMBILTYP,TABRTY20                                                
         BNE   RADJ100                                                          
RADJ95   TM    TMSTAT,TMSOHAND     IF THERE IS A HANDLING OVERRIDE              
         BZ    RADJX                                                            
         L     R1,TMBRFUI                                                       
         S     R1,TMBRHAND         BACK OUT HANDLING FEE FROM FUI RATE          
         ST    R1,TMBRFUI                                                       
         B     RADJX                                                            
*                                                                               
RADJ100  CLI   TMBILTYP,TABRTY14   IF BILLING TYPE 14                           
         BNE   RADJ120                                                          
         TM    TGUSSTAT,SESSION    AND IT IS A RE-USE PAYMENT                   
         BO    RADJ115                                                          
         LA    R0,4                SUBTRACT WORKMEN'S COMP FROM                 
         LA    RE,TMBRATES         INDIVIDUAL RATES                             
RADJ110  L     R1,0(RE)                                                         
         S     R1,TMBRWCRP                                                      
         ST    R1,0(RE)                                                         
         LA    RE,L'TMBRATES(RE)                                                
         BCT   R0,RADJ110                                                       
*                                                                               
         USING TARAD,RE                                                         
*        L     RE,TMACURRA         RE=A(BILLING RATES ELEMENT)                  
         L     RE,TMACPYRA         RE=A(BILLING RATES ELEMENT)                  
         LTR   RE,RE               DO WE HAVE NEW RATES?                        
         BZ    RADJX                                                            
         XC    TARATWC,TARATWC     DON'T CHARGE WC ON REUSE PAYMENTS            
         B     RADJX                                                            
         DROP  RE                                                               
*                                                                               
*                                  IF BILLING TYPE 14 SESSION,                  
RADJ115  CLI   TMW4TYPE,TAW4TYCA   IF W4 IS NOT A CANADIAN,                     
         BE    RADJX                                                            
         CLI   TMW4TYPE,TAW4TYCO   AND NOT A CORPORATION,                       
         BE    RADJX                                                            
         L     R5,TMYEARN          YTD TAXABLE EARNINGS                         
         A     R5,TMTXEARN         TAXABLE EARNINGS FROM THIS CHECK             
         C     R5,=F'10000000'     IF YTD + THIS CHECK > 100,000                
         BNH   RADJX               DO NOT CHARGE WCTAX FOR INDIVIDUALS          
*                                                                               
         USING TARAD,RE                                                         
*        L     RE,TMACURRA         RE=A(BILLING RATES ELEMENT)                  
         L     RE,TMACPYRA         RE=A(BILLING RATES ELEMENT)                  
         LTR   RE,RE               DO WE HAVE NEW RATES?                        
         BZ    RADJX                                                            
         XC    TARATWC,TARATWC     DON'T CHARGE WC IF YTD+CHECK > 100K          
         B     RADJX                                                            
         DROP  RE                                                               
*                                                                               
RADJ120  CLI   TMBILTYP,TABRTY15   IF BILLING TYPE 15                           
         BNE   RADJX                                                            
         L     R5,TMYEARN          YTD TAXABLE EARNINGS                         
         A     R5,TMTXEARN         TAXABLE EARNINGS FROM THIS CHECK             
         C     R5,=F'20000000'     IF YTD + THIS CHECK > 200,000                
         BNH   RADJX               DO NOT CHARGE WCTAX                          
         L     R1,TMBRMED          SUBTRACT WC RATE FROM OVER FICA RATE         
         S     R1,TMBRWCRP                                                      
         ST    R1,TMBRMED                                                       
*                                                                               
         USING TARAD,RE                                                         
*        L     RE,TMACURRA         RE=A(BILLING RATES ELEMENT)                  
         L     RE,TMACPYRA         RE=A(BILLING RATES ELEMENT)                  
         LTR   RE,RE               DO WE HAVE NEW RATES?                        
         BZ    RADJX                                                            
         XC    TARATWC,TARATWC     DON'T CHARGE WC IF YTD+CHECK > 200K          
         B     RADJX                                                            
         DROP  RE                                                               
*                                                                               
RADJX    B     XIT                                                              
         EJECT                                                                  
*                                                                               
*        ROUTINE TO ADJUST BILLING TYPES 1 & 2                                  
*                                                                               
RTN1N2   NTR1                                                                   
         BAS   RE,ADJFUI           ADJUST FUI RATE                              
         BAS   RE,ADJMED           ADJUST MEDICARE RATE                         
**NO-OP**BAL   RE,ADJOMED          ADJUST OVER MEDICARE RATE - 12/2/97          
         B     XIT                                                              
         SPACE 2                                                                
*                                                                               
*        ROUTINE TO ADJUST BILLING TYPE 6                                       
*                                                                               
RTN6     NTR1                                                                   
         BAS   RE,ADJFUI           ADJUST FUI RATE                              
         BAS   RE,ADJSUI           ADJUST SUI RATE                              
         B     XIT                                                              
         SPACE 2                                                                
*                                                                               
*        ROUTINE TO ADJUST BILLING TYPES 7 & 8                                  
*                                                                               
RTN7N8   NTR1                                                                   
         BAS   RE,ADJFUI           ADJUST FUI RATE                              
         B     XIT                                                              
         SPACE 2                                                                
*                                                                               
*        ROUTINE TO ADJUST BILLING TYPE 11                                      
*                                                                               
RTN11    NTR1                                                                   
         CLI   TMW4TYPE,TAW4TYIN   CHARGE ONLY FOR INDIVIDUALS                  
         BE    RTN11A                                                           
         CLI   TMW4TYPE,TAW4TYES   & ESTATES                                    
         BNE   *+8                                                              
*                                                                               
RTN11A   BAS   RE,ADJHAND          ADJUST HANDLING RATE                         
         B     XIT                                                              
*                                                                               
*        ROUTINE TO ADJUST BILLING TYPE 25 AND 26                               
*                                                                               
RTN25    NTR1                                                                   
         BAS   RE,ADJHAND          ADJUST HANDLING RATE                         
         B     XIT                                                              
         SPACE 2                                                                
         EJECT                                                                  
*        ROUTINE TO ADJUST BILLING TYPES 9, 10, 12 & 13 & 14                    
*        (BASED ON WAGE BASE)                                                   
*                                                                               
         USING SURTABD,R2                                                       
RTNWGBS  NTR1                                                                   
         BAS   RE,ADJFUI           ADJUST FUI RATE                              
         BAS   RE,ADJSUI           ADJUST SUI RATE                              
*                                                                               
*              DON'T ADJUST WAGE BASE ANYMORE (6/9)                             
*                                                                               
* NO-OP  ZIC   R1,TMLEVEL          SAVE LEVEL TAXED AT                          
* NO-OP  L     RF,TMBSUI           SAVE REAL WAGE BASE                          
* NO-OP  MVC   TMBSUI,SURWGBS      SET SPECIAL WAGE BASE                        
* NO-OP  XC    FUITAXBL(TAXBLNQ),FUITAXBL  CLEAR PREVIOUSLY CALCULATED          
* NO-OP  BAS   RE,GETTAXBL         TAXABLE AMOUNTS AND RECALCULATE THEM         
* NO-OP  STC   R1,TMLEVEL          RESTORE LEVEL TAXED                          
* NO-OP  ST    RF,TMBSUI           RESTORE REAL WAGE BASE                       
         B     XIT                                                              
*                                                                               
*        ROUTINE TO ADJUST FUI RATE                                             
*                                                                               
ADJFUI   DS    0H                                                               
         L     R1,TMBRFUI          FUI RATE                                     
         AH    R1,SURRATE          ADD SURCHARGE RATE                           
         ST    R1,TMBRFUI                                                       
         BR    RE                                                               
         SPACE 2                                                                
*                                                                               
*        ROUTINE TO ADJUST SUI RATE                                             
*                                                                               
ADJSUI   DS    0H                                                               
         L     R1,TMBRSUI          SUI RATE                                     
         AH    R1,SURRATE          ADD SURCHARGE RATE                           
         ST    R1,TMBRSUI                                                       
         BR    RE                                                               
         SPACE 2                                                                
*                                                                               
*        ROUTINE TO ADJUST MEDICARE RATE                                        
*                                                                               
ADJMED   DS    0H                                                               
         L     R1,TMBRMED          MEDICARE RATE                                
         AH    R1,SURRATE          ADD SURCHARGE RATE                           
         ST    R1,TMBRMED                                                       
         BR    RE                                                               
         SPACE 2                                                                
*                                                                               
*        ROUTINE TO ADJUST OVER MEDICARE RATE                                   
*                                                                               
ADJOMED  DS    0H                                                               
         L     R1,TMBROMED         OVER MEDICARE RATE                           
         AH    R1,SURRATE          ADD SURCHARGE RATE                           
         ST    R1,TMBROMED                                                      
         BR    RE                                                               
         SPACE 2                                                                
*                                                                               
*        ROUTINE TO ADJUST HANDLING RATE                                        
*                                                                               
ADJHAND  DS    0H                                                               
         USING TARAD,RF                                                         
*        L     RF,TMACURRA                                                      
         L     RF,TMACPYRA                                                      
         CLI   TARAEL,TARAELQ      LOOK FOR BILLING RATES                       
         BNE   ADJHND10                                                         
         ICM   R1,15,TARAHFB       HANDLING RATE                                
         AH    R1,SURRATE          ADD SURCHARGE RATE                           
         STCM  R1,15,TARAHFB                                                    
         BR    RE                                                               
         DROP  RF                                                               
                                                                                
ADJHND10 L     R1,TMBRHAND         HANDLING RATE                                
         AH    R1,SURRATE          ADD SURCHARGE RATE                           
         ST    R1,TMBRHAND                                                      
         BR    RE                                                               
         EJECT                                                                  
*        SET NEW YTD AMOUNTS IN RETURN BLOCK                                    
SETNYTD  NTR1                                                                   
         L     R5,TMNTEARN         NONTAXABLE EARNINGS                          
         A     R5,TMYNTAX          + OLD YTD NON TAXABLE EARNINGS               
         ST    R5,TMNYNTAX         =NEW YTD NON TAXABLE EARNINGS                
*                                                                               
         L     R5,TMTXEARN         TAXABLE EARNINGS                             
         A     R5,TMYEARN          + OLD YTD TAXABLE EARNINGS                   
         ST    R5,TMNYEARN         = NEW YTD TAXABLE EARNINGS                   
*                                                                               
         LTR   R5,R5               IF NEW YTD EARNINGS ARE NEGATIVE             
         BNM   SN40                                                             
         LA    R1,TMNYFUI          SET ALL OTHER AMOUNTS TO = IT                
         LA    R2,TMYTDN-2         N'YTD AMOUNTS                                
*                                                                               
SN30     ST    R5,0(R1)                                                         
         LA    R1,4(R1)            BUMP TO NEXT AMOUNT                          
         BCT   R2,SN30                                                          
*                                                                               
* P+ CANADIAN                                                                   
*                                                                               
         LA    R1,TMCNYTD          SET ALL OTHER AMOUNTS TO = IT                
         LA    R2,TMCYTDN          N'YTD AMOUNTS                                
*                                                                               
SN35     ST    R5,0(R1)                                                         
         LA    R1,4(R1)            BUMP TO NEXT AMOUNT                          
         BCT   R2,SN35                                                          
         B     SNX                                                              
*                                                                               
SN40     LA    R1,TMNYFUI          NEW FUI YTD                                  
         LA    R2,TMTFUI           TAXABLE AMOUNT AT EACH LEVEL                 
         LA    R3,TMYFUI           OLD FUI YTD PASSED TO TALIM                  
         LA    R4,TMYTDN-2         N'YTD AMOUNTS                                
*                                                                               
SN50     L     R5,0(R3)            PREVIOUS YTD                                 
         A     R5,0(R2)            + TAXABLE THIS CHECK                         
*                                                                               
         ST    R5,0(R1)            = NEW YTD                                    
*                                                                               
         LA    R1,4(R1)            BUMP TO NEXT AMOUNTS                         
         LA    R2,4(R2)                                                         
         LA    R3,4(R3)                                                         
         BCT   R4,SN50                                                          
*                                                                               
* P+ CANADIAN                                                                   
*                                                                               
         LA    R1,TMCNYTD          NEW YTD TABLE                                
         LA    R2,TMCTTAXB         TAXABLE AMOUNTS THIS TIME                    
         LA    R3,TMCYTD           OLD YTD TABLE                                
         LA    R4,TMCYTDN          N'YTD AMOUNTS                                
*                                                                               
SN60     L     R5,0(R3)            PREVIOUS YTD                                 
         A     R5,0(R2)            + TAXABLE THIS CHECK                         
*                                                                               
         ST    R5,0(R1)            = NEW YTD                                    
*                                                                               
         LA    R1,4(R1)            BUMP TO NEXT AMOUNTS                         
         LA    R2,4(R2)                                                         
         LA    R3,4(R3)                                                         
         BCT   R4,SN60                                                          
*                                                                               
         TM    TMNYFICA,X'80'      IF NEGATIVE CONTINUE                         
         BO    SN70                                                             
         CLC   TMNYFICA,TMBFICA    ELSE MAKE SURE FICA & MED AREN'T             
         BNH   SN70                                                             
         MVC   TMNYFICA,TMBFICA    OVER WAGE BASE                               
*                                                                               
SN70     TM    TMNYMED,X'80'       (NEEDED WHEN SUI MAX GOES UP AFTER           
         BO    SNX                                                              
         CLC   TMNYMED,TMBMED      PERFORMER ALREADY WENT OVER FICA             
         BNH   SNX                 OR MED)                                      
         MVC   TMNYMED,TMBMED                                                   
*                                                                               
SNX      B     XIT                                                              
         EJECT                                                                  
*                                                                               
*        CALC TAX & HANDLING FOR OTHER BILLING TYPES                            
*                                                                               
OTHTNH   NTR1                                                                   
         MVI   TMLEVEL,0           CLEAR LEVEL                                  
         BAS   RE,TY1N2            OTHERS HAVE TO HAVE THEIR                    
         BE    OTHNX                                                            
         BAS   RE,TY6N8            TAX & HANDLING RE-CALCULATED                 
         BE    OTHNX                                                            
         BAS   RE,TY7                                                           
         BE    OTHNX                                                            
         BAS   RE,TY11                                                          
         BE    OTHNX                                                            
         TM    TMSTAT,TMSNOBTY     NO BILLING TYPE PASSED                       
         BO    *+6                                                              
         DC    H'0'                                                             
*                                                                               
OTHNX    BAS   RE,CALCGSTU         CALCULATE US$ GST                            
         B     XIT                                                              
         EJECT                                                                  
*        BILLING TYPES 1, 2, 16, AND E (EMS)                                    
*                                                                               
TY1N2    NTR1                                                                   
         CLI   TMBILTYP,TABRTY1                                                 
         BE    TY1N3                                                            
         CLI   TMBILTYP,TABRTY2                                                 
         BE    TY1N3                                                            
         CLI   TMBILTYP,TABRTY16                                                
         BE    TY1N3                                                            
         CLI   TMBILTYP,TABRTY20                                                
         BE    TY1N3                                                            
         CLI   TMBILTYP,TABRTYE                                                 
         BNE   NO                                                               
*                                                                               
TY1N3    L     R2,TMNTEARN         CALCULATE HANDLING FEE ON                    
*                                  NON-TAXABLE EARNINGS                         
         CLI   TMBILTYP,TABRTY1    IF BILLING TYPE 1,                           
         BNE   *+8                                                              
         A     R2,TMADVR           ADD ANY ADVANCED RECONCILIATION              
                                                                                
         XR    R6,R6                                                            
                                                                                
         USING TARAD,RE                                                         
*        L     RE,TMACURRA         RE=A(BILLING RATES ELEMENT)                  
         L     RE,TMACPYRA         RE=A(BILLING RATES ELEMENT)                  
         LTR   RE,RE               IF NEW WAY                                   
         BZ    TY1N3A                                                           
         TM    TMSTAT2,TMSCORP     AND NO CORPS                                 
         BO    TY1N3A                                                           
         TM    TARASTA2,TARASSHD+TARASSPD IF EITHER HAND IS IN DOLLARS          
         BNZ   TY1N3B              SKIP APPL GRT HERE                           
         DROP  RE                                                               
                                                                                
TY1N3A   GOTO1 =A(HNDRULES),DMCB,(R2)                                           
         L     R2,DMCB                                                          
         L     R6,FULL                                                          
                                                                                
***      GOTOR CALCP,DMCB,TMBRHAND,(R2),TMOCORP  NON TAXABLE EARNINGS           
TY1N3B   GOTOR CALCHAND,DMCB,('CHANDIC',0),(R2),(R6)  REGULAR HAND RATE         
                                                                                
         USING TARAD,RE                                                         
*        L     RE,TMACURRA         RE=A(BILLING RATES ELEMENT)                  
         L     RE,TMACPYRA         RE=A(BILLING RATES ELEMENT)                  
         LTR   RE,RE               IF NEW WAY, CALC HAND ON TAXABLE             
         BZ    TY1N4A                                                           
         L     R2,TMTXEARN         CALC HANDLING ON TAXABLE EARNINGS            
         XR    R6,R6                                                            
         TM    TMSTAT2,TMSCORP     IF NO CORPS AND                              
         BO    TY1N4                                                            
         TM    TARASTA2,TARASSHD+TARASSPD IF EITHER HAND IS IN DOLLARS          
         BZ    TY1N4                                                            
         DROP  RE                                                               
                                                                                
         GOTO1 =A(HNDRULES),DMCB,(R2)   CALCULATE HANDLING ON APPL GRT          
         L     R2,DMCB                                                          
         L     R6,FULL                                                          
TY1N4    GOTOR CALCHAND,DMCB,('CHANDIP',0),(R2),(R6)  REGULAR HAND RATE         
*                                                                               
TY1N4A   MVC   WCTRATE,TMBRWCRP                                                 
         BRAS  RE,WCTAX            CALCULATE WC ON CORPS                        
*                                                                               
         BAS   RE,CFICR            CALCULATE FICA CREDITS                       
*                                                                               
         OC    TMTXEARN,TMTXEARN   IF NO TAXABLE EARNINGS                       
         BZ    T100                THEN EXIT                                    
*                                                                               
         MVC   TRATE,TMBRFUI       SET FUTA FOR TAXES                           
         CLI   TMLEVEL,0           IF RATE LEVEL ISN'T SET YET                  
         BNE   *+8                                                              
         MVI   TMLEVEL,C'A'        SET IT                                       
****     L     R2,TMTXEARN                                                      
****     GOTOR CALCP,DMCB,TRATE,(R2),TMXFUI                                     
*                                                                               
         L     R6,TMTXEARN                                                      
*                                                                               
         TM    RATESTAT,RTSTNOLM   IF BRATE HAS NO LIMIT,                       
         BZ    TY1N5               USE BASE AMOUNT                              
         GOTOR CALCTAX2,DMCB,('CTAXFUTA',0),(R6),('CTAXBASE',0)                 
         B     T100                                                             
*                                                                               
TY1N5    GOTOR CALCTAX2,DMCB,('CTAXFUTA',0),(R6),0  CALC NEW FUTA               
**NO-OP  L     R1,0(R1)                                                         
**FEB12  A     R1,TMXTOTAL                                                      
**NO-OP  ST    R1,TMXTOTAL                                                      
*                                                                               
T100     TM    TMSTAT,TMSOHAND     IF THERE IS A HANDLING OVERRIDE              
         BZ    TX                                                               
         CLI   TMW4TYPE,TAW4TYIN   AND THIS IS AN INDIVIDUAL OR                 
         BE    *+12                                                             
         CLI   TMW4TYPE,TAW4TYES   AN ESTATE                                    
         BNE   TX                                                               
*                                                                               
         L     R1,TMXTOTAL         ADD THE OVERRIDE HANDLING AMOUNT             
         A     R1,MYHAND           THAT WAS PASSED, TO THE TOTAL                
         ST    R1,TMXTOTAL                                                      
TX       B     YES                                                              
         EJECT                                                                  
*        BILLING TYPES 6 & 8                                                    
*                                                                               
TY6N8    NTR1                                                                   
         CLI   TMBILTYP,TABRTY23   IF TYPE 23, 25, 26, NO FICA CREDITS          
         BE    T610                                                             
         CLI   TMBILTYP,TABRTY25                                                
         BE    T610                                                             
         CLI   TMBILTYP,TABRTY26                                                
         BE    T610                                                             
         CLI   TMBILTYP,TABRTY6                                                 
         BE    T610                                                             
         CLI   TMBILTYP,TABRTY8                                                 
         BE    T610                                                             
         CLI   TMBILTYP,TABRTY18                                                
         BE    T610                                                             
         CLI   TMBILTYP,TABRTY21                                                
         BNE   NO                                                               
*                                                                               
T610     BAS   RE,CFICR            CALCULATE FICA CREDITS                       
T620     MVI   TMLEVEL,0           DON'T BOTHER WITH TAX LEVEL                  
*                                                                               
         MVC   TRATE,TMBRFUI       DEFAULT TAX RATE = FUTA                      
         MVC   WCTRATE,TMBRWCRP         & CHARGE WC ON CORPS                    
         MVI   BYTE,C'F'                                                        
         CLI   TMBILTYP,TABRTY8                                                 
         BE    T630                                                             
         CLI   TMBILTYP,TABRTY18                                                
         BE    T630                                                             
         CLI   TMBILTYP,TABRTY21                                                
         BE    T630                                                             
         CLI   TMBILTYP,TABRTY23                                                
         BE    T630                                                             
         CLI   TMBILTYP,TABRTY6    ONLY TYPE 6 HERE                             
         BE    T625                                                             
         CLI   TMBILTYP,TABRTY25                                                
         BE    T630                                                             
         CLI   TMBILTYP,TABRTY26                                                
         BE    T630                                                             
         BNE   NO                                                               
T625     TM    TGUSSTAT,SESSION    SESSION, USE SUTA FOR TAX RATE               
         BO    T628                                                             
         CLI   TMOFCDE,C'O'        REUSE                                        
         BE    T630                                                             
         CLI   TMOFCDE,C'Q'        OFFICE O AND Q CHARGE WC ON CORPS            
         BE    T630                                                             
         XC    WCTRATE,WCTRATE     DON'T CHARGE WC ON CORPS                     
         B     T630                                                             
                                                                                
T628     MVI   BYTE,C'S'           ELSE IT IS A SESSION PAYMENT -               
         MVC   TRATE,TMBRSUI       USE SUTA FIELD FOR TAX RATE                  
*                                                                               
T630     BRAS  RE,CTAX             CALC TAXES INCLUDING WC ON CORPS             
         CLI   BYTE,C'S'                                                        
         BE    *+14                                                             
         MVC   TMXFUI,TMXTOTAL                                                  
         B     *+10                                                             
         MVC   TMXSUI,TMXTOTAL                                                  
*                                                                               
         L     R2,TMTXEARN         HANDLING * (  TAXABLE EARNINGS               
         CLI   TMBILTYP,TABRTY23              DOESN'T WANT PAYROLL TAX          
         BE    T640                                                             
*                                                                               
         CLI   TMBILTYP,TABRTY25                                                
         BE    T640                                                             
         CLI   TMBILTYP,TABRTY26                                                
         BNE   T631                                                             
         A     R2,TMXTOTAL                                                      
         S     R2,TMXFICR                      - FICA CREDITS                   
         B     T640                                                             
*                                                                               
T631     CLI   TMBILTYP,TABRTY18              DOESN'T WANT PAYROLL TAX          
         BE    *+12                                                             
         A     R2,TMXTOTAL                     + PAYROLL TAXES                  
         S     R2,TMXFICR                      - FICA CREDITS                   
*2/3/98  A     R2,TMMDED                       + MISC DEDUCTIONS                
T640     A     R2,TMNTEARN                     + NON TAXABLE EARNINGS           
         CLI   TMBILTYP,TABRTY25                                                
         BE    T645                                                             
         CLI   TMBILTYP,TABRTY26                                                
         BE    T645                                                             
         CLI   TMCURR,C'C'                     IF CANADIAN INVOICE              
         BE    *+12                                                             
         A     R2,TMPNH                        + P & H                          
         B     *+8                                                              
         A     R2,TMINR                        + I & R ) - IRV 4/12/01          
T645     GOTO1 =A(HNDRULES),DMCB,(R2)                                           
*                                                                               
         L     R2,DMCB                                                          
         L     R6,FULL                                                          
         CLI   TMBILTYP,TABRTY21   IF BILLING TYPE 21,                          
         BE    T648                                                             
         CLI   TMBILTYP,TABRTY23   OR IF BILLING TYPE 23,                       
         BNE   T650                                                             
T648     TM    TGUSSTAT,SESSION    AND IT IS A SESSION PAYMENT,                 
         BNO   T650                USE SUTA FIELD FOR HANDLING RATE             
****     GOTOR CALCP,DMCB,TMBRSUI,(R2),TMOHAND                                  
         GOTOR CALCHAND,DMCB,('CHANDSI',0),(R2),(R6)  REGULAR HAND RATE         
         B     YES                                                              
*                                                                               
T650     CLI   TGUSEQU,UPNH        PNH TYPE?                                    
         BNE   T690                                                             
*                                                                               
T670     CLI   TMBILTYP,TABRTY25   BILLING TYPE 25                              
         BE    T680                                                             
         CLI   TMBILTYP,TABRTY26   BILLING TYPE 26                              
         BNE   T690                                                             
T680     L     R2,TMPNH                                                         
****     GOTOR CALCP,DMCB,TMBRCORP,(R2),TMOHAND     USE CORP RATE               
         GOTOR CALCHAND,DMCB,('CHANDCI',0),(R2),(R6)  USE REGULAR RATE          
         B     YES                                                              
*                                                                               
***690   GOTOR CALCP,DMCB,TMBRHAND,(R2),TMOHAND     REG HANDLING                
T690     GOTOR CALCHAND,DMCB,('CHANDII',0),(R2),(R6)  USE REG HAND RATE         
         B     YES                                                              
         EJECT                                                                  
*        BILLING TYPE 7                                                         
*                                                                               
TY7      NTR1                                                                   
         CLI   TMBILTYP,TABRTY7                                                 
         BNE   NO                                                               
         L     R2,TMTXEARN       HANDLING * (GROSS + MISC PAYMENTS)             
         A     R2,TMNTEARN                                                      
         GOTO1 =A(HNDRULES),DMCB,(R2)                                           
         L     R2,DMCB                                                          
         L     R6,FULL                                                          
***      GOTOR CALCP,DMCB,TMBRHAND,(R2),TMOHAND                                 
         GOTOR CALCHAND,DMCB,('CHANDII',0),(R2),(R6)  USE REG HAND              
*                                                                               
         BAS   RE,CFICR          CALCULATE FICA CREDITS                         
         MVI   TMLEVEL,0         DON'T BOTHER WITH TAX LEVEL                    
*                                                                               
         MVC   TRATE,TMBRFUI     SET FUTA FOR TAXES                             
         MVI   BYTE,C'F'                                                        
         MVC   WCTRATE,TMBRWCRP  SET WC ON CORP                                 
         BRAS  RE,CTAX           CALC TAXES INCLUDING WC ON CORPS               
         B     YES                                                              
         SPACE 2                                                                
*        BILLING TYPE 11                                                        
*                                                                               
TY11     NTR1                                                                   
         CLI   TMBILTYP,TABRTY11                                                
         BNE   NO                                                               
         L     R2,TMTXEARN        HANDLING * (  GROSS WAGES                     
         A     R2,TMNTEARN                    + MISC PAYMENTS                   
         A     R2,TMPNH                       + P & H )                         
         GOTO1 =A(HNDRULES),DMCB,(R2)                                           
         L     R2,DMCB                                                          
         L     R6,FULL                                                          
***      GOTOR CALCP,DMCB,TMBRHAND,(R2),TMOHAND                                 
         GOTOR CALCHAND,DMCB,('CHANDII',0),(R2),(R6)  USE REG HAND              
*                                                                               
         BAS   RE,CFICR          CALCULATE FICA CREDITS                         
*                                                                               
         MVC   WCTRATE,TMBRWCRP                                                 
         BRAS  RE,WCTAX           + WC CORP RATE * NON TAXABLE EARNINGS         
         B     YES                                                              
         SPACE 2                                                                
*        ROUTINE CALCULATES FICA CREDITS.  FICA CREDITS ARE                     
*        GIVEN IF PERFORMER EXCEEDS THE FICA AND/OR MEDICARE                    
*        CUTTOFF WITH THIS PAYMENT (IT IS UP TO THE CALLING                     
*        PROGRAM TO REDUCE PAYROLL TAXES BY THE FICA CREDITS)                   
*                                  XIT - TMXFICR AND TMLEVEL SET                
CFICR    NTR1                                                                   
         OC    TMTXEARN,TMTXEARN   IF NO TAXABLE EARNINGS                       
         BZ    CFICRX              THEN EXIT                                    
*                                                                               
         MVC   FEDYTD,TMYEARN                                                   
         L     R2,FEDYTD                                                        
         L     R3,TMTXEARN         SAVE TAXABLE EARNINGS                        
         AR    R2,R3               ADD EARNINGS TO YTD                          
         TM    TMTXEARN,X'80'      IF THIS IS A NEGATIVE INVOICE                
         BNO   CFICR20                                                          
         ST    R2,FEDYTD           SET NEW 'YTD'                                
         LTR   R2,R2               BUT INSURE IT'S NOT NEGATIVE                 
         BNM   *+6                                                              
         XR    R2,R2                                                            
         LCR   R3,R3               COMPLEMENT EARNINGS TO POSITIVE              
         AR    R2,R3               ADD                                          
*                                                                               
CFICR20  DS    0H                  NO-OP AS OF 12/2/97                          
**NO-OP**C     R2,TMBMED           CHECK IF OVER MEDICARE                       
*        BNH   CFICR40                                                          
*        S     R2,TMBMED           GET AMOUNT THAT IS OVER MEDICARE             
*        CR    R2,R3               IF IT'S OVER TAXABLE EARNINGS                
*        BNH   *+6                                                              
*        LR    R2,R3               THEN ONLY USE TAXABLE EARNINGS               
*                                                                               
*        MVC   TRATE,TMBROMED      SET OVER MEDICARE                            
*        MVI   TMLEVEL,C'E'        SET RATE LEVEL                               
*        GOTOR CALCP,DMCB,TRATE,(R2),TMXFICR                                    
**NO-OP**SR    R3,R2               R3 = AMOUNT LEFT TO TAX                      
*                                                                               
CFICR40  L     R2,FEDYTD                                                        
         AR    R2,R3               DON'T ADD EARNINGS                           
         C     R2,TMBFICA          CHECK IF OVER FICA MAX                       
         BNH   CFICR70                                                          
         S     R2,TMBFICA          GET AMOUNT THAT IS OVER FICA MAX             
         CR    R2,R3               ENSURE IT'S LESS THAN REMAINING              
         BNH   *+6                    TAXABLE AMOUNT                            
         LR    R2,R3               ELSE USE REMAINING TAXABLE AMOUNT            
*                                                                               
         MVC   TRATE,TMBRMED       SET OVER FICA                                
**NO-OP**CLI   TMLEVEL,C'E'        IF RATE LEVEL IS SET LOWEST                  
**NO-OP**BE    *+8                 LEAVE IT - 12/2/97                           
         MVI   TMLEVEL,C'D'        ELSE SET RATE LEVEL                          
***      GOTOR CALCP,DMCB,TRATE,(R2),TMXFICR                                    
         GOTOR CALCTAX2,DMCB,('CTAXFICR',0),(R2),0 CALC FICA CREDIT             
*                                                                               
CFICR70  TM    TMTXEARN,X'80'      IF EARNINGS ARE NEGATIVE                     
         BNO   CFICRX                                                           
         L     R1,TMXFICR                                                       
         LCR   R1,R1                                                            
         ST    R1,TMXFICR                                                       
CFICRX   B     XIT                                                              
         EJECT                                                                  
*                                                                               
*        CALCULATE TAXABLE AMOUNT GIVEN A WAGE BASE & YTD                       
*        P1 = A(MAX TAXABLE AMOUNT)                                             
*        P2 = A(CORRESPONDING YTD AMOUNT)                                       
*        RETURN                                                                 
*        P3 = A(TAXABLE AMOUNT)                                                 
*        REMAIN - (REMAINING TAXABLE)                                           
*                                                                               
         SPACE 1                                                                
MAXTAX   NTR1                                                                   
         OC    REMAIN,REMAIN                                                    
         BZ    MAXX                                                             
         L     R5,0(R1)            A(MAX TAXABLE AMOUNT)                        
         L     R5,0(R5)                                                         
         L     R4,4(R1)            A(YTD)                                       
         L     R4,0(R4)                                                         
         L     R6,8(R1)            A(RETURNED TAXABLE AMOUNT)                   
*                                                                               
         L     R2,REMAIN                                                        
         SR    R5,R4               R1 <= MAX - YTD                              
         BNP   MAXX                                                             
         CR    R5,R2               IF MAX TAXABLE >= PAYMENT                    
         BL    MAX10                                                            
         XC    REMAIN,REMAIN       USE ENTIRE PAYMENT AMOUNT                    
         B     MAX20                                                            
*                                                                               
MAX10    SR    R2,R5               ELSE US DIFFERENCE AT THIS RATE              
         ST    R2,REMAIN           AND REMAINDER USE AT HIGHER RATE             
         LR    R2,R5                                                            
*                                                                               
MAX20    TM    TMTXEARN,X'80'      IF THIS IS A NEGATIVE INVOICE                
         BNO   MAX30                                                            
         LCR   R2,R2               RESTORE AMT TO (-) FOR CALCULATION           
*                                                                               
MAX30    ST    R2,0(R6)            AMOUNT TAXED THIS INVOICE                    
         TM    TMTXEARN,X'80'      IF THIS IS A NEGATIVE INVOICE                
         BNO   MAX40                                                            
         LCR   R2,R2               RESTORE AMT TO (+) TO SUM TAXED AMT          
*                                                                               
MAX40    L     R3,TAXED            UPDATE AMOUNT TAXED THIS INVOICE             
         AR    R3,R2                                                            
         ST    R3,TAXED                                                         
*                                                                               
MAXX     B     XIT                                                              
*                                                                               
         EJECT                                                                  
*        BLD TABLE OF SUI/SDI WAGE BASES & RATES                                
*        GET FUI, FICA, MED WAGE BASES & RATES                                  
*                                                                               
         USING LIMD,R4                                                          
BLDLIM   NTR1                                                                   
         BRAS  RE,CLRLIM           CLEAR LIMITS                                 
                                                                                
         LA    R2,ATREF                                                         
         OPEN  ((R2),(INPUT))      OPEN THE FILE                                
         LA    R3,ATREFIO                                                       
         USING ATREFD,R3           ALLTAX DSECT                                 
*                                                                               
BLDL10   GET   (R2),(R3)                GET A RECORD                            
         CLC   DATE20,ATEFDATE          GET FIRST EFFECTIVE DATE                
         BL    BLDL10                   BEFORE TODAY'S                          
         CLC   ATEFDATE,=C'00000000'    & ENSURE IT'S A VALID REC               
         BE    BLDL10                                                           
*                                                                               
         CLC   =C'ATTXU',ATUNIT    IF RECORD FOR UNITED STATES                  
         BNE   BLDL300             NO, MUST BE FOR CANADA (ATTXC*)              
*---------------------------------------------------------------------          
* UNITED STATES VALUES                                                          
*---------------------------------------------------------------------          
         LA    R1,FICRATE                                                       
         TM    TMSTAT2,TMSEFICA         NEEDS EMPLOYER FICA RATE                
         BO    BLDL15                                                           
         CLC   =C'OASDITRATE',ATFLDNAM  IF THIS IS FICA RATE                    
         BE    BLDL900                  SET ADDRESS OF FICRATE                  
         B     BLDL18                                                           
BLDL15   CLC   =C'EROASDITRT',ATFLDNAM  IF THIS IS FICA RATE                    
         BE    BLDL900                  SET ADDRESS OF FICRATE                  
                                                                                
BLDL18   LA    R1,MEDRATE                                                       
         CLC   =C'MEDCTXRATE',ATFLDNAM  IF THIS IS MEDICARE RATE                
         BE    BLDL900                  SET ADDRESS OF MEDRATE                  
         LA    R1,FUIRATE                                                       
         CLC   =C'STERUNMPRT',ATFLDNAM  IF THIS IS FUI RATE                     
         BE    BLDL900                  SET ADDRESS OF FUIRATE                  
*                                                                               
         CLC   =C'ERUNMPRATE',ATFLDNAM  IF THIS IS OVERRIDE SUI RATE            
         BE    BLDL80                                                           
         CLC   =C'EEDISRATE',ATFLDNAM   OR SDI RATE                             
         BE    BLDL80                   SET RATE IN TABLE                       
*                                                                               
BLDL20   CLC   =C'DFERUNMPRT',ATFLDNAM  IF SUI UNEMPLOYMENT RATE                
         BE    BLDL80                                                           
         CLC   =C'DFEEDISRT',ATFLDNAM   OR SDI RATE                             
         BE    BLDL80                   SET RATE IN TABLE                       
         CLC   =C'EEDISWGBS',ATFLDNAM   ELSE IT MUST BE SDI MAX                 
         BE    BLDL70                                                           
         CLC   =C'EEQTDISMX',ATFLDNAM   HAWAII QTRLY SDI MAX                    
         BE    BLDL70                                                           
         CLC   =C'ERUNMPWGBS',ATFLDNAM  OR FUI/SUI MAX                          
         BE    BLDL50                                                           
*                                                                               
         CLC   TMEFDTE0(2),=C'90'       IF 1990 & BEFORE                        
         BH    BLDL30                                                           
         CLC   =C'SSWAGEBASE',ATFLDNAM  GET FICA FROM SSWAGEBASE                
         BNE   BLDL10                                                           
         LA    R1,FICMAX                                                        
         B     BLDL1000                                                         
*                                                                               
BLDL30   CLC   =C'OASDIWBASE',ATFLDNAM  ELSE GET IT FROM OASDIWBASE             
         BNE   BLDL40                                                           
         LA    R1,FICMAX                                                        
         B     BLDL1000                                                         
*                                                                               
BLDL40   CLC   =C'MEDCWGBASE',ATFLDNAM  GET MEDICARE TOO                        
         BNE   BLDL10                                                           
         LA    R1,MEDMAX                                                        
         B     BLDL1000                                                         
*                                                                               
BLDL50   CLC   ATUNIT+5(2),=C'FD'       FEDERAL MAX                             
         BNE   BLDL60                                                           
         LA    R1,FUIMAX                                                        
         B     BLDL1000                                                         
*                                                                               
BLDL60   CLC   =C'ATTXU',ATUNIT         IF THIS IS A STATE TAX UNIT             
         BNE   BLDL10                                                           
         BAS   RE,FNDENTRY              FIND CORRECT ENTRY IN TABLE             
         L     R4,ATHISTAB                                                      
         TM    LMTYPE,LMTYSUIW          IF SUI WAS ENTERED ALREADY              
         BO    BLDL10                   SKIP THIS ONE                           
         OI    LMTYPE,LMTYSUIW          SET - SUI WAS ENTERED ALREADY           
         LA    R1,LMSUIW                                                        
         B     BLDL1000                                                         
*                                                                               
BLDL70   CLC   =C'ATTXU',ATUNIT         IF THIS IS A STATE TAX UNIT             
         BNE   BLDL10                                                           
         BAS   RE,FNDENTRY              FIND CORRECT ENTRY IN TABLE             
         L     R4,ATHISTAB                                                      
         TM    LMTYPE,LMTYSDIW          IF SDI WAS ENTERED ALREADY              
         BO    BLDL10                   SKIP THIS ONE                           
         OI    LMTYPE,LMTYSDIW          SET - SDI WAS ENTERED ALREADY           
         LA    R1,LMSDIW                                                        
         B     BLDL1000                                                         
*                                                                               
BLDL80   CLC   =C'ATTXU',ATUNIT         IF THIS IS A STATE TAX UNIT             
         BNE   BLDL10                                                           
         BAS   RE,FNDENTRY              FIND CORRECT ENTRY IN TABLE             
         L     R4,ATHISTAB                                                      
*                                                                               
         CLC   =C'DFEEDISRT',ATFLDNAM   SDI RATE                                
         BE    BLDL85                                                           
         CLC   =C'EEDISRATE',ATFLDNAM                                           
         BE    BLDL85                                                           
         TM    LMTYPE,LMTYSUIR          IF SUI RATE WAS READ ALREADY            
         BO    BLDL10                   SKIP THIS ONE                           
         OI    LMTYPE,LMTYSUIR          SET - RATE WAS READ                     
         LA    R1,LMSUIR                STORE AMOUNT IN TABLE                   
         B     BLDL900                                                          
*                                                                               
BLDL85   TM    LMTYPE,LMTYSDIR          IF SDI RATE WAS READ ALREADY            
         BO    BLDL10                   SKIP THIS ONE                           
         OI    LMTYPE,LMTYSDIR          SET - RATE WAS READ                     
         LA    R1,LMSDIR                STORE AMOUNT IN TABLE                   
         B     BLDL900                                                          
         EJECT                                                                  
*---------------------------------------------------------------------          
* CANADIAN VALUES                                                               
*---------------------------------------------------------------------          
BLDL300  DS    0H                                                               
         LA    R1,CPPRATE                                                       
         CLC   =C'CPPRATE',ATFLDNAM     IF THIS IS CPP RATE                     
         BE    BLDL800                                                          
*                                                                               
         LA    R1,QPPRATE                                                       
         CLC   =C'QPPRATE',ATFLDNAM     IF THIS IS QUEBEC PP RATE               
         BE    BLDL800                                                          
*                                                                               
         LA    R1,CPPMAX                                                        
         CLC   =C'ANMXPNTXWA',ATFLDNAM  IF THIS IS CPP/QPP WAGE MAX             
         BNE   *+8                                                              
         BE    BLDL990                                                          
*                                                                               
         LA    R1,EIRATE                                                        
         CLC   =C'EMPEUICTRT',ATFLDNAM  IF THIS IS EI RATE                      
         BE    BLDL900                                                          
*                                                                               
         LA    R1,QEIRATE                                                       
         CLC   =C'QCEEUICTRT',ATFLDNAM  IF THIS IS QUEBEC EI RATE               
         BE    BLDL900                                                          
*                                                                               
         LA    R1,EIMAX                                                         
         CLC   =C'MAXUICWAGE',ATFLDNAM  IF THIS IS EI/QEI WAGE MAX              
         BNE   *+8                                                              
         BE    BLDL1000                                                         
*                                                                               
         LA    R1,QPIPRATE                                                      
         CLC   =C'QCEEPIPTRT',ATFLDNAM  IF THIS IS QPIP RATE                    
         BE    BLDL900                                                          
*                                                                               
         LA    R1,QPIPMAX                                                       
         CLC   =C'QCMAXPIPWG',ATFLDNAM  IF THIS IS QPIP WAGE MAX                
         BE    BLDL1000                                                         
*                                                                               
         B     BLDL10                   SKIP UNKNOWN ONE                        
         EJECT                                                                  
*---------------------------------------------------------------------          
* STORE THE VALUES                                                              
*---------------------------------------------------------------------          
BLDL800  CLC   =C'2000',ATEFDATE        IF YEAR 2000, SKIP                      
         BE    BLDL10                                                           
BLDL900  OC    0(4,R1),0(R1)            IF AMOUNT ALREADY SET                   
         BNZ   BLDL10                   DON'T SET IT AGAIN                      
         PACK  DUB,ATAMOUNT(13)         ELSE                                    
         B     BLDL1100                                                         
*                                                                               
BLDL990  CLC   =C'2000',ATEFDATE        IF YEAR 2000, SKIP                      
         BE    BLDL10                                                           
BLDL1000 OC    0(4,R1),0(R1)            IF AMOUNT ALREADY SET                   
         BNZ   BLDL10                   DON'T SET IT AGAIN                      
         PACK  DUB,ATAMOUNT(10)         SET AMOUNT IN R0                        
*                                                                               
BLDL1100 CLC   DUB,=X'000002147483647F'                                         
         BNH   BLDL1150                                                         
         MVC   DUB,=X'000002147483647F'                                         
BLDL1150 CVB   R0,DUB                                                           
         ST    R0,0(R1)                 STORE AMOUNT IN PASSED ADDRESS          
         B     BLDL10                                                           
*                                                                               
BLDLX    DS    0H                                                               
         CLOSE ((R2))                   CLOSE THE FILE                          
*                                                                               
         CLI   TMTRACE,C'Y'        TEST TRACE REQUESTED                         
         BNE   XIT                                                              
* -- REPLACED TMASYCOM WITH TMOPST (JAN 20, 2010)                               
*        ICM   R4,15,TMASYCOM      IF WE HAVE A(SYSTEM ROUTINES)                
*        BZ    XIT                                                              
*        USING SYSCOMMD,R4         OK TO TRACE                                  
*        LA    R2,LIMTAB                                                        
*        L     RF,=A(300*LIMLNQ)                                                
*        GOTO1 TRACE,DMCB,(R2),(RF),=C'LIMTAB',6                                
         B     XIT                                                              
         EJECT                                                                  
*                                                                               
*        FIND THE CORRECT ENTRY FOR THIS UNIT                                   
*                                                                               
FNDENTRY NTR1                                                                   
         LA    R4,LIMTAB                                                        
         USING LIMD,R4             A(SUI/SDI WAGE BASE TABLE)                   
*                                                                               
FND10    CLI   0(R4),X'FF'                                                      
         BNE   *+6                                                              
         DC    H'0'                NO MORE ROOM IN TABLE                        
*                                                                               
         CLI   0(R4),0             IF TABLE IS NOT EMPTY                        
         BE    FND20                                                            
         CLC   LMUNIT,ATUNIT+5     AND UNIT/EMP IS NOT FOUND                    
         BNE   FND15                                                            
         CLC   LMEMP,ATEMP                                                      
         BE    FND20                                                            
*                                                                               
FND15    LA    R4,LIMNEXT           BUMP TO NEXT POSITION IN TABLE              
         B     FND10                                                            
*                                                                               
FND20    MVC   LMUNIT,ATUNIT+5     SET UNIT                                     
         MVC   LMEMP,ATEMP             EMPLOYER CODE                            
         ST    R4,ATHISTAB                                                      
         B     XIT                                                              
         EJECT                                                                  
         SPACE 2                                                                
YES      SR    RC,RC               SET CONDITION CODE                           
NO       LTR   RC,RC                                                            
*                                                                               
XIT      XIT1                                                                   
*                                                                               
         GETEL R4,DATADISP,ELCODE                                               
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
ATREF    DCB   DDNAME=ATREF,DSORG=IS,EODAD=BLDLX,MACRF=GM                       
*                                                                               
*        MISC VARIABLES                                                         
*                                                                               
         DS    0D                                                               
         DC    C'*STORAGE'                                                      
*                                                                               
FIRST    DC    C'Y'                FIRST TIME THROUGH PROGRAM                   
*                                                                               
MYEMP    DS    CL3                 LOCAL EMPLOYER CODE                          
MYHAND   DS    F                   SAVED OVERRIDE HANDLING AMOUNT               
GSTRATE  DS    F                   GOODS & SERVICE TAX                          
TRATE    DS    F                   TAX RATE                                     
WCTRATE  DS    F                   TAX RATE (WC ON CORPS)                       
ATHISTAB DS    A                   ENTRY TO SUI/SDI TABLE                       
*                                                                               
DATE20   DS    CL8                 CCYYMMDD                                     
EFDATE1  DS    PL3                 PACKED EFFECTIVE DATE                        
*                                                                               
RATESTAT DS    XL1                 RATE STATUS                                  
RTSTSUR  EQU   X'80'               RATE SURCHARGE                               
RTSTBRAT EQU   X'40'               BRATE RECORD EXISTS FOR AGY/CLI              
RTSTFICA EQU   X'20'               BRATE RECORD HAS FICA WAGE BREAK             
RTSTNOLM EQU   X'10'               BRATE RECORD HAS NO WAGE BREAK LIMIT         
         DS    CL3                                                              
*                                                                               
FUIMAX   DS    F                   FUI WAGE BASE                                
FICMAX   DS    F                   FICA                                         
MEDMAX   DS    F                   MEDICARE                                     
*                                                                               
FUIRATE  DS    F                   FUI RATES                                    
FICRATE  DS    F                   FICA                                         
MEDRATE  DS    F                   MEDICARE                                     
*                                                                               
CPPMAX   DS    F                   PP WAGE MAX                                  
EIMAX    DS    F                   EI WAGE MAX                                  
QPIPMAX  DS    F                   QPIP WAGE MAX                                
*                                                                               
CPPRATE  DS    F                   PP RATE                                      
QPPRATE  DS    F                   QPP RATE                                     
EIRATE   DS    F                   EI RATE                                      
QEIRATE  DS    F                   QEI RATE                                     
QPIPRATE DS    F                   QPIP RATE                                    
*                                                                               
REMAIN   DS    F                   AMOUNT LEFT TO BE TAXED                      
REMAINC  DS    F                   AMOUNT LEFT TO BE TAXED                      
TAXED    DS    F                   AMOUNT TAXED THIS INVOICE                    
*                                                                               
AMOUNTS  DS    0F                                                               
FEDYTD   DS    F                   FEDERAL YTD                                  
FUIYTD   DS    F                   FUI YTD                                      
SUIYTD   DS    F                   SUI YTD                                      
FICYTD   DS    F                   FICA YTD                                     
MEDYTD   DS    F                   MEDICARE YTD                                 
YTDAMTS  EQU   *-AMOUNTS                                                        
*                                                                               
FUITAXBL DS    F                   TAXABLE FUI                                  
SUITAXBL DS    F                           SUI                                  
FICTAXBL DS    F                           FICA                                 
MEDTAXBL DS    F                           MEDICARE                             
OMDTAXBL DS    F                           OVER MEDICARE                        
TAXBLNQ  EQU   *-FUITAXBL                                                       
*                                                                               
CAMOUNTS DS    0F                                                               
UTAXYTD  DS    F                   TAX EARNINGS                                 
UPPYTD   DS    F                   PENSION PLAN EARNINGS                        
UEIYTD   DS    F                   EMP INSURANCE EARNINGS                       
UPIPYTD  DS    F                   PARENTAL PLAN EARNINGS                       
CTAXYTD  DS    F                   TAX EARNINGS CAD                             
CPPYTD   DS    F                   PENSION PLAN EARNINGS CAD                    
CEIYTD   DS    F                   EMP INSURANCE EARNINGS CAD                   
CPIPYTD  DS    F                   PARENTAL PLAN EARNINGS CAD                   
CYTDAMTS EQU   *-CAMOUNTS                                                       
*                                                                               
CTAXTXBL DS    F                   TAXABLE TAX CAD                              
CPPTXBL  DS    F                           PENSION PLAN  CAD                    
CEITXBL  DS    F                           EMP INSURANCE CAD                    
CPIPTXBL DS    F                           PARENTAL PLAN CAD                    
CTAXBLNQ EQU   *-CTAXTXBL                                                       
AMTSLNQ  EQU   *-AMOUNTS                                                        
*                                                                               
         EJECT                                                                  
*                                                                               
*        TABLE OF CANADIAN I&R AND ACTRAWORKS SSN'S                             
*                                                                               
CANXTAB  DS    0CL9                                                             
         DC    CL9'000000055'                                                   
         DC    CL9'000003755'                                                   
         DC    CL9'000003855'                                                   
         DC    CL9'000003876'                                                   
         DC    CL9'000007106'                                                   
         DC    CL9'000000066'                                                   
         DC    CL9'000008213'                                                   
         DC    CL9'000006816'      ACTRAWORKS                                   
         DC    X'FF'                                                            
*                                                                               
*        TABLE OF AFM H&W FUNDS                                                 
*                                                                               
AFMTAB   DS    0CL9                                                             
         DC    CL9'237385560'                                                   
         DC    CL9'999999999'                                                   
         DC    CL9'210390905'                                                   
         DC    CL9'131801294'                                                   
         DC    X'FF'                                                            
*                                                                               
*        TABLE OF STATE'S WITH SPECIAL RATE CHANGES                             
*                                                                               
RATETAB  DS    0F                                                               
*        DC    CL2'HI',AL1(0,0),H'0450',H'0450',H'0450'                         
*NO-OP   DC    CL2'MI',AL1(TABRTY1,0),H'0',H'0',H'0235'                         
*        DC    CL2'MI',AL1(TABRTY2,0),H'0',H'0',H'0235'                         
*        DC    CL2'MI',AL1(TABRTY16,0),H'0',H'0',H'0235'                        
*09/02   DC    CL2'MI',AL1(0,0),H'0235',H'0',H'0'                               
         DC    X'FF'                                                            
         SPACE 3                                                                
                                                                                
*                                                                               
*        TABLE OF STATE'S WITH EXTRA TAXES                                      
**DUMMY  DC    CL2'HI',H'4000',XL6'000000B40131'   4.712%                       
**RATES  DC    CL2'HI',H'4712',XL6'B40201FFFFFF'   4.712%  LATEST RATE          
**DUMMY  DC    CL2'WA',H'1000',XL6'000000B40331'   1.500%                       
**RATES  DC    CL2'WA',H'1500',XL6'B40401FFFFFF'   1.500%  LATEST RATE          
*                                                                               
EXTAXTAB DS    0F                                                               
         DC    CL2'HI',H'4712',XL6'000000FFFFFF'   4.712%  LATEST RATE          
         DC    CL2'WA',H'1500',XL6'000000FFFFFF'   1.500%  LATEST RATE          
         DC    X'FF'                                                            
         SPACE 3                                                                
*&&DO                                                                           
*        TABLE OF STATE'S WITH SURCHARGE RATES                                  
*                                                                               
SURTAB   DS    0F                                                               
         DC    CL2'NY',AL2(80),AL4(700000),C'93',AL2(0,0)                       
         DC    X'FF'                                                            
         SPACE 3                                                                
*&&                                                                             
*                                                                               
*        TABLE OF SURCHARGE ADJUST ROUTINES FOR EACH BILLING TYPE               
*                                                                               
RTNTAB   DS    0F                                                               
         DC    AL1(TABRTY1,0),AL2(RTN1N2-TALIM)                                 
         DC    AL1(TABRTY2,0),AL2(RTN1N2-TALIM)                                 
         DC    AL1(TABRTY6,0),AL2(RTN6-TALIM)                                   
         DC    AL1(TABRTY7,0),AL2(RTN7N8-TALIM)                                 
         DC    AL1(TABRTY8,0),AL2(RTN7N8-TALIM)                                 
         DC    AL1(TABRTY9,0),AL2(RTNWGBS-TALIM)                                
         DC    AL1(TABRTY10,0),AL2(RTNWGBS-TALIM)                               
         DC    AL1(TABRTY11,0),AL2(RTN11-TALIM)                                 
         DC    AL1(TABRTY12,0),AL2(RTNWGBS-TALIM)                               
         DC    AL1(TABRTY13,0),AL2(RTNWGBS-TALIM)                               
         DC    AL1(TABRTY14,0),AL2(RTNWGBS-TALIM)                               
         DC    AL1(TABRTY15,0),AL2(RTNWGBS-TALIM)                               
         DC    AL1(TABRTY16,0),AL2(RTN1N2-TALIM)                                
         DC    AL1(TABRTY18,0),AL2(RTN7N8-TALIM)                                
         DC    AL1(TABRTY21,0),AL2(RTN7N8-TALIM)                                
         DC    AL1(TABRTY22,0),AL2(RTNWGBS-TALIM)                               
         DC    AL1(TABRTY23,0),AL2(RTN7N8-TALIM)                                
         DC    AL1(TABRTY24,0),AL2(RTNWGBS-TALIM)                               
         DC    AL1(TABRTY25,0),AL2(RTN25-TALIM)                                 
         DC    AL1(TABRTY26,0),AL2(RTN25-TALIM)                                 
         DC    AL1(TABRTYE,0),AL2(RTN1N2-TALIM)                                 
         DC    X'FF'                                                            
         SPACE 3                                                                
*                                                                               
*        ATREF I/O AREA                                                         
*                                                                               
         DS    0D                                                               
         DC    CL8'*ATREF**'                                                    
ATREFIO  DS    256C                AREA FOR ATREF FILE                          
         SPACE 1                                                                
*                                                                               
*        TABLE OF WAGE BASES & RATES                                            
*                                                                               
         DS    0D                                                               
         DC    C'*LIMTAB*'                                                      
LIMTAB   DC    (300*LIMLNQ)X'00'                                                
LIMTABX  DC    X'FF'                                                            
         EJECT                                                                  
*                                                                               
*        ADJUST YTD                                                             
*              R1 - A(YTD TO ADJUST)                                            
*                                                                               
ADJYTD   NTR1  BASE=*,LABEL=*                                                   
         L     R2,0(R1)                                                         
         C     R2,FEDYTD           IF YTD > FED YTD                             
         BNH   AY10                                                             
         A     R2,TMTXEARN         ADD EARNINGS                                 
         C     R2,FEDYTD           IF YTD < FED YTD                             
         BNL   AY10                                                             
         L     R2,FEDYTD           SET IT TO BE FED YTD                         
*                                                                               
AY10     ST    R2,0(R1)                                                         
         J     XIT                                                              
                                                                                
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*        FOR EVENTS, NEED TO SET TAX UNIT WITH HIGHEST SUI LIMIT                
*                                                                               
SETUNIT  NTR1  BASE=*,LABEL=*                                                   
         CLI   LIMTAB,0            IF LIMIT TABLE IS NOT EMPTY                  
         JE    XIT                                                              
                                                                                
         TM    TMSTAT2,TMEVTSUI    GOING TO CALC SUI SUR FOR EVENTS             
         BO    XIT                 LEAVE, NO NEED FOR THIS                      
         BRAS  RE,GETCHK           GET CHECK RECORD                             
         JNE   XIT                                                              
                                                                                
         TM    TMSTAT2,TMOLDSUI    SKIP IF NOT DOING OLD SUI                    
         JZ    XIT                                                              
                                                                                
         XC    TGFULL,TGFULL       CLEAR TO SAVE HIGHEST WAGE BASE              
                                                                                
         USING TATUD,R4                                                         
         L     R4,AIO3                                                          
         MVI   ELCODE,TATUELQ      READ ALL TAX UNIT ELEMENTS                   
         BRAS  RE,GETEL                                                         
         B     *+8                                                              
SU10     BRAS  RE,NEXTEL                                                        
         JNE   XIT                                                              
         CLC   TATUUNIT,=C'FD '    SKIPPING FEDERAL                             
         BE    SU10                                                             
                                                                                
         MVC   TGTHREE,TATUUNIT                                                 
         CLC   =C'DET',TATUUNIT    IF TAX UNIT IS FOR A CITY                    
         BNE   *+10                SET APPROPRIATE STATE                        
         MVC   TGTHREE,=C'MI '                                                  
         CLC   =C'NYC',TATUUNIT                                                 
         BNE   *+10                                                             
         MVC   TGTHREE,=C'NY '                                                  
         CLC   =C'CIN',TATUUNIT                                                 
         BNE   *+10                                                             
         MVC   TGTHREE,=C'OH '                                                  
         CLC   =C'CLV',TATUUNIT                                                 
         BNE   *+10                                                             
         MVC   TGTHREE,=C'OH '                                                  
         CLC   =C'PHL',TATUUNIT                                                 
         BNE   *+10                                                             
         MVC   TGTHREE,=C'PA '                                                  
         DROP  R4                                                               
                                                                                
         USING LIMD,R2                                                          
         LA    R2,LIMTAB                                                        
SU20     CLI   0(R2),X'FF'                                                      
         BE    SU10                                                             
         CLC   LMUNIT,TGTHREE      FIND STATE IN LIMIT TABLE                    
         BNE   SU30                                                             
         CLC   LMEMP,SPACES                                                     
         BE    SU40                                                             
SU30     LA    R2,LIMNEXT                                                       
         B     SU20                                                             
                                                                                
SU40     CLC   LMSUIW,TGFULL       IF STATE HAS THE HIGHEST WAGE                
         BNH   SU10                BASE                                         
         MVC   TGFULL,LMSUIW       SAVE THAT WAGE BASE                          
         MVC   TMUNIT,TGTHREE      AND SET THE STATE                            
         B     SU10                                                             
         DROP  R2                                                               
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*        CLEAR LIMITS BEFORE BUILDING LIMITS                                    
*                                                                               
CLRLIM   NTR1  BASE=*,LABEL=*                                                   
         XC    FICMAX,FICMAX                                                    
         XC    FUIMAX,FUIMAX                                                    
         XC    MEDMAX,MEDMAX                                                    
                                                                                
         LA    R0,LIMTAB           CLEAR LIMTAB                                 
         LHI   R1,LIMTABX-LIMTAB                                                
         XR    RE,RE                                                            
         XR    RF,RF                                                            
         MVCL  R0,RE                                                            
                                                                                
         J     XIT                                                              
                                                                                
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*        SUI SURCHARGE IF NEEDED                                                
*                                                                               
SUISURCH NTR1  BASE=*,LABEL=*                                                   
                                                                                
         CLI   TMW4TYPE,TAW4TYIN   ONLY FOR INDIVIDUALS                         
         JE    SUISUR05                                                         
         CLI   TMW4TYPE,TAW4TYES   AND ESTATES                                  
         JNE   XIT                                                              
                                                                                
         USING SSUITBD,RF                                                       
SUISUR05 LA    RF,SUISURTB                                                      
SUISUR10 CLI   0(RF),X'FF'                                                      
         JE    XIT                                                              
         CLC   TMUNIT,SSUIUNIT                                                  
         JNE   SUISUR25                                                         
         CLC   SSUIEFST,EFDATE1    FIND EFFECTIVE DATE RANGE                    
         BH    SUISUR25                                                         
         CLC   SSUIEFED,EFDATE1                                                 
         BNL   SUISUR30                                                         
SUISUR25 AHI   RF,SSUILNQ                                                       
         J     SUISUR10                                                         
*                                                                               
SUISUR30 XR    R4,R4                                                            
         ICM   R4,3,SSUIRATE       GET SURCHARGE RATE                           
         XR    R0,R0                                                            
         L     R1,TMTSUI           TAXABLE SUI                                  
         BRAS  RE,TNHWGBS          TEST IF BILLING TYPE IS WAGE BASED           
         BE    *+8                                                              
         L     R1,TMTXEARN         NO, USE TOTAL TAXABLE EARNINGS               
                                                                                
         MR    R0,R4                                                            
         LH    RF,=H'5000'                                                      
         DR    R0,RF                                                            
         LTR   R1,R1               R3 = QUOTIENT                                
         BM    *+8                                                              
         AH    R1,=H'1'            ROUND                                        
         SRA   R1,1                                                             
                                                                                
         ST    R1,TMSUISUR                                                      
         J     XIT                                                              
                                                                                
**DUMMY  DC    CL2'CA',H'250',XL6'000000B40331'   2.500%                        
**RATES  DC    CL2'CA',H'300',XL6'B40401FFFFFF'   3.000%  LATEST RATE           
**DUMMY  DC    CL2'NY',H'350',XL6'000000B40331'   3.500%                        
**RATES  DC    CL2'NY',H'400',XL6'B40401FFFFFF'   4.000%  LATEST RATE           
****     DC    CL2'CA',H'300'       3.00%                                       
****     DC    CL2'NY',H'400'       4.00%                                       
*                                                                               
*        TABLE OF STATE SURCHARGES                                              
*                                                                               
SUISURTB DS    0F                                                               
**DUMMY  DC    CL2'CA',H'300',XL6'B40101FFFFFF'   3.000%  LATEST RATE           
**RATES  DC    CL2'NY',H'400',XL6'B40101FFFFFF'   4.000%  LATEST RATE           
         DC    X'FF'                                                            
         SPACE 3                                                                
         EJECT                                                                  
*                                                                               
*        TEST BILLING TYPES CALCULATION BASED ON WAGE BASE                      
*                                                                               
TNHWGBS  NTR1  BASE=*,LABEL=*                                                   
         CLI   TMBILTYP,TABRTY9                                                 
         JE    YES                                                              
         CLI   TMBILTYP,TABRTY10                                                
         JE    YES                                                              
         CLI   TMBILTYP,TABRTY12                                                
         JE    YES                                                              
         CLI   TMBILTYP,TABRTY13                                                
         JE    YES                                                              
         CLI   TMBILTYP,TABRTY14                                                
         JE    YES                                                              
         CLI   TMBILTYP,TABRTY15                                                
         JE    YES                                                              
         CLI   TMBILTYP,TABRTY22                                                
         JE    YES                                                              
         CLI   TMBILTYP,TABRTY24                                                
         JE    YES                                                              
         J     NO                                                               
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*        GET EACH LEVEL'S TAXABLE AMOUNT (P+ CANADIAN)                          
*                                                                               
CGETTXBL NTR1  BASE=*,LABEL=*                                                   
         MVC   UTAXYTD(CYTDAMTS),TMCYTD     SET YTD AMOUNTS                     
         TM    TMSTATUS,TMSTCRDT   CREDIT INVOICE?                              
         BNO   CGT05                                                            
         BRAS  RE,GETCRYTD         GET YTD OF INVOICE BEING CREDITED            
         BE    CGT10                                                            
CGT05    L     R1,TMYNTAX          IF NEW YTD EARNINGS < 0                      
         A     R1,TMNTEARN         THEN TREAT YTD AMOUNTS                       
         BNM   CGT10               AS = 0 TO CALCULATE TAXABLE AMOUNTS          
         XC    UTAXYTD(CYTDAMTS),UTAXYTD                                        
*                                                                               
CGT10    MVC   REMAIN,TMNTEARN                                                  
         MVC   REMAINC,TMNTCERN                                                 
         XC    TAXED,TAXED         CLEAR AMOUNT TAXED THIS INVOICE              
*                                                                               
         OC    REMAINC,REMAINC                                                  
         JZ    CGT30                                                            
*                                                                               
         TM    REMAINC,X'80'       IF AMOUNT IS NEGATIVE                        
         BNO   *+8                                                              
         BAS   RE,CNEGAMT          NEED TO BACK OUT EARNINGS FROM YTD           
*                                                                               
         GOTO1 CMAXTAX,DMCB,TMBCPP,CPPYTD,CPPTXBL                               
*                                                                               
         GOTO1 CMAXTAX,DMCB,TMBEI,CEIYTD,CEITXBL                                
*                                                                               
         CLC   TGTHREE,=C'QC '     ONLY QUEBEC                                  
         BNE   CGT30                                                            
         GOTO1 CMAXTAX,DMCB,TMBQPIP,CPIPYTD,CPIPTXBL                            
*                                                                               
CGT30    TM    RATESTAT,RTSTSUR    IF RATE SURCHARGE - DONE                     
         BO    *+8                                                              
         BAS   RE,ADJAMT           ADJUST TAXABLE AMOUNTS                       
*                                                                               
CGTX     J     XIT                                                              
         EJECT                                                                  
*                                                                               
*        CALCULATE TAXABLE AMOUNT GIVEN A WAGE BASE & YTD                       
*        P1 = A(MAX TAXABLE AMOUNT)                                             
*        P2 = A(CORRESPONDING YTD AMOUNT)                                       
*        RETURN                                                                 
*        P3 = A(TAXABLE AMOUNT)                                                 
*        REMAIN - (REMAINING TAXABLE)                                           
*                                                                               
         SPACE 1                                                                
CMAXTAX  NTR1                                                                   
         L     R5,0(R1)            A(MAX TAXABLE AMOUNT)                        
         L     R5,0(R5)                                                         
         L     R4,4(R1)            A(YTD)                                       
         L     R4,0(R4)                                                         
         L     R6,8(R1)            A(RETURNED TAXABLE AMOUNT)                   
*                                                                               
         L     R2,REMAINC                                                       
         SR    R5,R4               R1 <= MAX - YTD                              
         BNP   CMAXX                                                            
         CR    R5,R2               IF MAX TAXABLE >= PAYMENT                    
         BL    CMAX10                                                           
         B     CMAX20                                                           
*                                                                               
CMAX10   LR    R2,R5                                                            
*                                                                               
CMAX20   TM    TMNTEARN,X'80'      IF THIS IS A NEGATIVE INVOICE                
         BNO   MAX30                                                            
         LCR   R2,R2               RESTORE AMT TO (-) FOR CALCULATION           
*                                                                               
CMAX30   ST    R2,0(R6)            AMOUNT TAXED THIS INVOICE                    
         TM    TMNTEARN,X'80'      IF THIS IS A NEGATIVE INVOICE                
         BNO   CMAX40                                                           
         LCR   R2,R2               RESTORE AMT TO (+) TO SUM TAXED AMT          
*                                                                               
CMAX40   L     R3,TAXED            UPDATE AMOUNT TAXED THIS INVOICE             
         AR    R3,R2                                                            
         ST    R3,TAXED                                                         
*                                                                               
CMAXX    J     XIT                                                              
*                                                                               
*        ADJUST YTD AMOUNTS IF CHECK AMOUNT IS NEGATIVE                         
*        NEED TO BACK OUT THIS CHECK AMOUNT FROM YTD                            
*                                                                               
CNEGAMT  NTR1                                                                   
         L     R1,UTAXYTD          ADD NEGATIVE AMOUNT INTO YTD                 
         A     R1,TMNTEARN                                                      
         BNM   *+6                 BUT YTD CANNOT BE NEGATIVE                   
         XR    R1,R1                                                            
         ST    R1,UTAXYTD          & USE THIS AMOUNT FOR CALCULATING            
         ST    R1,UPPYTD                                                        
         ST    R1,UEIYTD                                                        
         ST    R1,UPIPYTD                                                       
*                                                                               
         L     R1,REMAIN                                                        
         LCR   R1,R1                                                            
         ST    R1,REMAIN                                                        
*                                                                               
*&&DO                                                                           
         LA    R1,UPPYTD                                                        
         BAS   RE,CADJYTD                                                       
         LA    R1,UEIYTD                                                        
         BAS   RE,CADJYTD                                                       
         LA    R1,UPIPYTD                                                       
         BAS   RE,CADJYTD                                                       
*&&                                                                             
         L     R1,CTAXYTD          ADD NEGATIVE AMOUNT INTO YTD                 
         A     R1,TMNTCERN                                                      
         BNM   *+6                 BUT YTD CANNOT BE NEGATIVE                   
         XR    R1,R1                                                            
         ST    R1,CTAXYTD          & USE THIS AMOUNT FOR CALCULATING            
         ST    R1,CPPYTD                                                        
         ST    R1,CEIYTD                                                        
         ST    R1,CPIPYTD                                                       
*                                                                               
         L     R1,REMAINC                                                       
         LCR   R1,R1                                                            
         ST    R1,REMAINC                                                       
*                                                                               
*&&DO                                                                           
         LA    R1,CPPYTD                                                        
         BAS   RE,CADJYTD2                                                      
         LA    R1,CEIYTD                                                        
         BAS   RE,CADJYTD2                                                      
         LA    R1,CPIPYTD                                                       
         BAS   RE,CADJYTD2                                                      
*&&                                                                             
         J     XIT                                                              
         SPACE 2                                                                
*                                                                               
*        ADJUST YTD                                                             
*              R1 - A(YTD TO ADJUST)                                            
*                                                                               
CADJYTD  NTR1                                                                   
         L     R2,0(R1)                                                         
         C     R2,UTAXYTD          IF YTD > FED YTD                             
         BNH   CAY10                                                            
         A     R2,TMNTEARN         ADD EARNINGS                                 
         C     R2,UTAXYTD          IF YTD < FED YTD                             
         BNL   CAY10                                                            
         L     R2,UTAXYTD          SET IT TO BE FED YTD                         
*                                                                               
CAY10    ST    R2,0(R1)                                                         
         J     XIT                                                              
CADJYTD2 NTR1                                                                   
         L     R2,0(R1)                                                         
         C     R2,CTAXYTD          IF YTD > FED YTD CAD                         
         BNH   CAY210                                                           
         A     R2,TMNTCERN         ADD EARNINGS CAD                             
         C     R2,UTAXYTD          IF YTD < FED YTD CAD                         
         BNL   CAY210                                                           
         L     R2,CTAXYTD          SET IT TO BE FED YTD CAD                     
*                                                                               
CAY210   ST    R2,0(R1)                                                         
         J     XIT                                                              
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*        SET TAXABLE AMOUNTS - TO BE USED IN CALCULATING NEW YTD'S              
*        THESE AMOUNTS WILL INCLUDE THE PREVIOUS LEVEL'S AMOUNTS                
*                                                                               
ADJAMT   NTR1  BASE=*,LABEL=*                                                   
         TM    TMSTAT2,TMSPCAN     P+ CANADIAN TAXES?                           
         BO    AT20                                                             
*                                                                               
         MVC   TMTFUI,FUITAXBL     FUI TAXABLE INCLUDES                         
*                                                                               
         L     R1,SUITAXBL         SUI TAXABLE INCLUDES                         
         A     R1,TMTFUI           FUI TAXABLE                                  
         ST    R1,TMTSUI                                                        
*                                  CALCULATE FICA TAXABLE WAGES                 
         L     R1,TMTSUI           SUM OF SUI TAXABLE WAGES                     
         A     R1,FICTAXBL         + FICA TAXABLE WAGES                         
         LR    R0,R1               (SAVE IT)                                    
*                                  ENSURE WE DON'T EXCEED WAGE BASE             
*                                                                               
         L     RF,TMTXEARN                                                      
         LTR   RF,RF                                                            
         BP    AT02                IS IT A VOID?                                
*                                                                               
         L     RF,TMTXEARN         TAXABLE EARNINGS                             
         A     RF,TMYEARN          + OLD YTD TAXABLE EARNINGS                   
         C     RF,TMBFICA          IS EARNINGS > FICA WAGE BASE?                
         BNL   AT04                YES - DO NOT ADJUST FICA                     
*                                                                               
AT02     A     R0,TMYFICA          ADD IN PREV FICA YTD WAGES                   
         S     R0,TMBFICA          SUBTRACT FICA WAGE BASE                      
         BM    *+6                 IF WE EXCEEDED WAGE BASE THEN                
         SR    R1,R0               REDUCE FICA TAXABLE WAGES BY EXCESS          
         ST    R1,TMTFICA                                                       
*                                  CALCULATE MEDICARE TAXABLE WAGES             
AT04     L     R1,TMTSUI           SUM OF SUI TAXABLE WAGES                     
         A     R1,FICTAXBL         + FICA TAXABLE WAGES                         
         A     R1,MEDTAXBL         + MEDICARE TAXABLE WAGES                     
**NO-OP*                           NO MORE MEDICARE WAGE BASE                   
*        LR    R0,R1               (SAVE IT)                                    
*                                  ENSURE WE DON'T EXCEED WAGE BASE             
*        A     R0,TMYMED           ADD IN PREV MEDICARE YTD WAGES               
*        S     R0,TMBMED           SUBTRACT MEDICARE WAGE BASE                  
*        BM    *+6                 IF WE EXCEEDED WAGE BASE THEN                
**NO-OP* SR    R1,R0               REDUCE MED TAXABLE WAGES BY EXCESS           
         ST    R1,TMTMED                                                        
*                                                                               
         L     R1,OMDTAXBL         OVER MEDICARE TAXABLE INCLUDES               
         A     R1,TMTMED           MEDICARE TAXABLE                             
         ST    R1,TMTOMED                                                       
*                                                                               
         L     R1,TMTTOTAL         SUM OF BROKEN DOWN TAXABLE AMOUNTS           
         LA    R2,FUITAXBL                                                      
         LA    R3,TMTNAMT-1        EQUALS TOTAL TAXABLE                         
*                                                                               
AT10     A     R1,0(R2)            ADD EACH TAXABLE PART TO TOTAL               
         LA    R2,4(R2)            BUMP TO NEXT AMT                             
         BCT   R3,AT10                                                          
         ST    R1,TMTTOTAL                                                      
*                                                                               
         C     R1,TMTXEARN         SAFETY CHECK                                 
         BE    *+6                                                              
         DC    H'0'                                                             
         B     ATX                                                              
*                                                                               
AT20     MVC   TMCTTOTA,TMNTEARN                                                
         GOTOR CONVUSD,DMCB,CPPTXBL,TMCTPP                                      
         GOTOR CONVUSD,DMCB,CEITXBL,TMCTEI                                      
         GOTOR CONVUSD,DMCB,CPIPTXBL,TMCTPIP                                    
*                                                                               
         MVC   TMCCTTOT,TMNTCERN                                                
         MVC   TMCCTPP,CPPTXBL                                                  
         MVC   TMCCTEI,CEITXBL                                                  
         MVC   TMCCTPIP,CPIPTXBL                                                
*                                                                               
ATX      J     XIT                                                              
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
* CONVERT TO CANADIAN DOLLARS                                                   
*        ON ENTRY, PARAM 1 = FIELD TO BE CONVERTED                              
*                  PARAM 2 = CONVERTED LOCATION                                 
*                                                                               
CONVCAD  NTR1  BASE=*,LABEL=*                                                   
         L     R2,0(R1)                                                         
         L     R3,4(R1)                                                         
*                                                                               
         ZAP   DUB,TMCCVT                                                       
         CVB   R1,DUB                                                           
         ST    R1,FULL                                                          
*                                                                               
         ICM   R1,15,0(R2)                                                      
         TM    0(R2),X'80'         NEGATIVE? (VOID/CREDIT/CANCEL)               
         BZ    *+6                                                              
         LPR   R1,R1                                                            
*                                                                               
         XR    R0,R0                                                            
         M     R0,=F'10000'                                                     
         D     R0,FULL                                                          
*                                                                               
         L     RE,FULL                                                          
         AHI   RE,1                                                             
         SRA   RE,1                                                             
         CR    R0,RE                                                            
         BL    *+8                                                              
         AHI   R1,1                                                             
*                                                                               
         TM    0(R2),X'80'         NEGATIVE? (VOID/CREDIT/CANCEL)               
         BZ    *+6                                                              
         LNR   R1,R1                                                            
*                                                                               
         STCM  R1,15,0(R3)                                                      
         J     XIT                                                              
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
* CONVERT TO US DOLLARS                                                         
*        ON ENTRY, PARAM 1 = FIELD TO BE CONVERTED                              
*                  PARAM 2 = CONVERTED LOCATION                                 
*                                                                               
CONVUSD  NTR1  BASE=*,LABEL=*                                                   
         L     R2,0(R1)                                                         
         L     R3,4(R1)                                                         
*                                                                               
         ZAP   DUB,TMCCVT                                                       
         CVB   R1,DUB                                                           
         ST    R1,FULL                                                          
*                                                                               
         ICM   R1,15,0(R2)                                                      
         TM    0(R2),X'80'         NEGATIVE? (VOID/CREDIT/CANCEL)               
         BZ    *+6                                                              
         LPR   R1,R1                                                            
*                                                                               
         XR    R0,R0                                                            
         M     R0,FULL                                                          
         D     R0,=F'5000'                                                      
*                                                                               
         LTR   R1,R1                                                            
         BM    *+8                                                              
         AH    R1,=H'1'                                                         
         SRA   R1,1                                                             
*                                                                               
         TM    0(R2),X'80'         NEGATIVE? (VOID/CREDIT/CANCEL)               
         BZ    *+6                                                              
         LNR   R1,R1                                                            
*                                                                               
         STCM  R1,15,0(R3)                                                      
         J     XIT                                                              
         LTORG                                                                  
         EJECT                                                                  
*        CALCULATE TAXES                                                        
*                                                                               
CTAX     NTR1  BASE=*,LABEL=*                                                   
***      L     R2,TMTXEARN                                                      
***      GOTOR CALCP,DMCB,TRATE,(R2),TMXTOTAL                                   
         L     R6,TMTXEARN                                                      
**NO-OP  CLC   TRATE,TMBRFUI       USING FUTA FIELD FOR TAX RATE?               
         CLI   BYTE,C'F'           USING FUTA FIELD FOR TAX RATE?               
         BNE   CTAX03                                                           
         CLI   TMBILTYP,TABRTY6    IF TYPE 6 REUSE (USES FUTA)                  
         BNE   CTAX04                                                           
         CLI   TMOFCDE,C'O'        AND NOT OFFICE O                             
         BE    CTAX04                                                           
         CLI   TMOFCDE,C'Q'        OR OFFICE Q                                  
         BE    CTAX04                                                           
         USING TARAD,RE                                                         
*        L     RE,TMACURRA         RE=A(BILLING RATES ELEMENT)                  
         L     RE,TMACPYRA         RE=A(BILLING RATES ELEMENT)                  
         LTR   RE,RE               DO WE HAVE NEW RATES?                        
         BZ    CTAX04                                                           
         XC    TARATWC,TARATWC     DON'T CHARGE WC ON REUSE PAYMENTS            
         B     CTAX04                                                           
         DROP  RE                                                               
*                                  IF TYPE 6 SESSION (USES SUTA)                
         USING TARAD,RE                                                         
*TAX03   L     RE,TMACURRA         RE=A(BILLING RATES ELEMENT)                  
CTAX03   L     RE,TMACPYRA         RE=A(BILLING RATES ELEMENT)                  
         LTR   RE,RE               DO WE HAVE NEW RATES?                        
         BZ    CTAX10                                                           
         DROP  RE                                                               
*                                                                               
CTAX04   TM    RATESTAT,RTSTNOLM   IF BRATE HAS NO LIMIT,                       
         BZ    CTAX05              USE BASE AMOUNT                              
         GOTOR CALCTAX2,DMCB,('CTAXFUTA',0),(R6),('CTAXBASE',0)                 
         B     CTAX20                                                           
*                                                                               
CTAX05   GOTOR CALCTAX2,DMCB,('CTAXFUTA',0),(R6),0  CALCULATE FUTA              
         B     CTAX20                                                           
*                                  SESSIONS WITH NO NEW RATES (TYP6)            
CTAX10   TM    RATESTAT,RTSTNOLM   IF BRATE HAS NO LIMIT,                       
         BZ    CTAX15              USE BASE AMOUNT                              
         GOTOR CALCTAX2,DMCB,('CTAXSUTA',0),(R6),('CTAXBASE',0)                 
         B     CTAX20                                                           
CTAX15   GOTOR CALCTAX2,DMCB,('CTAXSUTA',0),(R6),0  CALCULATE SUTA              
*                                                                               
CTAX20   BRAS  RE,WCTAX            CALCULATE WC ON CORPS                        
         J     XIT                                                              
         LTORG                                                                  
         EJECT                                                                  
*        CALCULATE WORKMEN'S COMP. TAXES ON CORPS                               
*                                                                               
WCTAX    NTR1  BASE=*,LABEL=*                                                   
         CLI   TMW4TYPE,TAW4TYCA   IF W4 IS A CANADIAN                          
         BE    *+12                                                             
         CLI   TMW4TYPE,TAW4TYCO   OR CORPORATION                               
         BNE   WCTAXX                                                           
*                                  INCLUDE AS OF 1/1/03                         
*        CLI   TGOFF,C'7'          AND NOT OFFICE 7 / G                         
*        BE    XIT                                                              
*        CLI   TGOFF,C'G'                                                       
*        BE    XIT                                                              
         L     R2,TMNTEARN         R2=NON-TAXABLE EARNINGS                      
         S     R2,TMREXP           NEVER WC ON REIMB EXP (8/21/09)              
*                                                                               
         CLI   TMBILTYP,TABRTY15   IF BILLING TYPE 15                           
         BNE   WCTAX2                                                           
         L     R5,TMNTEARN                                                      
         A     R5,TMYNTAX                                                       
         C     R5,=F'20000000'     IF YTD + THIS CHECK > 200,000                
         BH    WCTAXX              DO NOT CHARGE WCTAX                          
         B     WCTAX10                                                          
*                                                                               
WCTAX2   CLI   TMBILTYP,TABRTY14   IF BILLING TYPE 14                           
         BNE   WCTAX8                                                           
         TM    TGUSSTAT,SESSION    AND IT IS A RE-USE PAYMENT                   
         BZ    WCTAXX              DON'T CHARGE WORKMEN'S COMP.                 
*                                  ELSE, CHARGE ON FIRST 100,000 ONLY           
         L     R5,TMNTEARN                                                      
         A     R5,TMYNTAX                                                       
         C     R5,=F'10000000'     IF YTD + THIS CHECK < 100,000                
         BH    WCTAX5                                                           
         CLC   TMYNTAX,=F'10000000' AND IF YTD <= 100,000                       
         BNH   WCTAX10             CHARGE WC ON THIS CHECK                      
         L     R2,=F'10000000'     ELSE CHARGE ON AMT UP TO MAX                 
         SR    R2,R5                                                            
         LTR   R2,R2                                                            
         BZ    WCTAXX                                                           
         TM    TMNTEARN,X'80'                                                   
         BZ    *+6                                                              
         LCR   R2,R2                                                            
         B     WCTAX10                                                          
*                                                                               
WCTAX5   CLC   TMYNTAX,=F'10000000' IF YTD > 100,000                            
         BH    WCTAXX              THEN DON'T CHARGE WCTAX ANYMORE              
         L     R2,=F'10000000'     ELSE, CHARGE ON AMT UP TO MAX                
         S     R2,TMYNTAX                                                       
         B     WCTAX10                                                          
*                                                                               
WCTAX8   CLI   TMBILTYP,TABRTY6    IF BILLING TYPE 6                            
         BNE   WCTAX10                                                          
         CLI   TMOFCDE,C'O'        AND NOT OFFICE O                             
         BE    WCTAX10                                                          
         CLI   TMOFCDE,C'Q'        OR OFFICE Q                                  
         BE    WCTAX10                                                          
         TM    TGUSSTAT,SESSION    AND IT IS A RE-USE PAYMENT                   
         BZ    WCTAXX              DON'T CHARGE WORKMEN'S COMP.                 
*                                                                               
**TAX10  GOTOR CALCP,DMCB,WCTRATE,(R2),TMXTOTAL                                 
WCTAX10  GOTOR CALCTAX2,DMCB,('CTAXWCC',0),(R2),0 CALCULATE WC CORP             
WCTAXX   J     XIT                                                              
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        ROUTINE CALCULATES HANDLING BASED ON SUPPLIED PARAMETERS     *         
*        ON ENTRY ... P1 BYTE 0 = CALCULATION INSTRUCTIONS            *         
*                                 (SEE EQUATES BELOW)                 *         
*                     P2        = BASE AMOUNT + ANY GRT APPLIED AMT   *         
*                     P3        = GRT APPLIED AMT                     *         
***********************************************************************         
                                                                                
CALCHAND NTR1  BASE=*,LABEL=*                                                   
                                                                                
         MVC   TGBYTE,0(R1)        TGBYTE = CALCULATION INSTRUCTIONS            
         L     R2,4(R1)            R2 = BASE AMOUNT                             
         MVC   FULL,8(R1)          FULL = GRT APPLIED AMOUNT                    
                                                                                
         USING TARAD,RE                                                         
*        L     RE,TMACURRA         RE=A(BILLING RATES ELEMENT)                  
         L     RE,TMACPYRA         RE=A(BILLING RATES ELEMENT)                  
         LTR   RE,RE               IF NEW WAY, DEDUCT GRT APPL AMT              
         JZ    *+8                                                              
         S     R2,FULL                                                          
                                                                                
         USING TAPGD,RF                                                         
         L     RF,TMACURPG         RF=A(POSTING DETAILS ELEMENT)                
                                                                                
         CLI   TGBYTE,CHANDII      IF USING IND HAND RATE TO CALC               
         JNE   CHAND10             IND HANDLING ...                             
         MVC   TGFULL,TMBRHAND     TGFULL=HANDLING RATE                         
         LA    R3,TMOHAND          R3=A(OLD HANDLING FIELD)                     
         LTR   RE,RE                                                            
         JZ    CHAND60                                                          
         MVC   TGFULL,TARAHFB      BASIC HANDLING                               
         CLI   TMBILTYP,TABRTY21   BILL TYPES 21 AND 23, USE REUSE RATE         
         JE    CHAND05                                                          
         CLI   TMBILTYP,TABRTY23                                                
         JNE   *+10                                                             
CHAND05  MVC   TGFULL,TARARFB      BASIC REUSE HANDLING                         
         TM    TARASTA2,TARASSHD   IS HANDLING IN DOLLARS?                      
         JNZ   CHAND65                                                          
         LA    R4,TAPGHNDI         R4=A(NEW HANDLING FIELD)                     
         J     CHAND60                                                          
                                                                                
CHAND10  CLI   TGBYTE,CHANDIC      IF USING IND HAND RATE TO CALC               
         JNE   CHAND20             CORP HANDLING                                
         MVC   TGFULL,TMBRHAND     SET ADDRESSES OF RATES AND TOTALS            
         LA    R3,TMOCORP                                                       
         LTR   RE,RE                                                            
         JZ    CHAND60                                                          
         MVC   TGFULL,TARAHFB      BASIC HANDLING                               
         TM    TARASTA2,TARASSHD   IS HANDLING IN DOLLARS?                      
         JNZ   CHAND65                                                          
         LA    R4,TAPGHNDC                                                      
         J     CHAND60                                                          
                                                                                
CHAND20  CLI   TGBYTE,CHANDSI      IF USING SUI RATE TO CALC IND                
         JNE   CHAND30             HANDLING                                     
         MVC   TGFULL,TMBRSUI      SET ADDRESSES OF RATES AND TOTALS            
         LA    R3,TMOHAND                                                       
         LTR   RE,RE                                                            
         JZ    CHAND60                                                          
         MVC   TGFULL,TARAHFB                                                   
         TM    TARASTA2,TARASSHD   IS HANDLING IN DOLLARS?                      
         JNZ   CHAND65                                                          
         LA    R4,TAPGHNDI                                                      
         J     CHAND60                                                          
                                                                                
CHAND30  CLI   TGBYTE,CHANDCC      IF USING CORP HAND RATE TO CALC              
         JNE   CHAND40             CORP HANDLING                                
         MVC   TGFULL,TMBRCORP     SET ADDRESSES OF RATES AND TOTALS            
         LA    R3,TMOCORP                                                       
         LTR   RE,RE                                                            
         JZ    CHAND60                                                          
         MVC   TGFULL,TARAHFB      BASIC HANDLING                               
         CLI   TMBILTYP,TABRTY24   BILL TYPE 24, USE REUSE RATE                 
         JNE   *+10                                                             
         MVC   TGFULL,TARARFB      BASIC REUSE HANDLING                         
         TM    TARASTA2,TARASSHD   IS HANDLING IN DOLLARS?                      
         JNZ   CHAND65                                                          
         LA    R4,TAPGHNDC                                                      
         J     CHAND60                                                          
                                                                                
CHAND40  CLI   TGBYTE,CHANDAA      IF USING CAN HAND RATE TO CALC               
         JNE   CHAND45             CAN HANDLING                                 
         MVC   TGFULL,TMBRCAN      SET ADDRESSES OF RATES AND TOTALS            
         LA    R3,TMOCAN                                                        
         LTR   RE,RE                                                            
         JZ    CHAND60                                                          
         MVC   TGFULL,TARAHFBC     BASIC CANADIAN HANDLING                      
         TM    TGUSSTAT,SESSION    IF REUSE PAYMENT,                            
         JO    CHAND44                                                          
         CLI   TMBILTYP,TABRTY21   AND BILL TYPE 21, USE REUSE RATE             
         JE    CHAND43                                                          
         CLI   TMBILTYP,TABRTY23   OR BILL TYPE 23, USE REUSE RATE              
         JNE   CHAND44                                                          
CHAND43  MVC   TGFULL,TARARFBC     BASIC CAN REUSE HANDLING                     
CHAND44  TM    TARASTA2,TARASSCD   IS HANDLING IN DOLLARS?                      
         JNZ   CHAND65                                                          
         LA    R4,TAPGHNDC                                                      
         J     CHAND60                                                          
                                                                                
CHAND45  CLI   TGBYTE,CHANDCA      IF USING CORP HAND RATE TO CALC              
         JNE   CHAND50             CAN HANDLING                                 
         MVC   TGFULL,TMBRCORP     SET ADDRESSES OF RATES AND TOTALS            
         LA    R3,TMOCAN                                                        
         LTR   RE,RE                                                            
         JZ    CHAND60                                                          
         CLI   TMCURR,C'C'         IF CANADIAN $                                
         BNE   CHAND47                                                          
         MVC   TGFULL,TARARFBC     USE CANADIAN REUSE RATE                      
         TM    TARASTA2,TARASSCD   IS CAN HANDLING IN DOLLARS?                  
         JNZ   CHAND65                                                          
         J     CHAND48                                                          
CHAND47  MVC   TGFULL,TARARFB      IF EURO $                                    
         TM    TARASTA2,TARASSHD   IS HANDLING IN DOLLARS?                      
         JNZ   CHAND65                                                          
CHAND48  LA    R4,TAPGHNDC                                                      
         J     CHAND60                                                          
                                                                                
CHAND50  CLI   TGBYTE,CHANDIA      IF USING IND HAND RATE TO CALC               
         JNE   CHAND55             CAN HANDLING                                 
         MVC   TGFULL,TMBRHAND     SET ADDRESSES OF RATES AND TOTALS            
         LA    R3,TMOCAN                                                        
         LTR   RE,RE                                                            
         JZ    CHAND60                                                          
         CLI   TMCURR,C'C'         IF CANADIAN $                                
         BNE   CHAND51                                                          
         MVC   TGFULL,TARAHFBC     BASIC CANADIAN SESSION HANDLING              
         TM    TARASTA2,TARASSCD   IS CANADIAN HANDLING IN DOLLARS?             
         JNZ   CHAND65                                                          
         B     CHAND54                                                          
*                                  IF EURO $                                    
CHAND51  MVC   TGFULL,TARAHFB      BASIC SESSION HANDLING                       
         TM    TGUSSTAT,SESSION    IF REUSE PAYMENT,                            
         JO    CHAND53                                                          
         CLI   TMBILTYP,TABRTY21   AND BILL TYPE 21, USE REUSE RATE             
         JE    CHAND52                                                          
         CLI   TMBILTYP,TABRTY23   OR BILL TYPE 23, USE REUSE RATE              
         JNE   CHAND53                                                          
CHAND52  MVC   TGFULL,TARARFB      BASIC REUSE HANDLING                         
CHAND53  TM    TARASTA2,TARASSHD   IS HANDLING IN DOLLARS?                      
         JNZ   CHAND65                                                          
CHAND54  LA    R4,TAPGHNDC                                                      
         J     CHAND60                                                          
                                                                                
CHAND55  CLI   TGBYTE,CHANDCI      IF USING CORP HAND RATE TO CALC              
         JNE   CHAND57             IND HANDLING ...                             
         MVC   TGFULL,TMBRCORP                                                  
         LA    R3,TMOHAND                                                       
         LTR   RE,RE                                                            
         JZ    CHAND60                                                          
         MVC   TGFULL,TARAHFB      BASIC HANDLING                               
         CLI   TMBILTYP,TABRTY24   BILL TYPE 24, USE REUSE RATE                 
         JNE   *+10                                                             
         MVC   TGFULL,TARARFB                                                   
         TM    TARASTA2,TARASSHD   IS HANDLING IN DOLLARS?                      
         JNZ   CHAND65                                                          
         LA    R4,TAPGHNDI                                                      
         J     CHAND60                                                          
                                                                                
CHAND57  CLI   TGBYTE,CHANDIP      IF USING IND HAND RATE TO CALC               
         JE    *+6                 PAYROLL TAX                                  
         DC    H'00'               SET ADDRESSES OF RATES AND TOTALS            
         LA    R3,TMXTOTAL         R3=A(OLD P/R TAX FIELD)                      
         TM    TARASTA2,TARASSPD   IF PREM HANDLING IN DOLLARS,                 
         JZ    *+8                 SAVE BASIC HANDLING AS HANDLING              
         LA    R3,TMOHAND          R3=A(OLD HANDLING FIELD)                     
         LTR   RE,RE               SKIP IF NO BRATE                             
         JZ    XIT                                                              
         MVC   TGFULL,TARAHFB      BASIC HANDLING                               
         TM    TARASTA2,TARASSHD   IS HANDLING IN DOLLARS?                      
         JNZ   CHAND65                                                          
         LA    R4,TAPGHNDI         R4=A(NEW HANDLING FIELD)                     
         J     CHAND60                                                          
         DROP  RE,RF                                                            
                                                                                
CHAND60  CLC   TGFULL,=XL4'FFFFFFFF'   IS RATE = ZERO?                          
         BE    CHAND62                                                          
                                                                                
         GOTOR CALCP,DMCB,TGFULL,(R2),(R3)  CALCULATE BASIC HANDLING            
                                                                                
*HAND62  L     RE,TMACURRA                                                      
CHAND62  L     RE,TMACPYRA                                                      
         LTR   RE,RE               IF CALCULATING HANDLING BASED                
         JZ    XIT                 ON NEW BILLING RATE ELEMENT                  
         CLC   TGFULL,=XL4'FFFFFFFF'    IS RATE = ZERO?                         
         JE    CHAND63                                                          
         CLI   TMEMSFLG,3          IF 2ND PASS, CALCULATED ALREADY              
         JE    CHAND63                                                          
         L     R5,0(R4)            SAVE IN NEW AMOUNT FIELD TOO                 
         A     R5,0(R1)                                                         
         ST    R5,0(R4)                                                         
*                                                                               
         USING TAPGD,RF                                                         
CHAND63  L     RF,TMACURPG         RF=A(POSTING DETAILS ELEMENT)                
         OC    FULL,FULL           ANY APPLIED GRT?                             
         JZ    CHAND65                                                          
         LA    R1,TAPGHNDI                                                      
         CR    R4,R1               IND HANDLING OR CORP HANDLING?               
         JNE   CHAND64                                                          
         LA    R4,TAPGHIAG         IND HANDLING ON APPLIED GRT                  
         J     *+8                                                              
CHAND64  LA    R4,TAPGHCAG         CORP HANDLING ON APPLIED GRT                 
         L     R5,FULL             APPLIED GRT                                  
         DROP  RF                                                               
*                                                                               
         CLC   TGFULL,=XL4'FFFFFFFF'    IS RATE = ZERO?                         
         JE    CHAND65                                                          
         GOTOR CALCP,DMCB,TGFULL,(R5),(R3)  CALCULATE GRT HANDLING              
         CLI   TMEMSFLG,3          IF 2ND PASS, CALCULATED ALREADY              
         JE    CHAND65                                                          
         L     R5,0(R4)            SAVE IN NEW AMOUNT FIELD TOO                 
         A     R5,0(R1)                                                         
         ST    R5,0(R4)                                                         
*                                                                               
         USING TARAD,RE                                                         
         USING TAPGD,RF                                                         
CHAND65  L     RF,TMACURPG         RF=A(POSTING DETAILS ELEMENT)                
*        L     RE,TMACURRA         RE=A(BILLING RATES ELEMENT)                  
         L     RE,TMACPYRA         RE=A(BILLING RATES ELEMENT)                  
                                                                                
         XC    TGFULL,TGFULL       NOW SET TO CALC PREM SERVICES HAND           
                                                                                
         CLI   TGBYTE,CHANDII      IF USING IND HAND RATE TO CALC               
         JNE   CHAND70             IND HANDLING ...                             
         MVC   TGFULL,TARAHFP                                                   
         CLI   TMBILTYP,TABRTY21   BILL TYPES 21 AND 23, USE REUSE RATE         
         JE    CHAND68                                                          
         CLI   TMBILTYP,TABRTY23                                                
         JNE   *+10                                                             
CHAND68  MVC   TGFULL,TARARFP      PREMIUM REUSE HANDLING                       
         TM    TARASTA2,TARASSPD   IF RATE IS IN DOLLARS, EXIT                  
         JNZ   XIT                                                              
         LA    R3,TMOHAND          SET ADDRESSES OF RATES AND TOTALS            
         LA    R4,TAPGHPRI         SET ADDRESSES OF RATES AND TOTALS            
         J     CHAND110                                                         
                                                                                
CHAND70  CLI   TGBYTE,CHANDIC      IF USING IND HAND RATE TO CALC               
         JNE   CHAND75             CRP HANDLING ...                             
         MVC   TGFULL,TARAHFP                                                   
         TM    TARASTA2,TARASSPD   IF RATE IS IN DOLLARS, EXIT                  
         JNZ   XIT                                                              
         LA    R3,TMOCORP          SET ADDRESSES OF RATES AND TOTALS            
         LA    R4,TAPGHPRC         SET ADDRESSES OF RATES AND TOTALS            
         J     CHAND110                                                         
                                                                                
CHAND75  CLI   TGBYTE,CHANDSI      IF USING SUTA RATE TO CALC                   
         JNE   CHAND80             IND HANDLING ...                             
         MVC   TGFULL,TARAHFP                                                   
         TM    TARASTA2,TARASSPD   IF RATE IS IN DOLLARS, EXIT                  
         JNZ   XIT                                                              
         LA    R3,TMOHAND          SET ADDRESSES OF RATES AND TOTALS            
         LA    R4,TAPGHPRI         SET ADDRESSES OF RATES AND TOTALS            
         J     CHAND110                                                         
                                                                                
CHAND80  CLI   TGBYTE,CHANDCC      IF USING CRP HAND RATE TO CALC               
         JNE   CHAND90             CRP HANDLING ...                             
         MVC   TGFULL,TARAHFP      PREMIUM HANDLING                             
         CLI   TMBILTYP,TABRTY24   BILL TYPE 24, USE REUSE RATE                 
         JNE   *+10                                                             
         MVC   TGFULL,TARARFP      PREMIUM REUSE HANDLING                       
         TM    TARASTA2,TARASSPD   IF RATE IS IN DOLLARS, EXIT                  
         JNZ   XIT                                                              
         LA    R3,TMOCORP          SET ADDRESSES OF RATES AND TOTALS            
         LA    R4,TAPGHPRC         SET ADDRESSES OF RATES AND TOTALS            
         J     CHAND110                                                         
                                                                                
CHAND90  CLI   TGBYTE,CHANDAA      IF USING CAN HAND RATE TO CALC               
         JNE   CHAND95             CAN HANDLING ...                             
         MVC   TGFULL,TARAHFP                                                   
         TM    TGUSSTAT,SESSION    IF REUSE PAYMENT,                            
         JO    CHAND94                                                          
         CLI   TMBILTYP,TABRTY21   AND BILL TYPE 21, USE REUSE RATE             
         JE    CHAND93                                                          
         CLI   TMBILTYP,TABRTY23   OR BILL TYPE 23, USE REUSE RATE              
         JNE   CHAND94                                                          
CHAND93  MVC   TGFULL,TARARFP      PREM CAN REUSE HANDLING                      
CHAND94  TM    TARASTA2,TARASSPD   IF RATE IS IN DOLLARS, EXIT                  
         JNZ   XIT                                                              
         LA    R3,TMOCAN           SET ADDRESSES OF RATES AND TOTALS            
         LA    R4,TAPGHPRC                                                      
         J     CHAND110                                                         
                                                                                
CHAND95  CLI   TGBYTE,CHANDCA      IF USING CRP HAND RATE TO CALC               
         JNE   CHAND100            CAN HANDLING ...                             
         MVC   TGFULL,TARARFP                                                   
         TM    TARASTA2,TARASSPD   IF RATE IS IN DOLLARS, EXIT                  
         JNZ   XIT                                                              
         LA    R3,TMOCAN           SET ADDRESSES OF RATES AND TOTALS            
         LA    R4,TAPGHPRC                                                      
         J     CHAND110                                                         
                                                                                
CHAND100 CLI   TGBYTE,CHANDIA      IF USING IND HAND RATE TO CALC               
         JNE   CHAND105            CAN HANDLING ...                             
         MVC   TGFULL,TARAHFP                                                   
         TM    TGUSSTAT,SESSION    IF REUSE PAYMENT,                            
         JO    CHAND104                                                         
         CLI   TMBILTYP,TABRTY21   AND BILL TYPE 21, USE REUSE RATE             
         JE    CHAND103                                                         
         CLI   TMBILTYP,TABRTY23   OR BILL TYPE 23, USE REUSE RATE              
         JNE   CHAND104                                                         
CHAND103 MVC   TGFULL,TARARFP      PREMIUM REUSE HANDLING                       
CHAND104 TM    TARASTA2,TARASSPD   IF RATE IS IN DOLLARS, EXIT                  
         JNZ   XIT                                                              
         LA    R3,TMOCAN           SET ADDRESSES OF RATES AND TOTALS            
         LA    R4,TAPGHPRC                                                      
         J     CHAND110                                                         
                                                                                
CHAND105 CLI   TGBYTE,CHANDCI      IF USING CRP HAND RATE TO CALC               
         JNE   CHAND108            CAN HANDLING ...                             
         MVC   TGFULL,TARAHFP      PREMIUM HANDLING                             
         CLI   TMBILTYP,TABRTY24   BILL TYPE 24, USE REUSE RATE                 
         JNE   *+10                                                             
         MVC   TGFULL,TARARFP                                                   
         TM    TARASTA2,TARASSPD   IF RATE IS IN DOLLARS, EXIT                  
         JNZ   XIT                                                              
         LA    R3,TMOHAND          SET ADDRESSES OF RATES AND TOTALS            
         LA    R4,TAPGHPRI         SET ADDRESSES OF RATES AND TOTALS            
         J     CHAND110                                                         
                                                                                
CHAND108 CLI   TGBYTE,CHANDIP      IF USING IND HAND RATE TO CALC               
         JE    *+6                 PAYROLL TAX                                  
         DC    H'00'               SET ADDRESSES OF RATES AND TOTALS            
         MVC   TGFULL,TARAHFP      PREMIUM HANDLING                             
         TM    TARASTA2,TARASSPD   IS PREM HANDLING IN DOLLARS?                 
         JNZ   XIT                                                              
         LA    R3,TMXTOTAL         SAVE PREM HANDLING IN P/R TAX                
         TM    TARASTA2,TARASSHD   IF BASIC HANDLING IS IN DOLLARS,             
         JZ    *+8                                                              
         LA    R3,TMOHAND          SAVE PREM HANDLING IN HANDLING               
         LA    R4,TAPGHPRI         R4=A(NEW HANDLING FIELD)                     
         J     CHAND110                                                         
         DROP  RE,RF                                                            
                                                                                
CHAND110 OC    TGFULL,TGFULL                                                    
         JZ    XIT                                                              
         CLC   TGFULL,=XL4'FFFFFFFF'     IS RATE = ZERO?                        
         JE    XIT                                                              
                                                                                
         GOTOR CALCP,DMCB,TGFULL,(R2),(R3)                                      
*                                                                               
         CLI   TMEMSFLG,3          IF 2ND PASS, CALCULATED ALREADY              
         JE    XIT                                                              
         L     R5,0(R4)            SAVE IN NEW AMOUNT FIELD TOO                 
         A     R5,0(R1)                                                         
         ST    R5,0(R4)                                                         
                                                                                
         USING TAPGD,RF                                                         
         L     RF,TMACURPG         RF=A(POSTING DETAILS ELEMENT)                
         OC    FULL,FULL           ANY APPLIED GRT?                             
         JZ    XIT                                                              
         LA    R1,TAPGHPRI                                                      
         CR    R4,R1               IND HANDLING OR CORP HANDLING?               
         JNE   CHAND114                                                         
         LA    R4,TAPGHIAP         IND HANDLING ON APPLIED GRT PREM             
         J     *+8                                                              
CHAND114 LA    R4,TAPGHCAP         CORP HANDLING ON APPLIED GRT PREM            
         L     R5,FULL             APPLIED GRT                                  
         DROP  RF                                                               
*                                                                               
         CLC   TGFULL,=XL4'FFFFFFFF'    IS RATE = ZERO?                         
         JE    XIT                                                              
         GOTOR CALCP,DMCB,TGFULL,(R5),(R3)  CALCULATE GRT HANDLING PREM         
         L     R5,0(R4)            SAVE IN NEW AMOUNT FIELD TOO                 
         A     R5,0(R1)                                                         
         ST    R5,0(R4)                                                         
         J     XIT                                                              
                                                                                
***********************************************************************         
*        CONSTANTS AND LITERALS                                       *         
***********************************************************************         
                                                                                
CHANDII  EQU   X'00'  USE IND HAND RATE TO CALC IND HANDLING                    
CHANDIC  EQU   X'01'  USE IND HAND RATE TO CALC CRP HANDLING                    
CHANDSI  EQU   X'02'  USE SUTA RATE TO CALC IND HANDLING                        
CHANDCC  EQU   X'03'  USE CRP HAND RATE TO CALC CRP HANDLING                    
CHANDAA  EQU   X'04'  USE CAN HAND RATE TO CALC CAN HANDLING                    
CHANDIA  EQU   X'05'  USE IND HAND RATE TO CALC CAN HANDLING                    
CHANDCI  EQU   X'06'  USE CRP HAND RATE TO CALC IND HANDLING                    
CHANDCA  EQU   X'07'  USE CRP HAND RATE TO CALC CAN HANDLING                    
CHANDIP  EQU   X'08'  USE IND HAND RATE TO CALC PAYROLL TAX                     
                                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        ROUTINE CALCULATES PAYROLL TAX BASED ON SUPPLIED PARAMETERS  *         
*        ON ENTRY ... P1 BYTE 0 = CALCULATION INSTRUCTIONS            *         
*                                 (SEE EQUATES BELOW)                 *         
*                     P2        = BASE AMOUNT                         *         
***********************************************************************         
                                                                                
CALCTAX2 NTR1  BASE=*,LABEL=*                                                   
         MVC   TGBYTE,0(R1)        TGBYTE = CALCULATION INSTRUCTIONS            
         MVC   TGBYTE2,8(R1)       TGBYTE2 = TAXABLE AMT INSTRUCTIONS           
         L     R2,4(R1)            R2 = BASE AMOUNT                             
                                                                                
         USING TARAD,R6                                                         
*        L     R6,TMACURRA         R6=A(BILLING RATES ELEMENT)                  
         L     R6,TMACPYRA         R6=A(BILLING RATES ELEMENT)                  
                                                                                
         USING TAPGD,RF                                                         
         L     RF,TMACURPG         RF=A(POSTING DETAILS ELEMENT)                
                                                                                
         CLI   TGBYTE,CTAXWCC      IF CALCULATING WC CORP,                      
         JE    CTAX290                                                          
         CLI   TGBYTE,CTAXFICR     IF CALCULATING FICA CREDITS,                 
         JE    CTAX300                                                          
         CLI   TGBYTE,CTAXWCOV     IF CALCULATING WC OVERCHARGE,                
         JE    CTAX310                                                          
                                                                                
         CLI   TGBYTE,CTAXFUTA     IF CALCULATING FUTA                          
         JNE   CTAX220             FUTA = FUTA + SUTA + FICA + MED + WC         
         LTR   R6,R6               IF WE ONLY HAVE OLD RATES,                   
         JNZ   CTAX210                                                          
         LA    R4,TMBRFUI                                                       
         GOTOR CALCP,DMCB,(R4),(R2),TMXFUI      CALCULATE FUTA OLD WAY          
         L     RE,TMXTOTAL         ADD TO TOTAL                                 
         A     RE,0(R1)                                                         
         ST    RE,TMXTOTAL                                                      
         J     XIT                                                              
                                                                                
CTAX210  LA    R4,TAPGFUTA         R4=A(NEW FUTA FIELD)                         
         MVC   TGFULL,TARATFU                                                   
         TM    TARASTA2,TARASFUD   IS FUTA IN DOLLARS?                          
         JNZ   CTAX218                                                          
         CLC   TGFULL,=XL4'FFFFFFFF'  IS RATE = ZERO?                           
         JE    CTAX218                                                          
                                                                                
         L     R3,TMTFICA          FICA TAXABLE AMOUNT                          
         CLI   TGBYTE2,CTAXBASE    IF USING BASE AMOUNT,                        
         JNE   *+6                                                              
         LR    R3,R2               USE BASE AMOUNT                              
         GOTOR CALCP2,DMCB,TGFULL,(R3),(R4)  CALC NEW FUTA NEW WAY              
         L     RE,TMXTOTAL         ADD TO TOTAL                                 
         A     RE,0(R1)                                                         
         ST    RE,TMXTOTAL                                                      
                                                                                
CTAX218  GOTOR CALCTAX3,DMCB,('CTAXFUTA',0),(R2)  CALC OLD FUTA NEW WAY         
         J     CTAX230                                                          
                                                                                
CTAX220  CLI   TGBYTE,CTAXSUTA     IF CALCULATING SUTA                          
         JNE   CTAX240             SUTA = SUTA + FICA + MED +WC                 
         LTR   R6,R6               IF WE ONLY HAVE OLD RATES,                   
         JNZ   CTAX230                                                          
         LA    R4,TMBRSUI                                                       
         GOTOR CALCP,DMCB,(R4),(R2),TMXSUI      CALCULATE SUTA OLD WAY          
         L     RE,TMXTOTAL         ADD TO TOTAL                                 
         A     RE,0(R1)                                                         
         ST    RE,TMXTOTAL                                                      
         J     XIT                                                              
                                                                                
CTAX230  LA    R4,TAPGSUTA         R4=A(NEW SUTA FIELD)                         
         MVC   TGFULL,TARATSU                                                   
         TM    TARASTA2,TARASSUD   IS SUTA IN DOLLARS?                          
         JNZ   CTAX238                                                          
         CLC   TGFULL,=XL4'FFFFFFFF'  IS RATE = ZERO?                           
         JE    CTAX238                                                          
                                                                                
         L     R3,TMTFICA                                                       
         CLI   TGBYTE2,CTAXBASE   IF USING BASE AMOUNT,                         
         JNE   *+6                                                              
         LR    R3,R2               USE BASE AMOUNT                              
         GOTOR CALCP2,DMCB,TGFULL,(R3),(R4)  CALC NEW SUTA NEW WAY              
         L     RE,TMXTOTAL         ADD TO TOTAL                                 
         A     RE,0(R1)                                                         
         ST    RE,TMXTOTAL                                                      
                                                                                
CTAX238  CLI   TGBYTE,CTAXSUTA     IF CALCULATING SUTA ONLY                     
         JNE   CTAX250                                                          
         GOTOR CALCTAX3,DMCB,('CTAXSUTA',0),(R2)  CALC OLD SUTA NEW WAY         
         J     CTAX250                                                          
                                                                                
CTAX240  CLI   TGBYTE,CTAXFICA     IF CALCULATING FICA                          
         JNE   CTAX260             FICA = FICA + MED + WC                       
         LTR   R6,R6               IF WE ONLY HAVE OLD RATES,                   
         JNZ   CTAX250                                                          
         LA    R4,TMBRFICA                                                      
         GOTOR CALCP,DMCB,(R4),(R2),TMXFICA  CALC FICA OLD WAY                  
         L     RE,TMXTOTAL         ADD TO TOTAL                                 
         A     RE,0(R1)                                                         
         ST    RE,TMXTOTAL                                                      
         J     XIT                                                              
                                                                                
CTAX250  LA    R4,TAPGFICA         R4=A(NEW FICA FIELD)                         
         MVC   TGFULL,TARATFI                                                   
         TM    TARASTA2,TARASFID   IS FICA IN DOLLARS?                          
         JNZ   CTAX258                                                          
         CLC   TGFULL,=XL4'FFFFFFFF'  IS RATE = ZERO?                           
         JE    CTAX258                                                          
                                                                                
         L     R3,TMTFICA                                                       
         CLI   TGBYTE2,CTAXBASE   IF USING BASE AMOUNT,                         
         JNE   *+6                                                              
         LR    R3,R2               USE BASE AMOUNT                              
         GOTOR CALCP2,DMCB,TGFULL,(R3),(R4)  CALC NEW FICA NEW WAY              
         L     RE,TMXTOTAL         ADD TO TOTAL                                 
         A     RE,0(R1)                                                         
         ST    RE,TMXTOTAL                                                      
                                                                                
CTAX258  CLI   TGBYTE,CTAXFICA     IF CALCULATING FICA ONLY                     
         JNE   CTAX270                                                          
         GOTOR CALCTAX3,DMCB,('CTAXFICA',0),(R2)  CALC OLD FICA NEW WAY         
         J     CTAX270                                                          
                                                                                
CTAX260  CLI   TGBYTE,CTAXMED      IF CALCULATING MEDICARE                      
         JNE   CTAX280             MED = MED + WC                               
         LTR   R6,R6               IF WE ONLY HAVE OLD RATES,                   
         JNZ   CTAX270                                                          
         LA    R4,TMBRMED                                                       
         GOTOR CALCP,DMCB,(R4),(R2),TMXMED     CALC MEDICARE OLD WAY            
         L     RE,TMXTOTAL         ADD TO TOTAL                                 
         A     RE,0(R1)                                                         
         ST    RE,TMXTOTAL                                                      
         J     XIT                                                              
                                                                                
CTAX270  LA    R4,TAPGMED          R4=A(NEW MEDICARE FIELD)                     
         MVC   TGFULL,TARATM                                                    
         TM    TARASTA2,TARASMED   IS MEDICARE IN DOLLARS?                      
         JNZ   CTAX278                                                          
         CLC   TGFULL,=XL4'FFFFFFFF'  IS RATE = ZERO?                           
         JE    CTAX278                                                          
                                                                                
         GOTOR CALCP2,DMCB,TGFULL,(R2),(R4)  CALCULATE NEW MED NEW WAY          
         L     RE,TMXTOTAL         ADD TO TOTAL                                 
         A     RE,0(R1)                                                         
         ST    RE,TMXTOTAL                                                      
                                                                                
CTAX278  CLI   TGBYTE,CTAXMED      IF CALCULATING MEDICARE ONLY                 
         JNE   CTAX280                                                          
         GOTOR CALCTAX3,DMCB,('CTAXMED',0),(R2)  CALC OLD MED NEW WAY           
                                                                                
CTAX280  LA    R4,TAPGWCN          R4=A(NEW WC FIELD)                           
         MVC   TGFULL,TARATWC                                                   
         TM    TARASTA2,TARASWCD   IS WC IN DOLLARS?                            
         JNZ   XIT                                                              
         CLC   TGFULL,=XL4'FFFFFFFF'  IS RATE = ZERO?                           
         JE    XIT                                                              
                                                                                
         GOTOR CALCP2,DMCB,TGFULL,(R2),(R4)  CALCULATE NEW WC NEW WAY           
         L     RE,TMXTOTAL         ADD TO TOTAL                                 
         A     RE,0(R1)                                                         
         ST    RE,TMXTOTAL                                                      
         J     XIT                                                              
                                                                                
CTAX290  LTR   R6,R6               IF WE ONLY HAVE OLD RATES,                   
         JNZ   CTAX295                                                          
         GOTOR CALCP,DMCB,WCTRATE,(R2),TMXTOTAL  CALC WC CORP OLD WAY           
         J     XIT                                                              
                                                                                
CTAX295  LA    R4,TAPGWCC          R4=A(NEW WC FIELD)                           
         MVC   TGFULL,TARATWC                                                   
                                                                                
         TM    TARASTA2,TARASWCD   IS WC IN DOLLARS?                            
         JNZ   XIT                                                              
         CLC   TGFULL,=XL4'FFFFFFFF'  IS RATE = ZERO?                           
         JE    XIT                                                              
                                                                                
         GOTOR CALCP2,DMCB,TGFULL,(R2),(R4)  CALCULATE NEW WCC NEW WAY          
         L     RE,TMXTOTAL         ADD TO TOTAL                                 
         A     RE,0(R1)                                                         
         ST    RE,TMXTOTAL                                                      
         J     XIT                                                              
                                                                                
CTAX300  LTR   R6,R6               IF WE ONLY HAVE OLD RATES,                   
         JNZ   XIT                                                              
         GOTOR CALCP,DMCB,TMBRMED,(R2),TMXFICR   CALC FICA CR OLD WAY           
         J     XIT                 NEW RATES HAVE NO FICA CREDIT                
                                                                                
CTAX310  LTR   R6,R6               IF WE ONLY HAVE OLD RATES,                   
         JNZ   XIT                                                              
         GOTOR CALCP,DMCB,TMBRWCRP,(R2),TRATE     CALC WC OVER OLD WAY          
         J     XIT                                                              
                                                                                
         DROP  R6,RF                                                            
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        ROUTINE CALCULATES PAYROLL TAX NEW WAY FOR OLD FIELDS        *         
*        R6 ---> TARA ELEMENT                                                   
*        ON ENTRY ... P1 BYTE 0 = CALCULATION INSTRUCTIONS            *         
*                                 (SEE EQUATES BELOW)                 *         
*                     P2        = BASE AMOUNT                         *         
***********************************************************************         
CALCTAX3 NTR1  BASE=*,LABEL=*                                                   
         USING TARAD,R6                                                         
         L     R2,4(R1)            BASE AMOUNT                                  
                                                                                
         MVC   FULL,TMXFUI                                                      
         CLI   TGBYTE,CTAXFUTA     IF CALCULATING FUTA                          
         JE    CT3X05              FUTA = FUTA + SUTA + FICA + MED + WC         
         MVC   FULL,TMXSUI                                                      
         CLI   TGBYTE,CTAXSUTA     IF CALCULATING SUTA                          
         JE    CT3X20              SUTA = SUTA + FICA + MED + WC                
         MVC   FULL,TMXFICA                                                     
         CLI   TGBYTE,CTAXFICA     IF CALCULATING FICA                          
         JE    CT3X40              FICA = FICA + MED + WC                       
         MVC   FULL,TMXMED                                                      
         CLI   TGBYTE,CTAXMED      IF CALCULATING MED                           
         JE    CT3X60              MED = MED + WC                               
         J     XIT                                                              
                                                                                
CT3X05   MVC   TGFULL,TARATFU                                                   
         TM    TARASTA2,TARASFUD   IS FUTA IN DOLLARS?                          
         JNZ   CT3X20                                                           
         CLC   TGFULL,=XL4'FFFFFFFF'  IS RATE = ZERO?                           
         JE    CT3X20                                                           
                                                                                
         L     R3,TMTFICA                                                       
         CLI   TGBYTE2,CTAXBASE   IF USING BASE AMOUNT,                         
         JNE   *+6                                                              
         LR    R3,R2               USE BASE AMOUNT                              
         GOTOR CALCP,DMCB,TGFULL,(R3),FULL    ADD FUTA CALC                     
                                                                                
CT3X20   MVC   TGFULL,TARATSU                                                   
         TM    TARASTA2,TARASSUD   IS SUTA IN DOLLARS?                          
         JNZ   CT3X40                                                           
         CLC   TGFULL,=XL4'FFFFFFFF'  IS RATE = ZERO?                           
         JE    CT3X40                                                           
                                                                                
         L     R3,TMTFICA                                                       
         CLI   TGBYTE2,CTAXBASE   IF USING BASE AMOUNT,                         
         JNE   *+6                                                              
         LR    R3,R2               USE BASE AMOUNT                              
         GOTOR CALCP,DMCB,TGFULL,(R3),FULL    ADD SUTA CALC                     
                                                                                
CT3X40   MVC   TGFULL,TARATFI                                                   
         TM    TARASTA2,TARASFID   IS FICA IN DOLLARS?                          
         JNZ   CT3X60                                                           
         CLC   TGFULL,=XL4'FFFFFFFF'  IS RATE = ZERO?                           
         JE    CT3X60                                                           
                                                                                
         L     R3,TMTFICA                                                       
         CLI   TGBYTE2,CTAXBASE   IF USING BASE AMOUNT,                         
         JNE   *+6                                                              
         LR    R3,R2               USE BASE AMOUNT                              
         GOTOR CALCP,DMCB,TGFULL,(R3),FULL    ADD FICA CALC TO FUTA             
                                                                                
CT3X60   MVC   TGFULL,TARATM                                                    
         TM    TARASTA2,TARASMED   IS MEDICARE IN DOLLARS?                      
         JNZ   CT3X80                                                           
         CLC   TGFULL,=XL4'FFFFFFFF'  IS RATE = ZERO?                           
         JE    CT3X80                                                           
         GOTOR CALCP,DMCB,TGFULL,(R2),FULL    ADD MED CALC TO FUTA              
                                                                                
CT3X80   MVC   TGFULL,TARATWC                                                   
         TM    TARASTA2,TARASWCD   IS WC IN DOLLARS?                            
         JNZ   CT3X95                                                           
         CLC   TGFULL,=XL4'FFFFFFFF'  IS RATE = ZERO?                           
         JE    CT3X95                                                           
         GOTOR CALCP,DMCB,TGFULL,(R2),FULL    ADD WC CALC TO FUTA               
                                                                                
CT3X95   CLI   TGBYTE,CTAXFUTA     IF CALCULATING FUTA                          
         JNE   CT3X100                                                          
         MVC   TMXFUI,FULL                                                      
         J     XIT                                                              
CT3X100  CLI   TGBYTE,CTAXSUTA     IF CALCULATING SUTA                          
         JNE   CT3X110                                                          
         MVC   TMXSUI,FULL                                                      
         J     XIT                                                              
CT3X110  CLI   TGBYTE,CTAXFICA     IF CALCULATING FICA                          
         JNE   CT3X120                                                          
         MVC   TMXFICA,FULL                                                     
         J     XIT                                                              
CT3X120  CLI   TGBYTE,CTAXMED      IF CALCULATING MED                           
         JNE   XIT                                                              
         MVC   TMXMED,FULL                                                      
         J     XIT                                                              
         DROP  R6                                                               
         LTORG                                                                  
         EJECT                                                                  
                                                                                
***********************************************************************         
*        CONSTANTS AND LITERALS                                       *         
***********************************************************************         
CTAXWCOV EQU   X'07'  CALCULATE WC OVERCHARGE                                   
CTAXFICR EQU   X'06'  CALCULATE FICA CREDITS                                    
CTAXWCC  EQU   X'05'  CALCULATE WC CORP                                         
CTAXFUTA EQU   X'04'  CALCULATE FUTA                                            
CTAXSUTA EQU   X'03'  CALCULATE SUTA                                            
CTAXFICA EQU   X'02'  CALCULATE FICA                                            
CTAXMED  EQU   X'01'  CALCULATE MEDICARE                                        
                                                                                
CTAXBASE EQU   X'01'  USE BASE AMOUNT                                           
                                                                                
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*        CALCULATE P1 * P2 + P3 = P3                                            
*                  P1 = A(RATE)                                                 
*                  P2 = AMOUNT                                                  
*                  P3 = TOTAL                                                   
*                                                                               
         SPACE 1                                                                
CALCP    NTR1  BASE=*,LABEL=*                                                   
         L     R4,0(R1)            A(P1)                                        
         L     RE,0(R4)            RATE                                         
*                                                                               
         XR    R2,R2                                                            
         L     R3,4(R1)            P2                                           
*                                                                               
         MR    R2,RE               R2,R3 = RE * R3                              
         LH    RF,=H'5000'                                                      
         TM    TMSTAT,TMSBIL       BILLING HAS 4 DECIMAL PLACES                 
         BO    *+8                                                              
         L     RF,=F'50000'        ALLTAX RATES CARRY 5 DECIMAL PLACES          
         DR    R2,RF                                                            
         LTR   R3,R3               R3 = QUOTIENT                                
         BM    *+8                                                              
         AH    R3,=H'1'            ROUND                                        
         SRA   R3,1                                                             
         ST    R3,0(R1)            RETURN CALC FOR 1                            
*                                                                               
         L     R4,8(R1)            A(P3) - TOTAL                                
         A     R3,0(R4)                                                         
         ST    R3,0(R4)                                                         
         J     XIT                                                              
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*        CALCULATE P1 * P2 + P3 = P3                                            
*                  P1 = A(RATE)                                                 
*                  P2 = AMOUNT                                                  
*                  P3 = TOTAL                                                   
*                                                                               
         SPACE 1                                                                
CALCP2   NTR1  BASE=*,LABEL=*                                                   
         L     R4,0(R1)            A(P1)                                        
         L     RE,0(R4)            RATE                                         
*                                                                               
         XR    R2,R2                                                            
         L     R3,4(R1)            P2                                           
*                                                                               
         MR    R2,RE               R2,R3 = RE * R3                              
         LH    RF,=H'5000'                                                      
         TM    TMSTAT,TMSBIL       BILLING HAS 4 DECIMAL PLACES                 
         BO    *+8                                                              
         L     RF,=F'50000'        ALLTAX RATES CARRY 5 DECIMAL PLACES          
         DR    R2,RF                                                            
         LTR   R3,R3               R3 = QUOTIENT                                
         BM    *+8                                                              
         AH    R3,=H'1'            ROUND                                        
         SRA   R3,1                                                             
         ST    R3,0(R1)            RETURN CALC FOR 1                            
*                                                                               
         CLI   TMEMSFLG,3          IF 2ND PASS, CALCULATED ALREADY              
         JE    XIT                                                              
         L     R4,8(R1)            A(P3) - TOTAL                                
         A     R3,0(R4)                                                         
         ST    R3,0(R4)                                                         
         J     XIT                                                              
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        IF OVERCHARGED BY WCTAX - GIVE BACK THE MONEY                *         
***********************************************************************         
                                                                                
CHKOVER  NTR1  BASE=*,LABEL=*                                                   
         L     R5,TMTXEARN                                                      
         A     R5,TMYEARN                                                       
         C     R5,=F'10000000'     IF YTD + THIS CHECK > 100,000                
         JH    CHKO30              THEN OVERCHARGED BY WCTAX                    
         TM    TMYEARN,X'80'       IF AMOUNT IS NEGATIVE, SKIP                  
         JO    XIT                                                              
         CLC   TMYEARN,=F'10000000' ELSE, IF YTD >= 100,000                     
         JL    XIT                                                              
         L     R2,=F'10000000'     THEN OVERCHARGED BY WCTAX                    
         S     R2,TMYEARN                                                       
         J     CHKO40                                                           
                                                                                
CHKO30   L     R2,TMTXEARN         IF YTD > 100,000 THEN                        
         CLC   TMYEARN,=F'10000000' OVERCHARGED BY THIS CHECK                   
         JH    CHKO40                                                           
         S     R5,=F'10000000'     ELSE, OVERCHARGED BY AMT ABOVE MAX           
         LR    R2,R5                                                            
                                                                                
CHKO40   XC    TRATE,TRATE                                                      
         XR    R0,R0               AMOUNT WAS NOT A NEGATIVE                    
         GOTOR CALCTAX2,DMCB,('CTAXWCOV',0),(R2),0 CALC NEW WC OVER             
         L     R1,TRATE            GIVE OVERCHARGED AMOUNT BACK                 
                                                                                
         L     R2,TMXMED                                                        
         SR    R2,R1                                                            
         ST    R2,TMXMED                                                        
         L     R2,TMXTOTAL                                                      
         SR    R2,R1                                                            
         ST    R2,TMXTOTAL                                                      
         J     XIT                                                              
                                                                                
***********************************************************************         
*        CONSTANTS AND LITERALS                                       *         
***********************************************************************         
                                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        ROUTINE TO TAKE TAXES FOR EURO PAYMENTS                      *         
***********************************************************************         
                                                                                
EUROTAX  NTR1  BASE=*,LABEL=*                                                   
         CLI   TMBILTYP,TABRTY99   BILLING TYPE 99 - NO HANDLING                
         JE    XIT                                                              
         L     R2,TMNTEARN         ELSE CALCULATE HANDLING FEE ON               
         A     R2,TMTXEARN         ENTIRE AMOUNT                                
         CLI   TMBILTYP,TABRTY24   BILLING TYPE 24                              
         JNE   ET05                                                             
         TM    TGUSSTAT,SESSION    AND REUSE                                    
         JO    ET05                                                             
         GOTOR CALCHAND,DMCB,('CHANDCA',0),(R2),0    USE REUSE RATE             
         J     XIT                                                              
                                                                                
ET05     GOTOR CALCHAND,DMCB,('CHANDIA',0),(R2),0    USE REG. RATE              
         J     XIT                                                              
                                                                                
***********************************************************************         
*        CONSTANTS AND LITERALS                                       *         
***********************************************************************         
                                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        ROUTINE GETS YTD OF INVOICE BEING CREDITED TO GET CORRECT    *         
*        LEVELS TO CALCULATE PAYROLL TAX                              *         
***********************************************************************         
GETCRYTD NTR1  BASE=*,LABEL=*                                                   
         USING TARAD,RE                                                         
*        L     RE,TMACURRA       DOES INVOICE HAVA TARA ELEMENT?                
         L     RE,TMACPYRA       DOES INVOICE HAVA TARA ELEMENT?                
         LTR   RE,RE                                                            
         JZ    NO                                                               
         DROP  RE                                                               
                                                                                
         MVC   AIO,AIO1                                                         
         L     R4,AIO                                                           
         USING TANUD,R4                                                         
         MVI   ELCODE,TANUELQ                                                   
         BRAS  RE,GETEL                                                         
         B     *+8                                                              
GCRYTD5  BRAS  RE,NEXTEL                                                        
         JNE   NO                                                               
         CLI   TANUTYPE,TANUTINV                                                
         BNE   GCRYTD5                                                          
         USING TANUD,R4          GET CREDIT OF INVOICE ELEMENT                  
         MVC   WORK(6),TANUMBER  CREDITED INVOICE NUMBER                        
         DROP  R4                                                               
*                                                                               
         MVC   SYSFIL,=CL8'CHKFIL'                                              
         MVC   SYSDIR,=CL8'CHKDIR'                                              
         MVC   AIO,AIO2                                                         
         XC    KEY,KEY                                                          
         LA    R4,KEY                                                           
         USING TLCKD,R4                                                         
         MVI   TLCKCD,TLCKCDQ    CHECK RECORD                                   
         MVC   TLCKAGY,TMAGY     AGENCY                                         
         MVC   TLCKINV,WORK      CREDITED INVOICE                               
         GOTO1 HIGH                                                             
         B     GCRYTD20                                                         
GCRYTD10 GOTO1 SEQ                                                              
GCRYTD20 CLC   KEY(TLCKSORT-TLCKD),KEYSAVE                                      
         JNE   NOCRYTD                                                          
         CLC   TLCKSSN,TMSSN     FIND SSN                                       
         BNE   GCRYTD10                                                         
         CLC   TMCSEQ,TLCKSORT+4 AND CAST SEQ NUMBER                            
         BNE   GCRYTD10                                                         
         GOTO1 GETREC                                                           
                                                                                
         L     R4,AIO2                                                          
         USING TAYED,R4                                                         
         MVI   ELCODE,TAYEELQ                                                   
         BRAS  RE,GETEL                                                         
         B     *+8                                                              
GCRYTD30 BRAS  RE,NEXTEL                                                        
         JNE   NOCRYTD                                                          
         CLI   TAYETYPE,TAYETBIL                                                
         BNE   GCRYTD30                                                         
         MVC   FEDYTD(YTDAMTS),TAYENYTD    SET TAXABLE AMOUNTS                  
                                                                                
         XC    KEY,KEY                                                          
         LA    R4,KEY                                                           
         USING TLCKPD,R4                                                        
         MVI   TLCKPCD,TLCKRCDQ  CREDIT INVOICE CHECK POINTER                   
         MVC   TLCKRAGY,TMAGY    AGENCY                                         
         MVC   TLCKRCIN,WORK     CREDITED INVOICE                               
         MVC   TLCKRCOM,TMCOM    INTERNAL COMMERCIAL NUMBER                     
         MVC   TLCKRCSQ,TMCSEQ   CAST SEQ NUMBER                                
         GOTO1 HIGH                                                             
         B     GCRYTD50                                                         
GCRYTD40 GOTO1 SEQ                                                              
GCRYTD50 CLC   KEY(TLCKRINV-TLCKPD),KEYSAVE                                     
         JNE   GCRYTD80                                                         
         LA    R4,KEY                                                           
         MVC   DUB(L'TGINV),TGINV                                               
         XC    DUB,=X'FFFFFFFFFFFF'   COMPLEMENT INVOICE                        
         CLC   TLCKRINV,DUB      SKIP IF INVOICE IS THE CURRENT INVOICE         
         BE    GCRYTD40                                                         
         GOTO1 GETREC                                                           
                                                                                
         L     R4,AIO2                                                          
         USING TLCKD,R4                                                         
         CLC   TLCKSSN,TMSSN     FIND SSN                                       
         BNE   GCRYTD40                                                         
         USING TAYED,R4                                                         
         MVI   ELCODE,TAYEELQ                                                   
         BRAS  RE,GETEL                                                         
         B     *+8                                                              
GCRYTD60 BRAS  RE,NEXTEL                                                        
         JNE   GCRYTD80                                                         
         CLI   TAYETYPE,TAYETBIL                                                
         BNE   GCRYTD60                                                         
         LA    RE,5                NUMBER OF ENTRIES                            
         LA    R1,FEDYTD                                                        
         LA    R2,TAYETXBL                                                      
GCRTYD70 L     R3,0(R1)                                                         
         A     R3,0(R2)            ADD TAXABLE AMT TO YTD                       
         ST    R3,0(R1)                                                         
         LA    R1,4(R1)                                                         
         LA    R2,4(R2)                                                         
         BCT   RE,GCRTYD70                                                      
         B     GCRYTD40            LOOK FOR MORE CREDITED INVOICES              
                                                                                
GCRYTD80 L     R1,FEDYTD           IF NEW YTD EARNINGS < 0                      
         A     R1,TMTXEARN         THEN TREAT YTD AMOUNTS                       
         JNM   *+10                AS = 0 TO CALCULATE TAXABLE AMOUNTS          
         XC    FEDYTD(YTDAMTS),FEDYTD                                           
         MVC   SYSFIL,=CL8'TALFIL'   RESET FILES                                
         MVC   SYSDIR,=CL8'TALDIR'                                              
         MVC   AIO,AIO1                                                         
         J     YES                                                              
                                                                                
NOCRYTD  MVC   SYSFIL,=CL8'TALFIL'   RESET FILES                                
         MVC   SYSDIR,=CL8'TALDIR'                                              
         MVC   AIO,AIO1                                                         
         J     NO                                                               
*=====================================================================          
*        HANDLING RULES CALCULATION                                             
*        VALUE OF DMCB IS RETURNED                                              
*        BE CAREFUL THAT DMCB ISN'T CHANGED FROM CALLING A ROUTINE              
*=====================================================================          
HNDRULES NTR1  BASE=*,LABEL=*                                                   
         CLC   TMEMP,=C'PP '       SKIP FOR EMPLOYER PP                         
         BE    HNDRX                                                            
         CLC   TMEMP,=C'P+ '       AND EMPLOYER P+                              
         BE    HNDRX                                                            
                                                                                
         XC    FULL,FULL                                                        
         CLI   TMHNDRLS,0          NOTHING SET UP, LEAVE                        
         BE    HNDRX                                                            
         CLI   TMHNDRLS,X'FF'      DON'T WANT IT, LEAVE                         
         BE    HNDRX                                                            
         L     R2,DMCB             AMOUNT THAT MIGHT BE CHANGED                 
         CLI   TMHNDRLS,1          ON GRT APPL (FOR CORPS)                      
         BE    HNDR05                                                           
         CLI   TMHNDRLS,3          (FOR INDS & CORPS)                           
         BE    HNDR05                                                           
         CLI   TMHNDRLS,2          ON GRT APPL + PNH(GRT APPL)                  
         BE    HNDR05              (FOR CORPS)                                  
         CLI   TMHNDRLS,4          (FOR INDS AND CORPS)                         
         BNE   HNDRX                                                            
HNDR05   L     R3,TMAPPLG          GRT APPL AMOUNT                              
         LCR   R3,R3               REVERSE THE SIGN                             
         AR    R2,R3                                                            
         ST    R3,FULL             SAVE GRT APPL AMOUNT                         
*                                                                               
         CLI   TMHNDRLS,2          ON GRT APPL + PNH(GRT APPL)                  
         BE    *+8                                                              
         CLI   TMHNDRLS,4                                                       
         BNE   HNDRX0                                                           
*&&DO                                                                           
**NO-OP  07/2014                                                                
         LA    RF,YEARTAB          RF = YEARS TABLE                             
         USING YEARD,RF                                                         
HNDR10   CLI   0(RF),0             EOT, DON'T GET P&H CALC                      
         BE    HNDRX0                                                           
         CLC   YEARYR,TGYREQU      MAKE SURE YEARS MATCH                        
         BE    HNDR20                                                           
         LA    RF,YEARNEXT                                                      
         B     HNDR10                                                           
*&&                                                                             
                                                                                
         ICM   R4,15,TMASYCOM      IF WE HAVE A(SYSTEM ROUTINES)                
         BNZ   *+6                                                              
         DC    H'00'               THIS VALUE MUST BE SET BY CALLER             
         USING SYSCOMMD,R4         OK TO TRACE                                  
         GOTO1 YRVAL,DMCB,(X'80',TGYREQU)                                       
         BNE   HNDRX0              RESET DMCB AND EXIT                          
**NO-OP  BNE   HNDRX                                                            
         DROP  R4                                                               
                                                                                
         L     RF,TGAYEAR                                                       
         USING YRTABD,RF                                                        
*                                                                               
HNDR20   SR    R0,R0                                                            
         SR    RE,RE                                                            
         LR    R1,R3               COPY GRT APPL AMOUNT                         
**NO-OP  ICM   RE,3,YEARPNHT       P&H RATE                                     
         ICM   RE,3,YRPNHT         P&H RATE                                     
         DROP  RF                                                               
         MR    R0,RE               MULTIPLY                                     
         D     R0,=F'10000'        DIVIDE BY 10000                              
*&&DO                                                                           
**NO-OP  10/13  - OVERFLOW                                                      
         MVC   DUB(2),YEARPNHT     P&H RATE                                     
         MH    R1,DUB                                                           
         CVD   R1,DUB              PACKED NUMBER                                
         SRP   DUB,60,5            DIVIDE BY 1000 AND ROUND                     
         CVB   R1,DUB                                                           
*&&                                                                             
         AR    R2,R1               ADD ASSUMED P&H TO R2                        
         L     RE,FULL                                                          
         AR    RE,R1               ADD P&H AMT TO GRT APPL AMT                  
         ST    RE,FULL                                                          
*                                                                               
HNDRX0   ST    R2,DMCB                                                          
*                                                                               
HNDRX    XIT1                                                                   
*                                                                               
         EJECT                                                                  
         LTORG                                                                  
***********************************************************************         
*        CONSTANTS AND LITERALS                                       *         
***********************************************************************         
*                                                                               
*NO-OP ++INCLUDE TAYRTABLE                                                      
         EJECT                                                                  
***********************************************************************         
*        FOR EVENTS, NEED TO ADJUST TAXABLE SUI AMOUNT                          
*        FOR CANCELS AND CREDITS                                                
***********************************************************************         
ASUICXCR NTR1  BASE=*,LABEL=*                                                   
         CLI   LIMTAB,0            IF LIMIT TABLE IS NOT EMPTY                  
         JE    XIT                                                              
                                                                                
         BRAS  RE,GETCHK           GET CHECK RECORD                             
         JNE   XIT                                                              
                                                                                
         TM    TMSTAT2,TMOLDSUI    GET SUI THE OLD WAY?                         
         BNZ   ASC03                                                            
         BRAS  RE,CXCRTATD         NEW WAY - GET TATD ELEMENTS                  
         J     XIT                                                              
                                                                                
ASC03    XR    R1,R1               COUNTER FOR TATU ELEMENTS                    
         L     R4,AIO3                                                          
         MVI   ELCODE,TATUELQ      READ ALL TAX UNIT ELEMENTS                   
         BRAS  RE,GETEL                                                         
         B     *+8                                                              
ASC05    BRAS  RE,NEXTEL                                                        
         BNE   ASC08                                                            
         AHI   R1,1                INCREMENT COUNTER                            
         B     ASC05                                                            
                                                                                
ASC08    STH   R1,HALF             COUNTER TO READ TATUS BACKWARDS              
                                                                                
         XR    R0,R0               KEEP TRACK OF SUI TAXABLE AMOUNT             
                                                                                
         USING TATUD,R4                                                         
ASC10    L     R4,AIO3                                                          
         MVI   ELCODE,TATUELQ      GET FIRST TAX UNIT ELEMENT                   
         BRAS  RE,GETEL                                                         
         BNE   ASC50                                                            
         LH    R1,HALF             COUNTER                                      
         CHI   R1,0                IF ALREADY DID 1ST ELEMENT,                  
         BE    ASC50               WE ARE DONE                                  
         BCTR  R1,0                                                             
         STH   R1,HALF                                                          
         CHI   R1,0                IS THIS THE FIRST ELEMENT?                   
         BNH   ASC17                                                            
ASC15    BRAS  RE,NEXTEL                                                        
         BNE   ASC50                                                            
         BCT   R1,ASC15                                                         
ASC17    CLC   TATUUNIT,=C'FD '    SKIPPING FEDERAL                             
         BE    ASC18                                                            
         CLC   TATUUNIT,=C'CN '    AND CANADIAN                                 
         BE    ASC18                                                            
         CLI   TATUUNIT+2,C' '     AND CITY ELEMENTS                            
         BNH   ASC19                                                            
                                                                                
ASC18    LH    R1,HALF             COUNTER                                      
         CHI   R1,0                IF FIRST ELEMENT, GET OUT                    
         BNH   ASC50                                                            
         B     ASC10                                                            
                                                                                
         USING LIMD,R2                                                          
ASC19    LA    R2,LIMTAB                                                        
ASC20    CLI   0(R2),X'FF'                                                      
         BE    ASC18                                                            
         CLC   LMUNIT,TATUUNIT     FIND STATE IN LIMIT TABLE                    
         BNE   ASC30                                                            
         CLC   LMEMP,SPACES                                                     
         BE    ASC40                                                            
ASC30    LA    R2,LIMNEXT                                                       
         B     ASC20                                                            
         DROP  R4                                                               
                                                                                
ASC40    BRAS  RE,SETTAXA2         SET TAXABLE AMOUNT FOR STATE IN R3           
                                                                                
         ICM   RE,15,LMSUIW        STATE LIMIT                                  
         L     R1,TMYEARN          YTD NOT INCLUDING THIS CHECK                 
         SR    R1,R0               - SUI FROM PREVIOUS STATE                    
         LTR   R3,R3               IF STATE AMOUNT IS NEGATIVE,                 
         BNM   *+6                 MAKE POSITIVE                                
         LCR   R3,R3                                                            
         SR    R1,R3               YTD - NEW SUI - STATE TAXABLE AMOUNT         
         CR    R1,RE               IF NEW YTD >= STATE SUI LIMIT,               
         BL    ASC42                                                            
         L     R4,SUIYTD           OLD SUIYTD                                   
         SR    R4,R0               - SUI FROM PREVIOUS STATE                    
         SR    RE,R4               SUI LIMIT - SUIYTD                           
         BNP   ASC10               DON'T TAKE SUI ON THIS STATE                 
         CR    RE,R3               IF MAX TAXABLE >= STATE TAXABLE AMT          
         BNL   ASC45               USE ENTIRE AMOUNT                            
         LR    R3,RE               OTHERWISE USE DIFFERENCE                     
         B     ASC45               OF SUI LIMIT - SUIYTD                        
ASC42    SR    RE,R1               STATE LIMIT - NEW YTD                        
         CR    RE,R3               IF DIFFERENCE < STATE TAXABLE AMT            
         BNL   ASC45                                                            
         AR    R0,RE               ADD DIFFERENCE TO SUI TAXABLE AMOUNT         
         B     ASC10                                                            
ASC45    AR    R0,R3               ADD TO SUI TAXABLE AMOUNT                    
         B     ASC10                                                            
         DROP  R2                                                               
                                                                                
ASC50    L     RE,FUITAXBL         UPDATE AMOUNT TAXED THIS INVOICE             
         LCR   RE,RE               MAKE IT POSITIVE                             
         SR    R0,RE               DEDUCT FUITAXBL FROM SUI TAXABLE             
                                                                                
         L     RE,FUITAXBL         FUITAXBL + SUITAXBL                          
         LCR   RE,RE               MAKE IT POSITIVE                             
         AR    RE,R0                                                            
         ST    RE,TAXED            UPDATE AMOUNT TAXED THIS INVOICE             
                                                                                
         L     R1,TMTXEARN         ADJUST REMAINING AMOUNT                      
         LCR   R1,R1                                                            
         SR    R1,RE               TAXABLE EARNINGS - TAXED ALREADY             
         ST    R1,REMAIN                                                        
                                                                                
         LCR   R0,R0               MAKE YTD SUI TAXABLE NEGATIVE                
         ST    R0,SUITAXBL         SAVE NEW SUI TAXABLE YTD AMOUNT              
         J     XIT                                                              
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        FOR EVENTS, NEED TO ADJUST TAXABLE SUI AMOUNT NEW WAY                  
***********************************************************************         
GETTATD  NTR1 BASE=*,LABEL=*                                                    
                                                                                
         BRAS  RE,GETEVT           GET EVTIME RECORD                            
         JNE   XIT                                                              
                                                                                
         L     R0,TMYSUI           R0=HIGHEST SUI WAGE BASE ENCOUNTERED         
                                                                                
         USING TATDD,R4                                                         
         L     R4,AIO3             R3 -> EVTIME RECORD                          
         MVI   ELCODE,TATDELQ      READ ALL EVENT PAY DETAILS ELEMENTS          
         BRAS  RE,GETEL                                                         
         B     *+8                                                              
GTATD10  BRAS  RE,NEXTEL                                                        
         BNE   GTATD50                                                          
         TM    TATDSTAT,TATDSTAX   ONLY TAKE TAXABLE WAGES                      
         BO    *+12                                                             
         TM    TATDSTAT,TATDSWAG                                                
         BZ    GTATD10                                                          
         CLC   TATDUNIT,=C'FD '    SKIPPING FEDERAL                             
         BE    GTATD10                                                          
         CLC   TATDUNIT,=C'CN '    AND CANADIAN ELEMENTS                        
         BE    GTATD10                                                          
                                                                                
         CLI   TATDUNIT+2,C' '     IF CITY,                                     
         BNH   *+8                                                              
         BRAS  RE,GETSTATE         GET STATE TO FIND LIMIT                      
                                                                                
         USING LIMD,R2                                                          
         LA    R2,LIMTAB                                                        
GTATD20  CLI   0(R2),X'FF'                                                      
         BE    GTATD10                                                          
         CLC   LMUNIT,TATDUNIT     FIND STATE IN LIMIT TABLE                    
         BNE   GTATD30                                                          
         CLC   LMEMP,SPACES                                                     
         BE    GTATD40                                                          
GTATD30  LA    R2,LIMNEXT                                                       
         B     GTATD20                                                          
                                                                                
GTATD40  ICM   R3,15,TATDAMNT      SET TAXABLE AMOUNT FOR STATE IN R3           
         AR    R3,R0               ADD ON PREVIOUS SUI AMOUNT                   
         DROP  R4                                                               
                                                                                
         ICM   RE,15,LMSUIW        R3 = THE HIGHER OF ...                       
         CR    R3,RE                    (1) THE STATE'S TAXABLE AMOUNT          
         BNH   *+6                          ON THIS CHECK                       
         LR    R3,RE               OR   (2) STATE'S SUI LIMIT                   
         DROP  R2                                                               
                                                                                
         CR    R0,R3               IF THIS IS THE HIGHEST SUI BASE              
         BNL   GTATD10             WE'VE ENCOUNTERED, RESET R0                  
         LR    R0,R3                                                            
         B     GTATD10                                                          
                                                                                
GTATD50  L     RE,FUITAXBL                                                      
         SR    R0,RE                                                            
         L     RE,TMYSUI                                                        
         SR    R0,RE               R0 = NEW SUI TAXABLE AMOUNT                  
                                                                                
         L     RE,SUITAXBL                                                      
         SR    RE,R0               RE = DIFFERENCE BETWEEN OLD AND NEW          
                                                                                
         ST    R0,SUITAXBL         SAVE NEW SUI TAXABLE YTD AMOUNT              
                                                                                
         L     RF,REMAIN                                                        
         AR    RF,RE                                                            
         ST    RF,REMAIN           AND ADJUST REMAINING AMOUNT                  
         J     XIT                                                              
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        FOR EVENTS, NEED TO ADJUST TAXABLE SUI AMOUNT                          
*        FOR CANCELS AND CREDITS USING TATD ELEMENT (NEW WAY)                   
***********************************************************************         
CXCRTATD NTR1 BASE=*,LABEL=*                                                    
                                                                                
         BRAS  RE,GETEVT           GET EVTIME RECORD                            
         JNE   XIT                                                              
                                                                                
         XR    R1,R1               COUNTER FOR TATD ELEMENTS                    
         L     R4,AIO3             R4 -> EVTIME RECORD                          
         MVI   ELCODE,TATDELQ      READ ALL EVENT PAY DETAILS ELEMENTS          
         BRAS  RE,GETEL                                                         
         B     *+8                                                              
CTATD05  BRAS  RE,NEXTEL                                                        
         BNE   CTATD08                                                          
         AHI   R1,1                INCREMENT COUNTER                            
         B     CTATD05                                                          
                                                                                
CTATD08  STH   R1,HALF             COUNTER TO READ TATDS BACKWARDS              
                                                                                
         XR    R0,R0               KEEP TRACK OF SUI TAXABLE AMOUNT             
         L     R5,TMYEARN          R5 = YTD BEORE THIS CHECK                    
                                                                                
         USING TATDD,R4                                                         
CTATD10  L     R4,AIO3                                                          
         MVI   ELCODE,TATDELQ      GET FIRST EVENT PAY DETAILS ELEMENT          
         BRAS  RE,GETEL                                                         
         BNE   CTATD50                                                          
         LH    R1,HALF             COUNTER                                      
         CHI   R1,0                IF ALREADY DID 1ST ELEMENT,                  
         BE    CTATD50             WE ARE DONE                                  
         BCTR  R1,0                                                             
         STH   R1,HALF                                                          
         CHI   R1,0                IS THIS THE FIRST ELEMENT?                   
         BNH   CTATD17                                                          
CTATD15  BRAS  RE,NEXTEL                                                        
         BNE   CTATD50                                                          
         BCT   R1,CTATD15                                                       
CTATD17  TM    TATDSTAT,TATDSTAX   ONLY TAKE TAXABLE WAGES                      
         BO    *+12                                                             
         TM    TATDSTAT,TATDSWAG                                                
         BZ    CTATD18                                                          
         CLC   TATDUNIT,=C'FD '    SKIPPING FEDERAL                             
         BE    CTATD18                                                          
         CLC   TATDUNIT,=C'CN '    AND CANADIAN ELEMENTS                        
         BE    CTATD18                                                          
                                                                                
         CLI   TATDUNIT+2,C' '     IF CITY,                                     
         BNH   CTATD19                                                          
         BRAS  RE,GETSTATE         GET STATE TO FIND LIMIT                      
         B     CTATD19                                                          
                                                                                
CTATD18  LH    R1,HALF             COUNTER                                      
         CHI   R1,0                IF FIRST ELEMENT, GET OUT                    
         BNH   CTATD50                                                          
         B     CTATD10                                                          
                                                                                
         USING LIMD,R2                                                          
CTATD19  LA    R2,LIMTAB                                                        
CTATD20  CLI   0(R2),X'FF'                                                      
         BE    CTATD18                                                          
         CLC   LMUNIT,TATDUNIT     FIND STATE IN LIMIT TABLE                    
         BNE   CTATD30                                                          
         CLC   LMEMP,SPACES                                                     
         BE    CTATD40                                                          
CTATD30  LA    R2,LIMNEXT                                                       
         B     CTATD20                                                          
                                                                                
CTATD40  ICM   R3,15,TATDAMNT      SET TAXABLE AMOUNT FOR STATE IN R3           
         DROP  R4                                                               
                                                                                
         ICM   RE,15,LMSUIW        STATE LIMIT                                  
**NO-OP  L     R1,TMYEARN          YTD NOT INCLUDING THIS CHECK                 
**01/15  SR    R1,R0               - SUI FROM PREVIOUS STATE                    
                                                                                
         LTR   R3,R3               IF STATE AMOUNT IS NEGATIVE,                 
         BNM   *+6                 MAKE POSITIVE                                
         LCR   R3,R3                                                            
**NO-OP  SR    R1,R3               YTD - NEW SUI - STATE TAXABLE AMOUNT         
         SR    R5,R3               R5 = YTD AFTER THIS TATD                     
         LR    R1,R5               R1 = YTD - STATE TAXABLE AMOUNT              
         CR    R1,RE               IF NEW YTD >= STATE SUI LIMIT,               
         BNL   CTATD10             DON'T TAKE SUI ON THIS STATE                 
*&&DO                                                                           
         CR    R1,RE               IF NEW YTD >= STATE SUI LIMIT,               
         BL    CTATD42                                                          
                                                                                
         L     R4,SUIYTD           OLD SUIYTD                                   
         SR    R4,R0               - SUI FROM PREVIOUS STATE                    
         SR    RE,R4               SUI LIMIT - SUIYTD                           
         BNP   CTATD10             DON'T TAKE SUI ON THIS STATE                 
         CR    RE,R3               IF MAX TAXABLE >= STATE TAXABLE AMT          
         BNL   CTATD45             USE ENTIRE AMOUNT                            
         LR    R3,RE               OTHERWISE USE DIFFERENCE                     
         B     CTATD45             OF SUI LIMIT - SUIYTD                        
*&&                                                                             
CTATD42  SR    RE,R1               STATE LIMIT - NEW YTD                        
         CR    RE,R3               IF DIFFERENCE < STATE TAXABLE AMT            
         BNL   CTATD45                                                          
         AR    R0,RE               ADD DIFFERENCE TO SUI TAXABLE AMOUNT         
         B     CTATD10                                                          
CTATD45  AR    R0,R3               ADD TO SUI TAXABLE AMOUNT                    
         B     CTATD10                                                          
         DROP  R2                                                               
                                                                                
CTATD50  L     RE,FUITAXBL         UPDATE AMOUNT TAXED THIS INVOICE             
         LCR   RE,RE               MAKE IT POSITIVE                             
         SR    R0,RE               DEDUCT FUITAXBL FROM SUI TAXABLE             
                                                                                
         L     RE,FUITAXBL         FUITAXBL + SUITAXBL                          
         LCR   RE,RE               MAKE IT POSITIVE                             
         AR    RE,R0                                                            
         ST    RE,TAXED            UPDATE AMOUNT TAXED THIS INVOICE             
                                                                                
         L     R1,TMTXEARN         ADJUST REMAINING AMOUNT                      
         LCR   R1,R1                                                            
         SR    R1,RE               TAXABLE EARNINGS - TAXED ALREADY             
         ST    R1,REMAIN                                                        
                                                                                
         LCR   R0,R0               MAKE YTD SUI TAXABLE NEGATIVE                
         ST    R0,SUITAXBL         SAVE NEW SUI TAXABLE YTD AMOUNT              
         J     XIT                                                              
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        FOR EVENTS, COMPUTES STATE'S TAXABLE AMOUNT                            
*        ON ENTRY ... R0 = SUI YTD AMOUNT                                       
*                     R4 = A(CURRENT TAX UNIT ELEMENT)                          
***********************************************************************         
         USING TATUD,R4                                                         
SETTAXAM NTR1  BASE=*,LABEL=*                                                   
         ICM   R3,15,TATUWAGE                                                   
         ICM   RE,15,TATUSTRE      R3=A(THIS STATE'S TAXABLE AMOUNT)            
         AR    R3,RE                                                            
                                                                                
         GOTO1 ADDCITY,DMCB,=C'MI ',=C'DET'                                     
         GOTO1 (RF),(R1),=C'NY ',=C'NYC'                                        
         GOTO1 (RF),(R1),=C'OH ',=C'CIN'                                        
         GOTO1 (RF),(R1),=C'OH ',=C'CLV'                                        
         GOTO1 (RF),(R1),=C'PA ',=C'PHL'                                        
                                                                                
         AR    R3,R0               FINALLY ADD ON PREVIOUS SUI AMOUNT           
XITR3    XIT1  REGS=(R3)                                                        
         DROP  R4                                                               
***********************************************************************         
*        FOR EVENTS, COMPUTES STATE'S TAXABLE AMOUNT                            
*        ON ENTRY ... R0 = SUI YTD AMOUNT                                       
*                     R4 = A(CURRENT TAX UNIT ELEMENT)                          
***********************************************************************         
         USING TATUD,R4                                                         
SETTAXA2 NTR1  BASE=*,LABEL=*                                                   
         ICM   R3,15,TATUWAGE                                                   
         ICM   RE,15,TATUSTRE      R3=A(THIS STATE'S TAXABLE AMOUNT)            
         AR    R3,RE                                                            
                                                                                
         GOTO1 ADDCITY,DMCB,=C'MI ',=C'DET'                                     
         GOTO1 (RF),(R1),=C'NY ',=C'NYC'                                        
         GOTO1 (RF),(R1),=C'OH ',=C'CIN'                                        
         GOTO1 (RF),(R1),=C'OH ',=C'CLV'                                        
         GOTO1 (RF),(R1),=C'PA ',=C'PHL'                                        
                                                                                
         XIT1  REGS=(R3)                                                        
         DROP  R4                                                               
***********************************************************************         
*        FOR EVENTS, GETS STATE IF TAX UNIT IS A CITY                           
*                     R4 = A(CURRENT EVENT DETAILS ELEMENT)                     
***********************************************************************         
         USING TATDD,R4                                                         
GETSTATE NTR1  BASE=*,LABEL=*                                                   
                                                                                
         CLC   =C'DET',TATDUNIT    IF TAX UNIT IS FOR A CITY                    
         BNE   GSTATE10            SET APPROPRIATE STATE                        
         MVC   TATDUNIT,=C'MI '                                                 
         J     XIT                                                              
                                                                                
GSTATE10 CLC   =C'NYC',TATDUNIT                                                 
         BNE   GSTATE20                                                         
         MVC   TATDUNIT,=C'NY '                                                 
         J     XIT                                                              
                                                                                
GSTATE20 CLC   =C'CIN',TATDUNIT                                                 
         BNE   GSTATE30                                                         
         MVC   TATDUNIT,=C'OH '                                                 
         J     XIT                                                              
                                                                                
GSTATE30 CLC   =C'CLV',TATDUNIT                                                 
         BNE   GSTATE40                                                         
         MVC   TATDUNIT,=C'OH '                                                 
         J     XIT                                                              
                                                                                
GSTATE40 CLC   =C'PHL',TATDUNIT                                                 
         JNE   XIT                                                              
         MVC   TATDUNIT,=C'PA '                                                 
         J     XIT                                                              
                                                                                
         DROP  R4                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        FOR EVENTS, ADDS PROVIDED CITY'S TAXABLE AMOUNT TO R3                  
*        ON ENTRY ... P1 = A(CITY'S STATE CODE)                                 
*                     P2 = A(CITY'S CITY CODE)                                  
*                     R4 = A(CURRENT TAX UNIT ELEMENT)                          
***********************************************************************         
         USING TATUD,R4                                                         
ADDCITY  NTR1                                                                   
         L     R2,0(R1)            IF CURRENT TAX UNIT ELEMENT                  
         CLC   TATUUNIT,0(R2)      IS FOR THE STATE WE'RE CONCERNED             
         JNE   XIT                 WITH ...                                     
         DROP  R4                                                               
                                                                                
         L     R2,4(R1)                                                         
                                                                                
         USING TATUD,R4                                                         
         L     R4,AIO3             LOOK FOR A TAX UNIT ELEMENT FOR              
         BRAS  RE,GETEL            THE CITY WE'RE CONCERNED WITH                
         J     *+8                                                              
AC10     BRAS  RE,NEXTEL                                                        
         JNE   XIT                                                              
         CLC   TATUUNIT,0(R2)                                                   
         JNE   AC10                                                             
                                                                                
         ICM   RF,15,TATUWAGE                                                   
         AR    R3,RF               IF FOUND, ADD THE TAXABLE WAGES              
         ICM   RF,15,TATUSTRE      AND TAXABLE REIMBURSEMENTS TO                
         AR    R3,RF               THE STATE'S TAXABLE AMOUNT                   
         J     XITR3                                                            
         DROP  R4                                                               
                                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        FOR EVENTS, GET CHECK RECORD                                           
***********************************************************************         
GETCHK   NTR1  BASE=*,LABEL=*                                                   
         OC    TMACHK,TMACHK       IF WE HAVE A(CHECK RECORD)                   
         JZ    NO                                                               
                                                                                
         MVC   SYSFIL,=CL8'CHKFIL'                                              
         MVC   SYSDIR,=CL8'CHKDIR'                                              
         MVC   TGFULL,AIO                                                       
         MVC   AIO,AIO3                                                         
                                                                                
         USING TLDRD,R3                                                         
         LA    R3,KEY                                                           
         MVC   TLDRDA,TMACHK       READ CHECK RECORD                            
         GOTO1 GETREC                                                           
         DROP  R3                                                               
                                                                                
         MVC   SYSFIL,=CL8'TALFIL'                                              
         MVC   SYSDIR,=CL8'TALDIR'                                              
         MVC   AIO,TGFULL                                                       
         J     YES                                                              
                                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        FOR EVENTS, GET EVTIME RECORD                                          
***********************************************************************         
GETEVT   NTR1  BASE=*,LABEL=*                                                   
         OC    TMACHK,TMACHK       IF WE HAVE A(CHECK RECORD)                   
         JZ    NO                                                               
                                                                                
         MVC   TGFULL,AIO                                                       
         MVC   AIO,AIO3            CHECK RECORD IS IN AIO3                      
                                                                                
         USING TLCKD,R4                                                         
         L     R4,AIO              R4 -> CHECK RECORD                           
                                                                                
         USING TLTMD,R3                                                         
         LA    R3,KEY                                                           
         XC    KEY,KEY                                                          
         MVI   TLTMCD,TLTMCDQ                                                   
         OI    TLTMSTA,TLTMSPPL                                                 
         MVC   TLTMSSN,TLCKSSN     SSN                                          
         MVC   TLTMSORT+5(1),TLCKSORT+5                                         
                                                                                
         USING TAPDD,R4                                                         
         MVI   ELCODE,TAPDELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   GEVTNO                                                           
         MVC   TLTMCOM,TAPDCOM     INTERNAL COMMERCIAL CODE                     
         MVC   TLTMINV,TAPDINV     INVOICE                                      
                                                                                
         TM    TMTXEARN,X'80'      IF THIS IS A NEGATIVE INVOICE,               
         BZ    GEVT10                                                           
         TM    TMSTATUS,TMSTCRDT   AND NOT A CREDIT INVOICE,                    
         BO    GEVT10                                                           
         NI    TLTMINV+5,X'FF'-X'80'  TURN OFF CXL BIT TO FIND ORIGINAL         
                                                                                
GEVT10   XC    TLTMINV,=X'FFFFFFFFFFFF'  COMPLEMENT                             
         GOTO1 HIGH                                                             
         CLC   KEY(L'TLTMKEY),KEYSAVE                                           
         BNE   GEVTNO                                                           
         GOTO1 GETREC               GET EVTIME RECORD IN AIO3                   
                                                                                
         MVC   AIO,TGFULL                                                       
         J     YES                                                              
         DROP  R3,R4                                                            
                                                                                
GEVTNO   MVC   AIO,TGFULL                                                       
         J     NO                                                               
                                                                                
         LTORG                                                                  
         EJECT                                                                  
*&&DO                                                                           
*              DSECT TO COVER YEARTAB                                           
YEARD    DSECT                                                                  
YEARUN   DS    XL1                 UNION(S)                                     
YEARYR   DS    XL1                 CONTRACT YEAR(S)                             
CANRATE1 EQU   X'72'               OVERLAY FOR YEAR 1 CANADIAN RATES            
CANRATE2 EQU   X'71'               OVERLAY FOR YEAR 2 CANADIAN RATES            
CANRATE3 EQU   X'70'               OVERLAY FOR YEAR 3 CANADIAN RATES            
SORRATES EQU   X'6B'               OVERLAY FOR SOAP RESIDUAL RATES              
YEARPHAS DS    XL1                 CONTRACT RATE PHASE NUMBER                   
YEARFHNW DS    XL2                 FIXED H&W AMOUNT                             
YEARPNHT DS    XL2                 P&H RATE FOR TV                              
YEARPNHR DS    XL2                 P&H RATE FOR RADIO                           
YEARPHMT DS    XL2                 AFM P&H RATE FOR TV                          
YEARPHMR DS    XL2                 AFM P&H RATE FOR RADIO                       
YEARNEXT EQU   *                                                                
         EJECT                                                                  
*&&                                                                             
*        DSECT TO COVER SUI/SDI WAGE BASE TABLE                                 
         SPACE 1                                                                
LIMD     DSECT                                                                  
LMUNIT   DS    CL2                 UNIT                                         
LMEMP    DS    CL3                 EMPLOYER OVERRIDE                            
LMTYPE   DS    XL1                 TYPES OF RECORD READ                         
LMTYSUIW EQU   X'80'               SUI                                          
LMTYSDIW EQU   X'40'               SDI                                          
LMTYSUIR EQU   X'20'               SUI RATE                                     
LMTYSDIR EQU   X'10'               SDI RATE                                     
         DS    CL2                 SPARE                                        
*                                                                               
LMSUIW   DS    F                   SUI WAGE BASE                                
LMSUIR   DS    F                       RATE                                     
LMSDIW   DS    F                   SDI WAGE BASE                                
LMSDIR   DS    F                       RATE                                     
         DS    2F                  SPARE - MAKES TRACE EASY TO READ             
*                                                                               
LIMLNQ   EQU   *-LIMD                                                           
LIMNEXT  EQU   *                                                                
         SPACE 3                                                                
*                                                                               
*        DSECT TO COVER SURCHARGE TABLE                                         
*                                                                               
SURTABD  DSECT                                                                  
SURUNIT  DS    CL2                 UNIT                                         
SURRATE  DS    H                   SURCHARGE RATE (XX.XX %)                     
SURWGBS  DS    F                   SPECIAL WAGE BASE                            
SURYEAR  DS    CL2                 EFFECTIVE YEAR - INTERNAL FORMAT             
         DS    CL4                                                              
SURLNQ   EQU   *-SURTABD                                                        
         SPACE 3                                                                
*                                                                               
*        DSECT TO COVER SPECIAL RATE ADDITIONS TABLE                            
*                                                                               
RATETABD DSECT                                                                  
RATUNIT  DS    CL2                 UNIT                                         
RBILTYP  DS    XL1                 BILLING TYPE                                 
         DS    XL1                                                              
RHNDRATE DS    H                   HANDLING RATE ADDITION (XX.XX %)             
RCRPRATE DS    H                   CORP                                         
RFUIRATE DS    H                   GROSS                                        
RATLNQ   EQU   *-RATETABD                                                       
         SPACE 3                                                                
*                                                                               
*        DSECT TO COVER SPECIAL ROUTINE TABLE                                   
*                                                                               
RTNTABD  DSECT                                                                  
RTNBTYPE DS    XL1                 BILLING TYPE                                 
         DS    CL1                 SPARE (EASIER READING IN DUMP)               
RTNDISP  DS    XL2                 DISPLACEMENT TO ROUTINE                      
RTNLNQ   EQU   *-RTNTABD                                                        
         EJECT                                                                  
*                                                                               
*        DSECT TO COVER SUI SURCHARGE TABLE                                     
*                                                                               
SSUITBD  DSECT                                                                  
SSUIUNIT DS    CL2                 UNIT                                         
SSUIRATE DS    H                   SURCHARGE RATE (XX.XX %)                     
SSUIEFST DS    XL3                 EFFECTIVE DATE START                         
SSUIEFED DS    XL3                 EFFECTIVE DATE END                           
SSUILNQ  EQU   *-SSUITBD                                                        
         SPACE 3                                                                
*                                                                               
*        DSECT TO COVER EXTRA TAX TABLE                                         
*                                                                               
EXTXTBD  DSECT                                                                  
EXTXUNIT DS    CL2                 UNIT                                         
EXTXRATE DS    H                   SURCHARGE RATE (XX.XX %)                     
EXTXEFST DS    XL3                 EFFECTIVE DATE START                         
EXTXEFED DS    XL3                 EFFECTIVE DATE END                           
EXTXLNQ  EQU   *-EXTXTBD                                                        
         SPACE 3                                                                
       ++INCLUDE TATAXD                                                         
TMDD     DSECT                                                                  
       ++INCLUDE TALIMD                                                         
         EJECT                                                                  
       ++INCLUDE TAATREFD                                                       
         EJECT                                                                  
*        OTHER DSECTS ARE HIDDEN IN HERE                                        
         SPACE 3                                                                
*DDSPLWORKD                                                                     
*DDSPOOLD                                                                       
*TAGENFILE                                                                      
*TASYSDSECT                                                                     
*TASYSEQUS                                                                      
*TASYSWORKD                                                                     
*TASYSVALD                                                                      
         PRINT OFF                                                              
       ++INCLUDE DDSPLWORKD                                                     
       ++INCLUDE DDSPOOLD                                                       
       ++INCLUDE TAGENFILE                                                      
       ++INCLUDE TASYSDSECT                                                     
       ++INCLUDE TASYSEQUS                                                      
SYSWORKD DSECT                                                                  
       ++INCLUDE TASYSWORKD                                                     
SYSCOMMD DSECT                                                                  
       ++INCLUDE TASYSVALD                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'123TALIM     07/27/15'                                      
         END                                                                    
