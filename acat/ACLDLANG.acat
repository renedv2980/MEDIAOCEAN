*          DATA SET ACLDLANG   AT LEVEL 003 AS OF 11/16/01                      
*CATALP ACLDLANG                                                                
LDLANG   TITLE '- ESTABLISH LANGUAGE CODE FOR A COMPANY'                        
***********************************************************************         
* PARAMS VIA R1                                                       *         
* AL4   A(COMPANY RECORD)                                             *         
***********************************************************************         
         SPACE 1                                                                
LDLANG   CSECT                                                                  
         PRINT NOGEN                                                            
         ENTRY MASTC                                                            
         NMOD1 WORKX-WORKD,ACLDLANG                                             
         USING WORKD,RC                                                         
         LR    RA,R1                                                            
*                                                                               
         L     R2,0(RA)                                                         
         XC    0(4,RA),0(RA)                                                    
         USING CPYRECD,R2                                                       
         L     R7,VBOXAREA                                                      
         USING BOXD,R7                                                          
         MVI   MCCTRY,0                                                         
         MVI   MCLANG,0                                                         
         MVI   MCAGCURR,0                                                       
         MVI   BOXLANG,0                                                        
         MVI   BOXCTRY,0                                                        
         MVI   BOXSYS,X'06'                                                     
*                                                                               
         CLI   FTSWITCH,0          TEST FIRST TIME                              
         BNE   LANG01                                                           
         GOTO1 VDATAMGR,DMCB,DMOPEN,CONSYS,CONSYSL,IO                           
         MVI   FTSWITCH,1                                                       
*                                                                               
LANG01   LA    R2,CPYRFST                                                       
         SR    R0,R0                                                            
         USING CPYELD,R2                                                        
LANG02   CLI   CPYEL,0                                                          
         BE    LANGX                                                            
         CLI   CPYEL,CPYELQ                                                     
         BE    *+14                                                             
         IC    R0,CPYLN                                                         
         AR    R2,R0                                                            
         B     LANG02                                                           
         MVC   DIVID,CPYUID        EXTRACT USER ID NUMBER                       
*                                                                               
LANG04   LA    R2,IO                                                            
         USING CTIREC,R2                                                        
         XC    CTIKEY,CTIKEY                                                    
         MVI   CTIKTYP,CTIKTYPQ    ID RECORD                                    
         MVC   CTIKNUM,DIVID                                                    
         GOTO1 VDATAMGR,DMCB,DMREAD,CTFILE,CTIREC,CTIREC                        
         BNE   LANGX                                                            
*                                                                               
         LA    R1,CTIDATA                                                       
         SR    R0,R0                                                            
         USING CTAGYD,R1                                                        
LANG06   CLI   CTAGYEL,0           TEST END OF RECORD                           
         BE    LANGX                                                            
         CLI   CTAGYEL,CTAGYELQ    TEST AGENCY ALPHA ID ELEMENT                 
         BE    *+14                                                             
         IC    R0,CTAGYLEN                                                      
         AR    R1,R0                                                            
         B     LANG06                                                           
         MVC   BOXLANG,CTAGYLNG    SAVE LANGUAGE CODE                           
         MVC   MCLANG,CTAGYLNG                                                  
         MVC   0(1,RA),CTAGYLNG    RETURN LANGUAGE TO CALLER                    
         MVC   2(2,RA),DIVID                                                    
*                                                                               
         LA    R2,IO                                                            
         USING CT5REC,R2                                                        
         XC    CT5KEY,CT5KEY                                                    
         MVI   CT5KTYP,CT5KTYPQ    ACCESS RECORD                                
         MVC   CT5KALPH,CTAGYID                                                 
         GOTO1 VDATAMGR,DMCB,DMREAD,CTFILE,CT5REC,CT5REC                        
         BNE   LANGX                                                            
*                                                                               
         LA    R1,CT5DATA                                                       
         SR    R0,R0                                                            
         USING CTAGDD,R1                                                        
LANG08   CLI   CTAGDEL,0           TEST END OF RECORD                           
         BE    LANGX                                                            
         CLI   CTAGDEL,CTAGDELQ    TEST AGENCY GROUP DETAILS ELEMENT            
         BE    *+14                                                             
         IC    R0,CTAGDLEN                                                      
         AR    R1,R0                                                            
         B     LANG08                                                           
         CLI   CTAGDLEN,CTAGDL2Q   TEST LONG ELEMENT                            
         BL    LANGX                                                            
         MVC   BOXCTRY,CTAGDCTY                                                 
         MVC   MCCTRY,CTAGDCTY                                                  
         MVC   MCAGCURR,CTAGDCUR                                                
         MVC   1(1,RA),CTAGDCTY                                                 
         SPACE 2                                                                
LANGX    XIT1  ,                                                                
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
FTSWITCH DC    X'00'                                                            
CONSYS   DC    C'CONTROL '                                                      
CONSYSL  DC    C'NCTFILE X'                                                     
CTFILE   DC    C'CTFILE  '                                                      
DMREAD   DC    C'DMREAD  '                                                      
DMOPEN   DC    C'DMOPEN  '                                                      
DIVID    DC    AL2(0)              USER ID NUMBER FOR DIVIDER                   
VDATAMGR DC    V(DATAMGR)                                                       
VBOXAREA DC    V(BOXAREA)                                                       
*                                                                               
MASTC    DS    0D                                                               
       ++INCLUDE DDMASTC                                                        
         EJECT                                                                  
WORKD    DSECT                     ** LOCAL WORKING STORAGE **                  
DMCB     DS    6F                                                               
WORK     DS    XL64                                                             
IO       DS    XL1024              IO AREA                                      
WORKX    EQU   *                                                                
         SPACE 1                                                                
* DDBIGBOX                                                                      
         PRINT OFF                                                              
       ++INCLUDE DDBIGBOX                                                       
         PRINT ON                                                               
         SPACE 1                                                                
* ACGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* CTGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE CTGENFILE                                                      
         PRINT ON                                                               
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003ACLDLANG  11/16/01'                                      
         END                                                                    
