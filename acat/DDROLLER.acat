*          DATA SET DDROLLER   AT LEVEL 003 AS OF 05/01/02                      
*CATALP ROLLER                                                                  
         TITLE 'GENERALISED MODULE FOR ACCUMULATORS'                            
ROLLER   CSECT                                                                  
         NMOD1 0,XXROLLER                                                       
         LM    R2,R5,4(R1)                                                      
         EJECT                                                                  
*                   DEFINE AND CLEAR TABLE                                      
         SPACE 3                                                                
         CLI   3(R1),0                                                          
         BNE   LINE                                                             
         STM   R3,R4,0(R2)         PARAS 2  LINES                               
         LR    R5,R3                     3  COLUMNS                             
         SLL   R5,2                                                             
         MR    R4,R4               COMPUTE BYTES TO CLEAR                       
         LA    R2,8(R2)                                                         
         BAS   RE,CLEAR                                                         
         B     ROLLEX                                                           
         EJECT                                                                  
*                   FACILITY TO CLEAR A LINE OR POINT TO A LINE                 
         SPACE 3                                                                
LINE     CLI   3(R1),2                                                          
         BH    POST                                                             
         L     R5,4(R2)            COMPUTE BYTES PER LINE                       
         SLL   R5,2                                                             
         BCTR  R3,R0               PARA 2 IS THE LINE TO CLEAR                  
         LR    R7,R3                                                            
         MR    R6,R5               WORK OUT DISPLACEMENT IN R7                  
         LA    R2,8(R7,R2)                                                      
         CLI   3(R1),2                                                          
         BE    LINE2                                                            
         ST    R2,0(R1)                                                         
         B     ROLLEX                                                           
         SPACE 2                                                                
LINE2    BAS   RE,CLEAR                                                         
         B     ROLLEX                                                           
         EJECT                                                                  
*                   ROUTINE TO CLEAR BYTES                                      
         SPACE 3                                                                
CLEAR    NTR                                                                    
         SPACE 2                                                                
CLEARLUP CH    R5,=H'250'          R2 HAS ADDRESS TO BE CLEARED                 
         BL    CLEARLES            R5 HAS NUMBER OF BYTES                       
         XC    0(250,R2),0(R2)                                                  
         LA    R2,250(R2)                                                       
         SH    R5,=H'250'                                                       
         B     CLEARLUP                                                         
         SPACE 2                                                                
CLEARLES LTR   R5,R5                                                            
         BZ    CLEAREND                                                         
         BCTR  R5,R0                                                            
         EX    R5,CLEAREST                                                      
         SPACE 2                                                                
CLEAREND XIT                                                                    
         SPACE 2                                                                
CLEAREST XC    0(0,R2),0(R2)                                                    
         EJECT                                                                  
*                   ROUTINE TO ADD A VALUE INTO TABLE                           
         SPACE 3                                                                
POST     CLI   3(R1),3                                                          
         BNE   LINADD                                                           
*                                  PARA1 (R2) TABLE                             
*                                      2 (R3) VALUE                             
*                                      3 (R4) LINE NUMBER                       
*                                      4 (R5) COLUMN NUMBER                     
         SPACE 2                                                                
         L     R7,4(R2)            COMPUTE TABLE WIDTH                          
         SLL   R7,2                                                             
         BCTR  R4,R0                                                            
         MR    R6,R4               NOW R7 HAS DISPLACEMENT                      
         BCTR  R5,R0                                                            
         SLL   R5,2                                                             
         AR    R7,R5               ADD IN COLUMNAR DISPLACEMENT                 
         A     R3,8(R7,R2)                                                      
         ST    R3,8(R7,R2)         STORE INCREMENTED VALUE                      
         B     ROLLEX                                                           
         EJECT                                                                  
*                   FACILITY TO ADD LINES AND CROSS CAST                        
         SPACE 3                                                                
LINADD   CLI   3(R1),4                                                          
         BNE   CROSS                                                            
         BAS   RE,ADDLINE                                                       
         B     ROLLEX                                                           
         SPACE 2                                                                
CROSS    CLI   3(R1),5                                                          
         BNE   TOTALS                                                           
         BAS   RE,CROSSCST                                                      
         B     ROLLEX                                                           
         EJECT                                                                  
*                   CONTROL OF ADDING LINE1 TO THE OTHER LINES                  
         SPACE 3                                                                
TOTALS   CLI   3(R1),6                                                          
         BNE   GRAND                                                            
         L     R6,0(R2)            R6 = NUMBER OF LINES                         
         LA    R3,1                                                             
         BAS   RE,CROSSCST         CROSS CAST LINE 1                            
         LA    R4,2                                                             
         SPACE 2                                                                
TOTLOOP  BAS   RE,ADDLINE          NOW ADD IT INTO THE OTHER LINES              
         CR    R4,R6                                                            
         BE    ROLLEX                                                           
         LA    R4,1(R4)                                                         
         B     TOTLOOP                                                          
         EJECT                                                                  
*                   CONTROL OF ADDING ALL LINES TO THE LAST                     
         SPACE 3                                                                
GRAND    CLI   3(R1),7                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    R3,1                                                             
         L     R4,0(R2)            R4 = NUMBER OF LAST LINE                     
         SPACE 2                                                                
GRANDLUP BAS   RE,CROSSCST         CROSS CAST LINE                              
         BAS   RE,ADDLINE          AND ADD INTO GRAND TOTALS                    
         LA    R3,1(R3)                                                         
         CR    R3,R4                                                            
         BNE   GRANDLUP                                                         
         SPACE 2                                                                
ROLLEX   XMOD1 1                                                                
         EJECT                                                                  
*                   ROUTINE TO CROSS CAST                                       
         SPACE 2                                                                
CROSSCST NTR                                                                    
         SPACE 2                                                                
         L     R4,4(R2)            R3 = LINE TO CROSS CAST                      
         SLL   R4,2                R4 = WIDTH OF TABLE                          
         BCTR  R3,R0                                                            
         LR    R7,R3                                                            
         MR    R6,R4               DISLACEMENT IN R7                            
         LA    R7,8(R7,R2)         R7 NOW POINTS TO START OF LINE               
         AR    R4,R7                                                            
         SH    R4,=H'4'            R4 TO THE LAST COLUMN                        
         SR    R6,R6                                                            
         SPACE 2                                                                
CROSSLUP CR    R7,R4                                                            
         BE    CROSSEND                                                         
         A     R6,0(R7)                                                         
         LA    R7,4(R7)                                                         
         B     CROSSLUP                                                         
         SPACE 2                                                                
CROSSEND ST    R6,0(R4)                                                         
         XIT                                                                    
         EJECT                                                                  
*                   ROUTINE TO ADD TWO LINES TOGETHER                           
         SPACE 3                                                                
ADDLINE  NTR                                                                    
         L     R5,4(R2)            R3 LINE 1                                    
*                                  R4 LINE 2                                    
         SLL   R5,2                R5 WIDTH OF LINE                             
         SPACE 2                                                                
         LR    R7,R3                                                            
         BCTR  R7,R0                                                            
         MR    R6,R5                                                            
         LA    R3,8(R7,R2)         R3 TO START OF LINE 1                        
         SPACE 2                                                                
         LR    R7,R4                                                            
         BCTR  R7,R0                                                            
         MR    R6,R5                                                            
         LA    R4,8(R7,R2)         R4 TO START OF LINE 2                        
         L     R5,4(R2)                                                         
         SPACE 2                                                                
ADDLOOP  L     R6,0(R3)            NOW ADD THE LINES TOGETHER                   
         A     R6,0(R4)                                                         
         ST    R6,0(R4)                                                         
         LA    R3,4(R3)                                                         
         LA    R4,4(R4)                                                         
         BCT   R5,ADDLOOP                                                       
         XIT                                                                    
         EJECT                                                                  
