*          DATA SET FATOADDS   AT LEVEL 006 AS OF 05/01/02                      
*CATALP FATOADDS                                                                
         TITLE 'TOADDS - ADDS TWX TERMINAL OUTPUT TRANSLATOR'                   
TOADDS   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 OUTX-OUTD,**TOADDS,CLEAR=YES                                     
         USING OUTD,RC             RC=A(LOCAL W/S)                              
         LR    RA,R1               RA=A(PARAMETER LIST)                         
         L     R2,0(RA)            P1=A(TWA),P2=A(BUFFER),P3=A(UTL)             
         L     RF,8(RA)                                                         
         MVC   SVSVCREQ,TSVCREQ-UTLD(RF)                                        
         B     TWACHK                                                           
         EJECT                                                                  
* CHECK INTEGRITY OF TWA & BUILD A LIST OF LINES TO BE TRANSMITTED              
*                                                                               
TWACHK   LA    R3,64(R2)           R3=A(FIRST TWA FIELD)                        
         SR    R4,R4                                                            
         LA    R5,MAXSCRLN(R3)                                                  
         XC    DUB(2),DUB                                                       
         ZIC   RE,0(R3)                                                         
         LA    RE,0(R3,RE)         RE=A(FIRST UNPROTECTED FIELD)                
         ST    RE,FUNADDR          SAVE A(FIRST UNPROTECTED FIELD)              
         OC    SVSVCREQ,SVSVCREQ   TEST SERVICE REQUEST SCREEN                  
         BZ    TWACHK2             NO                                           
         OI    1(RE),MODIFY        SET MODIFIED FLAG IN FIRST UNP FIELD         
*                                                                               
TWACHK2  CLI   0(R3),0             TEST END-OF-TWA                              
         BE    TWACHKX                                                          
         IC    R4,0(R3)            R4=L'TWA FIELD+HEADER                        
         CLI   0(R3),9                                                          
         BNL   *+6                                                              
         DC    H'0'                                                             
         CLC   2(2,R3),=Y(24*80)   CHECK ADDRESS NOT GREATER THAN MAX           
         BNH   *+6                                                              
         DC    H'0'                                                             
         CLC   2(2,R3),DUB         CHECK THAT ADDRESSES ASCEND                  
         BH    *+6                                                              
         DC    H'0'                                                             
         TM    1(R3),PROTECT       TEST PROTECTED FIELD                         
         BO    TWACHK4                                                          
         OC    CURADDR,CURADDR     TEST CURSOR ADDRESS SET                      
         BNZ   TWACHK4                                                          
         TM    6(R3),CURSOR        TEST CURSOR SET TO THIS FIELD                
         BZ    TWACHK4                                                          
         ST    R3,CURADDR          SAVE FIRST CURSOR FIELD ADDRESS              
TWACHK4  TM    7(R3),NEWFIELD      IS THIS A NEW FIELD                          
         BZ    TWACHK6                                                          
         NI    7(R3),255-NEWFIELD  RESET NEW FIELD INDICATOR                    
         SR    RE,RE                                                            
         ICM   RE,3,2(R3)          CALCULATE LINE NUMBER                        
         SRDA  RE,32                                                            
         D     RE,COLWIDTH                                                      
         LA    RF,FORMLINE(RF)                                                  
         MVI   0(RF),1             SET LINE TO BE TRANSMITTED                   
         MVI   FORMANY,1           SET AT LEAST ONE FORMAT LINE                 
TWACHK6  MVC   DUB(2),2(R3)                                                     
         BXLE  R3,R4,TWACHK2                                                    
         DC    H'0'                                                             
*                                                                               
TWACHKX  MVC   INDICS,1(R3)        SAVE SCRGEN INDICATORS                       
         ST    R3,EOTADDR          SAVE A(END-OF-TWA)                           
         CLI   FORMANY,0           TEST ANY FORMAT LINES                        
         BE    XMTALL              NO                                           
         EJECT                                                                  
* ROUTINE TO SET TRANSMIT BITS ON IN FIELD OF LINES THAT ARE FORMATTED          
*                                                                               
SETXMT   LA    R3,64(R2)           R2=A(FIRST TWA FIELD)                        
         SR    R4,R4                                                            
         LA    R5,MAXSCRLN(R3)                                                  
*                                                                               
SETXMT2  CLI   0(R3),0             TEST END-OF-TWA                              
         BE    SETXMTX                                                          
         LH    RE,2(R3)                                                         
         SRDA  RE,32                                                            
         D     RE,COLWIDTH                                                      
         LA    RF,FORMLINE(RF)     IS THIS FIELD ON A LINE THAT IS              
         CLI   0(RF),0             GOING TO BE REFORMATTED                      
         BE    *+8                                                              
         OI    6(R1),TRANSMIT      YES - TURN ON TRANSMIT BIT                   
         IC    R4,0(R3)                                                         
         BXLE  R3,R4,SETXMT2                                                    
         DC    H'0'                                                             
*                                                                               
SETXMTX  DS    0H                                                               
         EJECT                                                                  
* CHECK IF ALL FIELDS ARE TO BE TRANSMITTED                                     
* IF SO SET ON TRANSMIT BITS IN ALL TWA FIELDS                                  
*                                                                               
XMTALL   LA    R3,64(R2)           R3=A(FIRST TWA FIELD)                        
         SR    R4,R4                                                            
         LA    R5,MAXSCRLN(R3)                                                  
         L     R1,EOTADDR                                                       
         CLI   0(RA),1             CHECK OPTION TO TRANSMIT ALL                 
         BNE   *+12                                                             
         MVI   INDICS,1            YES - SET TRANSMIT ALL IN INDICS             
         B     XMTALL2                                                          
         OC    INDICS,INDICS       ARE SCRGEN INDICS SET                        
         BZ    XMTALLX                                                          
         XC    1(2,R1),1(R1)       CLEAR SCRGEN INDICATORS                      
         MVI   0(RA),1             TELL LCM TO WRITE ERASE                      
         MVI   0(R2),0             RESET HIGH LINE NUMBER                       
*                                                                               
XMTALL2  CLI   0(R3),0             TEST END-OF-TWA                              
         BE    XMTALLX             YES                                          
         OI    6(R3),TRANSMIT      SET ON TRANSMIT BIT                          
         IC    R4,0(R3)            BUMP TO NEXT TWA FIELD                       
         BXLE  R3,R4,XMTALL2                                                    
         DC    H'0'                                                             
*                                                                               
XMTALLX  DS    0H                                                               
         EJECT                                                                  
* INITIALISE OUTPUT BUFFER                                                      
*                                                                               
INIBUF   L     R5,4(RA)            R5=BUFFER POINTER                            
         MVI   0(R5),STX                                                        
         MVI   1(R5),ESC           SET FORMS MODE                               
         MVI   2(R5),MODFORM                                                    
         LA    R5,3(R5)                                                         
         OC    INDICS,INDICS       TEST TRANSMIT ALL FIELDS                     
         BZ    INIBUFX             NO                                           
*                                                                               
         MVI   0(R5),FF            SET ERASE ALL                                
         MVI   1(R5),ESC                                                        
         MVI   2(R5),CMDTABE       SET MUST TAB ENABLE                          
         LA    R5,3(R5)                                                         
*                                                                               
         MVI   0(R5),ESC           SET ROW 0/COL 0 PROTECTED                    
         MVI   1(R5),CMDCURS       SET CURSOR ADDRESS                           
         IC    R0,ADDRTAB                                                       
         STC   R0,2(R5)                                                         
         STC   R0,3(R5)                                                         
         MVI   4(R5),ESC                                                        
         MVI   5(R5),CMDVATB       SET VIDEO ATTRIBUTE                          
         MVI   6(R5),ATBPH         SET PROTECTED/HIGH                           
         LA    R5,7(R5)                                                         
*                                                                               
INIBUFX  DS    0H                                                               
         EJECT                                                                  
* MOVE FIELDS FROM TWA TO BUFFER                                                
*                                                                               
SETBUF   LA    R4,64(R2)           R4=A(CURRENT TWA FIELD)                      
         MVI   CURSW,0             SET NORMAL CALL FOR FIELDS                   
         OC    INDICS,INDICS       TEST TRANSMIT ALL                            
         BNZ   SETBUF4             YES                                          
*                                                                               
         BAS   RE,SETCUR           NO - SEND CURSOR ADDR AND FIRST FLD          
         TM    6(R4),TRANSMIT      TEST TRANSMIT THIS TIME                      
         BO    SETBUF6                                                          
*                                                                               
SETBUF2  ZIC   R7,0(R4)            BUMP TO NEXT TWA FIELD                       
         AR    R4,R7                                                            
         CLI   0(R4),0             TEST END-OF-TWA                              
         BE    SETBUFX                                                          
         OC    INDICS,INDICS       TEST TRANSMIT ALL                            
         BNZ   SETBUF4             YES                                          
         CLI   FORMANY,0           TEST FORMATTING                              
         BE    SETBUF4             NO                                           
*                                                                               
         SR    RE,RE                                                            
         ICM   RE,3,2(R4)          HAS THIS LINE BEEN FORMATTED                 
         SRDA  RE,32                                                            
         D     RE,COLWIDTH         COMPUTE ROW                                  
         LR    R7,RF               SAVE ROW                                     
         LA    RF,FORMLINE(RF)                                                  
         CLI   0(RF),1             TEST THIS LINE TO BE FORMATTED               
         BNE   SETBUF4                                                          
*                                                                               
         MVI   0(RF),2             SET THIS LINE WAS FORMATTED                  
         MVI   0(R5),ESC           POSN CURSOR TO START OF LINE                 
         MVI   1(R5),CMDCURS       SET CURSOR ADDRESS                           
         IC    RE,ADDRTAB(R7)      R7 HAS ROW                                   
         STC   RE,2(R5)                                                         
         MVC   3(1,R5),ADDRTAB     COL = 0                                      
         MVI   4(R5),ESC                                                        
         MVI   5(R5),CMDXEOP       SET ERASE TO E-O-S                           
         LA    R5,6(R5)                                                         
*                                                                               
SETBUF4  TM    6(R4),TRANSMIT      TEST TRANSMIT THIS TIME                      
         BZ    SETBUF2                                                          
         BAS   RE,SETCUR                                                        
         OC    INDICS,INDICS       TEST TRANSMIT ALL                            
         BZ    SETBUF6             NO                                           
         TM    1(R4),PROTECT                                                    
         BO    SETBUF6                                                          
         CLI   0(R4),12            MORE THAN 4 DATA CHARS                       
         BNH   SETBUF6                                                          
         ZIC   RE,0(R4)                                                         
         SH    RE,=H'9'                                                         
         TM    1(R4),X'02'         TEST EXTENDED FLDHDR                         
         BZ    *+8                                                              
         SH    RE,=H'8'                                                         
         EX    RE,*+8                                                           
         B     *+10                                                             
         OC    8(0,R4),8(R4)       TEST FIELD IS BINARY ZEROES                  
         BZ    SETBUF8                                                          
         CLI   8(R4),C' '                                                       
         BNE   SETBUF6                                                          
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         CLC   9(0,R4),8(R4)       TEST FIELD IS SPACES                         
         BE    SETBUF8                                                          
*                                                                               
SETBUF6  BAS   RE,SETFLD                                                        
         B     SETBUF2                                                          
         EJECT                                                                  
* DONT NEED TO TRANSMIT BLANK FIELD BUT MUST CALCULATE ADDR FOR E-O-F           
*                                                                               
SETBUF8  MVI   0(R5),ESC                                                        
         MVI   1(R5),CMDVATB       SET VIDEO ATTRIBUTE                          
         MVI   2(R5),ATBUN         SET UNPROTECTED/NORMAL                       
         TM    6(R4),MODIFY        TEST MODIFY ON                               
         BNZ   *+12                                                             
         TM    1(R4),MODIFY                                                     
         BZ    *+8                                                              
         MVI   2(R5),ATBUNU        SET UNPROTECTED/NORMAL/UNDERLINED            
         LA    R5,3(R5)                                                         
         MVI   0(R5),ESC                                                        
         MVI   1(R5),CMDCURS       SET CURSOR ADDRESS                           
         SR    R6,R6                                                            
         ICM   R6,3,2(R4)                                                       
         SRDA  R6,32                                                            
         D     R6,COLWIDTH         R6=COLUMN,R7=ROW                             
         ZIC   R0,0(R4)            GET LEN OF FIELD + HDR                       
         SH    R0,=H'8'            LEAVES FIELD LEN                             
         TM    1(R4),X'02'         TEST EXTENDED FLDHDR                         
         BZ    *+8                                                              
         SH    R0,=H'8'                                                         
         AR    R6,R0                                                            
         C     R6,COLWIDTH         TEST LAST CHARCTER ON LINE                   
         BNE   *+10                                                             
         LA    R7,1(R7)            SET ROW TO ROW+1                             
         SR    R6,R6               SET COLUMN TO ONE                            
         IC    R0,ADDRTAB(R7)                                                   
         STC   R0,2(R5)            SET ROW                                      
         IC    R6,ADDRTAB(R6)      SET COLUMN                                   
         STC   R6,3(R5)                                                         
         MVI   4(R5),ESC                                                        
         MVI   5(R5),CMDVATB       SET VIDEO ATTRIBUTE                          
         MVI   6(R5),ATBPH         SET PROTECTED/HIGH                           
         LA    R5,7(R5)                                                         
         NI    6(R4),255-TRANSMIT  RESET TRANSMIT BIT                           
         NI    4(R4),255-PREVINPT  RESET PREVIOUSLY INPUT                       
         B     SETBUF2                                                          
*                                                                               
SETBUFX  DS    0H                                                               
         EJECT                                                                  
* IF FORMATTING - NEED TO CLEAR LINES WE DIDN'T USE THIS TIME                   
*                                                                               
SETFMT   CLI   FORMANY,0           TEST FORMATTING                              
         BE    SETFMTX             NO                                           
         OC    INDICS,INDICS       TEST TRANSMIT ALL                            
         BNZ   SETFMT4             YES                                          
*                                                                               
         SR    R4,R7               BACK UP TO LAST FIELD                        
         SR    R6,R6                                                            
         ICM   R6,3,2(R4)          GET SCREEN ADDRESS                           
         SRDA  R6,32                                                            
         D     R6,COLWIDTH         GIVES ROW IN R7                              
         ZIC   R0,0(R2)            GET PREVIOUS LAST ROW                        
         LA    R7,1(R7)                                                         
         CR    R0,R7               MORE LINES LAST TIME                         
         BNH   SETFMT4             NO - DONE                                    
         LA    R0,L'FORMLINE                                                    
         CR    R7,R0                                                            
         BH    SETFMT4                                                          
*                                                                               
SETFMT2  MVI   0(R5),ESC                                                        
         MVI   1(R5),CMDCURS       SET CURSOR ADDRESS                           
         IC    RE,ADDRTAB(R7)      SET ROW                                      
         STC   RE,2(R5)                                                         
         MVC   3(1,R5),ADDRTAB     COL=0                                        
         MVI   4(R5),ESC                                                        
         MVI   5(R5),CMDXEOP       SET ERASE TO E-O-S                           
         LA    R5,6(R5)                                                         
*                                                                               
SETFMT4  LA    R0,L'FORMLINE       SAVE LAST FORMATTED ROW NUM IN TWA           
         LA    R1,FORMLINE+L'FORMLINE-1                                         
         CLI   0(R1),0                                                          
         BNE   *+10                                                             
         BCTR  R1,0                                                             
         BCT   R0,*-10                                                          
         STC   R0,0(R2)                                                         
*                                                                               
SETFMTX  DS    0H                                                               
         EJECT                                                                  
* POSITION THE SETCUR TO INPUT FIELD                                            
*                                                                               
XMTCUR   ICM   R4,15,CURADDR       TEST CURSOR ADDRESS SET                      
         BNZ   *+8                                                              
         ICM   R4,15,FUNADDR       POINT TO FIRST UNPROT FIELD                  
         MVI   CURSW,C'C'          SET CURSOR POSITION ONLY                     
         BAS   RE,SETCUR                                                        
         NI    6(R4),255-CURSOR    RESET CURSOR SWITCH                          
*                                                                               
         MVI   0(R5),ESC                                                        
         MVI   1(R5),MODMODF       SET MODIFY MODE                              
         MVI   2(R5),ETX                                                        
         LA    R5,3(R5)                                                         
*                                                                               
         L     RE,4(RA)                                                         
         LA    RE,0(RE)                                                         
         SR    R5,RE               CALCULATE BUFFER LENGTH                      
         SH    RE,=H'2'                                                         
         STH   R5,0(RE)            STORE L'OUTPUT IN FRONT OF BUFFER            
*                                                                               
EXIT     XIT1                                                                   
         EJECT                                                                  
* ROUTINE TO SET THE CURSOR ADDRESS                                             
*                                                                               
* ON ENTRY R4=A(CURRENT TWA FIELD)                                              
* CURSW=X'0' IF NORMAL CALL FOR OUTPUT FIELDS                                   
* CURSW=C'C' IF SPECIAL CALL TO POSITION CURSOR ONLY                            
*                                                                               
SETCUR   MVI   0(R5),ESC                                                        
         MVI   1(R5),CMDCURS       SET CURSOR ADDRESS                           
         SR    R6,R6                                                            
         ICM   R6,3,2(R4)          R6=BUFFER ADDRESS                            
         CLI   CURSW,0             IS THIS POSTION CURSOR ONLY                  
         BNE   SETCUR2             YES - IGNORE FIELD TYPE                      
         TM    1(R4),PROTECT       TEST PROTECTED                               
         BO    SETCUR2                                                          
         BCTR  R6,0                NO - BACK-UP ONE POSITION IN BUFFER          
SETCUR2  SRDA  R6,32                                                            
         D     R6,COLWIDTH         ROW IN R7, COL IN R6                         
         IC    R0,FORMLINE(R7)                                                  
         STC   R0,FORMTHIS         SET FORMAT VALUE FOR THIS FIELD              
         IC    R7,ADDRTAB(R7)                                                   
         STC   R7,2(R5)                                                         
         IC    R6,ADDRTAB(R6)                                                   
         STC   R6,3(R5)                                                         
         LA    R5,4(R5)            BUMP BUFFER POINTER                          
*                                                                               
SETCURX  BR    RE                                                               
         EJECT                                                                  
* ROUTINE TO FORMAT BUFFER DATA FOR THIS FIELD                                  
*                                                                               
* ON ENTRY R4=A(CURRENT TWA FIELD)                                              
*                                                                               
SETFLD   CLI   0(R4),9             CHECK VALID TWA FIELD                        
         BNL   *+6                                                              
         DC    H'0'                                                             
         MVI   DUB,0               RESET FORMAT FLAG                            
         CLI   FORMANY,0           TEST FORMATTING                              
         BE    *+12                NO                                           
         CLI   FORMTHIS,0          THIS LINE FORMATTED                          
         BNE   *+14                YES                                          
         OC    INDICS,INDICS       TEST TRANSMIT ALL                            
         BZ    *+8                 NO                                           
         MVI   DUB,C'F'            SET FORMAT FLAG                              
*                                                                               
         TM    1(R4),PROTECT       TEST PROTECTED                               
         BO    SETFLD2                                                          
         MVI   0(R5),ESC           SET UNPROTECTED ATTRIBUTE                    
         MVI   1(R5),CMDVATB       SET CURSOR ADDRESS                           
         MVI   2(R5),ATBUN         SET UNPROTECTED/NORMAL                       
         TM    6(R4),MODIFY        TEST MODIFIED (TEMP OR PERM)                 
         BNZ   *+12                                                             
         TM    1(R4),MODIFY                                                     
         BZ    *+8                                                              
         MVI   2(R5),ATBUNU        SET UNPROTECTED/NORMAL/UNDERLINED            
         LA    R5,3(R5)                                                         
*                                                                               
SETFLD2  CLI   DUB,C'F'            TEST FORMAT FLAG                             
         BE    SETFLD6                                                          
*                                                                               
         MVI   0(R5),ESC                                                        
         MVI   1(R5),CMDXEOF       SET ERASE TO EOF                             
         LA    R5,2(R5)                                                         
         SPACE 1                                                                
* CALCULATE DATA LENGTH PRESENT *                                               
         SPACE 1                                                                
SETFLD6  ZIC   R7,0(R4)                                                         
         TM    1(R4),X'02'         TEST EXTENDED FLDHDR                         
         BZ    *+8                                                              
         SH    R7,=H'8'                                                         
         LA    R7,0(R4,R7)                                                      
         BCTR  R7,0                POINT TO LAST CHAR OF FIELD                  
         CLI   0(R7),C' '                                                       
         BH    *+8                                                              
         BCT   R7,*-8                                                           
*                                                                               
         LA    R6,8(R4)            SET A(DATA)                                  
         SR    R7,R6               GIVES LEN-1 TO XMT                           
         BM    SETFLD8                                                          
*                                                                               
         EX    R7,*+8              MOVE DATA TO BUFFER                          
         B     *+10                                                             
         MVC   0(0,R5),8(R4)       *EXECUTED*                                   
         LA    R5,1(R5,R7)         UPDATE POINTER                               
         SPACE 1                                                                
* IF FORMATTING UNPROTECTED, MUST SEND EOF *                                    
         SPACE 1                                                                
SETFLD8  CLI   DUB,C'F'                                                         
         BNE   SETFLDX                                                          
         TM    1(R4),PROTECT       TEST THIS FIELD PROTECTED                    
         BNZ   SETFLDX                                                          
*                                                                               
         MVI   0(R5),ESC                                                        
         MVI   1(R5),CMDCURS       SET CURSOR ADDRESS                           
         SR    R6,R6                                                            
         ICM   R6,3,2(R4)                                                       
         SRDA  R6,32                                                            
         D     R6,COLWIDTH                                                      
         ZIC   R0,0(R4)            GET LEN OF FIELD + HDR                       
         SH    R0,=H'8'            LEAVES FIELD LEN                             
         AR    R6,R0                                                            
         C     R6,COLWIDTH         TEST COLUMN GT LINE LENGTH                   
         BNH   *+10                                                             
         LA    R7,1(R7)            YES - BUMP ROW & SET COLUMN 1                
         SR    R6,R6                                                            
         IC    R7,ADDRTAB(R7)                                                   
         STC   R7,2(R5)                                                         
         IC    R6,ADDRTAB(R6)                                                   
         STC   R6,3(R5)                                                         
         MVI   4(R5),ESC                                                        
         MVI   5(R5),CMDVATB       SET VIDEO ATTRIBUTE                          
         MVI   6(R5),ATBPH         SET PROTECTED/HIGH                           
         LA    R5,7(R5)                                                         
*                                                                               
SETFLDX  NI    6(R4),255-TRANSMIT  RESET TRANSMIT BIT                           
         NI    4(R4),255-PREVINPT  RESET PREVIOUSLY INPUT                       
         BR    RE                                                               
         EJECT                                                                  
         LTORG                                                                  
COLWIDTH DC    F'80'                                                            
MAXSCRLN EQU   4095                                                             
         SPACE 2                                                                
*                                  ** TWA FIELD HEADER EQUATES **               
TRANSMIT EQU   X'80'               TRANSMIT FIELD                               
CURSOR   EQU   X'40'               INSERT CURSOR                                
NEWFIELD EQU   X'80'               NEW FIELD                                    
PREVINPT EQU   X'40'               FIELD INPUT PREVIOUSLY                       
MODIFY   EQU   X'01'               MODIFY FIELD                                 
INTHIGH  EQU   X'08'               HIGH INTENSITY FIELD                         
INTZERO  EQU   X'0C'               ZERO INTENSITY FIELD                         
NUMERIC  EQU   X'10'               NUMERIC FIELD                                
PROTECT  EQU   X'20'               PROTECTED FIELD                              
LOWCASE  EQU   X'40'               LOWER CASE FIELD                             
DELBLANK EQU   X'80'               DELETE BLANKS                                
         SPACE 1                                                                
*                                  ** TERMINAL MODES **                         
MODFORM  EQU   C'R'                FORMS MODE                                   
MODMODF  EQU   C'C'                MODIFY MODE                                  
         SPACE 1                                                                
*                                  ** TERMINAL/CURSOR COMMANDS **               
CMDTABE  EQU   C'H'                MUST TAB ENABLE                              
CMDCURS  EQU   C'Y'                CURSOR ADDRESS                               
CMDXEOP  EQU   X'92'               ERASE TO EOP        (LOWER CASE K)           
CMDXEOF  EQU   C'K'                ERASE TO EOF                                 
CMDNSEE  EQU   C'D'                DISABLE VIEW                                 
CMDYSEE  EQU   X'84'               ENABLE VIEW         (LOWER CASE D)           
CMDVATB  EQU   C'0'                SET VIDEO ATTRIBUTE                          
         SPACE 1                                                                
*                                  ** FIELD ATTRIBUTES **                       
ATBUN    EQU   C'A'                UNPROT/NORMAL                                
ATBUH    EQU   X'7C'               UNPROT/HIGH         (AT SIGN)                
ATBUZ    EQU   C'D'                UNPROT/ZERO                                  
ATBUNU   EQU   X'81'               UNPROT/NORMAL/ULINE (LOWER CASE A)           
ATBUHU   EQU   X'79'               UNPROT/HIGH/ULINE   (FUNNY DINK)             
ATBPN    EQU   C'I'                PROT/NORMAL                                  
ATBPH    EQU   C'H'                PROT/HIGH                                    
ATBPZ    EQU   C'L'                PROT/ZERO                                    
         EJECT                                                                  
* CONVERSION TABLE FOR ROW & COLUMN ADDRESSING                                  
*                                                                               
ADDRTAB  DS    0F                                                               
*                                                                               
         DC    X'405A7F7B5B6C507D4D5D'   00-09                                  
*                40414243444546474849    MOD USASCII                            
*                20212223242526272829    ASCII                                  
*                                                                               
         DC    X'5C4E6B604B61F0F1F2F3'   10-19                                  
*                4A4B4C4D4E4F50515253    MOD USASCII                            
*                2A2B2C2D2E2F30313233    ASCII                                  
*                                                                               
         DC    X'F4F5F6F7F8F97A5E4C7E'   20-29                                  
*                5455565758595A5B5C5D    MOD USASCII                            
*                3435363738393A3B3C3D    ASCII                                  
*                                                                               
         DC    X'6E6FC0C1C2C3C4C5C6C7'   30-39                                  
*                5E5FA0A1A2A3A4A5A6A7    MOD USASCII                            
*                3E3F4041424344454647    ASCII                                  
*                                                                               
         DC    X'C8C9D1D2D3D4D5D6D7D8'   40-49                                  
*                A8A9AAABACADAEAFB0B1    MOD USASCII                            
*                48494A4B4C4D4E4F5051    ASCII                                  
*                                                                               
         DC    X'D9E2E3E4E5E6E7E8E9AD'   50-59                                  
*                B2B3B4B5B6B7B8B9BABB    MOD USASCII                            
*                52535455565758595A5B    ASCII                                  
*                                                                               
         DC    X'E0BD5F6D798182838485'   60-69                                  
*                BCBDBEBFE0E1E2E3E4E5    MOD USASCII                            
*                5C5D5E5F606162636465    ASCII                                  
*                                                                               
         DC    X'86878889919293949596'   70-79                                  
*                E6E7E8E9EAEBECEDEEEF    MOD USASCII                            
*                666768696A6B6C6D6E6F    ASCII                                  
*                                                                               
         EJECT                                                                  
* DSECT FOR OUTPUT TRANSLATOR                                                   
*                                                                               
OUTD     DSECT                                                                  
DUB      DS    D                                                                
CURADDR  DS    A                   A(FIRST FIELD WITH CURSOR SET)               
FUNADDR  DS    A                   A(FIRST UNPROTECTED FIELD IN TWA)            
EOTADDR  DS    A                   A(END-OF-TWA)                                
SVSVCREQ DS    XL2                 TSVCREQ VALUE FORM UTL                       
INDICS   DS    XL2                 OUTPUT INDICATORS                            
CURSW    DS    X                   'C'=SET CURSOR TO THIS FIELD                 
FORMTHIS DS    X                   FORMAT LINE VALUE FOR THIS FIELD             
FORMANY  DS    X                   N/Z IF ANY FORMAT FORMAT LINES               
FORMLINE DS    XL24                ONE BYTE PER SCREEN ROW FOR FORMAT           
OUTX     EQU   *                                                                
         SPACE 1                                                                
* FASCREQUS                                                                     
       ++INCLUDE FASCREQUS                                                      
         SPACE 1                                                                
* FAUTL                                                                         
         PRINT OFF                                                              
       ++INCLUDE FAUTL                                                          
         PRINT ON                                                               
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'006FATOADDS  05/01/02'                                      
         END                                                                    
