*          DATA SET NEPRDACC   AT LEVEL 019 AS OF 12/03/08                      
*          DATA SET NEPRDACC   AT LEVEL 019 AS OF 07/25/05                      
*******************************************************************             
*******************************************************************             
*******************************************************************             
********** THIS IS THE EXTENDED BRANDS VERSION                                  
********** THE OLD NON EXTENDED BRANDS IS NEPRDACCS IN PZIR LIBRARY             
*******************************************************************             
*******************************************************************             
*******************************************************************             
*CATALP NEPACC                                                                  
         TITLE 'NEPACCN - ROUTINE TO RETURN NETWORK PROD ELEM VALUES'           
NEPACC   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 10,**NEPC**,RA                                                   
         USING NEPACCD,RC                                                       
         L     R3,0(R1)          PARAMETER 1 ADDRESS OF PROD ELEM (R3)          
         ZIC   R4,0(R1)                      SPECIFIC PROD CODE                 
         STC   R4,PRODNUM                                                       
         L     R4,4(R1)          PARAMETER 2 VALUE TO SPLIT       (R4)          
         L     R5,8(R1)          PARAMETER 3 ADDR OF 6F OUT AREA   (R5)         
         ST    R5,ADOUTBLK                                                      
         ZIC   R6,8(R1)                 PROD/FEED PCT                           
         STC   R6,BYTE                                                          
         L     R6,12(R1)         PARAMETER 4 ADDRESS OF NETBLOCK   (R6)         
         USING NETBLOCK,R6                                                      
*                                                                               
         LA    R3,1(R3)            BUMP TO PRDS IN TBL                          
         USING NXPRDS,R3                                                        
         CLI   NBPRDNO,0           TOTAL NUMBER OF PRODS ON ELEM                
         BNE   *+12                                                             
         MVI   0(R5),X'FF'         ERROR                                        
         B     PRDACCX                                                          
         SR    R2,R2               USE R2 FOR CUMMULATIVE SLIT TOTAL            
         ZIC   R0,NBPRDNO                                                       
         CLI   NBPRDNO,1           IF ONLY 1 PROD                               
         BNH   PDA32               SET TOTAL                                    
         BCTR  R0,0                ELSE -1 FOR LAST PRODUCT                     
*                                                                               
PDA20    LR    RF,R4                                                            
         C     RF,=F'0'            IF VALUE ZERO                                
         BE    PDA30                                                            
         SR    RE,RE                                                            
         ICM   RE,3,NXPPCT                                                      
         CLI   BYTE,2                                                           
         BNE   *+8                                                              
         ICM   RE,3,NXPFD                                                       
         C     RE,=F'10000'        IF 100%                                      
         BE    PDA30                                                            
         MR    RE,RE                                                            
         D     RE,=F'5000'                                                      
         LTR   RF,RF                                                            
         BM    *+8                                                              
         AH    RF,=H'1'                                                         
         SRA   RF,1                                                             
PDA30    ST    RF,0(R5)            STORE VALUE                                  
         AR    R2,RF               ADD TO CUM SPLIT TOTAL                       
         LA    R5,4(R5)            BUMP OUTPUT AREA                             
         LA    R3,NXPPLENE(R3)            BUMP TO NEXT PRODUCT                  
         BCT   R0,PDA20                                                         
* - LAST PRODUCT - SUBTRACT CUM TOTAL FROM OVERALL TOTAL                        
*                                                                               
PDA32    DS    0H                                                               
         SR    R4,R2                                                            
         ST    R4,0(R5)                                                         
         CLI   PRODNUM,0           RETURN ALL PROD VALUES                       
         BE    PRDACCX             YES                                          
         L     R5,ADOUTBLK         NO/RETURN REQUESTED PROD VALUE ONLY          
                                                                                
                                                                                
         CLI   PRODNUM,0                                                        
         BL    PRDACCX                                                          
         CLI   PRODNUM,6                                                        
         BH    PRDACCX                                                          
                                                                                
         ZIC   R1,PRODNUM          RETURN SPECIFIC PROD $ ONLY                  
         BCTR  R1,0                                                             
         MHI   R1,4                                                             
         AR    R5,R1               JUMP TO SPECIFIC PROD VALUE                  
PDA50    MVC   FULL,0(R5)          SAVE VALUE                                   
         L     R5,ADOUTBLK                                                      
         XC    0(24,R5),0(R5)      CLEAR OUT BLOCK                              
         MVC   0(4,R5),FULL        RETURN REQUESTED PROD VALUE                  
PRDACCX  XIT1                                                                   
         EJECT                                                                  
*                                  LTORG                                        
         LTORG                                                                  
         SPACE 1                                                                
*                                  DSECT FOR MODULE                             
NEPACCD  DSECT                                                                  
         DS    0CL80                                                            
ADOUTBLK DS    F                   ADDRESS OF OUTPUT BLOCK                      
FULL     DS    F                                                                
BYTE     DS    CL1                                                              
PRODNUM  DS    CL1                                                              
         DS    CL70                SPARE                                        
         SPACE 1                                                                
*                                                                               
       ++INCLUDE NETBLOCKN                                                      
       ++INCLUDE NETXBLKD                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'019NEPRDACC  12/03/08'                                      
         END                                                                    
