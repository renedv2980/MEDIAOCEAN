*          DATA SET ACADDTRN   AT LEVEL 094 AS OF 06/30/20                      
*CATALP ADDTRN                                                                  
ADDTRN   TITLE '- MODULE TO ADD DRAFT OR MAKE LIVE TRANSACTIONS'                
*                                                                               
*---------------------------------------------------------------------*         
*TSO  LVL DATE     JIRA         COMMENTS                              *         
*---  --- ----     ----         --------                              *         
*MNAS 094 16APR20  <DSRD-24207> NON-BILLABLE EXPENSE JOBS NOT REPORTING         
*RKEJ 093 16JUL19  <SPEC-36729> FIS VE PAYMENT METHOD DISPLAY         *         
*RGUP 092 21MAY18  <SPEC-20692> NEED ADDITIONAL MEDIA FOR DIGIAL AUDIO*         
*---------------------------------------------------------------------*         
ADDTRN   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKL,**ATRN**,CLEAR=YES,RR=RE                                   
         USING WORKD,RC            RC=A(LOCAL WORKING STORAGE)                  
                                                                                
         ST    RE,RELO             SAVE RELOCATION FACTOR                       
         MVC   SAVERD,4(RD)        SAVE A(START OF W/S)                         
         ST    RB,ABASE            SAVE BASE REGISTER                           
         BASR  RA,0                                                             
         AHI   RA,GLOBALS-*                                                     
         USING GLOBALS,RA          RA=A(GLOBALS)                                
                                                                                
         GOTOR ADDINI              INITIALIZE ADDTRN                            
                                                                                
         GOTOR VALTRN              VALIDATE TRANSACTION                         
         BNE   ADDTRNX                                                          
                                                                                
         GOTOR VALACT              VALIDATE ACCOUNT                             
         BNE   ADDTRNX                                                          
                                                                                
         GOTOR VALCAC              VALIDATE CONTRA-ACCOUNT                      
         BNE   ADDTRNX                                                          
                                                                                
         GOTOR VALOFA              VALIDATE OFFICE/ACCOUNT                      
         BNE   ADDTRNX                                                          
                                                                                
         GOTOR VALCHD              VALIDATE CONTRA HEADER                       
         BNE   ADDTRNX                                                          
                                                                                
         GOTOR UPDTRN              ADD/MAKE LIVE TRANSACTION                    
         BNE   ADDTRNX                                                          
                                                                                
         GOTOR UPDACT              ADD/UPDATE ACCOUNT RECORD                    
                                                                                
         GOTOR UPDOFA              ADD/UPDATE OFFICE/ACCOUNT RECORD             
                                                                                
         GOTOR UPDCAC              ADD/UPDATE CONTRA-ACCOUNT BUCKETS            
                                                                                
ADDTRNX  BRAS  RE,GLUPDT           GL UPDATE POSTINGS IF ANY, PLUS              
                                                                                
         MVC   TRNINDS,SAVEINDS    RESTORE CALLER'S TRNINDS VALUE               
         L     R0,ATRNBLK          RESTORE CALLER'S BLOCK                       
         LHI   R1,TRNBLKL                                                       
         LA    RE,TRNBLK                                                        
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
                                                                                
         CLI   TRNERRS,0           SET CONDITION CODE FOR CALLER & EXIT         
         J     EXIT                                                             
         DROP  RB                                                               
         EJECT                                                                  
***********************************************************************         
* MODULE INITIALIZATION                                               *         
***********************************************************************         
                                                                                
ADDINI   NTR1  BASE=*,LABEL=*                                                   
                                                                                
         ST    R1,ATRNBLK          SAVE A(CALLER'S TRNBLK)                      
                                                                                
         LA    R1,ADCONS           RELOCATE ADDRESS CONSTANTS                   
         LHI   R0,ADCONN                                                        
         LA    R2,WADCONS                                                       
         BASR  RE,0                                                             
         ICM   RF,15,0(R1)                                                      
         BZ    *+12                DON'T RELOCATE IF NOT LINKED                 
         A     RF,RELO                                                          
         ST    RF,0(,R2)                                                        
         AHI   R1,L'ADCONS                                                      
         AHI   R2,L'ADCONS                                                      
         BCTR  R0,RE                                                            
                                                                                
         L     RE,ATRNBLK          COPY CALLER'S TRNBLK INTO LOCAL W/S          
         LHI   R1,TRNBLKL                                                       
         LA    R0,TRNBLK                                                        
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
         MVC   SAVEINDS,TRNINDS    SAVE CALLER'S INDICATORS                     
         MVI   TRNERRS,0           RESET ERROR RETURN BYTE                      
         ZAP   TRNTRNDR,PZERO      CLEAR TRANSACTION AMOUNT                     
         ZAP   TRNTRNCR,PZERO                                                   
                                                                                
         ZAP   SSCITSAM,PZERO      INIT SAVED SCIAMNT VALUES                    
         ZAP   SSCITMAM,PZERO                                                   
         ZAP   SSCITCAM,PZERO                                                   
                                                                                
         CLI   TRNCTRY,0           SET DEFAULT COUNTRY CODE                     
         BNE   *+8                                                              
*&&US*&& MVI   TRNCTRY,CTRYUSA                                                  
*&&UK                                                                           
         MVI   TRNCTRY,CTRYGBR                                                  
         CLI   TRNCTRY,CTRYSCA     TEST SCANDINAVIA                             
         BNE   *+8                                                              
         MVI   TRNCTRY,CTRYGBR     SET SCANDINAVIA AS UK                        
         MVC   VTOBACCO,TRNTOBA    SET ADDRESS OF TOBACCO                       
*&&                                                                             
         NI    TRNMODE,FF-TRNMREVS                                              
         TM    TRNMODE,TRNMONLN+TRNMOFLN                                        
         BNZ   *+8                                                              
         MVI   TRNMODE,TRNMONLN    SET ONLINE                                   
                                                                                
         USING COMFACSD,RF                                                      
         ICM   RF,15,TRNCOMF       TEST COMFACS RESOLVED                        
         BZ    ADDINI10                                                         
         OC    CMASTC,CMASTC       MAKE SURE REALY ON OR OFF-LINE               
         BNZ   *+8                                                              
         OI    TRNMODE,TRNMON      TRUELY ON-LINE                               
         MVC   VDATCON,CDATCON     SET EXTERNAL ADDRESSES FROM COMFACS          
         MVC   VHELLO,CHELLO                                                    
         MVC   VDATAMGR,CDATAMGR                                                
         MVC   VADDAY,CADDAY                                                    
         MVC   VPROTON,CPROTON                                                  
         MVC   VPROTOFF,CPROTOFF                                                
         MVC   VGETDAY,CGETDAY                                                  
         MVC   VSWITCH,CSWITCH                                                  
         MVC   VDICTATE,CDICTATE                                                
         DROP  RF                                                               
                                                                                
ADDINI10 MVC   DATADISP,NEWDATA    SET DISPLACEMENTS TO RECORD VALUES           
         MVC   FILE,ACCMST                                                      
                                                                                
         L     RF,=A(DCDICTL)                                                   
         L     R0,RELO                                                          
         AR    RF,R0                                                            
         GOTO1 VDICTATE,DMCB,C'LL  ',(RF),DSDICTL                               
                                                                                
         MVI   SPACES,C' '                                                      
         MVC   SPACES+1(L'SPACES-1),SPACES                                      
                                                                                
         LA    RF,WORKD                                                         
         AHI   RF,WORKX-WORKD      RF=A(END OF MNOD AREA)                       
         LHI   RE,IOL              RE=L'I/O CONTROL BLOCK                       
         SR    R0,R0               R0=L'ACQUIRED STORAGE SO FAR                 
                                                                                
         OC    AACT,TRNACC         SET A(ACCOUNT I/O AREA)                      
         BNZ   *+16                                                             
         ST    RF,AACT                                                          
         MVI   TRNACCI,TRNIOXC                                                  
         AR    RF,RE               INCREMENT ADDRESS POINTER                    
         AR    R0,RE               INCREMENT ACQUIRED STORAGE LENGTH            
                                                                                
         OC    AOFA,TRNOFA         SET A(OFFICE/ACCOUNT I/O AREA)               
         BNZ   *+16                                                             
         ST    RF,AOFA                                                          
         MVI   TRNOFAI,TRNIOXC                                                  
         AR    RF,RE               INCREMENT ADDRESS POINTER                    
         AR    R0,RE               INCREMENT ACQUIRED STORAGE LENGTH            
                                                                                
         OC    ACAC,TRNCAC         SET A(CONTRA-ACCOUNT I/O AREA)               
         BNZ   *+16                                                             
         ST    RF,ACAC                                                          
         MVI   TRNCACI,TRNIOXC                                                  
         AR    RF,RE               INCREMENT ADDRESS POINTER                    
         AR    R0,RE               INCREMENT ACQUIRED STORAGE LENGTH            
                                                                                
         OC    ABUK,TRNBUK         SET A(BUCKET LIST)                           
         BNZ   *+16                                                             
         ST    RF,ABUK                                                          
         MVI   TRNBUKI,TRNIOXC                                                  
         AR    RF,RE               INCREMENT ADDRESS POINTER                    
         AR    R0,RE               INCREMENT ACQUIRED STORAGE LENGTH            
                                                                                
         OC    APAL,TRNPAL         SET A(P&L BUCKET AREA)                       
         BNZ   ADDINI20                                                         
         ST    RF,APAL                                                          
         AHI   RF,PALLNQ                                                        
         AHI   R0,PALLNQ                                                        
                                                                                
ADDINI20 MVC   LEDGERS#,TRN#LDGS                                                
         CLI   LEDGERS#,0          WAS THIS SET ?                               
         BNE   *+8                                                              
         MVI   LEDGERS#,1          SUPPORT AT LEAST 1                           
         OC    ALDG,TRNLDG         SET A(LEDGER TABLE)                          
         BNZ   ADDINI38                                                         
         ST    RF,ALDG                                                          
         MVI   LEDGERS#,MAXLDGQ                                                 
         AHI   RF,MAXLDGQ*LDGTABL                                               
         AHI   R0,MAXLDGQ*LDGTABL                                               
                                                                                
ADDINI38 AHI   R0,7                ROUND TO NEAREST DOUBLE WORD                 
         SRL   R0,3                                                             
         SLL   R0,3                                                             
                                                                                
         L     RE,4(,RD)           SET NEW WORKING-STORAGE LINKAGES             
         AR    RD,R0                                                            
         LR    RF,RE                                                            
         AR    RF,R0                                                            
         MVC   ELEMENT(72),0(RE)                                                
         MVC   0(72,RF),ELEMENT                                                 
         ST    RF,4(RD)                                                         
         ST    RD,8(RF)                                                         
         L     RE,4(RF)                                                         
         ST    RF,8(RE)                                                         
                                                                                
         LTR   R1,R0               TEST ANY STORAGE ACQUIRED                    
         BZ    ADDINI40                                                         
         LA    R0,WORKD            YES - CLEAR TO BINARY ZEROES                 
         AHI   R0,WORKX-WORKD                                                   
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
                                                                                
*----------------------------------*                                            
* ON-LINE INITIALIZE OF GL BLOCK   *                                            
*----------------------------------*                                            
ADDINI40 TM    TRNMODE,TRNMON      SET ONLINE                                   
         BZ    ADDINI46            NO, MUST BE OFF-LINE                         
         TM    TRNGLIND,TRNGLPAS                                                
         BO    ADDINI48            GO AROUND                                    
         XC    DUB,DUB             PROCESS ON-LINE                              
         MVC   DUB(4),=X'00FFFFFF'                                              
         GOTO1 VSWITCH,DUB                                                      
         MVC   ATCB,DUB                                                         
         MVI   ATCB,0                                                           
                                                                                
         USING TCBD,RF                                                          
         L     RF,ATCB                                                          
         MVC   ATIA,TCBPAR2        A(TIA)                                       
         MVC   TRNGLBLK,ATIA       USE AS STORAGE FOR HIT OF ENTER              
         MVI   TRNGLBLK,GL#OFK     SIZE WE IN K WE WANT TO USE                  
         L     RE,TCBMINI2                                                      
         AHI   RE,16               FOR EYE CATCHER                              
         ST    RE,AMINBUF2         1M BLOCK OF 31 BIT ADDRESSING                
         DROP  RF                                                               
                                                                                
         GOTOR SWAP#K,DMCB,ATIA,AMINBUF2,GL#OFK   SWAP                          
         TM    TRNGLIND,TRNGLINT   WAS THIS INITIALIZED                         
         BO    ADDINI48            YES                                          
         GOTOR SWAP#K,DMCB,ATIA,ATIA,GL#OFK       CLEAR                         
         MVI   TRNGLIND,TRNGLINT   INITIALIZE                                   
         B     ADDINI48                                                         
                                                                                
*-----------------------------------*                                           
* OFF-LINE INITIALIZE OF GL BLOCK   *                                           
*-----------------------------------*                                           
ADDINI46 ICM   R1,15,TRNGLBLK      WAS THIS SET ?                               
         BNZ   ADDINI48            YES                                          
         TM    TRNGLIND,TRNGLINT   WAS THIS INITIALIZED                         
         BZ    *+6                 SOMETHINGS WRONG                             
         DC    H'00'                                                            
                                                                                
         ICM   R1,15,LGLBLOCK      LOCAL STATIC VALUE                           
         BNZ   ADDINI47                                                         
         LHI   R0,GL#OFK*K         CLEAR FOR SIZE WE SAVED                      
         GETMAIN RU,LV=(0),BNDRY=PAGE                                           
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'00'               FAILED TO GET THE STORAGE                    
         ST    R1,LGLBLOCK                                                      
                                                                                
ADDINI47 ST    R1,TRNGLBLK                                                      
         MVI   TRNGLBLK,GL#OFK                                                  
         MVI   TRNGLIND,TRNGLINT   INITIALIZE                                   
                                                                                
         USING GLBLKD,R1                                                        
ADDINI48 TM    TRNGLIND,TRNGLINT   WAS THIS INITIALIZED ?                       
         BO    *+6                                                              
         DC    H'00'               MUST BE SET BEFORE WE GET HERE               
                                                                                
         ICM   R1,15,TRNGLBLK      WAS THIS SET ?                               
         BNZ   *+6                                                              
         DC    H'00'               GOT SOMETHING WRONG                          
                                                                                
         LA    R1,0(,R1)           CLEAR HOB                                    
         MVC   @COMFACS,TRNCOMF    PASS COMFACS                                 
         MVC   @TRANACT,TRNREC     A(TRANACTION)                                
         LA    RE,TRNBLK                                                        
         ST    RE,@ATRNBLK                                                      
         MVI   GLPRG#,GLDATRN      ADDED VIA ADDTRANS                           
         MVI   GLWRITE,NO                                                       
         TM    TRNINDS,TRNIWRNO    TEST WRITE=NO                                
         BO    *+8                                                              
         MVI   GLWRITE,YES                                                      
                                                                                
         NI    PRGIND,FF-PRGNEWF   RESET EACH TIME IN                           
         TM    TRNINDS,TRNICONV    NEW FILE                                     
         BZ    *+8                 NO                                           
         OI    PRGIND,PRGNEWF      NEW FILE (ACCMST/ACCDIR)                     
         DROP  R1                                                               
                                                                                
         USING TRNRECD,R6          R6=A(TRANSACTION RECORD)                     
         ICM   R6,15,TRNREC                                                     
         LA    R7,TRNRECD                                                       
         LA    RE,NEWDATA                                                       
         MVC   FILETRN,ACCMST                                                   
         TM    TRNINDS,TRNICONV+TRNILIVE                                        
         BNZ   *+14                                                             
         LA    RE,OLDDATA                                                       
         MVC   FILETRN,ACCFIL                                                   
         AH    R7,0(RE)                                                         
                                                                                
         USING TRNELD,R7           R7=A(TRANSACTION ELEMENT)                    
         TM    TRNINDS,TRNILAST+TRNILIVE                                        
         BNZ   ADDINI65                                                         
         OC    TRNRLEN,TRNRLEN     TEST RECORD LENGTH SET                       
         BNZ   ADDINI65                                                         
                                                                                
         LA    R1,TRNELD                                                        
         SR    R0,R0                                                            
ADDINI60 CLI   0(R1),0             TEST EOR                                     
         BE    ADDINI62                                                         
         ICM   R0,1,1(R1)                                                       
         BNZ   *+12                                                             
         MVI   0(R1),0                                                          
         B     ADDINI62                                                         
         AR    R1,R0                                                            
         B     ADDINI60                                                         
                                                                                
ADDINI62 AHI   R1,1                ADD ONE FOR EOR INDICATOR                    
         LA    R0,TRNRECD                                                       
         SR    R1,R0                                                            
         STCM  R1,3,TRNRLEN        SET RECORD LENGTH                            
                                                                                
ADDINI65 OC    TRNPDATS,TRNPDATS   INITIALIZE DATES IF NOT SET                  
         BNZ   ADDINI70                                                         
         GOTOR VDATCON,PARM,(5,0),(2,TRNPDAT2)                                  
         GOTOR (RF),(R1),(2,TRNPDAT2),(1,TRNPDAT1)                              
                                                                                
ADDINI70 OC    TRNUDATE,TRNUDATE   TEST USER OVERRIDE DATE                      
         BZ    ADDINIX                                                          
         CLC   TRNUDATE,TRNPDAT2   TEST SAME AS SET DATE                        
         BE    ADDINIX                                                          
         MVC   TRNPDAT2,TRNUDATE                                                
         GOTOR VDATCON,PARM,(2,TRNPDAT2),(1,TRNPDAT1)                           
                                                                                
ADDINIX  XIT1  REGS=(R6,R7)                                                     
         DROP  RB                                                               
         EJECT                                                                  
***********************************************************************         
* VALIDATE TRANSACTION RECORD                                         *         
***********************************************************************         
                                                                                
VALTRN   NTR1  BASE=*,LABEL=*                                                   
         TM    TRNINDS,TRNILAST    TEST FINAL CALL                              
         BNZ   VALTRNX                                                          
         CLC   PRODUL,TRNKUNT      SET PRODUCTION LEDGER FLAGS                  
         BNE   *+8                                                              
         OI    INDS2,INDPRODN+INDKEYWC                                          
*&&UK                                                                           
         CLC   ARTULUK,TRNKUNT     SET ST LEDGER FLAGS                          
         BNE   *+8                                                              
         OI    INDS2,INDSTLDG                                                   
*&&                                                                             
         CLC   PCTLUL,TRNKUNT      SET PROJECT CONTROL FLAG                     
         BNE   *+8                                                              
         OI    INDS2,INDKEYWC                                                   
         TM    TRNINDS2,TRNIUBKO   TEST UPDATING BUCKETS ONLY                   
         BZ    VALTRN02                                                         
         GOTOR GETMOS              ESTABLISH MOS FOR BUCKET                     
         TM    TRNCPYS4,CPYSOFF2   TEST IF COMPANY USES NEW OFFICE              
         BZ    VALTRNX                                                          
         TM    TRNINDS2,TRNIUOFC   TEST UPDATING OFFICE CONTRA HEADER           
         BZ    VALTRNX                                                          
         CLC   TRNOFFC,SPACES      ENSURE OFFICE IS PRESENT                     
         BNH   VALTRNE1                                                         
         MVC   TRNKOFF,TRNOFFC     SET OFFICE CODE FROM ELEMENT                 
         B     VALTRNX                                                          
                                                                                
VALTRN02 TM    TRNINDS,TRNIDRFT+TRNILIVE                                        
         BZ    VALTRN04                                                         
         BM    *+6                                                              
         DC    H'0'                ACTION CAN'T BE LIVE AND DRAFT               
                                                                                
         NI    TRNRSTAT,FF-TRNSDRFT                                             
         TM    TRNINDS,TRNILIVE    TEST DRAFT TO LIVE                           
         BNZ   VALTRN03                                                         
         OI    TRNRSTAT,TRNSDRFT   SET DRAFT STATUS BIT ON                      
         B     VALTRN04                                                         
                                                                                
         USING GLBLKD,R1                                                        
VALTRN03 L     R1,TRNGLBLK         GL BLOCK                                     
         OI    PRGIND,PRGNEWF      NEW FILE (ACCMST/ACCDIR)                     
         OI    TRNINDS,TRNICONV    SET RECORD IS CONVERTED (NEW FORMAT)         
         DROP  R1                                                               
                                                                                
         GOTOR READ,TRNKEY         READ DRAFT TRANSACTION RECORD                
         BNE   VALTRNE1                                                         
         TM    TRNRSTAT,TRNSDRFT   ENSURE TRANSACTION IS DRAFT                  
         BZ    VALTRNE2                                                         
         NI    TRNRSTAT,FF-TRNSDRFT AND MAKE IT LIVE                            
                                                                                
VALTRN04 CLI   TRNOFFC+0,C' '      ENSURE NO FUNNY CHARACTERS IN OFFICE         
         BH    *+8                                                              
         MVI   TRNOFFC+0,C' '                                                   
         CLI   TRNOFFC+1,C' '                                                   
         BH    *+8                                                              
         MVI   TRNOFFC+1,C' '                                                   
                                                                                
         CLC   PRODUL,TRNKUNT      SET PRODUCTION LEDGER FLAGS                  
         BNE   *+8                                                              
         OI    INDS2,INDPRODN+INDKEYWC                                          
                                                                                
         CLI   TRNKUNT,C'G'        GENERAL LEDGER ?                             
         BNE   VALTRN05                                                         
         CLI   TRNKLDG,C'B'                                                     
         BE    *+8                                                              
         CLI   TRNKLDG,C'P'                                                     
         BNE   VALTRN05                                                         
         OI    INDS2,INDGENLG                                                   
                                                                                
VALTRN05 CLC   PCTLUL,TRNKUNT      SET PROJECT CONTROL FLAG                     
         BNE   *+8                                                              
         OI    INDS2,INDKEYWC                                                   
                                                                                
         TM    TRNINDS,TRNILIVE    DON'T CHANGE KEY IF DRAFT TO LIVE            
         BNZ   VALTRN06                                                         
         MVC   TRNKSBR,TRNSUB      SET SUB-REFERENCE FROM ELEMENT               
         TM    TRNCPYS4,CPYSOFF2   TEST COMPANY ON NEW OFFICES                  
         BZ    VALTRN06                                                         
         TM    INDS2,INDKEYWC      TEST LEDGER HAS W/C IN KEY                   
         BNZ   VALTRN06                                                         
         MVC   TRNKOFF,TRNOFFC     SET OFFICE CODE FROM ELEMENT                 
                                                                                
VALTRN06 DS    0H                                                               
*&&UK                                                                           
         CLC   FILMULUK,TRNKUNT    TEST UK FILM LEDGER                          
         BNE   *+16                                                             
         CLI   TRNTYPE,255         TEST DUMMY TRANSACTION                       
         BNE   *+8                                                              
         OI    INDS1,IND2FDUM      SET GLOBAL INDICATOR                         
*&&                                                                             
         CLI   TRNKUNT,C'3'        TEST RETAIL UNIT                             
         BNE   *+16                                                             
         CLI   TRNTYPE,TRNTEPRD    TEST BATCH TYPE 47                           
         BNE   *+8                                                              
         OI    INDS1,INDRET47      SET GLOBAL INDICATOR                         
                                                                                
         CLC   CPERULUS,TRNKUNT    TEST POSTING TO COSTING/PERSON               
         BNE   VALTRN08                                                         
*&&US                                                                           
         CLC   TRNKCULC,SPACES     TEST CONTRA ACCOUNT IS SPACES                
         BNE   VALTRN08                                                         
         OI    INDS1,IND1RDUM      SET GLOBAL INDICATOR                         
*&&                                                                             
*&&UK                                                                           
         CLC   TRNKCULA,TRNKCULC   TEST ACCOUNT SAME AS CONTRA-ACCOUNT          
         BNE   VALTRN08                                                         
         CLI   TRNTYPE,TRNTMEBL    AND BATCH TYPE 9                             
         BNE   VALTRN08                                                         
         CP    TRNAMNT,PZERO       AND ZERO POSTING AMOUNT                      
         BNE   VALTRN08                                                         
         OI    INDS1,IND1RDUM      SET GLOBAL INDICATOR                         
*&&                                                                             
VALTRN08 TM    INDS1,IND2FDUM+INDRET47+IND1RDUM                                 
         BNZ   VALTRNX                                                          
                                                                                
         LA    R1,TRNKCULA         PRE-VALIDATE ACCOUNT CODE                    
         LHI   R0,L'TRNKCULA                                                    
         MVI   BYTE1,0                                                          
         BASR  RE,0                                                             
         CLI   0(R1),C' '                                                       
         BNH   *+12                                                             
         MVI   BYTE1,1                                                          
         B     *+8                                                              
         MVI   0(R1),C' '                                                       
         AHI   R1,1                                                             
         BCTR  R0,RE                                                            
         CLI   BYTE1,0             TEST ACCOUNT FOR ALL SPACES                  
         BE    VALTRNE1                                                         
                                                                                
         LA    R1,TRNKCULC         PRE-VALIDATE CONTRA-ACCOUNT CODE             
         LHI   R0,L'TRNKCULC                                                    
         MVI   BYTE1,0                                                          
         BASR  RE,0                                                             
         CLI   0(R1),C' '                                                       
         BNH   *+12                                                             
         MVI   BYTE1,1                                                          
         B     *+8                                                              
         MVI   0(R1),C' '                                                       
         AHI   R1,1                                                             
         BCTR  R0,RE                                                            
         CLI   BYTE1,0             TEST CONTRA-ACCOUNT FOR ALL SPACES           
         BE    VALTRNE1                                                         
                                                                                
         GOTOR VALDAT,TRNKDATE     VALIDATE KEY DATE                            
         BNE   VALTRNE1                                                         
                                                                                
         LA    R1,TRNKREF          VALIDATE REFERENCE                           
         LHI   R0,L'TRNKREF                                                     
         CLI   0(R1),C' '          NO CHARACTER LESS THAN BLANK                 
         BH    *+8                                                              
         MVI   0(R1),C' '                                                       
         AHI   R1,1                                                             
         BCT   R0,*-16                                                          
                                                                                
         CLI   TRNEL,TRNELQ        TEST TRANSACTION RECORD PASSED               
         BNE   VALTRNE1                                                         
*&&US                                                                           
         CLI   TRNTYPE,TRNTTAXA    TALENT TAX ADJUSTMENT                        
         BNE   VALTRN10                                                         
         CLC   TRNOFFC,BILLWC      SPECIAL POSTINGS HAVE 99                     
         BNE   VALTRN10                                                         
         MVC   TRNKOFF,BILLWC      IN KEY                                       
         MVC   TRNOFFC,SPACES      NOT IN ANALYSIS                              
         B     VALTRN12                                                         
*&&                                                                             
VALTRN10 TM    INDS2,INDPRODN      TEST PRODUCTION LEDGER                       
         BZ    VALTRN12                                                         
         CLI   TRNKOFF+0,C' '      TEST PRODUCTION ANALYSIS CODE                
         BNH   VALTRNE1                                                         
         CLI   TRNKOFF+1,C' '                                                   
         BL    VALTRNE1                                                         
*&&UK                                                                           
         CLC   CPERULUK,TRNKCUNT   TEST CONTRA IS COSTING/PERSON                
         BNE   VALTRN12                                                         
         CLI   TRNTYPE,TRNTJBTX    TYPE 34 MAY POST MEMO                        
         BE    VALTRN12                                                         
         CLI   TRNTYPE,TRNTCLTM    TYPE 49 MAY POST MEMO                        
         BE    VALTRN12                                                         
         CLI   TRNTYPE,99          TYPE 99 MAY POST MEMO                        
         BE    VALTRN12                                                         
         CP    TRNAMNT,PZERO       DROP ZERO 1P POSTINGS                        
         JE    EXITN                                                            
*&&                                                                             
VALTRN12 LA    R1,TRNRECD          CLEAR STATUS AREA IN TRANSACTION             
         LA    RE,NEWSTAT                                                       
         LHI   RF,ACCRFST-ACCRSTA-1                                             
         TM    TRNINDS,TRNICONV                                                 
         BNZ   *+12                                                             
         LA    RE,OLDSTAT                                                       
         LHI   RF,ACCORFST-ACCOSTAT-1                                           
         AH    R1,0(RE)                                                         
         EX    RF,*+8                                                           
         B     *+10                                                             
         XC    0(0,R1),0(R1)       CLEAR STATUS AREA IN RECORD                  
                                                                                
         GOTOR VALDAT,TRNDATE      VALIDATE TRANSACTION DATE                    
         BNE   VALTRNE1                                                         
                                                                                
         CP    TRNAMNT,PZERO       ENSURE TRANSACTION AMOUNT IS PACKED          
*&&UK                                                                           
         LA    R1,TRNELD                                                        
         USING SCIELD,R1                                                        
         SR    R0,R0               ENSURE ALL SCIAMTS ARE PACKED                
VALTRN14 CLI   SCIEL,0                                                          
         BE    VALTRN18                                                         
         CLI   SCIEL,SCIELQ                                                     
         BNE   VALTRN16                                                         
         CP    SCIAMNT,PZERO                                                    
         CLI   SCILN,SCILN2Q                                                    
         BL    VALTRN16                                                         
         CP    SCINET,PZERO                                                     
VALTRN16 IC    R0,SCILN                                                         
         AR    R1,R0                                                            
         B     VALTRN14                                                         
         DROP  R1                                                               
*&&                                                                             
VALTRN18 GOTOR GETLDG,TRNRECD      GET LEDGER TABLE ENTRY                       
         L     R1,ALDGNTRY                                                      
         USING LDGTABD,R1                                                       
         MVC   CLOS,LDGTCLOS       SET CLOSE OUT TYPE                           
         LA    R1,LDGTLVA                                                       
         LHI   R0,4                                                             
         SR    RE,RE                                                            
         CLI   0(R1),0                                                          
         BE    *+16                                                             
         AHI   R1,1                                                             
         AHI   RE,1                                                             
         BCT   R0,*-16                                                          
         STC   RE,ACTLEV           SET LOWEST ACCOUNT LEVEL                     
         DROP  R1                                                               
                                                                                
VALTRN22 GOTOR GETTRS,TRNRECD      GET TRANSACTION STATUS ELEMENT               
         LR    R2,R1               AND UPDATE WITH VALUES                       
         USING TRSELD,R2                                                        
         TM    TRSSTAT,TRSSREVS                                                 
         BZ    *+8                                                              
         OI    TRNMODE,TRNMREVS    SET THIS TRANSACTION IS A REVERSAL           
         MVC   MOSREV,TRSRMOS      CALLER MAY SUPPLY REVERSING MOS              
         TM    TRNINDS,TRNILIVE    TEST DRAFT TO LIVE                           
         BZ    VALTRN24                                                         
         MVC   TRSEFDT,TRSDATE     SAVE ADDED DATE IN EFFECTIVE DATE            
         MVC   TRSDATE,TRNPDAT2    SET LIVE TODAY                               
         XC    TRSRMOS,TRSRMOS     CLEAR REVERSING MONTH IN ELEMENT             
         B     VALTRN26                                                         
                                                                                
VALTRN24 OC    TRSDATE,TRSDATE     TEST USER SUPPLIED DATE                      
         BNZ   *+10                                                             
         MVC   TRSDATE,TRNPDAT2    SET TODAY'S DATE                             
         MVC   TRSUSER,TRNPUSER    SET ADDING USER-ID NUMBER                    
         MVC   TRSBSEQ,TRNBSEQN    SET TRANSACTION SEQUENCE NUMBER              
         MVC   TRSEFDT,TRNEFDT     SET EFFECTIVE (LIVE) DATE                    
         TM    TRNINDS,TRNIDRFT    TEST DRAFT TO LIVE                           
         BNZ   *+10                                                             
         XC    TRSRMOS,TRSRMOS     CLEAR REVERSING MONTH IN ELEMENT             
         TM    TRNTIND1,TRNTIACR   TEST ACCRUAL (IN)                            
         BZ    *+8                                                              
         OI    TRSSTAT2,TRSSACRL                                                
         TM    TRNTIND1,TRNTIARV   TEST ACCRUAL REVERSAL (OUT)                  
         BZ    *+8                                                              
         OI    TRSSTAT2,TRSSACRV                                                
         NI    TRNTIND1,FF-(TRNTIACR+TRNTIARV)                                  
                                                                                
VALTRN26 TM    TRNINDS,TRNILIVE    TEST DRAFT TO LIVE                           
         BZ    VALTRN27                                                         
         MVC   MOS,TRSPMOS         YES - USE ORIGINAL MOS                       
         B     VALTRN28                                                         
                                                                                
VALTRN27 GOTOR GETMOS              ESTABLISH TRANSACTION MOS VALUE              
         MVC   TRSPMOS,MOS         SET MOS IN STATUS ELEMENT                    
                                                                                
VALTRN28 DS    0H                                                               
*&&UK*&& TM    TRNCPYS4,CPYSOFF2   TEST NEW OFFICES                             
*&&UK*&& BZ    VALTRN30                                                         
         CLI   TRNOFFC+0,C' '      ENSURE HAS OFFICE OR WORKCODE                
         BNH   VALTRNE1                                                         
         TM    INDS2,INDPRODN      PRODUCTION CAN HAVE 1 CHARACTER W/C          
         BO    VALTRN30                                                         
*&&US*&& TM    TRNCPYS4,CPYSOFF2   TEST NEW OFFICES                             
*&&US*&& BZ    VALTRN30                                                         
         CLI   TRNOFFC+1,C' '                                                   
         BNH   VALTRNE1                                                         
                                                                                
VALTRN30 DS    0H                                                               
*&&UK                                                                           
         TM    TRNINDS,TRNILIVE+TRNILAST                                        
         BNZ   VALTRNX                                                          
         TM    TRNCPYS8,CPYS1P13   TEST POST 1PDDX TO 13X                       
         BZ    VALTRNX                                                          
         CLC   CEXPUL,TRNKUNT      TEST POSTING TO EXPENSE LEDGER               
         BE    *+14                                                             
         CLC   CCSTUL,TRNKUNT      TEST POSTING TO COSTING CLIENT               
         BNE   VALTRN38                                                         
                                                                                
         LA    R1,TRNELD                                                        
         SR    R0,R0                                                            
         USING APEELD,R1                                                        
VALTRN32 IC    R0,APELN            LOCATE ATTRIBUTE ELEMENT ON POSTING          
         AR    R1,R0                                                            
         CLI   APEEL,0                                                          
         BE    VALTRNX                                                          
         CLI   APEEL,APEELQ                                                     
         BNE   VALTRN32                                                         
                                                                                
         IC    R0,APENUM                                                        
         LA    RE,APENTRY                                                       
         USING APENTRY,RE                                                       
         SR    RF,RF                                                            
VALTRN34 CLC   CPERULUK,APENACT    LOCATE 1P ATTRIBUTE                          
         BE    VALTRN36                                                         
         IC    RF,APENLEN                                                       
         AR    RE,RF                                                            
         BCT   R0,VALTRN34                                                      
         B     VALTRN32                                                         
                                                                                
VALTRN36 MVC   WORK(L'APENACT),SPACES                                           
         IC    RF,APENLEN                                                       
         SHI   RF,APELN2Q+1                                                     
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   WORK(0),APENACT                                                  
         CLC   WORK+5(9),SPACES    TEST 1PDDX ATTRIBUTE                         
         BNE   VALTRNX                                                          
         CLI   WORK+4,C' '                                                      
         BNH   VALTRNX                                                          
                                                                                
         LA    RF,ELEMENT                                                       
         USING FFTELD,RF           BUILD & ADD EXPENSE FFTEL TO POSTING         
         MVI   FFTEL,FFTELQ                                                     
         MVI   FFTLN,FFTLN1Q+L'FFTDLEN+1                                        
         MVI   FFTTYPE,FFTTETYP                                                 
         MVI   FFTSEQ,1                                                         
         MVI   FFTDLEN,1                                                        
         MVC   FFTDATA(1),WORK+4                                                
         GOTO1 VHELLO,ELIST,(C'P',FILETRN),TRNRECD,FFTELD,0                     
         CLI   ELERR,0                                                          
         BNE   VALTRNE1                                                         
         B     VALTRNX                                                          
         DROP  RF                                                               
                                                                                
VALTRN38 CLC   CPERULUK,TRNKUNT    TEST 1PDDX POSTING                           
         BNE   VALTRNX                                                          
         CLI   TRNKACT+2,C' '                                                   
         BNH   VALTRNX                                                          
         CLC   TRNKACT+3(9),SPACES                                              
         BNE   VALTRNX                                                          
         IC    RF,TRNKACT+2                                                     
         MVI   TRNKUNT,C'1'        CHANGE IT TO POST TO 13X INSTEAD             
         MVI   TRNKLDG,C'3'                                                     
         MVC   TRNKACT,SPACES                                                   
         STC   RF,TRNKACT                                                       
*&&                                                                             
VALTRNX  J     EXITY                                                            
                                                                                
VALTRNE1 MVI   TRNERRS,TRNETRNI    SET INVALID TRANSACTION ERROR                
         J     EXITN                                                            
                                                                                
VALTRNE2 MVI   TRNERRS,TRNETRNS    SET TRANSACTION STATUS ERROR                 
         J     EXITN                                                            
         DROP  R2,RB                                                            
         EJECT                                                                  
***********************************************************************         
* VALIDATE ACCOUNT BEING POSTED TO                                    *         
***********************************************************************         
                                                                                
VALACT   NTR1  BASE=*,LABEL=*                                                   
         L     R5,AACT                                                          
         USING ACTRECD,R5          R5=A(ACCOUNT RECORD)                         
                                                                                
         TM    TRNACCI,TRNIOXC     TEST I/O AREA INITIALIZED                    
         BNZ   VALACT02                                                         
         LA    R0,ACTRECD                                                       
         LHI   R1,IOL                                                           
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
         OI    TRNACCI,TRNIOXC     SET I/O AREA INITIALIZED                     
                                                                                
VALACT02 OC    TRNACC,TRNACC       TEST A(ACCOUNT PASSED)                       
         BZ    VALACT06                                                         
         TM    TRNINDS,TRNILAST    TEST LAST TIME CALL                          
         BNZ   VALACT04                                                         
         CLC   TRNKCULA,ACTKCULA   TEST CHANGE OF ACCOUNT                       
         BNE   VALACT04                                                         
         TM    TRNACCI,TRNAINVA    TEST ACCOUNT IS INVALID                      
         BNZ   VALACTE                                                          
         B     VALACT10                                                         
                                                                                
VALACT04 GOTOR PUTACT              ADD OR WRITE BACK LAST ACCOUNT               
         XC    ACTKCULA,ACTKCULA   FORCE READ ACCOUNT NEXT TIME                 
                                                                                
VALACT06 TM    TRNINDS,TRNILAST    TEST FINAL CALL                              
         BNZ   VALACTX                                                          
         NI    TRNACCI,TRNIOXC                                                  
         MVC   ACTKEY,SPACES       READ ACCOUNT RECORD                          
         MVC   ACTKCULA,TRNKCULA                                                
         GOTOR READ,ACTKEY                                                      
         TM    IOERR,IOEBAD        TEST SERIOUS ERROR (NF/EOF/ERROR)            
         BNZ   VALACT08                                                         
         TM    TRNINDS2,TRNIUBKO   TEST UPDATING BUCKETS ONLY                   
         BNZ   VALACTX                                                          
         NI    ACTRSTAT,FF-(ACTSDELT)                                           
         GOTOR GETRST              GET/ADD STATUS ELEMENT                       
         BNE   VALACTE                                                          
         USING RSTELD,R1                                                        
         TM    RSTSTAT1,RSTSACIC+RSTSACIL                                       
         BZ    *+12                                                             
         TM    TRNINDS,TRNIDUCL    TEST DON'T UNLOCK/UNCLOSE ACCOUNTS           
         BNZ   VALACTC                                                          
*&&UK                                                                           
         CLI   RSTLN,RSTLN3Q                                                    
         BL    *+16                                                             
         TM    RSTSTAT5,RSTSSUND   TEST SUNDRY CREDITOR ACCOUNT                 
         BZ    *+8                                                              
         OI    TRNACCI,TRNASUND    YES - SET INDICATOR BIT ON                   
*&&                                                                             
         OI    TRNACCI,TRNIOWP     SET WRITE PENDING FOR ACCOUNT                
         MVC   TRNACCDA,DA         SAVE D/A OF ACCOUNT RECORD                   
         B     VALACT10                                                         
         DROP  R1                                                               
                                                                                
VALACT08 TM    TRNINDS2,TRNIUBKO   TEST UPDATING BUCKETS ONLY                   
         BNZ   VALACTE                                                          
                                                                                
         OI    TRNACCI,TRNIOAP     BUILD NEW ACCOUNT RECORD                     
         GOTOR BLDREC,PARM,KEYSAVE,ACTRECD,0                                    
                                                                                
         GOTOR BLDABL,ACTRECD      ADD BALANCE ELEMENT TO RECORD                
         BNE   VALACTE                                                          
                                                                                
         GOTOR GETRST              ADD STATUS ELEMENT TO RECORD                 
         BNE   VALACTE                                                          
                                                                                
         GOTOR BLDAST,ACTRECD      ADD A/C STATUS ELEMENT TO RECORD             
         BNE   VALACTE                                                          
                                                                                
         GOTOR BLDAPO,ACTRECD      ADD PEEL-OFF ELEMENT TO RECORD               
         BNE   VALACTE                                                          
                                                                                
VALACT10 LA    R2,TRNELD           PROCESS TRANSACTION FOR AUTOS                
         SR    R0,R0                                                            
         MVI   BYTE1,0             SET NO ACCOUNT ELEMENTS FOUND YET            
VALACT12 IC    R0,1(,R2)           BUMP TO NEXT ELEMENT                         
         AR    R2,R0                                                            
         CLI   0(R2),0             TEST END OF TRANSACTION RECORD               
         BE    VALACT40                                                         
                                                                                
         CLI   0(R2),DELELQ        TEST SPECIAL DELETE ELEMENT                  
         BNE   *+12                                                             
         OI    BYTE1,QDELEL                                                     
         B     VALACT12                                                         
                                                                                
         USING FFTELD,R2                                                        
         CLI   FFTEL,FFTELQ        TEST FOR ATTRIBUTE ELEMENT                   
         BNE   VALACT14                                                         
         CLI   FFTTYPE,FFTTATRB                                                 
         BNE   VALACT14                                                         
         MVI   FFTEL,DELELQ                                                     
         OI    BYTE1,QANYEL        SET ELEMENT FOUND                            
         B     VALACT12                                                         
                                                                                
VALACT14 DS    0H                                                               
*&&UK                                                                           
         USING ASIELD,R2                                                        
         CLI   ASIEL,ASIELQ        TEST SYSTEM INTERFACE INFO ELEMENT           
         BNE   VALACT18                                                         
         OI    BYTE1,QANYEL        SET ELEMENT FOUND                            
         MVI   ASIEL,DELELQ                                                     
                                                                                
         GOTOR GETRST              GET ACCOUNT STATUS ELEMENT                   
         BNE   VALACTE                                                          
                                                                                
         USING RSTELD,R1                                                        
         CLI   TRNCTRY,CTRYGBR     TEST UK                                      
         BNE   VALACT16                                                         
         NI    RSTSTAT2,FF-(RSTSPRNT+RSTSBRDC)                                  
         TM    ASINDS1,ASIUKBDC                                                 
         BZ    *+8                                                              
         OI    RSTSTAT2,RSTSBRDC                                                
         TM    ASINDS1,ASIUKPRT                                                 
         BZ    *+8                                                              
         OI    RSTSTAT2,RSTSPRNT                                                
         B     VALACT12                                                         
                                                                                
VALACT16 CLI   TRNCTRY,CTRYGER     TEST GERMANY                                 
         BNE   VALACT12                                                         
         NI    RSTSTAT2,FF-(RSTSPLOK)                                           
         TM    ASINDS1,ASIGERDD                                                 
         BZ    *+8                                                              
         OI    RSTSTAT2,RSTSPLOK                                                
         NI    RSTSTAT4,FF-(RSTSMSDR)                                           
         TM    ASINDS1,ASIMERGE                                                 
         BZ    *+8                                                              
         OI    RSTSTAT4,RSTSMSDR                                                
         CLI   ASILN,ASILN2Q                                                    
         BL    VALACT12                                                         
         MVC   RSTFILT4,ASIFILT4                                                
         B     VALACT12                                                         
         DROP  R1                                                               
*&&                                                                             
         USING ADRELD,R2                                                        
VALACT18 CLI   ADREL,ADRELQ        TEST ADDRESS ELEMENT                         
         BNE   VALACT20                                                         
*&&UK*&& TM    TRNACCI,TRNASUND    KEEP ADDRESS FOR SUNDRY CREDITORS            
*&&UK*&& BNZ   VALACT12                                                         
         OI    BYTE1,QADREL        SET ADDRESS ELEMENT FOUND                    
         MVI   BYTE2,DELELQ        SET TO DELETE FROM TRANSACTION               
         B     VALACT38                                                         
                                                                                
         USING BACELD,R2                                                        
VALACT20 CLI   BACEL,BACELQ        TEST BANK ACCOUNT ELEMENT                    
         BNE   *+16                                                             
         OI    BYTE1,QANYEL        SET ELEMENT FOUND                            
         MVI   BYTE2,DELELQ        SET TO DELETE FROM TRANSACTION               
         B     VALACT38                                                         
                                                                                
         USING GPNELD,R2                                                        
         CLI   GPNEL,GPNELQ        TEST GENERAL PURPOSE NAME ELEMENT            
         BNE   *+16                                                             
         OI    BYTE1,QANYEL        SET ELEMENT FOUND                            
         MVI   BYTE2,DELELQ        SET TO DELETE FROM TRANSACTION               
         B     VALACT38                                                         
                                                                                
         USING RATELD,R2                                                        
         CLI   RATEL,RATEDSCQ      TEST DISCOUNT RATE ELEMENT                   
         BNE   VALACT22                                                         
         OI    BYTE1,QANYEL        SET ELEMENT FOUND                            
         MVC   BYTE2,RATEL         SET TO LEAVE ON TRANSACTION                  
         CLC   MFOLULUK,TRNKUNT    DON'T PUT DISCOUNT ONTO ACCOUNT              
         BNE   VALACT12            RECORDS IF NOT SF POSTING                    
         B     VALACT38                                                         
                                                                                
         USING RSTELD,R2                                                        
VALACT22 CLI   RSTEL,RSTELQ        TEST STATUS ELEMENT                          
         BNE   VALACT24                                                         
         OI    BYTE1,QRSTEL        SET STATUS ELEMENT FOUND                     
         MVI   RSTEL,DELELQ        SET TO DELETE FROM TRANSACTION               
         GOTOR GETRST              GET ACCOUNT STATUS ELEMENT                   
         BNE   VALACTE                                                          
         MVC   RSTSTAT1-RSTELD(L'RSTSTAT1,R1),RSTSTAT1                          
*&&US*&& B     VALACT12                                                         
*&&UK                                                                           
         NI    RSTSTAT2-RSTELD(R1),FF-(RSTSPLOK)                                
         TM    RSTSTAT2,RSTSPLOK                                                
         BZ    *+8                                                              
         OI    RSTSTAT2-RSTELD(R1),RSTSPLOK                                     
         LA    RE,RSTSPRNT                                                      
         CLI   RSTFILT3,X'62'      TEST PRINT LIKE MEDIA                        
         BE    *+16                                                             
         LA    RE,RSTSBRDC                                                      
         CLI   RSTFILT3,X'52'      TEST BROADCAST LIKE MEDIA                    
         BNE   VALACT12                                                         
         NI    RSTSTAT2-RSTELD(R1),FF-(RSTSPRNT+RSTSBRDC)                       
         EX    RE,*+8              SET PRINT/BROADCAST TYPE IN STATUS2          
         B     VALACT12                                                         
         OI    RSTSTAT2-RSTELD(R1),0                                            
*&&                                                                             
         USING MSAELD,R2                                                        
VALACT24 CLI   MSAEL,MSAELQ        TEST MONTHLY SALARY ELEMENT                  
         BNE   VALACT32                                                         
         OI    BYTE1,QMSAEL        SET MONTHLY SALARY ELEMENT FOUND             
         LA    R1,ACTRECD          ADD SALARY ELEMENT TO ACCOUNT                
         AH    R1,DATADISP                                                      
         SR    RE,RE                                                            
         MVC   WORK(L'MSABEG),EFFS                                              
VALACT26 IC    R0,1(R1)            BUMP TO NEXT ACCOUNT RECORD ELEMENT          
         AR    R1,R0                                                            
         CLI   0(R1),0             TEST EOR                                     
         BE    VALACT28                                                         
         CLI   0(R1),MSAELQ        TEST MONTHLY SALARY ELEMENT                  
         BNE   VALACT26                                                         
                                                                                
         CLC   MSABEG-MSAELD(L'MSABEG,R1),WORK                                  
         BNL   *+12                                                             
         MVC   WORK(L'MSABEG),MSABEG-MSAELD(R1)                                 
         LR    RE,R1               POINT TO OLDEST ELEMENT                      
                                                                                
         CLC   MSABEG-MSAELD(L'MSABEG,R1),MSABEG                                
         BNE   VALACT26                                                         
*&&US*&& CLC   MSAEND-MSAELD(L'MSAEND,R1),MSAEND                                
*&&US*&& BNE   VALACT26                                                         
         CLC   MSATYPE-MSAELD(L'MSATYPE,R1),MSATYPE                             
         BNE   VALACT26                                                         
         MVC   0(MSALNQ,R1),MSAELD COPY DATA IF DUPLICATED ELEMENT              
         B     VALACT12                                                         
                                                                                
VALACT28 ST    RE,FULL             SAVE A(OLDEST ELEMENT)                       
         SR    R0,R0                                                            
         SR    RE,RE                                                            
         ICM   RE,3,ACTRLEN        TEST THIS ELEMENT FITS                       
         AHI   RE,MSALNQ                                                        
         CHI   RE,IOAREAL-10                                                    
         BH    VALACT30                                                         
         GOTOR VHELLO,ELIST,(C'P',FILE),ACTRECD,MSAELD,0                        
*&&UK*&& CLI   ELERR,0             TEST RECORD TOO BIG                          
*&&UK*&& BE    VALACT12                                                         
*&&US*&& CLC   ACTRLEN,=AL2(999)   TEST RECORD TOO BIG                          
*&&US*&& BL    VALACT12                                                         
         LHI   R0,1                                                             
VALACT30 ICM   RE,15,FULL          POINT TO OLDEST ELEMENT                      
         BZ    VALACTE                                                          
         MVI   MSAEL-MSAELD(RE),DELELQ                                          
         GOTOR VHELLO,ELIST,(C'D',FILE),('DELELQ',ACTRECD),0                    
         CLI   ELERR,0                                                          
         BNE   VALACTE                                                          
         LTR   R0,R0                                                            
         BNZ   VALACT12                                                         
         SR    RE,RE                                                            
         B     VALACT28            TRY ADDING ELEMENT AGAIN                     
                                                                                
         USING NAMELD,R2                                                        
VALACT32 CLI   NAMEL,NAMELQ        TEST NAME ELEMENT                            
         BNE   VALACT12                                                         
*&&UK*&& TM    TRNACCI,TRNASUND    KEEP NAME FOR SUNDRY CREDITORS               
*&&UK*&& BNZ   VALACT12                                                         
         MVI   BYTE2,DELELQ        SET TO DELETE FROM TRANSACTION               
         TM    TRNACCI,TRNIOAP     TEST ADD PENDING FOR ACCOUNT                 
         BNZ   VALACT36                                                         
                                                                                
         LA    R1,ACTRECD                                                       
         AH    R1,DATADISP                                                      
VALACT34 CLI   0(R1),0             TEST EOR                                     
         BE    VALACT36                                                         
         CLI   0(R1),NAMELQ        TEST NAME ELEMENT                            
         BE    *+14                                                             
         IC    R0,1(R1)                                                         
         AR    R1,R0                                                            
         B     VALACT34                                                         
         SR    RE,RE                                                            
         IC    RE,NAMLN                                                         
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         CLC   NAMEL(0),0(R1)      TEST ACCOUNT NAME CHANGED                    
         BNE   *+16                                                             
         OI    BYTE1,QANYEL        SET ELEMENT DELETION REQUIRED                
         MVI   NAMEL,DELELQ        SET TO DELETE NAME ELEMENT                   
         B     VALACT12                                                         
                                                                                
         OI    TRNACCI,TRNANMCH    SET NAME HAS CHANGED                         
         USING ANCRECD,KEY         BUILD KEY OF NAME CHANGE POINTER             
         XC    ANCKEY,ANCKEY                                                    
         MVI   ANCKTYP,ANCKTYPQ                                                 
         MVC   ANCKCULA,ACTKCULA                                                
         XC    ANCKSTA,ANCKSTA                                                  
         MVI   ANCKSTAT,ANCSDELT                                                
         MVC   ANCKDA,TRNACCDA                                                  
                                                                                
         GOTOR READDIR,ANCRECD                                                  
         TM    IOERR,IOERNF        TEST RECORD NOT FOUND                        
         BZ    VALACT36                                                         
         MVC   ANCRECD(L'KEYSAVE),KEYSAVE                                       
         GOTOR ADDDIR,ANCRECD                                                   
         BNE   VALACT36                                                         
         ICM   R1,15,TRNANCA       INCREMENT NUMBER OF POINTERS ADDED           
         AHI   R1,1                                                             
         STCM  R1,15,TRNANCA                                                    
                                                                                
VALACT36 OI    BYTE1,QNAMEL        SET NAME ELEMENT FOUND                       
                                                                                
VALACT38 GOTOR VHELLO,ELIST,(C'D',FILE),(0(R2),ACTRECD),0                       
         GOTOR (RF),(R1),(C'P',FILE),ACTRECD,(R2),0                             
         MVC   0(1,R2),BYTE2       SET ELEMENT CODE IN TRANSACTION              
         CLI   ELERR,0                                                          
         BNE   VALACTE                                                          
         B     VALACT12                                                         
                                                                                
VALACT40 TM    INDS2,INDPRODN      TEST PRODUCTION DEBIT POSTING                
         BZ    VALACT44                                                         
         TM    TRNINDS,TRNIDRFT+TRNILIVE                                        
         BNZ   VALACT44                                                         
         TM    TRNSTAT,TRNSDR                                                   
         BNZ   VALACT44                                                         
         TM    TRNMODE,TRNMONLN                                                 
         BNZ   VALACT44                                                         
                                                                                
         LA    R1,ACTRECD          LOCATE SCIEL ON JOB RECORD                   
         AH    R1,DATADISP                                                      
         SR    R0,R0                                                            
         USING SCIELD,R1                                                        
VALACT42 CLI   SCIEL,0             TEST END OF RECORD                           
         BE    VALACT44                                                         
         CLI   SCIEL,SCIELQ        TEST SCIEL                                   
         BNE   *+12                                                             
         CLI   SCITYPE,SCITT99S                                                 
         BE    *+14                                                             
         IC    R0,SCILN                                                         
         AR    R1,R0                                                            
         B     VALACT42                                                         
         SP    SCIAMNT,TRNAMNT     SUBTRACT POSTING FROM BILLED AMOUNT          
         DROP  R1,R2                                                            
                                                                                
VALACT44 CLI   BYTE1,0             TEST ANY AUTO ELEMENTS FOUND                 
         BE    VALACT46                                                         
         GOTOR VHELLO,ELIST,(C'D',FILETRN),('DELELQ',TRNRECD),0                 
                                                                                
VALACT46 TM    BYTE1,QMSAEL        TEST MONTHLY SALARY ELEMENTS FOUND           
         BZ    VALACT48                                                         
         GOTOR SRTMSA              YES - SORT THEM INTO SEQUENCE                
         BNE   VALACTE             EXIT ON SORT ERROR                           
                                                                                
VALACT48 TM    TRNACCI,TRNIOAP     TEST ADD PENDING                             
         BZ    VALACT52                                                         
         TM    BYTE1,QNAMEL        TEST NAME CHANGED                            
         BNZ   VALACT52                                                         
         TM    TRNINDS2,TRNIDNAM   TEST ADD DUMMY NAME TO ACCOUNT               
         BZ    VALACT50                                                         
         LA    RF,ELEMENT                                                       
         USING NAMELD,RF                                                        
         MVI   NAMEL,NAMELQ                                                     
         MVI   NAMLN,NAMLN1Q+1                                                  
         MVI   NAMEREC,C'?'                                                     
         GOTOR VHELLO,ELIST,(C'P',FILE),ACTRECD,NAMELD,0                        
         CLI   ELERR,0                                                          
         BE    VALACT54                                                         
         DROP  RF                                                               
                                                                                
VALACT50 NI    TRNACCI,FF-(TRNIOAP+TRNIOWP)                                     
         B     VALACTE                                                          
                                                                                
VALACT52 GOTOR VHELLO,ELIST,(C'G',FILE),('ABLELQ',ACTRECD),0                    
         CLI   ELERR,0                                                          
         BNE   VALACT50                                                         
                                                                                
VALACT54 GOTOR GETRST              GET ACCOUNT STATUS ELEMENT                   
         BNE   VALACT56                                                         
         USING RSTELD,R1                                                        
         MVC   CLOSTAT1,RSTSTAT1                                                
         MVC   CLOSTAT3,RSTSTAT3                                                
                                                                                
VALACT56 GOTOR VHELLO,ELIST,(C'G',FILE),('ASTELQ',ACTRECD),0                    
         CLI   ELERR,0                                                          
         BE    VALACT58                                                         
         GOTOR BLDAST,ACTRECD      ADD STATUS ELEMENT TO RECORD                 
         BNE   VALACTE                                                          
                                                                                
VALACT58 GOTOR VHELLO,ELIST,(C'G',FILE),('APOELQ',ACTRECD),0                    
         CLI   ELERR,0                                                          
         BNE   VALACT60                                                         
         L     R1,ELADDR                                                        
         USING APOELD,R1                                                        
         CLI   APOLN,APOLN2Q       TEST NEW LENGTH ELEMENT                      
         BNE   VALACTX                                                          
         MVC   CLOSMOS,APOCMOS     SAVE LATEST CLOSED MOS                       
                                                                                
VALACT60 MVI   ACTRSTAT,0          YES - BUILD STATUS AREA                      
         XC    ACTRSAF1(L'ACTRSTA-1),ACTRSAF1                                   
                                                                                
         LA    R1,ACTRECD                                                       
         AH    R1,DATADISP                                                      
VALACT62 CLI   0(R1),0             TEST END OF RECORD                           
         BE    VALACTX                                                          
                                                                                
         USING ABLELD,R1                                                        
         CLI   ABLEL,ABLELQ        TEST BALANCE ELEMENT                         
         BNE   VALACT64                                                         
         OI    ACTRSTAT,ACTSABLP                                                
         B     VALACT68                                                         
                                                                                
         USING PPRELD,R1                                                        
VALACT64 CLI   PPREL,PPRELQ        TEST PRODUCTION PROFILE ELEMENT              
         BNE   VALACT66                                                         
         MVC   ACTRSOFF,PPRGAOFF   SET OFFICE                                   
         B     VALACT68                                                         
                                                                                
         USING RSTELD,R1                                                        
VALACT66 CLI   RSTEL,RSTELQ        TEST STATUS ELEMENT                          
         BNE   VALACT68                                                         
         TM    RSTSTAT1,RSTSACIC                                                
         BZ    *+8                                                              
         OI    ACTRSTAT,ACTSCLOS   SET ACCOUNT CLOSED                           
         TM    RSTSTAT1,RSTSACIL                                                
         BZ    *+8                                                              
         OI    ACTRSTAT,ACTSLOCK   SET ACCOUNT LOCKED                           
         TM    RSTSTAT3,RSTSLABS                                                
         BZ    *+8                                                              
         OI    ACTRSTAT,ACTSLABS                                                
         TM    RSTSTAT3,RSTSLAPL                                                
         BZ    *+8                                                              
         OI    ACTRSTAT,ACTSLAPL                                                
         MVC   ACTRSAF1,RSTFILT1                                                
         MVC   ACTRSAF2,RSTFILT2                                                
         MVC   ACTRSAF3,RSTFILT3                                                
         MVC   ACTRSAF4,RSTFILT4                                                
         MVC   ACTRSAF5,RSTFILT5                                                
                                                                                
VALACT68 IC    R0,1(R1)            BUMP TO NEXT ELEMENT                         
         AR    R1,R0                                                            
         B     VALACT62                                                         
                                                                                
VALACTX  J     EXITY                                                            
                                                                                
VALACTE  MVI   TRNERRS,TRNEACCI    SET ACCOUNT INVALID & EXIT                   
         OI    TRNACCI,TRNAINVA                                                 
         J     EXITN                                                            
                                                                                
VALACTC  MVI   TRNERRS,TRNEACOL    SET ACCOUNT CLOSED/LOCKED & EXIT             
         OI    TRNACCI,TRNAINVA                                                 
         J     EXITN                                                            
         DROP  R1,R5,RB                                                         
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* VALIDATE CONTRA-ACCOUNT                                             *         
***********************************************************************         
                                                                                
VALCAC   NTR1  BASE=*,LABEL=*                                                   
         L     R5,ACAC                                                          
         USING CHDRECD,R5          R5=A(CONTRA-ACCOUNT RECORD)                  
                                                                                
         TM    TRNCACI,TRNIOXC     TEST I/O AREA INITIALIZED                    
         BNZ   VALCAC02                                                         
         LA    R0,CHDRECD                                                       
         LHI   R1,IOL                                                           
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
         OI    TRNCACI,TRNIOXC     SET I/O AREA INITIALIZED                     
                                                                                
VALCAC02 TM    TRNBUKI,TRNIOXC     TEST I/O AREA INITIALIZED                    
         BNZ   VALCAC04                                                         
         L     R0,ABUK                                                          
         LHI   R1,IOL                                                           
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
         OI    TRNBUKI,TRNIOXC     SET I/O AREA INITIALIZED                     
                                                                                
VALCAC04 OC    TRNCAC,TRNCAC       TEST A(CONTRA-ACCOUNT PASSED)                
         BZ    VALCAC08                                                         
         OC    TRNBUK,TRNBUK       AND A(BUCKET PASSED)                         
         BZ    VALCAC08                                                         
         TM    TRNINDS,TRNILAST    TEST LAST TIME CALL                          
         BNZ   VALCAC06                                                         
         CLC   TRNKCULA(TRNKDATE-TRNRECD),TRNKEYL                               
         BE    VALCACX                                                          
         OC    TRNKEYL(TRNKDATE-TRNKCULA),TRNKEYL                               
         BZ    VALCAC08                                                         
                                                                                
VALCAC06 GOTOR PUTCHD              UPDATE CONTRA POINTER                        
         GOTOR PUTCH2              UPDATE CONTRA POINTER 2                      
         GOTOR PUTBUK              UPDATE CONTRA ACCOUNT BUCKETS                
                                                                                
VALCAC08 MVC   TRNCACSV,TRNCACNM   SAVE CONTRA-ACCOUNT NAME                     
                                                                                
VALCACX  J     EXITY                                                            
         DROP  RB,R5                                                            
         EJECT                                                                  
***********************************************************************         
* VALIDATE OFFICE/ACCOUNT BEING POSTED TO                             *         
***********************************************************************         
                                                                                
VALOFA   NTR1  BASE=*,LABEL=*                                                   
         L     R5,AOFA                                                          
         USING OFARECD,R5          R5=A(OFFICE/ACCOUNT RECORD)                  
                                                                                
         TM    TRNOFAI,TRNIOXC     TEST I/O AREA INITIALIZED                    
         BNZ   VALOFA02                                                         
         LA    R0,OFARECD                                                       
         LHI   R1,IOL                                                           
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
         OI    TRNOFAI,TRNIOXC     SET I/O AREA INITIALIZED                     
                                                                                
VALOFA02 TM    TRNINDS,TRNILAST    TEST FINAL CALL                              
         BNZ   VALOFA04                                                         
         TM    TRNINDS2,TRNIUBKO   TEST UPDATING BUCKETS ONLY                   
         BNZ   VALOFAX                                                          
         TM    TRNCPYS4,CPYSOFF2   TEST COMPANY ON NEW OFFICES                  
         BZ    VALOFAX                                                          
         OC    TRNOFA,TRNOFA       TEST OFFICE/ACCOUNT RECORD PASSED            
         BZ    VALOFA06                                                         
         CLC   TRNKCULA(OFAKEND),OFAKEY                                         
         BNE   VALOFA04                                                         
         TM    TRNOFAI,TRNIOWP+TRNIOAP                                          
         BNZ   VALOFAX                                                          
         B     VALOFA06                                                         
                                                                                
VALOFA04 GOTOR PUTOFA              ADD OR WRITE BACK OFFICE/ACCOUNT             
         TM    TRNINDS,TRNILAST    TEST FINAL CALL                              
         BNZ   VALOFAX                                                          
                                                                                
VALOFA06 NI    TRNOFAI,FF-(TRNIOWP+TRNIOAP)                                     
         CP    TRNAMNT,PZERO       TEST ZERO AMOUNT TRANSACTION                 
         BE    VALOFAX                                                          
         TM    INDS1,IND2FDUM+INDRET47+IND1RDUM                                 
         BNZ   VALOFAX                                                          
         TM    INDS2,INDKEYWC      TEST LEDGER HAS W/C IN KEY                   
         BNZ   VALOFAX                                                          
         CLC   TRNOFFC,SPACES      TEST TRANSACTION OFFICE PRESENT              
         BNH   VALOFAX                                                          
         TM    TRNOFAI,TRNIORD     TEST OFFICE/ACCOUNT RECORD READ              
         BNZ   VALOFA08                                                         
                                                                                
         MVC   OFAKEY,SPACES       READ OFFICE/ACCOUNT RECORD                   
         MVC   OFAKEY(L'OFAKCULA),TRNKEY                                        
         MVC   OFAKOFF,TRNOFFC                                                  
         GOTOR READ,OFAKEY                                                      
         BNE   VALOFA10                                                         
         GOTOR VHELLO,ELIST,(C'G',FILE),('ABLELQ',OFAKEY),0                     
         CLI   ELERR,0                                                          
         BNE   VALOFAE                                                          
         OI    TRNOFAI,TRNIORD     SET OFFICE/ACCOUNT RECORD READ               
                                                                                
VALOFA08 OI    TRNOFAI,TRNIOWP     SET WRITE PENDING FOR OFFICE/ACCOUNT         
         B     VALOFAX                                                          
                                                                                
VALOFA10 OI    TRNOFAI,TRNIOAP     SET ADD PENDING FOR OFFICE/ACCOUNT           
                                                                                
         GOTOR BLDREC,PARM,KEYSAVE,OFARECD,0                                    
                                                                                
         GOTOR BLDABL,OFARECD      ADD BALANCE ELEMENT TO RECORD                
         BNE   VALOFAE                                                          
                                                                                
VALOFAX  J     EXITY                                                            
                                                                                
VALOFAE  MVI   TRNERRS,TRNEOFAI    SET OFFICE/ACCOUNT INVALID & EXIT            
         J     EXITN                                                            
         DROP  R5,RB                                                            
         EJECT                                                                  
***********************************************************************         
* VALIDATE CONTRA-POINTER FOR NEW FILE & NEW OFFICES                  *         
***********************************************************************         
                                                                                
VALCHD   NTR1  BASE=*,LABEL=*                                                   
         TM    TRNINDS,TRNILAST+TRNILIVE  TEST LAST TIME OR DRAFT               
         BNZ   VALCHDX                                                          
         TM    INDS1,IND2FDUM+INDRET47+IND1RDUM                                 
         BNZ   VALCHDX                                                          
         CLC   TRNKEY(CHDKSPCS-CHDKEY),TRNKEYL                                  
         BE    VALCHDX                                                          
                                                                                
         USING CHDRECD,R5                                                       
         LA    R5,IO                                                            
         MVC   CHDKEY,SPACES       BUILD KEY OF CONTRA-HEADER RECORD            
         MVC   CHDKEY(CHDKSPCS-CHDKEY),TRNKEY                                   
         XC    CHDKNULL,CHDKNULL                                                
         TM    INDS2,INDKEYWC      TEST LEDGER HAS W/C IN KEY                   
         BNZ   VALCHD06                                                         
         TM    TRNINDS2,TRNIUBKO   TEST UPDATING BUCKETS ONLY                   
         BZ    *+12                                                             
         TM    TRNINDS2,TRNIUOFC   TEST UPDATING OFFICE CONTRA HEADER           
         BZ    VALCHD04                                                         
         TM    TRNCPYS4,CPYSOFF2   IGNORE IF NOT 2 CHARACTER OFFICES            
         BZ    VALCHD04                                                         
                                                                                
         NI    TRNCACI,FF-(TRNCHDAP+TRNCHDWP)                                   
         GOTOR READDIR,CHDRECD                                                  
         TM    IOERR,IOEBAD        TEST NOT FOUND/EOF/ERROR                     
         BZ    *+12                                                             
         OI    TRNCACI,TRNCHDAP    SET CONTRA-HEADER ADD PENDING                
         B     VALCHD02                                                         
         TM    IOERR,IOEDEL        TEST RECORD IS DELETED                       
         BZ    *+8                                                              
         OI    TRNCACI,TRNCHDWP    YES - SET WRITE PENDING                      
                                                                                
         GOTOR GET,CHDRECD         READ CONTRA-HEADER AND TEST CHANGED          
         GOTOR TSTCAC,(R1)                                                      
         BE    VALCHD02                                                         
         OI    TRNCACI,TRNCHDWP    SET CONTRA-HEADER WRITE PENDING              
                                                                                
VALCHD02 MVC   CHDKEY,KEYSAVE                                                   
                                                                                
VALCHD04 MVC   CHDKOFF,SPACES                                                   
                                                                                
VALCHD06 NI    TRNCACI,FF-(TRNCH2AP+TRNCH2WP)                                   
         GOTOR READDIR,CHDRECD                                                  
         TM    IOERR,IOEBAD                                                     
         BZ    *+12                                                             
         OI    TRNCACI,TRNCH2AP    SET CONTRA-HEADER ADD PENDING                
         B     VALCHDX                                                          
         TM    IOERR,IOEDEL        TEST RECORD IS DELETED                       
         BZ    *+8                                                              
         OI    TRNCACI,TRNCH2WP    YES - SET WRITE PENDING                      
                                                                                
         GOTOR GET,CHDRECD         READ CONTRA-HEADER AND TEST CHANGED          
         GOTOR TSTCAC,(R1)                                                      
         BE    VALCHDX                                                          
         OI    TRNCACI,TRNCH2WP    SET CONTRA-HEADER WRITE PENDING              
                                                                                
VALCHDX  J     EXITY                                                            
         DROP  RB                                                               
         EJECT                                                                  
***********************************************************************         
* ADD TRANSACTION RECORD TO FILE                                      *         
***********************************************************************         
                                                                                
UPDTRN   NTR1  BASE=*,LABEL=*                                                   
         TM    INDS1,IND2FDUM+INDRET47+IND1RDUM                                 
         BNZ   UPDTRNX                                                          
         MVC   LASTTKEY,TRNKEYL                                                 
         MVC   TRNKEYL,TRNKEY      SAVE TRANSACTION KEY IN BLOCK                
         TM    TRNCPYS4,CPYSOFF2                                                
         BO    *+10                                                             
         MVC   TRNKOFF1,TRNANAL                                                 
                                                                                
         TM    TRNINDS2,TRNIUBKO   TEST UPDATING BUCKETS ONLY                   
         BNZ   UPDTRNX                                                          
         TM    TRNINDS,TRNILAST    TEST FINAL CALL                              
         BNZ   UPDTRNX                                                          
                                                                                
         LA    R4,IO               R4=A(I/O AREA FOR FILE READING)              
         TM    TRNINDS1,TRNIDNAT   TEST DON'T ADD TRANSACTION RECORD            
         BNZ   UPDTRN60                                                         
                                                                                
*&&US*&& GOTOR TIMTRN              HANDLE TIME TRANSFERS                        
*&&US*&& JE    EXITY               EXIT IF TRANSACTION HANDLED THERE            
                                                                                
*&&US*&& GOTOR JOBXFR              HANDLE PARTIAL JOB-TO-JOB TRANSFERS          
                                                                                
*&&UK*&& GOTOR ADDBND              ADD BNDEL IF NECESSARY                       
*&&UK*&& BNE   UPDTRNE                                                          
                                                                                
         GOTOR ADDPTA              ADD PTAEL IF NECESSARY                       
         BNE   UPDTRNE                                                          
                                                                                
         GOTOR SETPAY              SET PAYABLES INDICATORS                      
                                                                                
         GOTOR ADDMPY              ADD MPYEL IF NECESSARY                       
                                                                                
         GOTOR ADDGLD              ADD GLDEL IF NECESSARY                       
         BNE   UPDTRNE2                                                         
                                                                                
         TM    INDS2,INDPRODN      TEST PRODUCTION LEDGER                       
         BZ    UPDTRN06                                                         
*                                                                               
         CLI   TRNTYPE,10          TEST TIME POSTING DSRD-24207                 
         BE    *+8                                                              
         CLI   TRNTYPE,49          TEST TIME POSTING                            
         BE    *+8                                                              
         CLI   TRNTYPE,34          TEST JOB-TO-JOB TRANSFER                     
         BE    *+8                                                              
         CLI   TRNTYPE,21          TEST NON-BILL INVOICE POSTING                
         BNE   UPDTRN06                                                         
         CP    TRNAMNT,PZERO       TEST R OR N TIME, OR MEMO EXPENSE            
         BNZ   UPDTRN06            NO                                           
                                                                                
         SR    R0,R0                                                            
         USING SCIELD,RF                                                        
         LA    RF,TRNEL                                                         
         BASR  RE,0                                                             
         IC    R0,SCILN                                                         
         AR    RF,R0                                                            
         CLI   SCIEL,0                                                          
         BE    UPDTRN06                                                         
         CLI   SCIEL,SCIELQ                                                     
         BNE   UPDTRN04                                                         
*                                                                               
         CLI   TRNTYPE,10          TEST TIME POSTING DSRD-24207                 
         BE    *+12                                                             
         CLI   TRNTYPE,21                                                       
         BNE   UPDTRN02                                                         
*        CLI   SCITYPE,SCITMEXP                                                 
         CLI   SCITYPE,SCITSJXP    DSRD-24207                                   
         BNE   *+10                                                             
         AP    SSCITMAM,SCIAMNT    ACCUMULATE MEMO EXPENSE VALUES               
         BR    RE                                                               
*                                                                               
UPDTRN02 CLI   SCITYPE,SCITMSRT                                                 
         BNE   *+12                                                             
         AP    SSCITSAM,SCIAMNT    ACCUMULATE SALES AND COST VALUES             
         BR    RE                                                               
         CLI   SCITYPE,SCITMCRT                                                 
         BNE   *+10                                                             
         AP    SSCITCAM,SCIAMNT                                                 
         BR    RE                                                               
         DROP  RF                                                               
                                                                                
         USING PRTELD,RF                                                        
UPDTRN04 CLI   PRTEL,PRTELQ                                                     
         BNER  RE                                                               
         CLI   TRNTYPE,49                                                       
         BNER  RE                                                               
         TM    PRTSTAT,PRTSBILQ                                                 
         BNZR  RE                                                               
         ZAP   DUB,PRTRATE                                                      
         MP    DUB,PRTHOUR                                                      
         DP    DUB,P100                                                         
         ZAP   DUB2,DUB(6)                                                      
         ZAP   DUB,DUB2                                                         
         AP    SSCITSAM,DUB                                                     
         BR    RE                                                               
                                                                                
UPDTRN06 DS    0H                                                               
         GOTOR GETTRS,TRNRECD      GET TRANSACTION STATUS ELEMENT               
                                                                                
         USING TRSELD,R2                                                        
         LR    R2,R1               AND UPDATE WITH VALUES                       
         TM    TRNINDS,TRNIDRFT    TEST DRAFT TRANSACTION                       
         BNZ   UPDTRN14                                                         
         TM    INDS1,INDPAYAC      TEST PAYABLES LEDGER                         
         BZ    UPDTRN10                                                         
         TM    TRNSTAT,TRNSDR      TEST DEBIT (CHEQUE) POSTING                  
*&&US*&& BZ    UPDTRN10                                                         
*&&UK                                                                           
         BNZ   UPDTRN08                                                         
         SR    R0,R0               ONLY ALLOW CREDITS TO BE MARKED              
         LA    RF,TRNEL            IF THEY ARE EXCHANGE DIFFERENCES             
         USING TRXELD,RF                                                        
         BASR  RE,0                                                             
         IC    R0,TRXLN            THESE COME FROM WFM FOREIGN PAYMENTS         
         AR    RF,R0                                                            
         CLI   TRXEL,0                                                          
         BE    UPDTRN10                                                         
         CLI   TRXEL,TRXELQ        EXTRA TRANS ELEMENT TELLS YOU IF             
         BNER  RE                  RECORD IS EXCHANGE DIFFERENCE PSTG           
                                                                                
         TM    TRXSTA1,TRXSXDIF                                                 
         BNO   UPDTRN10            THIS ONE IS NOT                              
         TM    TRNSTAT,TRNSHOLD+TRNSBREC                                        
         BNO   UPDTRN10                                                         
         OC    TRSUDAT,TRSUDAT     DO WE ALREADY HAVE A USED DATE               
         BNZ   UPDTRN10            YES                                          
         MVC   TRSUDAT,TRNPDAT2    SET USED DATE AS TODAY                       
         NI    TRNSTAT,FF-(TRNSHOLD+TRNSBREC)                                   
         B     UPDTRN10                                                         
         DROP  RF                                                               
*&&                                                                             
UPDTRN08 TM    TRNSTAT,TRNSHOLD+TRNSBREC                                        
         BNO   UPDTRN10                                                         
         NI    TRNSTAT,FF-(TRNSHOLD+TRNSBREC)                                   
         MVC   TRSUDAT,TRNPDAT2    SET USED DATE AS TODAY                       
         OI    INDS1,INDPAYCR      SET TO LOCATE PAYABLE CREDIT ITEM            
                                                                                
UPDTRN10 CLI   CLOS,LDGCCSH        TEST CASH LEDGER                             
         BNE   UPDTRN12                                                         
         TM    TRNSTAT,TRNSDR      TEST DEBIT/CREDIT                            
         BZ    *+16                                                             
         TM    CLOSTAT3,RSTSPRDR   TEST CLOSING RECONCILED DEBITS               
         BNZ   UPDTRN12                                                         
         B     *+12                                                             
         TM    CLOSTAT3,RSTSPRCR   TEST CLOSING RECONCILED CREDITS              
         BNZ   UPDTRN12                                                         
         OI    TRNSTAT,TRNSBREC    SET BANK RECONCILED                          
                                                                                
UPDTRN12 DS    0H                                                               
*&&UK                                                                           
         TM    TRNCPYS4,CPYSIREG   TEST COMPANY ON INVOICE REGISTER             
         BNZ   UPDTRN14                                                         
         TM    INDS1,INDPAYAC+INDPAYCA                                          
         BZ    UPDTRN14                                                         
         OI    TRNSTAT,TRNSAUTH    SET TRANSACTION IS AUTHORISED                
*&&                                                                             
UPDTRN14 TM    TRNINDS,TRNICONV    TEST USER PASSING CONVERTED RECS             
         BZ    UPDTRN16                                                         
         TM    TRNCPYS4,CPYSOFF2                                                
         BNZ   *+10                                                             
         MVC   TRNRSANL,TRNANAL    SET OFFICE (IF OLD OFFICES IN USE)           
                                                                                
         MVC   TRNRSMOS,MOS        SET TRANSACTION MOS VALUE                    
         MVC   TRNRSTYP,TRNTYPE    SET TRANSACTION TYPE                         
         OC    TRSUDAT,TRSUDAT                                                  
         BZ    *+8                                                              
         OI    TRNRSTA2,TRNSUSED   SET USED STATUS BIT                          
         TM    TRNINDS,TRNIDRFT    TEST DRAFT TRANSACTION                       
         BZ    *+8                                                              
         OI    TRNRSTAT,TRNSDRFT                                                
                                                                                
*&&UK*&& GOTOR SETDUE              SET DUE DATE IN TRANSACTION STATUS           
                                                                                
         J     UPDTRN18                                                         
                                                                                
UPDTRN16 MVC   TRNRECD+ACCOUSED(ACCOULEN),TRSUDAT                               
         DROP  R2                                                               
                                                                                
UPDTRN18 XC    SUBREF,SUBREF       INITIALIZE TRANSACTION SUBREFERENCE          
         MVC   SUBREF+L'SUBREF-L'TRNSUB(L'TRNSUB),TRNSUB                        
         XC    SAVKREF,SAVKREF     CLEAR SAVED KEY REFERENCE                    
                                                                                
         TM    TRNINDS,TRNIDRFT    TEST DRAFT TRANSACTION                       
         BNZ   UPDTRN20                                                         
*&&UK                                                                           
         TM    TRNCPYS7,CPYSSCNV   TEST CONVERTED FOR 2ND CURRENCY              
         BZ    UPDTRN20                                                         
         ICM   RF,15,VTOBACCO      TEST TOBACCO RESOLVED                        
         BZ    UPDTRN20                                                         
         MVC   WORK(L'TRNCCCUR),TRNCCCUR                                        
         MVC   WORK+L'TRNCCCUR(L'TRNCCURS),TRNCCURS                             
         SR    R0,R0                                                            
         TM    TRNINDS,TRNICONV                                                 
         BNZ   *+8                                                              
         LHI   R0,TOBAIOLD                                                      
         GOTOR (RF),PARM,('TOBAACNV',WORK),((R0),TRNRECD),TRNCOMF,0,0           
         LA    RF,WORKD            YES - BUILD LIST OF CASH VALUES              
         AHI   RF,RECOCA-WORKD                                                  
         GOTOR VTOBACCO,PARM,('TOBAALST',WORK),((R0),TRNRECD),TRNCOMF, *        
               (RF)                                                             
         OI    INDS2,INDOCAEL      SET OCAEL INDICATOR BIT ON                   
*&&                                                                             
UPDTRN20 GOTOR PAYTRN              MATCH PAYABLE ITEMS IF NECESSARY             
                                                                                
         TM    TRNINDS,TRNILIVE    TEST DRAFT TO LIVE                           
         BNZ   UPDTRN50                                                         
                                                                                
         ZAP   SVTRNAMT,PZERO                                                   
         MVI   RESTORE,NO                                                       
         TM    INDS2,INDGENLG                                                   
         BZ    UPDTRN30            NOT GENERAL LEDGER POSTINGS                  
         TM    TRNINDS2,TRNIPSTG   CALLED VIA ACGLPOST ?                        
         BZ    UPDTRN30            NO, SO OLD WAY                               
         OC    TRNGLMOA,TRNGLMOA   COMPANY NOT ON NEW GL                        
         BZ    UPDTRN30                                                         
         CLC   MOS,TRNGLMOA        SEE IF ON NEW GL                             
         BL    UPDTRN30            NO, OLD GL                                   
***********************************************************************         
* GET TRANSACTION AND ADD OR UPDATE (NEW GL WAY)                                
*   ENFORCE EVEN ODD RULE, DEBIT IS EVEN, CREDIT IS ODD                         
***********************************************************************         
FIL      USING TRNRECD,R4          A(IO) - RECORD ON FILE                       
         MVC   SVSTAT,TRNSTAT                                                   
         NI    SVSTAT,TRNSDR                                                    
         LA    R1,BO               BRANCH ONE                                   
         TM    TRNSTAT,TRNSDR      DEBIT ?                                      
         JZ    *+8                 YES                                          
         LA    R1,BZ               BRANCH ZERO                                  
         TM    TRNKSBR,X'01'       IS IT ODD                                    
         EX    R1,*+6                                                           
         NOPR  0                   IF ODD AND CR, EVEN IF DR                    
         BC    0,UPDTRN22          BRANCH IF OKAY                               
                                                                                
         LLC   RF,TRNKSBR          MAKE SURE DR IS EVEN AND CR IS ODD           
         AHI   RF,1                                                             
         STC   RF,TRNKSBR                                                       
         STC   RF,TRNSUB                                                        
                                                                                
UPDTRN22 MVC   FIL.TRNKEY,TRNKEY                                                
         GOTOR READUL,FIL.TRNKEY   DIRECTORY READ                               
                                                                                
         TM    IOERR,IOEDEL        TEST RECORD WAS DELETED                      
         JO    UPDTRNE             THIS SHOULD NOT HAPPEN                       
         TM    IOERR,IOERNF        RECORD NOT FOUND                             
         JO    UPDTRN46            NOT FOUND SO ADD                             
         TM    IOERR,FF-(IOEDEL+IOERNF)                                         
         JO    UPDTRNE             EOF OR SOMETHING BAD                         
         GOTOR READ,FIL.TRNKEY     GET RECORD INTO A(IO)                        
         CLI   IOERR,0                                                          
         JE    *+6                                                              
         DC    H'00'               THAT'S NOT GOOD                              
                                                                                
NEW      USING TRNELD,RF           READ TRANACTION                              
         LA    RF,FIL.TRNRFST      SEE IF MATCH                                 
         CLI   0(RF),TRNELQ        X'44'                                        
         BE    *+6                                                              
         DC    H'00'               NOT GOOD AT ALL                              
                                                                                
         LR    RE,RF               RF=A(FIRST ELEMENT ON TRANSACTION)           
         USING FFTELD,RE                                                        
         SR    R0,R0               CREATE FFTKREF ELEMENT IF NECESSARY          
UPDTRN23 IC    R0,FFTLN            BUMP TO NEXT ELEMENT                         
         AR    RE,R0                                                            
         CLI   FFTEL,0             TEST E-O-R                                   
         BE    UPDTRN2A                                                         
         CLI   FFTEL,FFTELQ        TEST FFTKREF ELEMENT PRESENT                 
         BNE   UPDTRN23                                                         
         CLI   FFTTYPE,FFTTKREF    YES - DON'T OVERWRITE IT                     
         BNE   UPDTRN23                                                         
         MVC   SAVKREF,FFTDATA     SAVE OFF ORIGINAL KEY REFERENCE              
         DROP  RE                                                               
                                                                                
UPDTRN2A MVC   BYTE,NEW.TRNSTAT                                                 
         NI    BYTE,TRNSDR         MAKE SURE IT IS THE SAME                     
         CLC   BYTE,SVSTAT         BOTH MUST BE CREDIT OR DEBIT                 
         JNE   UPDTRN26            NOT MATCHED SO TRY AGAIN                     
                                                                                
         TM    TRNCPYS4,CPYSOFF2   TEST IF COMPANY USES NEW OFFICE              
         JO    UPDTRN24                                                         
         CLC   TRNOFFC,NEW.TRNOFFC OFFICE MATCHES ?                             
         JNE   UPDTRN26            NO, SO FIND A MATCH OR ADD NEW               
                                                                                
UPDTRN24 AP    NEW.TRNAMNT,TRNAMNT ADD NEW AMOUNT TO OLD AMOUNT                 
         BO    UPDTRN26            OVERFLOW, TRY AGIAN                          
         MVI   RESTORE,YES         ALL SEEMS OKAY                               
         ZAP   SVTRNAMT,TRNAMNT    SAVE AMOUNT PASSED                           
         LA    RE,TRNRECD          NEED TO MOVE OVER PASSED RECORD              
         LA    R0,FIL.TRNRECD                                                   
         LA    RF,IOL              LENGTH OF IO AREA AND CONTROL BLOCK          
         LR    R1,RF                                                            
         MVCL  RE,R0                                                            
         B     UPDTRN56            NO OVERFLOW, PUT RECORD                      
         DROP  FIL,NEW                                                          
                                                                                
UPDTRN26 LLC   R1,TRNKSBR          GROUPS OF 2 DR=EVEN, CR=ODD                  
         AHI   R1,2                                                             
         STC   R1,TRNKSBR                                                       
         STC   R1,TRNSUB                                                        
         B     UPDTRN22            TRY AGAIN                                    
         EJECT ,                                                                
***********************************************************************         
* CHECK FOR REVERSALS                                                           
***********************************************************************         
REVMATCH USING TRNRECD,R4          FOR REVERALS MATCHING                        
UPDTRN30 TM    TRNINDS,TRNILIVE    TEST DRAFT TO LIVE                           
         BO    UPDTRN50                                                         
         MVC   REVMATCH.TRNKEY,TRNKEY                                           
         GOTOR READUL,REVMATCH.TRNKEY                                           
         TM    IOERR,FF-IOEDEL     TEST RECORD WAS FOUND                        
         BNZ   UPDTRN40                                                         
                                                                                
UPDTRN36 TM    TRNINDS,TRNIDRFT    TEST DRAFT TRANSACTION                       
         BNZ   UPDTRN38                                                         
                                                                                
         TM    TRNINDS1,TRNIDNAD   TEST DO NOT ALLOW DUPLICATES                 
         BZ    *+6                                                              
         DC    H'0'                                                             
         TM    INDS1,INDMATCH      TEST FOUND A MATCH YET                       
         BNZ   UPDTRN38                                                         
         TM    TRNINDS1,TRNIDNMR   TEST DO NOT MARK REVERSALS                   
         BNZ   UPDTRN38                                                         
         TM    INDS2,INDPRODN                                                   
         BZ    *+12                                                             
         TM    TRNRSTA2,TRNSBILP                                                
         BNZ   UPDTRN38                                                         
         TM    REVMATCH.TRNKSTAT,TRNSDELT+TRNSDRFT+TRNSREVS                     
         BNZ   UPDTRN38                                                         
         GOTOR GETU,REVMATCH.TRNKEY                                             
         BNE   UPDTRN38                                                         
         GOTOR REVTRN              TEST FOR REVERSAL                            
                                                                                
UPDTRN38 LH    R1,SUBREF           INCREMENT TRANSACTION SUBREFERENCE           
         AHI   R1,1                                                             
         TM    PAYINDS,PAYTTWOS    TEST ACCOUNT IS PAYABLES                     
         BZ    *+8                                                              
         AHI   R1,1                YES, INCREMENT IN 2'S                        
         STH   R1,SUBREF                                                        
         STC   R1,TRNKSBR                                                       
         STC   R1,TRNSUB                                                        
         SRA   R1,8                TEST OVERFLOW                                
         BZ    UPDTRN30                                                         
         CHI   R1,100              ALLOW 100*256 (IN TWOS)                      
         BH    UPDTRNE                                                          
         OC    SAVKREF,SAVKREF     TEST FIRST KEY CHANGE                        
         BNZ   *+10                                                             
         MVC   SAVKREF,TRNKREF     YES - SAVE ORIGINAL KEY VALUE                
         BCTR  R1,0                AMEND REFERENCE NUMBER                       
         CVD   R1,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  TRNKREF+4(2),DUB                                                 
         MVI   TRNKREF+3,C'*'                                                   
         MVC   TRNREF,TRNKREF                                                   
         B     UPDTRN30                                                         
                                                                                
UPDTRN40 OC    SAVKREF,SAVKREF     TEST KEY REFERENCE CHANGE                    
         BZ    UPDTRN46                                                         
         LA    RF,TRNELD           RF=A(FIRST ELEMENT ON TRANSACTION)           
         USING FFTELD,RF                                                        
         SR    R0,R0               CREATE FFTKREF ELEMENT IF NECESSARY          
UPDTRN42 IC    R0,FFTLN            BUMP TO NEXT ELEMENT                         
         AR    RF,R0                                                            
         CLI   FFTEL,0             TEST E-O-R                                   
         BE    UPDTRN44                                                         
         CLI   FFTEL,FFTELQ        TEST FFTKREF ELEMENT PRESENT                 
         BNE   UPDTRN42                                                         
         CLI   FFTTYPE,FFTTKREF    YES - DON'T OVERWRITE IT                     
         BE    UPDTRN46                                                         
UPDTRN44 LA    RF,ELEMENT          BUILD & ADD FFTKREF ELEMENT                  
         MVI   FFTEL,FFTELQ                                                     
         MVI   FFTLN,FFTLN1Q+L'FFTDLEN+L'SAVKREF                                
         MVI   FFTTYPE,FFTTKREF                                                 
         MVI   FFTSEQ,0                                                         
         MVI   FFTDLEN,L'SAVKREF                                                
         MVC   FFTDATA(L'SAVKREF),SAVKREF                                       
         GOTOR VHELLO,ELIST,(C'P',FILETRN),TRNRECD,FFTELD,0                     
         CLI   ELERR,0             TEST FOR ERRORS                              
         BNE   UPDTRNE                                                          
         DROP  RF                                                               
                                                                                
UPDTRN46 DS    0H                                                               
         GOTOR GETSER              GET TRS SERIAL# IF NECESSARY                 
         BNE   UPDTRNE                                                          
                                                                                
         GOTOR ADD,TRNRECD         ADD TRANSACTION RECORD                       
         MVC   TRNDA,DA            SET TRANSACTION DISK ADDRESS                 
         MVC   TRNIS,IS            (NOTE: THESE MAY BE NOT SET)                 
         ICM   R1,15,TRNTRNA       INCREMENT TRANSACTIONS ADDED                 
         AHI   R1,1                                                             
         STCM  R1,15,TRNTRNA                                                    
                                                                                
         GOTOR ADDSER              ADD TRS PASSIVE IF NECESSARY                 
         GOTOR ADDMOS              ADD MOS PASSIVE IF NECESSARY                 
         GOTOR ADDGIN              ADD GIN PASSIVE IF NECESSARY                 
         GOTOR ADDRNS              ADD RNS PASSIVE IF NECESSARY                 
         GOTOR ADDINV              ADD INV PASSIVE IF NECESSARY                 
*&&UK*&& GOTOR ADDBDP              ADD BDP PASSIVE IF NECESSARY                 
*&&US*&& GOTOR ADDAAV              ADD VEND AUTO APPROVE PASSIVE                
*&&US*&& GOTOR ADDAAR              ADD RECV AUTO APPROVE PASSIVE                
         B     UPDTRN60                                                         
                                                                                
UPDTRN50 TM    INDS2,INDPRODN      MAKE A DRAFT TRANSACTION LIVE                
         BZ    *+12                                                             
         TM    TRNRSTA2,TRNSBILP                                                
         BNZ   UPDTRN56                                                         
         MVC   REVMATCH.TRNKEY,TRNKEY                                           
         CLI   TRNKSBR,0                                                        
         BNE   *+12                                                             
         MVI   REVMATCH.TRNKSBR,1  SET SEQUENCE NUMBER TO ONE                   
         B     *+8                                                              
         MVI   REVMATCH.TRNKSBR,0  SET SEQUENCE NUMBER ZERO                     
         GOTOR RDHDIRUP,REVMATCH.TRNKEY                                         
         B     UPDTRN54                                                         
                                                                                
UPDTRN52 GOTOR RDSQU,REVMATCH.TRNKEY                                            
UPDTRN54 BNE   UPDTRN56                                                         
         CLC   TRNKEY(TRNKSBR-TRNRECD),REVMATCH.TRNKEY                          
         BNE   UPDTRN56                                                         
         CLC   TRNKEY,REVMATCH.TRNKEY                                           
         BE    UPDTRN52                                                         
         GOTOR GETU,REVMATCH.TRNKEY                                             
         BNE   UPDTRN52                                                         
         GOTOR REVTRN                                                           
         BZ    UPDTRN52                                                         
                                                                                
UPDTRN56 GOTOR PUT,TRNRECD         PUT BACK LIVE TRANSACTION                    
*&&US*&& MVC   TRNDA,DA                                                         
*&&US*&& BRAS  RE,UPDMOS           UPDATE GL IN MOS PASSIVE                     
                                                                                
         CLI   RESTORE,YES                                                      
         BNE   UPDTRN60                                                         
         ZAP   TRNAMNT,SVTRNAMT    RESTORE ORIGINAL AMOUNT                      
         ZAP   SVTRNAMT,PZERO                                                   
                                                                                
UPDTRN60 GOTOR CLOTST,TRNRECD                                                   
         MVI   POSTINDS,POSTCURO   SET TRANSACTION IS OPEN                      
         BE    *+8                                                              
         MVI   POSTINDS,POSTCURC   SET TRANSACTION IS CLOSED                    
                                                                                
UPDTRNX  J     EXITY                                                            
                                                                                
UPDTRNE  MVI   TRNERRS,TRNETRNI    SET TRANSACTION IN ERROR & EXIT              
         J     EXITN                                                            
                                                                                
UPDTRNE2 MVI   TRNERRS,TRNERRGL    GL ERROR                                     
         J     EXITN                                                            
         DROP  RB                                                               
         EJECT                                                                  
***********************************************************************         
* UPDATE ACCOUNT BALANCE AND STATUS ELEMENTS                          *         
***********************************************************************         
                                                                                
         USING ACTRECD,R5          R5=A(ACCOUNT RECORD)                         
UPDACT   NTR1  BASE=*,LABEL=*                                                   
         L     R5,AACT                                                          
         TM    TRNINDS,TRNILAST    TEST FINAL CALL                              
         BNZ   UPDACTX                                                          
         TM    TRNINDS2,TRNIUBKO   TEST UPDATING BUCKETS ONLY                   
         BNZ   UPDACTX                                                          
         TM    TRNINDS,TRNIDRFT    TEST DRAFT TRANSACTION                       
         BNZ   UPDACT08                                                         
         TM    TRNINDS1,TRNIDNUB   TEST DO NOT UPDATE BALANCES                  
         BNZ   UPDACTX                                                          
         TM    INDS1,IND2FDUM      TEST DUMMY 2F TRANSACTION                    
         BNZ   UPDACTX                                                          
         TM    INDS1,IND1RDUM+INDRET47                                          
         BNZ   UPDACT26                                                         
         CP    TRNAMNT,PZERO       DON'T UPDATE BALANCE IF ZERO AMOUNT          
         BE    UPDACT02                                                         
         LA    RE,TRNTRNDR         SET TRANSACTION AMOUNT                       
         TM    TRNSTAT,TRNSDR                                                   
         BNZ   *+8                                                              
         LA    RE,TRNTRNCR                                                      
         AP    0(L'TRNTRNDR,RE),TRNAMNT                                         
         GOTOR PSTBAL,PARM,ACTRECD,TRNELD                                       
         TM    PAY2IND,PAY2L34+PAY2CSH    X'11'        NMAL                     
         JNO   UPDACT02                                NMAL                     
         XC    PAY2IND,PAY2IND      RESET INDICATOR    NMAL                     
         GOTOR PAY2JOB,PARM,ACTRECD,TRNELD             NMAL                     
         TM    PAY2IND,PAY2NCS      DO WE NEED TO ADD SCIEL NMAL                
         JNO   UPDACT02                                NMAL                     
         XC    PAY2IND,PAY2IND      RESET INDICATOR    NMAL                     
         LA    R3,ELEMENT                              NMAL                     
         USING SCIELD,R3                                   NMAL                 
         GOTOR VHELLO,ELIST,(C'P',FILE),ACTRECD,SCIELD,0   NMAL                 
         CLI   ELERR,0                                     NMAL                 
         JNE   *+2                                         NMAL                 
         DROP  R3                                                               
                                                                                
UPDACT02 GOTOR GETRST              GET ACCOUNT STATUS ELEMENT                   
         BE    *+6                                                              
         DC    H'0'                                                             
         USING RSTELD,R1                                                        
         CLC   RSTTDATE,TRNPDAT1   TEST ACCOUNT ACTIVE TODAY                    
         BNE   UPDACT04                                                         
         TM    TRNINDS,TRNILIVE    TEST DRAFT TO LIVE                           
         BNZ   UPDACT04                                                         
         CP    TRNAMNT,PZERO       TEST ZERO POSTING AMOUNT                     
         BE    UPDACT08            YES - DON'T UPDATE ACCOUNT RECORD            
                                                                                
UPDACT04 MVC   RSTTDATE,TRNPDAT1   UPDATE LAST ACTIVITY DATE                    
         MVC   BYTE1,RSTSTAT1                                                   
         NI    BYTE1,RSTSACIC+RSTSACIL                                          
*&&UK*&& NI    RSTSTAT1,FF-(RSTSACIC+RSTSACIL)                                  
*&&US*&& NI    RSTSTAT1,FF-(RSTSACIC)                                           
         MVC   BYTE2,RSTSTAT1                                                   
         NI    BYTE2,RSTSACIC+RSTSACIL                                          
*&&UK*&& NI    ACTRSTAT,FF-(ACTSCLOS+ACTSLOCK)                                  
*&&US*&& NI    ACTRSTAT,FF-(ACTSCLOS)                                           
         CLC   BYTE1,BYTE2         TEST CHANGE OF ACCOUNT STATUS                
         BE    UPDACT06                                                         
*&&UK*&& GOTOR UPDSAP              REFRESH SAPRECD STATUS                       
         TM    TRNCPYS6,CPYSRAPP   TEST GENERATING ACTIVITY POINTERS            
         BZ    UPDACT06                                                         
         TM    TRNINDS,TRNIWRNO    TEST WRITE=NO                                
         BNZ   UPDACT06                                                         
         ICM   RF,15,VRAPPER       TEST RAPPER RESOLVED                         
         BZ    UPDACT06                                                         
                                                                                
         CLC   PCRDUL,ACTKUNT      TEST PRODUCTION SUPPLIER OR JOB              
         BE    *+14                                                             
         CLC   PRODUL,ACTKUNT                                                   
         BNE   UPDACT06                                                         
         MVC   RAPCPY,ACTKCPY                                                   
         MVI   RAPRTYP,RAPKRJOB                                                 
         CLC   PRODUL,ACTKUNT                                                   
         BE    *+8                                                              
         MVI   RAPRTYP,RAPKRSUP                                                 
         MVC   RAPAREC,AACT                                                     
         MVC   RAPACOM,TRNCOMF                                                  
         MVI   RAPEMU,C'N'                                                      
         MVI   RAPACTN,RAPAELEM                                                 
         LA    R1,RAPBLK                                                        
         BASR  RE,RF                                                            
         BE    *+6                                                              
         DC    H'0'                                                             
         MVI   RAPACTN,RAPAPTR                                                  
         BASR  RE,RF                                                            
         BE    *+6                                                              
         DC    H'0'                                                             
                                                                                
UPDACT06 GOTOR VHELLO,ELIST,(C'G',FILE),('APOELQ',ACTRECD),0                    
         CLI   ELERR,0                                                          
         BNE   UPDACT08                                                         
         L     R1,ELADDR           RETURN A(ELEMENT) IN R1                      
         USING APOELD,R1                                                        
         CLI   APOLN,APOLN2Q                                                    
         BL    UPDACT08                                                         
         CLC   APOLMOS,MOS         TEST THIS IS LOWEST MOS POSTED               
         BH    UPDACT08                                                         
         MVC   APOLMOS,MOS         YES - SET LOWEST VALUE                       
                                                                                
UPDACT08 TM    TRNINDS,TRNIDRFT                                                 
         BNZ   UPDACT14                                                         
         GOTOR VHELLO,ELIST,(C'G',FILE),('ABLELQ',ACTRECD),0                    
         CLI   ELERR,0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
         L     R3,ELADDR                                                        
         USING ABLELD,R3                                                        
         SR    RE,RE                                                            
         CLI   ABLLN,ABLLN3Q                                                    
         BL    UPDACT10                                                         
         ICM   RE,15,ABLTXS        UPDATE NUMBER OF TRANSACTIONS                
         AHI   RE,1                                                             
         STCM  RE,15,ABLTXS                                                     
         B     UPDACT14                                                         
                                                                                
UPDACT10 XC    ELEMENT,ELEMENT                                                  
         IC    RE,ABLLN                                                         
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   ELEMENT(0),ABLELD                                                
         MVI   ABLEL,DELELQ                                                     
         GOTOR VHELLO,ELIST,(C'D',FILE),('DELELQ',ACTRECD),0                    
         LA    R3,ELEMENT                                                       
         MVI   ABLLN,ABLLN3Q                                                    
         OC    ABLURG,ABLURG                                                    
         BNZ   *+10                                                             
         ZAP   ABLURG,PZERO                                                     
         LA    RE,1                                                             
         STCM  RE,15,ABLTXS                                                     
UPDACT12 GOTOR VHELLO,ELIST,(C'P',FILE),ACTRECD,ABLELD,0                        
         CLI   ELERR,0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
         DROP  R3                                                               
                                                                                
UPDACT14 TM    TRNINDS,TRNILIVE+TRNIDRFT                                        
         BZ    UPDACT16                                                         
         GOTOR VHELLO,ELIST,(C'G',FILE),('ASTELQ',ACTRECD),0                    
         CLI   ELERR,0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
         L     R1,ELADDR                                                        
         USING ASTELD,R1                                                        
         SR    RE,RE                                                            
         ICM   RE,7,ASTDRAFT       UPDATE NUMBER OF DRAFT TRANSACTIONS          
         LHI   R0,1                                                             
         TM    TRNINDS,TRNILIVE                                                 
         BZ    *+6                                                              
         LNR   R0,R0                                                            
         AR    RE,R0                                                            
         BNM   *+6                                                              
         SR    RE,RE               CLEAR COUNT IF NEGATIVE                      
         STCM  RE,7,ASTDRAFT                                                    
                                                                                
UPDACT16 TM    INDS2,INDPRODN      TEST PRODUCTION LEDGER                       
         BZ    UPDACT30                                                         
*                                                                               
         CLI   TRNTYPE,10          TEST TIME POSTING DSRD-24207                 
         BE    *+8                                                              
         CLI   TRNTYPE,49          TEST TIME POSTING                            
         BE    *+8                                                              
         CLI   TRNTYPE,34          TEST JOB-TO-JOB TRANSFER                     
         BE    *+8                                                              
         CLI   TRNTYPE,21          TEST NON-BILL INVOICE                        
         BNE   UPDACT30                                                         
         CP    TRNAMNT,PZERO       TEST R OR N TIME, OR MEMO EXPENSE            
         BNZ   UPDACT30            NO                                           
         TM    TRNINDS,TRNIDRFT    TEST DRAFT                                   
         BNZ   UPDACT30                                                         
         MVI   BYTE,SCITMSRT       BYTE=SCITYPE TO UPDATE                       
         CP    SSCITSAM,PZERO                                                   
         BNZ   UPDACT22                                                         
*                                                                               
UPDACT18 MVI   BYTE,SCITMCRT                                                    
         CP    SSCITCAM,PZERO                                                   
         BNZ   UPDACT22                                                         
*                                                                               
*UPDACT20 MVI   BYTE,SCITMEXP                                                   
UPDACT20 MVI   BYTE,SCITSJXP       DSRD-24207                                   
         CP    SSCITMAM,PZERO                                                   
         BZ    UPDACT30                                                         
                                                                                
UPDACT22 SR    R0,R0               UPDATE/BUILD SCIELS                          
         USING SCIELD,RF                                                        
         LA    RF,ACTRFST                                                       
                                                                                
UPDACT24 CLI   SCIEL,0                                                          
         BE    UPDACT26                                                         
         CLI   SCIEL,SCIELQ                                                     
         BNE   *+14                                                             
         CLC   SCITYPE,BYTE                                                     
         BE    *+14                                                             
                                                                                
         IC    R0,SCILN                                                         
         AR    RF,R0                                                            
         B     UPDACT24                                                         
                                                                                
         CLI   BYTE,SCITMSRT       TEST SCITYPE FOUND                           
         BNE   *+14                                                             
         AP    SCIAMNT,SSCITSAM    UPDATE VAL FROM SAVED ACCUM                  
         B     UPDACT18            AND LOOK FOR NEXT                            
                                                                                
         CLI   BYTE,SCITMCRT       TEST SCITYPE FOUND                           
         BNE   *+14                                                             
         AP    SCIAMNT,SSCITCAM                                                 
         B     UPDACT20            AND LOOK FOR NEXT                            
         AP    SCIAMNT,SSCITMAM                                                 
         B     UPDACT30                                                         
*                                                                               
UPDACT26 XC    ELEMENT,ELEMENT     SCIEL NOT FOUND - BUILD ONE                  
         LA    RF,ELEMENT                                                       
         MVI   SCIEL,SCIELQ                                                     
         MVI   SCILN,SCILN1Q                                                    
         MVC   SCITYPE,BYTE                                                     
         CLI   BYTE,SCITMSRT                                                    
         BNE   *+14                                                             
         ZAP   SCIAMNT,SSCITSAM                                                 
         B     UPDACT28                                                         
         CLI   BYTE,SCITMCRT                                                    
         BNE   *+14                                                             
         ZAP   SCIAMNT,SSCITCAM                                                 
         B     UPDACT28                                                         
         ZAP   SCIAMNT,SSCITMAM                                                 
                                                                                
UPDACT28 LA    R0,FILE                                                          
         GOTOR VHELLO,ELIST,(C'P',(R0)),ACTRECD,SCIELD                          
         CLI   ELERR,0                                                          
         BE    *+6                                                              
         DC    H'0'                CAN'T ADD SCIEL                              
         CLI   BYTE,SCITMSRT       TEST SCIELS UPDATED/ADDED SO FAR             
         BE    UPDACT18            DONE SALES, NOW DO COST                      
         CLI   BYTE,SCITMCRT                                                    
         BE    UPDACT20            DONE COST, NOW DO MEMO EXP                   
         B     UPDACT30                                                         
         DROP  RF                                                               
                                                                                
UPDACT30 ZAP   SSCITSAM,PZERO                                                   
         ZAP   SSCITMAM,PZERO                                                   
         OC    TRNACC,TRNACC       TEST ACCOUNT RECORD PASSED                   
         BNZ   UPDACTX             YES - DON'T WRITE ACCOUNT RECORD             
         GOTOR PUTACT                                                           
                                                                                
UPDACTX  J     EXITY                                                            
         DROP  R1,RB                                                            
         EJECT                                                                  
***********************************************************************         
* UPDATE OFFICE/ACCOUNT BALANCE ELEMENT & HISTORY ELEMENTS            *         
***********************************************************************         
                                                                                
UPDOFA   NTR1  BASE=*,LABEL=*                                                   
         L     R5,AOFA                                                          
         USING OFARECD,R5          R5=A(ACCOUNT/OFFICE RECORD)                  
         TM    TRNINDS,TRNILAST    TEST FINAL CALL                              
         BNZ   UPDOFAX                                                          
         TM    TRNINDS2,TRNIUBKO   TEST UPDATING BUCKETS ONLY                   
         BNZ   UPDOFAX                                                          
         TM    TRNINDS,TRNIDRFT    TEST DRAFT TRANSACTION                       
         BZ    UPDOFA02                                                         
         TM    TRNOFAI,TRNIOAP     IF OFFICE/ACCOUNT NEEDS TO BE ADDED          
         BNZ   UPDOFA04            THEN DO IT                                   
         B     UPDOFAX             ELSE DON'T UPDATE RECORD                     
                                                                                
UPDOFA02 TM    TRNINDS,TRNIDNUB    TEST DO NOT UPDATE BALANCES                  
         BNZ   UPDOFAX                                                          
         TM    INDS1,IND1RDUM+INDRET47+IND2FDUM                                 
         BNZ   UPDOFAX                                                          
         TM    TRNOFAI,TRNIOAP+TRNIOWP                                          
         BZ    UPDOFAX                                                          
         GOTOR PSTBAL,PARM,OFARECD,TRNELD                                       
                                                                                
         OC    OFARLMOS,OFARLMOS   SET LOW MOS VALUE IN RECORD STATUS           
         BZ    *+14                                                             
         CLC   MOS,OFARLMOS                                                     
         BNL   *+10                                                             
         MVC   OFARLMOS,MOS                                                     
         OC    OFARHMOS,OFARHMOS   SET HIGH MOS VALUE IN RECORD STATUS          
         BZ    *+14                                                             
         CLC   MOS,OFARHMOS                                                     
         BNH   *+10                                                             
         MVC   OFARHMOS,MOS                                                     
                                                                                
UPDOFA04 OC    TRNOFA,TRNOFA       TEST OFFICE/ACCOUNT RECORD PASSED            
         BNZ   UPDOFAX             YES - DON'T WRITE ACCOUNT RECORD             
         GOTOR PUTOFA                                                           
                                                                                
UPDOFAX  J     EXITY                                                            
         DROP  RB                                                               
         EJECT                                                                  
***********************************************************************         
* UPDATE CONTRA-ACCOUNT BUCKET RECORDS                                *         
***********************************************************************         
                                                                                
UPDCAC   NTR1  BASE=*,LABEL=*                                                   
         TM    TRNINDS,TRNILAST    TEST FINAL CALL                              
         BNZ   UPDCACX                                                          
         MVI   BUKSTAT,BUKSLIVE    SET BUCKET STATUS                            
         TM    TRNINDS,TRNIDRFT    TEST DRAFT TRANSACTION                       
         BZ    *+8                                                              
         MVI   BUKSTAT,BUKSDRFT                                                 
         TM    INDS1,INDRET47+IND1RDUM                                          
         BNZ   UPDCACX                                                          
                                                                                
         LA    R3,BUKELEM1                                                      
         USING BUKELD,R3                                                        
         MVI   BUKEL,BUKELQ        BUILD VIRGIN HISTORY ELEMENT                 
         MVI   BUKLN,BUKLN7Q                                                    
         MVC   BUKMOS,MOS                                                       
         ZAP   BUKDR7,PZERO                                                     
         ZAP   BUKCR7,PZERO                                                     
                                                                                
         TM    TRNINDS2,TRNIUBKO   TEST UPDATING SPECIAL BUCKETS ONLY           
         BNZ   UPDCAC08                                                         
         TM    INDS1,IND2FDUM                                                   
         BNZ   UPDCAC08                                                         
*&&US                                                                           
         TM    TRNINDS1,TRNICOKE   TEST IF IGNORING COKE EXPENDITURE            
         BNZ   UPDCAC06            YES                                          
         LA    R1,TRNELD           LOOK FOR COKE EXPENDITURE ELEMENTS           
         USING SCIELD,R1                                                        
         SR    R0,R0                                                            
UPDCAC02 CLI   SCIEL,0             TEST EOR                                     
         BE    UPDCAC06                                                         
         CLI   SCIEL,SCIELQ        TEST COKE SUBSIDIARY CASH ELEMENTS           
         BNE   UPDCAC04                                                         
         CLI   SCITYPE,SCITGRNT                                                 
         BE    UPDCAC08                                                         
         CLI   SCITYPE,SCITCOKE                                                 
         BE    UPDCAC08                                                         
UPDCAC04 IC    R0,SCILN            BUMP TO NEXT ELEMENT                         
         AR    R1,R0                                                            
         B     UPDCAC02                                                         
         DROP  R1                                                               
*&&                                                                             
UPDCAC06 MVI   BUKTYPE,C' '        BUILD HISTORY ELEMENT                        
         MVC   BUKSTYP,SPACES                                                   
         LA    R1,BUKDR7                                                        
         TM    TRNSTAT,TRNSDR                                                   
         BNZ   *+8                                                              
         LA    R1,BUKCR7                                                        
         AP    0(L'BUKCR7,R1),TRNAMNT                                           
         GOTOR PSTBUK              UPDATE HISTORY ELEMENT                       
                                                                                
UPDCAC08 LA    R2,TRNELD                                                        
         USING SCIELD,R2                                                        
UPDCAC10 SR    R0,R0               BUMP TO NEXT ELEMENT                         
         IC    R0,SCILN                                                         
         AR    R2,R0                                                            
         CLI   SCIEL,0             TEST EOR                                     
         BE    UPDCAC24                                                         
         CLI   SCIEL,SCIELQ                                                     
         BNE   UPDCAC20                                                         
                                                                                
         CLI   SCITYPE,SCITNULL    TEST SPECIAL BUCKET TYPE                     
         BNE   UPDCAC12                                                         
         CLI   SCILN,SCILN2Q       THIS MUST HAVE SUB-TYPE SPECIFIED            
         BNH   UPDCAC10                                                         
         MVC   BUKTYPE,SCISUBTY    SCISUBTY CONTAINS BUCKET TYPE AND            
         MVC   BUKSTYP,SPACES      SUB-BUCKET TYPE                              
         MVC   BUKSTYP(L'SCISUBTY-1),SCISUBTY+1                                 
         ZAP   BUKDR7,SCIAMNT                                                   
         ZAP   BUKCR7,SCIADMN                                                   
         GOTOR PSTBUK                                                           
         B     UPDCAC10                                                         
                                                                                
UPDCAC12 ZAP   BUKDR7,PZERO        LOCATE SUBSIDIARY CASH ELEMENTS              
         ZAP   BUKCR7,PZERO                                                     
                                                                                
         BASR  R1,0                                                             
         AHI   R1,BUKTAB-*                                                      
         USING BUKTABD,R1          R1=A(BUCKET TYPE TABLE)                      
UPDCAC14 CLI   BUKTABD,BUKTEOTQ    TEST EOT                                     
         BE    UPDCAC10                                                         
         CLC   BUKTITYP,SCITYPE    MATCH ON BUCKET (ELEMENT) TYPE               
         BNE   UPDCAC16                                                         
         CLC   BUKTLEDG,SPACES     TEST SPECIAL LEDGER IN TABLE                 
         BE    UPDCAC18                                                         
         CLC   BUKTLEDG,TRNKUNT    YES - MATCH ON LEDGER                        
         BE    UPDCAC18                                                         
UPDCAC16 AHI   R1,BUKTABL          BUMP TO NEXT TABLE ENTRY                     
         B     UPDCAC14                                                         
                                                                                
UPDCAC18 CLI   BUKTOTYP,0          TEST DON'T CREATE BUCKET RECORD              
         BE    UPDCAC10                                                         
         MVC   SCITYPE,BUKTOTYP    SET BUCKET TYPE IN ELEMENT                   
         MVC   BUKTYPE,SCITYPE     SET BUCKET TYPE FOR RECORD                   
         MVC   BUKSTYP,SPACES                                                   
         CLI   SCILN,SCILN2Q                                                    
         BNH   *+10                                                             
         MVC   BUKSTYP,SCISUBTY    SET BUCKET SUB-TYPE                          
         SR    RF,RF                                                            
         ICM   RF,3,BUKTROUT                                                    
         BZ    UPDCAC10                                                         
         A     RF,ABASE                                                         
         BASR  RE,RF               CALL SUBSIDIARY CASH ROUTINE                 
         B     UPDCAC10            RETURNS AT 0(RE) TO SKIP BUCKET              
         GOTOR PSTBUK              RETURNS AT 4(RE) TO UPDATE BUCKET            
         B     UPDCAC10                                                         
         DROP  R1                                                               
                                                                                
         PUSH  USING                                                            
         DROP  R3                                                               
         USING BUKELD,R2                                                        
UPDCAC20 CLI   BUKEL,BUKELQ        TEST BUCKET ELEMENT                          
         BNE   UPDCAC10                                                         
         MVI   BUKTYPE,C' '        SET DEFAULT BUCKET                           
         MVC   BUKELEM1,BUKELD                                                  
         GOTOR PSTBUK              ADD TO BUCKET RECORD                         
         B     UPDCAC10                                                         
         POP   USING                                                            
                                                                                
UPDCAC24 OC    TRNCAC,TRNCAC       TEST A(CONTRA-ACCOUNT PASSED)                
         BZ    *+14                                                             
         OC    TRNBUK,TRNBUK       AND A(BUCKET LIST PASSED)                    
         BNZ   UPDCACX                                                          
         GOTOR PUTCHD                                                           
         GOTOR PUTCH2                                                           
         GOTOR PUTBUK                                                           
                                                                                
UPDCACX  J     EXITY                                                            
         DROP  RB                                                               
         EJECT                                                                  
***********************************************************************         
* HANDLE SUBSIDIARY CASH ELEMENTS FOR BUCKET UPDATING                 *         
***********************************************************************         
                                                                                
BTGRNT   DS    0H                  GROSS/NET FOR US COKE EXPENDITURE            
         TM    TRNINDS1,TRNICOKE   EXIT IF IGNORE COKE BIT IS ON                
         BNZ   0(RE)                                                            
         ZAP   BUKDR7,SCIGRS       FOR COKE GROSS=DEBIT, NET=CREDIT             
         CLI   SCILN,SCILN2Q                                                    
         JL    *+10                                                             
         ZAP   BUKCR7,SCINET                                                    
         B     4(RE)                                                            
                                                                                
BTVEHI   DS    0H                  VEHICLES                                     
BTHOUR   DS    0H                  HOURS                                        
BTGRSS   DS    0H                  GROSS                                        
BTIVAT2P DS    0H                  VAT                                          
BTGLEV   DS    0H                  NET (GROSS LESS VAT)                         
*&&UK                                                                           
BTHOUR1R DS    0H                  HOURS (1R ONLY)                              
*&&                                                                             
         LA    R1,BUKDR7           POST DEBIT/CREDIT BASED ON TRNSTAT           
         TM    TRNSTAT,TRNSDR                                                   
         JNZ   *+8                                                              
         LA    R1,BUKCR7                                                        
         ZAP   0(L'BUKCR7,R1),SCIAMNT                                           
         B     4(RE)                                                            
*&&US                                                                           
BTHOUR1R DS    0H                  HOURS (1R ONLY)                              
*&&                                                                             
BTLOAT1R DS    0H                  LEAVE OF ABSENCE HOURS (1R ONLY)             
         LA    R1,BUKCR7                                                        
         CP    TRNAMNT,PZERO                                                    
         JE    *+8                                                              
         LA    R1,BUKDR7                                                        
         ZAP   0(L'BUKDR7,R1),SCIAMNT                                           
         B     4(RE)                                                            
                                                                                
BTGRSS1J DS    0H                  GROSS (US 1J LEDGER ONLY)                    
BTHOUR1J DS    0H                  HOURS (US 1J LEDGER ONLY)                    
BTVEHI1J DS    0H                  VEHICLES (US 1J LEDGER ONLY)                 
BTFEEA   DS    0H                  FEE ADJUSTMENTS                              
BTFEES   DS    0H                  FEES                                         
BTANAL   DS    0H                  ANALYSIS                                     
         ZAP   BUKDR7,SCIAMNT      AMOUNT TO DEBIT                              
         CLI   SCILN,SCILN2Q                                                    
         JL    *+10                                                             
         ZAP   BUKCR7,SCIADMN      ADMIN TO CREDIT (IF PRESENT)                 
         B     4(RE)                                                            
                                                                                
BTDEPR41 DS    0H                  ASSET DEPRECIATION (LEDGER 41 ONLY)          
BTBENE   DS    0H                  BENEFITS (US 1R LEDGER ONLY?)                
         ZAP   BUKDR7,SCIAMNT      BENEFIT                                      
         ZAP   BUKCR7,SCIADMN      ADMINISTRATIVE                               
         B     4(RE)                                                            
                                                                                
BTIAMTST DS    0H                  INSURABLE AMOUNT (UK LEDGER ST ONLY)         
BTNICTST DS    0H                  NIC AMOUNT (UK LEDGER ST ONLY)               
BTFORDST DS    0H                  INCOME DEBIT (UK LEDGER ST ONLY)             
BTJHRSST DS    0H                  NET DEBIT (UK LEDGER ST ONLY)                
         ZAP   BUKDR7,SCIAMNT                                                   
         B     4(RE)                                                            
                                                                                
BTINCAST DS    0H                  INCOME CREDIT (UK ST LEDGER ONLY)            
BTGLEVST DS    0H                  NET CREDIT (UK LEDGER ST ONLY)               
         ZAP   BUKCR7,SCIAMNT                                                   
         B     4(RE)                                                            
         DROP  R2,R3                                                            
         EJECT                                                                  
***********************************************************************         
* POST HISTORY ELEMENTS TO BUCKET BUFFER                              *         
***********************************************************************         
                                                                                
LST      USING TRNKEY,TRNKEYL                                                   
                                                                                
PSTBUK   ST    RE,SAVERE                                                        
         TM    TRNINDS2,TRNIRBKA   TEST REPLACING BUCKET VALUES                 
         JNZ   PSTBUK02                                                         
         CP    BUKELEM1+(BUKDR7-BUKELD)(L'BUKDR7),PZERO                         
         JNE   PSTBUK02                                                         
         CP    BUKELEM1+(BUKCR7-BUKELD)(L'BUKCR7),PZERO                         
         JE    PSTBUKX                                                          
                                                                                
PSTBUK02 MVC   BUKOFFC,SPACES      ADD WITHOUT OFFICE                           
         TM    INDS2,INDKEYWC      UNLESS LEDGER HAS W/C IN KEY                 
         JZ    *+10                                                             
         MVC   BUKOFFC,LST.TRNKOFF                                              
         GOTOR UPDBUK                                                           
                                                                                
         TM    INDS2,INDKEYWC      LEDGER HAS W/C IN KEY                        
         JNZ   PSTBUKX                                                          
         TM    TRNCPYS4,CPYSOFF2   TEST IF COMPANY ON NEW OFFICE                
         JZ    PSTBUK10                                                         
         MVC   BUKOFFC,LST.TRNKOFF                                              
         CLC   BUKOFFC,SPACES      DON'T POST IF NO OFFICE                      
         JE    PSTBUK10                                                         
         GOTOR UPDBUK              ADD OFFICE BUCKET                            
                                                                                
PSTBUK10 TM    TRNCPYSA,CPYSACCT   TEST P&L RECORDS REQUIRED                    
         JZ    PSTBUKX                                                          
         TM    TRNINDS,TRNIDRFT    TEST DRAFT TRANSACTION                       
         JO    PSTBUKX                                                          
         CLC   CCSTUL,LST.TRNKUNT               LEDGER - C'1C'                  
         JNE   PSTBUKX                                                          
         CLC   LST.TRNKCUNT(L'CBILUL),CBILUL    LEDGER - C'11'                  
         JL    PSTBUKX                                                          
         CLC   LST.TRNKCUNT(L'CBILUL),COHDUL    LEDGER - C'16'                  
         JH    PSTBUKX                                                          
                                                                                
         MVI   BUKSTAT,BUKSXTRA     SPECIAL EXTRA BUCKET                        
         MVC   BUKSTYP,SPACES       NO SPECIAL TYPE                             
         MVC   BUKOFFC,SPACES       NO OFFICE FOR 14 TO 16, IN CONTRA           
                                                                                
PSTBUK20 CLI   LST.TRNKCLDG,C'3'    LEDGER 11 TO 13                             
         JH    PSTBUK25             NO, 14 TO 16, SO POST                       
         MVC   BUKOFFC,LST.TRNKOFF  OKAY FOR 11 TO 13                           
         TM    TRNCPYS4,CPYSOFF2                                                
         JO    PSTBUK22                                                         
         MVC   BUKOFFC(1),TRNKOFF1  ONE BYTE OFFICE                             
         MVI   BUKOFFC+1,C' '                                                   
*&&UK                                                                           
         TM    TRNCPYS1,CPYSOROE                                                
         JO    PSTBUK22                                                         
         MVC   BUKOFFC,SPACES       NO OFFICES                                  
         J     PSTBUK25                                                         
*&&                                                                             
PSTBUK22 CLC   BUKOFFC,SPACES                                                   
         JNH   PSTBUKX                                                          
                                                                                
PSTBUK25 GOTOR UPDBUK               ADD EXTRA BUCKET                            
                                                                                
PSTBUKX  L     RE,SAVERE                                                        
         BR    RE                                                               
         DROP  LST                                                              
                                                                                
         USING BUKBUFD,R1          R1=A(BUCKET BUFFER)                          
UPDBUK   L     R1,ABUK                                                          
         ICM   RF,15,BUKBHNUM      RF=NUMBER OF ENTRIES IN TABLE                
         AHI   R1,BUKBHDRL         BUMP OVER TABLE HEADER                       
         LTR   R0,RF               TEST TABLE EMPTY                             
         JZ    UPDBUK06                                                         
                                                                                
UPDBUK02 CLI   BUKBKEY,BUKBEOTQ    TEST END OF TABLE                            
         JE    UPDBUK06                                                         
         CLC   BUKEY(BUKEYL),BUKBKEY MATCH ON TYPE STATUS & MONTH               
         JNE   UPDBUK04                                                         
         CLC   BUKBMOS,BUKELEM1+(BUKMOS-BUKELD)                                 
         JE    UPDBUK08                                                         
                                                                                
UPDBUK04 AHI   R1,BUKBUFL          BUMP TO NEXT BUFFER ENTRY                    
         BRCT  R0,UPDBUK02                                                      
                                                                                
UPDBUK06 AHI   RF,1                INCREMENT NUMBER OF TABLE ENTRIES            
         CHI   RF,BUKBMAXN         TEST SPACE FOR NEW ENTRY                     
         JNH   *+6                                                              
         DC    H'0'                                                             
         MVC   BUKBKEY(BUKBKEYL),BUKEY                                          
         MVC   BUKBMOS,BUKELEM1+(BUKMOS-BUKELD)                                 
         ZAP   BUKBDR,BUKELEM1+(BUKDR7-BUKELD)(L'BUKDR7)                        
         ZAP   BUKBCR,BUKELEM1+(BUKCR7-BUKELD)(L'BUKCR7)                        
         MVI   BUKBUFD+BUKBUFL,BUKBEOTQ                                         
         L     R1,ABUK                                                          
         STCM  RF,15,BUKBHNUM                                                   
         J     UPDBUKX                                                          
                                                                                
UPDBUK08 AP    BUKBDR,BUKELEM1+(BUKDR7-BUKELD)(L'BUKDR7)                        
         AP    BUKBCR,BUKELEM1+(BUKCR7-BUKELD)(L'BUKCR7)                        
                                                                                
UPDBUKX  BR    RE                                                               
         DROP  R1                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO UPDATE NEW GL BUCKETS AND GL POSTINGS                    *         
***********************************************************************         
                                                                                
         USING GLBLKD,R2                                                        
GLUPDT   NTR1  BASE=*,LABEL=*                                                   
         TM    TRNGLIND,TRNGLPAS   RECURSIVE CALL SO                            
         BO    GLUPDXIT            SKIP SINCE INVOKED FORM ADDTRANS             
         OI    TRNGLIND,TRNGLPAS                                                
                                                                                
         TM    TRNINDS,TRNILAST                                                 
         BZ    GLUPDT80                                                         
         L     R2,TRNGLBLK                                                      
         OC    GL2#OF,GL2#OF       NUMBER OF ITEMS TO POST                      
         BZ    GLUPDT80            NO ITEMS TO POST                             
         TM    TRNINDS2,TRNIUPDG   SET BY PROGRAM                               
         BZ    GLUPDT80            NO, SO DON'T PROCESS YET                     
         MVC   @ADTRANS,ABASE                                                   
         NI    TRNINDS2,FF-TRNIUPDG                                             
         MVI   GLACTION,GLPOST                                                  
         GOTOR VGLPOST,DMCB,TRNGLBLK                                            
                                                                                
GLUPDT80 TM    TRNMODE,TRNMON      ON-LINE ?                                    
         BZ    GLUPDXIT            NO, SO OKAY AS IS                            
         TM    TRNGLIND,TRNGLINT   WAS THIS INITIALIZED                         
         BO    *+6                                                              
         DC    H'00'               SOMETHING IS WRONG                           
                                                                                
         GOTOR SWAP#K,DMCB,ATIA,AMINBUF2,GL#OFK   SWAP                          
                                                                                
GLUPDXIT NI    TRNGLIND,FF-TRNGLPAS                                             
         J     EXITY                                                            
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* SWAP #K OF STORAGE, WORKS IN 31 BIT ADDRESSING                                
*      IF P1 = P2 THEN IT WILL CLEAR THE AREA                                   
*      P1 = AREA 1                                                              
*      P2 = AREA 2                                                              
*      P3 = # OF 1024 BYTES TO SWAP                                             
***********************************************************************         
SWAP#K   NTR1  BASE=*,LABEL=*                                                   
         SAM31                     31 BIT ADDRESSING                            
         ICM   R2,15,0(R1)         WHERE TO SWAP FROM                           
         JNZ   *+6                                                              
         DC    H'00'               PROGRAMMING ERROR                            
                                                                                
         ICM   R4,15,4(R1)         WHERE TO SWAP TO                             
         JNZ   *+6                                                              
         DC    H'00'               PROGRAMMING ERROR                            
                                                                                
         ICM   R3,15,8(R1)         # OF K TO SWAP                               
         JNZ   *+6                                                              
         DC    H'00'               PROGRAMMING ERROR                            
                                                                                
         ICM   RF,15,VPROTOFF      TURN MEMORY PROTECTION OFF                   
         JZ    *+6                                                              
         BASR  RE,RF                                                            
                                                                                
         CR    R2,R4               ARE THEY THE SAME                            
         JE    SWAP#K20                                                         
         SLL   R3,2                #K*(1024/256) OR #K*4                        
                                                                                
SWAP#K10 XC    0(256,R2),0(R4)     MAGIC SWAPPING TECHNIQUE                     
         XC    0(256,R4),0(R2)                                                  
         XC    0(256,R2),0(R4)                                                  
         LA    R2,256(,R2)                                                      
         LA    R4,256(,R4)                                                      
         BRCT  R3,SWAP#K10                                                      
         J     SWAP#K30                                                         
                                                                                
SWAP#K20 SLL   R3,10               #K*1024 FOR # OF BYTES TO CLEAR              
         SR    R5,R5                                                            
         MVCL  R2,R4                                                            
                                                                                
SWAP#K30 ICM   RF,15,VPROTON       TURN MEMORY PROTECTION BACK ON               
         JZ    *+6                                                              
         BASR  RE,RF                                                            
         SAM24                                                                  
         J     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* GET OR ADD A TRANSACTION STATUS ELEMENT                             *         
***********************************************************************         
                                                                                
GETTRS   NTR1  BASE=*,LABEL=*                                                   
         LA    RE,TRNRECD                                                       
         LA    R0,FILETRN                                                       
         CR    R1,RE               TEST USER TRANSACTION RECORD                 
         BE    *+8                                                              
         LA    R0,FILE                                                          
         LR    R6,R1                                                            
                                                                                
         GOTOR VHELLO,ELIST,(C'G',(R0)),('TRSELQ',TRNRECD),0                    
         CLI   ELERR,0                                                          
         BNE   GETTRS02            FOUND TRANSACTION STATUS ELEMENT             
         L     RF,ELADDR                                                        
         USING TRSELD,RF                                                        
         CLI   TRSLN,TRSLNQ        TEST NEW ELEMENT LENGTH                      
         BE    GETTRSX                                                          
         XC    ELEMENT,ELEMENT                                                  
         SR    RE,RE               EXTRACT SIGNIFICANT STATUS DATA              
         ICM   RE,1,TRSLN                                                       
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   ELEMENT(0),TRSELD                                                
         MVI   TRSEL,DELELQ                                                     
         GOTOR VHELLO,ELIST,(C'D',(R0)),('DELELQ',TRNRECD),0                    
         LA    RF,ELEMENT          BUILD ELEMENT WITH NEW LENGTH                
         MVI   TRSLN,TRSLNQ                                                     
         B     GETTRS04                                                         
                                                                                
GETTRS02 XC    ELEMENT,ELEMENT     BUILD VIRGIN STATUS ELEMENT                  
         LA    RF,ELEMENT                                                       
         MVI   TRSEL,TRSELQ                                                     
         MVI   TRSLN,TRSLNQ                                                     
                                                                                
GETTRS04 GOTOR VHELLO,ELIST,(C'P',(R0)),TRNRECD,TRSELD                          
         CLI   ELERR,0                                                          
         BE    *+6                                                              
         DC    H'0'                CAN'T ADD TRANSACTION STATUS ELEMENT         
         MVC   ELADDR,ELADDR2                                                   
                                                                                
GETTRSX  L     R1,ELADDR           RETURN WITH R1=A(ELEMENT)                    
         J     EXITR1                                                           
         DROP  RB,RF                                                            
         EJECT                                                                  
***********************************************************************         
* GET OR ADD AN ACCOUNT STATUS ELEMENT                                *         
***********************************************************************         
                                                                                
GETRST   NTR1  BASE=*,LABEL=*                                                   
         L     R5,AACT                                                          
         USING ACTRECD,R5                                                       
         SR    R0,R0                                                            
         LA    RF,ACTRECD                                                       
         AH    RF,DATADISP                                                      
         USING RSTELD,RF                                                        
GETRST02 CLI   RSTEL,0             LOCATE STATUS ELEMENT ON ACCOUNT             
         BE    GETRST04                                                         
         CLI   RSTEL,RSTELQ                                                     
         BE    *+14                                                             
         IC    R0,RSTLN                                                         
         AR    RF,R0                                                            
         B     GETRST02                                                         
                                                                                
         LA    R1,RSTELD                                                        
         MVI   ELERR,0                                                          
         CLI   RSTLN,RSTLN2Q       TEST EXTENDED ELEMENT LENGTH                 
         BNL   GETRSTX                                                          
         SR    RE,RE               EXTRACT, DELETE & EXTEND ELEMENT             
         IC    RE,RSTLN                                                         
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   ELEMENT(0),RSTELD                                                
         MVI   RSTEL,DELELQ                                                     
         GOTOR VHELLO,ELIST,(C'D',FILE),('DELELQ',ACTRECD),0,0                  
         LA    RF,ELEMENT                                                       
         XC    RSTEL+RSTLN1Q(RSTLN2Q-RSTLN1Q),RSTEL+RSTLN1Q                     
         MVI   RSTFILT5,C' '                                                    
         MVI   RSTLN,RSTLN2Q                                                    
         B     GETRST06                                                         
                                                                                
GETRST04 LA    RF,ELEMENT          ADD STATUS ELEMENT IF NOT FOUND              
         XC    RSTELD(RSTLN2Q),RSTELD                                           
         MVI   RSTEL,RSTELQ                                                     
         MVI   RSTLN,RSTLN2Q                                                    
         MVI   RSTFILT1,C' '                                                    
         MVI   RSTFILT2,C' '                                                    
         MVI   RSTFILT3,C' '                                                    
         MVI   RSTFILT4,C' '                                                    
         MVI   RSTFILT5,C' '                                                    
         MVC   RSTBDATE,TRNPDAT1                                                
                                                                                
GETRST06 GOTOR VHELLO,ELIST,(C'P',FILE),ACTRECD,RSTELD,0                        
         L     R1,ELADDR2                                                       
                                                                                
GETRSTX  CLI   ELERR,0                                                          
         J     EXITR1                                                           
         DROP  R5,RB,RF                                                         
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO POST TRANSACTION TO BALANCE ELEMENTS                     *         
*                                                                     *         
* NTRY - R1 POINTS TO PARAMETER LIST AS FOLLOWS:-                     *         
*        P1=A(RECORD CONTAINING BALANCE ELEMENT)                      *         
*        P2=A(TRANSACTION ELEMENT)                                    *         
*        POSTINDS CONTAIN INDICATORS                                  *         
***********************************************************************         
                                                                                
PSTBAL   STM   RE,R1,12(RD)                                                     
         LM    RE,RF,0(R1)         RE=A(RECORD), RF=A(TRNELD)                   
         AH    RE,DATADISP                                                      
         SR    R0,R0                                                            
         USING ABLELD,RE           LOCATE BALANCE ELEMENT ON RECORD             
PSTBAL02 CLI   ABLEL,0             TEST EOR                                     
         JE    PSTBALX                                                          
         CLI   ABLEL,ABLELQ        TEST BALANCE ELEMENT FOUND                   
         JE    *+14                                                             
         IC    R0,ABLLN            BUMP TO NEXT ELEMENT                         
         AR    RE,R0                                                            
         J     PSTBAL02                                                         
                                                                                
         TM    POSTINDS,POSTPRVO   TEST PREVIOUSLY OPEN                         
         JZ    PSTBAL04                                                         
         TM    TRNSTAT-TRNELD(RF),TRNSDR                                        
         JZ    *+14                                                             
         SP    ABLDR,TRNAMNT-TRNELD(L'TRNAMNT,RF)                               
         J     PSTBAL06                                                         
         SP    ABLCR,TRNAMNT-TRNELD(L'TRNAMNT,RF)                               
         J     PSTBAL06                                                         
                                                                                
PSTBAL04 TM    POSTINDS,POSTPRVC   TEST PREVIOUSLY CLOSED                       
         JZ    PSTBAL06                                                         
         TM    TRNSTAT-TRNELD(RF),TRNSDR                                        
         JZ    *+14                                                             
         SP    ABLFRWD,TRNAMNT-TRNELD(L'TRNAMNT,RF)                             
         J     PSTBAL06                                                         
         AP    ABLFRWD,TRNAMNT-TRNELD(L'TRNAMNT,RF)                             
                                                                                
PSTBAL06 TM    POSTINDS,POSTCURO   TEST CURRENTLY OPEN                          
         JZ    PSTBAL08                                                         
         TM    TRNSTAT-TRNELD(RF),TRNSDR                                        
         JZ    *+14                                                             
         AP    ABLDR,TRNAMNT-TRNELD(L'TRNAMNT,RF)                               
         J     PSTBALX                                                          
         AP    ABLCR,TRNAMNT-TRNELD(L'TRNAMNT,RF)                               
                                                                                
PSTBAL08 TM    POSTINDS,POSTCURC   TEST CURRENTLY CLOSED                        
         JZ    PSTBALX                                                          
         TM    TRNSTAT-TRNELD(RF),TRNSDR                                        
         JZ    *+14                                                             
         AP    ABLFRWD,TRNAMNT-TRNELD(L'TRNAMNT,RF)                             
         J     PSTBALX                                                          
         SP    ABLFRWD,TRNAMNT-TRNELD(L'TRNAMNT,RF)                             
                                                                                
PSTBALX  LM    RE,R1,12(RD)        RETURN TO CALLER                             
         BR    RE                                                               
         DROP  RE                                                               
         EJECT                                                                  
***********************************************************************         
*  NMAL :-  THIS ROUTINE IS FOR PAY2JOB NMAL     NEERAJ MALIK         *         
* ROUTINE TO UPDATE ACCOUNT RECORD WITH PAY2JOB DETAIL FOR J2J TRANS  *         
*                                                                     *         
* NTRY - R1 POINTS TO PARAMETER LIST AS FOLLOWS:-                     *         
*        P1=A(RECORD CONTAINING CASH ELEMENT )                        *         
*        P2=A(TRANSACTION ELEMENT)                                    *         
*        POSTINDS CONTAIN INDICATORS                                  *         
***********************************************************************         
                                                                                
PAY2JOB  STM   RE,R1,12(RD)                                                     
         LM    RE,RF,0(R1)         RE=A(RECORD), RF=A(TRNELD)                   
         AH    RE,DATADISP                                                      
         SR    R0,R0                                                            
         USING SCIELD,RE           LOCATE BALANCE ELEMENT ON RECORD             
PAY2L10  CLI   SCIEL,0             TEST EOR                                     
         JE    PAY2L30             ADD CASH ELEMENT                             
         CLI   SCIEL,SCIELQ        TEST BALANCE ELEMENT FOUND                   
         JNE   PAY2L20                                                          
         CLI   SCITYPE,SCITCPAT    CHECK FOR JOB CRED PAID TOTAL                
         JE    *+14                                                             
PAY2L20  IC    R0,SCILN            BUMP TO NEXT ELEMENT                         
         AR    RE,R0                                                            
         J     PAY2L10                                                          
                                                                                
         AP    SCIAMNT,TRNAMNT-TRNELD(L'TRNAMNT,RF)                             
         J     PAY2JOBX                                                         
         DC    H'0'        FORCE ABEND                                          
PAY2L30  DS    0H                                                               
         LA    RE,ELEMENT                                                       
*        USING SCIELD,RE                                                        
         XC    SCIELD(SCILN1Q),SCIELD                                           
         MVI   SCIEL,SCIELQ                                                     
         MVI   SCILN,SCILN1Q                                                    
         MVI   SCITYPE,SCITCPAT                                                 
         ZAP   SCIAMNT,TRNAMNT-TRNELD(L'TRNAMNT,RF)                             
         OI    PAY2IND,PAY2NCS       X'20'NEED TO ADD SCIELD                    
*                                                                               
PAY2JOBX LM    RE,R1,12(RD)        RETURN TO CALLER                             
         BR    RE                                                               
         DROP  RE                                                               
         EJECT                                                                  
***********************************************************************         
* ADD BNDEL TO DRAFT PRODUCTION DEBIT TRANSACTION OF NOT PRESENT      *         
***********************************************************************         
                                                                                
ADDBND   ST    RE,SAVERE                                                        
                                                                                
         MVI   ELERR,0                                                          
         TM    TRNINDS,TRNILIVE    TEST DRAFT TO LIVE                           
         JNZ   ADDBNDX                                                          
         TM    INDS2,INDPRODN      TEST PRODUCTION LEDGER                       
         JZ    ADDBNDX                                                          
         TM    TRNSTAT,TRNSDR      TEST DEBIT (CHARGE) POSTING                  
         JZ    ADDBNDX                                                          
         GOTOR VHELLO,ELIST,(C'G',FILETRN),('BNDELQ',TRNRECD),0                 
         CLI   ELERR,0                                                          
         JE    ADDBNDX             FOUND BILL NUMBER/DATE ELEMENT               
         LA    RF,ELEMENT                                                       
         USING BNDELD,RF           BUILD AND ADD ONE IF NOT FOUND               
         XC    BNDELD(BNDLNQ),BNDELD                                            
         MVI   BNDEL,BNDELQ                                                     
         MVI   BNDLN,BNDLNQ                                                     
         MVC   BNDBNO,SPACES                                                    
         GOTOR VHELLO,ELIST,(C'P',FILETRN),TRNRECD,BNDELD,0                     
                                                                                
ADDBNDX  L     RE,SAVERE                                                        
         CLI   ELERR,0                                                          
         BR    RE                                                               
         DROP  RF                                                               
         EJECT                                                                  
***********************************************************************         
* ADD PTA ELEMENT TO DRAFT TRANSACTION RECORD IF NECESSARY            *         
***********************************************************************         
                                                                                
ADDPTA   ST    RE,SAVERE                                                        
                                                                                
         MVI   ELERR,0                                                          
         TM    TRNINDS,TRNILIVE    TEST DRAFT TO LIVE                           
         JNZ   ADDPTAX                                                          
         TM    INDS2,INDPRODN      TEST PRODUCTION LEDGER                       
         JZ    ADDPTAX                                                          
         TM    TRNSTAT,TRNSDR      TEST DEBIT (CHARGE) POSTING                  
         JZ    ADDPTAX                                                          
         GOTOR VHELLO,ELIST,(C'G',FILETRN),('PTAELQ',TRNRECD),0                 
         CLI   ELERR,0                                                          
         JE    ADDPTA02            FOUND BILL NUMBER/DATE ELEMENT               
         LA    RF,ELEMENT                                                       
         USING PTAELD,RF           BUILD AND ADD ONE IF NOT FOUND               
         XC    PTAEL(PTARLN1Q),PTAEL                                            
         MVI   PTAEL,PTAELQ                                                     
         MVI   PTALN,PTARLN1Q                                                   
         ZAP   PTANET,PZERO                                                     
         ZAP   PTANETF,PZERO                                                    
         ZAP   PTACDSC,PZERO                                                    
         ZAP   PTARCORT,PZERO                                                   
         ZAP   PTARCOM,PZERO                                                    
         GOTOR VHELLO,ELIST,(C'P',FILETRN),TRNRECD,PTAELD,0                     
         CLI   ELERR,0                                                          
         JNE   ADDPTAX                                                          
         MVC   ELADDR,ELADDR2                                                   
                                                                                
ADDPTA02 L     RF,ELADDR                                                        
         CLI   PTATYPE,PTATRAL     TEST BILL ALLOCATION                         
         JNE   ADDPTAX                                                          
         TM    PTASTAT1,PTASPEND   TEST ACTIVITY PENDING                        
         JZ    ADDPTAX                                                          
         OI    TRNRSTA2,TRNSBILP   YES - TURN ON RECORD STATUS BIT              
                                                                                
ADDPTAX  L     RE,SAVERE                                                        
         CLI   ELERR,0                                                          
         BR    RE                                                               
         DROP  RF                                                               
         EJECT                                                                  
***********************************************************************         
* SET PAYABLES INDICATORS                                             *         
***********************************************************************         
                                                                                
SETPAY   ST    RE,SAVERE                                                        
                                                                                
         BASR  R1,0                                                             
         AHI   R1,PAYTAB-*                                                      
         USING PAYTABD,R1          R1=A(TABLE OF PAYABLES LEDGERS)              
SETPAY02 CLI   PAYTABD,PAYTEOTQ    TEST END OF TABLE                            
         JE    SETPAYX                                                          
         CLC   PAYTLEDG,TRNKCUNT   MATCH ON CONTRA UNIT/LEDGER                  
         JNE   *+8                                                              
         OI    INDS1,INDPAYCA      SET CONTRA-ACCOUNT IS PAYABLES               
         CLC   PAYTLEDG,TRNKUNT    MATCH ON ACCOUNT UNIT/LEDGER                 
         JNE   SETPAY04                                                         
         OI    INDS1,INDPAYAC      SET ACCOUNT IS PAYABLES                      
         MVC   PAYINDS,PAYTINDS    SAVE INDICATOR BYTE                          
         J     SETPAYX                                                          
SETPAY04 AHI   R1,PAYTABL          BUMP TO NEXT TABLE ENTRY                     
         J     SETPAY02                                                         
                                                                                
SETPAYX  L     RE,SAVERE                                                        
         BR    RE                                                               
         DROP  R1                                                               
         EJECT                                                                  
***********************************************************************         
* ADD MPY ELEMENT TO DRAFT TRANSACTION RECORD IF NECESSARY            *         
***********************************************************************         
                                                                                
ADDMPY   ST    RE,SAVERE                                                        
                                                                                
         MVI   ELERR,0                                                          
         TM    TRNINDS,TRNILIVE    TEST DRAFT TO LIVE                           
         JNZ   ADDMPYX                                                          
         TM    PAYINDS,PAYTIMPY    TEST MANUAL CHEQUE ELEMENT REQUIRED          
         JZ    ADDMPYX                                                          
         TM    TRNSTAT,TRNSDR      TEST CREDIT POSTING                          
         JNZ   ADDMPYX                                                          
                                                                                
         GOTOR VHELLO,ELIST,(C'G',FILETRN),('MPYELQ',TRNRECD),0                 
         CLI   ELERR,0                                                          
         JE    ADDMPYX             FOUND MANUAL PAYMENT ELEMENT                 
         LA    RF,ELEMENT                                                       
         USING MPYELD,RF           BUILD & ADD MANUAL PAYMENT ELEMENT           
         XC    MPYELD(MPYLN4Q),MPYELD                                           
         MVI   MPYEL,MPYELQ                                                     
         MVI   MPYLN,MPYLN4Q                                                    
         MVC   MPYNO,SPACES                                                     
         ZAP   MPYAMNT,PZERO                                                    
         MVC   MPYBNK,SPACES                                                    
         ZAP   MPYPART,PZERO                                                    
         ZAP   MPYPARC,PZERO                                                    
         MVC   MPYCUR,SPACES                                                    
         GOTOR VHELLO,ELIST,(C'P',FILETRN),TRNRECD,MPYELD,0                     
                                                                                
ADDMPYX  L     RE,SAVERE                                                        
         CLI   ELERR,0                                                          
         BR    RE                                                               
         DROP  RF                                                               
                                                                                
***********************************************************************         
* ADD GLDELD ELEM TO DRAFT UNIT S TRASACTIONS                         *         
***********************************************************************         
                                                                                
ADDGLD   ST    RE,SAVERE                                                        
         MVI   TRNGLER#,0          SET TO NO ERROR                              
         CLI   TRNKUNT,C'S'        UNIT S TRANSACTION?                          
         JNE   ADDGLDX             ONLY POST UNIT S TO GL                       
         CLI   TRNKLDG,C'9'        UNIT/LEDGER S9 ?                             
         JE    ADDGLDX             YES, DON'T POST TO GL                        
         CLI   TRNOFFC,C'*'        PURCHASE ORDERS?                             
         JNE   ADDGLD00            YES, SO EXIT                                 
         CLI   TRNOFFC+1,C'*'      PURCHASE ORDERS?                             
         JE    ADDGLDX             YES, SO EXIT                                 
                                                                                
ADDGLD00 DS    0H                                                               
*&&US*&& TM    TRNCPYS8,CPYNEWGL                                                
*&&UK*&& TM    TRNCPYSC,CPYNEWGL                                                
         JZ    ADDGLD40            DON'T PROCESS IF NOT ON NEW GL               
         OC    TRNGLMOA,TRNGLMOA   COMPANY NOT ON NEW GL                        
         JZ    ADDGLD40            NOT SET BUT SHOULD BE SINCE CPYNEWGL         
         CLC   MOS,TRNGLMOA        SEE IF ON NEW GL                             
         JL    ADDGLD40            NO, MOS PRIOR TO NEW GL                      
*        BRAS  RE,ISTTYPE          ONLY DO FOR THESE TYPES                      
*        JE    ADDGLD40            DON'T DO FOR THESE TYPES THEN                
         TM    TRNINDS,TRNILIVE    MAKE DRAFT LIVE                              
         JZ    ADDGLD18            NO, SO START FROM SCRATCH                    
                                                                                
         USING GLDELD,R1                                                        
ADDGLD10 LR    R1,R7               R7 = A(TRNELD)                               
ADDGLD12 CLI   0(R1),EOR           END OF RECORD                                
         JE    ADDGLD18            YES, REMOVE ANY GLDEL'S THEN                 
         CLI   0(R1),GLDELQ        X'63' ELEMENT                                
         JNE   ADDGLD16                                                         
         OC    GLDDATE,GLDDATE                                                  
         JNZ   ADDGLD18            SHOULD NOT HAVE THIS ON DRAFT                
         TM    GLDIND,GLDDRAFT     IS GL SET TO DRAFT?                          
         JO    ADDGLD20            YES, SO UPDATE, DON'T DELETE                 
                                                                                
ADDGLD16 LLC   RF,1(,R1)                                                        
         AR    R1,RF                                                            
         J     ADDGLD12                                                         
         DROP  R1                                                               
                                                                                
*---------------------------------------------------------------------*         
* DELETE ANY GLDELDS AND ADD REAL GLDELD                                        
*---------------------------------------------------------------------*         
ADDGLD18 GOTOR VHELLO,ELIST,(C'D',FILETRN),('GLDELQ',TRNRECD),0,0               
         CLI   ELERR,0                                                          
         JE    ADDGLD20            FOUND GENERAL LEDGER DETAIL ELEMENT          
         CLI   ELERR,6             ELEMENT NOT FOUND                            
         JE    ADDGLD20            THAT'S OKAY                                  
         DC    H'00'               SHOULD NOT HAPPEN                            
                                                                                
         USING GLBLKD,R1                                                        
ADDGLD20 L     R1,TRNGLBLK                                                      
         MVI   GLACTION,GLADDEL    ADD GLDEL                                    
         MVC   @ADTRANS,ABASE                                                   
         TM    TRNINDS,TRNIDRFT    DRAFT MODE ?                                 
         JO    ADDGLD30            NO                                           
         TM    TRNINDS2,TRNIADDG   IF SET THEN UPDATE TRSEL AND GLDEL           
         JZ    ADDGLDX             APPLICATION MUST SET TO BE ON NEW GL         
         MVI   GLACTION,GLUPDTE                                                 
                                                                                
ADDGLD30 GOTOR VGLPOST,DMCB,(8,TRNGLBLK)                                        
         JE    ADDGLDX                                                          
         L     R1,TRNGLBLK                                                      
         MVC   TRNGLER#,ERROR#                                                  
         TM    TRNMODE,TRNMON      ONLINE                                       
         JZ    ADDGLDX             SET ERROR IN RF                              
         LLC   RF,ERROR#                                                        
         DC    H'00'               UNWIND                                       
                                                                                
*---------------------------------------------------------------------*         
* DELETE ANY GLDELDS AND ADD DUMMY GLDELD                                       
*---------------------------------------------------------------------*         
ADDGLD40 GOTOR VHELLO,ELIST,(C'D',FILETRN),('GLDELQ',TRNRECD),0,0               
         CLI   ELERR,0                                                          
         JE    ADDGLD42            FOUND GENERAL LEDGER DETAIL ELEMENT          
         CLI   ELERR,6             ELEMENT NOT FOUND                            
         JE    ADDGLD42            THAT'S OKAY                                  
         DC    H'00'               SHOULD NOT HAPPEN                            
                                                                                
         USING GLDELD,RF                                                        
ADDGLD42 LA    RF,ELEMENT                                                       
         XC    GLDELD(GLDLNQ),GLDELD                                            
         MVI   GLDEL,GLDELQ                                                     
         MVI   GLDLN,GLDLNQ                                                     
         GOTOR VHELLO,ELIST,(C'P',FILETRN),TRNRECD,GLDELD,0                     
         CLI   ELERR,0                                                          
         JE    *+6                                                              
         DC    H'00'                                                            
         DROP  RF                                                               
                                                                                
ADDGLDX  L     RE,SAVERE                                                        
         CLI   TRNGLER#,0                                                       
         BR    RE                                                               
         DROP  R1                                                               
                                                                                
*---------------------------------------------------------------------*         
* PROCESS ALL TYPES EXCEPT FOR 19                                               
*---------------------------------------------------------------------*         
ISTTYPE  CLI   TRNTYPE,19          DON'T DO FOR TYPE 19 OR TYPE 9               
         BR    RE                                                               
                                                                                
***********************************************************************         
* SET DUE DATE IN TRAMNSACTION STATUS IF DUE DATE ELEMENT FOUND       *         
***********************************************************************         
                                                                                
SETDUE   TM    TRNRSTA2,TRNSUSED   TEST TRANSACTION USED                        
         BNZR  RE                  YES - EXIT                                   
         LA    RF,TRNELD           ELSE LOOK FOR DUE DATE ELEMENT               
         USING DUEELD,RF                                                        
         SR    R0,R0                                                            
SETDUE02 IC    R0,DUELN            BUMP TO NEXT TRANSACTION ELEMENT             
         AR    RF,R0                                                            
         CLI   DUEEL,0             TEST EOR                                     
         BER   RE                                                               
         CLI   DUEEL,DUEELQ        TEST DUE DATE ELEMENT                        
         JNE   SETDUE02                                                         
         MVC   TRNRSDUE,DUEDATE-DUEELD(RF)                                      
         BR    RE                                                               
         DROP  RF                                                               
         EJECT                                                                  
***********************************************************************         
* UPDATE MOS PASSIVE IF NECESSARY                                     *         
***********************************************************************         
                                                                                
UPDMOS   CLI   TRNKUNT,C'S'        TEST FINANCIAL UNIT                          
         BNER  RE                                                               
         MVI   MOSIND,MOSUPD       UPDATING MOS PASSIVE                         
         TM    TRNKSTAT,TRNSDRFT                            NMAL                
         JNZ   ADDMOS00                                     NMAL                
         TM    TRNRSTYP,TRNTJBTX   IS IT A JOB TRANSFER     NMAL                
         JZ    ADDMOS00            NO                       NMAL                
         OI    PAY2IND,PAY2L34     SET X'01' FOR PAY2IND    NMAL                
         J     ADDMOS00                                                         
                                                                                
***********************************************************************         
* ADD MOS PASSIVE IF NECESSARY                                        *         
***********************************************************************         
                                                                                
ADDMOS   CLI   TRNKUNT,C'S'        TEST FINANCIAL UNIT                          
         BNER  RE                                                               
         MVI   MOSIND,MOSADD       ADD MOS PASSIVE                              
                                                                                
ADDMOS00 ST    RE,SAVERE                                                        
                                                                                
         GOTOR GETTRN,TRNRECD      ESTABLISH TRANSACTION DISK ADDRESS           
                                                                                
         USING MOSPASD,MOSKEY                                                   
         XC    MOSPKEY,MOSPKEY                                                  
         MVI   MOSPTYP,MOSPTYPQ    BUILD AND ADD MOS PASSIVE POINTER            
         MVC   MOSPCPY,TRNKCPY                                                  
         MVC   MOSPMOS,MOS                                                      
         MVC   MOSPULA,TRNKULA                                                  
         MVC   MOSPOFF,TRNKOFF                                                  
         MVC   MOSPCULC,TRNKCULC                                                
         MVC   MOSPDATE,TRNKDATE                                                
                                                                                
         LR    RF,R7                                                            
ADDMOS10 CLI   0(RF),EOR           END OF RECORD                                
         JE    ADDMOS30                                                         
                                                                                
         USING TRSELD,RF                                                        
         CLI   0(RF),TRSELQ        X'60'                                        
         JNE   ADDMOS20                                                         
         OC    TRSUPDT,TRSUPDT                                                  
         JZ    ADDMOS20                                                         
         TM    TRSSTAT,TRSSGLUP                                                 
         JZ    ADDMOS20                                                         
         MVC   MOSPUPDT,TRSUPDT    SAVE DATE UPDATED TO GL                      
                                                                                
         USING GLDELD,RF                                                        
ADDMOS20 CLI   0(RF),GLDELQ        X'63'                                        
         JNE   ADDMOS25                                    NMAL                 
         MVC   MOSPUPTM,GLDUPTM    SAVE TIME OF DAY                             
                                                                                
         USING SCIELD,RF                                   NMAL                 
ADDMOS25 CLI   0(RF),SCIELQ        X'50'                   NMAL                 
         JNE   ADDMOS28                                    NMAL                 
         CLI   SCITYPE,SCITCPAD    TYPE X'06'              NMAL                 
         JNE   ADDMOS28                                    NMAL                 
         OI    PAY2IND,PAY2CSH     SET X'10' FOR PAY2IND   NMAL                 
                                                                                
ADDMOS28 DS    0H                                          NMAL                 
         LLC   R1,1(,RF)                                                        
         AR    RF,R1                                                            
         J     ADDMOS10                                                         
         DROP  RF                                                               
                                                                                
ADDMOS30 MVC   MOSPDA,TRNDA                                                     
         MVC   MOSPKSTA(MOSPUPDT-MOSPKSTA),TRNIS                                
         MVC   MOSPKDA,TRNDA                                                    
         TM    TRNINDS,TRNIWRNO    TEST WRITE=NO                                
         JO    ADDMOSX             IF ON THEN IT DON'T WRITE IT                 
         CLI   MOSIND,MOSUPD       UPDATING MOS PASSIVE                         
         JNE   ADDMOS80                                                         
         MVC   MOSPKSV,MOSPKEY                                                  
         MVC   MOSPKSVS,MOSPKSTA   SAVE STATUS                                  
         GOTOR READDIR,MOSPASD                                                  
         JE    ADDMOS40                                                         
         MVC   MOSPKEY,MOSPKSV     RESETORE                                     
         MVC   MOSPKSTA,MOSPKSVS                                                
         J     ADDMOS80            ADD IT THEN                                  
                                                                                
ADDMOS40 MVC   MOSPKSTA,MOSPKSVS   RESTORE                                      
         CLC   IS,MOSPKSTA         CHECK FOR CHANGE IN STATUS                   
         JE    ADDMOSX             NONE                                         
         GOTOR WRITEDIR,MOSPASD                                                 
         JE    ADDMOSX                                                          
         DC    H'0'                                                             
                                                                                
ADDMOS80 GOTOR ADDDIR,MOSPASD                                                   
         JE    *+6                                                              
         DC    H'0'                                                             
                                                                                
ADDMOSX  L     RE,SAVERE                                                        
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* ADD GROUP INVOICE PASSIVE POINTER IF NECESSARY                      *         
***********************************************************************         
                                                                                
ADDGIN   ST    RE,SAVERE                                                        
                                                                                
         LA    RF,TRNELD                                                        
         USING GINELD,RF                                                        
         SR    R0,R0                                                            
ADDGIN02 IC    R0,GINLN            LOCATE GROUP INVOICE# ELEMENT                
         AR    RF,R0                                                            
         CLI   GINEL,0                                                          
         JE    ADDGINX                                                          
         CLI   GINEL,GINELQ                                                     
         JNE   ADDGIN02                                                         
                                                                                
         USING GINPASD,GINKEY                                                   
         XC    GINPKEY,GINPKEY                                                  
         MVI   GINPTYP,GINPTYPQ                                                 
         MVC   GINPCPY,TRNKCPY                                                  
         MVC   GINPINV,GININV                                                   
         CLI   GINLN,GINLN2Q                                                    
         JL    *+16                                                             
         MVC   GINPISN,GINISN                                                   
         MVC   GINPPTYP,GINTYP                                                  
         MVC   GINPULA,TRNKUNT                                                  
         MVC   GINPWORK,TRNANAL                                                 
                                                                                
         GOTOR GETTRN,TRNRECD                                                   
                                                                                
         MVC   GINPKSTA,TRNIS                                                   
         MVC   GINPDA,TRNDA                                                     
         MVC   GINPKDA,TRNDA                                                    
         GOTOR READDIR,GINPASD                                                  
         JNE   *+6                                                              
         DC    H'0'                                                             
         MVC   GINKEY,KEYSAVE      RESTORE DIRECTORY RECORD                     
         TM    IOERR,IOERNF        TEST RECORD NOT FOUND                        
         JNZ   ADDGIN04                                                         
         TM    IOERR,IOEDEL        TEST FOUND AND DELETED                       
         JNZ   *+6                                                              
         DC    H'0'                                                             
         GOTOR WRITEDIR,GINPASD                                                 
         JE    ADDGINX                                                          
         DC    H'0'                                                             
                                                                                
ADDGIN04 GOTOR ADDDIR,GINPASD                                                   
         JE    *+6                                                              
         DC    H'0'                                                             
         ICM   R1,15,TRNGINA       INCREMENT GIN PASSIVES ADDED                 
         AHI   R1,1                                                             
         STCM  R1,15,TRNGINA                                                    
                                                                                
ADDGINX  L     RE,SAVERE                                                        
         BR    RE                                                               
         DROP  RF                                                               
         EJECT                                                                  
***********************************************************************         
* ADD INVOICE# PASSIVE POINTER IF NECESSARY                           *         
***********************************************************************         
                                                                                
ADDINV   TM    PAYINDS,PAYTINVP    TEST ADDING INVOICE# PASSIVES                
         BZR   RE                                                               
         ST    RE,SAVERE                                                        
                                                                                
         MVC   WORK(L'INVPINV),SPACES                                           
         MVC   WORK(L'TRNKREF),TRNKREF                                          
                                                                                
         LA    RF,TRNELD           LOCATE INVOICE# FFTEL                        
         SR    R0,R0                                                            
         USING FFTELD,RF                                                        
ADDINV02 IC    R0,FFTLN                                                         
         AR    RF,R0                                                            
         CLI   FFTEL,0                                                          
         JE    ADDINV04                                                         
         CLI   FFTEL,FFTELQ                                                     
         JNE   ADDINV02                                                         
         CLI   FFTTYPE,FFTTINVN                                                 
         JNE   ADDINV02                                                         
         MVC   WORK(L'INVPINV),SPACES                                           
         SR    R1,R1                                                            
         IC    R1,FFTLN                                                         
         SHI   R1,FFTLN1Q+2                                                     
         CHI   R1,L'INVPINV-1                                                   
         JNH   *+8                                                              
         LHI   R1,L'INVPINV-1                                                   
         BASR  RE,0                                                             
         EX    R1,8(RE)                                                         
         J     ADDINV04                                                         
         MVC   WORK(0),FFTDATA                                                  
                                                                                
ADDINV04 GOTOR GETTRN,TRNRECD                                                   
                                                                                
         USING INVPASD,INVKEY                                                   
         XC    INVPKEY,INVPKEY                                                  
         MVI   INVPTYP,INVPTYPQ                                                 
         MVC   INVPCPY,TRNKCPY                                                  
         MVC   INVPLDG,TRNKLDG                                                  
         MVC   INVPACT,TRNKACT                                                  
         MVC   INVPINV,WORK                                                     
         MVC   INVPDAT,TRNKDATE                                                 
         MVC   INVPDA,TRNDA                                                     
         XC    INVPKSTA,INVPKSTA                                                
         MVC   INVPKDA,TRNDA                                                    
                                                                                
         GOTOR ADDDIR,INVPASD                                                   
         JE    *+6                                                              
         DC    H'0'                                                             
         ICM   R1,15,TRNINVA       INCREMENT INVOICE# PASSIVES ADDED            
         AHI   R1,1                                                             
         STCM  R1,15,TRNINVA                                                    
                                                                                
ADDINVX  L     RE,SAVERE                                                        
         BR    RE                                                               
         DROP  RF                                                               
         EJECT                                                                  
***********************************************************************         
* ADD REFERENCE NUMBER SEARCH POINTER IF NECESSARY                    *         
* ADD BRANDOCEAN INVOICE LOG NUMBER SEARCH POINTER IF NECESSARY       *         
***********************************************************************         
*                                                                               
ADDRNS   CLI   TRNKUNT,C'S'        ONLY FOR UNIT S                              
         BNER  RE                                                               
         ST    RE,SAVERE                                                        
                                                                                
         MVC   WORK(6),TRNKREF     PRESET FOR REAL REFERENCE NUMBER             
         MVC   WORK+6(13),SPACES   INIT FOR BRANDOCEAN INVOICES LOG             
         MVI   BYTE1,C'N'                                                       
         LA    RF,TRNELD                                                        
         XR    R0,R0                                                            
         USING TRSELD,RF                                                        
ADDRNS00 IC    R0,TRSLN                                                         
         AR    RF,R0                                                            
         CLI   TRSEL,0                                                          
         JE    ADDRNS02                                                         
         CLI   TRSEL,TRSELQ                                                     
         JNE   ADDRNS00                                                         
         TM    TRSSTAT5,TRSSINVQ                                                
         JZ    *+8                                                              
         MVI   BYTE1,C'Y'          SET BRANDOCEAN INVOICES POSTING              
                                                                                
ADDRNS02 LA    RF,TRNELD                                                        
         XR    R0,R0                                                            
         USING FFTELD,RF                                                        
ADDRNS04 IC    R0,FFTLN                                                         
         AR    RF,R0                                                            
         CLI   FFTEL,0                                                          
         JE    ADDRNS14                                                         
         CLI   BYTE1,C'Y'          TEST BRANDOCEAN INVOICE POSTING              
         JNE   ADDRNS06                                                         
         CLI   FFTEL,GPXELQ                                                     
         JNE   ADDRNS06                                                         
         CLI   GPXTYP-GPXELD(RF),GPXNVLQ                                        
         JNE   ADDRNS04                                                         
         MVC   WORK+6(7),GPXDATA-GPXELD(RF) EXTRACT INVOICE LOG NUMBER          
         J     ADDRNS04                                                         
ADDRNS06 CLI   FFTEL,FFTELQ                                                     
         JNE   ADDRNS08                                                         
         CLI   FFTTYPE,FFTTKREF                                                 
         JNE   ADDRNS04                                                         
         MVC   WORK(6),FFTDATA                                                  
         J     ADDRNS04                                                         
                                                                                
         USING FFNELD,RF                                                        
ADDRNS08 CLI   FFNEL,FFNELQ                                                     
         JNE   ADDRNS04                                                         
         CLC   FFNONUM,AC@NOORD                                                 
         JE    ADDRNS04                                                         
         MVC   WORK+13(6),FFNONUM                                               
         J     ADDRNS04                                                         
         DROP  RF                                                               
                                                                                
ADDRNS14 GOTOR GETTRN,TRNRECD                                                   
                                                                                
         USING RNSPASD,RNSKEY      BUILD KEY OF PASSIVE POINTER                 
ADDRNS16 XC    RNSPKEY,RNSPKEY                                                  
         MVI   RNSPTYP,RNSPTYPQ                                                 
         MVI   RNSPSUB,RNSPSUBQ                                                 
         MVC   RNSPCPY,TRNKCPY                                                  
         MVC   RNSPBTY,TRNTYPE                                                  
*        CLC   =C'**',TRNOFFC                                                   
         CLC   CHKOFC,TRNOFFC                                                   
         JNE   *+8                                                              
         CLI   TRNTYPE,12          EXCLUDE ORDER TRANSACTIONS                   
         JNE   *+10                                                             
         MVC   WORK+13(6),TRNKREF                                               
         CLC   WORK(6),SPACES      TEST REFERENCE PASSIVE ALREADY DONE          
         JNH   ADDRNS18                                                         
         MVI   RNSPIND,RNSPRFQ                                                  
         MVC   RNSPREF,WORK                                                     
         MVC   WORK(6),SPACES      CLEAR                                        
         J     ADDRNS22                                                         
ADDRNS18 CLC   WORK+6(7),SPACES    TEST INVOICE LOG NUMBER FOUND                
         JNH   ADDRNS20                                                         
         MVI   RNSPIND,RNSPINQ                                                  
         MVC   RNSPINT,WORK+6                                                   
         MVC   WORK+6(7),SPACES    CLEAR                                        
         J     ADDRNS22                                                         
ADDRNS20 CLC   WORK+13(6),SPACES   TEST ORDER NUMBER FOUND                      
         JNH   ADDRNSX                                                          
         MVI   RNSPIND,RNSPORQ                                                  
         MVC   RNSPREF,WORK+13                                                  
         MVC   WORK+13(6),SPACES   CLEAR                                        
ADDRNS22 MVI   RNSPFIL,RNSPMSQ                                                  
         TM    TRNIS,TRNSARCH      IS RECORD ARCHIVED?                          
         JZ    *+8                                                              
         MVI   RNSPFIL,RNSPARQ                                                  
         MVC   RNSPDA,TRNDA                                                     
         MVC   RNSPSTA,TRNIS                                                    
         MVC   RNSPKDA,TRNDA                                                    
                                                                                
         GOTOR ADDDIR,RNSPASD      CARRY ON EVEN IF IN ERROR                    
                                                                                
         CLC   WORK+6(7),SPACES    TEST INVOICE LOG NUMBR PASSIVE TO DO         
         JH    ADDRNS16                                                         
         CLC   WORK+13(6),SPACES   TEST ORDER NUMBER PASSIVE TO DO              
         JH    ADDRNS16                                                         
                                                                                
ADDRNSX  L     RE,SAVERE                                                        
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* ADD BILL/DEBTOR PASSIVE POINTER IF NECESSARY                        *         
***********************************************************************         
                                                                                
ADDBDP   CLC   DEBTUL,TRNKUNT      TEST DEBTORS POSTING                         
         BNER  RE                                                               
         TM    TRNSTAT,TRNSDR                                                   
         JZ    *+10                                                             
         CLI   TRNTYPE,TRNTCLBL    TEST CLIENT BILLING (DEBITS ONLY)            
         BNER  RE                                                               
                                                                                
         ST    RE,SAVERE                                                        
         LA    RF,TRNELD                                                        
         SR    R0,R0                                                            
         USING SORELD,RF                                                        
ADDBDP02 IC    R0,SORLN            LOCATE BILLING SOURCE ELEMENT                
         AR    RF,R0                                                            
         CLI   SOREL,0                                                          
         JE    ADDBDPX                                                          
         CLI   SOREL,SORELQ                                                     
         JNE   ADDBDP02                                                         
         CLI   SORSYS,SORSACC                                                   
         JNE   ADDBDP02                                                         
                                                                                
         USING BDPPASD,BDPKEY                                                   
         XC    BDPPKEY,BDPPKEY                                                  
         MVI   BDPPTYP,BDPPTYPQ                                                 
         MVC   BDPPCPY,TRNKCPY                                                  
         MVI   BDPPSYS,BDPPACCQ                                                 
         MVC   BDPPOFF,TRNOFFC                                                  
         MVC   BDPPJOB,SORAACT                                                  
         MVC   BDPPBILM,MOS                                                     
         MVC   BDPPBIL#,TRNKREF                                                 
         DROP  RF                                                               
                                                                                
         GOTOR VDATCON,PARM,(1,TRNKDATE),(2,BDPPBILD)                           
         MVI   BDPPRTYP,BDPPRDDR                                                
         TM    TRNSTAT,TRNSDR                                                   
         JNZ   *+8                                                              
         MVI   BDPPRTYP,BDPPRDCR                                                
                                                                                
         GOTOR GETTRN,TRNRECD                                                   
                                                                                
         MVC   BDPPDA,TRNDA                                                     
         MVC   BDPPKSTA,TRNIS                                                   
         MVC   BDPPKDA,TRNDA                                                    
         GOTOR ADDDIR,BDPPASD                                                   
         JE    *+6                                                              
         DC    H'0'                                                             
         ICM   R1,15,TRNBDPA       INCREMENT BDP PASSIVES ADDED                 
         AHI   R1,1                                                             
         STCM  R1,15,TRNBDPA                                                    
                                                                                
ADDBDPX  L     RE,SAVERE                                                        
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* ADD VENDOR AUTO APPROVE PASSIVE IF NECESSARY                        *         
***********************************************************************         
                                                                                
ADDAAV   NTR1  BASE=*,LABEL=*                                                   
         TM    PAYINDS,PAYAAPAS    TEST ADDING PASSIVE FOR THIS PAYABLE         
         BZ    ADDAAVX             LEDGER                                       
         TM    TRNSTAT,TRNSDR      WANT CREDITS ONLY                            
         BO    ADDAAVX                                                          
*                                                                               
         XC    SVSYS,SVSYS                                                      
         LA    RF,LDSYTAB          TABLE OF LEDGER/SYSTEM                       
ADDAAV02 CLI   0(RF),X'FF'                                                      
         BE    ADDAAVX                                                          
         CLC   TRNKLDG,0(RF)                                                    
         BE    ADDAAV04                                                         
         LA    RF,2(RF)                                                         
         B     ADDAAV02                                                         
*                                                                               
ADDAAV04 MVC   SVSYS,1(RF)         SAVE SYSTEM                                  
         CLI   SVSYS,C'S'          IF IT'S SPOT CHECK TO SEE IF IT'S            
         BNE   *+16                REALLY NET BY CHECKING THE 1ST CHAR          
         CLI   TRNKACT,C'N'        OF THE ACCOUNT FOR 'N' (SSN)                 
         BNE   *+8                                                              
         MVI   SVSYS,C'N'                                                       
         NI    FLAG1,X'FF'-MOSF                                                 
         BRAS  RE,CHK60            SAVE USED DATE                               
         BRAS  RE,RDE5             READ E5 ELEM FOR MOS                         
         BE    ADDAAV10                                                         
         BRAS  RE,RDF3             READ F3 FOR MOS                              
         BE    ADDAAV10            FOUND IT HERE                                
         BRAS  RE,RD23             READ 23 FOR MOS                              
ADDAAV10 BRAS  RE,RD46             READ 46 ELEM FOR EST                         
         BE    ADDAAV15                                                         
         BRAS  RE,RD1A             LOOK FOR ESTIMATE HERE                       
*                                                                               
ADDAAV15 GOTOR GETTRN,TRNRECD      ESTABLISH TRANSACTION DISK ADDRESS           
         USING AAVPASD,AAVKEY                                                   
         MVC   AAVPKEY,SPACES                                                   
         MVI   AAVPTYP,AAVPTYPQ    X'24'                                        
         MVI   AAVPSUB,AAVPSUBQ    X'01'                                        
         MVC   AAVPCPY,TRNKCPY                                                  
         MVC   AAVPCLT,TRNKCACT+9  LAST 3 CHAR OF CONTRA IS CLIENT              
         MVC   AAVPPRD,TRNKREF     1ST 3 CHAR OF REFERENCE IS PRODUCT           
         MVC   AAVPEST,SVEST       ESTIMATE                                     
         MVC   AAVPMOS,SVMOS       MOS                                          
         MVC   AAVPSYS,SVSYS       SYSTEM                                       
         MVC   AAVPOFF,TRNOFFC     OFFICE                                       
         MVC   AAVPACCT,TRNKLDG    STATION (SS ACCOUNT)                         
         BRAS  RE,CHKEST           CHECK ESTIMATE                               
         MVI   AAVPFLG1,0          DEFAULT TO A GOOD ESTIMATE                   
         CLI   ESTIND,C'Z'         IS THIS A ZERO ESTIMATE                      
         BNE   *+12                NO                                           
         OI    AAVPFLG1,AAVPNEST   YES                                          
         B     ADDAAV20                                                         
         CLI   ESTIND,C'B'         IS THIS A BAD (NON-NUMERIC) EST?             
         BNE   ADDAAV20            NO                                           
         OI    AAVPFLG1,AAVPBEST   YES                                          
ADDAAV20 MVC   AAVPKDA,TRNDA       SAVE DISK ADDRESS AS PART OF KEY             
         XC    AAVPSTA,AAVPSTA                                                  
         TM    TRNSTAT,TRNSAPPR    APPROVED?                                    
         BZ    *+8                                                              
         OI    AAVPSTAT,AAVPAPP    SET APPROVED IN STATUS                       
         TM    TRNSTAT,TRNSHOLD    ON HOLD?                                     
         BZ    *+8                                                              
         OI    AAVPSTAT,AAVPHLD    SET HELD IN STATUS                           
         MVC   AAVPUSED,SVUSED     USED DATE                                    
         MVC   AAVPDA,TRNDA                                                     
         GOTOR ADDDIR,AAVPASD                                                   
         JE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
ADDAAVX  J     EXIT                                                             
*                                                                               
*                                  TABLE OF LEDGER AND SYSTEM                   
LDSYTAB  DC    C'SS'               LEDGER S - SYS SPOT (BUT MAYBE NET)          
         DC    C'TS'               LEDGER T - SYS SPOT                          
         DC    C'PP'               LEDGER P - SYS PRINT                         
         DC    C'QP'               LEDGER Q - SYS PRINT                         
         DC    C'UN'               LEDGER U - SYS NET                           
         DC    X'FF'                                                            
         SPACE 2                                                                
***********************************************************************         
* ADD RECEVIABLE AUTO APPROVE PASSIVE IF NECESSARY                    *         
***********************************************************************         
                                                                                
ADDAAR   NTR1  BASE=*,LABEL=*                                                   
         CLC   TRNKUNT(L'TRNKUNT+L'TRNKLDG),=C'SR'                              
         BE    *+14                                                             
         CLC   TRNKUNT(L'TRNKUNT+L'TRNKLDG),=C'SB'                              
         BNE   ADDAARX                                                          
         CLI   TRNTYPE,TRNTMABL    IGNORE TYPE 6                                
         BE    ADDAARX                                                          
         TM    TRNSTAT,TRNSDR      WANT DEBITS ONLY                             
         JZ    ADDAARX                                                          
*                                                                               
         XC    SVSYS,SVSYS                                                      
         BRAS  RE,RD1A             READ 1A ELEM FOR CLT,PRD,EST AND SYS         
         BNE   ADDAAR05                                                         
         TM    FLAG1,NOPTR         NO POINTER NEEDED?                           
         BO    ADDAARX                                                          
         B     ADDAAR10                                                         
*                                                                               
ADDAAR05 BRAS  RE,RDF3                                                          
*                                                                               
ADDAAR10 CLC   SVEST,SPACES        ANY ESTIMATE?                                
         BE    ADDAARX             NO SO SKIP                                   
         GOTOR GETTRN,TRNRECD      ESTABLISH TRANSACTION DISK ADDRESS           
         USING AARPASD,AARKEY                                                   
         MVC   AARPKEY,SPACES                                                   
         MVI   AARPTYP,AARPTYPQ    X'24'                                        
         MVI   AARPSUB,AARPSUBQ    X'02'                                        
         MVC   AARPCPY,TRNKCPY                                                  
         MVI   AARPIND,AARPIEST    ESTIMATE INDICATOR                           
         MVC   AARPCLT,SVCLI       CLIENT                                       
         MVC   AARPPRD,SVPRD       PRODUCT                                      
         MVC   AARPEST,SVEST       ESTIMATE                                     
         MVC   AARPSYS,SVSYS       SYSTEM                                       
         MVC   AARPOFF,TRNOFFC     OFFICE                                       
         MVC   AARPACCT,TRNKLDG    RECEIVABLE (SR OR SB ACCOUNT)                
         MVC   AARPKDA,TRNDA       SAVE DISK ADDRESS AS PART OF KEY             
         XC    AARPSTA,AARPSTA                                                  
         MVC   AARPDA,TRNDA                                                     
ADDAAR20 GOTOR ADDDIR,AARPASD                                                   
         JE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
ADDAARX  J     EXIT                                                             
*                                                                               
***********************************************************************         
* CHECK ESTIMATE TO SEE IF IT'S NUMERIC                               *         
* EXIT - ESTIND CONTAINS 'Z' FOR ZERO ESTIMATE, 'B' FOR BAD ESTIMATE  *         
***********************************************************************         
         SPACE 1                                                                
CHKEST   NTR1  BASE=*,LABEL=*                                                   
         XC    ESTIND,ESTIND                                                    
         CLC   SVEST,SPACES                                                     
         BNE   *+12                                                             
         MVI   ESTIND,C'Z'                                                      
         B     CESTX                                                            
         CLC   SVEST+3(3),SPACES                                                
         BNE   CEST20                                                           
*                                                                               
         LA    RF,SVEST                                                         
         LHI   RE,3                                                             
CEST10   CLI   0(RF),C'0'                                                       
         BL    CEST20                                                           
         CLI   0(RF),C'9'                                                       
         BH    CEST20                                                           
         BCT   RE,CEST10                                                        
         B     CESTX                                                            
*                                                                               
CEST20   MVI   ESTIND,C'B'                                                      
CESTX    XIT1                                                                   
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* READ 60 ELEMENT FOR USED DATE                                                 
***********************************************************************         
         SPACE 1                                                                
         USING TRSELD,RF                                                        
CHK60    NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         XC    SVUSED,SVUSED                                                    
         LA    RF,TRNELD                                                        
         SR    R0,R0                                                            
CHK6010  IC    R0,TRSLN                                                         
         AR    RF,R0                                                            
         CLI   TRSEL,0                                                          
         BE    CHK60X                                                           
         CLI   TRSEL,TRSELQ        60 ELEMENT                                   
         BNE   CHK6010                                                          
         MVC   SVUSED,TRSUDAT      SAVE USED DATE                               
*                                                                               
CHK60X   J     EXIT                                                             
         DROP  RF                                                               
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* READ 23 ELEMENT FOR MOS FOR VENDOR PAYABLES                                   
***********************************************************************         
         SPACE 1                                                                
         USING OTHELD,RF                                                        
RD23     NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         XC    SVMOS,SVMOS                                                      
         LA    RF,TRNELD                                                        
         SR    R0,R0                                                            
RD2310   IC    R0,OTHLN                                                         
         AR    RF,R0                                                            
         CLI   OTHEL,0                                                          
         BE    RD23L                                                            
         CLI   OTHEL,OTHELQ        23 ELEMENT                                   
         BNE   RD2310                                                           
*                                                                               
RD2320   CLI   OTHPROF-3,C' '      IF SOMETHING IN THIS FIELD THAN NO           
         BH    RD23L               MOS IN THIS ELEMENT                          
         MVC   SVMOS,OTHDATE                                                    
         OI    FLAG1,MOSF                                                       
                                                                                
RD23E    J     EXITY                                                            
RD23L    J     EXITN                                                            
         DROP  RF                                                               
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* READ E5 ELEMENT FOR MOS FOR VENDOR PAYABLES                                   
***********************************************************************         
         SPACE 1                                                                
         USING GDAELD,RF                                                        
RDE5     NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         XC    SVMOS,SVMOS                                                      
         LA    RF,TRNELD                                                        
         SR    R0,R0                                                            
RDE510   IC    R0,GDALN                                                         
         AR    RF,R0                                                            
         CLI   GDAEL,0                                                          
         BE    RDE5L                                                            
         CLI   GDAEL,GDAELQ        E5 ELEMENT                                   
         BNE   RDE510                                                           
         CLI   GDATYPE,GDAMMOS     TYPE MUST BE MEDIA MOS                       
         BNE   RDE510                                                           
*                                                                               
RDE520   MVC   SVMOS,GDAYYMM                                                    
         OI    FLAG1,MOSF                                                       
                                                                                
RDE5E    J     EXITY                                                            
RDE5L    J     EXITN                                                            
         DROP  RF                                                               
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* READ 46 ELEMENT FOR EST FOR VENDOR PAYABLES                                   
***********************************************************************         
         SPACE 1                                                                
         USING XPYELD,R3                                                        
RD46     NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         MVC   SVEST,SPACES                                                     
         LA    R3,TRNELD                                                        
         SR    R0,R0                                                            
RD4610   IC    R0,XPYLN                                                         
         AR    R3,R0                                                            
         CLI   XPYEL,0                                                          
         BE    RD46L                                                            
         CLI   XPYEL,XPYELQ        46 ELEMENT                                   
         BNE   RD4610                                                           
*                                                                               
RD4620   TM    FLAG1,MOSF          DID WE FIND THE MOS ALREADY?                 
         BO    RD4640              YES                                          
         CLI   TRNKCULA+3,C'N'       IF NETWORK                                 
         BE    *+8                                                              
         OI    FLAG1,FLGBRD          GET DATE FROM BROADCAST CAL                
         XC    WORK,WORK                                                        
         MVC   WORK(L'XPYPER),XPYPER XXXXXX-XXXXXX                              
         LA    R2,WORK                                                          
         CLI   WORK+6,C' '                                                      
         BNH   *+8                                                              
         LA    R2,WORK+6                                                        
         CLI   WORK+26,C'-'                                                     
         BNE   *+8                                                              
         LA    R2,WORK+7                                                        
         TM    FLAG1,FLGBRD                                                     
         BNO   RD4630                                                           
         GOTOR VGETBRD,DMCB,(1,(R2)),WORK+15,VGETDAY,VADDAY                     
         LA    R2,WORK+21           START DTE(6 BYTES) END DTE(6 BYTES)         
RD4630   GOTO1 VDATCON,DMCB,(0,(R2)),(1,WORK+30)                                
         MVC   SVMOS,WORK+30        MOVE IN YYMM                                
*                                                                               
RD4640   CLC   XPYEST,SPACES       ANY ESTIMATE?                                
         BE    RD46E                                                            
         OC    XPYEST,XPYEST                                                    
         BZ    RD46E               NO                                           
         LH    RE,XPYEST                                                        
         CVD   RE,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  SVEST(3),DUB           UNPACK THE ESTIMATE                       
*                                                                               
RD46E    J     EXITY                                                            
RD46L    J     EXITN                                                            
         DROP  R3                                                               
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* READ 1A/6A ELEMENT LOOKING FOR MOS, EST, CLT, PRD AND SYS FOR       *         
* RECEIVABLE PASSIVE POINTER                                                    
***********************************************************************         
         SPACE 1                                                                
         USING MDTELD,RF                                                        
RD1A     NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         NI    FLAG1,X'FF'-NOPTR                                                
         XC    SVMOS,SVMOS                                                      
         MVC   SVEST,SPACES                                                     
         XC    SVCLI,SVCLI                                                      
         XC    SVPRD,SVPRD                                                      
         LA    RF,TRNELD                                                        
         SR    R0,R0                                                            
RD1A10   IC    R0,MDTLN                                                         
         AR    RF,R0                                                            
         CLI   MDTEL,0                                                          
         BE    RD1AL                                                            
         CLI   MDTEL,MDTELQ        1A ELEMENT                                   
         BE    RD1A20                                                           
         CLI   MDTEL,MDPELQ        6A ELEMENT                                   
         BNE   RD1A10                                                           
*                                                                               
RD1A20   CLI   MDTSYS,C'J'         IS SYSTEM PRODUCTION?                        
         BNE   *+12                YES NO POINTER NEEDED                        
         OI    FLAG1,NOPTR                                                      
         B     RD1AE                                                            
*                                                                               
         OC    SVSYS,SVSYS         DID WE ALREADY FIGURE OUT THE                
         BNZ   RD1A30              SYSTEM? (FOR PAYABLES)                       
         MVC   SVSYS,MDTSYS        SYSTEM                                       
         LA    R1,PSYSTAB          TABLE OF EQUIVALENT SYSTEMS-PRINT            
RD1A22   CLI   0(R1),X'FF'                                                      
         BE    RD1A24                                                           
         CLC   MDTSYS,0(R1)        IF MATCH ON SYSTEM THAN THE ACTUAL           
         BNE   *+12                SYSTEM MUST BE PRINT                         
         MVI   SVSYS,C'P'                                                       
         B     RD1A30                                                           
         LA    R1,1(R1)                                                         
         B     RD1A22                                                           
RD1A24   CLI   SVSYS,C'P'          IF PRINT DON'T NEED TO CHK SUB-MEDIA         
         BE    RD1A30                                                           
*                                                                               
         LA    R1,SMEDTAB          TABLE OF SUB-MEDIA'S FOR NET                 
RD1A25   CLI   0(R1),X'FF'                                                      
         BE    RD1A30                                                           
         CLC   MDTMED,0(R1)        IF MATCH ON SUB-MED THAN THE                 
         BNE   *+12                MUST BE NET                                  
         MVI   SVSYS,C'N'                                                       
         B     RD1A30                                                           
         LA    R1,1(R1)                                                         
         B     RD1A25                                                           
*                                                                               
RD1A30   MVC   SVMOS,MDTMOS        MOVE IN MONTH OF SERVICE                     
         MVC   SVCLI,MDTCLI        CLIENT                                       
         MVC   SVPRD,MDTPRD        PRODUCT                                      
         MVC   SVEST,MDTEST        ESTIMATE                                     
*                                                                               
RD1AE    J     EXITY                                                            
RD1AL    J     EXITN                                                            
         DROP  RF                                                               
         SPACE 2                                                                
SMEDTAB  DC    C'NCDSOVH'          TABLE OF SUB-MEDIA'S FOR NET                 
         DC    X'FF'                                                            
PSYSTAB  DC    C'I'                 INTERACTIVE                                 
         DC    C'L'                 SOCIAL                                      
         DC    C'B'                 MOBILE                                      
         DC    C'V'                 NATIONAL VIDEO                              
         DC    C'W'                 LOCAL VIDEO                                 
         DC    C'D'                 DIGITAL PRINT SYSTEM MEDIA                  
         DC    X'FF'                                                            
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* READ F3 ELEMENT LOOKING FOR MOS, EST, CLT, PRD AND SYS FOR          *         
* RECEIVABLE PASSIVE POINTER                                                    
***********************************************************************         
         SPACE 1                                                                
         USING MBIELD,RF                                                        
RDF3     NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         XC    SVMOS,SVMOS                                                      
         MVC   SVEST,SPACES                                                     
         XC    SVCLI,SVCLI                                                      
         XC    SVPRD,SVPRD                                                      
         LA    RF,TRNELD                                                        
         SR    R0,R0                                                            
RDF310   IC    R0,MBILN                                                         
         AR    RF,R0                                                            
         CLI   MBIEL,0                                                          
         BE    RDF3L                                                            
         CLI   MBIEL,MBIELQ        F3 ELEMENT                                   
         BNE   RDF310                                                           
*                                                                               
RDF320   MVC   SVMOS,MBIMOS                                                     
         MVC   SVCLI,MBICLI                                                     
         MVC   SVPRD,MBIPRD                                                     
         MVC   SVEST,MBIEST                                                     
         OI    FLAG1,MOSF                                                       
*                                                                               
RDF3E    J     EXITY                                                            
RDF3L    J     EXITN                                                            
         DROP  RF                                                               
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*91 : GETSER & ADDSER IS PREVIOUSLY GETTING EXECUTED FOR UK ONLY, NOW *         
*     GETSER & ADDSER GET EXECUTED FOR US & UK BOTH.                  *         
***********************************************************************         
* ESTABLISH SERIAL# IF NECESSARY                                      *         
***********************************************************************         
                                                                                
GETSER   ST    RE,SAVERE                                                        
                                                                                
         MVI   ELERR,0                                                          
         TM    TRNCPYS9,CPYSSRNM   DOES THIS COMPANY USE SERIAL#                
         JZ    GETSERX             NO                                           
         USING TRSPASD,KEY                                                      
         XC    TRSPKEY,TRSPKEY                                                  
         MVI   TRSPTYP,TRSPTYPQ                                                 
         MVI   TRSPSUB,TRSPSUBQ                                                 
         MVC   TRSPCPY,TRNKCPY                                                  
         MVC   TRSKEY,KEY                                                       
         GOTOR RDHDIRUP,TRSPASD                                                 
         CLI   TRSPTYP,TRSPTYPQ                                                 
         JNE   GETSERX                                                          
         CLI   TRSPSUB,TRSPSUBQ                                                 
         JNE   GETSERX                                                          
         CLC   TRSPCPY,TRNKCPY                                                  
         JNE   GETSERX                                                          
         ICM   RE,15,TRSPSER       GET 2S COMPLIMENT SERIAL#                    
         LPR   RE,RE               WORK OUT SERIAL NUMBER                       
         AHI   RE,1                ADD ONE TO GET NEXT SERIAL#                  
         STCM  RE,15,SSERNUM       SAVE NEXT SERIAL#                            
         XC    STRSKEY,STRSKEY     CLEAR SAVED PASSIVE KEY                      
         MVC   STRSKEY,TRSPASD     SAVE THE KEY WE READ FOR UNLOCK              
                                                                                
         LA    RF,TRNELD           RF=A(FIRST ELEMENT ON TRANSACTION)           
         USING SERELD,RF                                                        
         SR    R0,R0               LOCATE SERIAL# ELEMENT                       
GETSER02 IC    R0,SERLN            BUMP TO NEXT ELEMENT                         
         AR    RF,R0                                                            
         CLI   SEREL,0             TEST E-O-R                                   
         JE    GETSER04                                                         
         CLI   SEREL,SERELQ                                                     
         JNE   GETSER02                                                         
         MVI   SEREL,X'FF'                                                      
         GOTOR VHELLO,ELIST,(C'D',FILETRN),(X'FF',TRNRECD),0,0                  
         CLI   ELERR,0                                                          
         JNE   GETSERX                                                          
                                                                                
GETSER04 LA    RF,ELEMENT          BUILD SERIAL# ELEMENT                        
         MVI   SEREL,SERELQ                                                     
         MVI   SERLN,SERLNQ                                                     
         ICM   RE,15,SSERNUM       GET SAVED SERIAL#                            
         MVC   SERNM,SSERNUM       USE NEXT SERIAL#                             
         LNR   RE,RE               DO 2S COMPLIMENT FOR NEW PASSIVE             
         STCM  RE,15,TRSKEY+(TRSPSER-TRSPKEY)                                   
         GOTOR VHELLO,ELIST,(C'P',FILETRN),TRNRECD,SERELD                       
                                                                                
GETSERX  L     RE,SAVERE                                                        
         CLI   ELERR,0                                                          
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* ADD SERIAL# PASSIVE IF NECESSARY                                    *         
***********************************************************************         
                                                                                
ADDSER   TM    TRNCPYS9,CPYSSRNM   DOES COMPANY USE SERIAL#                     
         BZR   RE                  NO                                           
         ST    RE,SAVERE                                                        
                                                                                
         GOTOR GETTRN,TRNRECD                                                   
                                                                                
         USING TRSPKEY,TRSKEY      ADD NEW SERIAL# PASSIVE POINTER              
         MVC   TRSPSTA,TRNIS                                                    
         MVC   TRSPDA,TRNDA                                                     
         GOTOR ADDDIR,TRSPASD                                                   
         JE    *+6                                                              
         DC    H'0'                                                             
         GOTOR UNLOKDIR,STRSKEY    UNLOCK THE SERIAL KEY                        
         L     RE,SAVERE                                                        
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO ESTABLISH MONTH-OF-SERVICE FROM A TRANSACTION            *         
***********************************************************************         
                                                                                
GETMOS   LR    R0,RE                                                            
*&&UK                                                                           
         TM    INDS1,IND2FDUM      TEST DUMMY 2F POSTING                        
         JZ    *+14                                                             
         MVC   WORK(L'MOS),TRNMOS                                               
         J     GETMOSX                                                          
         MVC   WORK(L'MOS),TRNDATE                                              
         CLI   TRNKUNT,C'4'        TEST ASSET UNIT                              
         JE    GETMOSX                                                          
*&&                                                                             
*&&US                                                                           
         MVC   WORK(L'MOS),TRNDATE                                              
         CLI   TRNKUNT,C'1'        FOR UNITS 1 AND 2                            
         JL    GETMOS02                                                         
         CLI   TRNKUNT,C'2'                                                     
         JH    GETMOS02                                                         
         CLI   TRNTYPE,TRNTTRAN    FOR SOME BATCH TRANSFER TYPES                
         JE    GETMOSX                                                          
         CLI   TRNTYPE,TRNTMJRN                                                 
         JE    GETMOSX                                                          
         CLI   TRNTYPE,TRNTSPTX                                                 
         JL    GETMOS02                                                         
         CLI   TRNTYPE,TRNTSJRN                                                 
         JNH   GETMOSX             USE TRANSACTION DATE FOR MOS                 
*&&                                                                             
GETMOS02 OC    TRNBMOS,TRNBMOS     TEST MOS SET BY CALLER                       
         JZ    *+14                                                             
         MVC   WORK(L'TRNBMOS),TRNBMOS                                          
         J     GETMOSX                                                          
         GOTOR VCONVMOS,DMCB,(X'FD',TRNELD),WORK                                
                                                                                
GETMOSX  MVC   MOS,WORK            RETURN PACKED MOS VALUE                      
         LR    RE,R0                                                            
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO TEST IF A TRANSACTION IS CLOSED                          *         
***********************************************************************         
                                                                                
CLOTST   NTR1  BASE=*,LABEL=*                                                   
         LH    RF,DATADISP                                                      
         LA    R0,TRNRECD                                                       
         CR    R1,R0                                                            
         BNE   *+16                                                             
         TM    TRNINDS,TRNICONV                                                 
         BNZ   *+8                                                              
         LHI   RF,ACCORFST                                                      
         AR    R1,RF               R1=A(TRANSACTION ELEMENT)                    
         CLI   0(R1),TRNELQ        MUST BE A TRANSACTION                        
         BE    *+6                                                              
         DC    H'0'                                                             
                                                                                
         LR    RF,R1                                                            
         SR    RE,RE                                                            
         USING TRSEL,RF            LOCATE TRANSACTION STATUS ELEMENT            
CLOTST02 IC    RE,TRSLN            BUMP TO NEXT ELEMENT                         
         AR    RF,RE                                                            
         CLI   TRSEL,0             TEST EOR                                     
         BE    CLOTSTN                                                          
         CLI   TRSEL,TRSELQ        TEST TRANSACTION STATUS ELEMENT              
         BNE   CLOTST02                                                         
         CLI   TRSLN,TRSLNQ        TEST NEW STYLE ELEMENT                       
         BNE   CLOTSTN                                                          
                                                                                
         SR    RE,RE                                                            
         IC    RE,CLOS             RE=LEDGER CLOSE OUT TYPE                     
         CHI   RE,CLOBRM                                                        
         BNH   *+6                                                              
         SR    RE,RE               BBF IS DEFAULT IF BAD                        
         SLL   RE,2                                                             
         B     CLOBR(RE)                                                        
                                                                                
CLOBR    DS    0XL4                                                             
         B     CLOBBF              BALANCE BROUGHT FORWARD (SA, SQ ETC)         
         B     CLOPBL              PAYABLES LEDGER (SV, SX ETC)                 
         B     CLOBBF              (ZERO) BALANCE FORWARD (SE, SI)              
         B     CLORCV              RECEIVABLES LEDGER (SR)                      
         B     CLOPRD              PRODUCTION LEDGER (SJ)                       
         B     CLOCSH              CASH LEDGER (SC)                             
         B     CLOMBF              MATCHED BALANCE FORWARD (SZ, SK)             
CLOBRM   EQU   (*-CLOBR)/L'CLOBR                                                
                                                                                
CLOBBF   CLC   TRSPMOS,CLOSMOS     TEST TRANSACTION MOS GR CLOSED MOS           
         BH    CLOTSTN                                                          
         B     CLOTSTY                                                          
                                                                                
CLOPBL   OC    TRSUMOS,TRSUMOS     TEST USED MOS SET                            
         BZ    CLOTSTN                                                          
         CLC   TRSPMOS,CLOSMOS     TEST TRANSACTION MOS GR CLOSED MOS           
         BH    CLOTSTN                                                          
         CLC   TRSUMOS,CLOSMOS     TEST USED MOS GR CLOSED MOS                  
         BH    CLOTSTN                                                          
         B     CLOTSTY                                                          
                                                                                
CLORCV   TM    TRSSTAT,TRSSPEEL    TEST PEELED ITEM                             
         BZ    CLOTSTN                                                          
         B     CLOTSTY                                                          
                                                                                
CLOPRD   TM    CLOSTAT1,RSTSACIC   TEST FOR CLOSED JOB                          
         BZ    CLOTSTN                                                          
         B     CLOTSTY                                                          
                                                                                
CLOCSH   CLC   TRSPMOS,CLOSMOS     TEST TRANSACTION IN CLOSED PERIOD            
         BH    CLOTSTN                                                          
         TM    TRNSTAT-TRNELD(R1),TRNSDR                                        
         BZ    *+16                                                             
         TM    CLOSTAT3,RSTSPRDR                                                
         BZ    CLOTSTY                                                          
         B     *+12                                                             
         TM    CLOSTAT3,RSTSPRCR                                                
         BZ    CLOTSTY                                                          
         TM    TRNSTAT-TRNELD(R1),TRNSBREC                                      
         BZ    CLOTSTN                                                          
         B     CLOTSTY                                                          
                                                                                
CLOMBF   CLC   TRSPMOS,CLOSMOS     TEST TRANSACTION IN CLOSED PERIOD            
         BH    CLOTSTN                                                          
         TM    TRNSTAT-TRNELD(R1),TRNSMTCH                                      
         BZ    CLOTSTN                                                          
                                                                                
CLOTSTY  J     EXITN               CC=NOT EQUAL IF CLOSED                       
                                                                                
CLOTSTN  J     EXITY               CC=EQUAL IF NOT CLOSED                       
         DROP  RB,RF                                                            
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO ADD OR WRITE BACK ACCOUNT RECORD                         *         
***********************************************************************         
                                                                                
PUTACT   TM    TRNACCI,TRNIOWP+TRNIOAP                                          
         BZR   RE                                                               
         TM    TRNACCI,TRNAINVA                                                 
         BNZR  RE                                                               
                                                                                
PUTACTN  NTR1  BASE=*,LABEL=*                                                   
         L     R5,AACT                                                          
         USING ACTRECD,R5                                                       
         LA    RE,TRNACCU                                                       
         TM    TRNACCI,TRNIOAP     TEST ADD ACCOUNT                             
         BZ    *+8                                                              
         LA    RE,TRNACCA                                                       
         ICM   R1,15,0(RE)         INCREMENT ADD/UPDATE COUNTER                 
         AHI   R1,1                                                             
         STCM  R1,15,0(RE)                                                      
         TM    TRNACCI,TRNIOWP     TEST WRITE COMMAND                           
         BNZ   PUTACT02                                                         
         GOTOR ADD,ACTRECD                                                      
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   TRNACCDA,DA                                                      
         GOTOR PUTSRC              UPDATE NAME SEARCH POINTERS                  
         B     PUTACTX                                                          
                                                                                
PUTACT02 GOTOR PUTSRC              UPDATE NAME SEARCH POINTERS                  
         GOTOR PUTREC,ACTRECD      PUT ACCMST RECORD                            
         CLC   ACTRSTA,IS          TEST ANY STATUS AREA CHANGES                 
         BE    PUTACTX                                                          
         MVC   KEY(L'ACTKEY),ACTKEY                                             
         MVC   KEY+(ACTKSTA-ACTRECD)(L'ACTKSTA),ACTRSTA                         
         MVC   KEY+(ACTKDA-ACTRECD)(L'ACTKDA),DA                                
         GOTOR WRITEDIR,KEY                                                     
         BE    *+6                                                              
         DC    H'0'                                                             
                                                                                
PUTACTX  NI    TRNACCI,FF-(TRNIOAP+TRNIOWP)                                     
         J     EXIT                                                             
         DROP  R5,RB                                                            
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO UPDATE NAME SEARCH POINTERS FOR ACCOUNT RECORD           *         
***********************************************************************         
                                                                                
PUTSRC   TM    TRNACCI,TRNIOAP+TRNANMCH                                         
         BZR   RE                                                               
         OC    VACSRCHP,VACSRCHP                                                
         BZR   RE                                                               
         TM    TRNINDS,TRNIWRNO    TEST WRITE=NO                                
         BNZR  RE                                                               
                                                                                
PUTSRCN  NTR1  BASE=*,LABEL=*                                                   
         L     R1,AACT                                                          
         GOTOR GETLDG              GET LEDGER RECORD/SET LDGSTA2                
         TM    LDGSTA2,LDGSSRCH    TEST SEARCH VALID FOR RECORD                 
         BZ    PUTSRCX                                                          
         GOTOR ,DMCB,C'C   ',AACT,TRNACCDA,0,TRNCOMF,(ACTLEV,0)                 
         TM    TRNACCI,TRNIOAP     TEST ADDED, NOT CHANGING                     
         BZ    *+8                                                              
         MVI   0(R1),C'A'                                                       
         TM    TRNMODE,TRNMOFLN    TEST OFF-LINE                                
         BZ    *+8                                                              
         MVI   3(R1),C'F'                                                       
         GOTOR VACSRCHP                                                         
         BNE   PUTSRCX                                                          
         ICM   RF,15,TRNSRCA       UPDATE NO. OF POINTERS ADDED                 
         AH    RF,0(R1)                                                         
         STCM  RF,15,TRNSRCA                                                    
         ICM   RF,15,TRNSRCU       UPDATE NO. OF POINTERS CHANGED               
         AH    RF,2(R1)                                                         
         STCM  RF,15,TRNSRCU                                                    
                                                                                
PUTSRCX  J     EXIT                                                             
         DROP  RB                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO SET LDGSTA2 FROM EITHER THE PASSED LDGTAB OR BY READING  *         
* THE LEDGER RECORD                                                   *         
***********************************************************************         
                                                                                
GETLDG   NTR1  BASE=*,LABEL=*                                                   
         MVC   SAVEDA,DA           SAVE CURRENT DISK ADDRESS                    
         MVC   SAVEKEY,IO          AND CURRENT KEY                              
         ICM   R1,7,0(R1)          GET CUL INTO R1                              
                                                                                
         XC    LDGVALS(LDGVALL),LDGVALS                                         
                                                                                
         USING LDGTABD,R3          R3=A(CALLER'S LEDGER TABLE)                  
         L     R3,ALDG             TEST A(LEDGER TABLE) PASSED                  
         ZIC   R0,LEDGERS#         MAX LEDGERS SUPPORTED                        
GETLDG02 ST    R3,ALDGNTRY         SET A(LEDGER TABLE ENTRY)                    
         CLI   LDGTUL,LDGTEOTQ     TEST END OF TABLE                            
         BE    GETLDG04                                                         
         CLM   R1,3,LDGTUL         MATCH ON TABLE UNIT/LEDGER CODE              
         BE    GETLDG16                                                         
         AHI   R3,LDGTABL                                                       
         BCT   R0,GETLDG02                                                      
                                                                                
         LA    R3,LDGXTRA          RAN OUT OF SPACE, USE LOCAL AREA             
         CLM   R1,3,LDGTUL                                                      
         BE    GETLDG16                                                         
         ST    R3,ALDGNTRY         SET A(LEDGER TABLE ENTRY)                    
                                                                                
         USING LDGRECD,R2                                                       
GETLDG04 LA    R2,IO               READ LEDGER RECORD                           
         MVC   LDGKEY,SPACES                                                    
         STCM  R1,7,LDGKCPY                                                     
                                                                                
         XC    LDGTABD(LDGTABL),LDGTABD                                         
         MVC   LDGTUL,LDGKUNT                                                   
                                                                                
         GOTOR READ,LDGKEY                                                      
         BNE   GETLDG16                                                         
                                                                                
         USING LDGELD,R1                                                        
         LA    R1,LDGRFST          PROCESS LEDGER RECORD                        
         SR    R0,R0                                                            
GETLDG06 CLI   LDGEL,0             TEST END OF RECORD                           
         BE    GETLDG16                                                         
         CLI   LDGEL,LDGELQ        TEST LEDGER ELEMENT                          
         BE    GETLDG10                                                         
         CLI   LDGEL,ACLELQ        TEST ACCOUNT LENGTHS ELEMENT                 
         BE    GETLDG12                                                         
         CLI   LDGEL,RSTELQ        TEST ACCOUNT STATUS ELEMENT                  
         BE    GETLDG14                                                         
GETLDG08 IC    R0,LDGLN                                                         
         AR    R1,R0                                                            
         B     GETLDG06                                                         
                                                                                
GETLDG10 MVC   LDGTLIKE,LDGLIKE                                                 
         MVC   LDGTOFFP,LDGOPOS                                                 
         MVC   LDGTCLOS,LDGCLOS                                                 
         CLI   LDGLN,LDGSTAT2-LDGELD                                            
         BL    *+10                                                             
         MVC   LDGTSTA2,LDGSTAT2                                                
         CLC   CCSTUL,LDGKUNT      TEST COSTING CLIENT LEDGER                   
         BNE   GETLDG08                                                         
         MVC   LDGTDDL,LDGCPOS     YES - EXTRACT CLIENT DISPLACEMENT            
         CLI   LDGTDDL,0                                                        
         BNE   *+8                                                              
         MVI   LDGTDDL,FF                                                       
         B     GETLDG08                                                         
                                                                                
         USING ACLELD,R1                                                        
GETLDG12 MVC   LDGTLVA,ACLVLEN+(L'ACLVALS*0)                                    
         MVC   LDGTLVB,ACLVLEN+(L'ACLVALS*1)                                    
         MVC   LDGTLVC,ACLVLEN+(L'ACLVALS*2)                                    
         MVC   LDGTLVD,ACLVLEN+(L'ACLVALS*3)                                    
         B     GETLDG08                                                         
                                                                                
         USING RSTELD,R1                                                        
GETLDG14 MVC   LDGTSEC,RSTSECY+1                                                
         B     GETLDG08                                                         
                                                                                
GETLDG16 MVC   LDGSTA2,LDGTSTA2    SET LEDGER STATUS 2                          
         MVC   LDGOFFP,LDGTOFFP    SET OFFICE POSITION                          
         MVC   LDGL1AL,LDGTLVA     SET L'ACCOUNT LEVEL 1                        
         MVC   LDGCLIP,LDGTDDL     SET CLIENT POSITION                          
         CLI   LDGCLIP,FF          TEST SET BUT NOT VALID                       
         BNE   *+8                                                              
         MVI   LDGCLIP,0           YES - SET ZERO DISPLACEMENT                  
                                                                                
         MVC   DA,SAVEDA           RESTORE SAVED DISK ADDRESS AND KEY           
         MVC   IO(L'SAVEKEY),SAVEKEY                                            
         J     EXIT                                                             
         DROP  R1,R2,R3,RB                                                      
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO ADD OR WRITE BACK OFFICE/ACCOUNT RECORD                  *         
***********************************************************************         
                                                                                
PUTOFA   NI    TRNOFAI,FF-TRNIORD  SET RECORD NOT READ                          
         TM    TRNOFAI,TRNIOWP+TRNIOAP                                          
         BZR   RE                                                               
                                                                                
PUTOFAN  NTR1  BASE=*,LABEL=*                                                   
         LA    RE,TRNOFAU                                                       
         TM    TRNOFAI,TRNIOAP     TEST ADD OFFICE/ACCOUNT                      
         BZ    *+8                                                              
         LA    RE,TRNOFAA                                                       
         ICM   R1,15,0(RE)         INCREMENT ADD/UPDATE COUNTER                 
         AHI   R1,1                                                             
         STCM  R1,15,0(RE)                                                      
         L     R1,AOFA                                                          
         TM    TRNOFAI,TRNIOAP                                                  
         BZ    *+12                                                             
         GOTOR ADD                                                              
         B     *+8                                                              
         GOTOR WRITE                                                            
         BE    *+6                                                              
         DC    H'0'                                                             
                                                                                
         LA    R5,KEY                                                           
         USING OAPRECD,R5          BUILD OFFICE-ACCOUNT PASSIVE POINTER         
         L     RE,AOFA             RE=A(OFFICE ACCOUNT RECORD)                  
         USING OFARECD,RE                                                       
         XC    OAPKEY,OAPKEY                                                    
         MVI   OAPKTYP,OAPKTYPQ                                                 
         MVC   OAPKCUL,OFAKCULA                                                 
         MVC   OAPKOFF,OFAKOFF                                                  
         MVC   OAPKACT,OFAKACT                                                  
         MVC   OAPKDA,DA                                                        
         MVC   OAPKSTA,IS                                                       
         TM    TRNOFAI,TRNIOAP     TEST FOR ADDING RECORD                       
         BNO   PUTOFA06            NO                                           
                                                                                
PUTOFA04 GOTOR ADDDIR,OAPRECD                                                   
         BNE   PUTOFAX                                                          
         ICM   R1,15,TRNOAPA       INCREMENT NUMBER OF POINTERS ADDED           
         AHI   R1,1                                                             
         STCM  R1,15,TRNOAPA                                                    
         B     PUTOFAX                                                          
                                                                                
PUTOFA06 LA    R5,IO               READ CURRENT POINTER INTO IO                 
         MVC   OAPKEY,KEY                                                       
         GOTOR READDIR,OAPKEY                                                   
         BE    PUTOFA08            FOUND ONE                                    
         LA    R5,KEY              ADD A POINTER FOR RECORD                     
         B     PUTOFA04                                                         
                                                                                
PUTOFA08 LA    R5,KEY                                                           
         CLC   IS,OAPKSTA          CHECK FOR CHANGE IN STATUS                   
         BE    PUTOFAX             NONE                                         
                                                                                
         GOTOR WRITEDIR,OAPRECD                                                 
         BNE   PUTOFAX                                                          
         ICM   R1,15,TRNOAPU       INCREMENT NUMBER OF POINTERS CHANGED         
         AHI   R1,1                                                             
         STCM  R1,15,TRNOAPU                                                    
                                                                                
PUTOFAX  NI    TRNOFAI,FF-(TRNIOAP+TRNIOWP)                                     
         J     EXIT                                                             
         DROP  R5,RE,RB                                                         
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO ADD OR WRITE BACK CONTRA ACCOUNT RECORD                  *         
***********************************************************************         
                                                                                
         USING CACRECD,R5                                                       
PUTCAC   TM    TRNCACI,TRNIOWP+TRNIOAP                                          
         BZR   RE                                                               
                                                                                
PUTCACN  NTR1  BASE=*,LABEL=*                                                   
         LA    RE,TRNBUKU                                                       
         TM    TRNCACI,TRNIOAP     TEST ADD CONTRA-ACCOUNT                      
         BZ    *+8                                                              
         LA    RE,TRNBUKA                                                       
         ICM   R1,15,0(RE)         INCREMENT ADD/UPDATE COUNTER                 
         AHI   R1,1                                                             
         STCM  R1,15,0(RE)                                                      
         NI    CACRSTAT,X'FF'-X'80'                                             
         LA    R1,CACRECD                                                       
         TM    TRNCACI,TRNIOAP                                                  
         BZ    *+12                                                             
         GOTOR ADD                                                              
         B     *+8                                                              
         GOTOR WRITE                                                            
         BE    PUTCACX                                                          
         DC    H'0'                                                             
                                                                                
PUTCACX  NI    TRNCACI,FF-(TRNIOAP+TRNIOWP)                                     
         J     EXIT                                                             
         DROP  R5,RB                                                            
         EJECT                                                                  
***********************************************************************         
* UPDATE CONTRA-POINTER RECORD IF REQUIRED                            *         
***********************************************************************         
                                                                                
PUTCHD   TM    TRNCACI,TRNCHDWP+TRNCHDAP                                        
         BZR   RE                                                               
                                                                                
PUTCHDN  NTR1  BASE=*,LABEL=*                                                   
         LA    R5,IO                                                            
         USING CHDRECD,R5          BUILD KEY OF CONTRA-HEADER                   
         MVC   CHDKEY,SPACES                                                    
         MVC   CHDKEY(CHDKSPCS-CHDKEY),TRNKEYL                                  
         XC    CHDKNULL,CHDKNULL                                                
                                                                                
         TM    TRNCACI,TRNCHDWP                                                 
         BNZ   PUTCHD02                                                         
         GOTOR BLDREC,PARM,CHDRECD,CHDRECD,0                                    
         GOTOR BLDCAC,CHDRECD                                                   
         GOTOR VHELLO,ELIST,(C'P',FILE),CHDRECD,ELEMENT,0                       
         CLI   ELERR,0                                                          
         BE    *+6                                                              
         DC    H'0'                CAN'T ADD ELEMENT                            
         GOTOR ADD,CHDRECD                                                      
         BE    PUTCHD06                                                         
         DC    H'0'                CAN'T ADD RECORD                             
                                                                                
PUTCHD02 GOTOR READDIR,CHDRECD                                                  
         BE    PUTCHD04                                                         
         TM    IOERR,IOEDEL        TEST RECORD IS DELETED                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         NI    CHDKSTAT,X'FF'-X'80'                                             
         GOTOR WRITEDIR,CHDRECD                                                 
         BE    *+6                                                              
         DC    H'0'                                                             
         GOTOR GET,CHDRECD         GET CONTRA-HEADER RECORD                     
         NI    CHDRSTAT,X'FF'-X'80'                                             
         GOTOR TSTCAC,(R1)                                                      
         GOTOR PUTREC,CHDRECD      PUT CONTRA HEADER IF NAME CHANGED            
         BE    PUTCHD06                                                         
         DC    H'0'                CAN'T WRITE RECORD                           
                                                                                
PUTCHD04 GOTOR GET,CHDRECD         GET CONTRA-HEADER RECORD                     
         GOTOR TSTCAC,(R1)         NAME CHANGED ?                               
         BE    PUTCHDX             NO                                           
         GOTOR PUTREC,CHDRECD      PUT CONTRA HEADER IF NAME CHANGED            
         BE    *+6                                                              
         DC    H'0'                CAN'T WRITE RECORD                           
                                                                                
PUTCHD06 LA    RE,TRNCACU                                                       
         TM    TRNCACI,TRNCHDWP                                                 
         BNZ   *+8                                                              
         LA    RE,TRNCACA                                                       
         ICM   R1,15,0(RE)         INCREMENT ADD/WRITE COUNTER                  
         AHI   R1,1                                                             
         STCM  R1,15,0(RE)                                                      
                                                                                
PUTCHDX  NI    TRNCACI,FF-(TRNCHDWP+TRNCHDAP)                                   
         J     EXIT                                                             
         DROP  RB                                                               
         EJECT                                                                  
***********************************************************************         
* UPDATE CONTRA-POINTER 2 RECORD IF REQUIRED                          *         
***********************************************************************         
                                                                                
PUTCH2   TM    TRNCACI,TRNCH2WP+TRNCH2AP                                        
         BZR   RE                                                               
                                                                                
PUTCH2N  NTR1  BASE=*,LABEL=*                                                   
         LA    R5,IO                                                            
         USING CHDRECD,R5          BUILD KEY OF CONTRA-HEADER                   
         MVC   CHDKEY,SPACES                                                    
         MVC   CHDKEY(CHDKSPCS-CHDKEY),TRNKEYL                                  
         CLC   PRODUL,CHDKUNT      TEST LEDGER HAS W/C IN KEY                   
         BE    *+10                                                             
         CLC   PCTLUL,CHDKUNT                                                   
         BE    *+10                                                             
         MVC   CHDKOFF,SPACES      NO - SET KEY OFFICE TO SPACES                
         XC    CHDKNULL,CHDKNULL                                                
                                                                                
         TM    TRNCACI,TRNCH2WP                                                 
         BNZ   PUTCH202                                                         
         GOTOR BLDREC,PARM,CHDRECD,CHDRECD,0                                    
         GOTOR BLDCAC,CHDRECD                                                   
         GOTOR VHELLO,ELIST,(C'P',FILE),CHDRECD,ELEMENT,0                       
         CLI   ELERR,0                                                          
         BE    *+6                                                              
         DC    H'0'                CAN'T ADD ELEMENT                            
         GOTOR ADD,CHDRECD                                                      
         BE    *+6                                                              
         DC    H'0'                CAN'T ADD RECORD                             
         CLC   CHDKCPY,CHDKCCPY                                                 
         BNE   PUTCH206                                                         
         GOTOR GETLDG,CHDRECD      SET LDGSTA2                                  
         TM    LDGSTA2,LDGSGCAP    TEST CONTRA-POINTERS REQUIRED                
         BZ    PUTCH206                                                         
                                                                                
         USING TCPPASD,KEY                                                      
         XC    TCPPKEY,TCPPKEY                                                  
         MVI   TCPPTYP,TCPPTYPQ                                                 
         MVC   TCPPCPY,CHDKCPY                                                  
         MVC   TCPPUL,CHDKUNT                                                   
         MVC   TCPPULC,CHDKULC                                                  
         MVC   TCPPACT,CHDKACT                                                  
         MVC   TCPPWORK,CHDKWRK                                                 
         XC    TCPPKSTA,TCPPKSTA                                                
         MVC   TCPPKDA,DA                                                       
         GOTOR ADDDIR,TCPPASD                                                   
         BE    *+6                                                              
         DC    H'0'                                                             
         ICM   R1,15,TRNTCPA       INCREMENT NUMBER OF POINTERS ADDED           
         AHI   R1,1                                                             
         STCM  R1,15,TRNTCPA                                                    
         B     PUTCH206                                                         
                                                                                
PUTCH202 GOTOR READDIR,CHDRECD                                                  
         BE    PUTCH204                                                         
         TM    IOERR,IOEDEL                                                     
         BNZ   *+6                                                              
         DC    H'0'                                                             
         NI    CHDKSTAT,X'FF'-X'80'                                             
         GOTOR WRITEDIR,CHDRECD                                                 
         GOTOR GET,CHDRECD         GET CONTRA-HEADER RECORD                     
         NI    CHDRSTAT,X'FF'-X'80'                                             
         GOTOR TSTCAC,(R1)                                                      
         GOTOR PUTREC,CHDRECD                                                   
         BE    PUTCH206                                                         
         DC    H'0'                CAN'T WRITE RECORD                           
                                                                                
PUTCH204 GOTOR GET,CHDRECD         GET CONTRA-HEADER RECORD                     
         GOTOR TSTCAC,(R1)                                                      
         BNE   *+6                                                              
         DC    H'0'                                                             
         GOTOR PUTREC,CHDRECD      PUT CONTRA HEADER IF NAME CHANGED            
         BE    *+6                                                              
         DC    H'0'                CAN'T WRITE RECORD                           
                                                                                
PUTCH206 LA    RE,TRNCACU                                                       
         TM    TRNCACI,TRNCH2WP                                                 
         BNZ   *+8                                                              
         LA    RE,TRNCACA                                                       
         ICM   R1,15,0(RE)         INCREMENT ADD/WRITE COUNTER                  
         AHI   R1,1                                                             
         STCM  R1,15,0(RE)                                                      
                                                                                
PUTCH2X  NI    TRNCACI,FF-(TRNCH2WP+TRNCH2AP)                                   
         J     EXIT                                                             
         DROP  RB                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO UPDATE BUCKET RECORDS                                    *         
***********************************************************************         
                                                                                
PUTBUK   NTR1  BASE=*,LABEL=*                                                   
         L     R2,ABUK             R2=A(FIRST BUCKET ELEMENT)                   
         USING BUKBUFD,R2                                                       
         ICM   R0,15,BUKBHNUM      R0=NUMBER OF BUCKET ELEMENTS                 
         BZ    PUTBUKX                                                          
         AHI   R2,BUKBHDRL                                                      
         SHI   R0,1                                                             
         BZ    PUTBUK06            NO SORT IF ONLY 1 BUCKET ELEMENT             
                                                                                
PUTBUK02 LA    RF,BUKBUFL(R2)      SORT ELEMENTS INTO ASCENDING SEQ             
         LR    RE,R0                                                            
PUTBUK04 CLC   BUKBUFD(BUKBKEYL),0(RF)                                          
         BNH   *+22                                                             
         XC    0(BUKBUFL,R2),0(RF)                                              
         XC    0(BUKBUFL,RF),0(R2)                                              
         XC    0(BUKBUFL,R2),0(RF)                                              
         AHI   RF,BUKBUFL                                                       
         BCT   RE,PUTBUK04                                                      
         AHI   R2,BUKBUFL                                                       
         BCT   R0,PUTBUK02                                                      
                                                                                
PUTBUK06 XC    BUKEY(BUKEYL),BUKEY INITIALIZE KEY                               
                                                                                
         L     R2,ABUK                                                          
         ICM   R0,15,BUKBHNUM      R0=NUMBER OF BUFFER ENTRIES                  
         AHI   R2,BUKBHDRL                                                      
                                                                                
         LA    R5,IO                                                            
         USING CACRECD,R5          R5=A(CONTRA RECORD)                          
PUTBUK08 CLC   BUKBKEY(BUKBKEYL),BUKEY TEST CHANGE OF BUCKET TYPE               
         BE    PUTBUK14                                                         
         CLI   BUKEY,0             TEST FIRST TIME                              
         BE    PUTBUK12                                                         
         CLI   BUKSTAT,BUKSLIVE                                                 
         BNE   PUTBUK12                                                         
                                                                                
PUTBUK10 GOTOR PUTCAC                                                           
                                                                                
PUTBUK12 NI    TRNCACI,FF-(TRNIOAP+TRNIOWP)                                     
                                                                                
         MVC   BUKEY(BUKEYL),BUKBKEY SET CURRENT BUCKET TYPE                    
         CLI   BUKSTAT,BUKSLIVE    TEST LIVE  BUCKET DATA                       
         BE    PUTBUK13                                                         
         CLI   BUKSTAT,BUKSDRFT    TEST DRAFT BUCKET DATA                       
         BNE   PUTBUK18                                                         
         CLI   BUKTYPE,C' '        TEST DEFAULT BUCKET RECORD                   
         BNE   PUTBUK18                                                         
                                                                                
PUTBUK13 GOTOR GETBUK                                                           
                                                                                
PUTBUK14 CLI   BUKBSTAT,BUKSLIVE   TEST LIVE DATA                               
         BNE   PUTBUK18                                                         
         CLC   BUKBOFFC,SPACES     TEST ALL OFFICE BUCKET                       
         BE    PUTBUK16                                                         
         CLC   TRNKEYL+(TRNKUNT-TRNKEY)(L'CCSTUL),CCSTUL                        
         BE    PUTBUK16                                                         
         CLI   TRNKEYL+(TRNKUNT-TRNKEY),C'G'                                    
         BE    PUTBUK16                                                         
         CLI   TRNKEYL+(TRNKUNT-TRNKEY),C'S'                                    
         BNE   PUTBUK18                                                         
                                                                                
         USING BUKELD,R1                                                        
PUTBUK16 LA    R1,BUKELEM1                                                      
         MVI   BUKEL,BUKELQ                                                     
         MVI   BUKLN,BUKLN7Q                                                    
         MVC   BUKMOS,BUKBMOS                                                   
         ZAP   BUKDR7,BUKBDR                                                    
         ZAP   BUKCR7,BUKBCR                                                    
*                                  ADD BUCKET ELEMENT TO RECORD                 
         MVI   BYTE,0                                                           
         TM    TRNINDS2,TRNIRBKA                                                
         BZ    *+8                                                              
         MVI   BYTE,X'80'          SET TO REPLACE BUCKET                        
         GOTOR VADDBUK,DMCB,(X'80',CACRECD),(BYTE,BUKELEM1),VHELLO              
                                                                                
PUTBUK18 AHI   R2,BUKBUFL          BUMP TO NEXT BUFFER ENTRY                    
         BCT   R0,PUTBUK08                                                      
                                                                                
         CLI   BUKSTAT,BUKSLIVE    TEST LIVE TRANSACTION                        
         BNE   PUTBUK20                                                         
                                                                                
         GOTOR PUTCAC              PUT FINAL CONTRA BUCKET RECORD               
                                                                                
PUTBUK20 NI    TRNCACI,FF-(TRNIOAP+TRNIOWP)                                     
                                                                                
LST      USING TRNKEY,TRNKEYL                                                   
                                                                                
         TM    TRNCPYSA,CPYSACCT   TEST P&L RECORDS REQUIRED                    
         BZ    PUTBUK46                                                         
         CLC   CCSTUL,LST.TRNKUNT                LEDGER - C'1C'                 
         BNE   PUTBUK46            NOT A COSTING CLIENT POSTING/BUCKET          
         CLC   LST.TRNKCUNT(L'CBILUL),CBILUL     LEDGER - C'11'                 
         BL    PUTBUK46                                                         
         CLC   LST.TRNKCUNT(L'COHDUL),COHDUL     LEDGER - C'16'                 
         BH    PUTBUK46                                                         
                                                                                
         USING PALD,R3             R3=A(P&L BUCKET CONTROL AREA)                
         L     R3,APAL                                                          
         TM    TRNPALI,TRNIOXC     TEST AREA INITIALIZED                        
         BZ    PUTBUK21            NO                                           
         TM    PALDINDS,PALDINOT   YES, STILL OK TO CREATE P&L RECS             
         BZ    PUTBUK24            YES, SO GO BUILD                             
         B     PUTBUK46            NO, FAIL TEST IN INITIALIZATION              
                                                                                
PUTBUK21 XC    PALD(PALLNQ),PALD                                                
         OI    TRNPALI,TRNIOXC     SET INITIALIZE RAN                           
                                                                                
         USING LDGTABD,R1                                                       
         USING LDGRECD,R5          ESTABLISH CONTRA LEDGER VALUES               
         LA    R5,IO                                                            
         MVC   LDGKCPY,LST.TRNKCPY                                              
         MVC   LDGKUNT(L'CBILUL),CBILUL     LEDGER - C'11'                      
         GOTOR GETLDG,LDGRECD                                                   
                                                                                
         L     R1,ALDGNTRY                                                      
         MVC   PAL11LLN,LDGTLVB         DEFAULT TO LEVEL 2 LENGTH               
         CLI   LDGTLVB,0                IS THERE A 2ND LEVEL ?                  
         BNE   *+10                                                             
         MVC   PAL11LLN,LDGTLVA         SET TO LEVEL 1 LENGTH (IS 12)           
                                                                                
         MVC   LDGKCPY,LST.TRNKCPY                                              
         MVC   LDGKUNT(L'CINCUL),CINCUL    LEDGER - C'12'                       
         GOTOR GETLDG,LDGRECD                                                   
                                                                                
         L     R1,ALDGNTRY                                                      
         MVC   PAL12LLN,LDGTLVB         DEFAULT TO LEVEL 2 LENGTH               
         CLI   LDGTLVB,0                IS THERE A 2ND LEVEL ?                  
         BNE   *+10                                                             
         MVC   PAL12LLN,LDGTLVA         SET TO LEVEL 1 LENGTH (IS 12)           
                                                                                
         MVC   LDGKCPY,LST.TRNKCPY                                              
         MVC   LDGKUNT(L'CEXPUL),CEXPUL   LEDGER - C'13'                        
         GOTOR GETLDG,LDGRECD                                                   
                                                                                
         L     R1,ALDGNTRY                                                      
         MVC   PAL13LLN,LDGTLVB         DEFAULT TO LEVEL 2 LENGTH               
         CLI   LDGTLVB,0                IS THERE A 2ND LEVEL ?                  
         BNE   *+10                                                             
         MVC   PAL13LLN,LDGTLVA         SET TO LEVEL 1 LENGTH (IS 12)           
                                                                                
         MVC   LDGKCPY,LST.TRNKCPY                                              
         MVC   LDGKUNT(L'CPERULUS),CPERULUS     LEDGER - C'1R'                  
         GOTOR GETLDG,LDGRECD                                                   
                                                                                
         L     R1,ALDGNTRY                                                      
         CLI   LDGTUL,LDGTEOTQ     IS THERE 1R LEDGER ?                         
         BE    PUTBUK24            NO, THEN NO 14,15 OR 16 POSTINGS             
         MVC   PALCAOFL,LDGTLVA    1ST LEVEL OF 1R - OFFICE                     
         MVC   PAL1RLLN,LDGTLVB    2ND LEVEL OF 1R                              
                                                                                
PUTBUK24 L     R2,ABUK                                                          
         ICM   R0,15,BUKBHNUM      R0=NUMBER OF BUCKET ELEMENTS                 
         AHI   R2,BUKBHDRL         R2=A(FIRST BUCKET ELEMENT)                   
                                                                                
PUTBUK25 CLI   BUKBSTAT,BUKSXTRA   ONLY PROCESS XTRA BUCKETS                    
         BNE   PUTBUK40                                                         
                                                                                
         USING PLCRECD,KEY                                                      
PUTBUK26 XC    PLCKEY,PLCKEY                                                    
         MVI   PLCKTYP,PLCKTYPQ                                                 
         MVI   PLCKSUB,PLCKSUBQ                                                 
         MVC   PLCKCPY,LST.TRNKCPY                                              
         MVC   PLCK1CAC,LST.TRNKACT      FULL 1C ACCOUNT                        
         MVC   PLCKCLDG,LST.TRNKCLDG     CONTRA LEDGER                          
         MVC   PLCKYYMM,BUKBMOS                                                 
         MVC   PLCKAGYO,BUKBOFFC         SET OFFICE CODE FOR 11 TO 13           
                                                                                
         CLI   LST.TRNKCLDG,C'3'   LEDGER 13                                    
         BNH   PUTBUK30                                                         
         LA    RE,LST.TRNKCACT     OFFICE FOR 16                                
         CLI   LST.TRNKCLDG,C'6'   LEDGER 16                                    
         BE    *+8                                                              
         LA    RE,LST.TRNKCACT+1   OFFICE FOR 14 AND 15                         
                                                                                
         ZIC   RF,PALCAOFL         CONTRA OFF LEN (1R OFFICE 1ST LEVEL)         
         BCTR  RF,0                                                             
         EXMVC RF,PLCKAGYO,0(RE)                                                
                                                                                
         MVC   PLCKMTHD,BUKBTYPE                                                
         ZIC   RF,PALCAOFL         OFFICE LENGTH                                
         AHI   RF,1                ANALYSIS CODE LENGTH                         
         CLI   LST.TRNKCLDG,C'6'   LEDGER 16                                    
         BNE   *+8                                                              
         IC    RF,PAL1RLLN         1ST & 2ND LEVEL OF 1R                        
         ZAP   DUB,BUKBCR                                                       
         SP    DUB,BUKBDR                                                       
         B     PUTBUK32                                                         
                                                                                
PUTBUK30 SR    RF,RF                                                            
         IC    RF,PAL11LLN         LEVEL LENGTH                                 
         ZAP   DUB,BUKBDR                                                       
         SP    DUB,BUKBCR                                                       
         CLI   LST.TRNKCLDG,C'1'   BILLING                                      
         BE    PUTBUK32                                                         
         IC    RF,PAL12LLN         LEVEL LENGTH                                 
         CLI   LST.TRNKCLDG,C'2'   REVENUE                                      
         BE    PUTBUK32                                                         
         IC    RF,PAL13LLN         LEVEL LENGTH                                 
         ZAP   DUB,BUKBCR                                                       
         SP    DUB,BUKBDR                                                       
         CLI   LST.TRNKCLDG,C'3'   EXPENSE                                      
         BE    PUTBUK32                                                         
         DC    H'0'                                                             
                                                                                
PUTBUK32 MVC   PLCKCACT,SPACES                                                  
         STC   RF,CACTLN           SAVE LENGTH OF CONTRA ACCOUNT                
         SHI   RF,1                CONTRA ACCOUNT FOR 11 TO 13                  
         BNM   *+6                                                              
         DC    H'0'                                                             
         EXMVC RF,PLCKCACT,LST.TRNKCACT                                         
                                                                                
SV       USING TRNKEY,LASTTKEY                                                  
                                                                                
PUTBUK35 GOTOR READDIR,PLCRECD     GET DIRECTORY FOR KEY                        
         BNE   PUTBUK39            NOT FOUND                                    
         AP    PLCKAMT,DUB         FOUND SO UPDATE                              
         CLI   LST.TRNKCLDG,C'3'   IF 11, 12 OR 13 THEN ADD                     
         BNH   PUTBUK38                                                         
         TM    TRNINDS2,TRNIRBKA   TEST REPLACING BUCKET VALUES                 
         BZ    PUTBUK38                                                         
         CLC   LST.TRNKCULA,SV.TRNKCULA     SAME ACCOUNT ?                      
         BNE   PUTBUK36                     NO, SO REPLACE                      
         ZIC   RF,CACTLN                    LEN OF CONTRA ACCOUNT               
         EXCLC RF,LST.TRNKCLDG,SV.TRNKCLDG  SAME CONTRA ACCOUNT ?               
         BE    PUTBUK38                     YES, SO ADD                         
         DROP  SV                                                               
                                                                                
PUTBUK36 ZAP   PLCKAMT,DUB         IF 14, 15 OR 16 THEN REPLACE                 
                                                                                
PUTBUK38 GOTOR WRITEDIR,PLCRECD                                                 
         BE    *+6                                                              
         DC    H'0'                                                             
         ICM   RF,15,TRNPALU                                                    
         AHI   RF,1                                                             
         STCM  RF,15,TRNPALU                                                    
         B     PUTBUK40                                                         
                                                                                
PUTBUK39 MVC   PLCKEY,KEYSAVE                                                   
         MVI   PLCKSTA,0                                                        
         ZAP   PLCKAMT,DUB                                                      
         XC    PLCKDA,PLCKDA                                                    
                                                                                
         GOTOR ADDDIR,PLCRECD                                                   
         BE    *+6                                                              
         DC    H'0'                                                             
         ICM   RF,15,TRNPALA                                                    
         AHI   RF,1                                                             
         STCM  RF,15,TRNPALA                                                    
                                                                                
PUTBUK40 AHI   R2,BUKBUFL          BUMP TO NEXT BUCKET ENTRY                    
         BCT   R0,PUTBUK25         DO FOR NUMBER OF ENTRIES                     
         B     PUTBUK46                                                         
                                                                                
PUTBUK44 OI    PALDINDS,PALDINOT   SET TO NOT CREATE/UPDATE P&L RECORDS         
                                                                                
PUTBUK46 L     R2,ABUK                                                          
         XC    BUKBHNUM,BUKBHNUM   CLEAR BUCKET ENTRY COUNT                     
                                                                                
PUTBUKX  J     EXIT                                                             
         DROP  LST,R1,R2,R3,R5,RB                                               
         EJECT                                                                  
***********************************************************************         
* READ OR BUILD CONTRA-HEADER                                         *         
***********************************************************************         
                                                                                
         USING BUKBUFD,R2                                                       
         USING CACRECD,R5          READ CONTRA BUCKET RECORD                    
GETBUK   NTR1  BASE=*,LABEL=*                                                   
         MVC   CACKEY,SPACES                                                    
         MVC   CACKEY(CACKSPAC-CACKEY),TRNKEYL                                  
         CLC   PRODUL,CACKUNT      TEST LEDGER HAS W/C IN KEY                   
         BE    *+10                                                             
         CLC   PCTLUL,CACKUNT                                                   
         BE    *+10                                                             
         MVC   CACKOFF,BUKOFFC     SET KEY OFFICE FORM BUCKET BUFFER            
         MVC   CACKBTYP,BUKTYPE                                                 
         MVC   CACKSTYP,BUKSTYP                                                 
         GOTOR READ,CACRECD                                                     
         TM    IOERR,IOEBAD                                                     
         BNZ   *+12                                                             
         OI    TRNCACI,TRNIOWP                                                  
         B     GETBUKX                                                          
         GOTOR BLDREC,PARM,KEYSAVE,CACRECD,0                                    
         OI    TRNCACI,TRNIOAP                                                  
                                                                                
GETBUKX  J     EXIT                                                             
         DROP  R2,R5,RB                                                         
         EJECT                                                                  
***********************************************************************         
* TEST FOR CHANGE OF CONTRA-ACCOUNT NAME & UPDATE RECORD IF IT HAS    *         
***********************************************************************         
                                                                                
TSTCAC   NTR1  BASE=*,LABEL=*                                                   
         TM    TRNINDS,TRNIDUCN    TEST DON'T UPDATE CONTRA NAMES               
         JNZ   EXITY                                                            
         LR    R2,R1                                                            
         USING CACRECD,R2                                                       
         GOTOR BLDCAC,CACRECD                                                   
         CLI   ELEMENT+(CACLN-CACELD),CACLN1Q                                   
         JNH   EXITY                                                            
         LA    R1,CACRECD                                                       
         AH    R1,DATADISP                                                      
         USING CACELD,R1                                                        
         CLI   CACEL,CACELQ        TEST FOR GOOD CONTRA-HEADER RECORD           
         BE    *+6                                                              
         DC    H'0'                BAD CONTRA-HEADER RECORD                     
         SR    RE,RE                                                            
         IC    RE,CACLN                                                         
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         CLC   CACELD(0),ELEMENT   TEST CONTRA HEADER CHANGED                   
         JE    EXITY                                                            
         GOTOR VHELLO,ELIST,(C'D',FILE),('CACELQ',CACRECD),0                    
         CLI   ELERR,0                                                          
         BE    *+6                                                              
         DC    H'0'                CAN'T DELETE CONTRA-HEADER ELEMENT           
         GOTOR (RF),(R1),(C'P',FILE),CACRECD,ELEMENT,0                          
         CLI   ELERR,0                                                          
         JE    EXITN                                                            
         DC    H'0'                CAN'T ADD ELEMENT                            
         DROP  R1,R2,RB                                                         
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO BUILD A CONTRA HEADER ELEMENT                            *         
*                                                                     *         
* NTRY - R1=A(RECORD)                                                 *         
*        TRNCACSV CONTAINS CONTRA-ACCOUNT NAME                        *         
* EXIT - ELEMENT CONTAINS CONTRA-ACCOUNT HEADER ELEMENT               *         
***********************************************************************         
                                                                                
BLDCAC   STM   RE,R1,12(RD)                                                     
                                                                                
         XC    ELEMENT,ELEMENT     BUILD CONTRA HEADER ELEMENT                  
         LA    RF,ELEMENT                                                       
         USING CACELD,RF                                                        
         MVI   CACEL,CACELQ                                                     
         MVI   CACLN,CACLN1Q                                                    
         MVC   CACCNT,TRNKCULC-TRNRECD(R1)                                      
         MVC   CACNAME,SPACES                                                   
         OC    TRNCACSV,SPACES                                                  
         CLC   TRNCACSV,SPACES                                                  
         JE    BLDCACX                                                          
         LA    R1,TRNCACSV+L'TRNCACSV-1                                         
         CLI   0(R1),C' '                                                       
         JH    *+8                                                              
         BRCT  R1,*-8                                                           
         LA    R0,TRNCACSV                                                      
         SR    R1,R0                                                            
         BASR  RE,0                                                             
         EX    R1,8(RE)                                                         
         J     *+10                                                             
         MVC   CACNAME(0),TRNCACSV                                              
         AHI   R1,CACLN1Q+1                                                     
         STC   R1,CACLN                                                         
                                                                                
BLDCACX  LM    RE,R1,12(RD)                                                     
         BR    RE                                                               
         DROP  RF                                                               
                                                                                
***********************************************************************         
* ROUTINE TO BUILD A VIRGIN RECORD                                    *         
*                                                                     *         
* NTRY - R1=A(PARAMETER LIST)                                         *         
*           P1=A(KEY)                                                 *         
*           P2=A(I/O AREA)                                            *         
*           P3=A(ELEMENT LIST TO ADD TO RECORD) OR 0                  *         
***********************************************************************         
                                                                                
BLDREC   STM   RE,R3,12(RD)                                                     
                                                                                
         LM    RE,RF,0(R1)                                                      
         MVC   WORK(L'ACCKEY),0(RE)                                             
         USING ACCRECD,RF                                                       
         XC    ACCKEY(256),ACCKEY                                               
         MVC   ACCKEY,WORK                                                      
         LHI   R0,ACCRFST+1-ACCRECD                                             
         ICM   R2,15,8(R1)         TEST ANY ELEMENTS TO ADD                     
         JZ    BLDRECX                                                          
         LA    R3,ACCRFST          R3=A(FIRST ELEMENT ON OUTPUT RECORD)         
         SR    R1,R1                                                            
BLDREC02 CLI   0(R2),0             TEST END OF ELEMENT LIST                     
         JE    BLDRECX                                                          
         IC    R1,1(R2)            R1=L'ELEMENT TO ADD                          
         AR    R0,R1               BUMP TOTAL RECORD LENGTH                     
         BASR  RE,0                                                             
         EX    R1,8(RE)                                                         
         J     *+10                                                             
         MVC   0(0,R3),0(R2)       MOVE ELEMENT TO RECORD                       
         AR    R2,R0               BUMP INPUT ELEMENT POINTER                   
         AR    R3,R0               BUMP OUTPUT RECORD POINTER                   
         J     BLDREC02                                                         
                                                                                
BLDRECX  STCM  R0,3,ACCRLEN        SET OUTPUT RECORD LENGTH                     
         LM    RE,R3,12(RD)                                                     
         BR    RE                                                               
         DROP  RF                                                               
         EJECT                                                                  
***********************************************************************         
* INTERFACE TO DATAMGR FOR OLD AND NEW FILES                          *         
***********************************************************************         
                                                                                
GETTRN   OC    TRNDA,TRNDA         TEST TRANSACTION D/A ESTABLISHED             
         BNZR  RE                                                               
         TM    TRNINDS,TRNIWRNO+TRNINDIR                                        
         JZ    GETTRN02                                                         
         XC    IS,IS                                                            
         XC    DA,DA                                                            
         MVI   IOERR,0                                                          
         CLI   IOERR,0                                                          
         BR    RE                                                               
GETTRN02 ST    RE,DMSAVERE                                                      
         MVC   KEY(L'TRNKEY),0(R1)                                              
         GOTOR READDIR,KEY                                                      
         JE    *+6                                                              
         DC    H'0'                                                             
         MVC   TRNDA,DA                                                         
         MVC   TRNIS,IS                                                         
         L     RE,DMSAVERE                                                      
         BR    RE                                                               
                                                                                
HIGH     LA    RF,DMRDHIQ                                                       
         MVI   IOCTL,IOCDIR+IOCFIL                                              
         J     DMRALL                                                           
                                                                                
READ     LA    RF,DMREADQ                                                       
         MVI   IOCTL,IOCDIR+IOCFIL                                              
         J     DMRALL                                                           
                                                                                
READUN   LA    RF,DMRDUNQ                                                       
         MVI   IOCTL,IOCDIR+IOCFIL                                              
         J     DMRALL                                                           
                                                                                
WRITE    LA    RF,DMWRTQ                                                        
         MVI   IOCTL,IOCDIR+IOCFIL+IOCUPD                                       
         J     DMRALL                                                           
                                                                                
ADD      LA    RF,DMADDQ                                                        
         MVI   IOCTL,IOCDIR+IOCFIL+IOCUPD                                       
         J     DMRALL                                                           
                                                                                
GET      LA    RF,DMGETRQ                                                       
         MVI   IOCTL,IOCDIR+IOCFIL                                              
         J     DMRALL                                                           
                                                                                
GETU     LA    RF,DMGRULQ                                                       
         MVI   IOCTL,IOCDIR+IOCFIL                                              
         J     DMRALL                                                           
                                                                                
PUT      LA    RF,DMPUTRQ                                                       
         MVI   IOCTL,IOCDIR+IOCFIL+IOCUPD                                       
         J     DMRALL                                                           
                                                                                
UNLOKDIR LA    RF,DMRUNLKQ                                                      
         MVI   IOCTL,IOCDIR                                                     
         J     DMRALL                                                           
                                                                                
READDIR  LA    RF,DMREADQ                                                       
         MVI   IOCTL,IOCDIR                                                     
         J     DMRALL                                                           
                                                                                
READUL   LA    RF,DMRDULQ                                                       
         MVI   IOCTL,IOCDIR                                                     
         J     DMRALL                                                           
                                                                                
RDHI     LA    RF,DMRDHIQ                                                       
         MVI   IOCTL,IOCDIR                                                     
         J     DMRALL                                                           
                                                                                
RDHDIRUP LA    RF,DMRDHIDQ                                                      
         MVI   IOCTL,IOCDIR                                                     
         J     DMRALL                                                           
                                                                                
RDSQ     LA    RF,DMRSEQQ                                                       
         MVI   IOCTL,IOCDIR                                                     
         J     DMRALL                                                           
                                                                                
RDSQU    LA    RF,DMRSULQ                                                       
         MVI   IOCTL,IOCDIR                                                     
         J     DMRALL                                                           
                                                                                
ADDDIR   LA    RF,DMADDQ                                                        
         MVI   IOCTL,IOCDIR+IOCUPD                                              
         J     DMRALL                                                           
                                                                                
WRITEDIR LA    RF,DMWRTQ                                                        
         MVI   IOCTL,IOCDIR+IOCUPD                                              
         J     DMRALL                                                           
                                                                                
GETREC   LA    RF,DMGETRQ                                                       
         MVI   IOCTL,IOCFIL                                                     
         J     DMRALL                                                           
                                                                                
GETRECT  LA    RF,DMGETRQT                                                      
         MVI   IOCTL,IOCFIL                                                     
         J     DMRALL                                                           
                                                                                
PUTREC   LA    RF,DMPUTRQ                                                       
         MVI   IOCTL,IOCFIL+IOCUPD                                              
         J     DMRALL                                                           
                                                                                
RDHIU    LA    RF,DMRDHIUQ                                                      
         MVI   IOCTL,IOCDIR                                                     
         J     DMRALL                                                           
                                                                                
DMRALL   NTR1  BASE=*,LABEL=*                                                   
         LA    R2,0(,R1)           R2=A(I/O AREA)                               
         MVC   KEYSAVE,0(R2)       SAVE KEY                                     
         MVI   IOERR,0                                                          
         LA    R0,TRNRECD                                                       
         CR    R2,R0               TEST A(TRANSACTION RECORD) PASSED            
         BNE   DMRNEW                                                           
         TM    TRNINDS,TRNICONV    TEST USER PASSING CONVERTED RECORDS          
         BNZ   DMRNEW                                                           
                                                                                
         XC    DA,DA               OLD FILE INTERFACE                           
         XC    IS,IS                                                            
         CHI   RF,DMADDQ           CAN'T ISSUE GET/PUT ETC TO OLD FILE          
         BH    DMRALLX                                                          
         MHI   RF,L'DMACTS                                                      
         LA    RF,DMACTS(RF)                                                    
         GOTOR DMGR,DMCB,(X'88',(RF)),ACCFIL,(R2),(R2)                          
         CLC   ACTRLEN-ACTRECD(L'ACTRLEN,R2),=Y(IOAREAL-7)                      
         BNH   *+8                 IS RECORD BIGGER THAN I/O AREA               
         OI    8(R1),X'10'         THEN TURN ON NOT FOUND                       
         MVC   IOERR,8(R1)                                                      
         B     DMRALLX                                                          
                                                                                
DMRNEW   LA    R3,IOAREAL(R2)                                                   
         LR    RE,RF                                                            
         SLL   RE,2                                                             
         LHI   RF,X'88'            SET DEFAULT - READ LOCK FOR UPDATE           
         B     *+4(RE)                                                          
                                                                                
DMACTLST DS    0XL4                                                             
                                                                                
DMRDHIQ  EQU   (*-DMACTLST)/L'DMACTLST                                          
         B     DMRAHI              DMRDHI USES DMRDHI(+GETREC)                  
DMREADQ  EQU   (*-DMACTLST)/L'DMACTLST                                          
         B     DMRARD              DMREAD USES DMREAD(+GETREC)                  
DMRSEQQ  EQU   (*-DMACTLST)/L'DMACTLST                                          
         B     DMRARS              DMRSEQ USES DMRSEQ(+GETREC)                  
DMWRTQ   EQU   (*-DMACTLST)/L'DMACTLST                                          
         B     DMRAPR              DMWRT USES PUTREC (OR DMWRT ONLY)            
DMADDQ   EQU   (*-DMACTLST)/L'DMACTLST                                          
         B     DMRAAR              DMADD USES ADDREC (OR DMADD ONLY)            
                                                                                
DMRDUNQ  EQU   (*-DMACTLST)/L'DMACTLST                                          
         B     DMRARDUN            DMREAD USES DMREAD(+GETREC)                  
DMRUNLKQ EQU   (*-DMACTLST)/L'DMACTLST                                          
         B     DMRUNLK             DMRUNLK                                      
DMGETRQ  EQU   (*-DMACTLST)/L'DMACTLST                                          
         B     DMRAGR              GETREC ONLY                                  
DMGETRQT EQU   (*-DMACTLST)/L'DMACTLST                                          
         B     DMRAGRT             GETREC ONLY                                  
DMPUTRQ  EQU   (*-DMACTLST)/L'DMACTLST                                          
         B     DMRAPR              PUTREC ONLY                                  
DMADDRQ  EQU   (*-DMACTLST)/L'DMACTLST                                          
         B     DMRAAR              ADDREC ONLY                                  
DMRDULQ  EQU   (*-DMACTLST)/L'DMACTLST                                          
         B     DMRARDU             DMREAD - NO LOCKING                          
DMRSULQ  EQU   (*-DMACTLST)/L'DMACTLST                                          
         B     DMRARSU             DMRSEQ - NO LOCKING                          
DMGRULQ  EQU   (*-DMACTLST)/L'DMACTLST                                          
         B     DMRAGRU             GETREC - NO LOCKING                          
DMRDHIDQ EQU   (*-DMACTLST)/L'DMACTLST                                          
         B     DMRAHID             DMRDHI - NO DELETED                          
DMRDHIUQ EQU   (*-DMACTLST)/L'DMACTLST                                          
         B     DMRAHIU             DMRDHI - NO LOCKING/NO DELETES               
                                                                                
DMRAHID  LHI   RF,X'80'            READ HIGH - NOT DELETED                      
         B     DMRAHI                                                           
DMRAHIU  SR    RF,RF               READ HIGH - NO LOCKING/NO DELETES            
DMRAHI   LA    R0,DMRDHI                                                        
         B     DMRREAD                                                          
                                                                                
DMRARDUN SR    RF,RF               READ - NO DELETES/NO LOCKING                 
         B     DMRARD                                                           
DMRARDU  LHI   RF,X'08'            READ DELETES - NO LOCKING                    
DMRARD   LA    R0,DMREAD                                                        
         B     DMRREAD                                                          
                                                                                
DMRARSU  LHI   RF,X'80'            READ HIGH - NOT DELETED                      
DMRARS   LA    R0,DMRSEQ                                                        
                                                                                
DMRREAD  STC   RF,DMBYTE2          SAVE ACTION QUALIFIER BYTE                   
         GOTOR DMGR,DMCB,((RF),(R0)),ACCDIR,(R2),(R2)                           
         MVC   IOERR,8(R1)                                                      
         MVC   IS,ACCKSTA-ACCRECD(R2)                                           
         MVC   DA,ACCKDA-ACCRECD(R2)                                            
         MVC   0(L'DA,R3),DA       SAVE DA AND IS STATUS                        
         MVC   L'DA+L'WK(L'IS,R3),IS                                            
         TM    IOCTL,IOCFIL        TEST WANT ACCMST/ACCARC RECORD TOO           
         BZ    DMRALLX                                                          
         IC    RF,DMBYTE2          RESTORE SAVED ACTION QUALIFIER               
         TM    IOERR,IOEBAD        TEST I/S I/O WAS OK                          
         BZ    DMRAGR2             YES - ISSUE GETREC                           
         B     DMRALLX                                                          
                                                                                
DMRAGRU  LHI   RF,X'08'            READ DELETES - NO LOCKING                    
DMRAGR   MVC   DA,0(R3)            EXTRACT DA AND IS VALUES                     
         MVC   IS,L'DA+L'WK(R3)                                                 
                                                                                
DMRAGR2  LA    R0,ACCMST           POINT TO CORRECT FILE NAME                   
         TM    IS,TRNSARCH                                                      
         BZ    *+8                                                              
         LA    R0,ACCARC                                                        
         GOTOR DMGR,DMCB,((RF),DMGETR),(R0),DA,(R2),WK                          
         BE    *+6                                                              
         DC    H'0'                                                             
         CLC   ACTRLEN-ACTRECD(L'ACTRLEN,R2),=Y(IOAREAL)                        
         BNH   *+8                 IS RECORD BIGGER THAN I/O AREA               
         OI    8(R1),IOERNF        THEN TURN ON NOT FOUND                       
         MVC   IOERR,8(R1)         AND SET IOERR                                
         B     DMRNEWX                                                          
                                                                                
DMRAGRT  MVC   DA,0(R3)            GETREC FOR TIMTRN ONLY                       
         MVC   IS,L'DA+L'WK(R3)                                                 
         LA    R0,ACCMST           POINT TO CORRECT FILE NAME                   
         TM    IS,TRNSARCH                                                      
         BZ    *+8                                                              
         LA    R0,ACCARC                                                        
*        LHI   RF,X'08'                                                         
         GOTOR DMGR,DMCB,((RF),DMGETR),(R0),DA,(R2),WK                          
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   IOERR,8(R1)         AND SET IOERR                                
         B     DMRNEWX                                                          
                                                                                
DMRAPR   TM    IOCTL,IOCFIL        TEST FILE I/O REQUIRED                       
         BZ    DMRAWD                                                           
         MVC   DA,0(R3)            EXTRACT DA, IS AND WK VALUES                 
         MVC   WK,L'DA(R3)                                                      
         MVC   IS,L'DA+L'WK(R3)                                                 
                                                                                
DMRAPR2  LA    R0,DMPUTR                                                        
*&&UK*&& TM    IS,TRNSARCH         TEST OLD RECORD ON ARCHIVE                   
*&&UK*&& BZ    DMRAPR4             NO                                           
*&&UK*&& GOTOR VPROMOTE,DMCB,(R2),TRNCOMF  YES PROMOTE RECORD                   
*&&UK*&& B     DMRNEWX                                                          
DMRAPR4  GOTOR DMGR,DMCB,(X'88',(R0)),ACCMST,DA,(R2),WK                         
         MVC   IOERR,8(R1)                                                      
         TM    IOCTL,IOCDIR        TEST FILE I/O ONLY                           
         BZ    DMRNEWX                                                          
         CLC   IS,ACCRSTA-ACCRECD(R2)                                           
         BE    DMRNEWX                                                          
         MVC   IS,ACCRSTA-ACCRECD(R2)                                           
         MVC   KEY(L'ACCKEY),0(R2)                                              
         MVC   KEY+(ACCKSTA-ACCRECD)(L'ACCKSTA),IS                              
         MVC   KEY+(ACCKDA-ACCRECD)(L'ACCKDA),DA                                
         GOTOR DMGR,(R1),(X'88',DMWRT),ACCDIR,KEY,KEY                           
         MVC   IOERR,8(R1)                                                      
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
         B     DMRNEWX                                                          
                                                                                
DMRAAR   TM    IOCTL,IOCFIL        TEST ADDING TO FILE                          
         BZ    DMRAAD                                                           
         TM    ACCRSTA-ACCRECD(R2),TRNSARCH                                     
         BZ    *+6                                                              
         DC    H'0'                CAN'T ISSUE ADDREC TO ACCARC                 
         GOTOR DMGR,DMCB,(X'88',DMADDR),ACCMST,DA,(R2),WK                       
         MVC   IOERR,8(R1)                                                      
         MVC   IS,ACCRSTA-ACCRECD(R2)                                           
                                                                                
DMRNEWX  MVC   0(L'DA,R3),DA       RETURN VALUES IN SAVE AREA                   
         MVC   L'DA(L'WK,R3),WK                                                 
         MVC   L'DA+L'WK(L'IS,R3),IS                                            
         B     DMRALLX                                                          
                                                                                
DMRAAD   GOTOR DMGR,DMCB,(X'88',DMADD),ACCDIR,(R2),(R2),0                       
         MVC   IOERR,8(R1)                                                      
         B     DMRALLX                                                          
                                                                                
DMRAWD   GOTOR DMGR,DMCB,(X'88',DMWRT),ACCDIR,(R2),(R2),0                       
         MVC   IOERR,8(R1)                                                      
         B     DMRALLX                                                          
                                                                                
DMRUNLK  GOTOR DMGR,DMCB,(X'08',DMUNLK),ACCDIR,(R2),(R2),0                      
         MVC   IOERR,8(R1)                                                      
         B     DMRALLX                                                          
                                                                                
DMRALLX  CLI   IOERR,0             SET CC FOR CALLER (EQUAL=OK)                 
         J     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO CALL DATAMGR AND CALLER'S I/O HOOK                       *         
***********************************************************************         
                                                                                
DMGR     LR    R0,RE                                                            
         MVI   8(R1),0             RESET ERROR BYTES                            
         MVI   DMBYTE1,0                                                        
         TM    IOCTL,IOCUPD        TEST UPDATIVE I/O                            
         BZ    *+12                                                             
         TM    TRNINDS,TRNIWRNO    TEST WRITE=NO SET                            
         BNZ   DMGR02                                                           
         GOTOR VDATAMGR            CALL DATAMGR                                 
         MVC   DMBYTE1,8(R1)                                                    
         NI    DMBYTE1,X'6D'       TEST SERIOUS ERRORS                          
         BZ    *+6                                                              
         DC    H'0'                                                             
         MVC   DMBYTE1,8(R1)                                                    
         TM    IOCTL,IOCUPD        TEST UPDATIVE I/O COMMAND                    
         BZ    DMGR02                                                           
         TM    DMBYTE1,X'80'       TEST END OF FILE                             
         BZ    DMGR02                                                           
         DC    H'0'                DIE IF EOF ON UPDATIVE I/O                   
                                                                                
DMGR02   TM    TRNINDS2,TRNIHKAL   TEST TRACING ALL I/O CALLS                   
         BNZ   *+12                                                             
         TM    IOCTL,IOCUPD        TEST UPDATIVE I/O COMMAND                    
         BZ    DMGRX                                                            
         OC    TRNHOOK,TRNHOOK     TEST CALLER SUPPLIED HOOK                    
         BZ    DMGRX                                                            
                                                                                
         LA    RE,DMGRX            SET RETURN ADDRESS                           
         NTR1  LABEL=NO            SAVE CURRENT REGISTERS                       
         L     RE,SAVERD                                                        
         LA    R0,DMCB             R0=A(DMCB)                                   
         ICM   RF,15,TRNHOOK       RF=A(CALLER'S HOOK ROUTINE)                  
         LM    R1,RC,24(RE)        RESTORE R1 THRU RC                           
         BASR  RE,RF               GO TO CALLER'S I/O HOOK                      
         J     EXIT                                                             
                                                                                
DMGRX    LR    RE,R0               RETURN TO CALLER                             
         BR    RE                                                               
         DROP  RB                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO VALIDATE A PWOS DATE                                     *         
***********************************************************************         
                                                                                
VALDAT   STM   RE,R1,12(RD)                                                     
         CLI   0(R1),X'70'         SET CURRENT YEAR IF BAD                      
         JNL   *+14                                                             
         MVC   0(1,R1),TRNPDAT1                                                 
         J     *+12                AND CHECK DATE                               
         TM    TRNINDS1,TRNIVDAT   TEST DON'T CHECK DATE                        
         JNZ   VALDATY                                                          
         SR    RF,RF                                                            
         ICM   RF,14,0(R1)         RF=X'YYMMDDXX'                               
         SLDL  RE,4                SHIFT OFF DECADE                             
         LHI   R0,5                R2=CHECK DIGIT COUNT                         
         SR    RE,RE                                                            
         SLDL  RE,4                                                             
         CHI   RE,9                X'A' THRU X'F' NOT ALLOWED                   
         JH    VALDATN                                                          
         BRCT  R0,*-14                                                          
         CLI   1(R1),X'01'         MONTH CAN'T BE ZERO                          
         JL    VALDATN                                                          
         CLI   1(R1),X'12'         OR GREATER THAN 12                           
         JH    VALDATN                                                          
         CLI   2(R1),X'01'         DAY CAN'T BE ZERO                            
         JL    VALDATN                                                          
         CLI   2(R1),X'31'         OR GREATER THAN 31                           
         JH    VALDATN                                                          
VALDATY  CR    R1,R1               SET CC EQUAL IF OK                           
         J     VALDATX                                                          
VALDATN  LTR   R1,R1               SET CC NOT EQUAL ON ERROR                    
VALDATX  LM    RE,R1,12(RD)                                                     
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO SORT MSA ELEMENT ON ACCOUNT RECORD                       *         
*                                                                     *         
* EXIT - CC SET NOT NOT EQUAL ON ERROR                                *         
***********************************************************************         
                                                                                
         USING ACTRECD,R5          R5=A(ACCOUNT RECORD)                         
SRTMSA   NTR1  BASE=*,LABEL=*                                                   
         LA    R1,ACTRECD                                                       
         AH    R1,DATADISP                                                      
         SR    R0,R0                                                            
         SR    RE,RE                                                            
         XC    FULL,FULL                                                        
SRTMSA02 IC    R0,1(R1)            BUMP TO NEXT ELEMENT ON RECORD               
         AR    R1,R0                                                            
         CLI   0(R1),0             TEST END OF RECORD                           
         BE    SRTMSA04                                                         
         CLI   0(R1),MSAELQ        TEST MONTHLY SALARY ELEMENT                  
         BNE   SRTMSA02                                                         
         AHI   RE,1                BUMP NUMBER OF SALARY ELEMENTS               
         OC    FULL,FULL                                                        
         BNZ   *+8                                                              
         ST    R1,FULL             SAVE A(FIRST SALARY ELEMENT)                 
         B     SRTMSA02                                                         
                                                                                
SRTMSA04 ICM   R1,15,FULL          R1=A(FIRST ELEMENT)                          
         JZ    EXITY                                                            
         LTR   R0,RE               R0=NUMBER OF ELEMENTS                        
         JZ    EXITY                                                            
         SHI   R0,1                                                             
         JZ    EXITY               EXIT IF ONLY ONE ELEMENT ON RECORD           
                                                                                
SRTMSA06 LA    RF,MSALNQ(R1)       SORT ELEMENTS INTO DESCENDING SEQ            
         LR    RE,R0                                                            
SRTMSA08 CLC   MSABEG-MSAELD(MSASTAT-MSABEG,R1),MSABEG-MSAELD(RF)               
         BNL   *+22                                                             
         XC    0(MSALNQ,R1),0(RF)                                               
         XC    0(MSALNQ,RF),0(R1)                                               
         XC    0(MSALNQ,R1),0(RF)                                               
         AHI   RF,MSALNQ                                                        
         BCT   RE,SRTMSA08                                                      
         AHI   R1,MSALNQ                                                        
         BCT   R0,SRTMSA06                                                      
                                                                                
         L     R2,FULL                                                          
         USING MSAELD,R2           SET END DATES IF NOT ALREADY SET             
SRTMSA10 CLI   MSAEL,MSAELQ                                                     
         JNE   EXITY                                                            
         MVC   WORK(L'MSABEG),MSABEG                                            
         MVI   WORK+L'MSABEG,1                                                  
         GOTOR VDATCON,PARM,(1,WORK),(0,WORK+3)                                 
         GOTOR VADDAY,PARM,WORK+3,WORK+9,-1                                     
         GOTOR VDATCON,PARM,(0,WORK+9),(1,WORK)                                 
         LR    R1,R2                                                            
SRTMSA12 AHI   R1,MSALNQ                                                        
         CLI   0(R1),MSAELQ                                                     
         BNE   SRTMSA14                                                         
         CLC   MSATYPE,MSATYPE-MSAELD(R1)                                       
         BNE   SRTMSA12                                                         
         OC    MSAEND-MSAELD(,R1),MSAEND-MSAELD(R1)                             
         BZ    *+14                                                             
         CLC   MSAEND-MSAELD(,R1),WORK                                          
         BNH   SRTMSA14                                                         
         CLC   MSABEG-MSAELD(,R1),WORK                                          
         BH    SRTMSA14                                                         
         MVC   MSAEND-MSAELD(,R1),WORK                                          
SRTMSA14 AHI   R2,MSALNQ                                                        
         B     SRTMSA10                                                         
         DROP  R2,R5,RB                                                         
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO UPDATE SAPRECD STATUS                                    *         
*                                                                     *         
* NTRY - R5=A(ACCOUNT RECORD)                                         *         
***********************************************************************         
*&&UK                                                                           
         USING ACTRECD,R5                                                       
UPDSAP   NTR1  LABEL=*                                                          
         CLC   PRODUL,ACTKUNT      TEST PRODUCTION JOB                          
         JNE   EXIT                                                             
         USING SAPRECD,KEY                                                      
         XC    SAPKEY,SAPKEY       YES - REFRESH SAPRECD STATUS                 
         MVI   SAPKTYP,SAPKTYPQ                                                 
         MVC   SAPKCUL,ACTKCPY                                                  
         MVI   SAPKSUB,SAPKSUBQ                                                 
         MVI   SAPKLVL,3                                                        
         MVC   SAPKACT,ACTKACT                                                  
         GOTOR READDIR,PARM,SAPRECD                                             
         CLI   IOERR,0             TEST RECORD NOT FOUND                        
         JNE   EXIT                                                             
         MVC   SAPKSTAT,ACTRSTAT                                                
         GOTOR WRITEDIR,PARM,SAPRECD                                            
         J     EXIT                                                             
         DROP  R5                                                               
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO TEST FOR A TRANSACTION REVERSAL                          *         
*                                                                     *         
* EXIT - CC=NON ZERO IF TRANSACTION IS A DUPLICATE, ZERO IF NOT       *         
***********************************************************************         
                                                                                
REVTRN   NTR1  BASE=*,LABEL=*                                                   
         NI    INDS2,FF-(INDFCURR+INDXSTAT)                                     
         TM    TRNMODE,TRNMREVS    TEST THIS IS A REVERSAL POSTING              
         BNZ   REVTRN00                                                         
         TM    INDS1,INDPAYAC      TEST PAYABLE ACCOUNT                         
         BZ    REVTRN00                                                         
         TM    TRNSTAT,TRNSDR      TEST THIS IS A DEBIT POSTING                 
         BZ    REVTRN00                                                         
         OI    INDS1,INDMATCH      SET MATCHED TO STOP COMING HERE              
         B     REVTRNX2                                                         
                                                                                
REVTRN00 LA    R2,REVMATCH.TRNKEY                                               
         AH    R2,DATADISP         R2=A(TRANSACTION ELEMENT)                    
REVELEM  USING TRNELD,R2                                                        
         CLI   0(R2),TRNELQ        THIS MUST BE A TRANSACTION                   
         BNE   REVTRNX                                                          
         TM    REVELEM.TRNSTAT,TRNSREV                                          
         BNZ   REVTRNX                                                          
         GOTOR GETTRS,TRNRECD      LOCATE/ADD TRANSACTION STATUS                
         ST    R1,ELADDR3                                                       
         GOTOR GETTRS,REVMATCH.TRNKEY                                           
         ST    R1,ELADDR4                                                       
                                                                                
         XC    ROFKREF,ROFKREF                                                  
         LA    R1,TRNELD                                                        
         USING FFTEL,R1                                                         
         XR    R0,R0                                                            
REVTRN01 IC    R0,FFTLN                                                         
         AR    R1,R0                                                            
         CLI   FFTEL,0                                                          
         BE    REVTRN02                                                         
         CLI   FFTEL,FFTELQ                                                     
         BNE   REVTRN01                                                         
         CLI   FFTTYPE,FFTTKREF                                                 
         BNE   REVTRN01                                                         
         MVC   ROFKREF,FFTDATA                                                  
                                                                                
REVTRN02 LA    R1,REVMATCH.TRNKEY                                               
         AH    R1,DATADISP                                                      
REVTRN03 IC    R0,FFTLN                                                         
         AR    R1,R0                                                            
         CLI   FFTEL,0                                                          
         BE    REVTRN04                                                         
         CLI   FFTEL,FFTELQ                                                     
         BNE   REVTRN03                                                         
         CLI   FFTTYPE,FFTTKREF                                                 
         BNE   REVTRN03                                                         
         CLC   SAVKREF,FFTDATA                                                  
         BE    REVTRN06                                                         
         CLC   ROFKREF,FFTDATA                                                  
         BE    REVTRN06                                                         
         CLC   TRNKREF,FFTDATA                                                  
         BE    REVTRN06                                                         
         B     REVTRNX                                                          
                                                                                
REVTRN04 OC    SAVKREF,SAVKREF                                                  
         BZ    REVTRN05                                                         
         CLC   SAVKREF,REVMATCH.TRNKREF                                         
         BNE   REVTRNX                                                          
         B     REVTRN06                                                         
                                                                                
REVTRN05 OC    ROFKREF,ROFKREF                                                  
         BZ    REVTRN06                                                         
         CLC   ROFKREF,REVMATCH.TRNKREF                                         
         BNE   REVTRNX                                                          
         DROP  R1                                                               
                                                                                
REVTRN06 LM    RE,RF,ELADDR3       TEST MATCHING ACCRUAL STATUS BITS            
         MVC   BYTE1,TRSSTAT2-TRSELD(RE)                                        
         OC    BYTE1,TRSSTAT2-TRSELD(RF)                                        
         TM    BYTE1,TRSSACRL+TRSSACRV                                          
         BO    REVTRNX                                                          
                                                                                
         LA    R1,TRNELD           LOCATE TRANSACTION EXTRA STATUS              
         USING TRXELD,R1           ELEMENT ON NEW POSTING                       
         SR    R0,R0                                                            
         XC    AELEMS(AELEML),AELEMS                                            
REVTRN07 IC    R0,TRXLN                                                         
         AR    R1,R0                                                            
         CLI   TRXEL,0                                                          
         BE    REVTRN08                                                         
         CLI   TRXEL,TRXELQ                                                     
         BNE   *+8                                                              
         ST    R1,ATRXEL           SAVE A(EXTRA STATUS ELEMENT)                 
         CLI   TRXEL,PRTELQ                                                     
         BNE   *+8                                                              
         ST    R1,APRTEL           SAVE A(PRODUCTION RATE ELEMENT)              
         CLI   TRXEL,AFCELQ                                                     
         BNE   *+8                                                              
         ST    R1,AAFCEL           SAVE A(FOREIGN CURRENCY ELEMENT)             
         CLI   TRXEL,MXPELQ                                                     
         BNE   *+8                                                              
         ST    R1,AMXPEL           SAVE A(MEDIA EXTRA PAYMENT ELEMENT)          
         CLI   TRXEL,FFNELQ                                                     
         BNE   *+8                                                              
         ST    R1,AFFNEL           SAVE A(FREE FORM NUMBER ELEMENT)             
*&&UK                                                                           
         CLI   TRXEL,FFTELQ                                                     
         BNE   *+8                                                              
         CLI   TRXELD+(FFTTYPE-FFTELD),FFTTAART                                 
         BNE   *+8                                                              
         ST    R1,AFFTEL                                                        
*&&                                                                             
         B     REVTRN07                                                         
         DROP  R1                                                               
                                                                                
REVTRN08 TM    PAYINDS,PAYTDRCR    TEST DON'T REVERSE PAYABLE CREDITS           
         BZ    *+12                                                             
         TM    TRNSTAT,TRNSDR      EXIT IF TRANSACTION IS A CREDIT              
         BZ    REVTRNX                                                          
                                                                                
         GOTOR CLOTST,REVMATCH.TRNKEY                                           
         MVI   POSTINDS,POSTPRVO   SET TRANSACTION PREVIOUSLY OPEN              
         BE    *+8                                                              
         MVI   POSTINDS,POSTPRVC   SET TRANSACTION PREVIOUSLY CLOSED            
                                                                                
         TM    REVMATCH.TRNRSTAT,TRNSDRFT                                       
         BNZ   REVTRNX             CAN'T OFFSET IF DRAFT                        
         TM    POSTINDS,POSTPRVC                                                
         BNZ   REVTRNX             CAN'T OFFSET IF CLOSED                       
         TM    REVMATCH.TRNRSTA2,TRNSUSED                                       
         BNZ   REVTRN10                                                         
         TM    TRNRSTA2,TRNSUSED                                                
         BNZ   REVTRNX             CAN'T OFFSET IF ORIGINAL USED                
         B     REVTRN14                                                         
                                                                                
REVTRN10 TM    TRNINDS,TRNICONV    ENSURE NEW POSTING IS A REVERSAL TOO         
         BZ    REVTRN12                                                         
         TM    TRNRSTA2,TRNSUSED                                                
         BZ    REVTRNX                                                          
         B     REVTRN14                                                         
                                                                                
REVTRN12 OC    TRNRECD+ACCOUSED(ACCOULEN),TRNRECD+ACCOUSED                      
         BZ    REVTRNX                                                          
                                                                                
REVTRN14 ZAP   DUB,TRNAMNT                                                      
         AP    DUB,REVELEM.TRNAMNT                                              
         BNZ   REVTRNX                                                          
                                                                                
         TM    INDS2,INDPRODN      TEST PRODUCTION LEDGER                       
         BZ    REVTRN18                                                         
         CLC   BILLWC,TRNKOFF      TEST BILLING WORK CODE                       
         BNE   REVTRN18                                                         
*&&UK                                                                           
         CLI   TRNLN,121           TEST FOR SPECIAL BILLING ELEMENT             
         BNE   REVTRN18                                                         
         CLI   REVELEM.TRNLN,121                                                
         BNE   REVTRNX                                                          
         LA    RE,TRNNARR+15       TEST ALL BILL ACCUMS WASH TO ZERO            
         LA    RF,REVELEM.TRNNARR+15                                            
         LHI   R0,13                                                            
REVTRN16 ZAP   DUB,0(6,RE)                                                      
         AP    DUB,0(6,RF)                                                      
         BNZ   REVTRNX                                                          
         AHI   RE,6                                                             
         AHI   RF,6                                                             
         BCT   R0,REVTRN16                                                      
*&&                                                                             
*&&US                                                                           
         ZAP   DUB,TRNNARR+15(6)   CHECK THAT BUCKETS FOR                       
         AP    DUB,REVELEM.TRNNARR+15(6) COMMISSION, CD, + PAYABLE              
         BNZ   REVTRNX             SUM TO ZERO                                  
                                                                                
         ZAP   DUB,TRNNARR+21(6)                                                
         AP    DUB,REVELEM.TRNNARR+21(6)                                        
         BNZ   REVTRNX                                                          
                                                                                
         ZAP   DUB,TRNNARR+27(6)                                                
         AP    DUB,REVELEM.TRNNARR+27(6)                                        
         BNZ   REVTRNX                                                          
                                                                                
         GOTOR GSTCHK              CHECK GST BILLED WASHES TO ZERO              
         BNE   REVTRNX                                                          
                                                                                
         GOTOR PSTCHK              CHECK PST BILLED WASHES TO ZERO              
         BNE   REVTRNX                                                          
*&&                                                                             
REVTRN18 DS    0H                                                               
*&&UK                                                                           
         TM    TRNMODE,TRNMREVS    TEST REVERSAL TRANSACTION GIVEN              
         BNZ   REVTRN20            YES - DON'T TEST BATCH TYPE                  
         TM    INDS2,INDPRODN      TEST PRODUCTION LEDGER                       
         BNZ   REVTRN20            YES - DON'T TEST BATCH TYPES                 
         CLC   TRNTYPE,REVELEM.TRNTYPE                                          
         BNE   REVTRNX             DON'T OFFSET IF DIFFERENT TYPES              
*&&                                                                             
REVTRN20 CLI   REVELEM.TRNOFFC+0,C' '                                           
         BH    *+8                                                              
         MVI   REVELEM.TRNOFFC+0,C' '                                           
         CLI   REVELEM.TRNOFFC+1,C' '                                           
         BH    *+8                                                              
         MVI   REVELEM.TRNOFFC+1,C' '                                           
         CLC   TRNOFFC,REVELEM.TRNOFFC                                          
         BNE   REVTRNX             DON'T OFFSET IF DIFFERENT OFFICES            
                                                                                
         TM    TRNCPYS7,CPYSTMSY   TEST USING TMS SYSTEM                        
         BZ    REVTRN22                                                         
         CLI   TRNTYPE,TRNTCLTM    AND BOTH ARE BATCH TYPE 49'S                 
         BNE   REVTRN22                                                         
         CLI   REVELEM.TRNTYPE,TRNTCLTM                                         
         BNE   REVTRN22                                                         
         SR    RE,RE                                                            
         IC    RE,TRNLN                                                         
         CLM   RE,1,REVELEM.TRNLN                                               
         BNE   REVTRNX                                                          
         SHI   RE,TRNLN1Q+1                                                     
         BM    REVTRN22                                                         
         EX    RE,*+8              THEN THE NARRATIVE MUST MATCH                
         BNE   REVTRNX                                                          
         CLC   TRNNARR(0),REVELEM.TRNNARR                                       
                                                                                
REVTRN22 DS    0H                                                               
*&&US                                                                           
         CLI   TRNTYPE,TRNTEPRD    TYPES 47, 48 AND 57                          
         BE    REVTRN24            CAN ONLY OFFSET SAME TYPE                    
         CLI   TRNTYPE,TRNTEPRV                                                 
         BE    REVTRN24                                                         
         CLI   TRNTYPE,TRNTWRTO                                                 
         BE    REVTRN24                                                         
         CLI   REVELEM.TRNTYPE,TRNTEPRD                                         
         BE    REVTRN24                                                         
         CLI   REVELEM.TRNTYPE,TRNTEPRV                                         
         BE    REVTRN24                                                         
         CLI   REVELEM.TRNTYPE,TRNTWRTO                                         
         BNE   REVTRN26                                                         
REVTRN24 CLC   TRNTYPE,REVELEM.TRNTYPE                                          
         BNE   REVTRNX                                                          
*&&                                                                             
REVTRN26 TM    INDS2,INDPRODN      TEST PRODUCTION LEDGER                       
         BZ    REVTRN38            YES - DON'T TEST BILLING                     
         LR    RF,R2                                                            
         ZAP   DUB,REVELEM.TRNAMNT                                              
         SR    R0,R0                                                            
         USING BNDELD,RF           LOCATE BILLING ELEMENTS                      
REVTRN28 IC    R0,BNDLN            BUMP TO NEXT ELEMENT                         
         AR    RF,R0                                                            
         CLI   BNDEL,0             TEST EOR                                     
         BE    REVTRN38                                                         
                                                                                
         CLI   BNDEL,BNDELQ        TEST BILLING ELEMENT                         
         BNE   REVTRN30                                                         
*&&UK*&& CLC   BNDBNO,SPACES       IS ITEM BILLED OR ALLOCATED?                 
*&&US*&& OC    BNDBNO,BNDBNO                                                    
         BE    REVTRN28            IF NOT CHECK NEXT                            
         B     REVTRNX             IF IT IS WE CANNOT OFFSET                    
                                                                                
         USING TRXELD,RF                                                        
REVTRN30 CLI   TRXEL,TRXELQ        TEST TRANSACTION EXTRA STATUS                
         BNE   REVTRN32                                                         
         OI    INDS2,INDXSTAT      SET TRXEL FOUND                              
         TM    TRXSTA2,TRXSXFRP+TRXSWOFP+TRXSBILP+TRXSEXCL                      
         BNZ   REVTRNX                                                          
         MVC   BYTE1,TRXSTA1       TEST IF BOTH EXCLUDED FROM BILLING           
         MVI   BYTE2,0             (NO TRXEL - NOT EXCLUDED)                    
         ICM   RE,15,ATRXEL                                                     
         BZ    *+10                                                             
         MVC   BYTE2,TRXSTA1-TRXELD(RE)                                         
         NI    BYTE1,TRXSXALC                                                   
         NI    BYTE2,TRXSXALC                                                   
         XC    BYTE1,BYTE2         ONLY REVERSE IF BOTH ARE EXCLUDED            
         BNZ   REVTRNX                                                          
         B     REVTRN28                                                         
                                                                                
         USING PTAELD,RF                                                        
REVTRN32 CLI   PTAEL,PTAELQ        TEST NEW BILLING ELEMENT                     
         BNE   REVTRN36                                                         
         CLI   TRNTYPE,TRNTJBTX    TEST JOB-TO-JOB TRANSFER POSTING             
         BNE   REVTRN34                                                         
         ICM   RE,15,ATRXEL        TEST POSTING FROM CBILL                      
         BZ    REVTRN34                                                         
         TM    TRXSTA1-TRXELD(RE),TRXSXALC                                      
         BZ    REVTRN34                                                         
         OI    INDS2,INDFXPTA      SET PTA ELEMENT FOUND                        
         CLI   PTATYPE,PTATTRFT    TEST TRANSFERRED IN CBILL                    
         BNE   REVTRN28                                                         
         CP    DUB,PTANET          TEST FULL TRANSFER                           
         BNE   REVTRNX                                                          
         NI    INDS2,FF-(INDFXPTA)                                              
         B     REVTRN38                                                         
                                                                                
REVTRN34 CLI   PTATYPE,0           IS THERE ANY CBILL ACTIVITY                  
         BE    REVTRN28            IF NOT CHECK NEXT                            
         B     REVTRNX                                                          
                                                                                
         USING PRTELD,RF                                                        
REVTRN36 CLI   PRTEL,PRTELQ        TEST PRODUCTION CHARGE RATE ELEMENT          
         BNE   REVTRN28                                                         
         ICM   RE,15,APRTEL                                                     
         BZ    REVTRNX                                                          
         ZAP   WORK(L'DUB),PRTHOUR                                              
         AP    WORK(L'DUB),PRTHOUR-PRTELD(,RE)                                  
         BNZ   REVTRNX                                                          
         CP    PRTRATE,PRTRATE-PRTELD(,RE)                                      
         BNE   REVTRNX                                                          
         B     REVTRN28                                                         
         DROP  RF                                                               
                                                                                
REVTRN38 TM    INDS2,INDFXPTA      TEST ANY PTA ELEMENTS FOUND                  
         BNZ   REVTRNX                                                          
         MVI   BYTE1,TRNSDR        ENSURE MATCHING STATUS BITS                  
         MVC   BYTE2,BYTE1                                                      
         NC    BYTE1,TRNSTAT                                                    
         NC    BYTE2,REVELEM.TRNSTAT                                            
         CLC   BYTE1,BYTE2         BOTH MUST BE DEBITS OR CREDITS               
         BNE   REVTRNX                                                          
                                                                                
         CLI   TRNTYPE,TRNTJBTX    DON'T MATCH STATUS FOR J-J TRANSFERS         
         BE    REVTRN40                                                         
         TM    INDS2,INDXSTAT      TEST EXTRA STATUS ELEMENT FOUND              
         BNZ   REVTRN40                                                         
         ICM   RE,15,ATRXEL        IF NEITHER HAVE TRXEL WE CAN REVERSE         
         BZ    REVTRN40                                                         
         TM    TRXSTA1-TRXELD(RE),TRXSXALC TEST EXCLUDE FROM BILLING            
         BNZ   REVTRNX                                                          
                                                                                
REVTRN40 TM    TRNMODE,TRNMREVS    TEST REVERSAL TRANSACTION GIVEN              
         BNZ   REVTRN42                                                         
*&&UK*&& MVI   BYTE1,FF-(TRNSURG)  MATCH OTHER STATUS BITS                      
*&&US*&& MVI   BYTE1,FF-(TRNSURG+TRNSAUTH+TRNSHOLD)                             
         MVC   BYTE2,BYTE1                                                      
         NC    BYTE1,TRNSTAT                                                    
         NC    BYTE2,REVELEM.TRNSTAT                                            
         CLC   BYTE1,BYTE2         TEST TRANSACTION BITS MATCH REVERSAL         
         BNE   REVTRNX                                                          
                                                                                
REVTRN42 LA    RF,REVMATCH.TRNKEY  ESTABLISH MOS OF DUPLICATE POSTING           
         AH    RF,DATADISP                                                      
         GOTOR VCONVMOS,DMCB,(X'FE',(RF)),MOS2                                  
*&&US*&& TM    INDS2,INDPRODN      TEST PRODUCTION LEDGER                       
*&&US*&& BNZ   REVTRN48            YES - NO MOS CHECK IN USA                    
                                                                                
REVTRN44 OC    MOSREV,MOSREV       TEST REVERSAL MONTH SET                      
         BZ    REVTRN46                                                         
         CLC   MOSREV,MOS2         TEST THIS MATCHES REVERSING MONTH            
         BNE   REVTRNX                                                          
         B     REVTRN48                                                         
                                                                                
REVTRN46 TM    INDS2,INDPRODN      TEST PRODUCTION LEDGER                       
         BNZ   REVTRN48                                                         
         CLC   MOS,MOS2            MATCH MONTHS OF SERVICE                      
         BNE   REVTRNX                                                          
                                                                                
REVTRN48 LA    R1,REVELEM.TRNELD   R1=A(FIRST ELEMENT ON REVERSAL)              
         SR    RF,RF                                                            
         SR    R0,R0                                                            
REVTRN50 IC    RF,1(R1)            BUMP TO NEXT ELEMENT                         
         AR    R1,RF                                                            
         CLI   0(R1),0             TEST EOR                                     
         BE    REVTRN68                                                         
         LA    RE,TRNELD           RE=A(FIRST ELEMENT ON NEW RECORD)            
         CLI   0(R1),SCIELQ        TEST SUBSIDIARY CASH ELEMENT                 
         BE    REVTRN56                                                         
         CLI   0(R1),AFCELQ        TEST FOREIGN CURRENCY ELEMENT                
         BE    REVTRN60                                                         
*&&UK*&& CLI   0(R1),MXPELQ        TEST MEDIA EXTRA PAYMENT ELEMENT             
*&&UK*&& BE    REVTRN62                                                         
*&&UK*&& CLI   0(R1),FFNELQ        TEST FREE FORM NUMBER ELEMENT                
*&&UK*&& BE    REVTRN64                                                         
         CLI   0(R1),OTHELQ        TEST SUBREF ELEMENT                          
         BE    REVTRN52                                                         
*&&UK*&& CLI   0(R1),DUEELQ        TEST DUE DATE ELEMENT                        
*&&UK*&& BE    REVTRN52                                                         
         CLI   0(R1),CPJELQ        TEST BILLING SOURCE ELEMENT                  
         BE    REVTRN52                                                         
         CLI   0(R1),MPYELQ        TEST PAYMENT ELEMENT                         
         BE    REVTRN52                                                         
*&&UK*&& CLI   0(R1),FFTELQ        TEST FREE-FORM TEXT ELEMENT                  
*&&UK*&& BE    REVTRN66                                                         
         CLI   0(R1),APEELQ        TEST ANALYSIS POINTER ELEMENT                
         BNE   REVTRN50                                                         
*&&US                                                                           
         CLI   TRNTYPE,TRNTJBTX    IF TYPE 34                                   
         BNE   REVTRN52                                                         
         CLI   REVELEM.TRNTYPE,TRNTCLTM AND OFFSETTING 49                       
         BE    REVTRN50                                                         
         CLI   REVELEM.TRNTYPE,TRNTMEPY OR OFFSETTING 10                        
         BE    REVTRN50            IGNORE APEEL                                 
*&&                                                                             
REVTRN52 TM    TRNMODE,TRNMREVS    TEST REVERSAL TRANSACTION GIVEN              
         BNZ   REVTRN50                                                         
         IC    RF,1(R1)            HANDLE OTHEL/CPJEL/APEEL                     
         BCTR  RF,0                RF=ELEMENT LENGTH-1                          
REVTRN54 IC    R0,1(RE)            LOCATE ELEMENT IN NEW TRANSACTION            
         AR    RE,R0                                                            
         CLI   0(RE),0             TEST EOR                                     
         BE    REVTRNX                                                          
         CLC   0(1,RE),0(R1)       MATCH ON ELEMENT CODE                        
         BNE   REVTRN54                                                         
         CLI   0(R1),MPYELQ        FUDGE COMPARE LENGTH FOR PAYMENTS            
         BNE   *+8                                                              
         LHI   RF,MPYLNQ-1                                                      
         EX    RF,*+12                                                          
         BE    REVTRN50                                                         
         B     REVTRNX                                                          
         CLC   0(0,RE),0(R1)                                                    
                                                                                
REVTRN56 TM    INDS2,INDPRODN      TEST PRODUCTION LEDGER                       
         BZ    REVTRN58                                                         
         CLI   SCITYPE-SCIELD(R1),SCITFORD                                      
         BE    REVTRN50                                                         
         CLI   SCITYPE-SCIELD(R1),SCITPORD                                      
         BE    REVTRN50                                                         
         CLI   SCITYPE-SCIELD(R1),SCITPART                                      
         BE    REVTRN50                                                         
         CLI   SCITYPE-SCIELD(R1),SCITCPAD              NMAL                    
         BE    REVTRN50                                 NMAL                    
         CLI   SCITYPE-SCIELD(R1),SCITCPAT              NMAL                    
         BE    REVTRN50                                 NMAL                    
                                                                                
         USING SCIELD,RE                                                        
REVTRN58 IC    R0,SCILN            HANDLE SCIEL                                 
         AR    RE,R0                                                            
         CLI   SCIEL,0             TEST EOR                                     
         BE    REVTRNX                                                          
         CLI   SCIEL,SCIELQ        TEST ELEMENT CODE/LENGTH/CASH TYPE           
         BNE   REVTRN58                                                         
         CLC   SCILN(L'SCILN+L'SCITYPE),SCILN-SCIELD(R1)                        
         BNE   REVTRN58                                                         
         ZAP   DUB,SCIGRS                                                       
         AP    DUB,SCIGRS-SCIELD(L'SCIGRS,R1)                                   
         BNZ   REVTRNX                                                          
         CLI   SCILN,SCILN2Q       TEST TWO CASH AMOUNTS                        
         BL    REVTRN50                                                         
         ZAP   DUB,SCINET                                                       
         AP    DUB,SCINET-SCIELD(L'SCINET,R1)                                   
         BNZ   REVTRNX                                                          
         B     REVTRN50                                                         
         DROP  RE                                                               
                                                                                
         USING AFCELD,RE                                                        
REVTRN60 ICM   RE,15,AAFCEL        HANDLE FOREIGN CURRENCY ELEMENTS             
         BZ    REVTRNX                                                          
         CLC   AFCCURR,AFCCURR-AFCELD(R1)                                       
         BNE   REVTRNX                                                          
         ZAP   DUB,AFCAMNT                                                      
         AP    DUB,AFCAMNT-AFCELD(L'AFCAMNT,R1)                                 
         BNZ   REVTRNX                                                          
         OI    INDS2,INDFCURR      SET TRANSACTIONS IN CURRENCY                 
         B     REVTRN50                                                         
         DROP  RE                                                               
                                                                                
         USING MXPELD,RE                                                        
REVTRN62 ICM   RE,15,AMXPEL        HANDLE MEDIA EXTRA PAYMENT ELEMENTS          
         BZ    REVTRNX                                                          
         CLC   MXPSER,MXPSER-MXPELD(R1)                                         
         BNE   REVTRNX                                                          
         B     REVTRN50                                                         
                                                                                
         USING FFNELD,RE                                                        
REVTRN64 ICM   RE,15,AFFNEL        HANDLE FREE FORM NUMBER ELEMENT              
         BZ    REVTRNX                                                          
         CLC   FFNONUM,FFNONUM-FFNELD(R1)                                       
         BNE   REVTRNX                                                          
         B     REVTRN50                                                         
*&&UK                                                                           
         USING FFTELD,RE                                                        
REVTRN66 CLI   FFTTYPE,FFTTAART    TEST ARTIST/AGENT ACCOUNT CODE               
         BNE   REVTRN50                                                         
         ICM   RE,15,AFFTEL                                                     
         BZ    REVTRNX                                                          
         CLC   FFTDATA(L'ACTKULA),FFTDATA-FFTELD(R1)                            
         BNE   REVTRNX                                                          
         B     REVTRN50                                                         
                                                                                
REVTRN68 TM    INDS2,INDFCURR      TEST TRANSACTIONS IN CURRENCY                
         BNZ   *+14                                                             
         OC    AAFCEL,AAFCEL       EXIT IF NEW ONE ONLY HAS AN AFCEL            
         BNZ   REVTRNX                                                          
                                                                                
         TM    INDS2,INDOCAEL      TEST SECOND CURRENCY SUPPORT                 
         BZ    REVTRN90                                                         
         LA    RF,WORKD            YES - BUILD LIST OF CASH VALUES              
         AHI   RF,REVOCA-WORKD                                                  
         GOTOR VTOBACCO,PARM,('TOBAALST',WORK),(0,REVMATCH.TRNKEY),    *        
               TRNCOMF,(RF)                                                     
                                                                                
         LA    RE,WORKD                                                         
         AHI   RE,RECOCA-WORKD                                                  
REVTRN70 CLI   0(RE),0             TEST END OF RECORD LIST                      
         BE    REVTRN86                                                         
                                                                                
         LA    RF,WORKD            LOOK UP VALUE IN REVERSAL LIST               
         AHI   RF,REVOCA-WORKD                                                  
         SR    R0,R0                                                            
REVTRN72 CLI   0(RF),0             VALUE NOT FOUND                              
         BNE   REVTRN74                                                         
         CP    2(8,RE),PZERO       ALLOW IF ZERO VALUE                          
         BE    REVTRN84                                                         
         LTR   RF,R0               TEST ADDRESS SET                             
         BNZ   REVTRN78            YES - TEST AMOUNTS                           
         B     REVTRNX             ELSE EXIT                                    
                                                                                
REVTRN74 CLC   0(1,RE),0(RF)       MATCH TYPE                                   
         BNE   REVTRN76                                                         
         CLC   1(1,RE),1(RF)       MATCH SEQUENCE                               
         BE    REVTRN78                                                         
         LTR   R0,R0               TEST ADDRESS ALREADY SET                     
         BNZ   REVTRN76                                                         
         LR    R0,RF               NO - SET AS ADDRESS OF FIRST FOUND           
                                                                                
REVTRN76 AHI   RF,L'REVOCA         BUMP TO NEXT IN REVERSAL LIST                
         B     REVTRN72                                                         
                                                                                
REVTRN78 CLI   0(RE),QMPYAMNT      TEST QMPYAMNT/QMPYPART                       
         BE    *+12                                                             
         CLI   0(RE),QMPYPART                                                   
         BNE   REVTRN80                                                         
         CP    2(8,RE),2(8,RF)     YES - MATCH AMOUNTS                          
         BNE   REVTRNX                                                          
         B     REVTRN82                                                         
                                                                                
REVTRN80 ZAP   DUB,2(8,RE)         ALL OTHER VALUES MUST BE REVERSED            
         AP    DUB,2(8,RF)                                                      
         BNZ   REVTRNX                                                          
                                                                                
REVTRN82 MVI   0(RF),X'FF'                                                      
                                                                                
REVTRN84 MVI   0(RE),X'FF'         MARK BOTH AS USED                            
         AHI   RE,L'RECOCA         BUMP TO NEXT VALUE                           
         B     REVTRN70                                                         
                                                                                
REVTRN86 LA    RF,WORKD            LOOK UP VALUE IN REVERSAL                    
         AHI   RF,REVOCA-WORKD                                                  
REVTRN88 CLI   0(RF),0             TEST END OF LIST                             
         BE    REVTRN90                                                         
         CLI   0(RF),X'FF'         TEST MATCHED ENTRY                           
         BE    *+14                NO - DIFFERENCE FOUND                        
         CP    2(8,RF),PZERO       TEST NON-ZERO VALUE                          
         BNE   REVTRNX                                                          
         AHI   RF,L'REVOCA                                                      
         B     REVTRN88                                                         
*&&                                                                             
*&&US                                                                           
REVTRN68 DS    0H                                                               
*&&                                                                             
REVTRN90 OI    INDS1,INDMATCH      SET TRANSACTION IS MATCHED                   
         ICM   R1,15,TRNTRNU       INCREMENT REVERSAL COUNT                     
         AHI   R1,1                                                             
         STCM  R1,15,TRNTRNU                                                    
                                                                                
         GOTOR READDIR,REVMATCH.TRNKEY                                          
         BE    *+6                                                              
         DC    H'0'                                                             
         GOTOR GET,REVMATCH.TRNKEY GET (& LOCK) FILE RECORD                     
         BE    *+6                                                              
         DC    H'0'                                                             
                                                                                
         LA    R2,REVMATCH.TRNKEY                                               
         AH    R2,DATADISP                                                      
         OI    TRNSTAT,TRNSREV     SET REVERSAL IN TRANSACTION ELEMENTS         
         TM    TRNINDS,TRNICONV    TEST NEW RECORD PASSED                       
         BZ    *+8                                                              
         OI    TRNRSTAT,TRNSREVS                                                
         OI    REVELEM.TRNSTAT,TRNSREV                                          
         OI    REVMATCH.TRNRSTAT,TRNSREVS                                       
         NI    TRNSTAT,FF-TRNSHOLD UNSET HOLD BILLING                           
         NI    REVELEM.TRNSTAT,FF-TRNSHOLD                                      
                                                                                
         L     R1,ELADDR3          UPDATE REVERSAL DATE IN BOTH RECORDS         
         USING TRSELD,R1                                                        
         MVC   TRSREVD,TRNPDAT2                                                 
         XC    TRSRMOS,TRSRMOS                                                  
         CLC   MOS,MOS2            TEST MONTHS OF SERVICE MATCH                 
         BE    *+10                                                             
         MVC   TRSRMOS,MOS2        NO - POST REVERSAL MOS IN ELEMENT            
                                                                                
         GOTOR GETTRS,REVMATCH.TRNKEY                                           
         MVC   TRSREVD,TRNPDAT2                                                 
         XC    TRSRMOS,TRSRMOS                                                  
         CLC   MOS,MOS2            TEST MONTHS OF SERVICE MATCH                 
         BE    *+10                                                             
         MVC   TRSRMOS,MOS         NO - POST REVERSAL MOS IN ELEMENT            
                                                                                
         GOTOR WRTTRN              WRITE BACK MATCHED REVERAL RECORD            
         DROP  R1                                                               
                                                                                
REVTRNX  TM    INDS1,INDMATCH      SET CC=NON ZERO IF MATCHED                   
         J     EXIT                                                             
                                                                                
REVTRNX2 CR    RE,RE               FUDGE EXIT WITH CC=EQUAL                     
         J     EXIT                                                             
         DROP  RB,RE                                                            
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO MATCH PAYABLE DEBIT POSTING TO CREDIT ON FILE            *         
***********************************************************************         
                                                                                
PAYTRN   TM    TRNINDS,TRNIDRFT    TEST ADDED A LIVE TRANSACTION                
         BNZR  RE                  NO                                           
         TM    INDS1,INDPAYCR      TEST LOOKING FOR MATCHING CREDIT             
         BZR   RE                                                               
                                                                                
PAYMATCH USING TRNRECD,R4                                                       
PAYTRNN  NTR1  BASE=*,LABEL=*                                                   
         ICM   R1,15,TRNTRND       INCREMENT PAYABLE DEBITS FOUND               
         AHI   R1,1                                                             
         STCM  R1,15,TRNTRND                                                    
         MVC   PAYMATCH.TRNKEY,TRNKEY                                           
         ICM   R1,1,TRNKSBR        BUILD KEY OF CREDIT POSTING                  
         BZ    *+10                                                             
         BCTR  R1,0                                                             
         STC   R1,PAYMATCH.TRNKSBR                                              
                                                                                
         LA    R1,TRNELD                                                        
         SR    R0,R0                                                            
         USING MPYELD,R1                                                        
         XC    AMPYEL,AMPYEL       CLEAR A(PAYMENT ELEMENT)                     
         XC    AAFCEL,AAFCEL       CLEAR A(FOREIGN ELEMENT)                     
PAYTRN02 IC    R0,MPYLN            LOCATE PAYMENT ELEMENT                       
         AR    R1,R0                                                            
         CLI   MPYEL,0             TEST EOR                                     
         BE    PAYTRN06                                                         
                                                                                
         CLI   MPYEL,MPYELQ        TEST PAYMENT ELEMENT                         
         BNE   PAYTRN04                                                         
         ST    R1,AMPYEL           SAVE A(PAYMENT ELEMENT)                      
         CLI   MPYLN,MPYLN2Q       TEST SUB-REFERENCE CARRIED                   
         BL    *+10                                                             
         MVC   PAYMATCH.TRNKSBR,MPYSUB                                          
         B     PAYTRN02                                                         
                                                                                
PAYTRN04 CLI   MPYEL,AFCELQ                                                     
         BNE   PAYTRN02                                                         
         ST    R1,AAFCEL                                                        
         B     PAYTRN02                                                         
                                                                                
PAYTRN06 OC    SAVKREF,SAVKREF                                                  
         BZ    PAYTRN08                                                         
         MVC   PAYMATCH.TRNKREF,SAVKREF                                         
                                                                                
PAYTRN08 GOTOR READ,PAYMATCH.TRNKEY                                             
         BNE   PAYTRNX                                                          
         LA    R2,PAYMATCH.TRNKEY                                               
         AH    R2,DATADISP                                                      
PAYELEM  USING TRNELD,R2                                                        
         CLI   PAYELEM.TRNEL,TRNELQ TEST FOR USED CREDIT TRANSACTION            
         BNE   PAYTRNX                                                          
         TM    PAYELEM.TRNSTAT,TRNSDR                                           
         BNZ   PAYTRNX                                                          
*&&US*&& MVC   WORK(L'TRNREF),PAYMATCH.TRNKREF                                  
                                                                                
         TM    PAYMATCH.TRNRSTAT,TRNSDRFT                                       
         BNZ   PAYTRNX                                                          
         TM    PAYMATCH.TRNRSTA2,TRNSUSED                                       
         BZ    PAYTRNX                                                          
                                                                                
         CP    TRNAMNT,PAYELEM.TRNAMNT                                          
         BE    PAYTRN12            THEY ARE EQUAL                               
         ICM   RE,15,AAFCEL        TEST ELEMENT FOUND ON DEBIT                  
         BZ    PAYTRNX                                                          
         LA    R1,PAYMATCH.TRNKEY  COMPARE FOREIGN CURRENCY AMOUNTS             
         AH    R1,DATADISP         IF EQUAL MATCH THE 2 RECS                    
         USING AFCELD,R1                                                        
         SR    R0,R0                                                            
PAYTRN10 IC    R0,AFCLN            LOCATE FOREIGN ELEMENT ON CREDIT             
         AR    R1,R0                                                            
         CLI   AFCEL,0                                                          
         BE    PAYTRNX                                                          
         CLI   AFCEL,AFCELQ        R1=A(CREDIT AFCEL)                           
         BNE   PAYTRN10                                                         
         CP    AFCAMNT,AFCAMNT-AFCELD(L'AFCAMNT,RE)                             
         BNE   PAYTRNX                                                          
                                                                                
PAYTRN12 OI    INDS1,INDPAYDR      SET MATCHING CREDIT FOUND                    
         GOTOR CLOTST,PAYMATCH.TRNKEY                                           
         MVI   POSTINDS,POSTPRVO   SET TRANSACTION PREVIOUSLY OPEN              
         BE    *+8                                                              
         MVI   POSTINDS,POSTPRVC                                                
                                                                                
         GOTOR GETTRS,TRNRECD      CROSS MATCH POSTING MOS VALUES               
         LR    R3,R1                                                            
         USING TRSELD,R3                                                        
                                                                                
         GOTOR GETTRS,PAYMATCH.TRNKEY                                           
         TM    TRNINDS,TRNICONV                                                 
         BZ    *+10                                                             
         MVC   TRNRSUSE,TRSPMOS-TRSELD(R1)                                      
         MVC   TRSUMOS,TRSPMOS-TRSELD(R1)                                       
         MVC   PAYMATCH.TRNRSUSE,TRSPMOS                                        
         MVC   TRSUMOS-TRSELD(L'TRSUMOS,R1),TRSPMOS                             
                                                                                
         ICM   RE,15,AMPYEL        TEST ELEMENT FOUND ON DEBIT                  
         BZ    PAYTRN26                                                         
         LA    R1,PAYMATCH.TRNKEY  COPY PAYMENT DETAILS FROM DR TO CR           
         AH    R1,DATADISP                                                      
         USING MPYELD,R1                                                        
         SR    R0,R0                                                            
PAYTRN14 IC    R0,MPYLN            LOCATE PAYMENT ELEMENT ON CREDIT             
         AR    R1,R0                                                            
         CLI   MPYEL,0                                                          
         BE    PAYTRN26                                                         
         CLI   MPYEL,MPYELQ        R1=A(CREDIT MPYEL)                           
         BNE   PAYTRN14                                                         
*&&UK                                                                           
         CLI   MPYLN,MPYLN2Q       FOR EUROPE MUST ENSURE LENGTH OF             
         BNL   PAYTRN16            MPYEL IS AT LEAST 2NQ                        
         MVC   WORK(MPYLNQ),MPYEL  SAVE EXISTING VALUES FOR TOBACCO DEL         
         MVI   MPYEL,DELELQ        DELETE ELEMENT                               
         GOTOR VHELLO,ELIST,(C'D',FILE),('DELELQ',PAYMATCH.TRNKEY),0,0          
         CLI   ELERR,0             CHECK TO SEE ANY ERRORS                      
         BE    *+6                                                              
         DC    H'0'                YES THEN DIE                                 
         XC    ELEMENT,ELEMENT     REBUILD ELEMENT TO NEW BIGGER LENGTH         
         LA    R1,ELEMENT                                                       
         MVC   MPYEL(MPYLNQ),WORK                                               
         MVI   MPYEL,MPYELQ                                                     
         MVI   MPYLN,MPYLN2Q       ADD ELEMENT TO RECORD                        
         GOTOR VHELLO,ELIST,(C'P',FILE),PAYMATCH.TRNKEY,ELEMENT,0               
         CLI   ELERR,0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
         L     R1,ELADDR2          GE ADDRESS OF ADDED ELEMENT                  
         ICM   RE,15,AMPYEL        TEST ELEMENT FOUND ON DEBIT                  
PAYTRN16 TM    INDS2,INDOCAEL      TEST SECOND CURRENCY SUPPORT                 
         BZ    PAYTRN18                                                         
         XC    ELEMENT,ELEMENT     BUILD FRESH CREDIT MPYEL                     
         MVC   ELEMENT(L'MPYEL+L'MPYLN),MPYELD                                  
         ST    R1,FULL             SAVE A(CREDIT MPYEL)                         
         LA    R1,ELEMENT          R1=A(FRESH CREDIT MPYEL)                     
*&&                                                                             
PAYTRN18 SR    RF,RF               COPY SIGNIFICANT DATA FROM DEBIT             
         IC    RF,MPYLN            LENGTH OF CR MPYEL                           
         CLC   MPYLN,1(RE)         IS IT LONGER THAN DR MPYEL                   
         BNH   *+8                 NO                                           
         IC    RF,1(RE)            ONLY WANT TO COPY FOR LENGTH OF DR           
         SHI   RF,MPYNO+1-MPYELD                                                
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   MPYNO(0),MPYNO-MPYELD(RE)                                        
         CLI   MPYLN,MPYLN2Q       SET DEBIT SUB-REF IN CREDIT RECORD           
         BL    *+10                                                             
         MVC   MPYSUB,TRNKSBR                                                   
*&&UK                                                                           
         TM    INDS2,INDOCAEL      TEST SECOND CURRENCY SUPPORT                 
         BZ    PAYTRN26                                                         
         L     RF,FULL             RF=A(CREDIT MPYEL)                           
         GOTOR VTOBACCO,PARM,('TOBAADEL',0),(0,PAYMATCH.TRNKEY),       *        
               TRNCOMF,0,(RF)                                                   
         LA    RE,WORKD            GET DEBIT 2ND CURRENCY CHEQUE AMOUNT         
         AHI   RE,RECOCA-WORKD     RE=A(LIST OF DEBIT OCAEL VALUES)             
         XC    FULL,FULL           CLEAR A(DEBIT OCAEL QMPYAMNT ENTRY)          
PAYTRN20 CLI   0(RE),0             TEST END OF DEBIT OCAEL VALUES LIST          
         BE    PAYTRN22                                                         
         CLI   0(RE),QMPYAMNT      TEST CHEQUE AMOUNT                           
         BNE   *+8                                                              
         ST    RE,FULL             SAVE A(LAST OCAEL QMPYAMNT ENTRY)            
         AHI   RE,L'RECOCA         BUMP TO NEXT VALUE                           
         B     PAYTRN20                                                         
PAYTRN22 XC    WORK,WORK           CLEAR OCAEL OVERRIDE LIST                    
         ICM   RE,15,FULL          RE=A(LAST DEBIT QMPYAMNT ENTRY)              
         BZ    PAYTRN24            NOT FOUND - JUST ADD CREDIT MPYEL            
         USING OCANTRY,RE          RECOCA FIXED FORMAT - XL1,XL1,PL8            
WRK      USING OCANTRY,WORK        BUILD OVERRIDE LIST FOR CREDIT OCAEL         
         MVC   WRK.OCANTYPE,OCANTYPE                                            
         MVC   WRK.OCANSEQN,OCANSEQN                                            
         ZAP   WRK.OCANCASH(L'MPYAMNT),OCANCASH(8)                              
         DROP  RE                                                               
PAYTRN24 MVC   DUB(L'TRNCCCUR),TRNCCCUR                                         
         MVC   DUB+L'TRNCCCUR(L'TRNCCURS),TRNCCURS                              
         GOTOR VTOBACCO,PARM,('TOBAAADD',DUB),(0,PAYMATCH.TRNKEY),     *        
               TRNCOMF,WRK.OCANTRY,ELEMENT,ELEMENT                              
         DROP  WRK                                                              
*&&                                                                             
PAYTRN26 DS    0H                                                               
*&&US                                                                           
         LA    R1,TRNELD           UPDATE/CREATE KEY REFERENCE ELEMENT          
         USING FFTELD,R1                                                        
PAYTRN28 IC    R0,FFTLN                                                         
         AR    R1,R0                                                            
         CLI   FFTEL,0                                                          
         BE    PAYTRN30                                                         
         CLI   FFTEL,FFTELQ                                                     
         BNE   PAYTRN32                                                         
         CLI   FFTTYPE,FFTTKREF                                                 
         BNE   PAYTRN32                                                         
         MVC   FFTDATA(L'TRNKREF),WORK                                          
         B     PAYTRN32                                                         
                                                                                
PAYTRN30 CLC   TRNKREF,WORK        TEST MATCHING KEY REFERENCE NUMBERS          
         BE    PAYTRN32                                                         
         LA    R1,ELEMENT                                                       
         MVI   FFTEL,FFTELQ                                                     
         MVI   FFTLN,FFTLN1Q+L'FFTDLEN+L'TRNKREF                                
         MVI   FFTTYPE,FFTTKREF                                                 
         MVI   FFTSEQ,0                                                         
         MVI   FFTDLEN,L'TRNKREF                                                
         MVC   FFTDATA(L'TRNKREF),WORK                                          
         GOTOR VHELLO,ELIST,(C'P',FILETRN),TRNRECD,ELEMENT,0                    
*&&                                                                             
PAYTRN32 ICM   R1,15,TRNTRNC       INCREMENT PAYABLE CREDITS MATCHED            
         AHI   R1,1                                                             
         STCM  R1,15,TRNTRNC                                                    
                                                                                
         GOTOR WRTTRN              WRITE BACK MATCHED PAYABLE RECORD            
                                                                                
PAYTRNX  XC    AAFCEL,AAFCEL       CLEAR A(FOREIGN ELEMENT)                     
         TM    INDS1,INDPAYDR      SET CC=NON ZERO IF MATCHED                   
         J     EXIT                                                             
         DROP  R1,R3,RB                                                         
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO WRITE BACK MATCHED TRANSACTION                           *         
***********************************************************************         
                                                                                
WRTTRN   NTR1  LABEL=N                                                          
                                                                                
         GOTOR WRITE,REVMATCH.TRNKEY                                            
         JE    *+6                                                              
         DC    H'0'                                                             
         GOTOR CLOTST,REVMATCH.TRNKEY                                           
         JNE   *+12                                                             
         OI    POSTINDS,POSTCURO   SET TRANSACTION CURRENTLY OPEN               
         J     *+8                                                              
         OI    POSTINDS,POSTCURC   SET TRANSACTION CURRENTLY CLOSED             
                                                                                
         LA    R0,REVMATCH.TRNKEY                                               
         AH    R0,DATADISP         R0=A(TRANSACTION ELEMENT)                    
         GOTOR PSTBAL,PARM,AACT,(R0)                                            
         TM    TRNOFAI,TRNIOAP+TRNIOWP                                          
         JZ    WRTTRNX                                                          
         GOTOR PSTBAL,PARM,AOFA,(R0)                                            
                                                                                
WRTTRNX  J     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* BUILD AND ADD A BALANCE ELEMENT TO RECORD POINTED TO BY R1          *         
***********************************************************************         
                                                                                
BLDABL   STM   RE,R1,SAVERE                                                     
                                                                                
         LR    RF,R1                                                            
         USING ABLELD,ELEMENT                                                   
         XC    ABLEL(ABLLN3Q),ABLEL                                             
         MVI   ABLEL,ABLELQ                                                     
         MVI   ABLLN,ABLLN3Q                                                    
         ZAP   ABLFRWD,PZERO                                                    
         ZAP   ABLDR,PZERO                                                      
         ZAP   ABLCR,PZERO                                                      
         GOTOR VHELLO,ELIST,(C'P',FILE),(RF),ABLELD,0                           
         CLI   ELERR,0             SET CONDITION CODE FOR CALLER                
                                                                                
BLDABLX  LM    RE,R1,SAVERE                                                     
         BR    RE                                                               
                                                                                
***********************************************************************         
* BUILD AND ADD A STATUS ELEMENT TO RECORD POINTED TO BY R1           *         
***********************************************************************         
                                                                                
BLDAST   STM   RE,R1,SAVERE                                                     
                                                                                
         LR    RF,R1                                                            
         USING ASTELD,ELEMENT                                                   
         XC    ASTELD(ASTLN1Q),ASTELD                                           
         MVI   ASTEL,ASTELQ                                                     
         MVI   ASTLN,ASTLN1Q                                                    
         GOTOR VHELLO,ELIST,(C'P',FILE),(RF),ASTELD,0                           
         CLI   ELERR,0             SET CONDITION CODE FOR CALLER                
                                                                                
BLDASTX  LM    RE,R1,SAVERE                                                     
         BR    RE                                                               
                                                                                
***********************************************************************         
* BUILD AND ADD A/C STATUS ELEMENT TO RECORD POINTED TO BY R1         *         
***********************************************************************         
                                                                                
BLDAPO   STM   RE,R1,SAVERE                                                     
                                                                                
         LR    RF,R1                                                            
         USING APOELD,ELEMENT                                                   
         XC    APOELD(APOLN2Q),APOELD                                           
         MVI   APOEL,APOELQ                                                     
         MVI   APOLN,APOLN2Q                                                    
         ZAP   APODR,PZERO                                                      
         ZAP   APOCR,PZERO                                                      
         GOTOR VHELLO,ELIST,(C'P',FILE),(RF),APOELD,0                           
         CLI   ELERR,0             SET CONDITION CODE FOR CALLER                
                                                                                
BLDAPOX  LM    RE,R1,SAVERE                                                     
         BR    RE                                                               
         EJECT                                                                  
*&&US                                                                           
**********************************************************************          
* ROUTINE TO CHECK GST ON SJ CREDIT POSTING FOR POSSIBLE OFFSET      *          
*                                                                    *          
* EXIT - CC=EQ IF ITEMS OFFSET, CC=NEQ IF ITEMS ARE NOT REVERSALS    *          
**********************************************************************          
                                                                                
GSTCHK   STM   RE,R1,SAVERE                                                     
         ZAP   DUB,PZERO           CLEAR BUCKET FOR OLD ITEM GST BILLED         
         ZAP   DUB2,PZERO          AND BUCKET FOR NEW ITEM GST BILLED           
         LA    RE,REVELEM.TRNELD   RE=A(OLD TRANSACTION ELEMENT)                
         LA    RF,TRNELD           RF=A(NEW TRANSACTION ELEMENT)                
         SR    R0,R0               CLEAR WORK REGISTER                          
                                                                                
* SEARCH OLD ITEM FOR A X'48' ELEMENT                                           
                                                                                
GSTCHK02 CLI   0(RE),0             TEST FOR EOR                                 
         JE    GSTCHK06            YES                                          
         CLI   0(RE),VBIELQ        TEST FOR VAT BILLED ELEMENT                  
         JE    GSTCHK08            YES                                          
                                                                                
GSTCHK04 IC    R0,1(RE)                                                         
         AR    RE,R0                                                            
         J     GSTCHK02                                                         
                                                                                
* OLD ITEM DID NOT HAVE ANY X'48' ELEMENTS -CHECK THAT NEW ITEM                 
* DOES NOT HAVE ANY X'48' ELEMENTS ALSO                                         
                                                                                
GSTCHK06 CLI   0(RF),0             TEST FOR EOR                                 
         JE    GSTCHKY             YES-ITEMS ARE REVERSALS                      
         CLI   0(RF),VBIELQ        TEST FOR VAT BILLED ELEMENT                  
         JE    GSTCHKN             YES-ITEMS ARE NOT REVERSALS                  
         IC    R0,1(RF)                                                         
         AR    RF,R0                                                            
         J     GSTCHK06                                                         
                                                                                
* SUM VAT BILLED ON OLD ITEM                                                    
                                                                                
         USING VBIELD,RE                                                        
GSTCHK08 AP    DUB,VBIVAT                                                       
                                                                                
GSTCHK10 IC    R0,1(RE)                                                         
         AR    RE,R0                                                            
         CLI   0(RE),0             TEST FOR EOR ON OLD ITEM                     
         JE    GSTCHK12            YES                                          
         CLI   0(RE),VBIELQ        TEST FOR ANOTHER VAT BILLED ELEM             
         JE    GSTCHK08                                                         
         J     GSTCHK10                                                         
         DROP  RE                                                               
                                                                                
* OLD ITEM HAS GST BILLED - NOW CHECK THAT NEW ITEM DOES ALSO                   
                                                                                
GSTCHK12 CLI   0(RF),0             TEST FOR EOR ON NEW ITEM                     
         JE    GSTCHKN             YES-ITEMS ARE NOT REVERSALS                  
         CLI   0(RF),VBIELQ        TEST FOR VAT BILLED ELEM                     
         JE    GSTCHK14            YES                                          
         IC    R0,1(RF)                                                         
         AR    RF,R0                                                            
         J     GSTCHK12                                                         
                                                                                
         USING VBIELD,RF                                                        
GSTCHK14 AP    DUB2,VBIVAT                                                      
                                                                                
GSTCHK16 IC    R0,1(RF)                                                         
         AR    RF,R0                                                            
         CLI   0(RF),0             TEST FOR EOR ON NEW ITEM                     
         JE    GSTCHK18            YES                                          
         CLI   0(RF),VBIELQ                                                     
         JE    GSTCHK14            ADD TO VAT BILLED BUCKET                     
         J     GSTCHK16            BUMP TO NEXT ELEMENT                         
         DROP  RF                                                               
                                                                                
GSTCHK18 AP    DUB,DUB2            TEST AMOUNTS WASH TO ZERO                    
         JZ    GSTCHKY             YES THEY DO--THEY ARE OFFSETS                
                                                                                
GSTCHKN  LTR   RE,RE               SET CC=NEQ                                   
         J     GSTCHKX                                                          
                                                                                
GSTCHKY  CR    RE,RE               SET CC=EQ                                    
                                                                                
GSTCHKX  LM    RE,R1,SAVERE                                                     
         BR    RE                                                               
         EJECT                                                                  
**********************************************************************          
* ROUTINE TO CHECK PST ON SJ CREDIT POSTING FOR POSSIBLE OFFSET      *          
*                                                                    *          
* EXIT - CC=EQ IF ITEMS OFFSET, CC=NEQ IF ITEMS ARE NOT REVERSALS    *          
**********************************************************************          
                                                                                
PSTCHK   STM   RE,R1,SAVERE                                                     
         ZAP   DUB,PZERO           CLEAR BUCKET FOR OLD ITEM PST BILLED         
         ZAP   DUB2,PZERO          AND BUCKET FOR NEW ITEM PST BILLED           
         LA    RE,REVELEM.TRNELD   RE=A(OLD TRANSACTION ELEMENT)                
         LA    RF,TRNELD           RF=A(NEW TRANSACTION ELEMENT)                
         SR    R0,R0               CLEAR WORK REGISTER                          
                                                                                
* SEARCH OLD ITEM FOR A X'7D' ELEMENT                                           
                                                                                
PSTCHK02 CLI   0(RE),0             TEST FOR EOR                                 
         JE    PSTCHK06            YES                                          
         CLI   0(RE),PBIELQ        TEST FOR PST BILLED ELEMENT                  
         JE    PSTCHK08            YES                                          
                                                                                
PSTCHK04 IC    R0,1(RE)                                                         
         AR    RE,R0                                                            
         J     PSTCHK02                                                         
                                                                                
* OLD ITEM DID NOT HAVE ANY X'7D' ELEMENTS -CHECK THAT NEW ITEM                 
* DOES NOT HAVE ANY X'7D' ELEMENTS ALSO                                         
                                                                                
PSTCHK06 CLI   0(RF),0             TEST FOR EOR                                 
         JE    PSTCHKY             YES-ITEMS ARE REVERSALS                      
         CLI   0(RF),PBIELQ        TEST FOR PST BILLED ELEMENT                  
         JE    PSTCHKN             YES-ITEMS ARE NOT REVERSALS                  
         IC    R0,1(RF)                                                         
         AR    RF,R0                                                            
         J     PSTCHK06                                                         
                                                                                
* SUM PST BILLED ON OLD ITEM                                                    
                                                                                
         USING PBIELD,RE                                                        
PSTCHK08 AP    DUB,PBIPST                                                       
                                                                                
PSTCHK10 IC    R0,1(RE)                                                         
         AR    RE,R0                                                            
         CLI   0(RE),0             TEST FOR EOR ON OLD ITEM                     
         JE    PSTCHK12            YES                                          
         CLI   0(RE),PBIELQ        TEST FOR ANOTHER PST BILLED ELEM             
         JE    PSTCHK08                                                         
         J     PSTCHK10                                                         
         DROP  RE                                                               
                                                                                
* OLD ITEM HAS PST BILLED - NOW CHECK THAT NEW ITEM DOES ALSO                   
                                                                                
PSTCHK12 CLI   0(RF),0             TEST FOR EOR ON NEW ITEM                     
         JE    PSTCHKN             YES-ITEMS ARE NOT REVERSALS                  
         CLI   0(RF),PBIELQ        TEST FOR PST BILLED ELEM                     
         JE    PSTCHK14            YES                                          
         IC    R0,1(RF)                                                         
         AR    RF,R0                                                            
         J     PSTCHK12                                                         
                                                                                
         USING PBIELD,RF                                                        
PSTCHK14 AP    DUB2,PBIPST                                                      
                                                                                
PSTCHK16 IC    R0,1(RF)                                                         
         AR    RF,R0                                                            
         CLI   0(RF),0             TEST FOR EOR ON NEW ITEM                     
         JE    PSTCHK18            YES                                          
         CLI   0(RF),PBIELQ                                                     
         JE    PSTCHK14            ADD TO PST BILLED BUCKET                     
         J     PSTCHK16            BUMP TO NEXT ELEMENT                         
         DROP  RF                                                               
                                                                                
PSTCHK18 AP    DUB,DUB2            TEST AMOUNTS WASH TO ZERO                    
         JZ    PSTCHKY             YES THEY DO--THEY ARE OFFSETS                
                                                                                
PSTCHKN  LTR   RE,RE               SET CC=NEQ                                   
         J     PSTCHKX                                                          
                                                                                
PSTCHKY  CR    RE,RE               SET CC=EQ                                    
                                                                                
PSTCHKX  LM    RE,R1,SAVERE                                                     
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* HANDLE PARTIAL JOB-TO-JOB TRANSFERS - READ ORIGINAL TRANSACTION     *         
* AND UPDATE TRANSFER AMOUNT IF APPROPRIATE (KEY REFERENCE) ELEMENTS  *         
* ARE FOUND ON THE TRANSACTION BEING ADDED OR MADE LIVE               *         
***********************************************************************         
                                                                                
JOBXFR   TM    TRNINDS,TRNSDRFT    TEST ADDING LIVE TRANSACTION                 
         BNZR  RE                                                               
         TM    INDS2,INDPRODN      TEST PRODUCTION LEDGER                       
         BZR   RE                                                               
         CLI   TRNTYPE,TRNTJBTX    TEST BATCH TYPE 34                           
         BNER  RE                                                               
                                                                                
JOBMATCH USING TRNRECD,R4                                                       
JOBXFRN  NTR1  BASE=*,LABEL=*                                                   
         LA    R2,TRNELD                                                        
         USING FFTELD,R2                                                        
         SR    R0,R0                                                            
         SR    RE,RE                                                            
         SR    RF,RF                                                            
JOBXFR02 IC    R0,FFTLN            LOCATE KEY REF/SUB-REF ELEMENTS              
         AR    R2,R0                                                            
         CLI   FFTEL,0                                                          
         BE    JOBXFR04                                                         
         CLI   FFTEL,FFTELQ                                                     
         BNE   JOBXFR02                                                         
         CLI   FFTTYPE,FFTTKREF                                                 
         BNE   *+8                                                              
         LA    RE,FFTELD                                                        
         CLI   FFTTYPE,FFTTTOSD                                                 
         BNE   *+8                                                              
         LA    RF,FFTELD                                                        
         B     JOBXFR02                                                         
                                                                                
JOBXFR04 LTR   RF,RF               TEST KEY SUB-REFERENCE FOUND                 
         BZ    JOBXFRX                                                          
         MVC   JOBMATCH.TRNKEY,TRNKEY                                           
         LTR   RE,RE               TEST KEY REFERENCE FOUND                     
         BZ    *+10                                                             
         MVC   JOBMATCH.TRNKREF,FFTDATA-FFTELD(RE)                              
         MVC   JOBMATCH.TRNKSBR,FFTSUBSQ-FFTELD(RF)                             
         GOTOR READ,JOBMATCH.TRNRECD                                            
         BE    *+6                                                              
         DC    H'0'                                                             
                                                                                
         LA    R2,JOBMATCH.TRNRFST                                              
         USING SCIELD,R2                                                        
         SR    R0,R0                                                            
JOBXFR06 IC    R0,SCILN            LOCATE SUBSIDIARY CASH ELEMENT               
         AR    R2,R0                                                            
         CLI   SCIEL,0                                                          
         BNE   JOBXFR08                                                         
                                                                                
         LA    R2,ELEMENT          NOT FOUND - ADD ONE NOW                      
         MVI   SCIEL,SCIELQ                                                     
         MVI   SCILN,SCILN1Q                                                    
         MVI   SCITYPE,SCITPART                                                 
         ZAP   SCIAMNT,PZERO                                                    
         GOTOR VHELLO,ELIST,(C'P',FILE),JOBMATCH.TRNRECD,SCIELD,0,0             
         CLI   ELERR,0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
         L     R2,ELADDR2                                                       
         B     JOBXFR10                                                         
                                                                                
JOBXFR08 CLI   SCITYPE,SCITPART    TEST PARTIAL TRANSFER ELEMENT                
         BNE   JOBXFR06                                                         
                                                                                
JOBXFR10 AP    SCIAMNT,TRNAMNT     UPDATE TRANSFER AMOUNT                       
         GOTOR PUTREC,JOBMATCH.TRNRECD                                          
         BE    JOBXFRX                                                          
         DC    H'0'                CAN'T PUT BACK UPDATED TRANSACTION           
                                                                                
JOBXFRX  J     EXIT                                                             
         DROP  R2,RB                                                            
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO UPDATE TIME RECORDS                                      *         
*                                                                     *         
* EXIT - CC=NON ZERO IF TRANSACTION IS A DUPLICATE, ZERO IF NOT       *         
***********************************************************************         
                                                                                
TIMTRN   NTR1  BASE=*,LABEL=*                                                   
         TM    TRNINDS,TRNIDRFT    TEST DRAFT TRANSACTION                       
         BNZ   TIMTRNN                                                          
         CLC   CPERULUS,TRNKUNT    TEST POSTING TO PERSON LEDGER                
         BNE   TIMTRNN                                                          
         TM    TRNCPYS7,CPYSTMSY   TEST COMPANY ON TMS                          
         BZ    TIMTRNN                                                          
         XC    TIMVALS(TIMVALL),TIMVALS                                         
*                                                                               
         USING TIMELD,R1           AND SAVE ADDRESS OF FIRST                    
         LA    R1,TRNELD           CALCULATE LENGTH OF TIME ELEMENTS            
         SR    RF,RF                                                            
         SR    R0,R0                                                            
         IC    R0,TIMLN                                                         
         BASR  RE,0                                                             
         AR    R1,R0               BUMP TO NEXT ELEMENT                         
         CLI   TIMEL,0             TEST END OF RECORD                           
         BE    TIMTRN02                                                         
         IC    R0,TIMLN                                                         
         CLI   TIMEL,TIMELQ        TEST TIME ELEMENT                            
         BNER  RE                                                               
         AR    RF,R0               YES - BUMP TOTAL LENGTH                      
         OC    TIMAFST,TIMAFST     TEST A(FIRST ELEMENT) SAVED                  
         BNZR  RE                                                               
         STCM  R1,7,TIMAFST        NO - SAVE IT                                 
         BR    RE                                                               
*                                                                               
TIMTRN02 LTR   RF,RF               TEST ANY TIME ELEMENTS FOUND                 
         BZ    TIMTRNN                                                          
         STH   RF,TIMTOTL          SAVE TOTAL LENGTH                            
         DROP  R1                                                               
                                                                                
         GOTOR GETRST              GET 1R ACCOUNT STATUS ELEMENT                
         BE    *+6                                                              
         DC    H'0'                CAN'T FIND IT                                
                                                                                
         USING RSTELD,R1                                                        
         CLI   RSTLN,RSTSNUM+L'RSTSNUM-RSTELD                                   
         BNL   *+6                                                              
         DC    H'0'                                                             
                                                                                
         SR    RE,RE                                                            
         ICM   RE,3,RSTSNUM                                                     
         AHI   RE,1                                                             
         STCM  RE,3,TIMLIN#        SET TIME LINE NUMBER TO USE                  
         AHI   RE,1                                                             
         STCM  RE,3,RSTSNUM                                                     
                                                                                
         GOTOR GETLDG,TRNRECD      GET 1R LEDGER TABLE ENTRY                    
         L     R3,ALDGNTRY                                                      
         USING LDGTABD,R3                                                       
                                                                                
         USING TSWRECD,R4          ESTABLISH SEQUENCE NUMBER                    
         XC    TSWKEY,TSWKEY                                                    
         MVI   TSWKTYP,TSWKTYPQ                                                 
         MVI   TSWKSUB,TSWKSUBQ                                                 
         MVC   TSWKCPY,TRNKCPY                                                  
         SR    RE,RE                                                            
         IC    RE,LDGTLVC                                                       
         SHI   RE,1                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   TSWKODS(0),TRNKACT                                               
         OC    TSWKODS,SPACES                                                   
         AHI   RE,1                                                             
         LA    R1,TRNKACT(RE)                                                   
         SR    RF,RF                                                            
         IC    RF,LDGTLVD                                                       
         SR    RF,RE                                                            
         SHI   RF,1                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   TSWKPER(0),0(R1)                                                 
         OC    TSWKPER,SPACES                                                   
         SR    RE,RE                                                            
         ICM   RE,7,TRNKDATE                                                    
         LNR   RE,RE                                                            
         STCM  RE,7,TSWKEND                                                     
         MVC   PTRKEY,TSWKEY       SAVE KEY FOR LATER                           
         DROP  R3                                                               
                                                                                
         MVC   KEYSAVE2,TSWKEY                                                  
         GOTOR RDHIU,TSWKEY                                                     
         B     TIMTRN06                                                         
                                                                                
TIMTRN04 GOTOR RDSQU,TSWKEY                                                     
TIMTRN06 CLC   TSWKEY(TSWKODS+L'TSWKODS-TSWKEY),KEYSAVE2                        
         BNE   TIMTRN12                                                         
         GOTOR GETRECT,TSWKEY                                                   
         BE    *+6                                                              
         DC    H'0'                                                             
                                                                                
         LA    R3,TSWKEY+(ACTRFST-ACTRECD)                                      
         USING TIMELD,R3                                                        
         SR    R0,R0                                                            
TIMTRN08 CLI   TIMEL,0                                                          
         BE    TIMTRN04                                                         
         CLI   TIMEL,TIMELQ                                                     
         BNE   TIMTRN10                                                         
         CLI   TIMETYP,TIMEINP                                                  
         BNE   TIMTRN10                                                         
         CLC   TIMSEQ#,TIMSEQ                                                   
         BH    TIMTRN10                                                         
         MVC   TIMSEQ#,TIMSEQ                                                   
TIMTRN10 IC    R0,TIMLN                                                         
         AR    R3,R0                                                            
         B     TIMTRN08                                                         
                                                                                
TIMTRN12 SR    R1,R1                                                            
         IC    R1,TIMSEQ#                                                       
         AHI   R1,1                                                             
         STC   R1,TIMSEQ#                                                       
                                                                                
         LA    R3,TRNELD                                                        
         SR    R0,R0                                                            
TIMTRN14 CLI   TIMEL,0                                                          
         BNE   *+6                                                              
         DC    H'0'                                                             
         CLI   TIMEL,TIMELQ                                                     
         BE    TIMTRN15                                                         
         IC    R0,TIMLN                                                         
         AR    R3,R0                                                            
         B     TIMTRN14                                                         
                                                                                
TIMTRN15 MVC   TIMSEQ,TIMSEQ#                                                   
         MVC   TIMLINE#,TIMLIN#                                                 
         MVC   TIMMOA,MOS                                                       
                                                                                
TIMTRN16 IC    R0,TIMLN                                                         
         AR    R3,R0                                                            
         CLI   TIMEL,0                                                          
         BE    TIMTRN17                                                         
         CLI   TIMEL,TIMELQ                                                     
         BNE   TIMTRN16                                                         
         MVC   TIMSEQ,TIMSEQ#                                                   
         B     TIMTRN16                                                         
         DROP  R3                                                               
                                                                                
         USING TIMRECD,R4                                                       
TIMTRN17 MVC   TIMKEY,TRNKEY                                                    
         MVC   TIMKREF,TIMEREF                                                  
         MVI   TIMKSBR,0                                                        
                                                                                
TIMTRN18 GOTOR RDHIU,TIMKEY                                                     
         BE    *+6                                                              
         DC    H'0'                                                             
                                                                                
         CLC   TIMKEY(TIMKREF+L'TIMKREF-TIMKEY),KEYSAVE                         
         BNE   TIMTRN19                                                         
         GOTOR GETRECT,TIMRECD                                                  
         BE    *+6                                                              
         DC    H'0'                                                             
                                                                                
         GOTOR TIMADD              TRY ADDING TO CURRENT RECORD                 
         BE    TIMTRN22                                                         
         SR    R0,R0                                                            
         IC    R0,TIMKSBR          CAN'T ADD - BUMP SEQUENCE#                   
         AHI   R0,1                                                             
         STC   R0,TIMKSBR                                                       
         B     TIMTRN18            AND TRY NEXT RECORD                          
                                                                                
TIMTRN19 MVC   KEYSAVE2,KEYSAVE    SAVE KEY OF RECORD TO BE ADDED               
                                                                                
TSWPAS   USING TSWRECD,PTRKEY                                                   
         USING PERRECD,R4          READ PERSON RECORD TO GET PID                
         MVC   PERKEY,SPACES                                                    
         MVI   PERKTYP,PERKTYPQ                                                 
         MVC   PERKCPY,TRNKCPY                                                  
         MVC   PERKCODE,TSWPAS.TSWKPER                                          
         GOTOR READUN,PERRECD      READ NO DELETES/NO LOCK                      
         BE    *+6                                                              
         DC    H'0'                                                             
                                                                                
         USING PIDELD,R1           LOCATE PIDEL ON PERSON RECORD                
         MVI   ELEMENT,0           SET NO PIDEL TO ADD                          
         LA    R1,PERRFST                                                       
         SR    R0,R0                                                            
         BASR  RE,0                                                             
         CLI   PIDEL,0             TEST END OF RECORD                           
         BE    TIMTRN20                                                         
         CLI   PIDEL,PIDELQ        TEST PIDEL                                   
         BE    *+12                                                             
         IC    R0,PIDLN            NO - BUMP TO NEXT ELEMENT                    
         AR    R1,R0                                                            
         BR    RE                                                               
                                                                                
         MVC   ELEMENT(PIDLNQ),PIDELD   SET PIDEL TO BE ADDED TO TIMREC         
         MVI   ELEMENT+PIDLNQ,0                                                 
         DROP  R1                                                               
                                                                                
         USING TIMRECD,R4                                                       
TIMTRN20 GOTOR BLDREC,PARM,KEYSAVE2,TIMRECD,ELEMENT                             
         MVI   TIMRRI1,TIMSFAPP    SETTING NOW ALWAYS X'40'                     
         MVI   TIMRRI2,TIMKRI2Q                                                 
         MVI   TIMRRI3,TIMKRI3Q                                                 
         MVI   TIMRRI4,TIMKRI4Q                                                 
         MVC   TIMRLMOS,EFFS                                                    
         XC    TIMRHMOS,TIMRHMOS                                                
         GOTOR TIMADD                                                           
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         GOTOR ADD,TIMRECD                                                      
         BE    *+6                                                              
         DC    H'0'                                                             
                                                                                
         MVC   TSWPAS.TSWKULC,TIMKULC                                           
         MVC   TSWPAS.TSWKCOFC,TIMKOFF                                          
         MVC   TSWPAS.TSWKSBR,TIMKSBR                                           
         MVC   TSWPAS.TSWKSTA,TIMRSTA                                           
         MVC   TSWPAS.TSWKDA,DA                                                 
         GOTOR ADDDIR,TSWPAS.TSWRECD                                            
         B     TIMTRN24                                                         
                                                                                
TIMTRN22 GOTOR PUTREC,TIMRECD      PUT BACK UPDATED TIME RECORD                 
         BE    *+6                                                              
         DC    H'0'                                                             
                                                                                
TIMTRN24 TM    TRNINDS,TRNILIVE    TEST DRAFT TO LIVE                           
         BZ    TIMTRNY                                                          
         OI    TRNRSTAT,TRNSDELT   YES - DELETE THE DUMMY TRANSACTION           
         GOTOR PUTREC,TRNRECD                                                   
         BE    *+6                                                              
         DC    H'0'                                                             
                                                                                
DELTRN   USING TRNRECD,KEY                                                      
         MVC   DELTRN.TRNKEY,TRNKEY                                             
         MVC   DELTRN.TRNKSTAT,TRNRSTAT                                         
         MVC   DELTRN.TRNKDA,DA                                                 
         GOTOR WRITEDIR,DELTRN.TRNRECD                                          
         BE    *+6                                                              
         DC    H'0'                                                             
                                                                                
TIMTRNY  J     EXITY                                                            
                                                                                
TIMTRNN  J     EXITN                                                            
         EJECT                                                                  
***********************************************************************         
* ADD TIME CLUSTER AT END OF TIME RECORD IF IT WILL FIT               *         
* EXIT - CC=EQUAL IF TIME RECORD UPDATED                              *         
*        CC=NOT EQUAL IF CLUSTER DIDN'T FIT ON CURRENT RECORD         *         
***********************************************************************         
                                                                                
TIMADD   STM   RE,R1,SAVERE                                                     
         SR    R0,R0                                                            
         ICM   R0,3,TIMRLEN                                                     
         AH    R0,TIMTOTL                                                       
         CHI   R0,1950                                                          
         BH    TIMADDN                                                          
         STCM  R0,3,TIMRLEN                                                     
                                                                                
         LA    RE,TIMRFST                                                       
         SR    RF,RF                                                            
         USING TIMELD,RE                                                        
TIMADD02 CLI   TIMEL,0                                                          
         BE    *+14                                                             
         IC    RF,TIMLN                                                         
         AR    RE,RF                                                            
         B     TIMADD02                                                         
                                                                                
         ICM   R1,7,TIMAFST                                                     
TIMCOPY  USING TIMELD,R1                                                        
TIMADD04 CLI   TIMCOPY.TIMEL,0                                                  
         BE    TIMADDY                                                          
         IC    RF,TIMCOPY.TIMLN                                                 
         CLI   TIMCOPY.TIMEL,TIMELQ                                             
         BNE   TIMADD06                                                         
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   TIMEL(0),TIMCOPY.TIMEL                                           
                                                                                
         CLI   TIMETYP,TIMEINP     TAKE TIMMOA ONLY FROM TYPE 1                 
         BNE   TIMADD05                                                         
         CLC   TIMMOA,TIMRLMOS     UPDATE LOW/HIGH MOS VALUES                   
         BH    *+10                                                             
         MVC   TIMRLMOS,TIMMOA                                                  
         CLC   TIMMOA,TIMRHMOS                                                  
         BL    *+10                                                             
         MVC   TIMRHMOS,TIMMOA                                                  
                                                                                
TIMADD05 AR    RE,RF                                                            
         MVI   0(RE),0                                                          
                                                                                
TIMADD06 AR    R1,RF                                                            
         B     TIMADD04                                                         
                                                                                
TIMADDN  LTR   RE,RE                                                            
         B     TIMADDX                                                          
TIMADDY  CR    RE,RE                                                            
                                                                                
TIMADDX  LM    RE,R1,SAVERE                                                     
         BR    RE                                                               
         DROP  TSWPAS,DELTRN,TIMCOPY,RB                                         
*&&                                                                             
         EJECT                                                                  
GLOBALS  DS    0D                  ** GLOBAL VALUES **                          
                                                                                
         LTORG                                                                  
                                                                                
ADCONS   DS    0A                                                               
         DC    V(CONVMOS)                                                       
         DC    V(ACSRCHP)                                                       
         DC    V(DATCON)                                                        
         DC    V(HELLO)                                                         
         DC    V(ADDAY)                                                         
         DC    V(GETDAY)                                                        
*&&UK*&& DC    V(PROMOTE)                                                       
         DC    V(ACRAPPER)                                                      
         DC    V(GETBROAD)                                                      
         DC    V(ACADDBUK)                                                      
         DC    V(ACGLPOST)                                                      
ADCONN   EQU   (*-ADCONS)/L'ADCONS                                              
                                                                                
LGLBLOCK DC    A(0)                OFF-LINE SAVE A(GETMAIN) -GLBLOCK            
                                                                                
OLDDATA  DC    Y(ACCORFST)                                                      
NEWDATA  DC    Y(ACCRFST-ACCRECD)                                               
NEWSTAT  DC    Y(ACCRSTA-ACCRECD)                                               
OLDSTAT  DC    Y(ACCOSTAT)                                                      
                                                                                
EFFS     DC    X'FFFFFFFFFFFFFFFF'                                              
PZERO    DC    P'0'                                                             
P100     DC    P'100'                                                           
PMINUS1  DC    P'-1'                                                            
PL6MAX   DC    PL6'99999999999'                                                 
CHKOFC   DC    C'**'                                                            
                                                                                
ACCFIL   DC    C'ACCFIL  '                                                      
ACCDIR   DC    C'ACCDIR  '                                                      
ACCMST   DC    C'ACCMST  '                                                      
ACCARC   DC    C'ACCARC  '                                                      
                                                                                
DMACTS   DS    0CL8                                                             
DMRDHI   DC    CL(L'DMACTS)'DMRDHI  '                                           
DMREAD   DC    CL(L'DMACTS)'DMREAD  '                                           
DMRSEQ   DC    CL(L'DMACTS)'DMRSEQ  '                                           
DMWRT    DC    CL(L'DMACTS)'DMWRT   '                                           
DMADD    DC    CL(L'DMACTS)'DMADD   '                                           
DMGETR   DC    CL(L'DMACTS)'GETREC  '                                           
DMPUTR   DC    CL(L'DMACTS)'PUTREC  '                                           
DMADDR   DC    CL(L'DMACTS)'ADDREC  '                                           
DMUNLK   DC    CL(L'DMACTS)'DMUNLK  '                                           
DMADFR   DC    CL(L'DMACTS)'ADFREC  '                                           
                                                                                
TIMEREF  DC    CL(L'TRNKREF)'*TIME*'                                            
                                                                                
DCDICTL  DS    0X                                                               
         DCDDL AC#NOORD,L'AC@NOORD                                              
DCDICTLX DC    X'FF'                                                            
                                                                                
BILLWC   DC    C'99'               BILLING/SPECIAL TALENT W/C                   
                                                                                
DEBTUL   DC    C'SR'               DEBTORS UNIT/LEDGER                          
PCRDUL   DC    C'SV'               PRODUCTION CREDITOR LEDGER                   
CPERULUK DC    C'1P'               UK COSTING/PERSON UNIT/LEDGER CODE           
CPERULUS DC    C'1R'               US COSTING/PERSON UNIT/LEDGER CODE           
FILMULUK DC    C'2F'               UK FILM LEDGER                               
PRODUL   DC    C'SJ'               PRODUCTION UNIT/LEDGER CODE                  
PCTLUL   DC    C'1J'               PROJECT CONTROL UNIT/LEDGER CODE             
MFOLULUK DC    C'SF'               UK MEDIA FOLIO LEDGER                        
CCSTUL   DC    C'1C'               COSTING CLIENT UNIT/LEDGER                   
                                                                                
CCONTRAS DS    0CL2                ** COSTING CLIENT CONTRA LEDGERS **          
CBILUL   DC    C'11'               COSTING BILLING LEDGER                       
CINCUL   DC    C'12'               COSTING INCOME LEDGER                        
CEXPUL   DC    C'13'               COSTING EXPENSE LEDGER                       
CDIRUL   DC    C'14'               COSTING DIRECT TIME LEDGER                   
CINDUL   DC    C'15'               COSTING INDIRECT TIME LEDGER                 
COHDUL   DC    C'16'               COSTING OVERHEAD LEDGER                      
CCONTRAN EQU   (*-CCONTRAS)/L'CCONTRAS                                          
                                                                                
BUKTAB   DS    0X                  ** CONTRA BUCKET TABLE **                    
*&&US                                                                           
         DC    AL1(SCITGRNT,SCITDFLT),C'SE'                                     
         DC    AL2(BTGRNT-ADDTRN)                                               
         DC    AL1(SCITCOKE,SCITGRNT),C'SE'                                     
         DC    AL2(BTGRNT-ADDTRN)                                               
         DC    AL1(SCITGRNT,SCITGRNT),C'  '                                     
         DC    AL2(BTGRNT-ADDTRN)                                               
         DC    AL1(SCITBENE,SCITBENE),C'  '                                     
         DC    AL2(BTBENE-ADDTRN)                                               
*&&                                                                             
*&&UK                                                                           
         DC    AL1(SCITGLEV,SCITGLEV),C'ST'                                     
         DC    AL2(BTGLEVST-ADDTRN)                                             
         DC    AL1(SCITINCA,SCITINCA),C'ST'                                     
         DC    AL2(BTINCAST-ADDTRN)                                             
         DC    AL1(SCITFORD,SCITGLEV),C'ST'                                     
         DC    AL2(BTFORDST-ADDTRN)                                             
         DC    AL1(SCITJHRS,SCITINCA),C'ST'                                     
         DC    AL2(BTJHRSST-ADDTRN)                                             
         DC    AL1(SCITIAMT,SCITIAMT),C'ST'                                     
         DC    AL2(BTIAMTST-ADDTRN)                                             
         DC    AL1(SCITNICL,SCITNICL),C'ST'                                     
         DC    AL2(BTNICTST-ADDTRN)                                             
         DC    AL1(SCITNICM,SCITNICM),C'ST'                                     
         DC    AL2(BTNICTST-ADDTRN)                                             
         DC    AL1(SCITNICU,SCITNICU),C'ST'                                     
         DC    AL2(BTNICTST-ADDTRN)                                             
         DC    AL1(SCITIVAT,SCITIVAT),C'2P'                                     
         DC    AL2(BTIVAT2P-ADDTRN)                                             
         DC    AL1(SCITDEPR,SCITDEPR),C'41'                                     
         DC    AL2(BTDEPR41-ADDTRN)                                             
         DC    AL1(SCITLOAT,SCITLOAT),C'1R'                                     
         DC    AL2(BTLOAT1R-ADDTRN)                                             
*&&                                                                             
         DC    AL1(SCITVEHI,SCITVEHI),C'1J'                                     
         DC    AL2(BTVEHI1J-ADDTRN)                                             
         DC    AL1(SCITHOUR,0),C'1C'                                            
         DC    AL2(0)                                                           
         DC    AL1(SCITHOUR,SCITHOUR),C'1J'                                     
         DC    AL2(BTHOUR1J-ADDTRN)                                             
         DC    AL1(SCITGRSS,SCITGRSS),C'1J'                                     
         DC    AL2(BTGRSS1J-ADDTRN)                                             
         DC    AL1(SCITGRSS,SCITGRSS),C'  '                                     
         DC    AL2(BTGRSS-ADDTRN)                                               
         DC    AL1(SCITJHRS,SCITHOUR),C'  '                                     
         DC    AL2(BTHOUR-ADDTRN)                                               
         DC    AL1(SCITHOUR,SCITHOUR),C'1R'                                     
         DC    AL2(BTHOUR1R-ADDTRN)                                             
         DC    AL1(SCITHOUR,SCITHOUR),C'  '                                     
         DC    AL2(BTHOUR-ADDTRN)                                               
         DC    AL1(SCITGLEV,SCITGLEV),C'  '                                     
         DC    AL2(BTGLEV-ADDTRN)                                               
         DC    AL1(SCITVEHI,SCITVEHI),C'  '                                     
         DC    AL2(BTVEHI-ADDTRN)                                               
         DC    AL1(SCITFEEA,SCITFEEA),C'  '                                     
         DC    AL2(BTFEEA-ADDTRN)                                               
         DC    AL1(SCITFEES,SCITFEES),C'  '                                     
         DC    AL2(BTFEES-ADDTRN)                                               
         DC    AL1(SCITANAL,SCITANAL),C'SI'                                     
         DC    AL2(BTANAL-ADDTRN)                                               
                                                                                
BUKTABX  DC    AL1(BUKTEOTQ)                                                    
                                                                                
BUKTABD  DSECT                     ** CONTRA BUCKET TABLE DSECT **              
BUKTEOTQ EQU   255                 END OF TABLE INDICATOR                       
BUKTITYP DS    C                   INPUT BUCKET TYPE (IN ELEMENT)               
BUKTOTYP DS    C                   OUTPUT BUCKET TYPE (IN RECORD)               
BUKTLEDG DS    CL2                 UNIT/LEDGER CODE (SPACES=ANY)                
BUKTROUT DS    AL2                 DISPLACEMENT TO ROUTINE                      
BUKTABL  EQU   *-BUKTABD                                                        
ADDTRN   CSECT                                                                  
         EJECT                                                                  
EXITY    LHI   RE,1                                                             
         J     EXITCC                                                           
EXITN    LHI   RE,0                                                             
EXITCC   CHI   RE,1                                                             
EXIT     XIT1  ,                                                                
EXITR1   XIT1  REGS=(R1)                                                        
                                                                                
QNAMEL   EQU   X'80'               NAME ELEMENT PRESENT                         
QADREL   EQU   X'40'               ADDRESS ELEMENT PRESENT                      
QRSTEL   EQU   X'20'               STATUS ELEMENT PRESENT                       
QMSAEL   EQU   X'10'               MONTHLY SALARY ELEMENT PRESENT               
QDELEL   EQU   X'08'               SPECIAL ELEMENT TO BE DELETED                
QANYEL   EQU   X'04'               OTHER ELEMENT TYPES                          
                                                                                
EOR      EQU   0                   END OF RECORD                                
FF       EQU   X'FF'               FOR GENERAL USE                              
DELELQ   EQU   X'FF'               FOR ELEMENT DELETION                         
                                                                                
*&&UK                                                                           
BUKMAXN  EQU   54                  MAXIMUM N'BUCKET ELEMENTS ON RECORD          
*&&                                                                             
*&&US                                                                           
BUKMAXN  EQU   108                 MAXIMUM N'BUCKET ELEMENTS ON RECORD          
*&&                                                                             
         EJECT                                                                  
PAYTAB   DS    0X                  ** PAYABLE LEDGERS TABLE **                  
*&&UK                                                                           
         DC    C'SF',AL1(PAYTIMPY+PAYTMEDS+PAYTTWOS)                            
         DC    C'ST',AL1(PAYTIMPY+PAYTTWOS)                                     
         DC    C'SV',AL1(PAYTIMPY+PAYTPROD+PAYTTWOS+PAYTINVP)                   
         DC    C'SX',AL1(PAYTIMPY+PAYTTWOS+PAYTINVP)                            
*&&                                                                             
*&&US                                                                           
         DC    C'SP',AL1(PAYTMEDS+PAYTIMPY+PAYTDRCR+PAYTTWOS+PAYAAPAS)          
         DC    C'SQ',AL1(PAYTMEDS+PAYTIMPY+PAYTDRCR+PAYTTWOS+PAYAAPAS)          
         DC    C'SS',AL1(PAYTMEDS+PAYTIMPY+PAYTDRCR+PAYTTWOS+PAYAAPAS)          
         DC    C'ST',AL1(PAYTMEDS+PAYTIMPY+PAYTDRCR+PAYTTWOS+PAYAAPAS)          
         DC    C'SU',AL1(PAYTMEDS+PAYTIMPY+PAYTDRCR+PAYTTWOS+PAYAAPAS)          
         DC    C'SV',AL1(PAYTPROD+PAYTIMPY+PAYTTWOS+PAYTINVP)                   
         DC    C'SW',AL1(PAYTPROD+PAYTIMPY+PAYTTWOS+PAYTINVP)                   
         DC    C'SX',AL1(PAYTIMPY+PAYTTWOS+PAYTINVP)                            
         DC    C'SY',AL1(PAYTIMPY+PAYTTWOS+PAYTINVP)                            
*&&                                                                             
PAYTABX  DC    AL1(PAYTEOTQ)                                                    
                                                                                
PAYTABD  DSECT                     ** PAYABLE LEDGERS TABLE DSECT **            
PAYTEOTQ EQU   255                 END OF TABLE INDICATOR                       
PAYTLEDG DS    CL2                 UNIT/LEDGER CODE                             
                                                                                
PAYTINDS DS    X                   ** INDICATOR BYTE **                         
PAYTIMPY EQU   X'80'               ADD MANUAL PAYMENT ELEMENT                   
PAYTMEDS EQU   X'40'               DDS MEDIA SYSTEM VENDORS                     
PAYTPROD EQU   X'20'               DDS PRODUCTION SYSTEM VENDORS                
PAYTDRCR EQU   X'10'               DON'T REVERSE CREDITS                        
PAYTTWOS EQU   X'04'               INCREMENT SUB-REFERENCE IN 2'S               
PAYTINVP EQU   X'02'               GENERATE INV PASSIVE POINTER RECORD          
PAYAAPAS EQU   X'01'               GENERATE AUTO APPROVE PAYBLE PASSIVE         
                                                                                
PAYTABL  EQU   *-PAYTABD                                                        
         EJECT                                                                  
BUKBUFD  DSECT                     ** LIST OF BUCKETS DSECT **                  
BUKBHDR  DS    0X                  TABLE HEADER                                 
BUKBHNUM DS    XL4                 NUMBER OF ENTRIES IN TABLE                   
BUKBHDRL EQU   *-BUKBHDR                                                        
         ORG   BUKBHDR                                                          
BUKBKEY  DS    0X                  ** BUCKET BUFFER KEY TYPE **                 
BUKBEOTQ EQU   255                 END OF TABLE INDICATOR                       
BUKBOFFC DS    CL2                 BUCKET OFFICE OR SPACES                      
BUKBTYPE DS    C                   BUCKET TYPE VALUE                            
BUKBSTYP DS    CL3                 BUCKET SUB-TYPE                              
BUKBSTAT DS    C                   BUCKET STATUS (LIVE/DRAFT DATA)              
BUKBMOS  DS    PL2                 BUCKET MONTH OF SERVICE (YYMM)               
BUKBKEYL EQU   *-BUKBKEY           LENGTH OF KEY FOR SORTING                    
BUKBDR   DS    PL7                 BUCKET DEBIT AMOUNT                          
BUKBCR   DS    PL7                 BUCKET CREDIT AMOUNT                         
BUKBUFL  EQU   *-BUKBKEY                                                        
BUKBMAXN EQU   95                  MAXIMUM NUMBER OF BUCKET ENTRIES             
                                                                                
PALD     DSECT                     ** P&L BUCKET CONTROL AREA **                
PALDCTLS DS    0X                  ** CONTROLS **                               
PALDINDS DS    X                   ** INDICATORS **                             
PALDINOT EQU   X'80'               .   DON'T CREATE/UPDATE P&L RECORDS          
         DS    XL4                 SPARE                                        
PAL11LLN DS    X                   11 LEVEL LENGTH (2ND LEVEL)                  
PAL12LLN DS    X                   12 LEVEL LENGTH (2ND LEVEL)                  
PAL13LLN DS    X                   13 LEVEL LENGTH (2ND LEVEL)                  
PAL1RLLN DS    X                   1R LEVEL LENGTH (2ND LEVEL)                  
PALCAOFL DS    X                   14 - 16 OFFICE (1R 1ST LEVEL LENGTH)         
         DS    XL10                SPARE                                        
PALLNQ   EQU   *-PALD                                                           
         EJECT                                                                  
***********************************************************************         
* GENERALIZE EQUATES                                                            
***********************************************************************         
YES      EQU   C'Y'                                                             
NO       EQU   C'N'                                                             
K        EQU   1024                                                             
GL#OFK   EQU   16                  AMOUNT OF STORAGE TO USE FOR GL              
BO       EQU   X'10'               BRANCH ONE                                   
BZ       EQU   X'80'               BRANCH ZERO                                  
         EJECT                                                                  
WORKD    DSECT                     ** LOCAL WORKING STORAGE **                  
                                                                                
WADCONS  DS    0A                                                               
VCONVMOS DS    V                                                                
VACSRCHP DS    V                                                                
VDATCON  DS    V                                                                
VHELLO   DS    V                                                                
VADDAY   DS    V                                                                
VGETDAY  DS    V                                                                
*&&UK                                                                           
VPROMOTE DS    V                                                                
*&&                                                                             
VRAPPER  DS    V                                                                
VGETBRD  DS    V                                                                
VADDBUK  DS    V                                                                
VGLPOST  DS    V                                                                
                                                                                
VSWITCH  DS    V                   ON-LINE ONLY                                 
VDICTATE DS    V                                                                
VPROTON  DS    V                   ON-LINE ONLY                                 
VPROTOFF DS    V                   ON-LINE ONLY                                 
                                                                                
ABASE    DS    A                                                                
ATRNBLK  DS    A                                                                
RELO     DS    A                                                                
SAVERD   DS    A                                                                
                                                                                
DUB      DS    D                                                                
DUB2     DS    D                                                                
DUB3     DS    D                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
BYTE1    DS    X                                                                
BYTE2    DS    X                                                                
WORK     DS    XL64                                                             
SPACES   DS    CL64                                                             
ELEMENT  DS    XL256                                                            
                                                                                
PARM     DS    6A                                                               
                                                                                
DMCB     DS    6A                                                               
                                                                                
ELIST    DS    3A                  HELLO PARAMETER LIST                         
ELERR    DS    0X                  HELLO ERROR RETURN BYTE                      
ELADDR   DS    A                   HELLO ELEMENT ADDRESS (GET)                  
ELADDR2  DS    A                   HELLO ELEMENT ADDRESS (ADD)                  
ELADDR3  DS    A                                                                
ELADDR4  DS    A                                                                
                                                                                
VDATAMGR DS    V                                                                
*&&UK                                                                           
VTOBACCO DS    V                                                                
*&&                                                                             
                                                                                
ATCB     DS    A                   A(TCB)                                       
ATIA     DS    A                   A(TIA)    - ON-LINE ONLY                     
AMINBUF2 DS    A                   A(MINIO2) - ON-LINE ONLY                     
                                                                                
AACT     DS    A                   A(ACCOUNT RECORD)                            
AOFA     DS    A                   A(OFFICE/ACCOUNT RECORD)                     
ACAC     DS    A                   A(CONTRA-HEADER RECORD)                      
ABUK     DS    A                   A(BUCKET LIST) (SEE:- BUKBUFD)               
APAL     DS    A                   A(P&L BUCKET AREA)                           
ALDG     DS    A                   A(LEDGER TABLE)                              
ALDGNTRY DS    A                   A(LEDGER TABLE ENTRY)                        
                                                                                
SAVERE   DS    A                   RE SAVE AREA                                 
SAVERF   DS    A                   RF SAVE AREA                                 
SAVER0   DS    A                   R0 SAVE AREA                                 
SAVER1   DS    A                   R1 SAVE AREA                                 
                                                                                
DMSAVERE DS    A                   RE SAVE AREA FOR DATAMGR ROUTINES            
                                                                                
AELEMS   DS    0A                  ADDRESSES OF TRANSATION ELEMENTS             
ATRXEL   DS    A                   A(TRANSACTION EXTRA STATUS ELEMENT)          
APRTEL   DS    A                   A(PRODUCTION CHARGE RATE ELEMENT)            
AAFCEL   DS    A                   A(FOREIGN CURRENCY ELEMENT)                  
AMXPEL   DS    A                   A(MEDIA EXTRA PAYMENT ELEMENT)               
AFFNEL   DS    A                   A(FREE FORM NUMBER ELEMENT)                  
AMPYEL   DS    A                   A(PAYMENT ELEMENT)                           
*&&UK                                                                           
AFFTEL   DS    A                   A(AGENT/ARTIST CODE ELEMENT)                 
*&&                                                                             
AELEML   EQU   *-AELEMS                                                         
                                                                                
DATADISP DS    H                   DISPLACEMENT TO FIRST ELEMENT                
SUBREF   DS    H                   TRANSACTION SUBREFERENCE                     
SAVKREF  DS    CL(L'TRNKREF)       SAVED KEY REFERENCE NUMBER                   
ROFKREF  DS    CL(L'TRNKREF)       2ND SAVED KEY REFERENCE NUMBER               
ACTLEV   DS    X                   ACCOUNT LEVEL FOR SEARCH                     
                                                                                
FILE     DS    CL8                 FILE NAME (ACCFIL/ACCMST)                    
FILETRN  DS    CL8                 FILE NAME FOR TRANSACTIONS                   
                                                                                
DMBYTE1  DS    X                   DATAMGR WORK BYTE 1                          
DMBYTE2  DS    X                   DATAMGR WORK BYTE 2                          
                                                                                
SAVEINDS DS    XL(L'TRNINDS)       SAVED TRNINDS VALUE                          
                                                                                
RESTORE  DS    C                   YES / NO                                     
PAYINDS  DS    X                   EXTRACTED PAYTINDS VALUE                     
PAY2IND  DS    X                   PAY2JOB INDICATOR          NMAL              
PAY2L34  EQU   X'01'               NOT DRAFT + TRAN TYPE -34  NMAL              
PAY2CSH  EQU   X'10'               TRAN HAVE CASH ELEM        NMAL              
PAY2NCS  EQU   X'20'               ACCT DON'T HAVE CASH       NMAL              
                                                                                
INDS1    DS    X                   ** GLOBAL INDICATORS - 1 **                  
IND2FDUM EQU   X'80'               DUMMY TRANSACTION (TYPE 255)                 
INDRET47 EQU   X'40'               TYPE 47 RETAIL TRANSACTION                   
IND1RDUM EQU   X'20'               1R CONTRA=SPACES                             
INDPAYAC EQU   X'10'               ACCOUNT IS PAYABLES                          
INDPAYCA EQU   X'08'               CONTRA-ACCOUNT IS PAYABLES                   
INDPAYCR EQU   X'04'               LOOK FOR MATCHING PAYABLE CREDIT             
INDPAYDR EQU   X'02'               PAYABLE CREDIT MATCHED TO DEBIT              
INDMATCH EQU   X'01'               FOUND A MATCHING TRANSACTION                 
                                                                                
INDS2    DS    X                   ** GLOBAL INDICATORS - 2 **                  
INDPRODN EQU   X'80'               PRODUCTION LEDGER                            
INDKEYWC EQU   X'40'               LEDGER HAS W/C IN KEY (NOT OFFICE)           
INDFCURR EQU   X'20'               TRANSACTION IS IN FOREIGN CURRENCY           
INDXSTAT EQU   X'10'               TRXEL FOUND ON REVERSING TRANSACTION         
INDFXPTA EQU   X'08'               FULLY TRANSFERRING PTA ELEMENT N/F           
INDOCAEL EQU   X'04'               OCAEL CREATED                                
INDGENLG EQU   X'02'               GENERAL LEDGER POSTINGS                      
                                                                                
POSTINDS DS    X                   ** TRANSACTION INDICATORS **                 
POSTPRVC EQU   X'80'               WAS PREVIOUSLY CLOSED                        
POSTPRVO EQU   X'40'               WAS PREVIOUSLY OPEN                          
POSTCURC EQU   X'08'               IS CURRENTLY CLOSED                          
POSTCURO EQU   X'04'               IS CURRENTLY OPEN                            
                                                                                
MOS      DS    PL2                 TRANSACTION/BUCKET MOS (INPUT)               
MOS2     DS    PL2                 TRANSACTION/BUCKET MOS (REVERSAL)            
MOSREV   DS    PL2                 TRANSACTION/BUCKET MOS (REVERSAL)            
                                                                                
CLOS     DS    X                   LEDGER CLOSE OUT TYPE                        
CLOSTAT1 DS    X                   ACCOUNT STATUS BYTE 1 VALUE                  
CLOSTAT3 DS    X                   ACCOUNT STATUS BYTE 3 VALUE                  
CLOSMOS  DS    PL2                 OFFICE LATEST CLOSED MOS                     
                                                                                
SSCITSAM DS    PL6                 SAVED SALES(NOT PRTBILQ > SCITMSRT)          
SSCITMAM DS    PL6                 SAVED MEMO (SCITMEXP > SCITMEXP)             
SSCITCAM DS    PL6                 SAVED SJ+TY49 SCITMCRT AMOUNT                
                                                                                
SVTRNAMT DS    PL6                                                              
SVEST    DS    CL6                 SAVED ESTIMATE                               
SVMOS    DS    XL2                 SAVED MOS                                    
SVCLI    DS    CL3                 SAVED CLIENT                                 
SVPRD    DS    CL3                 SAVED PRODUCT                                
SVSYS    DS    XL1                 SAVED SYSTEM                                 
SVUSED   DS    XL2                 SAVED USED DATE FROM 60 ELEMENT              
SVSTAT   DS    XL1                 SAVED TRANSACTION STATUS                     
ESTIND   DS    CL1                 ESTIMATE INDICATOR(Z=ZERO,B=BAD EST)         
BYTE     DS    XL1                                                              
FLAG1    DS    XL1                                                              
NOPTR    EQU   X'80'               NO AA POINTER NEEDED                         
MOSF     EQU   X'40'               MOS FOUND                                    
FLGBRD   EQU   X'20'               GETBROAD FLAG                                
                                                                                
MOSIND   DS    X                                                                
MOSADD   EQU   C'A'                ADD                                          
MOSUPD   EQU   C'U'                UPDATE                                       
MOSPKSV  DS    XL(L'MOSPKEY)                                                    
MOSPKSVS DS    XL(L'MOSPKSTA)                                                   
                                                                                
MAXLDGQ  EQU   10                                                               
LEDGERS# DS    XL1                 NUMBER OF LEDGERS SUPPORTED                  
LDGXTRA  DS    XL(LDGTABL)                                                      
LDGVALS  DS    0X                  ** EXTRACTED LEDGER VALUES **                
LDGOFFP  DS    XL(L'LDGOPOS)       LEDGER OFFICE POSITION                       
LDGCLIP  DS    XL(L'LDGCPOS)       LEDGER CLIENT POSITION                       
LDGL1AL  DS    XL(L'ACLVLEN)       LEDGER L'ACCOUNT LEVEL 1                     
LDGSTA2  DS    XL(L'LDGSTAT2)      LEDGER STATUS BYTE 2                         
LDGVALL  EQU   *-LDGVALS                                                        
                                                                                
METHOD   DS    CL(L'CACKBTYP)      ALLOCATION METHOD                            
CLIOFF   DS    CL2                 CLIENT OFFICE CODE                           
CPYOFF   DS    CL2                 COMPANY OFFICE CODE                          
CACTLN   DS    XL1                 SAVED CONTRA ACCOUNT LENGTH                  
LASTTKEY DS    XL42                LAST TRANSACTION KEY                         
                                                                                
BUKELEM1 DS    XL(BUKLN7Q)         BUCKET ELEMENT 1 BUILT HERE                  
BUKELEM2 DS    XL(BUKLN7Q)         BUCKET ELEMENT 2 BUILT HERE                  
BUKSAVE  DS    XL(BUKLN7Q)         BUCKET ELEMENT SAVE AREA                     
                                                                                
BUKEY    DS    0X                  ** BUCKET KEY **                             
BUKOFFC  DS    CL2                 BUCKET OFFICE                                
BUKTYPE  DS    C                   BUCKET TYPE                                  
BUKSTYP  DS    CL3                 BUCKET SUB-TYPE                              
BUKSTAT  DS    C                   BUCKET STATUS                                
BUKSLIVE EQU   C'L'                DATA IS LIVE                                 
BUKSDRFT EQU   C'D'                DATA IS DRAFT                                
BUKSXTRA EQU   C'X'                DATA IS LIVE EXTRA BUCKET                    
BUKEYL   EQU   *-BUKEY                                                          
                                                                                
PTRKEY   DS    0XL64               PASSIVE KEY BUILD AREA                       
BDPKEY   DS    0XL64               BDP PASSIVE POINTER KEY                      
GINKEY   DS    0XL64               GIN PASSIVE POINTER KEY                      
MOSKEY   DS    0XL64               MOS PASSIVE POINTER KEY                      
INVKEY   DS    0XL64               INVOICE NUMBER PASSIVE POINTER KEY           
RNSKEY   DS    0XL64               REF # SEARCH PASSIVE POINTER KEY             
AAVKEY   DS    0XL64               AUTO APPROVE VENDOR POINTER KEY              
AARKEY   DS    0XL64               AUTO APPROVE RECEIVABLE POINTER KEY          
TRSKEY   DS    XL64                TX SERIAL NUMBER PASSIVE POINTER             
                                                                                
STRSKEY  DS    XL64                SAVED TX SERIAL NUMBER PASSIVE               
SSERNUM  DS    XL4                 SAVED SERIAL NUMBER                          
                                                                                
DSDICTL  DS    0X                                                               
AC@NOORD DS    CL6                                                              
                                                                                
*&&US                                                                           
TIMVALS  DS    0X                  ** TIMTRN S/R VALUES **                      
TIMTOTL  DS    H                   TOTAL L'TIME ELEMENTS                        
TIMAFST  DS    AL3                 A(FIRST TIME ELEMENT)                        
TIMLIN#  DS    AL(L'RSTSNUM)       TIME LINE NUMBER                             
TIMSEQ#  DS    AL(L'TIMSEQ)        TIME SEQUENCE NUMBER                         
TIMVALL  EQU   *-TIMVALS                                                        
*&&                                                                             
                                                                                
IOCTL    DS    X                   ** I/O CONTROL BYTE **                       
IOCDIR   EQU   X'80'               READ DIRECTORY                               
IOCFIL   EQU   X'40'               READ FILE                                    
IOCUPD   EQU   X'01'               UPDATIVE I/O                                 
                                                                                
IOERR    DS    X                   ** I/O RETURN BYTE **                        
IOEEOF   EQU   X'80'               END OF FILE                                  
IOEIOE   EQU   X'40'               I/O ERROR                                    
IOERNF   EQU   X'10'               RECORD NOT FOUND                             
IOEBAD   EQU   IOEEOF+IOEIOE+IOERNF                                             
IOEDEL   EQU   X'02'               RECORD IS DELETED                            
                                                                                
SAVEDA   DS    XL(L'DA)            SAVED DISK ADDRESS                           
SAVEKEY  DS    XL(L'KEY)           SAVED DISK ADDRESS                           
DA       DS    XL4                 CURRENT DISK ADDRESS                         
IS       DS    XL8                 CURRENT INDEX SEQUENTIAL STATUS AREA         
TRNDA    DS    XL4                 TRANSACTION DISK ADDRESS                     
TRNIS    DS    XL8                 TRANSACTION STATUS AREA                      
WK       DS    XL40                CURRENT GETREC ETC. WORK AREA                
KEY      DS    XL64                CURRENT KEY                                  
KEYSAVE  DS    XL64                CURRENT KEY SAVE VALUE                       
KEYSAVE2 DS    XL64                SECOND KEY SAVE VALUE                        
                                                                                
IO       DS    0X                  I/O AREA FOR GENERAL READING                 
IOAREA   DS    XL1996                                                           
IOAREAL  EQU   *-IOAREA            LENGTH OF ACTUAL I/O AREA                    
IODA     DS    XL4                 DISK ADDRESS                                 
IOWK     DS    XL40                WORK AREA FOR GETREC, PUTREC ETC.            
IOIS     DS    XL8                 INDEX SEQUENTIAL RECORD STATUS BYTES         
IOL      EQU   *-IO                LENGTH OF I/O CONTROL BLOCK                  
                                                                                
       ++INCLUDE ACRAPPERD                                                      
                                                                                
       ++INCLUDE ACADDTRND                                                      
                                                                                
*&&UK                                                                           
RECOCA   DS    (OCANLENB)XL10      OCA VALUES FOR PASSED RECORD                 
REVOCA   DS    (OCANLENB)XL10      OCA VALUES FOR REVERSAL RECORD               
*&&                                                                             
                                                                                
WORKX    DS    0D                                                               
WORKL    EQU   *-WORKD                                                          
         EJECT                                                                  
* ACGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
         PRINT ON                                                               
BUKELD   DSECT                                                                  
         ORG   BUKDR                                                            
BUKDR7   DS    PL7                                                              
BUKCR7   DS    PL7                                                              
BUKLN7Q  EQU   *-BUKELD                                                         
                                                                                
* ACLDGTABD                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACLDGTABD                                                      
         PRINT ON                                                               
* ACGLPOSTD                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGLPOSTD                                                      
         PRINT ON                                                               
* DDCOMFACS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
* DDCTRYEQUS                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDCTRYEQUS                                                     
         PRINT ON                                                               
* ACDDEQUS                                                                      
         PRINT OFF                                                              
       ++INCLUDE ACDDEQUS                                                       
         PRINT ON                                                               
* FATCB                                                                         
         PRINT OFF                                                              
       ++INCLUDE FATCB                                                          
         PRINT ON                                                               
*&&UK                                                                           
* ACTOBACCOD                                                                    
         PRINT OFF                                                              
       ++INCLUDE ACTOBACCOD                                                     
         PRINT ON                                                               
*&&                                                                             
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'094ACADDTRN  06/30/20'                                      
         END                                                                    
