*          DATA SET SP133EST   AT LEVEL 026 AS OF 03/31/15                      
*CATALP SP133EST                                                                
         TITLE 'SP133DMP - SPOT DAILY ACTIVITY - ESTHDR DUMP/ROLL'              
SP133DMP CSECT                                                                  
         ENTRY SP133RST                                                         
         PRINT NOGEN                                                            
         NMOD1 0,**DMP**                                                        
         L     RC,=V(SP133WRK)                                                  
         USING SP133WRK,RC                                                      
*                                                                               
         L     RA,=V(CPRINT)                                                    
         USING DPRINT,RA                                                        
*                                                                               
         LA    R0,ESTHDR                                                        
         LHI   R1,1000                                                          
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         CLI   DUMPSW,C'N'                                                      
         BE    DMP0                                                             
         L     R8,=A(ESTOUT)       SET DDNAME TO ESTTPXY                        
         MVC   45(2,R8),SVSYS+4    THIS HAS TWO CHR SYSYEM ID XY                
         OPEN  ((8),(OUTPUT))                                                   
         SPACE 1                                                                
DMP0     XC    KEY,KEY                                                          
         MVI   KEY+1,X'11'                                                      
*                                                                               
         GOTO1 =V(DATAMGR),DMCB,=C'DMRDHI',=C'SPTDIR',KEY,KEY                   
         CLI   8(R1),0                                                          
         BE    *+8                                                              
         BAS   RE,DMPRDERR                                                      
         B     DMP4                                                             
         EJECT                                                                  
DMP2     GOTO1 =V(DATAMGR),DMCB,=C'DMRSEQ',=C'SPTDIR',,KEY                      
         CLI   8(R1),0                                                          
         BE    *+8                                                              
         BAS   RE,DMPRDERR                                                      
*                                                                               
DMP4     CLI   KEY,0               TEST FOR ESTHDR                              
         BNE   DMPEOF                                                           
         CLI   KEY+7,0                                                          
         BE    DMP2                                                             
         CLI   KEY+8,0                                                          
         BNE   DMP2                                                             
* GET ESTHDR                                                                    
         GOTO1 (RF),(R1),=C'GETREC',=C'SPTFILE',KEY+14,ESTHDR                   
*                                                                               
         CLI   8(R1),0                                                          
         BE    *+8                                                              
         BAS   RE,DMPRDERR                                                      
*                                                                               
         CLC   KEY(13),ESTHDR                                                   
         BE    *+8                                                              
         BAS   RE,DMPKYERR                                                      
*                                                                               
         MVC   ESTDA,KEY+14                                                     
         EJECT                                                                  
*===================================================================            
* WRITE ESTIMATE TO BACKUP TAPE                                                 
* WITH CHANGE TO NET BUCKETS, ALL ESTIMATES NEED TO BE WRITTEN                  
*===================================================================            
                                                                                
         CP    ECURPDN,=P'0'                                                    
         BE    DMP12                                                            
         ZAP   ECURPDN,=P'0'                                                    
*                                                                               
         CLI   WRITESW,C'Y'                                                     
         BNE   DMP12                                                            
*                                                                               
         GOTO1 =V(DATAMGR),DMCB,=C'DMWRT',=C'SPTFILE'                           
*                                                                               
         CLI   8(R1),0                                                          
         BE    *+8                                                              
         BAS   RE,DMPWTERR                                                      
*                                                                               
DMP12    AP    ESTCTR,=P'1'                                                     
*                                                                               
         CLI   DUMPSW,C'N'                                                      
         BE    DMP14                                                            
*                                                                               
         L     R1,=A(ESTOUT)                                                    
         LA    R0,ESTHDR           THANKS PWES !                                
         PUT   (1),(0)                                                          
*                                                                               
DMP14    B     DMP2                READ NEXT                                    
         EJECT                                                                  
* ALL ESTHDRS READ                                                              
*                                                                               
DMPEOF   CLI   DUMPSW,C'N'                                                      
         BE    DMPX                                                             
         OI    ESTCTR+3,X'0F'                                                   
         UNPK  P+1(6),ESTCTR                                                    
         MVC   P+10(20),ESTCTR+4                                                
         MVC   P+31(6),=C'DUMPED'                                               
         GOTO1 =V(LOGIO),DMCB,1,(37,P)                                          
         GOTO1 =V(PRINTER)                                                      
*                                                                               
         L     R8,=A(ESTOUT)                                                    
         CLOSE ((8))                                                            
*                                                                               
DMPX     XC    ESTDA,ESTDA                                                      
         XIT1                                                                   
         EJECT                                                                  
DMPRDERR MVC   P(27),=C'*SP133* ESTIMATE READ ERROR'                            
         B     DMPERRX                                                          
DMPWTERR MVC   P(28),=C'*SP133* ESTIMATE WRITE ERROR'                           
         B     DMPERRX                                                          
DMPKYERR MVC   P(31),=C'*SP133* DIRECTORY/FILE MISMATCH'                        
*                                                                               
DMPERRX  ST    RE,FULL                                                          
         GOTO1 =V(LOGIO),P1,1,(31,P)                                            
         GOTO1 =V(PRINTER)                                                      
         MVC   P(4),=C'DMCB'                                                    
         GOTO1 VHEXOUT,P1,DMCB,P+10,16,=C'1D'                                   
         GOTO1 =V(PRINTER)                                                      
*                                                                               
         MVC   P(3),=C'KEY'                                                     
         GOTO1 VHEXOUT,P1,KEY,P+10,18,=C'1D'                                    
         GOTO1 =V(PRINTER)                                                      
         MVC   P(3),=C'EST'                                                     
         GOTO1 VHEXOUT,P1,ESTHDR,P+10,18,=C'1D'                                 
         GOTO1 =V(PRINTER)                                                      
*                                                                               
         L     RE,FULL                                                          
         BR    RE                                                               
         LTORG                                                                  
         TITLE 'SP133RST - SPOT DAILY ACTIVITY - ESTHDR RESTORE'                
         DROP  RA,RB,RC                                                         
SP133RST NMOD1 0,**RST**                                                        
         L     RC,=V(SP133WRK)                                                  
         USING SP133WRK,RC                                                      
         L     RA,=V(CPRINT)                                                    
         USING DPRINT,RA                                                        
         SPACE 2                                                                
* TYPE WARNING MESSAGE                                                          
         MVC   RSTMSG1+20(6),SVSYS MOVE SYSTEM TO MSG                           
RST2     LA    R0,LRSTMSG1                                                      
         GOTO1 =V(LOGIO),P1,1,((R0),RSTMSG1)                                    
*                                                                               
         MVC   DUB,SPACES                                                       
         GOTO1 (RF),(R1),0,(8,DUB)                                              
*                                                                               
         CLC   =CL8'GO',DUB                                                     
         BE    RST10                                                            
         CLC   =CL8'CANCEL',DUB                                                 
         BE    RSTCNCL                                                          
* INVALID RESPONSE                                                              
         LA    R0,LRSTMSG2                                                      
         GOTO1 (RF),(R1),1,((R0),RSTMSG2)                                       
         B     RST2                                                             
* CANCEL                                                                        
RSTCNCL  DC    0H'0'                                                            
         ABEND 999                                                              
         EJECT                                                                  
RST10    L     R8,=A(ESTIN)        SET DDNAME TO ESTTPXY                        
         MVC   45(2,R8),SVSYS+4    THIS HAS TWO CHR SYSYEM ID XY                
         OPEN  ((8),(INPUT))                                                    
*                                                                               
         BAS   RE,GETEST           READ FIRST TAPE RECORD                       
*                                                                               
         XC    KEY,KEY             AND FIRST ESTHDR ON SPTDIR                   
         MVI   KEY+1,X'11'                                                      
         GOTO1 =V(DATAMGR),DMCB,=C'DMRDHI',=C'SPTDIR',KEY,KEY                   
         B     RST14                                                            
*                                                                               
RST12    GOTO1 =V(DATAMGR),DMCB,=C'DMRSEQ',=C'SPTDIR',KEY,KEY                   
*                                                                               
RST14    CLI   KEY,0               TEST A HEADER                                
         BNE   RSTEOF              NO - DONE                                    
         CLI   KEY+7,0             TEST EST NUM PRESENT                         
         BE    RST12               NO - SKIP                                    
         CLI   KEY+8,0             TEST BILL RECORD                             
         BNE   RST12               YES - IGNORE                                 
* READ ESTHDR FROM SPTFILE                                                      
         GOTO1 =V(DATAMGR),DMCB,=C'DMRDIR',=C'SPTFILE',KEY+14,ESTHDR            
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
RST20    CLC   KEY(13),TPEST       FILE KEY TO TAPE KEY                         
         BE    RST30                                                            
         BL    RST32               FILE LOW - EST NOT ON TAPE                   
         BAS   RE,GETEST           FILE HIGH - GET NEXT EST ON TAPE             
         B     RST20                                                            
         EJECT                                                                  
RST30    DS    0H                  RESTORE BUCKETS IF CHANGED                   
                                                                                
         MVI   CHANGED,C'N'                                                     
         LA    RE,EORD-ESTHDR+TPEST                                             
         CLC   EORD(EORDX-EORD),0(RE)                                           
         BE    *+14                                                             
         MVI   CHANGED,C'Y'                                                     
         MVC   EORD(EORDX-EORD),0(RE)                                           
*                                                                               
         LA    RE,EORDNET-ESTHDR+TPEST                                          
         CLC   EORDNET(EORDNETX-EORDNET),0(RE)                                  
         BE    *+14                                                             
         MVI   CHANGED,C'Y'                                                     
         MVC   EORDNET(EORDNETX-EORDNET),0(RE)                                  
*                                                                               
         LA    RE,EPAID-ESTHDR+TPEST                                            
         CLC   EPAID(EPAIDX-EPAID),0(RE)                                        
         BE    *+14                                                             
         MVI   CHANGED,C'Y'                                                     
         MVC   EPAID(EPAIDX-EPAID),0(RE)                                        
*                                                                               
         LA    RE,EPDNET-ESTHDR+TPEST                                           
         CLC   EPDNET(EPDNETX-EPDNET),0(RE)                                     
         BE    *+14                                                             
         MVI   CHANGED,C'Y'                                                     
         MVC   EPDNET(EPDNETX-EPDNET),0(RE)                                     
*                                                                               
         CP    ECURPDN,=P'0'                                                    
         BE    *+14                                                             
         MVI   CHANGED,C'Y'                                                     
         ZAP   ECURPDN,=P'0'                                                    
*                                                                               
RST32    CLI   CHANGED,C'Y'                                                     
         BNE   RST34                                                            
         CLI   WRITESW,C'Y'                                                     
         BNE   RST34                                                            
         GOTO1 =V(DATAMGR),DMCB,=C'DMWRT',=C'SPTFILE',KEY+14,ESTHDR             
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
RST34    B     RST12               READ NEXT                                    
*                                                                               
RSTEOF   OI    ESTCTR+3,X'0F'                                                   
         UNPK  P+1(6),ESTCTR                                                    
         MVC   P+10(20),ESTCTR+4                                                
         MVC   P+31(8),=C'RESTORED'                                             
         GOTO1 =V(LOGIO),DMCB,1,(40,P)                                          
         GOTO1 =V(PRINTER)                                                      
*                                                                               
         L     R8,=A(ESTIN)                                                     
         CLOSE ((8))                                                            
         XC    ESTDA,ESTDA                                                      
         XIT1                                                                   
         SPACE 1                                                                
*======================================================*                        
* READ NEXT ESTHDR FROM INPUT TAPE                     *                        
*======================================================*                        
         SPACE 1                                                                
GETEST   NTR1                                                                   
         L     R1,=A(ESTIN)                                                     
         LA    R0,TPEST                                                         
         GET   (1),(0)                                                          
         AP    ESTCTR,=P'1'                                                     
         XIT1                                                                   
CHANGED  DC    C'N'                                                             
         EJECT                                                                  
RSTRDERR MVC   P(27),=C'*SP133* ESTIMATE READ ERROR'                            
         B     RSTERRX                                                          
RSTWTERR MVC   P(28),=C'*SP133* ESTIMATE WRITE ERROR'                           
         B     RSTERRX                                                          
RSTKYERR MVC   P(31),=C'*SP133* DIRECTORY/FILE MISMATCH'                        
*                                                                               
RSTERRX  ST    RE,FULL                                                          
         GOTO1 =V(LOGIO),P1,1,(31,P)                                            
         GOTO1 =V(PRINTER)                                                      
         MVC   P(4),=C'DMCB'                                                    
         GOTO1 VHEXOUT,P1,DMCB,P+10,16,=C'1D'                                   
         GOTO1 =V(PRINTER)                                                      
*                                                                               
         MVC   P(3),=C'KEY'                                                     
         GOTO1 VHEXOUT,P1,KEY,P+10,18,=C'1D'                                    
         GOTO1 =V(PRINTER)                                                      
         MVC   P(3),=C'EST'                                                     
         GOTO1 VHEXOUT,P1,ESTHDR,P+10,18,=C'1D'                                 
         GOTO1 =V(PRINTER)                                                      
*                                                                               
         L     RE,FULL                                                          
         BR    RE                                                               
         SPACE 2                                                                
RSTMSG1  DC    C'* SP133 * RESTORING SPOTXY ESTHDRS. PROCEED?'                  
LRSTMSG1 EQU   *-RSTMSG1                                                        
RSTMSG2  DC    C'* SP133 * REPLY GO OR CANCEL'                                  
LRSTMSG2 EQU   *-RSTMSG2                                                        
         LTORG                                                                  
         EJECT                                                                  
         PRINT NOGEN                                                            
ESTIN    DCB   DDNAME=ESTIN,                                           X        
               DSORG=PS,                                               X        
               RECFM=FB,                                               X        
               MACRF=GM,                                               X        
               EODAD=RSTEOF                                                     
*                                                                               
         ORG   ESTIN+40                                                         
         DC    CL8'ESTTPXY'                                                     
         ORG                                                                    
ESTOUT   DCB   DDNAME=ESTOUT,                                          X        
               DSORG=PS,                                               X        
               RECFM=FB,                                               X        
               LRECL=874,                                              X        
               BLKSIZE=17480,                                          X        
               MACRF=PM                                                         
*                                                                               
         ORG   ESTOUT+40                                                        
         DC    CL8'ESTTPXY'                                                     
         ORG                                                                    
*                                                                               
         DS    0D                                                               
         DC    CL8'*TPEST*'                                                     
TPEST    DS    1000C                                                            
         PRINT OFF                                                              
         EJECT                                                                  
SP133WRK DSECT                                                                  
       ++INCLUDE SP133WRK                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'026SP133EST  03/31/15'                                      
         END                                                                    
