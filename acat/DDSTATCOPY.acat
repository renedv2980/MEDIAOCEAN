*          DATA SET DDSTATCOPY AT LEVEL 015 AS OF 05/01/02                      
*CATALP STATCOPY                                                                
*UPSI 1XXXXXXX TAPE INPUT ELSE TAPE OUTPUT (EQU INTAPE)                         
*UPSI X1XXXXXX INHIBIT DISK UPDATE         (EQU NODISKIO)                       
*UPSI XX1XXXXX INHIBIT TAPE I/0'S          (EQU NOTAPEIO)                       
*UPSI XXX1XXXX ENABLE ADROUT I/O'S         (EQU ENADROUT)                       
         SPACE 2                                                                
         TITLE 'STATCOPY - COPY ADRFILE TO TAPE'                                
         PRINT NOGEN                                                            
STATUPDT CSECT                                                                  
         ENTRY SSB                                                              
         ENTRY UTL                                                              
         NBASE 0,STATCOPY,WORK=A(STATWORK)                                      
GETSVP   ST    R1,ACOMRG           SAVE MVS PARM ADDR                           
         L     R1,0(R1)                                                         
         LH    R2,0(R1)            R2=L'PARM DATA                               
         LTR   R2,R2                                                            
         BZ    GETSVPX                                                          
         CH    R2,=H'8'                                                         
         BNH   *+8                                                              
         LA    R2,8                                                             
         LA    R1,2(R1)            R1=A(PARM DATA)                              
         LA    RF,GETSVPT                                                       
GETSVP1  CLI   0(R1),C','                                                       
         BE    GETSVPX                                                          
         CLI   0(R1),C'0'                                                       
         BE    GETSVP2                                                          
         CLI   0(R1),C'1'                                                       
         BNE   GETSVP2                                                          
         OC    UPSI,0(RF)                                                       
GETSVP2  LA    R1,1(R1)                                                         
         LA    RF,1(RF)                                                         
         BCT   R2,GETSVP1                                                       
         B     GETSVPX                                                          
GETSVPT  DC    X'8040201008040201'                                              
GETSVPX  EQU   *                                                                
         SPACE 2                                                                
INTAPE   EQU   X'80'                                                            
NODISKIO EQU   X'40'                                                            
NOTAPEIO EQU   X'20'                                                            
ENADROUT EQU   X'10'                                                            
         EJECT                                                                  
INITIAL  L     RA,=V(CPRINT)                                                    
         USING DPRINT,RA                                                        
         MVC   UTIME,TIMEUP        SYSTEM START UP TIME                         
         XC    DTIME,DTIME                                                      
         DATE  DATE,FUNNY=NO                                                    
         TIME                                                                   
         ST    R1,ALTDATE          CREATE 4 BYTE PACKED FORMAT                  
*                                                                               
         XC    DMCB(24),DMCB                                                    
         LA    R2,DMCB                                                          
         L     RE,=V(OPEN)                                                      
         ST    RE,DMCB                                                          
         L     RE,=V(STATS)                                                     
         ST    RE,DMCB+12                                                       
         LA    R1,DMCB                                                          
         L     RF,=V(DMOD000)                                                   
         BASR  RE,RF               OPEN STATISTICS FILE                         
         L     RE,=V(ADRFILE)                                                   
         ST    RE,DMCB+12                                                       
         BASR  RE,RF               OPEN RECORDER FILE                           
*                                                                               
         LA    RF,OPENOUT          OPEN TAPE FILE                               
         TM    UPSI,INTAPE                                                      
         BZ    *+8                                                              
         LA    RF,OPENIN                                                        
         BASR  R9,RF                                                            
         EJECT                                                                  
         L     R6,=A(NDXREC)                                                    
         LR    R0,R6                                                            
         BAS   R9,SSTATS           READ PROGRAM INDEX RECORD                    
         BNE   INPUT                                                            
         LH    R7,2(R6)                                                         
         BCTR  R7,0                R7=NUM OF TERMINAL INDEX RECORDS             
         L     R6,=A(TRMREC)                                                    
INIT1    SH    R6,=H'16'                                                        
         MVC   TEMP(16),0(R6)                                                   
         LR    R0,R6                                                            
         BAS   R9,SSTATS           READ TERMINAL INDEX RECORD                   
         BNE   INPUT                                                            
         MVC   0(16,R6),TEMP                                                    
         LA    R6,1216(R6)                                                      
         BCT   R7,INIT1                                                         
         B     INPUT                                                            
         EJECT                                                                  
INPUT    LA    R2,ADRRCD           R2=A(ADPAK RECORDER FILE RECORD)             
         USING ADRRECD,R2                                                       
*                                                                               
INPUT0   LH    R6,IOPTRMAX                                                      
         LA    R6,1(R6)                                                         
         STH   R6,IOPTR                                                         
         SPACE 2                                                                
INPUT1   L     R3,=A(IOBUFF)                                                    
         CLC   IOPTR,IOPTRMAX                                                   
         BNH   INPUT5                                                           
         MVC   IOPTR,=H'1'                                                      
*                                                                               
         TM    UPSI,INTAPE         TEST TAPE INPUT                              
         BZ    INPUT2                                                           
         BAS   R9,GETIN                                                         
         B     INPUT5                                                           
         SPACE 2                                                                
INPUT2   GOTO1 DATAMGR,DMCB,DMRSEQ,ADRFILE,ADRADR,(R3)                          
INPUT4   TM    8(R1),X'80'                                                      
         BO    ENDIT               END-OF-FILE                                  
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
         SPACE 2                                                                
INPUT5   LH    R7,IOPTR            R7=INDEX INTO BLOCK                          
         BCTR  R7,0                                                             
         MH    R7,=H'64'                                                        
         AR    R3,R7                                                            
         MVC   ADRRCD,0(R3)                                                     
         SPACE 2                                                                
INPUT6   LTR   R7,R7               TEST FIRST CHR OF NEW BLOCK                  
         BNZ   *+12                                                             
         CLI   ADRREC,C'*'         IF EQ C'*' THEN WRITE TO ADROUT              
         BE    INPUT6A                                                          
         CLI   ADRREC,C'A'         TEST FIRST CHR OF NEW RECORD                 
         BNL   INPUT7                                                           
         LH    R7,IOPTR            IF LT C'A' THEN IGNORE RECORD                
         LA    R7,1(R7)                                                         
         STH   R7,IOPTR                                                         
         B     INPUT1                                                           
*                                                                               
INPUT6A  BAS   R9,PUTOUT                                                        
         B     INPUT0                                                           
         SPACE 2                                                                
INPUT7   MVC   ALTREC(64),ADRRCD   TRM = 1/LINE/ADDR                            
         SPACE 2                                                                
         LH    R7,IOPTR                                                         
         LA    R7,1(R7)                                                         
         STH   R7,IOPTR                                                         
         XC    DUB,DUB             SEARCH PRG INDEX                             
         L     R7,=A(NDXREC)                                                    
         LA    R7,16(R7)                                                        
INPUT10  CLI   0(R7),X'FF'                                                      
         BE    INPUT14                                                          
         CLC   0(1,R7),ADROVSYS    SAME SYSNUM                                  
         BNE   INPUT12             NO SKIP ENTRY                                
         CLC   1(1,R7),ADRPRGNO                                                 
         BE    INPUT16                                                          
         CLI   1(R7),0                                                          
         BNE   INPUT12                                                          
         MVI   DUB,1               SET MISC PROG ENTRY FOUND                    
         ST    R7,DUB+4                                                         
INPUT12  LA    R7,21(R7)                                                        
         B     INPUT10                                                          
INPUT14  CLI   DUB,1               WAS MISC PROG ENTRY FOUND                    
         BNE   INPUT1              NO DO NOT RELEASE                            
         L     R7,DUB+4                                                         
INPUT16  MVC   ALTREC+64(14),2(R7) EXTRACT SYS & PRG NAMES                      
*                                                                               
         CLI   DUB,1                                                            
         BNE   INPUT17                                                          
         MVC   ALTREC+45(7),=C'PROG=NN'                                         
         GOTO1 =V(HEXOUT),DMCB,ADRPRGNO,ALTREC+50,2,=C'TOG'                     
*                                                                               
INPUT17  GOTO1 =V(DATCON),DMCB,(5,0),(0,WORK) YYMMDD                            
         OI    ALTREC+52,X'F0'                                                  
         L     RF,=F'-1'                                                        
         GOTO1 =V(ADDAY),DMCB,WORK,WORK+6,(RF)                                  
         GOTO1 =V(DATCON),DMCB,(0,WORK+6),(8,ALTREC+52) DDMMMYY                 
         B     INPUTX              RELEASE MISC PROG RECORD TO O/P              
         EJECT                                                                  
INPUTX   OC    COUNT,COUNT         FIRST TIME PRINT START-UP TIME               
         BNZ   INPUTX2                                                          
         MVC   STIME,UTIME         SAVE EST START TIME                          
         CLC   UTIME,ADRSTTM                                                    
         BH    *+10                                                             
         MVC   UTIME,ADRSTTM                                                    
*                                                                               
INPUTX2  L     R1,COUNT            BUMP TRANSACTION TIME                        
         AH    R1,=H'1'                                                         
         ST    R1,COUNT                                                         
         MVC   DTIME,UTIME                                                      
         MVC   UTIME,ADRNDTM                                                    
*                                                                               
         BAS   R9,PUTOUT2                                                       
         CLC   0(8,R2),=8X'FF'                                                  
         B     INPUT1                                                           
ENDIT    BAS   R9,CLOSEOUT                                                      
         BAS   R9,CLSEIN                                                        
         XBASE                                                                  
         DROP  R2                                                               
         EJECT                                                                  
RSTATS   LA    R8,DMRDIR                                                        
         B     IOSTATS                                                          
WSTATS   LA    R8,DMWRT                                                         
         TM    UPSI,NODISKIO       TEST OPTION (NO WRITE)                       
         BO    IOSTATSX                                                         
         B     IOSTATS                                                          
SSTATS   LA    R8,DMRSEQ                                                        
         SPACE 2                                                                
IOSTATS  GOTO1 DATAMGR,DMCB,(R8),STATS,DSKADR,(R0)                              
         CLI   8(R1),0                                                          
         BE    IOSTATSX                                                         
         B     IOSTATSX            I DON'T CARE JUST DONT DIE ON ME             
         DC    H'0'                                                             
IOSTATSX BR    R9                                                               
         EJECT                                                                  
OPENIN   L     R8,=A(ADRIN)        OPEN INPUT TAPE FILE                         
         TM    UPSI,NOTAPEIO                                                    
         BO    OPENINX                                                          
         OPEN  ((R8),INPUT)                                                     
OPENINX  BR    R9                                                               
         SPACE 2                                                                
OPENOUT  TM    UPSI,ENADROUT                                                    
         BZ    OPENO01                                                          
         L     R8,=A(ADROUT)       OPEN OUTPUT TAPE FILE                        
         OPEN  ((8),OUTPUT)                                                     
OPENO01  L     R8,=A(ADROUT2)                                                   
         OPEN  ((8),OUTPUT)                                                     
OPENOUTX BR    R9                                                               
         SPACE 2                                                                
GETIN    L     R8,=A(ADRIN)        GET INPUT TAPE RECORD                        
         L     R0,=A(IOBUFF)                                                    
         TM    UPSI,NOTAPEIO                                                    
         BO    GETINX                                                           
         GET   (8),(0)                                                          
GETINX   BR    R9                                                               
         SPACE 2                                                                
PUTOUT   L     R8,=A(ADROUT)       PUT OUTPUT TAPE RECORD                       
         L     R0,=A(IOBUFF)                                                    
         PUT   (8),(0)                                                          
         B     PUTOUTX                                                          
PUTOUT2  L     R8,=A(ADROUT2)      PUT ALT OUTPUT TAPE RECORD                   
         LA    R0,ALTREC                                                        
         PUT   (8),(0)                                                          
PUTOUTX  BR    R9                                                               
         SPACE 2                                                                
CLSEIN   L     R8,=A(ADRIN)        CLOSE INPUT TAPE FILE                        
         TM    UPSI,NOTAPEIO                                                    
         BO    CLSEINX                                                          
         CLOSE ((8))                                                            
CLSEINX  BR    R9                                                               
         SPACE 2                                                                
CLOSEOUT L     R8,=A(ADROUT2)                                                   
         CLOSE ((8))                                                            
CLSEOUTX BR    R9                                                               
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
         DS    0F                  BASIC TIME UNITS AND PARAMATERS              
*                                  UNITS ARE SECONDS/100                        
TUMINTM  DC    A(0480*60*100)      EARLIEST TRANSACTION HOUR....08.00           
TUHOUR   DC    A(0060*60*100)      LENGTH OF ONE HOUR IN TUS                    
DOWNDUR  DC    A(0002*60*100)      DOWN TIME AT LEAST 2 MINUTES                 
TIMEUP   DC    A(0510*60*100)      SYSTEM UP TIME IN TU'S.......08.30           
TIMEDOWN DC    A(1140*60*100)      SYSTEM DOWN TIME.............19.00           
SECFCTR  DC    F'100'              FACTOR FOR TUS TO SECS                       
MSFCTR   DC    H'1'                FACTOR FOR TUS TO SECS/100                   
         EJECT                                                                  
DUB      DS    D                                                                
TEMP     DS    CL80                                                             
SAVER1   DS    F                                                                
ACOMRG   DS    F                                                                
ADRADR   DC    F'0'                                                             
DSKADR   DC    F'0'                                                             
COUNT    DC    F'0'                                                             
DTIME    DC    F'0'                                                             
UTIME    DC    F'0'                                                             
STIME    DC    F'0'                                                             
TUQTM    DC    H'10'                                                            
IOPTR    DC    H'0'                                                             
IOPTRMAX DC    H'64'               ADRFILE BLOCKING FACTOR                      
FLAG     DC    X'00'                                                            
NEWTRM   DC    X'00'                                                            
*                                                                               
DMCB     DS    6F                                                               
DATAMGR  DC    V(DATAMGR)                                                       
DMRSEQ   DC    CL8'DMRSEQ'                                                      
DMRDIR   DC    CL8'DMRDIR'                                                      
DMWRT    DC    CL8'DMWRT'                                                       
ADRFILE  DC    CL8'ADRFILE'                                                     
STATS    DC    CL8'STATS'                                                       
*                                                                               
SUBA     DC    C'  TAKEN     BROUGHT       DOWN   REASON'                       
SUBB     DC    C' DOWN AT     UP AT        TIME   ------'                       
*                                                                               
DATE     DS    CL6,CL2                                                          
ALTDATE  DS    F                                                                
UPSI     DC    X'00'                                                            
WORK     DS    CL20                                                             
*                                                                               
ADRRCD   DC    XL64'00'                                                         
LADRRCD  DC    XL64'00'                                                         
ALTREC   DC    XL87'00'                                                         
*                                                                               
ROWHDR   DS    6F                                                               
ROWS     DS    150F                                                             
TROWHDR  DS    6F                                                               
TROWS    DS    150F                                                             
IOBUFF   DS    2000F                                                            
NDXREC   DS    4008F               MAX 200 PROGRAMS                             
TRMREC   DS    27000F              MAX 9000 TERMINALS                           
         DS    4F                                                               
         EJECT                                                                  
ADROUT   DCB   DDNAME=ADROUT,DSORG=PS,MACRF=(PM),                      X        
               RECFM=F,BLKSIZE=6400                                             
*                                                                               
ADROUT2  DCB   DDNAME=ADROUT2,DSORG=PS,MACRF=(PM),                     X        
               RECFM=FB,BLKSIZE=21960,LRECL=61                                  
*                                                                               
ADRIN    DCB   DDNAME=ADRIN,DSORG=PS,MACRF=(GM),EODAD=ENDIT,           X        
               RECFM=F,BLKSIZE=6400                                             
         EJECT                                                                  
STATWORK DS    0D                                                               
         DS    1024D                                                            
         SPACE 2                                                                
SSB      DS    0D                                                               
         DC    X'00000002'                                                      
         SPACE 2                                                                
UTL      DS    0D                                                               
         DC    F'0',X'01'                                                       
         SPACE 2                                                                
* FAADRREC                                                                      
       ++INCLUDE FAADRREC                                                       
*                                                                               
         ORG   ADRRECD             INTERNAL VERSION OF 1ST 16 BYTES             
ADRTYPE  DS    C                                                                
ADRTYPE1 DS    C                                                                
ADRNAME1 DS    CL7                                                              
ADRNAME2 DS    CL7                                                              
         EJECT                                                                  
       ++INCLUDE DDSTATREC                                                      
         SPACE 2                                                                
* DDDPRINT                                                                      
         PRINT OFF                                                              
       ++INCLUDE DDDPRINT                                                       
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'015DDSTATCOPY05/01/02'                                      
         END                                                                    
