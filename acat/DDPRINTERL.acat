*          DATA SET DDPRINTERL AT LEVEL 012 AS OF 12/06/13                      
*$PANAPT01S$ *******  FIRST LINE TO DELETE  *****************                   
*                                                           *                   
*  ATTN: THE LEVEL STAMPS ARE *LEGITIMATELY* OUT-OF-SYNC!   *                   
*        DELETE THIS COMMENT BLOCK ON THE NEXT PROMOTION!   *                   
*                                                           *                   
*  THIS SOURCE MODULE IS AT A HIGHER LEVEL NUMBER THAN ITS  *                   
*  CORRESPONDING LOAD OR OBJECT MODULE, BECAUSE THE MEMBER  *                   
*  WAS PROMOTED USING THE SRCE LIBCODE VIA MR# 043179.      *                   
*                                                           *                   
*  THIS COMMENT BLOCK WAS INSERTED *PROGRAMMATICALLY* BY    *                   
*  PANAPT, TO EXPLAIN WHY THE LEVEL STAMPS ARE OUT-OF-SYNC. *                   
*  BEFORE THIS MEMBER CAN BE PROMOTED AGAIN, THIS ENTIRE    *                   
*  COMMENT BLOCK *MUST* BE MANUALLY DELETED.                *                   
*                                                           *                   
*$PANAPT01X$ *******  LAST LINE TO DELETE  ******************                   
*PROCESS USING(WARN(15))                                                        
***********************************************************************         
*                                                                     *         
* DEIS DEC/2013:                                                      *         
* DDPRINTERL USED TO BE A SOURCE MODULE WHICH GENERATED SEVERAL       *         
* RMPRINT* MEMBERS. IT WAS NEVER PROMOTABLE VIA PANAPT, WHICH MEANT   *         
* THAT WE COULD NOT EASILY CHANGE IT. THE FORMER DDPRINTERL HAS NOW   *         
* BEEN SPLIT UP INTO THREE SOURCE MODULES: DDPRINTER, DDPRINTERL, AND *         
* DDPRINT132.                                                         *         
* THE PROBLEM IS THAT RMPRINTER AND RMPRINTERL HAVE NOT BEEN          *         
* ASSEMBLED FOR ALMOST 15 YEARS, AND SINCE WE HAVE NO MATCHING SOURCE *         
* FOR THESE RELOS, IT WOULD SEEM IMPRUDENT (I.E., STUPID) TO          *         
* REGENERATE THEM NOW. SO WE'RE DOING SOURCE-ONLY PROMOTIONS OF       *         
* DDPRINTER AND DDPRINTERL WHICH WE *HOPE* AND ASSUME CAN BE USED     *         
* GOING FORWARD IF WE EVER DO NEED TO CHANGE THEM.                    *         
*                                                                     *         
***********************************************************************         
*CATALP PRINTERL                                                                
         TITLE 'PRINTER - PRINT CONTROLLING MODULE'                             
         PRINT NOGEN                                                            
PRINTER  CSECT                                                                  
         NMOD1 WORKX-WORKD,**PRTL**,RR=RA                                       
         USING WORKD,RC                                                         
         ST    RA,RELO                                                          
*                                                                               
         STCM  RF,8,DUB                                                         
         MVC   VTYPES(VTYPESL),ADCONS1                                          
         CLI   DUB,0                                                            
         BE    INIT                                                             
         MVC   VTYPES(VTYPESL),ADCONS2                                          
*                                                                               
INIT     L     RA,VCPRINT          POINT TO CSECT CONTAINING PRINT DATA         
         USING DPRINT,RA                                                        
         A     RA,RELO                                                          
         CLI   PCTLFRST,C'Y'       TEST CALLER RESET OF FIRST TIME FLAG         
         BNE   *+10                                                             
         MVC   TIMENOW,=4X'FF'                                                  
INIT2    CLC   TIMENOW,=4X'FF'     TEST FIRST TIME                              
         BNE   INITX                                                            
         THMS                                                                   
         ST    R1,TIMENOW          SAVE TIME=P'0HHMMSS+'                        
         DATE  WORK,FUNNY=NO                                                    
         MVC   TODAY,WORK          SAVE DATE=C'YYMMDD'                          
*&&US                                                                           
         CP    MAXLINE,=P'60'      IBM LASER CANT DO 60 LINES ON PAGE           
         BL    *+10                                                             
         ZAP   MAXLINE,=P'59'                                                   
*&&                                                                             
         MVI   PCTLFRST,C'N'       RESET FIRST TIME FLAG                        
         L     R5,VBOXAREA                                                      
         USING BOXD,R5                                                          
INITA    TM    DDPDDCTL,X'80'      TEST IF DATA DICTIONARY REQUIRED             
         BZ    INITB                                                            
         LA    RE,LANGDATA                                                      
INITA1   CLI   0(RE),X'FF'         SEARCH FOR LANGUAGE                          
         BE    INITB                                                            
         CLC   0(1,RE),DDPLANG                                                  
         BE    *+12                                                             
         LA    RE,L'LANGDATA(RE)                                                
         B     INITA1                                                           
         MVC   HEADDATE(8),04(RE)  SET DATE/TIME/PAGE                           
         MVC   HEADTIME(8),12(RE)                                               
         MVC   HEADPAGE(8),20(RE)                                               
         MVC   MONTHS(36),28(RE)   SET MONTHS                                   
         MVC   BOXDDCTL(4),DDPDDCTL                                             
*                                                                               
INITB    MVC   COLUMNS(5),COLSMAX                                               
         CLC   COLUMNS(1),COLS080  TEST FOR STANDARD 80                         
         BNE   *+14                                                             
         MVC   COLUMNS(5),COLS080                                               
         B     INITX                                                            
         CLC   COLUMNS(1),COLS110  TEST FOR STANDARD 110                        
         BNE   *+14                                                             
         MVC   COLUMNS(5),COLS110                                               
         B     INITX                                                            
         CLC   COLUMNS(1),COLS132  TEST FOR STANDARD 132                        
         BH    *+14                                                             
         MVC   COLUMNS(5),COLS132                                               
         B     INITX                                                            
         ZIC   R0,COLSMAX          SET FOR LONG PRINT LINE                      
         STCM  R0,15,BOXWIDTH                                                   
INITX    LA    R2,PBYTE            SET PARAMETER LIST FOR V(PRINT)              
         LA    R3,PCONTROL                                                      
         STM   R2,R3,PARAS                                                      
         EJECT                                                                  
* PCLTBOX MAY BE SET BY CALLER TO CARRY OUT VARIOUS ACTIONS                     
* IF RULE (B OR D) OR OPEN BOX (O) REQUEST, DO IT BEFORE PRINT                  
* IF ROOM ON PAGE, OTHERWISE START NEW PAGE INSTEAD.                            
* CLOSE BOX (C) REQUEST IS DONE ON SAME LINE AS PRINT, BUT IGNORED              
* IF NO BOX IS OPEN.                                                            
*                                                                               
PBOX     CLI   PCTLBOX,C' '                                                     
         BNH   PRNTHEAD            SKIP IF NO BOX REQUEST                       
         L     R5,VBOXAREA                                                      
         CLI   PCTLBOX,C'C'                                                     
         BE    PBOXC               SKIP IF BOX CLOSE REQUEST                    
         AP    LINE,=P'1'                                                       
         CP    LINE,MAXLINE        ANY LINES LEFT AFTER RULE/TOP                
         BNL   PRHEAD0             NO - SKIP TO CHANNEL 1 INSTEAD               
*                                                                               
         MVC   BOXREQ,PCTLBOX      COPY PCTLBOX REQUEST                         
         MVC   PLINE,SPACES                                                     
         MVC   PCONTROL,=C'BL01'                                                
         BAS   R9,PRINTEM          PRINT RULE/TOP OVER BLANK LINE               
         B     PRDATA              THEN GO PRINT LINE                           
*                                                                               
PBOXC    CLI   BOXSTAT,C'I'        BOX CLOSE, ARE WE IN A BOX ?                 
         BNE   PRDATAX             NO - EXIT WITHOUT PRINTING                   
         MVI   BOXREQ,C'C'         SET CLOSE REQUEST                            
         B     PRDATA              CLOSE BOX WITHOUT TESTING LINE COUNT         
         DROP  R5                                                               
         EJECT                                                                  
* CONTROL OF HEADLINE PRINTING                                                  
*                                                                               
PRNTHEAD CP    LINE,MAXLINE        HEADLINES NEEDED                             
         BL    PRDATA                                                           
PRHEAD0  MVC   PLINE,SPACES        SKIP TO CHANNEL 1                            
         MVC   PCONTROL,=C'BC01'                                                
         BAS   R9,PRINTEM                                                       
*                                                                               
         MVC   WORK(8),TODAY       GET TODAYS DATE C'YYMMDD'                    
         CLC   SPECDATE(5),=C'DATE='                                            
         BNE   *+10                UNLESS OVERRIDEN                             
         MVC   WORK(6),SPECDATE+5                                               
         CLI   WORK,C'9'           TEST IF PASSED DDS FUNNY DATE                
         BNH   PRHEAD1                                                          
         SR    RE,RE               ADJUST FOR YR2000+ DATES                     
         IC    RE,WORK                                                          
         SH    RE,=H'10'                                                        
         STC   RE,WORK                                                          
PRHEAD1  LA    RE,WORK+16                                                       
         MVC   0(9,RE),SPACES                                                   
PRHEAD1A TM    DDPDDCTL,X'80'                                                   
         BZ    *+12                                                             
         CLI   DDPLANG,2           ENGLISH HAS DD MMM YY                        
         BH    PRHEAD1B                                                         
         MVC   0(2,RE),WORK+4                                                   
         PACK  DUB,WORK+2(2)                                                    
         CVB   R2,DUB                                                           
         BCTR  R2,0                                                             
         MH    R2,=H'3'                                                         
         LA    R2,MONTHS(R2)                                                    
         MVC   3(3,RE),0(R2)                                                    
         MVC   7(2,RE),WORK                                                     
         B     PRHEAD1C                                                         
PRHEAD1B MVC   0(2,RE),WORK+4      EUROPE HAS DD.MM.YY                          
         MVI   2(RE),C'.'                                                       
         MVC   3(2,RE),WORK+2                                                   
         MVI   5(RE),C'.'                                                       
         MVC   6(2,RE),WORK                                                     
PRHEAD1C LA    RF,HEADDATE                                                      
         CLI   0(RF),C' '                                                       
         BNH   *+12                                                             
         LA    RF,1(RF)                                                         
         B     *-12                                                             
         MVC   1(9,RF),0(RE)       SET DATE AFTER DATE TEXT                     
*                                                                               
PRHEAD2  MVC   DUB(4),TIMENOW      GET TIME                                     
         UNPK  WORK(6),DUB(4)                                                   
         LA    RE,WORK+16                                                       
         MVC   0(2,RE),WORK        SET HH.MM                                    
         MVI   2(RE),C'.'                                                       
         MVC   3(2,RE),WORK+2                                                   
PRHEAD2A LA    RF,HEADTIME                                                      
         CLI   0(RF),C' '                                                       
         BNH   *+12                                                             
         LA    RF,1(RF)                                                         
         B     *-12                                                             
         MVC   1(5,RF),0(RE)       SET TIME AFTER TIME TEXT                     
*                                                                               
PRHEAD3  LA    R2,HEADPAGE         SET PAGE NUMBER AFTER PAGE TEXT              
         CLI   0(R2),C' '                                                       
         BNH   *+12                                                             
         LA    R2,1(R2)                                                         
         B     *-12                                                             
         EDIT  (P4,PAGE),(4,1(R2)),ALIGN=LEFT                                   
*                                                                               
         ZAP   PACK,=P'0'          TEST IF NO HEADLINE REQUIRED                 
         OC    PCCDATE(4),PCCDATE                                               
         BZ    PRMID                                                            
*                                                                               
PRHEAD4  ZIC   R7,PCCDATE          GET COLUMN FOR DATE                          
         SH    R7,=H'1'                                                         
         BM    PRHEAD5                                                          
         LA    R7,PLINE(R7)                                                     
         MVC   0(16,R7),HEADDATE                                                
PRHEAD5  ZIC   R7,PCCTIME          GET COLUMN FOR TIME                          
         SH    R7,=H'1'                                                         
         BM    PRHEAD6                                                          
         LA    R7,PLINE(R7)                                                     
         MVC   0(12,R7),HEADTIME                                                
PRHEAD6  ZIC   R7,PCCTITLE         GET COLUMN FOR TITLE                         
         SH    R7,=H'1'                                                         
         BM    PRHEAD7                                                          
         LA    R7,PLINE(R7)                                                     
         MVC   0(60,R7),TITLE                                                   
PRHEAD7  ZIC   R7,PCCPAGE          GET COLUMN FOR PAGE NUMBER                   
         SH    R7,=H'1'                                                         
         BM    PRHEADX                                                          
         LA    R7,PLINE(R7)                                                     
         MVC   0(11,R7),HEADPAGE                                                
*                                                                               
PRHEADX  MVC   PCONTROL,=C'BL01'   PRINT HEADLINE                               
         BAS   R9,PRINTEM                                                       
         AP    PACK,=P'1'          BUMP HEADLINE COUNTER                        
         SPACE 2                                                                
* ROUTINE TO UNDERLINE TITLE                                                    
*                                                                               
ULHEAD   MVC   PLINE,SPACES        CLEAR OUT HEADLINE                           
         MVC   PCONTROL,=C'BL01'                                                
         CLI   CHRULT,X'00'                                                     
         BE    ULHEADX             TEST IF NO UNDERLINE CHR                     
         LA    R2,TITLE                                                         
         ZIC   R5,PCCTITLE                                                      
         SH    R5,=H'1'                                                         
         BM    ULHEAD5             TEST IF NO TITLE DEFINED                     
         LA    R5,PLINE(R5)                                                     
         LA    R4,60                                                            
*                                                                               
ULHEAD1  CLI   0(R2),C' '          A(FIRST NON-SPACE) TO R2                     
         BNE   ULHEAD2                                                          
         LA    R2,1(R2)                                                         
         LA    R5,1(R5)                                                         
         BCT   R4,ULHEAD1                                                       
         B     ULHEAD5             EXIT IF NO DATA IN TITLE                     
*                                                                               
ULHEAD2  LA    R3,TITLE+59         A(LAST NON-SPACE) TO R3                      
         ZIC   R6,PCCTITLE                                                      
         SH    R6,=H'1'                                                         
         LA    R6,PLINE+59(R6)                                                  
*                                                                               
ULHEAD3  CLI   0(R3),C' '                                                       
         BNE   ULHEAD4                                                          
         BCTR  R6,0                                                             
         BCT   R3,ULHEAD3                                                       
*                                                                               
ULHEAD4  MVC   0(1,R5),ULCHR       FILL WITH DASHES                             
         MVC   0(1,R5),CHRULT                                                   
         CR    R5,R6                                                            
         BE    ULHEAD5                                                          
         LA    R5,1(R5)                                                         
         B     ULHEAD4                                                          
*                                                                               
ULHEAD5  MVC   PCONTROL,=C'BL02'   SET TO UNDERLINE TITLE AND SPACE             
         AP    PACK,=P'1'                                                       
*                                                                               
ULHEADX  BAS   R9,PRINTEM          PRINT LINE(S) AFTER TITLE                    
         AP    PACK,=P'1'                                                       
         EJECT                                                                  
* CONTROL OF MIDLINE PRINTING                                                   
*                                                                               
PRMID    ZAP   LINE,=P'0'          SET LINE COUNTER                             
         AP    LINE,PACK                                                        
         AP    PAGE,=P'1'          SET PAGE COUNTER                             
*                                                                               
         CLC   MID1,SPACES         EXIT IF NO MIDLINES                          
         BE    PRSUB                                                            
         MVC   PLINE,MID1                                                       
         CLC   MID2,SPACES                                                      
         BE    PRMIDX                                                           
         MVI   PCONTROL+3,C'1'                                                  
         BAS   R9,PRINTEM          PRINT MIDLINE 1                              
         AP    LINE,=P'1'                                                       
         MVC   PLINE,MID2                                                       
         CLC   MID3,SPACES                                                      
         BE    PRMIDX                                                           
         BAS   R9,PRINTEM          PRINT MIDLINE 2                              
         AP    LINE,=P'1'                                                       
         MVC   PLINE,MID3                                                       
         CLC   MID4,SPACES                                                      
         BE    PRMIDX                                                           
         BAS   R9,PRINTEM          PRINT MIDLINE 3                              
         AP    LINE,=P'1'                                                       
         MVC   PLINE,MID4                                                       
*                                                                               
PRMIDX   MVI   PCONTROL+3,C'2'     PRINT LAST MIDLINE AND SPACE 2               
         BAS   R9,PRINTEM                                                       
         AP    LINE,=P'2'                                                       
         SPACE 2                                                                
* CONTROL OF SUBLINE PRINTING                                                   
*                                                                               
PRSUB    CLC   SUB1,SPACES         EXIT IF NO SUBLINES                          
         BE    PRDATA                                                           
         MVC   PLINE,SUB1                                                       
         CLC   SUB2,SPACES                                                      
         BE    PRSUBX                                                           
         MVI   PCONTROL+3,C'1'                                                  
         BAS   R9,PRINTEM          PRINT SUBLINE 1                              
         AP    LINE,=P'1'                                                       
         MVC   PLINE,SUB2                                                       
         CLC   SUB3,SPACES                                                      
         BE    PRSUBX                                                           
         BAS   R9,PRINTEM          PRINT SUBLINE 2                              
         AP    LINE,=P'1'                                                       
         MVC   PLINE,SUB3                                                       
         SPACE 2                                                                
PRSUBX   MVI   PCONTROL+3,C'2'     PRINT LAST SUBLINE AND SPACE 2               
         BAS   R9,PRINTEM                                                       
         AP    LINE,=P'2'                                                       
         EJECT                                                                  
* PRINT USERS LINE AND RETURN                                                   
*                                                                               
PRDATA   MVC   PLINE,P             USER PRINT LINE IS IN P                      
         MVC   PCONTROL,SPACING                                                 
         BAS   R9,PRINTEM                                                       
         PACK  DUB,SPACING+2(2)                                                 
         AP    LINE,DUB                                                         
PRDATAX  MVC   P,SPACES            CLEAR USER PRINT LINE AFTER PRINTING         
         MVI   PCTLBOX,C' '        RESET BOX                                    
         SPACE 2                                                                
EXIT     XMOD1 1                                                                
         SPACE 2                                                                
PRINTEM  GOTO1 VPRINT,PARAS,RR=RELO                                             
         BR    R9                                                               
         EJECT                                                                  
         DS    0F                                                               
TIMENOW  DC    XL4'FFFFFFFF'       TIME                                         
TODAY    DC    CL8'YYMMDDCC'       TODAYS DATE AND COUNTRY CODE                 
ULCHR    DC    C'-'                UNDERLINE CHARACTER                          
LANGLAST DC    X'FF'               LAST LANGUAGE CODE                           
*                                                                               
COLUMNS  DC    AL1(000)            COLUMN WIDTH                                 
PCCDATE  DC    AL1(000)            COLUMN FOR DATE                              
PCCTIME  DC    AL1(000)            COLUMN FOR TIME                              
PCCTITLE DC    AL1(000)            COLUMN FOR TITLE                             
PCCPAGE  DC    AL1(000)            COLUMN FOR PAGE NUMBER                       
*                                                                               
COLS080  DC    AL1(080,000,000,001,064)                                         
COLS110  DC    AL1(110,001,000,031,100)                                         
COLS132  DC    AL1(132,001,020,035,100)                                         
         SPACE 1                                                                
LANGDATA DS    0CL64                                                            
*                                                                               
LANGENG  DC    AL1(0),X'000000'                                                 
         DC    C'DATE    ',C'TIME    ',C'PAGE    '                              
         DC    C'JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC'                          
LANGENG1 DC    AL1(1),X'000000'                                                 
         DC    C'DATE    ',C'TIME    ',C'PAGE    '                              
         DC    C'JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC'                          
LANGENG2 DC    AL1(2),X'000000'                                                 
         DC    C'DATE    ',C'TIME    ',C'PAGE    '                              
         DC    C'JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC'                          
*                                                                               
LANGGER  DC    AL1(3),X'000000'                                                 
         DC    C'DATUM   ',C'ZEIT    ',C'SEITE   '                              
         DC    C'JANFEBMAEAPRMAIJUNJULAUGSEPOKTNOVDEZ'                          
*                                                                               
LANGFRE  DC    AL1(4),X'000000'                                                 
         DC    C'DATE    ',C'HEURE   ',C'PAGE    '                              
         DC    C'JANFEVMARAVRMAIJUNJULAOUSEPOCTNOVDEC'                          
*                                                                               
LANGDUT  DC    AL1(7),X'000000'                                                 
         DC    C'DATUM   ',C'TIJD    ',C'PAGINA  '                              
         DC    C'JANFEBMRTAPRMEIJUNJULAUGSEPOKTNOVDEC'                          
*                                                                               
LANGTABX DC    X'FFFFFFFF'                                                      
         SPACE 1                                                                
         LTORG                                                                  
                                                                                
ADCONS1  DC    V(PRINT,CPRINT,BOXAREA)                                          
ADCONS2  DC    V(PRINT2,CPRINT2,BOXAREA2)                                       
         EJECT                                                                  
* DSECT FOR PRINT CONTROLLER                                                    
*                                                                               
WORKD    DSECT                                                                  
                                                                                
VTYPES   DS    0V                                                               
VPRINT   DS    V                                                                
VCPRINT  DS    V                                                                
VBOXAREA DS    V                                                                
VTYPESL  EQU   *-VTYPES                                                         
                                                                                
DUB      DS    D                                                                
PARAS    DS    CL8                                                              
RELO     DS    A                                                                
PACK     DS    PL4                                                              
WORK     DS    CL32                                                             
PCONTROL DS    CL4                                                              
PBYTE    DS    CL1                                                              
PLINE    DS    CL198                                                            
WORKX    EQU   *                                                                
         EJECT                                                                  
       ++INCLUDE DDDPRINTL                                                      
         EJECT                                                                  
       ++INCLUDE DDCPRINTL                                                      
       ++INCLUDE DDCPRINT2L                                                     
         EJECT                                                                  
       ++INCLUDE DDBIGBOX                                                       
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'012DDPRINTERL12/06/13'                                      
         END                                                                    
