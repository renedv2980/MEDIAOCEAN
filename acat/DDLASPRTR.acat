*          DATA SET DDLASPRTR  AT LEVEL 003 AS OF 05/01/02                      
*CATALP LASPRTR                                                                 
         PRINT NOGEN                                                            
         SPACE 1                                                                
PRINTER  CSECT                                                                  
         NMOD1 42,PRINTER                                                       
         USING DCONT,RC                                                         
         L     RA,=A(CPRINT)                                                    
         USING DPRINT,RA                                                        
         SPACE 1                                                                
         MVC   PLINE,SPACES                                                     
         SPACE 1                                                                
         TM    MODE,X'24'          WIDE-PAGE BITS                               
         BZ    *+8                   NO                                         
         B     WIDEBITS              YES                                        
         SPACE 1                                                                
         TM    MODE,X'0A'          PAGING FLAGS                                 
         BZ    *+8                                                              
         B     PAGING                                                           
         SPACE 1                                                                
         TM    MODE,X'C0'          HEAD LINE - FITS ON PAGE                     
         BM    PRNTUSER                                                         
         SPACE 1                                                                
         TM    MODE,X'10'          NEW COLUMN HEADS IN PAGE BODY                
         BO    PRTCLHDS              YES                                        
         SPACE 1                                                                
         PACK  DUB(2),SPACING+2(2)                                              
         AP    DUB(2),LINE                                                      
         CLI   FIRSTPG,C'Y'        ADDRESS AT BOTTOM OF 1ST PAGE                
         BE    PG1TEST               YES                                        
         CP    DUB(2),MAXLINE      WILL LINE FIT?                               
         BH    ENDPAGE               NO                                         
         SPACE 1                                                                
PRNTUSER LA    R2,PLINE+3                                                       
         MVC   PLINE(4),SPACES                                                  
         CLI   ONESIDE,C'Y'        RECTO PGS ONLY                               
         BE    ODDLINE               NO                                         
         TM    PAGE+1,X'10'          NO  - RECTO NOW                            
         BO    ODDLINE                       YES                                
         LA    R2,PLINE                      NO  - SET FOR EVEN                 
ODDLINE  MVC   0(125,R2),P                                                      
         MVC   P,SPACES                                                         
         SPACE 1                                                                
         TM    MODE,X'C0'          UNDERSCORED HEAD?                            
         BM    DOHEAD                YES                                        
         SPACE 1                                                                
         MVC   PCONTROL,SPACING      NO                                         
         BAS   R7,PRINTEM                                                       
         SPACE 1                                                                
EXIT     XMOD1 1                                                                
         TITLE '6670 MANUALS PRINTER - NOT TRANSPARENT'                         
PRINTEM  BAS   R9,PRINTIT                                                       
         PACK  DUB(2),PCONTROL+2(2)                                             
         AP    LINE,DUB(2)                                                      
         BR    R7                                                               
         SPACE 1                                                                
PRINTIT  GOTO1 =V(PRINT),PARAS,PBYTE,PCONTROL                                   
         MVC   PLINE,SPACES                                                     
         BR    R9                                                               
         SPACE 1                                                                
MOVEIT   GOTO1 =V(PRINT),PARAS,SPACES-1,PCONTROL                                
         BR    R9                                                               
         SPACE 2                                                                
DOHEAD   MVC   PCONTROL,=C'BL02'                                                
         TM    MODE,X'40'                                                       
         BZ    *+14                                                             
         BAS   R9,MOVEIT           SKIP TWO LINES                               
         AP    LINE,=P'2'          COUNT THEM                                   
         NI    MODE,X'3F'                                                       
         LA    R3,80(R2)                                                        
         LA    R4,80                                                            
         MVI   PCONTROL+3,C'3'                                                  
         CLI   0(R2),C' '          A(FIRST NON-SPACE) TO R2                     
         BNE   *+16                                                             
         LA    R2,1(R2)                                                         
         BCT   R4,*-12                                                          
         B     EXIT                                                             
         SPACE 1                                                                
         CLI   0(R3),C' '                                                       
         BNE   *+8                                                              
         BCT   R3,*-8                                                           
         MVC   1(2,R3),=X'6AE6'    NON-TRANSP WUS                               
         SR    R3,R2               L(USCORE) IN R3                              
         EX    R3,UFINISH                                                       
         BAS   R7,PRINTEM                                                       
         B     EXIT                                                             
         SPACE 1                                                                
UFINISH  TR    0(0,R2),REQSPC                                                   
         EJECT                                                                  
*                   SPECIAL PAGE HANDLING                                       
         SPACE 1                                                                
PAGING   TM    MODE,X'02'          FINISH PAGE AND RETURN                       
         BO    ENDPAGE               YES                                        
         XI    MODE,X'08'          START NEW PAGE                               
         B     STARTPGE                                                         
         SPACE 1                                                                
ENDPAGE  CLI   WIDEFLAG,C'Y'       ANOTHER WIDE LINE PAGE                       
         BE    PAGEEND+4             YES                                        
         SPACE 1                                                                
         CLC   TITLE(8),SPACES     NO RUNNING FOOT                              
         BE    PAGEEND+4             YES                                        
         SPACE 1                                                                
         MVC   PCONTROL,=C'BC02'                                                
         BAS   R9,MOVEIT                                                        
         MVC   PCONTROL,=C'BL01'                                                
         BAS   R8,PAGENUM                                                       
         CLI   ONESIDE,C'Y'        RECTO PGS ONLY                               
         BE    ODDFOOT               YES                                        
         TM    PAGE+1,X'10'          NO  - RECTO NOW                            
         BO    ODDFOOT                       YES                                
         SPACE 1                                                                
         MVC   PLINE+72(10),REVDATE          NO                                 
         MVC   PLINE(10),PGNOWORK                                               
         LA    R2,PLINE+11                                                      
         LH    R3,LOFTITLE                                                      
         EX    R3,MOVEFOOT                                                      
         B     PAGEEND                                                          
         SPACE 1                                                                
MOVEFOOT MVC   0(0,R2),TITLE                                                    
         SPACE 1                                                                
ODDFOOT  MVC   PLINE+3(10),REVDATE                                              
         LA    R2,PLINE+74                                                      
         LH    R3,LOFTITLE                                                      
         SR    R2,R3                                                            
         EX    R3,MOVEFOOT                                                      
         LA    R2,PLINE+85                                                      
         SH    R2,LOFPGNO                                                       
         MVC   0(10,R2),PGNOWORK                                                
         SPACE 1                                                                
PAGEEND  BAS   R9,PRINTIT                                                       
         AP    PAGE,=P'1'                                                       
         ZAP   LINE,=P'0'                                                       
         CLI   REPFLAG,C'Y'                                                     
         BNE   PAGEENDX                                                         
         CLI   POINTNUM,C'Y'                                                    
         BE    PAGEENDX                                                         
         SPACE 1                                                                
         CP    PAGE,LASTPG         REP RANGE FILLED                             
         BNH   PAGEENDX              NO                                         
         SPACE 1                                                                
         MVI   POINTNUM,C'Y'                                                    
         MVC   SVPNOLEN,LOFPGNO                                                 
         ZAP   PAGE,=P'1'                                                       
         EJECT                                                                  
PAGEENDX TM    MODE,X'02'          FINISH PAGE AND RETURN                       
         BZ    STARTPGE              NO                                         
         XI    MODE,X'02'            YES - RESET BIT                            
         B     EXIT                                                             
         SPACE 3                                                                
WIDEBITS TM    MODE,X'20'          START (OR CONTINUE) WIDE PAGE(S)             
         BO    WIDESTRT              YES                                        
         SPACE 1                                                                
ENDWIDE  MVI   MODE,X'0A'                                                       
         MVI   WIDEFLAG,C' '       RESET FLAG                                   
         MVI   SKIPSET,C'Y'                                                     
         MVC   PLINE(3),=X'CAF461'                                              
         B     PAGEEND                                                          
         SPACE 1                                                                
WIDESTRT XI    MODE,X'20'                                                       
         CLI   WIDEFLAG,C'Y'       CONTINUED                                    
         BE    EXIT                  YES                                        
         SPACE 1                                                                
         MVI   SKIPSET,C'Y'          NO                                         
         MVI   WIDEFLAG,C'Y'                                                    
         MVC   PLINE(3),=X'CAF361'                                              
         MVC   PCONTROL,=C'BL01'                                                
         BAS   R9,PRINTIT                                                       
         B     EXIT                                                             
         EJECT                                                                  
*                   START NEW PAGE                                              
         SPACE 1                                                                
STARTPGE MVC   PCONTROL,=C'BC01'   SET SKIP TO NEW PAGE                         
         CLI   SKIPSET,C'Y'                                                     
         BE    *+8                                                              
         BAS   R9,MOVEIT             NO SKIPSET - START NEW PAGE                
         MVI   SKIPSET,C' '                                                     
         MVC   PCONTROL,=C'BL01'                                                
         MVC   PLINE,SPACES                                                     
         CLC   TITLE(8),SPACES     TITLE PRESENT                                
         BE    PRTCLHDS              NO  - TRY COLHEADS                         
         SPACE 1                                                                
         CLC   LOFTITLE,=H'0'      NEW TITLE                                    
         BNE   PRTCLHDS              NO                                         
         SPACE 1                                                                
         BAS   R8,TITLELEN           YES                                        
         CLI   ONESIDE,C'Y'        RECTO PGS ONLY                               
         BE    ODDTITLE              YES                                        
         TM    PAGE+1,X'10'          NO  - RECTO NOW                            
         BO    ODDTITLE                      YES                                
         LA    R2,PLINE                      NO                                 
         B     SETMOVT                                                          
         SPACE 1                                                                
ODDTITLE LA    R2,PLINE+85                                                      
         SR    R2,R4               LOFTITLE                                     
         SPACE 1                                                                
SETMOVT  EX    R4,MOVEFOOT                                                      
         EX    R4,CAPTITLE                                                      
         MVC   PCONTROL,=C'BL02'                                                
         BAS   R9,MOVEIT                                                        
         AP    LINE,=P'2'                                                       
         MVC   PCONTROL,=C'BL03'   TITLE + 2 SKIPPED LINES                      
         BAS   R7,PRINTEM                                                       
         B     PRTCLHDS            TRY COLHEADS BEFORE USER LINE PRINT          
         SPACE 1                                                                
CAPTITLE OC    0(0,R2),SPACES      MAKE TITLE ALL-CAPS                          
         EJECT                                                                  
*                   FORMAT PAGE NUMBER FOR PRINTING                             
         SPACE 1                                                                
PAGENUM  LA    R3,PGNOWORK                                                      
         TM    MODE,X'01'          FRONT MATTER                                 
         BZ    ARABIC                NO                                         
         SPACE 1                                                                
         MVC   PGNOWORK,SPACES                                                  
         TM    PAGE,X'03'                                                       
         BZ    DECADE0                                                          
         BO    DECADE3                                                          
         SPACE 1                                                                
         TM    PAGE,X'02'                                                       
         BO    DECADE2                                                          
         SPACE 1                                                                
         MVI   0(R3),X'A7'         TEN AND TEENS                                
         LA    R3,1(R3)                                                         
         B     DECADE0                                                          
         SPACE 1                                                                
DECADE2  MVC   0(2,R3),=X'A7A7'    TWENTIES                                     
         LA    R3,2(R3)                                                         
         B     DECADE0                                                          
         SPACE 1                                                                
DECADE3  MVC   0(3,R3),=X'A7A7A7'  THIRTIES                                     
         LA    R3,3(R3)                                                         
         SPACE 1                                                                
DECADE0  SR    R4,R4                                                            
         IC    R4,PAGE+1           UNITS DIGIT                                  
         SRA   R4,4                GET RID OF SIGN BITS                         
         SLA   R4,2                MULTIPLY UNITS DIGIT BY 4                    
         LA    R5,ROMANTBL(R4)     SET TABLE ADDR PLUS OFFSET                   
         MVC   0(4,R3),0(R5)                                                    
         B     PGNOLEN                                                          
         SPACE 1                                                                
ARABIC   CLI   POINTNUM,C'Y'                                                    
         BNE   ARABIC1                                                          
         SPACE 1                                                                
         LH    R3,SVPNOLEN                                                      
         LA    R3,PGNOWORK(R3)                                                  
         MVI   0(R3),C'.'                                                       
         LA    R3,1(R3)                                                         
         B     CONTIN                                                           
         SPACE 1                                                                
ARABIC1  MVC   PGNOWORK,SPACES                                                  
         CLI   SECTNUM,C' '        SECTIONAL PAGE NUMBERING                     
         BE    CONTIN                NO                                         
         SPACE 1                                                                
         CLI   SECTNUM,C'I'        INDEX                                        
         BE    ONEDIGIT                                                         
         TM    SECTNUM,X'F0'       YES - NUMERIC                                
         BO    ONEDIGIT                    YES                                  
         EJECT                                                                  
         MVN   P-1(1),SECTNUM                                                   
         NI    P-1,X'0F'                                                        
         SR    R4,R4                                                            
         IC    R4,P-1                                                           
         BCTR  R4,0                                                             
         SLA   R4,1                DOUBLE IT                                    
         LA    R4,DIGTAB(R4)                                                    
         MVC   0(2,R3),0(R4)                                                    
         MVI   2(R3),C'-'                                                       
         LA    R3,3(R3)                                                         
         B     CONTIN                                                           
         SPACE 1                                                                
ONEDIGIT MVC   0(1,R3),SECTNUM                                                  
         MVI   1(R3),C'-'                                                       
         LA    R3,2(R3)                                                         
CONTIN   EDIT  (P2,PAGE),(3,(R3)),ALIGN=LEFT                                    
         SPACE 1                                                                
PGNOLEN  LA    R3,10                                                            
         LA    R4,PGNOWORK+9                                                    
         CLI   0(R4),C' '                                                       
         BNE   *+10                                                             
         BCTR  R4,0                                                             
         BCT   R3,*-10                                                          
         SPACE 1                                                                
         STH   R3,LOFPGNO                                                       
         BR    R8                                                               
         SPACE 2                                                                
*                   FIND OUT TITLE LENGTH                                       
         SPACE 1                                                                
TITLELEN LA    R3,TITLE+59                                                      
         LA    R2,TITLE                                                         
         LA    R4,60                                                            
         SPACE 1                                                                
         CLI   0(R3),C' '                                                       
         BNE   LENGTH                                                           
         BCTR  R3,0                                                             
         BCT   R4,*-10                                                          
         SPACE 1                                                                
LENGTH   MVC   1(2,R3),=X'6AE6'    NON-TRANSP WUS                               
         EX    R4,UFINISH                                                       
         LA    R4,2(R4)                                                         
         STH   R4,LOFTITLE                                                      
         BR    R8                                                               
         EJECT                                                                  
*                   PRINT COLUMN HEADINGS AND REPEATS                           
         SPACE 1                                                                
PRTCLHDS CLC   COLHEADS,SPACES     ANYTHING TO PRINT                            
         BE    PRNTUSER              NO                                         
         SPACE 1                                                                
         TM    MODE,X'10'          NEW COLUMN HEADS                             
         BZ    CHPRINT               NO  - GO PRINT                             
         LA    R6,0                                                             
         LA    R3,COLHEADS                                                      
         LA    R4,COLHEADF                                                      
         MVC   COLHEADF,SPACES                                                  
         LA    R5,77(R3)                                                        
         SPACE 1                                                                
CHLP1    CLI   0(R3),C' '          LEADING SPACE                                
         BNE   CHLP2                 NO                                         
         LA    R3,1(R3)              YES                                        
         LA    R4,1(R4)                                                         
         CH    R6,=H'24'                                                        
         BNL   CHPRINT                                                          
         CR    R3,R5               MORE IN COLHEADS                             
         BNH   CHLP1                 YES                                        
         B     CHPRINT               NO  - SET TO PRINT                         
         SPACE 1                                                                
CHLP2    MVC   0(1,R4),0(R3)                                                    
         LA    R4,1(R4)                                                         
         LA    R3,1(R3)                                                         
         CR    R3,R5               CHECK LIMIT                                  
         BH    CHSETEND              REACHED                                    
         CLI   0(R3),C' '          TRAILING SPACE                               
         BNE   CHLP2                 NO - CONTINUE                              
         SPACE 1                                                                
         CLC   0(2,R3),SPACES      NON-FLOWING TEXT                             
         BE    *+12                  YES                                        
         MVI   0(R4),X'41'           NO  - PUT IN REQ SPACE                     
         B     CHLP2+6                     CONTINUE THIS ENTRY                  
         SPACE 1                                                                
         MVC   0(2,R4),=X'6AE6'    NON-TRANSP WUS                               
         LA    R4,4(R4)                                                         
         LA    R6,1(R6)            COUNT CH ENTRY                               
         LA    R3,2(R3)                                                         
         B     CHLP1                       LOOK FOR MORE                        
         SPACE 1                                                                
CHSETEND MVC   0(2,R4),=X'6AE6'    NON-TRANSP WUS                               
         EJECT                                                                  
CHPRINT  LA    R2,PLINE+3                                                       
         CLI   ONESIDE,C'Y'        RECTO PGS ONLY                               
         BE    ODDCOLHD              YES                                        
         TM    PAGE+1,X'10'          NO  - RECTO NOW                            
         BO    ODDCOLHD                      YES                                
         LA    R2,PLINE                      NO  - SET FOR EVEN                 
ODDCOLHD MVC   0(128,R2),COLHEADF                                               
         MVC   PCONTROL,=C'BL02'                                                
         BAS   R7,PRINTEM                                                       
         TM    MODE,X'10'                                                       
         BZ    PRNTUSER                                                         
         SPACE 1                                                                
         XI    MODE,X'10'          RESET NEW COLHEADS BIT                       
         CH    R6,=H'1'            SINGLE ENTRY                                 
         BNE   EXIT                  NO                                         
         SPACE 1                                                                
         LA    R3,COLHEADS+79        YES                                        
         LA    R5,0                                                             
         CLI   0(R3),C' '          FIND PLACE FOR (CONT'D)                      
         BNE   *+12                                                             
         LA    R5,1(R5)                                                         
         BCT   R3,*-12                                                          
         SPACE 1                                                                
         CH    R5,=H'11'           ROOM FOR IT                                  
         BL    EXIT                  NO                                         
         MVC   3(8,R4),=X'4DC39695A37D845D'     (CONT'D)                        
         B     EXIT                                                             
         SPACE 3                                                                
PG1TEST  CP    DUB(2),=P'48'       LIMIT FOR 1ST PAGE W/ADDRESS                 
         BNH   PRNTUSER              NOT YET                                    
         SPACE 1                                                                
         MVI   FIRSTPG,C' '          YES                                        
         MVC   PCONTROL,=C'BL01'                                                
         ZAP   DUB(2),=P'52'                                                    
         SP    DUB(2),LINE                                                      
         CP    DUB(2),=P'0'                                                     
         BNH   *+14                                                             
         UNPK  PCONTROL+2(2),DUB(2)                                             
         BAS   R9,MOVEIT                                                        
         SPACE 1                                                                
         MVC   PCONTROL,=C'BL01'                                                
         MVC   PLINE(6),=X'6AE36AE36AD1'  2 REQ TABS AND SWITCH                 
         CLI   SPACING+2,C'0'      USER LINE OVERFLOW                           
         BE    PAGEEND                                                          
         SPACE 1                                                                
         OI    MODE,X'02'            NO  - EJECT                                
         B     PAGEEND                                                          
         EJECT                                                                  
*                   WORK SPACE                                                  
         SPACE 1                                                                
COLHEADF DS    CL128                                                            
ROMANTBL DS    0CL40                                                            
         DC    CL4' '              UNIT = 0                                     
         DC    X'89404040'                1                                     
         DC    X'89894040'                2                                     
         DC    X'89898940'                3                                     
         DC    X'89A54040'                4                                     
         DC    X'A5404040'                5                                     
         DC    X'A5894040'                6                                     
         DC    X'A5898940'                7                                     
         DC    X'A5898989'                8                                     
         DC    X'89A74040'                9                                     
         DS    0H                                                               
DIGTAB   DC    C'1011121314151617'                                              
         SPACE 1                                                                
         LTORG                                                                  
         SPACE 2                                                                
*                  DSECT FOR PRINT CONTROLLER                                   
         SPACE 1                                                                
DCONT    DSECT                                                                  
PBYTE    DS    CL1                                                              
PLINE    DS    CL132                                                            
DUB      DS    D                                                                
WORK     DS    CL32                                                             
PCONTROL DS    CL4                                                              
PARAS    DS    CL8                                                              
         EJECT                                                                  
CPRINT   CSECT                                                                  
*              REQSPC TRANSLATE TABLE                                           
         DC    X'000102030405060708090A0B0C0D0E0F'                              
         DC    X'101112131415161718191A1B1C1D1E1F'                              
         DC    X'202122232425262728292A2B2C2D2E2F'                              
         DC    X'303132333435363738393A3B3C3D3E3F'                              
         DC    X'414142434445464748494A4B4C4D4E4F'                              
         DC    X'505152535455565758595A5B5C5D5E5F'                              
         DC    X'606162636465666768696A6B6C6D6E6F'                              
         DC    X'707172737475767778797A7B7C7D7E7F'                              
         DC    X'808182838485868788898A8B8C8D8E8F'                              
         DC    X'909192939495969798999A9B9C9D9E9F'                              
         DC    X'A0A1A2A3A4A5A6A7A8A9AAABACADAEAF'                              
         DC    X'B0B1B2B3B4B5B6B7B8B9BABBBCBDBEBF'                              
         DC    X'C0C1C2C3C4C5C6C7C8C9CACBCCCDCECF'                              
         DC    X'D0D1D2D3D4D5D6D7D8D9DADBDCDDDEDF'                              
         DC    X'E0E1E2E3E4E5E6E7E8E9EAEBECEDEEEF'                              
         DC    X'F0F1F2F3F4F5F6F7F8F9FAFBFCFDFEFF'                              
         DC    H'2'                                                             
         DS    CL2                                                              
         DC    CL132' '                                                         
         DC    CL132' '                                                         
         DC    CL80' '                                                          
         DC    CL62' '                                                          
         DC    CL16' '                                                          
         DC    C'BL01'                                                          
         DC    PL2'1'                                                           
         DC    PL2'54'                                                          
         DC    PL2'0'                                                           
         DC    C'N'                                                             
         DC    X'00'                                                            
         DC    CL10' '                                                          
         DC    H'0'                                                             
         DC    H'0'                                                             
         DC    PL2'0'                                                           
         EJECT                                                                  
       ++INCLUDE DDDLASPUN                                                      
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003DDLASPRTR 05/01/02'                                      
         END                                                                    
