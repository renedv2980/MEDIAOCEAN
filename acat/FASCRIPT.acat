*          DATA SET FASCRIPT   AT LEVEL 007 AS OF 11/02/10                      
*CATALP FASCRIPT                                                                
         TITLE 'FASCRIPT - INITIATE A SCRIPT'                                   
***********************************************************************         
* XL1    X'80'=CTFILE ID, X'40'= USE TBUFF FOR SCRIPT,X'20'=ROLLBACK  *         
* AL3    A(SCRIPT OR CTFILE SCRIPT ID)                                *         
* XL2    RETURN CODE IF ERROR OR ZERO IF OK                           *         
* XL2    RETURN INDEX INTO SCRIPT IF ERROR                            *         
* AL4    A(SCRIPT INPUT DATA BUFFER)                                  *         
* XL2    N/D                                                          *         
* XL2    L'SCRIPT INPUT DATA BUFFER                                   *         
* AL4    A(SUBROUTINE TO FILL INPUT BUFFER)                           *         
* AL4    A(PLIST FOR INPUT SUBROUTINE)                                *         
* AL4    A(SCRIPT OUTPUT DATA BUFFER)                                 *         
* XL2    RETURN ACTUAL LEN OF DATA IN OUTPUT BUFFER                   *         
* XL2    L'SCRIPT OUTPUT DATA BUFFER)                                 *         
* AL4    A(SUBROUTINE TO EMPTY OUTPUT BUFFER)                         *         
* AL4    A(PLIST FOR OUTPUT SUBROUTINE)                               *         
* XL2    N/D                                                          *         
* XL2    L'ERROR MESSAGE BUFFER                                       *         
* AL4    A(ERROR MESSAGE BUFFER)                                      *         
***********************************************************************         
         PRINT NOGEN                                                            
SCRIPT   CSECT                                                                  
         NMOD1 WORKL,**SCRP**,CLEAR=YES                                         
         USING WORKD,RC                                                         
         USING SCPLD,R6                                                         
         LR    R6,R1               R2=A(PARAMETER LIST)                         
         XC    SCPLERR,SCPLERR     CLEAR RETURN VALUES                          
         XC    SCPLDSP,SCPLDSP                                                  
         XC    SCPLLOD,SCPLLOD                                                  
         L     RA,VSYSFAC          RA=A(SYSFACS)                                
         USING SYSFACD,RA                                                       
         SAM31 ,                                                                
*                                                                               
         ICM   RF,15,VSSB          EXTRACT FACPAK INFO                          
         ICM   RF,15,SSBTKADR-SSBD(RF)                                          
         ST    RF,ATCB             SAVE A(TCB ENTRY)                            
         USING TCBD,RF                                                          
*NOP*    CLI   TCBSCPTK,0                                                       
*NOP*    BE    *+6                                                              
*NOP*    DC    H'0'                ONLY ONE CALL TO FASCRIPT PER TASK           
*                                                                               
         ICM   R9,15,TCBUTL                                                     
         ST    R9,AUTL             SAVE A(REAL UTL ENTRY)                       
RU       USING UTLD,R9                                                          
         TM    RU.TSTAT6,TST6SCRP                                               
         BZ    *+6                                                              
         DC    H'0'                CANT ALREADY BE EXECUTING A SCRIPT           
         DROP  RF                                                               
*                                                                               
         L     RE,VTCB             LOCATE THE DUMMY UTL FOR TASK                
         LH    R0,0(RE)                                                         
         LA    RE,6(RE)            POINT TO FIRST TCB ENTRY                     
         L     RF,ATCB                                                          
         SR    RF,RE                                                            
         SR    RE,RE                                                            
         DR    RE,R0               RF=RELATIVE TASK NUMBER                      
*                                                                               
         L     R7,VUTL                                                          
         MH    RF,0(R7)                                                         
         LA    R7,6(R7,RF)                                                      
         ST    R7,ADUMUTL          R7=A(DUMMY UTL ENTRY)                        
DU       USING UTLD,R7                                                          
         TM    DU.TTYPE2,TTYPEDUM                                               
         BNZ   *+6                                                              
         DC    H'0'                NEED DUMMY TRM TO EXECUTE A SCRIPT           
*                                                                               
         ICM   R5,15,DU.TBUFF                                                   
         BNZ   *+6                                                              
         DC    H'0'                NO FREE BUFFERS                              
*                                                                               
*        GOTO1 VLCM,DMCB,VTGETBUF,0,0                                           
*        LTR   R5,R1                                                            
*        BNZ   *+6                                                              
*        DC    H'0'                NO FREE BUFFERS                              
*                                                                               
         OI    RU.TSTAT6,TST6SCRP                                               
         OI    DU.TSTAT6,TST6SCRP  SET EXECUTING A SCRIPT                       
         STCM  R5,15,DU.TBUFF      SAVE XA TBUFF                                
*                                                                               
         MVI   DU.TSVCREQ,1                                                     
         MVI   DU.TSVCREQ+1,$SCRSR    SET SPECIAL S/R FOR SCRIPTS               
         MVC   DU.TTIMETU,RU.TTIMETU  COPY TRANSACTION START TIME               
*                                                                               
         AHI   R5,-(TBHL)          R5=A(DUMMY TBUFF BUFFER HEADER)              
         USING TBHD,R5                                                          
         MVI   TBHINDS,TBHISCRP    SET TBUFF CONTAINS A SCRIPT                  
         XC    TBHLINK,TBHLINK                                                  
         MVC   TBHSCINF,SCPLPARM                                                
         XC    TBHSCINX,TBHSCINX                                                
         XC    TBHSCEYE,TBHSCEYE                                                
         ST    R7,TBHADUM          SET A(DUMMY UTL) IN TBUFF                    
*                                                                               
         MVC   TBHATCB,ATCB        SET A(CALLING TASK)                          
         MVI   TBHCNTL,TBHCFRST    SET FIRST TIME FLAG                          
         TM    SCPLINDS,SCPLBALL                                                
         BZ    *+8                                                              
         OI    TBHCNTL,TBHCBALL    SET ROLLBACK ALL I/O'S ON ABEND              
         TM    SCPLINDS,SCPLTEST                                                
         BZ    *+8                                                              
         OI    TBHCNTL,TBHCTEST    SET RUN SCRIPT IN TEST MODE                  
*                                                                               
         XR    RF,RF               RF=A(SCRIPT)                                 
         ICM   RF,7,SCPLAS+1                                                    
         BNZ   *+6                                                              
         DC    H'0'                                                             
         STCM  RF,15,TBHSCAS       SET A(SCRIPT)                                
*                                                                               
         TM    SCPLINDS,SCPLCTF    DO WE NEED TO READ CTFILE?                   
         BZ    SCR10               NO, IT REALLY IS A SCRIPT                    
*                                                                               
         LA    R4,IO               BUILD CTFILE KEY AND READ RECORD             
         USING CT7REC,R4                                                        
         XC    CT7KEY,CT7KEY                                                    
         MVI   CT7KTYP,CT7KTYPQ                                                 
         MVC   CT7KCODE,0(RF)                                                   
         OC    CT7KCODE,SPACES     MAKE IT UPPERCASE                            
*                                                                               
         TM    SCPLINDS,SCPLUTB    TEST TO READ SCRIPT INTO TBUFF               
         BZ    SCR02                                                            
         MVC   TBHSCEYE,=CL8'*SCPREC*'                                          
         LA    RF,TBHSCREC                                                      
         ST    RF,TBHSCAS          SET A(SCRIPT)                                
*                                                                               
SCR02    L     R2,TBHSCAS          R2 = A(SCRIPT READ AREA)                     
         GOTO1 VDATAMGR,DMCB,(X'24',DMREAD),CTFILE,IO,IO,TO24=Y                 
         SAM31 ,                                                                
         CLI   8(R1),0                                                          
         BNE   ERRSNF              ONLY 1 RECORD READ REQUIRED                  
*                                                                               
         LA    R4,CT7DATA          START OF ELEMENTS                            
         USING CTSCRD,R4                                                        
         XR    RF,RF                                                            
*                                                                               
SCR04    CLI   CTSCREL,0           EOR?                                         
         BNE   SCR06                                                            
         C     R2,TBHSCAS          ANY DATA?                                    
         BNE   SCR10               YES                                          
         B     ERRSRI                                                           
*                                                                               
SCR06    CLI   CTSCREL,CTSCRELQ    TEST SCRIPT ELEMENT X'E1'                    
         BNE   SCR08                                                            
*                                                                               
         ICM   RF,1,CTSCRLEN       CHECK ELEMENT HAS DATA IN IT                 
         BZ    ERRSRI                                                           
         CHI   RF,4                AND DATA IS VALID                            
         BL    SCR08                                                            
         AHI   RF,-(CTSCRLNQ+1)                                                 
         EX    RF,*+4              MOVE IN THE DATA TO NEXT FREE                
         MVC   0(0,R2),CTSCRDTA                                                 
         LA    R2,1(RF,R2)         NEXT FREE                                    
*                                                                               
SCR08    ICM   RF,1,CTSCRLEN       BUMP TO NEXT ELEMENT                         
         BZ    ERRSRI                                                           
         BXH   R4,RF,SCR04                                                      
*                                                                               
ERRSRI   MVC   TBHSCERR,ESRI       ERROR - SCRIPT RECORD IS INVALID             
         B     SCR190                                                           
*                                                                               
ERRSNF   MVC   TBHSCERR,ESNF       SET SCRIPT NOT FOUND                         
         B     SCR190                                                           
*                                                                               
SCR10    LA    RE,TBHSCEYE                                                      
         CLC   TBHSCEYE,=CL8'*SCPREC*'                                          
         BNE   SCR180              SCRIPT NOT IN TBUFF                          
         LR    RE,R2                                                            
*                                                                               
SCR180   SR    RE,R5                                                            
         STH   RE,TBHLEN           SET LEN OF TBUFF USED SO FAR                 
         GOTO1 VMSGQIN,DMCB,VSYSFAC,TBHDATA,DU.UTLD                             
         XC    ECB,ECB                                                          
         LA    R1,ECB              SET DO NOT WAIT TYPE ECB                     
         OILH  GR1,X'FF00'                                                      
         GOTO1 VSCWAIT             WAIT FOR SCRIPT TO COMPLETE                  
*                                                                               
SCR190   MVC   SCPLERR,TBHSCERR                                                 
         MVC   SCPLERR,TBHSCERR                                                 
         MVC   SCPLDSP,TBHSCDSP                                                 
         MVC   SCPLLOD,TBHSCLOD                                                 
*                                                                               
         MVC   DUTLSVC,DU.TSVCREQ  SAVE DUMMY UTL S/R FIELD                     
         XC    DU.TSVCREQ,DU.TSVCREQ                                            
         NI    DU.TSTAT6,255-TST6SCRP                                           
*                                                                               
*        ICM   RF,15,DU.TBUFF      RELEASE TBUFF                                
*        GOTO1 VLCM,DMCB,VTRELBUF,(RF),0                                        
*        XC    DU.TBUFF,DU.TBUFF                                                
*                                                                               
         NI    RU.TSTAT6,255-TST6SCRP                                           
*                                                                               
         CLI   DUTLSVC+1,$ABEND    DID SCRIPT ABEND                             
         BNE   EXIT                                                             
         LH    R0,SCPLERR          YES TAKE SCRIPT ERROR NUMBER NNN             
         AH    R0,ESAB             AND CONVERT TO 9NNN                          
         STH   R0,SCPLERR                                                       
         B     EXIT                                                             
         EJECT                                                                  
**********************************************************************          
* USEFUL ROUTINES                                                    *          
**********************************************************************          
EXIT     OC    SCPLERR,SCPLERR     EXIT WITH CC EQUAL IF ALL OK                 
         XIT1  ,                                                                
         EJECT                                                                  
**********************************************************************          
* LITERALS AND CONSTANTS                                             *          
**********************************************************************          
         LTORG                                                                  
*                                                                               
VSYSFAC  DC    V(SYSFAC)                                                        
VSCWAIT  DC    V(SCWAIT)                                                        
VMSGQIN  DC    V(MSGQIN)                                                        
*                                                                               
ESNF     DC    H'1'                ERROR SCRIPT NOT FOUND                       
ESRI     DC    H'2'                ERROR SCRIPT RECORD INVALID                  
ESAB     DC    H'9000'             ERROR SCRIPT ABENDED                         
*                                                                               
DMREAD   DC    C'DMREAD  '                                                      
CTFILE   DC    C'CTFILE  '                                                      
SPACES   DC    CL10' '                                                          
*                                                                               
$SCRSR   EQU   X'1F'                                                            
$ABEND   EQU   X'FF'                                                            
$UNWIND  EQU   X'FB'                                                            
*                                                                               
WORKD    DSECT                                                                  
ECB      DS    XL4                 ECB TO WAIT FOR SCRIPT COMPLETION            
*                                                                               
FULL     DS    F                                                                
DUB      DS    D                                                                
DMCB     DS    6F                                                               
*                                                                               
ATCB     DS    A                   A(TCB ENTRY)                                 
AUTL     DS    A                   A(UTL ENTRY)                                 
ADUMUTL  DS    A                   A(DUMMY TCB ENTRY)                           
SVTBUFF  DS    A                   A(BUFFER)                                    
*                                                                               
SVHEADER DS    XL8                 SAVED TBUFF HEADER                           
DUTLSVC  DS    XL2                 TSVCREQ SAVED FROM DUMMY UTL ENTRY           
IO       DS    XL4100              CTFILE RECORD                                
WORKL    EQU   *-WORKD                                                          
         EJECT                                                                  
* FASCRIPTD                                                                     
       ++INCLUDE FASCRIPTD                                                      
         EJECT                                                                  
* FATBHD                                                                        
       ++INCLUDE FATBHD                                                         
         EJECT                                                                  
* FADSECTS                                                                      
       ++INCLUDE FADSECTS                                                       
         SPACE 1                                                                
* CTGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE CTGENFILE                                                      
         PRINT ON                                                               
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'007FASCRIPT  11/02/10'                                      
         END                                                                    
