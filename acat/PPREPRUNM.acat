*          DATA SET PPREPRUNM  AT LEVEL 019 AS OF 05/01/02                      
*CATALP PPRUNMST                                                                
         TITLE 'MODULE TO CONTROL PPG RUN'                                      
PPRUNCON CSECT                                                                  
         PRINT NOGEN                                                            
         NBASE 0,PPRUNCON,=V(PPSAVE)                                            
         L     RA,=V(PPWORKC)                                                   
         USING PPWORKD,RA                                                       
         L     R8,LOGOC                                                         
         USING LOGOD,R8                                                         
         L     R9,=V(MASTC)                                                     
         USING MASTD,R9                                                         
         EJECT                                                                  
*              INITIAL CONTROL CARDS                                            
         SPACE 3                                                                
         MVC   MCAPHAS1,PPSPECS                                                 
         MVC   MCAPHAS2,PPAPPLIC                                                
         ST    RA,MCAWORK                                                       
         GOTO1 =V(RUNSTART)                                                     
         MVC   RCTRACE,MCTRACE                                                  
         MVC   RCDUMP,MCDUMP                                                    
         MVC   RCWRITE,MCWRITE                                                  
         MVC   RCDATE,MCDATE                                                    
         MVC   RCSPECNO,MCNUMBER                                                
         MVC   RCPROG,MCPROG                                                    
         MVC   RCORIGID,MCORIGID                                                
         MVC   ORIGNAME,MCORIGIN                                                
         MVC   ORIGADDR,MCORIGAD                                                
         MVC   RCRQFILT,MCREQCOD                                                
         CLC   MCREQCOD,SPACES                                                  
         BNE   *+10                                                             
         MVC   RCRQFILT,MCPROG                                                  
         CLC   MCREQCOD,SPACES                                                  
         BNE   *+10                                                             
         MVC   RCRQFILT,MCPROG                                                  
         MVC   RCMEDFLT,MCMEDIA                                                 
         MVC   RCAGYFLT,MCUSER                                                  
*                                  DO FILE SPECS                                
         MVI   RCFLONLY,C'Y'                                                    
         MVI   FORCEHED,C'Y'                                                    
         MVI   RCSUBPRG,0                                                       
         GOTO1 REPORT                                                           
         MVI   RCFLONLY,C'N'                                                    
*                                                                               
         CLI   RCWRITE,C'N'                                                     
         BE    OPEN2                                                            
         CLC   FCUPPRTD(4),=C'NNNN'                                             
         BE    OPEN2                                                            
         MVC   FILELIST+00(1),FCUPPRTD                                          
         MVC   FILELIST+08(1),FCUPPRTF                                          
         MVC   FILELIST+16(1),FCUPPUBD                                          
         MVC   FILELIST+24(1),FCUPPUBF                                          
*                                                                               
*                                  CHANGE Y TO U                                
         LA    R2,FILELIST                                                      
         L     R3,=V(PPFILEC)                                                   
         LA    R0,4                                                             
OPEN1    DS    0H                                                               
         CLI   0(R2),C'Y'                                                       
         BNE   *+8                                                              
         MVI   0(R2),C'U'                                                       
         LA    R2,8(R2)                                                         
         BCT   R0,OPEN1                                                         
*                                                                               
         MVC   FILELIST+40(10),=C'UPRECV1 X '                                   
         L     RF,=V(SSB)                                                       
         XC    0(4,RF),0(RF)                                                    
         SPACE 2                                                                
OPEN2    DS    0H                                                               
         MVC   WORK(2),RCAGYFLT                                                 
         BAS   RE,GETAGINF         SET FILE NO IN UTL                           
*                                                                               
         GOTO1 DATAMGR,DMCB,=C'OPEN',=C'PRINT',FILELIST,(R3)                    
         COMRG                                                                  
         MVC   RCUPSI,23(R1)                                                    
         MVC   RCJOB,24(R1)                                                     
         B     OPEN4                                                            
         SPACE 2                                                                
FILELIST DC    CL8'NPRTDIR'                                                     
         DC    CL8'NPRTFILE'                                                    
         DC    CL8'NPUBDIR'                                                     
         DC    CL8'NPUBFILE'                                                    
         DC    CL8'NCTFILE'                                                     
         DC    CL10'X'                                                          
         SPACE 2                                                                
OPEN4    DS    0H                                                               
*                                  LINE-UP PATTERN                              
         CLI   RCLINEUP,C'Y'                                                    
         BNE   RC1B                                                             
         MVI   FORCEHED,C'Y'                                                    
         MVI   SKIPSPEC,C'Y'                                                    
         LA    R2,7                                                             
RC0D     DS    0H                                                               
         MVI   P,C'X'                                                           
         MVC   P+1(109),P                                                       
         GOTO1 REPORT                                                           
         BCT   R2,RC0D                                                          
         B     RC1B                                                             
*                                                                               
RC1      DS    0H                                                               
RC1B     DS    0H                                                               
         EJECT                                                                  
START    DS    0H                                                               
*                   CONTROL DIVIDERS AND REQUESTS                               
         SPACE 3                                                                
         CLI   LOGONAME,C' '       IF NO DESTINATION NAME                       
         BNH   GOREQS              NO DIVIDERS                                  
         GOTO1 LOGO,DMCB,LOGOC     START DIVIDER                                
         SPACE 2                                                                
GOREQS   DS    0H                                                               
         GOTO1 PPFILCON                                                         
         SR    R2,R2               SET FOR NORMAL TERMINATION                   
         SPACE 2                                                                
FINALISE DS    0H                                                               
         CLI   LOGONAME,C' '                                                    
         BNH   GOEND                                                            
         MVI   LOGOTYPE,C'E'                                                    
         ZAP   LOGOREQS,RCRQVAL                                                 
         GOTO1 LOGO,DMCB,LOGOC                                                  
         SPACE 2                                                                
GOEND    DS    0H                                                               
         GOTO1 PRINT,DMCB,=C'CLOSE'                                             
         CP    MCNDUMPS,=P'0'                                                   
         BE    NORMEND                                                          
         CANCEL                                                                 
         SPACE 2                                                                
NORMEND  DS    0H                                                               
         EOJ                                                                    
         SPACE 3                                                                
*                             ROUTINE TO EXTRACT FILE NUNBER FROM AGY           
GETAGINF NTR1                                                                   
         L     R3,PPFILEC                                                       
         USING CND,R3                                                           
         XC    CNLEN,CNLEN                                                      
         MVC   CNAGY,WORK                                                       
         CLC   RCORIGID,=X'002B'   IF TCH1 (OR NO ORIG ID GIVEN)                
         BE    GETAG3              SKIP SET OF ORIGIN ID                        
         CLC   RCORIGID,=X'005A'   IF SJR?   ORIG=90                            
         BE    GETAG3              SKIP SET OF ORIGIN ID                        
         MVC   CNID,RCORIGID                                                    
GETAG3   GOTO1 =V(CONFID),DMCB,CND,FULL                                         
         OC    FULL,FULL                                                        
         BNZ   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         L     R5,UTL                                                           
         MVC   4(1,R5),CNPSE                                                    
         MVC   PRNTSE,CNPSE                                                     
         MVC   SPOTSE,CNSSE                                                     
         MVC   ACCTSE,CNASE                                                     
         MVC   SPOTCD,CNSCD                                                     
         MVC   ACCTCD,CNACD                                                     
*                                                                               
*                                                                               
         LA    RF,PRINTSES                                                      
GETAG5   DS    0H                                                               
         CLC   1(1,RF),PRNTSE                                                   
         BE    GETAG6                                                           
         LA    RF,2(RF)                                                         
         B     GETAG5                                                           
*                                                                               
GETAG6   DS    0H                                                               
         MVC   FILENUM,0(RF)                                                    
*                                                                               
         XIT1                                                                   
*                                                                               
       ++INCLUDE PRINTSES                                                       
         SPACE 2                                                                
         LTORG                                                                  
*                                                                               
       ++INCLUDE DDCNTRL                                                        
*                                                                               
         EJECT                                                                  
         PRINT OFF                                                              
       ++INCLUDE DDREPMASTD                                                     
       ++INCLUDE DDLOGOD                                                        
       ++INCLUDE PPWORKD                                                        
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'019PPREPRUNM 05/01/02'                                      
         END                                                                    
