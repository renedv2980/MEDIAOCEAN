*          DATA SET SPMEDPOST  AT LEVEL 013 AS OF 07/05/06                      
*CATALP MEDPOST                                                                 
         TITLE 'BUILDS AND WRITES BUFFALO RECORDS'                              
VMDPOST  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,**POST**                                                       
         L     RA,0(R1)                                                         
         LA    R9,2048(RA)                                                      
         LA    R9,2048(R9)                                                      
         USING SPWORKD,RA,R9                                                    
         L     R8,BUFFBUFF                                                      
         USING BUFFALOD,R8                                                      
*                                                                               
         L     R2,MEDBUFF                                                       
         USING MEDBLOCK,R2                                                      
         MVC   SAVESLN,MEDSPTLN                                                 
*                                                                               
         L     R1,SVSLNADR         PICK UP PREVIOUS ENTRY ADDRESS               
         CLC   SVEQVAGY,AGY        AND IF AGENCY MATCHES                        
         BNE   *+14                                                             
         CLC   SVEQVMED,MED        AND MEDIA MATCHES                            
         BE    POST18              IT'S THE RIGHT TABLE ENTRY                   
*                                                                               
         MVC   SVEQVMED,MED                                                     
         MVC   SVEQVAGY,AGY                                                     
*                                                                               
         MVI   BYTE,C'R'           FIND EQTAB                                   
         CLI   MED,C'R'                                                         
         BE    POST10                                                           
         CLI   MED,C'X'                                                         
         BE    POST10                                                           
         MVI   BYTE,C'T'                                                        
         CLI   MED,C'T'                                                         
         BE    POST10                                                           
         CLI   MED,C'N'                                                         
         BE    POST10                                                           
         CLI   MED,C'C'                                                         
         BE    POST10                                                           
         DC    H'0'                                                             
*                                                                               
POST10   L     R1,VSLNTAB                                                       
         LH    RE,0(R1)            ENTRY LENGTH                                 
         L     RF,2(R1)            DSPL TO EOT                                  
         AR    RF,R1               RELOCATE EOT ADDRESS                         
         AHI   R1,6                POINT TO FIRST ENTRY                         
*                                                                               
POST12   CLC   0(2,R1),=C'00'      TEST DEFAULT ENTRY                           
         BE    POST14                                                           
         CLC   0(2,R1),AGY                                                      
         BNE   *+14                                                             
POST14   CLC   2(1,R1),BYTE                                                     
         BE    POST16                                                           
         BXLE  R1,RE,POST12                                                     
         DC    H'0'                                                             
*                                                                               
POST16   ST    R1,SVSLNADR         SAVE SLNTAB ENTRY ADDRESS                    
*                                                                               
POST18   SR    RE,RE                                                            
         IC    RE,MEDSPTLN                                                      
         AR    RE,RE               X 2                                          
         LA    RE,4(R1,RE)         POINT TO ENTRY FOR THIS SLN                  
         MVC   MEDSPTLN,1(RE)      REPORT EQUIVALENCING SLN                     
         DROP  R2                                                               
         EJECT                                                                  
*==================================================================             
* BUILD ONE RECORD FOR EACH ACTIVE LINE                                         
*==================================================================             
                                                                                
         L     R2,MEDTABLE                                                      
POST20   CLI   0(R2),X'FF'                                                      
         BE    POSTX                                                            
         TM    0(R2),X'80'                                                      
         BO    POST40                                                           
         L     R4,BUFFIO                                                        
         LR    RE,R4               PRECLEAR RECORD                              
         L     RF,BUFFCOLS                                                      
         SLL   RF,2                                                             
         A     RF,BUFFLKEY                                                      
         A     RF,BUFFLCOM                                                      
         LR    R7,RF                                                            
         L     RF,BUFFLKEY                                                      
         XCEF                                                                   
         LR    RE,R4                                                            
         A     RE,BUFFLKEY                                                      
         A     RE,BUFFLCOM                                                      
         L     RF,BUFFCOLS                                                      
         SLL   RF,2                                                             
         XCEF                                                                   
         BAS   RE,KEYS                                                          
         CLI   ANYACT,C'Y'                                                      
         BNE   POST40                                                           
         B     POST30              NO-OP THIS TO ENABLE TRACE                   
         MVC   DMCB+8(4),=C'DUMP'                                               
         GOTO1 =V(PRNTBL),DMCB,0,(R4),,(R7),=C'2D'                              
         SPACE 2                                                                
POST30   GOTO1 BUFFALO,DMCB,=C'PUT',(R8),(R4)                                   
         SPACE 2                                                                
POST40   LA    R2,4(R2)                                                         
         B     POST20                                                           
*                                                                               
SVSLNADR DS    A                                                                
SVEQVAGY DS    CL2                                                              
SVEQVMED DS    CL1                                                              
         EJECT                                                                  
KEYS     NTR1                                                                   
         L     R2,0(R2)                                                         
         L     R5,4(R2)                                                         
         MVI   ANYACT,C'N'                                                      
         BAS   RE,COL              FILL THE COLUMNS                             
         CLI   ANYACT,C'Y'                                                      
         BNE   XIT                                                              
         LA    R2,16(R2)                                                        
         SPACE 2                                                                
KEYS2    CLI   0(R2),X'FF'                                                      
         BE    XIT                                                              
         BAS   RE,DATA                                                          
         LA    R2,8(R2)                                                         
         B     KEYS2                                                            
         SPACE 2                                                                
COL      NTR1                                                                   
         LR    R2,R5                                                            
         B     KEYS2                                                            
         SPACE 2                                                                
DATA     TM    4(R2),X'80'         CODE TO MOVE OUT DATA                        
         BCR   7,RE                                                             
         LM    R3,R4,0(R2)                                                      
         SR    R5,R5                                                            
         IC    R5,0(R2)                                                         
         BCTR  R5,0                                                             
         EX    R5,ANYDATA                                                       
         BCR   8,RE                                                             
         MVI   ANYACT,C'Y'                                                      
         EX    R5,DATAMOVE                                                      
         BR    RE                                                               
         SPACE 2                                                                
DATAMOVE MVC   0(0,R4),0(R3)                                                    
KEYCLEAR XC    0(0,R4),0(R4)                                                    
ANYDATA  OC    0(0,R3),0(R3)                                                    
ANYACT   DC    C'N'                                                             
         SPACE 2                                                                
*                                                                               
POSTX    L     R2,MEDBUFF                                                       
         USING MEDBLOCK,R2                                                      
         MVC   MEDSPTLN,SAVESLN    RESTORE CALLING SLN                          
*                                                                               
XIT      XIT1                                                                   
         DROP  R2                                                               
SAVESLN  DS    X                                                                
         LTORG                                                                  
         PRINT OFF                                                              
       ++INCLUDE SPREPWORKD                                                     
       ++INCLUDE SPMEDBLOCK                                                     
       ++INCLUDE DDBUFFALOD                                                     
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'013SPMEDPOST 07/05/06'                                      
         END                                                                    
