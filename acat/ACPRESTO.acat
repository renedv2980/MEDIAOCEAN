*          DATA SET ACPRESTO   AT LEVEL 107 AS OF 08/26/08                      
*CATALP PRESTO                                                                  
ACPRESTO TITLE 'ACPRESTO - PRESTO SUPPORT RECORD OBJECT CREATORS'               
PRESTO   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 ACPRLOCL,**ACPR**,RA,CLEAR=YES,RR=R2                             
         LR    R9,RC                                                            
         USING ACPRLOCD,R9                                                      
         ST    R2,RELO                                                          
         LR    R8,R1                                                            
         USING ACPRESTD,R8                                                      
         L     R7,ACPRCOMF                                                      
         USING COMFACSD,R7                                                      
         MVI   BLANKS,C' '                                                      
         MVC   BLANKS+1(L'BLANKS-1),BLANKS+0                                    
*                                                                               
         LA    R2,RTNTAB                                                        
*                                                                               
ACPR10   OC    0(4,R2),0(R2)                                                    
         BNZ   *+6                                                              
         DC    H'0'                                                             
         CLC   ACPRTYP,3(R2)                                                    
         BE    ACPR20                                                           
         LA    R2,8(R2)                                                         
         B     ACPR10                                                           
*                                                                               
ACPR20   L     RF,4(R2)                                                         
         LA    RF,PRESTO(RF)                                                    
         GOTO1 (RF),DMCB,ACPRREC,ACPROBJ                                        
         B     XIT                                                              
         EJECT                                                                  
RTNTAB   DS    0A                                                               
         DC    A(ACPRBMGQ,BUILDMG-PRESTO)                                       
         DC    A(ACPRBMDQ,BUILDMD-PRESTO)                                       
         DC    A(ACPRBOGQ,BUILDOG-PRESTO)                                       
         DC    A(ACPRBOFQ,BUILDOF-PRESTO)                                       
         DC    A(ACPRBWGQ,BUILDWG-PRESTO)                                       
         DC    A(ACPRBWCQ,BUILDWC-PRESTO)                                       
         DC    A(ACPRBSCQ,BUILDSC-PRESTO)                                       
         DC    A(ACPRBCWQ,BUILDCW-PRESTO)                                       
         DC    A(ACPRBCLQ,BUILDCL-PRESTO)                                       
         DC    A(ACPRBPRQ,BUILDPR-PRESTO)                                       
         DC    A(ACPRBJBQ,BUILDJB-PRESTO)                                       
         DC    A(ACPRBSPQ,BUILDSP-PRESTO)                                       
         DC    A(ACPRBAGQ,BUILDAG-PRESTO)                                       
         DC    A(ACPRBOLQ,BUILDOL-PRESTO)                                       
         DC    A(ACPRBODQ,BUILDOD-PRESTO)                                       
         DC    A(ACPRBOSQ,BUILDOS-PRESTO)                                       
         DC    A(ACPRBPLQ,BUILDPL-PRESTO)                                       
         DC    A(ACPRBTXQ,BUILDTX-PRESTO)                                       
         DC    A(ACPRBOPQ,BUILDOP-PRESTO)                                       
         DC    A(ACPRBUFQ,BUILDUS-PRESTO)                                       
         DC    A(ACPRBLSQ,BUILDLS-PRESTO)                                       
         DC    A(ACPRBLNQ,BUILDLN-PRESTO)                                       
         DC    A(ACPRBPEQ,BUILDPE-PRESTO)                                       
         DC    A(ACPRBRAQ,BUILDRA-PRESTO)                                       
         DC    A(ACPREAPQ,BUILDEA-PRESTO)                                       
         DC    A(0)                                                             
         EJECT                                                                  
* THIS ROUTINE BUILDS THE MEDIA GROUP OBJECT BLOCK FROM THE MEDIA               
* GROUP RECORD PASSED.                                                          
*                                                                               
* PARAMETER 1 - A(MEDIA GROUP RECORD)                                           
* PARAMETER 2 - A(MEDIA GROUP OBJECT BLOCK)                                     
*                                                                               
BUILDMG  NTR1  ,                                                                
         LM    R2,R3,0(R1)         R2 = A(MEDIA GROUP RECORD)                   
*                                  R3 = A(MEDIA GROUP OBJECT BLOCK)             
*                                                                               
*                                  SET MEDIA GROUP OBJECT LENGTH/TYPE           
         MVC   0(2,R3),=Y(PRMGOBJL+4)                                           
         MVC   2(4,R3),=A(ITPRRTMG)                                             
*                                                                               
         USING PRMGOBJD,R3         BUMP TO OBJECT DATA AND PRECLEAR             
         LA    R3,6(R3)                                                         
         MVC   0(PRMGOBJL,R3),BLANKS                                            
*                                                                               
         USING MGRRECD,R2          EXTRACT RECORD KEY DATA                      
         MVC   PRMGCODE,MGRKCODE   CODE                                         
*                                                                               
         USING NAMELD,R6           EXTRACT NAME ELEMENT DATA                    
         LR    R6,R2                                                            
         MVI   ELCODE,NAMELQ                                                    
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         ZIC   RF,NAMLN            NAME                                         
         SH    RF,=Y(NAMLN1Q+1)                                                 
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   PRMGNAME(0),NAMEREC                                              
*                                                                               
         LA    R3,PRMGOBJL(R3)     BUMP R3 TO NEXT OBJECT                       
*                                                                               
BMGX     XC    0(2,R3),0(R3)       SET END OF OBJECT BLOCK                      
         B     YES                                                              
         DROP  R2,R3,R6                                                         
         EJECT                                                                  
* THIS ROUTINE BUILDS THE MEDIA OBJECT BLOCK FROM THE MEDIA                     
* RECORD PASSED.                                                                
*                                                                               
* PARAMETER 1 - A(MEDIA RECORD)                                                 
* PARAMETER 2 - A(MEDIA OBJECT BLOCK)                                           
*                                                                               
BUILDMD  NTR1  ,                                                                
         LM    R2,R3,0(R1)         R2 = A(MEDIA RECORD)                         
*                                  R3 = A(MEDIA OBJECT BLOCK)                   
*                                                                               
*                                  SET MEDIA OBJECT LENGTH/TYPE                 
         MVC   0(2,R3),=Y(PRMDOBJL+4)                                           
         MVC   2(4,R3),=A(ITPRRTMD)                                             
*                                                                               
         USING PRMDOBJD,R3         BUMP TO OBJECT DATA AND PRECLEAR             
         LA    R3,6(R3)                                                         
         MVC   0(PRMDOBJL,R3),BLANKS                                            
*                                                                               
         USING PMDRECD,R2          EXTRACT RECORD KEY DATA                      
         MVC   PRMDCODE,PMDKMED    CODE                                         
*                                                                               
         USING PMDELD,R6           GET PRODUCTION MEDIA ELEMENT DATA            
         LR    R6,R2                                                            
         MVI   ELCODE,PMDELQ                                                    
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         MVC   PRMDNAME,PMDDESC    NAME                                         
         MVC   PRMDMEDG,PMDGRP     MEDIA GROUP                                  
         CLI   PRMDMEDG,0                                                       
         BNE   *+8                                                              
         MVI   PRMDMEDG,C' '                                                    
*                                                                               
         USING MNAELD,R6           GET MEDIA NAME ELEMENT DATA                  
         MVC   HALF(1),PMDKMED                                                  
         MVI   HALF+1,MNAEST                                                    
         GOTO1 CHELLO,DMCB,(C'G',ACCMST),('MNAELQ',(R2)),(2,HALF),0             
         L     R6,12(R1)                                                        
         CLI   12(R1),0                                                         
         BNE   BMD20                                                            
*                                                                               
         LA    RE,MNAME-MNAEL                                                   
         ZIC   RF,MNALN                                                         
         SR    RF,RE                                                            
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   PRMDLONG(0),MNAME   LONG NAME                                    
*                                                                               
         USING XNMELD,R6           GET EXTRA NAME ELEMENT                       
BMD20    LR    R6,R2                                                            
         MVI   ELCODE,XNMELQ                                                    
         BAS   RE,GETEL                                                         
         BNE   BMD90                                                            
*                                                                               
         ZIC   RF,XNMSUBL          FOREIGN NAME                                 
         BCTR  RF,0                LENGTH VALUE INCLUDES LENGTH BYTE            
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   PRMDFNAM(0),XNMSUBN                                              
*                                                                               
BMD90    LA    R3,PRMDOBJL(R3)     BUMP R3 TO NEXT OBJECT                       
*                                                                               
BMDX     XC    0(2,R3),0(R3)       SET END OF OBJECT BLOCK                      
         B     YES                                                              
         DROP  R2,R3,R6                                                         
         EJECT                                                                  
* THIS ROUTINE BUILDS THE OFFICE GROUP OBJECT BLOCK FROM THE OFFICE             
* GROUP RECORD PASSED.                                                          
*                                                                               
* PARAMETER 1 - A(OFFICE GROUP RECORD)                                          
* PARAMETER 2 - A(OFFICE GROUP OBJECT BLOCK)                                    
*                                                                               
BUILDOG  NTR1  ,                                                                
         LM    R2,R3,0(R1)         R2 = A(OFFICE GROUP RECORD)                  
*                                  R3 = A(OFFICE GROUP OBJECT BLOCK)            
*                                                                               
*                                  SET OFFICE GROUP OBJECT LENGTH/TYPE          
         MVC   0(2,R3),=Y(PROGOBJL+4)                                           
         MVC   2(4,R3),=A(ITPRRTOG)                                             
*                                                                               
         USING PROGOBJD,R3         BUMP TO OBJECT DATA AND PRECLEAR             
         LA    R3,6(R3)                                                         
         MVC   0(PROGOBJL,R3),BLANKS                                            
*                                                                               
         USING OGRRECD,R2          EXTRACT RECORD KEY DATA                      
         MVC   PROGCODE,OGRKCODE   CODE                                         
*                                                                               
         USING NAMELD,R6           EXTRACT NAME ELEMENT DATA                    
         LR    R6,R2                                                            
         MVI   ELCODE,NAMELQ                                                    
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         ZIC   RF,NAMLN            NAME                                         
         SH    RF,=Y(NAMLN1Q+1)                                                 
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   PROGNAME(0),NAMEREC                                              
*                                                                               
         LA    R3,PROGOBJL(R3)     BUMP R3 TO NEXT OBJECT                       
*                                                                               
BOGX     XC    0(2,R3),0(R3)       SET END OF OBJECT BLOCK                      
         B     YES                                                              
         DROP  R2,R3,R6                                                         
         EJECT                                                                  
* THIS ROUTINE BUILDS THE OFFICE OBJECT BLOCK FROM THE OFFICE                   
* RECORD PASSED.                                                                
*                                                                               
* PARAMETER 1 - A(OFFICE RECORD)                                                
* PARAMETER 2 - A(OFFICE OBJECT BLOCK)                                          
*                                                                               
BUILDOF  NTR1  ,                                                                
         LM    R2,R3,0(R1)         R2 = A(OFFICE RECORD)                        
*                                  R3 = A(OFFICE OBJECT BLOCK)                  
*                                                                               
*                                  SET OFFICE OBJECT LENGTH/TYPE                
         MVC   0(2,R3),=Y(PROFOBJL+4)                                           
         MVC   2(4,R3),=A(ITPRRTOF)                                             
*                                                                               
         USING PROFOBJD,R3         BUMP TO OBJECT DATA AND PRECLEAR             
         LA    R3,6(R3)                                                         
         MVC   0(PROFOBJL,R3),BLANKS                                            
*                                                                               
         USING OGRRECD,R2          EXTRACT RECORD KEY DATA                      
         MVC   PROFCODE,OGRKOFC    CODE                                         
*                                                                               
         USING NAMELD,R6           EXTRACT NAME ELEMENT DATA                    
         LR    R6,R2                                                            
         MVI   ELCODE,NAMELQ                                                    
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         ZIC   RF,NAMLN            NAME                                         
         SH    RF,=Y(NAMLN1Q+1)                                                 
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   PROFNAME(0),NAMEREC                                              
*                                                                               
         USING PGRELD,R6           EXTRACT PRODUCTION GROUP ELEM DATA           
         LR    R6,R2                                                            
         MVI   ELCODE,PGRELQ                                                    
         BAS   RE,GETEL                                                         
         BNE   *+10                                                             
         MVC   PROFOFFG,PGRCODE                                                 
*                                                                               
         LA    R3,PROFOBJL(R3)     BUMP R3 TO NEXT OBJECT                       
*                                                                               
BOFX     XC    0(2,R3),0(R3)       SET END OF OBJECT BLOCK                      
         B     YES                                                              
         DROP  R2,R3,R6                                                         
         EJECT                                                                  
* THIS ROUTINE BUILDS THE WORK GROUP OBJECT BLOCK FROM THE WORK                 
* GROUP RECORD PASSED.                                                          
*                                                                               
* PARAMETER 1 - A(WORK GROUP RECORD)                                            
* PARAMETER 2 - A(WORK GROUP OBJECT BLOCK)                                      
*                                                                               
BUILDWG  NTR1  ,                                                                
         LM    R2,R3,0(R1)         R2 = A(WORK GROUP RECORD)                    
*                                  R3 = A(WORK GROUP OBJECT BLOCK)              
*                                                                               
*                                  SET WORK GROUP OBJECT LENGTH/TYPE            
         MVC   0(2,R3),=Y(PRWGOBJL+4)                                           
         MVC   2(4,R3),=A(ITPRRTWG)                                             
*                                                                               
         USING PRWGOBJD,R3         BUMP TO OBJECT DATA AND PRECLEAR             
         LA    R3,6(R3)                                                         
         MVC   0(PRWGOBJL,R3),BLANKS                                            
*                                                                               
         USING WGRRECD,R2          EXTRACT RECORD KEY DATA                      
         MVC   PRWGCODE,WGRKCODE   CODE                                         
*                                                                               
         USING NAMELD,R6           EXTRACT NAME ELEMENT DATA                    
         LR    R6,R2                                                            
         MVI   ELCODE,NAMELQ                                                    
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         ZIC   RF,NAMLN            NAME                                         
         SH    RF,=Y(NAMLN1Q+1)                                                 
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   PRWGNAME(0),NAMEREC                                              
*                                                                               
         LA    R3,PRWGOBJL(R3)     BUMP R3 TO NEXT OBJECT                       
*                                                                               
BWGX     XC    0(2,R3),0(R3)       SET END OF OBJECT BLOCK                      
         B     YES                                                              
         DROP  R2,R3,R6                                                         
         EJECT                                                                  
* THIS ROUTINE BUILDS THE WORKCODE OBJECT BLOCK FROM THE WORKCODE               
* RECORD PASSED.                                                                
*                                                                               
* PARAMETER 1 - A(WORKCODE RECORD)                                              
* PARAMETER 2 - A(WORKCODE OBJECT BLOCK)                                        
*                                                                               
BUILDWC  NTR1  ,                                                                
         LM    R2,R3,0(R1)         R2 = A(WORKCODE RECORD)                      
*                                  R3 = A(WORKCODE OBJECT BLOCK)                
*                                                                               
*                                  SET WORKCODE OBJECT LENGTH/TYPE              
         MVC   0(2,R3),=Y(PRWCOBJL+4)                                           
         MVC   2(4,R3),=A(ITPRRTWC)                                             
*                                                                               
         USING PRWCOBJD,R3         BUMP TO OBJECT DATA AND PRECLEAR             
         LA    R3,6(R3)                                                         
         MVC   0(PRWCOBJL,R3),BLANKS                                            
*                                                                               
         USING WCORECD,R2          EXTRACT RECORD KEY DATA                      
         MVC   PRWCCODE,WCOKWRK    CODE                                         
         CLC   PRWCCODE,=C'**'     SKIP SPECIAL '**' CODE                       
         BE    NO                                                               
*                                                                               
         USING WCOELD,R6           EXTRACT WORKCODE ELEMENT DATA                
         LR    R6,R2                                                            
         MVI   ELCODE,WCOELQ                                                    
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         MVC   WORKSTAT,WCOSTAT    SAVE STATUS BYTES FOR SUFFIX OBJECTS         
         MVC   WORKSTA2,WCOSTAT2                                                
         MVC   PRWCNAME,WCODESC    NAME                                         
         MVC   PRWCWGR,WCOGRP      WORK GROUP CODE                              
         MVC   WORKGRP,WCOGRP      SAVE WORK GROUP                              
         MVI   PRWCINT,C'0'        IS INTERNAL (0/1) UK ONLY                    
*&&US*&& MVI   PRWCEXT,C'0'        IS EXTERNAL (0/1) UK ONLY                    
*&&UK*&& MVI   PRWCEXT,C'1'        UK-DEFAULT TO EXTERNAL WORKCODE              
*&&UK*&& TM    WORKSTAT,WCOSHCOE   TEST TIME WORKCODE                           
*&&UK*&& BZ    *+12                                                             
*&&UK*&& MVI   PRWCEXT,C'0'        YES-ITS INTERNAL IN UK                       
*&&UK*&& MVI   PRWCINT,C'1'                                                     
*                                                                               
         MVI   PRWCINA,C'0'        IS INACTIVE (0/1) US ONLY                    
*&&US*&& TM    WORKSTA2,WCOSINA                                                 
*&&US*&& BZ    *+8                                                              
*&&US*&& MVI   PRWCINA,C'1'                                                     
         MVI   PRWCICO,C'1'        IS COMMISSIONABLE (0/1) US ONLY              
*&&US*&& TM    WORKSTAT,WCOSNONC   TEST FOR NON-COMMISSIONABLE                  
*&&US*&& BZ    *+8                                                              
*&&US*&& MVI   PRWCICO,C'0'                                                     
*                                                                               
         MVI   PRWCILO,C'0'        IS LOCKED FOR ORDERS UK ONLY                 
*&&UK*&& TM    WORKSTA2,WCOSLORD   TEST LOCKED FOR ORDERS                       
*&&UK*&& BNO   *+8                                                              
*&&UK*&& MVI   PRWCILO,C'1'        YES - SET TO ONE                             
*                                                                               
*&&UK*&& TM    WORKSTAT,WCOSHCOE   IS THIS A TIME WORKCODE?                     
*&&UK*&& BZ    *+8                 NO                                           
*&&UK*&& MVI   PRWCTYP,C'T'        YES, INDICATE TIME                           
*                                                                               
         USING NAMELD,R6           EXTRACT NAME ELEMENT DATA                    
         LR    R6,R2                                                            
         MVI   ELCODE,NAMELQ                                                    
         BAS   RE,GETEL                                                         
         BNE   BWC20                                                            
         ZIC   RF,NAMLN            LONG NAME                                    
         SH    RF,=Y(NAMLN1Q+1)                                                 
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   PRWCLONG(0),NAMEREC                                              
*                                                                               
         USING XNMELD,R6           EXTRACT EXTRA NAMES ELEMENT DATA             
BWC20    LR    R6,R2                                                            
         MVI   ELCODE,XNMELQ                                                    
         BAS   RE,GETEL                                                         
         BNE   BWC30                                                            
         ZIC   RF,XNMSUBL          FOREIGN NAME                                 
         BCTR  RF,0                                                             
         BCTR  RF,0                LENGTH VALUE INCLUDES LENGTH BYTE            
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   PRWCFORN(0),XNMSUBN                                              
*                                                                               
BWC30    LA    R3,PRWCOBJL(R3)     BUMP R3 TO NEXT OBJECT                       
*                                                                               
         USING FFTELD,R6           BUILD SUFFIX OBJECTS FOR EACH FFTEL          
         MVI   BYTE,FFTTFREE                                                    
         GOTO1 CHELLO,DMCB,(C'G',ACCMST),('FFTELQ',(R2)),(1,BYTE)               
         L     R6,12(R1)                                                        
         CLI   12(R1),0                                                         
         BNE   BWCX                                                             
*                                                                               
BWC50    CLI   FFTSEQ,0            TEST FOR SEQUENCE=0                          
         BE    BWC70               YES-ITS A SPECIAL BILLING COMMENT            
*                                  SET WORKCODE OBJECT LENGTH/TYPE              
         MVC   0(2,R3),=Y(PRWCOBJL+4)                                           
         MVC   2(4,R3),=A(ITPRRTWC)                                             
*                                                                               
         LA    R3,6(R3)            BUMP TO OBJECT DATA AND PRECLEAR             
         MVC   0(PRWCOBJL,R3),BLANKS                                            
*                                                                               
         MVC   PRWCCODE,WCOKWRK    CODE (AGAIN FROM RECORD KEY)                 
         MVC   PRWCWGR,WORKGRP     WORK GROUP FROM MASTER EL                    
*                                                                               
         MVI   PRWCINT,C'0'        IS INTERNAL (0/1) UK ONLY                    
*&&US*&& MVI   PRWCEXT,C'0'        IS EXTERNAL (0/1) UK ONLY                    
*&&UK*&& MVI   PRWCEXT,C'1'        UK-DEFAULT TO EXTERNAL WORKCODE              
*&&UK*&& TM    WORKSTAT,WCOSHCOE   TEST TIME WORKCODE                           
*&&UK*&& BZ    *+12                                                             
*&&UK*&& MVI   PRWCEXT,C'0'        YES-ITS INTERNAL IN UK                       
*&&UK*&& MVI   PRWCINT,C'1'                                                     
         MVI   PRWCINA,C'0'        IS INACTIVE (0/1) US ONLY                    
*&&US*&& TM    WORKSTA2,WCOSINA                                                 
*&&US*&& BZ    *+8                                                              
*&&US*&& MVI   PRWCINA,C'1'                                                     
         MVI   PRWCICO,C'1'        IS COMMISSIONABLE (0/1) US ONLY              
*&&US*&& TM    WORKSTAT,WCOSNONC   TEST FOR NON-COMMISSIONABLE                  
*&&US*&& BZ    *+8                                                              
*&&US*&& MVI   PRWCICO,C'0'                                                     
*                                  SUFFIX (FROM FFTEL)                          
         MVI   PRWCILO,C'0'        IS LOCKED FOR ORDERS UK ONLY                 
*&&UK*&& TM    WORKSTA2,WCOSLORD   TEST LOCKED FOR ORDERS                       
*&&UK*&& BNO   *+8                                                              
*&&UK*&& MVI   PRWCILO,C'1'        YES - SET TO ONE                             
*                                  SUFFIX (FROM FFTEL)                          
*&&UK*&& TM    WORKSTAT,WCOSHCOE   IS THIS A TIME WORKCODE?                     
*&&UK*&& BZ    *+8                 NO                                           
*&&UK*&& MVI   PRWCTYP,C'T'        YES, INDICATE TIME                           
*                                                                               
         GOTO1 CHEXOUT,DMCB,FFTSEQ,PRWCSUFF,1,MIXED                             
         ZIC   RF,FFTDLEN          LONG NAME                                    
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   PRWCLONG(0),FFTDATA                                              
         C     RF,=F'14'           NAME                                         
         BNH   *+8                                                              
         LA    RF,14                                                            
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   PRWCNAME(0),FFTDATA                                              
*                                                                               
         USING FFTELD,R6           GET FOREIGN NAME IF THERE IS ONE             
         MVI   HALF,FFTTFREE                                                    
         MVC   HALF+1(1),FFTSEQ                                                 
         OI    HALF+1,X'80'                                                     
         GOTO1 CHELLO,DMCB,(C'G',ACCMST),('FFTELQ',(R2)),(2,HALF)               
         L     RE,12(R1)           RE=A(FOREIGN NAME FREE FORM EL)              
         CLI   12(R1),0                                                         
         BNE   BWC60                                                            
         ZIC   RF,FFTDLEN-FFTELD(RE)  FOREIGN NAME                              
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   PRWCFORN(0),FFTDATA-FFTELD(RE)                                   
*                                                                               
BWC60    LA    R3,PRWCOBJL(R3)     BUMP R3 TO NEXT OBJECT                       
*                                                                               
BWC70    ZIC   R0,FFTLN            GET NEXT FFTEL AND LOOP BACK                 
         AR    R6,R0                                                            
         CLI   FFTEL,0                                                          
         BE    BWCX                                                             
         CLI   FFTEL,FFTELQ                                                     
         BNE   BWC70                                                            
         CLI   FFTTYPE,FFTTFREE                                                 
         BNE   BWC70                                                            
         TM    FFTSEQ,X'80'                                                     
         BO    BWC70                                                            
         B     BWC50                                                            
*                                                                               
BWCX     XC    0(2,R3),0(R3)       SET END OF OBJECT BLOCK                      
         B     YES                                                              
         DROP  R2,R3,R6                                                         
         EJECT                                                                  
* THIS ROUTINE BUILDS THE SCHEME OBJECT BLOCK FROM THE SCHEME                   
* RECORD PASSED.                                                                
*                                                                               
* PARAMETER 1 - A(SCHEME RECORD)                                                
* PARAMETER 2 - A(SCHEME OBJECT BLOCK)                                          
*                                                                               
BUILDSC  NTR1  ,                                                                
         LM    R2,R3,0(R1)         R2 = A(SCHEME RECORD)                        
*                                  R3 = A(SCHEME OBJECT BLOCK)                  
*                                                                               
*                                  SET SCHEME OBJECT LENGTH/TYPE                
         MVC   0(2,R3),=Y(PRSHOBJL+4)                                           
         MVC   2(4,R3),=A(ITPRRTSH)                                             
*                                                                               
         USING PRSHOBJD,R3         BUMP TO OBJECT DATA AND PRECLEAR             
         LA    R3,6(R3)                                                         
         MVC   0(PRSHOBJL,R3),BLANKS                                            
*                                                                               
         USING SCHRECD,R2          EXTRACT RECORD KEY DATA                      
         MVC   PRSHCODE,SCHKCODE   CODE                                         
*                                                                               
         USING SCHELD,R6           EXTRACT SCHEME DEF ELEMENT DATA              
         LR    R6,R2                                                            
         MVI   ELCODE,SCHELQ                                                    
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         MVC   PRSHNAME,SCHNAME    NAME                                         
*                                                                               
         LA    R3,PRSHOBJL(R3)     BUMP R3 TO NEXT OBJECT                       
*                                                                               
         USING SCSELD,R6           BUILD SCHEME CATEGORY OBJECTS FOR            
         LR    R6,R2                   EACH CATEGORY IN SCSEL                   
         MVI   ELCODE,SCSELQ                                                    
         BAS   RE,GETEL                                                         
         BNE   BSCX                                                             
*                                                                               
         ZIC   R5,SCSLN            R5 = NUMBER OF CATEGORIES                    
         S     R5,=F'2'                                                         
         SRL   R5,1                                                             
         LTR   R5,R5               IF NONE THEN SKIP TO END                     
         BZ    BSCX                                                             
*                                                                               
         LA    R4,SCSCAT           R4 = A(FIRST CATEGORY)                       
*                                                                               
*                                  SET SCHEME CAT OBJECT LENGTH/TYPE            
BSC50    MVC   0(2,R3),=Y(PRSCOBJL+4)                                           
         MVC   2(4,R3),=A(ITPRRTSC)                                             
*                                                                               
         USING PRSCOBJD,R3         BUMP TO OBJECT DATA AND PRECLEAR             
         LA    R3,6(R3)                                                         
         MVC   0(PRSCOBJL,R3),BLANKS                                            
*                                                                               
         MVC   PRSCSHCD,SCHKCODE   SCHEME CODE (AGAIN FROM RECORD KEY)          
*                                                                               
         MVC   PRSCCTCD,0(R4)      CATEGORY CODE (FROM SCSEL)                   
         CLC   PRSCCTCD,=X'FFFF'   CONVERT SLUSH (X'FFFF') TO 'ANDAND'          
         BNE   *+10                                                             
         MVC   PRSCCTCD,=X'5050'                                                
*                                                                               
         ZIC   RF,SCSLN            POSITION (CALCULATED FROM R5)                
         SRL   RF,1                                                             
         SR    RF,R5                                                            
         STC   RF,BYTE                                                          
         GOTO1 CHEXOUT,DMCB,BYTE,PRSCPOS,1,MIXED                                
*                                                                               
         LA    R3,PRSCOBJL(R3)     BUMP R3 TO NEXT OBJECT                       
*                                                                               
         LA    R4,2(R4)            BUMP TO NEXT CATEGORY                        
         BCT   R5,BSC50            REPEAT UNTIL NO MORE CATEGORIES              
*                                                                               
BSCX     XC    0(2,R3),0(R3)       SET END OF OBJECT BLOCK                      
         B     YES                                                              
         DROP  R2,R3,R6                                                         
         EJECT                                                                  
* THIS ROUTINE BUILDS THE CATEGORY OBJECT BLOCK FROM THE CATEGORY               
* RECORD PASSED.                                                                
*                                                                               
* PARAMETER 1 - A(CATEGORY RECORD)                                              
* PARAMETER 2 - A(CATEGORY OBJECT BLOCK)                                        
*                                                                               
BUILDCW  NTR1  ,                                                                
         LM    R2,R3,0(R1)         R2 = A(CATEGORY RECORD)                      
*                                  R3 = A(CATEGORY OBJECT BLOCK)                
*                                                                               
*                                  SET CATEGORY OBJECT LENGTH/TYPE              
         MVC   0(2,R3),=Y(PRCTOBJL+4)                                           
         MVC   2(4,R3),=A(ITPRRTCT)                                             
*                                                                               
         USING PRCTOBJD,R3         BUMP TO OBJECT DATA AND PRECLEAR             
         LA    R3,6(R3)                                                         
         MVC   0(PRCTOBJL,R3),BLANKS                                            
*                                                                               
         USING CATRECD,R2          EXTRACT RECORD KEY DATA                      
         MVC   PRCTSHCD,CATKSCH    SCHEME CODE                                  
         MVC   PRCTCTCD,CATKCODE   CATEGORY CODE                                
         CLC   PRCTCTCD,=X'FFFF'   CONVERT SLUSH (X'FFFF') TO 'ANDAND'          
         BNE   *+10                                                             
         MVC   PRCTCTCD,=X'5050'                                                
*                                                                               
         USING CADELD,R6           EXTRACT CAT DESC ELEMENT DATA                
         LR    R6,R2                                                            
         MVI   ELCODE,CADELQ                                                    
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         GOTO1 CHEXOUT,DMCB,CADTYPE,PRCTTYPE,1,MIXED TYPE                       
         GOTO1 CHEXOUT,DMCB,CADSTAT,PRCTFLGS,1,MIXED FLAGS                      
         MVC   PRCTNAME,CADNAME                    NAME                         
         MVC   PRCTTNAM,CADTNAM                    TOTAL NAME                   
         CLI   CADTYPE,CADTFORM                    FORMULA                      
         BNE   BCW20                                                            
         LA    RE,CADINST          RETURN I/P STRING                            
         ZIC   RF,0(RE)                                                         
*                                                                               
         SHI   RF,2                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   PRCTFORM(0),1(RE)                                                
*                                                                               
BCW20    TM    CADSTAT,CADSFLAN    TEST FOR FOREIGN LANGUAGE NAMES              
         BZ    BCW30               NO                                           
*                                                                               
         ZIC   R1,CADLINP          GET LENGTH OF STRING                         
         LA    RE,CADLINP          RE=A(LENGTH/STRING)                          
         CLI   CADTYPE,CADTFORM    TEST FOR FORMULA                             
         BNE   BCW25                                                            
*                                                                               
         LA    RE,0(R1,RE)         BUMP PAST FORMULA STRING                     
         ZIC   R1,0(RE)            GET LENGTH OF INTERNAL FORMULA               
         LA    RE,0(R1,RE)                                                      
         ZIC   R1,0(RE)            GET LENGTH OF FOREIGN NAME                   
*                                                                               
BCW25    SH    R1,=H'2'            ADJUST FOR LENGTH BYTE/EXEC                  
         BM    BCW30                                                            
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   PRCTFNAM(0),1(RE)   EXTRACT FOREIGN NAME                         
         LA    RE,2(R1,RE)         BUMP PAST FOREIGN NAME                       
*                                                                               
         ZIC   R1,0(RE)            GET LENGTH OF POSSIBLE TOTAL NAME            
         SH    R1,=H'2'                                                         
         BM    BCW30               THERE IS NONE                                
*                                                                               
         EX    R1,*+8                                                           
         B     BCW30                                                            
         MVC   PRCTFTNM(0),1(RE)   EXTRACT FOREIGN TOTAL NAME                   
*                                                                               
BCW30    LA    R3,PRCTOBJL(R3)     BUMP R3 TO NEXT OBJECT                       
*                                                                               
         CLI   CADTYPE,CADTWORK    TEST CATEGORY IS A WORKCODE LIST             
         BNE   BCWX                NO-SKIP WORKCODE SEARCH                      
*                                                                               
         USING CWKELD,R6           BUILD CATEGORY WORKCODE OBJECTS              
         LR    R6,R2                   FOR EACH CWKEL                           
         MVI   ELCODE,CWKELQ                                                    
         BAS   RE,GETEL                                                         
*                                                                               
BCW50    BNE   BCWX                IF END OF CWKELS THEN DONE                   
*                                                                               
         CLI   CWKTYPE,CWKTWORK    SKIP NON-WORKCODE ELEMENTS                   
         BNE   BCW90                                                            
*                                  SET CAT WORKCODE OBJECT LENGTH/TYPE          
         MVC   0(2,R3),=Y(PRCWOBJL+4)                                           
         MVC   2(4,R3),=A(ITPRRTCW)                                             
*                                                                               
         USING PRCWOBJD,R3         BUMP TO OBJECT DATA AND PRECLEAR             
         LA    R3,6(R3)                                                         
         MVC   0(PRCWOBJL,R3),BLANKS                                            
*                                                                               
         MVC   PRCWCODE,CWKWORK                 WORKCODE (FROM CWKEL)           
         GOTO1 CHEXOUT,DMCB,CWKSUFF,PRCWSUFF,1,MIXED SUFFIX                     
         GOTO1 CHEXOUT,DMCB,CWKSEQ,PRCWPOS,1,MIXED POSITION                     
         CLI   CWKLN,CWKLN1Q                    OVERRIDE NAME                   
         BE    *+10                                                             
         MVC   PRCWNAME,CWKDESC                                                 
         MVC   PRCWAPPL,CWKCAT                  APPLY TO CATEGORY               
         MVI   PRCWUPL,C'1'                     DEFAULT UPLOAD = 1              
         TM    CWKSTAT,CWKSPRE                  TEST PRESTO ONLY                
         BNO   *+8                                                              
         MVI   PRCWUPL,C'0'                     YES-SET UPLOAD = 0              
*                                                                               
         LA    R3,PRCWOBJL(R3)     BUMP R3 TO NEXT OBJECT                       
*                                                                               
BCW90    BAS   RE,NEXTEL           GET NEXT CWKEL AND LOOP BACK                 
         B     BCW50                                                            
*                                                                               
BCWX     XC    0(2,R3),0(R3)       SET END OF OBJECT BLOCK                      
         B     YES                                                              
         DROP  R2,R3,R6                                                         
         EJECT                                                                  
* THIS ROUTINE BUILDS THE CLIENT OBJECT BLOCK FROM THE CLIENT                   
* RECORD PASSED.                                                                
*                                                                               
* PARAMETER 1 - A(CLIENT RECORD)                                                
* PARAMETER 2 - A(CLIENT OBJECT BLOCK)                                          
*                                                                               
BUILDCL  NTR1  ,                                                                
         LM    R2,R3,0(R1)         R2 = A(CLIENT RECORD)                        
*                                  R3 = A(CLIENT OBJECT BLOCK)                  
*                                                                               
*                                  SET CLIENT OBJECT LENGTH/TYPE                
         MVC   0(2,R3),=Y(PRCLOBJL+4)                                           
         MVC   2(4,R3),=A(ITPRRTCL)                                             
*                                                                               
         USING PRCLOBJD,R3         BUMP TO OBJECT DATA AND PRECLEAR             
         LA    R3,6(R3)                                                         
         MVC   0(PRCLOBJL,R3),BLANKS                                            
*                                                                               
         USING ACTRECD,R2          EXTRACT RECORD KEY DATA                      
         ZIC   RF,ACPRLCLI         CLIENT CODE                                  
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   PRCLCLCD(0),ACTKACT                                              
*                                                                               
         USING NAMELD,R6           EXTRACT NAME ELEMENT DATA                    
         LR    R6,R2                                                            
         MVI   ELCODE,NAMELQ                                                    
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         ZIC   RF,NAMLN            NAME                                         
         SH    RF,=Y(NAMLN1Q+1)                                                 
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   PRCLNAME(0),NAMEREC                                              
*                                                                               
         USING ADRELD,R6           EXTRACT ADDRESS ELEMENT DATA                 
         LR    R6,R2                                                            
         MVI   ELCODE,ADRELQ                                                    
         BAS   RE,GETEL                                                         
         BNE   BCL50                                                            
         MVC   PRCLADD1,ADRADD1    ADDRESS LINE 1                               
         CLI   ADRNUM,1                                                         
         BE    BCL50                                                            
         MVC   PRCLADD2,ADRADD2    ADDRESS LINE 2                               
         CLI   ADRNUM,2                                                         
         BE    BCL50                                                            
         MVC   PRCLADD3,ADRADD3    ADDRESS LINE 3                               
         CLI   ADRNUM,3                                                         
         BE    BCL50                                                            
         MVC   PRCLADD4,ADRADD4    ADDRESS LINE 4                               
         CLI   ADRNUM,4                                                         
         BE    BCL50                                                            
         MVC   PRCLADD5,ADRADD5    ADDRESS LINE 5                               
*                                                                               
         USING PPRELD,R6           EXTRACT PROD PROFILE ELEMENT DATA            
BCL50    LR    R6,R2                                                            
         MVI   ELCODE,PPRELQ                                                    
         BAS   RE,GETEL                                                         
         BNE   BCL70                                                            
         MVC   PRCLPOB,PPRBILLP    PRINT ON BILLS                               
         OC    PRCLPOB,BLANKS                                                   
         MVC   PRCLAGYO,PPRGAOFF   AGENCY OFFICE                                
         OC    PRCLAGYO,BLANKS                                                  
*                                                                               
         USING RSTELD,R6           EXTRACT RECORD STATUS ELEMENT DATA           
BCL70    LR    R6,R2                                                            
         MVI   ELCODE,RSTELQ                                                    
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
*                                                                               
         MVI   PRCLLOCK,C'0'       TEST LOCKED                                  
         TM    RSTSTAT1,RSTSACIL                                                
         BZ    *+8                                                              
         MVI   PRCLLOCK,C'1'       YES                                          
         MVC   PRCLFILT(2),RSTFILT1     FILTERS                                 
         MVC   PRCLFILT+2(1),RSTFILT3                                           
         MVC   PRCLFILT+3(1),RSTFILT4                                           
         CLI   RSTLN,RSTLN1Q                                                    
         BE    *+10                                                             
         MVC   PRCLFILT+4(1),RSTFILT5                                           
*                                                                               
         LA    RE,PRCLFILT                                                      
         LA    R0,L'PRCLFILT                                                    
*                                                                               
BCL72    CLI   0(RE),C' '          MAKE SURE FILTERS OR EITHER                  
         BH    *+8                 BLANKS OR A VALUE                            
         MVI   0(RE),C' '                                                       
         LA    RE,1(RE)                                                         
         BCT   R0,BCL72                                                         
*                                                                               
         LA    R3,PRCLOBJL(R3)     BUMP R3 TO NEXT OBJECT                       
*                                                                               
BCLX     XC    0(2,R3),0(R3)       SET END OF OBJECT BLOCK                      
         B     YES                                                              
         BNE   BPR50                                                            
         EJECT                                                                  
* THIS ROUTINE BUILDS THE PRODUCT OBJECT BLOCK FROM THE PRODUCT                 
* RECORD PASSED.                                                                
*                                                                               
* PARAMETER 1 - A(PRODUCT RECORD)                                               
* PARAMETER 2 - A(PRODUCT OBJECT BLOCK)                                         
*                                                                               
BUILDPR  NTR1  ,                                                                
         LM    R2,R3,0(R1)         R2 = A(PRODUCT RECORD)                       
*                                  R3 = A(PRODUCT OBJECT BLOCK)                 
*                                                                               
*                                  SET PRODUCT OBJECT LENGTH/TYPE               
         MVC   0(2,R3),=Y(PRPROBJL+4)                                           
         MVC   2(4,R3),=A(ITPRRTPR)                                             
*                                                                               
         USING PRPROBJD,R3         BUMP TO OBJECT DATA AND PRECLEAR             
         LA    R3,6(R3)                                                         
         MVC   0(PRPROBJL,R3),BLANKS                                            
*                                                                               
         USING ACTRECD,R2          EXTRACT RECORD KEY DATA                      
         LA    RE,ACTKACT          CLIENT CODE                                  
         ZIC   RF,ACPRLCLI                                                      
         LR    R1,RF                                                            
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   PRPRCLCD(0),0(RE)                                                
         LA    RE,0(RE,R1)         PRODUCT CODE                                 
         ZIC   RF,ACPRLPRO                                                      
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   PRPRPRCD(0),0(RE)                                                
*                                                                               
         USING NAMELD,R6           EXTRACT NAME ELEMENT DATA                    
         LR    R6,R2                                                            
         MVI   ELCODE,NAMELQ                                                    
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         ZIC   RF,NAMLN            NAME                                         
         SH    RF,=Y(NAMLN1Q+1)                                                 
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   PRPRNAME(0),NAMEREC                                              
*                                                                               
         USING ADRELD,R6           EXTRACT ADDRESS ELEMENT DATA                 
         LR    R6,R2                                                            
         MVI   ELCODE,ADRELQ                                                    
         BAS   RE,GETEL                                                         
         BNE   BPR50                                                            
         MVC   PRPRADD1,ADRADD1    ADDRESS LINE 1                               
         CLI   ADRNUM,1                                                         
         BE    BPR50                                                            
         MVC   PRPRADD2,ADRADD2    ADDRESS LINE 2                               
         CLI   ADRNUM,2                                                         
         BE    BPR50                                                            
         MVC   PRPRADD3,ADRADD3    ADDRESS LINE 3                               
         CLI   ADRNUM,3                                                         
         BE    BPR50                                                            
         MVC   PRPRADD4,ADRADD4    ADDRESS LINE 4                               
         CLI   ADRNUM,4                                                         
         BE    BPR50                                                            
         MVC   PRPRADD5,ADRADD5    ADDRESS LINE 5                               
*                                                                               
         USING PPRELD,R6           EXTRACT PROD PROFILE ELEMENT DATA            
BPR50    LR    R6,R2                                                            
         MVI   ELCODE,PPRELQ                                                    
         BAS   RE,GETEL                                                         
         BNE   BPR70                                                            
         MVC   PRPRPOB,PPRBILLP    PRINT ON BILLS                               
         OC    PRPRPOB,BLANKS                                                   
         MVC   PRPRAGYO,PPRGAOFF   AGENCY OFFICE                                
         OC    PRPRAGYO,BLANKS                                                  
*                                                                               
         USING RSTELD,R6           EXTRACT RECORD STATUS ELEMENT DATA           
BPR70    LR    R6,R2                                                            
         MVI   ELCODE,RSTELQ                                                    
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
*                                                                               
         MVI   PRPRLOCK,C'0'                                                    
         TM    RSTSTAT1,RSTSACIL   TEST LOCKED                                  
         BZ    *+8                                                              
         MVI   PRPRLOCK,C'1'       YES                                          
         MVC   PRPRFILT(2),RSTFILT1     FILTERS                                 
         MVC   PRPRFILT+2(1),RSTFILT3                                           
         MVC   PRPRFILT+3(1),RSTFILT4                                           
         CLI   RSTLN,RSTLN1Q                                                    
         BE    *+10                                                             
         MVC   PRPRFILT+4(1),RSTFILT5                                           
*                                                                               
         LA    RE,PRPRFILT                                                      
         LA    R0,L'PRPRFILT                                                    
*                                                                               
BPR72    CLI   0(RE),C' '          MAKE SURE FILTERS OR EITHER                  
         BH    *+8                 BLANKS OR A VALUE                            
         MVI   0(RE),C' '                                                       
         LA    RE,1(RE)                                                         
         BCT   R0,BPR72                                                         
*                                                                               
         LA    R3,PRPROBJL(R3)     BUMP R3 TO NEXT OBJECT                       
*                                                                               
BPRX     XC    0(2,R3),0(R3)       SET END OF OBJECT BLOCK                      
         B     YES                                                              
         DROP  R2,R3,R6                                                         
         EJECT                                                                  
* THIS ROUTINE BUILDS THE JOB OBJECT BLOCK FROM THE JOB                         
* RECORD PASSED.                                                                
*                                                                               
* PARAMETER 1 - A(JOB RECORD)                                                   
* PARAMETER 2 - A(JOB OBJECT BLOCK)                                             
*                                                                               
BUILDJB  NTR1  ,                                                                
         LM    R2,R3,0(R1)         R2 = A(JOB RECORD)                           
         USING ACTRECD,R2          EXTRACT RECORD KEY DATA                      
         TM    ACTRSTAT,ACTSDRFT   IS JOB APPROVED?                             
         BO    NO                  NO, SKIP IT                                  
*                                                                               
*                                  R3 = A(JOB OBJECT BLOCK)                     
*                                                                               
*                                  SET JOB OBJECT LENGTH/TYPE                   
         MVC   0(2,R3),=Y(PRJBOBJL+4)                                           
         MVC   2(4,R3),=A(ITPRRTJB)                                             
*                                                                               
         USING PRJBOBJD,R3         BUMP TO OBJECT DATA AND PRECLEAR             
         LA    R3,6(R3)                                                         
         MVC   0(250,R3),BLANKS                                                 
         MVC   250(PRJBOBJL-250,R3),BLANKS                                      
         LA    RE,ACTKACT          CLIENT CODE                                  
         ZIC   RF,ACPRLCLI                                                      
         LR    R1,RF                                                            
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   PRJBCLCD(0),0(RE)                                                
         LA    RE,0(RE,R1)         PRODUCT CODE                                 
         ZIC   RF,ACPRLPRO                                                      
         LR    R1,RF                                                            
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   PRJBPRCD(0),0(RE)                                                
         LA    RE,0(RE,R1)         JOB CODE                                     
         ZIC   RF,ACPRLJOB                                                      
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   PRJBJBCD(0),0(RE)                                                
         MVI   PRJBDEL,C'0'        IS DELETED (0/1)                             
         TM    ACTRSTAT,ACTSDELT                                                
         BZ    *+8                                                              
         MVI   PRJBDEL,C'1'                                                     
*                                                                               
         USING NAMELD,R6           EXTRACT NAME ELEMENT DATA                    
         LR    R6,R2                                                            
         MVI   ELCODE,NAMELQ                                                    
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         ZIC   RF,NAMLN            NAME                                         
         SH    RF,=Y(NAMLN1Q+1)                                                 
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   PRJBNAME(0),NAMEREC                                              
*                                                                               
         USING ADRELD,R6           EXTRACT ADDRESS ELEMENT DATA                 
         LR    R6,R2                                                            
         MVI   ELCODE,ADRELQ                                                    
         BAS   RE,GETEL                                                         
         BNE   BJB50                                                            
         MVC   PRJBADD1,ADRADD1    ADDRESS LINE 1                               
         CLI   ADRNUM,1                                                         
         BE    BJB50                                                            
         MVC   PRJBADD2,ADRADD2    ADDRESS LINE 2                               
         CLI   ADRNUM,2                                                         
         BE    BJB50                                                            
         MVC   PRJBADD3,ADRADD3    ADDRESS LINE 3                               
         CLI   ADRNUM,3                                                         
         BE    BJB50                                                            
         MVC   PRJBADD4,ADRADD4    ADDRESS LINE 4                               
         CLI   ADRNUM,4                                                         
         BE    BJB50                                                            
         MVC   PRJBADD5,ADRADD5    ADDRESS LINE 5                               
*                                                                               
         USING PPRELD,R6           EXTRACT PROD PROFILE ELEMENT DATA            
BJB50    LR    R6,R2                                                            
         MVI   ELCODE,PPRELQ                                                    
         BAS   RE,GETEL                                                         
         BNE   BJB70                                                            
         MVC   PRJBPOB,PPRBILLP    PRINT ON BILLS                               
         OC    PRJBPOB,BLANKS      YES-UK DEFAULTS TO NULLS IN FIELD            
         MVC   PRJBAGYO,PPRGAOFF   AGENCY OFFICE                                
         OC    PRJBAGYO,BLANKS                                                  
*                                                                               
         USING RSTELD,R6           EXTRACT RECORD STATUS ELEMENT DATA           
BJB70    LR    R6,R2                                                            
         MVI   ELCODE,RSTELQ                                                    
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         MVC   PRJBFILT(2),RSTFILT1     FILTERS                                 
         MVC   PRJBFILT+2(1),RSTFILT3                                           
         MVC   PRJBFILT+3(1),RSTFILT4                                           
*                                                                               
         GOTO1 CHEXOUT,DMCB,RSTSTAT1,PRJBACST,1,MIXED ACCOUNT STATUS            
         MVI   PRJBCLOS,C'0'       IS CLOSED (0/1)                              
         TM    RSTSTAT1,RSTSACIC                                                
         BZ    *+8                                                              
         MVI   PRJBCLOS,C'1'                                                    
         MVI   PRJBLOCK,C'0'       IS LOCKED (0/1)                              
         TM    RSTSTAT1,RSTSACIL                                                
         BZ    *+8                                                              
         MVI   PRJBLOCK,C'1'                                                    
*                                                                               
         CLI   RSTLN,RSTLN1Q                                                    
         BE    BJB80                                                            
         MVC   PRJBFILT+4(1),RSTFILT5                                           
*                                                                               
BJB80    LA    RE,PRJBFILT                                                      
         LA    R0,L'PRJBFILT                                                    
*                                                                               
BJB82    CLI   0(RE),C' '          MAKE SURE FILTERS OR EITHER                  
         BH    *+8                 BLANKS OR A VALUE                            
         MVI   0(RE),C' '                                                       
         LA    RE,1(RE)                                                         
         BCT   R0,BJB82                                                         
*                                                                               
         USING JOBELD,R6           EXTRACT PROD JOB ELEMENT DATA                
BJB90    LR    R6,R2                                                            
         MVI   ELCODE,JOBELQ                                                    
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         MVI   PRJBNEW,C'0'        IS NEW ESTIMATING (0/1)                      
         TM    JOBSTA1,JOBSNEST                                                 
         BZ    *+8                                                              
         MVI   PRJBNEW,C'1'                                                     
*                                                                               
         MVC   THREE,JOBODATE                                                   
         OC    THREE,THREE         TEST FOR OPEN DATE                           
         BNZ   *+10                                                             
         MVC   THREE,JOBADATE      USE DATE ADDED IF NO OPEN DATE               
*                                                                               
         GOTO1 CDATCON,DMCB,(1,THREE),(X'20',PRJBOPDT)    OPEN DATE             
         GOTO1 CDATCON,DMCB,(1,JOBCDATE),(X'20',PRJBCLDT) CLOSE DATE            
*                                                                               
         LA    R3,PRJBOBJL(R3)     BUMP R3 TO NEXT OBJECT                       
*                                                                               
         TM    ACTRSTAT,ACTSDELT+ACTSCLOS TEST JOB DELETED OR CLOSED            
         BNZ   *+8                 YES                                          
         BAS   RE,BUILDUF          NO-ALSO SEND DOWN USER FIELDS                
         TM    ACTRSTAT,ACTSDELT   TEST DELETED JOB                             
         BO    BJBX                YES-SKIP HEADER/FOOTER COMMENTS              
*                                                                               
         TM    ACTRSTAT,ACTSCLOS   TEST CLOSED JOB                              
         BZ    *+12                NO                                           
         CLI   ACPRLOAD,C'Y'       YES ITS CLOSED-TEST LOADING JOBS             
         BNE   BJBX                NO-DON'T SEND THEM DOWN                      
*                                                                               
         BAS   RE,BUILDJH          SEND JOB HEADER COMMENTS                     
         BAS   RE,BUILDJF          SEND JOB FOOTER COMMENTS                     
*                                                                               
BJBX     XC    0(2,R3),0(R3)       SET END OF OBJECT BLOCK                      
         B     YES                                                              
         DROP  R2,R3,R6                                                         
         EJECT                                                                  
*                                                                               
*        BUILD JOB USER FIELD OBJECTS                                           
*              AT ENTRY, R3=A(OBJECTS), R2 = A(JOB)                             
*              CALLED BY BUILDJB, ON EXIT, RETURN R3 TO CALLER                  
*                                                                               
BUILDUF  NTR1  ,                                                                
         LR    R6,R2                                                            
         MVI   ELCODE,UFSELQ       USER FIELD ELEMENT                           
         BAS   RE,GETEL                                                         
*                                                                               
BUF10    BNE   BUFX                ALL DONE WITH PROCESSING                     
         USING UFSELD,R6                                                        
*                                                                               
         CLI   UFSLN,UFSDATA-UFSELD  TEST FOR NO DATA                           
         BE    BUF20               YES-SKIP ELEMENT                             
*                                                                               
         MVC   0(2,R3),=Y(PRUFOBJL+4)                                           
         MVC   2(4,R3),=A(ITPRJMRT)                                             
         LA    R3,6(R3)            BUMP POINTER TO OBJECT DATA                  
         USING PRUFOBJD,R3                                                      
*                                                                               
         MVC   PRUFOBJD(PRUFOBJL),BLANKS                                        
         MVC   PRUFCODE,UFSCODE                                                 
         MVC   PRUFNAME,UFSDESC                                                 
         GOTO1 CHEXOUT,DMCB,UFSSEQ,PRUFSEQ,L'UFSSEQ,MIXED                       
*                                                                               
         MVI   PRUFSHES,C'0'       SHOW ON ESTIMATES FLAG= 0 OR 1               
         TM    UFSSTAT,UFSSSHES                                                 
         BZ    *+8                                                              
         MVI   PRUFSHES,C'1'                                                    
*                                                                               
         ZIC   R1,UFSLN                                                         
         SH    R1,=Y(UFSDATA-UFSELD+1)                                          
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   PRUFDATA(0),UFSDATA                                              
         LA    R3,PRUFOBJL(R3)     BUMP TO NEXT OBJECT POSITION                 
*                                                                               
BUF20    BAS   RE,NEXTEL                                                        
         B     BUF10                                                            
*                                                                               
BUFX     XIT1  REGS=(R3)                                                        
         DROP  R3,R6                                                            
         EJECT                                                                  
*                                                                               
*        BUILD JOB HEADER TEXT OBJECTS                                          
*              AT ENTRY, R3=A(OBJECTS), R2 = A(JOB)                             
*              CALLED BY BUILDJB, ON EXIT, RETURN R3 TO CALLER                  
*                                                                               
BUILDJH  NTR1  ,                                                                
         LR    R6,R2                                                            
         MVI   ELCODE,PPRELQ       PRODUCTION PROFILE ELEMENT                   
         BAS   RE,GETEL                                                         
         BNE   BJHX                                                             
*                                                                               
         USING PPRELD,R6                                                        
BJH10    CLI   PPRLN,PPRLN1Q       TEST FOR ANY HEADER TEXT                     
         BNH   BJHX                NO-EXIT                                      
*                                                                               
         USING PRJTOBJD,R3                                                      
*                                                                               
         MVC   0(2,R3),=Y(PRJTOBJL+4)                                           
         MVC   2(4,R3),=A(ITPRRTJC)                                             
         LA    R3,6(R3)            BUMP POINTER TO OBJECT DATA                  
         MVC   PRJTOBJD(PRJTOBJL),BLANKS                                        
         MVI   PRJTTYPE,C'1'       TYPE = HEADER                                
         MVI   PRJTSEQ,C'1'                                                     
         MVC   PRJTTEXT,PPRNARRP                                                
         LA    R3,PRJTOBJL(R3)                                                  
*                                                                               
BJH20    CLI   PPRLN,PPRLN1Q+L'PRJTTEXT  TEST FOR SECOND TEXT                   
         BNH   BJHX                      NO                                     
*                                                                               
         MVC   0(2,R3),=Y(PRJTOBJL+4)                                           
         MVC   2(4,R3),=A(ITPRRTJC)                                             
         LA    R3,6(R3)            BUMP POINTER TO OBJECT DATA                  
         MVC   PRJTOBJD(PRJTOBJL),BLANKS                                        
         MVI   PRJTTYPE,C'1'       TYPE = HEADER                                
         MVI   PRJTSEQ,C'2'                                                     
         MVC   PRJTTEXT,PPRNARRP+L'PRJTTEXT                                     
         LA    R3,PRJTOBJL(R3)     BUMP TO NEXT OBJECT POSITION                 
*                                                                               
BJH30    CLI   PPRLN,PPRLN1Q+(2*L'PRJTTEXT)                                     
         BNH   BJHX                                                             
*                                                                               
         MVC   0(2,R3),=Y(PRJTOBJL+4)                                           
         MVC   2(4,R3),=A(ITPRRTJC)                                             
         LA    R3,6(R3)            BUMP POINTER TO OBJECT DATA                  
         MVC   PRJTOBJD(PRJTOBJL),BLANKS                                        
         MVI   PRJTTYPE,C'1'       TYPE = HEADER                                
         MVI   PRJTSEQ,C'3'                                                     
         MVC   PRJTTEXT,PPRNARRP+(2*L'PRJTTEXT)                                 
         LA    R3,PRJTOBJL(R3)     BUMP TO NEXT OBJECT POSITION                 
*                                                                               
BJHX     XIT1  REGS=(R3)                                                        
         DROP  R3,R6                                                            
         EJECT                                                                  
*                                                                               
*        BUILD JOB FOOTER TEXT OBJECTS                                          
*              AT ENTRY, R3=A(OBJECTS), R2 = A(JOB)                             
*              CALLED BY BUILDJB, ON EXIT, RETURN R3 TO CALLER                  
*                                                                               
BUILDJF  NTR1  ,                                                                
         LR    R6,R2                                                            
         MVI   ELCODE,SCMELQ       STANDARD COMMENT ELEMENT                     
         BAS   RE,GETEL                                                         
         BNE   BJFX                NO FOOTER COMMENTS                           
*                                                                               
         MVC   WORK,BLANKS         SPACE PAD TEXT AREA                          
         MVI   BYTE,1              SET SEQUENCE NUMBER TO 1                     
         MVI   NARRFLAG,C'Y'       SET NARRATION FLAG TO YES                    
         MVI   FIRST,C'Y'                                                       
         LA    R4,WORK             R4=A(TEXT OBJECT STRING)                     
*                                                                               
         USING SCMELD,R6                                                        
BJF10    CLI   SCMTYPE,SCMTSTND    TEST FOR NARRATION                           
         BE    BJF20               YES                                          
         CLI   SCMTYPE,C'M'                                                     
         BNE   BJF30                                                            
*                                                                               
BJF20    CLI   NARRFLAG,C'Y'       TEST FOR NARRATION IN PROGRESS               
         BE    BJF25                                                            
         BAS   RE,PUTJF            NO-ADD COMMENT STRING TO OBJ BUFFER          
         MVI   NARRFLAG,C'Y'       SET NARRATION FLAG                           
*                                                                               
BJF25    ZIC   R1,SCMLN                                                         
         SHI   R1,SCMLN1Q+1                                                     
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   WORK(0),SCMNARR     EXTRACT NARRATION TO WORK AREA               
         BAS   RE,PUTJF            ADD OBJECT TO BUFFER                         
         B     BJF60                                                            
*                                                                               
BJF30    LA    R1,L'SCMCODE        COMMENT CODE IS RIGHT JUSTIFIED              
         LA    RE,SCMCODE                                                       
         CLI   0(RE),C' '          TEST FOR FIRST SIGNIFICANT BYTE              
         BNE   BJF35               YES                                          
         LA    RE,1(RE)                                                         
         BCT   R1,*-12                                                          
         B     BJF60                                                            
*                                                                               
BJF35    MVI   NARRFLAG,C'N'       COMMENT CODE REFERENCE-NOT NARRATIVE         
         STH   R1,COMMLEN          SAVE COMMENT CODE LENGTH                     
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   COMMENT(0),0(RE)    EXTRACT ACTUAL CODE                          
*                                                                               
BJF40    LH    R1,COMMLEN          GET COMMENT CODE LENGTH                      
         LA    R1,5(R1)            ADD 5 FOR PREFIX, COMMA                      
         LR    RE,R4                                                            
         LA    R0,WORK             COMPUTE LENGTH OF STRING SO FAR              
         SR    RE,R0                                                            
         LA    RE,0(R1,RE)         ADD IN LENGTH OF ADDITION                    
         CHI   RE,L'PRJTTEXT       TEST IF IT FITS                              
         BNH   BJF45                                                            
         BAS   RE,PUTJF            NO-OUTPUT AN OBJECT NOW                      
*                                                                               
BJF45    CLI   FIRST,C'Y'          TEST IF FIRST FOR THIS LINE                  
         BE    *+12                YES-SKIP COMMA INSERTION                     
         MVI   0(R4),C','          INSERT A COMMA AFTER PREVIOUS ITEM           
         LA    R4,1(R4)            ADVANCE STRING POINTER                       
         MVI   FIRST,C'N'                                                       
*                                                                               
         MVC   0(4,R4),=C'B+E='    SET COMMENT REFERENCE PREFIX                 
         TM    SCMTYPE,SCMTPRBI+SCMTPRES                                        
         BO    BJF50                                                            
         MVC   0(3,R4),=C'BIL'     BILLING ONLY                                 
         TM    SCMTYPE,SCMTPRBI                                                 
         BO    BJF50                                                            
         MVC   0(3,R4),=C'EST'     ESTIMATE ONLY                                
*                                                                               
BJF50    LH    R1,COMMLEN          APPEND COMMENT CODE                          
         BCTR  R1,0                PRECEDING SPACES HAVE BEEN TRIMMED           
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   4(0,R4),COMMENT                                                  
         LA    R4,5(R1,R4)         ADVANCE STRING POINTER                       
*                                                                               
BJF60    BAS   RE,NEXTEL                                                        
         BE    BJF10                                                            
         CLI   NARRFLAG,C'N'                                                    
         BNE   BJFX                                                             
*                                                                               
         CLI   WORK,C' '           TEST FOR PENDING OUTPUT                      
         BE    *+8                                                              
         BAS   RE,PUTJF            YES-ADD OBJECT TO BUFFER                     
*                                                                               
BJFX     XIT1  REGS=(R3)                                                        
         SPACE 2                                                                
* SUB-ROUTINE TO PUT A JOB TEXT OBJECT INTO THE OBJECT BUFFER                   
* AT ENTRY, WORK=TEXT, BYTE=SEQUENCE NUMBER, R3=A(OBJECT BUFFER)                
*                                                                               
PUTJF    ST    RE,FULL                                                          
         MVC   0(2,R3),=Y(PRJTOBJL+4)                                           
         MVC   2(4,R3),=A(ITPRRTJC)                                             
*                                                                               
         LA    R3,6(R3)                                                         
         USING PRJTOBJD,R3                                                      
         MVC   PRJTOBJD(PRJTOBJL),BLANKS                                        
         MVI   PRJTTYPE,C'2'       FOOTER = 2                                   
         ZIC   RE,BYTE             GET SEQUENCE NUMBER                          
         STC   RE,PRJTSEQ                                                       
         OI    PRJTSEQ,X'F0'       MAKE IT NUMERIC                              
         MVC   PRJTTEXT,WORK                                                    
         LA    R3,PRJTOBJL(R3)                                                  
         LA    RE,1(RE)            INCREMENT SEQUENCE NUMBER                    
         STC   RE,BYTE                                                          
         MVC   WORK,BLANKS                                                      
         MVI   FIRST,C'Y'                                                       
         LA    R4,WORK             SET R4 AS TEXT POINTER                       
*                                                                               
         L     RE,FULL                                                          
         BR    RE                                                               
         DROP  R3,R6                                                            
         EJECT                                                                  
* THIS ROUTINE BUILDS THE SUPPLIER OBJECT BLOCK FROM THE SUPPLIER               
* RECORD PASSED.                                                                
*                                                                               
* PARAMETER 1 - A(SUPPLIER RECORD)                                              
* PARAMETER 2 - A(SUPPLIER OBJECT BLOCK)                                        
*                                                                               
BUILDSP  NTR1  ,                                                                
         LM    R2,R3,0(R1)         R2 = A(SUPPLIER RECORD)                      
*                                  R3 = A(SUPPLIER OBJECT BLOCK)                
*                                                                               
*                                  SET SUPPLIER OBJECT LENGTH/TYPE              
         MVC   0(2,R3),=Y(PRSPOBJL+4)                                           
         MVC   2(4,R3),=A(ITPRRTSP)                                             
*                                                                               
         USING PRSPOBJD,R3         BUMP TO OBJECT DATA AND PRECLEAR             
         LA    R3,6(R3)                                                         
         MVC   0(PRSPOBJL,R3),BLANKS                                            
*                                                                               
         USING ACTRECD,R2          EXTRACT RECORD KEY DATA                      
         MVC   PRSPACCT(2),ACTKUNT      UNIT/LEDGER                             
         MVC   PRSPACCT+2(12),ACTKACT   ACCOUNT CODE                            
         MVI   PRSPDEL,C'0'        IS DELETED (0/1)                             
         TM    ACTRSTAT,ACTSDELT                                                
         BZ    *+8                                                              
         MVI   PRSPDEL,C'1'                                                     
*                                                                               
         USING NAMELD,R6           EXTRACT NAME ELEMENT DATA                    
         LR    R6,R2                                                            
         MVI   ELCODE,NAMELQ                                                    
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         ZIC   RF,NAMLN            NAME                                         
         SH    RF,=Y(NAMLN1Q+1)                                                 
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   PRSPNAME(0),NAMEREC                                              
*                                                                               
         USING ADRELD,R6           EXTRACT ADDRESS ELEMENT DATA                 
         LR    R6,R2                                                            
         MVI   ELCODE,ADRELQ                                                    
         BAS   RE,GETEL                                                         
         BNE   BSP50                                                            
         MVC   PRSPADD1,ADRADD1    ADDRESS LINE 1                               
         CLI   ADRNUM,1                                                         
         BE    BSP50                                                            
         MVC   PRSPADD2,ADRADD2    ADDRESS LINE 2                               
         CLI   ADRNUM,2                                                         
         BE    BSP50                                                            
         MVC   PRSPADD3,ADRADD3    ADDRESS LINE 3                               
         CLI   ADRNUM,3                                                         
         BE    BSP50                                                            
         MVC   PRSPADD4,ADRADD4    ADDRESS LINE 4                               
*                                                                               
         USING RSTELD,R6                                                        
BSP50    LR    R6,R2                                                            
         MVI   ELCODE,RSTELQ       LOOK FOR STATUS ELEMENT                      
         BAS   RE,GETEL                                                         
         BNE   BSP90                                                            
         CLI   RSTLN,RSTLN1Q       MAKE SURE ITS MINIMUM LENGTH                 
         BL    BSP90                                                            
*                                                                               
         MVI   PRSPLOCK,C'0'                                                    
         TM    RSTSTAT1,RSTSACIL   TEST FOR LOCKED SUPPLIER                     
         BZ    *+8                                                              
         MVI   PRSPLOCK,C'1'                                                    
*                                                                               
BSP90    LA    R3,PRSPOBJL(R3)     BUMP R3 TO NEXT OBJECT                       
*                                                                               
BSPX     XC    0(2,R3),0(R3)       SET END OF OBJECT BLOCK                      
         B     YES                                                              
         DROP  R2,R3,R6                                                         
         EJECT                                                                  
* THIS ROUTINE BUILDS THE AGENCY OBJECT BLOCK FROM THE COMPANY                  
* RECORD PASSED.                                                                
*                                                                               
* PARAMETER 1 - A(COMPANY RECORD)                                               
* PARAMETER 2 - A(AGENCY OBJECT BLOCK)                                          
*                                                                               
BUILDAG  NTR1  ,                                                                
         LM    R2,R3,0(R1)         R2 = A(COMPANY RECORD)                       
*                                  R3 = A(AGENCY OBJECT BLOCK)                  
*                                                                               
*                                  SET AGENCY OBJECT LENGTH/TYPE                
         MVC   0(2,R3),=Y(PRAGOBJL+4)                                           
         MVC   2(4,R3),=A(ITPRRTAG)                                             
*                                                                               
         USING PRAGOBJD,R3         BUMP TO OBJECT DATA AND PRECLEAR             
         LA    R3,6(R3)                                                         
         MVC   0(PRAGOBJL,R3),BLANKS                                            
*                                                                               
         USING ACTRECD,R2          EXTRACT RECORD KEY DATA                      
         GOTO1 CHEXOUT,DMCB,ACTKCPY,PRAGCODE,1,MIXED                            
*                                                                               
         USING NAMELD,R6           EXTRACT NAME ELEMENT DATA                    
         LR    R6,R2                                                            
         MVI   ELCODE,NAMELQ                                                    
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         ZIC   RF,NAMLN            NAME                                         
         SH    RF,=Y(NAMLN1Q+1)                                                 
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   PRAGNAME(0),NAMEREC                                              
*                                                                               
         USING ADRELD,R6           EXTRACT ADDRESS ELEMENT DATA                 
         LR    R6,R2                                                            
         MVI   ELCODE,ADRELQ                                                    
         BAS   RE,GETEL                                                         
         BNE   BAG30                                                            
         MVC   PRAGADD1,ADRADD1    ADDRESS LINE 1                               
         CLI   ADRNUM,1                                                         
         BE    BAG30                                                            
         MVC   PRAGADD2,ADRADD2    ADDRESS LINE 2                               
         CLI   ADRNUM,2                                                         
         BE    BAG30                                                            
         MVC   PRAGADD3,ADRADD3    ADDRESS LINE 3                               
         CLI   ADRNUM,3                                                         
         BE    BAG30                                                            
         MVC   PRAGADD4,ADRADD4    ADDRESS LINE 4                               
*                                                                               
*                                  CLIENT/RPODUCT/JOB LENGTHS                   
BAG30    GOTO1 CHEXOUT,DMCB,ACPRLCLI,PRAGLCLI,1,MIXED                           
         GOTO1 CHEXOUT,DMCB,ACPRLPRO,PRAGLPRO,1,MIXED                           
         GOTO1 CHEXOUT,DMCB,ACPRLJOB,PRAGLJOB,1,MIXED                           
*                                                                               
         USING CPYELD,R6           EXTRACT COMPANY ELEMENT DATA                 
         LR    R6,R2                                                            
         MVI   ELCODE,CPYELQ                                                    
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         MVC   PRAGSUPP,CPYSUPP                                                 
         MVC   PRAGALPH,CPYALPHA                                                
         MVI   BYTE,0                                                           
         TM    CPYSTAT4,CPYSOFF2   TEST NEW OFFICES AGENCY                      
         BZ    *+8                 NO                                           
         MVI   BYTE,1              YES                                          
         GOTO1 CHEXOUT,DMCB,BYTE,PRAGNEWO,1,MIXED                               
         MVC   PRAGCUR,CPYCURR     PRIMARY CURRENCY                             
*&&US                                                                           
         CLI   CPYLN,CPYLN2Q                                                    
         BL    BAG40                                                            
         MVI   BYTE,0                                                           
         TM    CPYSTAT5,CPYSAEOE   TEST USING EXPENSE ANALYSIS                  
         BZ    *+8                 NO                                           
         MVI   BYTE,1              YES                                          
         GOTO1 CHEXOUT,DMCB,BYTE,HALF,1,MIXED                                   
         MVC   PRAGANXP,HALF+1                                                  
*&&                                                                             
         CLI   CPYLN,CPYLN3Q                                                    
         BL    BAG40                                                            
*&&UK*&& MVC   PRAGCURS,CPYCURRS   SECONDARY CURRENCY                           
         TM    CPYSTATA,CPYSPOLU+CPYSPOFU                                       
         BZ    BAG40                                                            
         MVC   PRAGXDAT,=C'OL'                                                  
         TM    CPYSTATA,CPYSPOLU                                                
         BO    BAG40                                                            
         MVC   PRAGXDAT,=C'OF'                                                  
*                                                                               
BAG40    LA    R3,PRAGOBJL(R3)     BUMP R3 TO NEXT OBJECT                       
*                                                                               
BAGX     XC    0(2,R3),0(R3)       SET END OF OBJECT BLOCK                      
         B     YES                                                              
         DROP  R2,R3,R6                                                         
         EJECT                                                                  
* THIS ROUTINE BUILDS THE OFFICE LIST OBJECT BLOCK FROM THE OFFICE              
* LIST RECORD PASSED.                                                           
*                                                                               
* PARAMETER 1 - A(OFFICE LIST RECORD)                                           
* PARAMETER 2 - A(OFFICE LIST OBJECT BLOCK)                                     
*                                                                               
BUILDOL  NTR1  ,                                                                
         LM    R2,R3,0(R1)         R2 = A(OFFICE LIST RECORD)                   
*                                  R3 = A(OFFICE LIST OBJECT BLOCK)             
*                                                                               
*                                  SET OFFICE LIST OBJECT LENGTH/TYPE           
         MVC   0(2,R3),=Y(PROHOBJL+4)                                           
         MVC   2(4,R3),=A(ITPRRTOH)                                             
*                                                                               
         USING PROHOBJD,R3         BUMP TO OBJECT DATA AND PRECLEAR             
         LA    R3,6(R3)                                                         
         MVC   0(PROHOBJL,R3),BLANKS                                            
*                                                                               
         USING OFFRECD,R2          EXTRACT RECORD KEY DATA                      
         MVC   PROHCODE,OFFKOFF    CODE                                         
*                                                                               
         USING NAMELD,R6           EXTRACT NAME ELEMENT DATA                    
         LR    R6,R2                                                            
         MVI   ELCODE,NAMELQ                                                    
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         ZIC   RF,NAMLN            NAME                                         
         SH    RF,=Y(NAMLN1Q+1)                                                 
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   PROHNAME(0),NAMEREC                                              
*                                                                               
         LA    R3,PROHOBJL(R3)     BUMP R3 TO NEXT OBJECT                       
*                                                                               
BOL10    MVI   ELCODE,OFLELQ       FIND OFFICE LIST ELEMENT                     
         LR    R6,R2                                                            
         BAS   RE,GETEL                                                         
         BNE   NO                  DIDN'T FIND IT                               
         USING OFLELD,R6                                                        
*                                                                               
         ZIC   R5,OFLLN                                                         
         SHI   R5,OFLLN1Q                                                       
         SRA   R5,1                FIND NUMBER OF OFFICES IN LIST               
         LTR   R5,R5                                                            
         BZ    NO                                                               
         LA    R4,OFLNTRY          R4=A(OFFICE CODE)                            
*                                                                               
BOL20    MVC   0(2,R3),=Y(PROLOBJL+4)                                           
         MVC   2(4,R3),=A(ITPRRTOL)                                             
         LA    R3,6(R3)                                                         
         USING PROLOBJD,R3                                                      
         MVC   0(PROLOBJL,R3),BLANKS                                            
*                                                                               
         MVC   PROLCODE,OFFKOFF    OFFICE LIST CODE                             
         MVC   PROLOFFC,0(R4)      OFFICE CODE                                  
         LA    R4,L'OFLNTRY(R4)                                                 
         LA    R3,PROLOBJL(R3)                                                  
         BCT   R5,BOL20                                                         
*                                                                               
BOLX     XC    0(2,R3),0(R3)       SET END OF OBJECT BLOCK                      
         B     YES                                                              
         DROP  R2,R3,R6                                                         
         EJECT                                                                  
* THIS ROUTINE BUILDS THE ORDER OBJECT BLOCK FROM THE ORDER RECORD              
* PASSED.                                                                       
*                                                                               
* PARAMETER 1 - A(ORDER RECORD)                                                 
* PARAMETER 2 - A(ORDER OBJECT BLOCK)                                           
*                                                                               
BUILDOD  NTR1  ,                                                                
         LM    R2,R3,0(R1)         R2 = A(ORDER RECORD)                         
*                                  R3 = A(ORDER OBJECT BLOCK)                   
*                                                                               
*                                  SET ORDER HEADER OBJECT LENGTH/TYPE          
         MVC   0(2,R3),=Y(PROUOBJL+4)                                           
         MVC   2(4,R3),=A(ITPROUPL)                                             
*                                                                               
         USING PROUOBJD,R3         BUMP TO OBJECT DATA AND PRECLEAR             
         LA    R3,6(R3)                                                         
         MVC   0(PROUOBJL,R3),BLANKS                                            
*                                                                               
         USING ORDRECD,R2          EXTRACT RECORD KEY DATA                      
         MVC   PROUORDN,ORDKORD    ORDER NUMBER                                 
*                                                                               
         USING ORDELD,R6           EXTRACT ORDER ELEMENT DATA                   
         LR    R6,R2                                                            
         MVI   ELCODE,ORDELQ                                                    
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
*                                                                               
         LA    RE,ORDACCA          CLIENT CODE                                  
         ZIC   RF,ACPRLCLI                                                      
         LR    R1,RF                                                            
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   PROUCLI(0),0(RE)                                                 
*                                                                               
         LA    RE,0(RE,R1)         PRODUCT CODE                                 
         ZIC   RF,ACPRLPRO                                                      
         LR    R1,RF                                                            
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   PROUPRO(0),0(RE)                                                 
*                                                                               
         LA    RE,0(RE,R1)         JOB CODE                                     
         ZIC   RF,ACPRLJOB                                                      
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   PROUJOB(0),0(RE)                                                 
         MVC   PROUSUPP,ORDSUPU    SUPPLIER ACCOUNT CODE                        
*                                                                               
         GOTO1 CDATCON,DMCB,(1,ORDDATE),(X'20',PROUORDT) ORDER DATE             
*&&US                                                                           
         GOTO1 CDATCON,DMCB,(1,ORDDDTE),(X'20',PROUORDU) DUE DATE               
         MVC   PROUATTN(L'ORDATTN),ORDATTN             ATTN COMMENT             
*&&                                                                             
*&&UK                                                                           
         MVC   PROUREQD,ORDREQD     REQUIRED BY                                 
         TM    ACPRSTA7,CPYSSCNV    TEST CURRENCY CONVERTED AGY                 
         BZ    BOD20                NO                                          
*                                                                               
         MVC   PROUCUR,ACPRCUR      CURRENCY=PRIMARY CURRENCY                   
         TM    ORDRSTAT,ORDSSECC    TEST SECONDARY CURRENCY                     
         BZ    *+10                 NO                                          
         MVC   PROUCUR,ACPRCURS     YES-USE SECONDARY CURRENCY                  
*&&                                                                             
BOD20    MVC   PROUAUTH(L'ORDAUTH),ORDAUTH             AUTHORIZED BY            
         XC    HALF,HALF                               APPROVAL STAT            
         MVI   HALF+1,PROUSCLO                         SET TO CLOSED            
         TM    ORDRSTAT,ORDSFMCH                                                
         BO    BOD40                                                            
         MVI   HALF+1,PROUSPAR                                                  
         TM    ORDSTAT,ORDSPART    TEST PARTIAL PAY                             
         BO    *+8                                                              
         MVI   HALF+1,PROUSOPN     NO-MUST BE OPEN                              
*                                                                               
BOD40    GOTO1 CHEXOUT,DMCB,HALF,PROUSTAT,2,MIXED                               
*                                                                               
         LA    R3,PROUOBJL(R3)     BUMP R3 TO NEXT OBJECT                       
*                                                                               
         MVI   ELCODE,OAMELQ       FIND ORDER AMOUNT ELEMENT                    
         LR    R6,R2                                                            
         BAS   RE,GETEL                                                         
         USING OAMELD,R6                                                        
*                                                                               
BOD50    BNE   BODX                WHILE NOT END OF ORDER AMNT ELEMS            
*                                                                               
*                                  SET ORDER LINE OBJECT TYPE/LEN               
         MVC   0(2,R3),=Y(PRULOBJL+4)                                           
         MVC   2(4,R3),=A(ITPROLIN)                                             
*                                                                               
         LA    R3,6(R3)            BUMP TO OBJECT DATA AND PRECLEAR             
         USING PRULOBJD,R3                                                      
         MVC   0(PRULOBJL,R3),BLANKS                                            
*                                                                               
         MVC   PRULWC,OAMWORK      WORK CODE                                    
         MVI   PRULFLAG,C'N'       COMMISSION FLAG                              
         TM    OAMSTAT,OAMSNOCM                                                 
         BO    *+8                                                              
         MVI   PRULFLAG,C'Y'                                                    
         ZAP   DUB,OAMAMNT         AMOUNT                                       
*                                                                               
*&&UK                                                                           
         TM    ORDRSTAT,ORDSSECC   TEST SECONDARY CURRENCY                      
         BZ    BOD54               NO                                           
*                                                                               
         ZAP   DUB,=P'0'                                                        
         MVI   ELCODE,OCAELQ                                                    
         LR    R6,R2                                                            
         BAS   RE,GETEL                                                         
         USING OCAELD,R6                                                        
         BNE   BOD54                                                            
         ZIC   R1,OCANUM                                                        
         LA    R4,OCANTRY                                                       
         CLI   OCANTYPE-OCANTRY(R4),QOAMAMNT                                    
         BE    BOD52                                                            
         LA    R4,OCANTRYL(R4)                                                  
         BCT   R1,*-12                                                          
         B     BOD54                                                            
*                                                                               
BOD52    ZAP   DUB,OCANCASH-OCANTRY(L'OCANCASH,R4)                              
*                                                                               
*&&                                                                             
BOD54    CVB   R1,DUB                                                           
         ST    R1,FULL                                                          
         GOTO1 CHEXOUT,DMCB,FULL,PRULAMNT,4,MIXED                               
         LA    R3,PRULOBJL(R3)     BUMP R3 TO NEXT OBJECT                       
*&&UK                                                                           
         TM    ORDRSTAT,ORDSSECC   ONLY ONE ELEMENT IN GERMANY                  
         BO    BODX                                                             
*&&                                                                             
*                                                                               
         BAS   RE,NEXTEL           BUMP TO NEXT ELEMENT AND LOOP BACK           
         B     BOD50                                                            
*                                                                               
BODX     XC    0(2,R3),0(R3)       SET END OF OBJECT BLOCK                      
         B     YES                                                              
         DROP  R2,R3,R6                                                         
         EJECT                                                                  
* THIS ROUTINE BUILDS THE ORDER STATUS OBJECT BLOCK FROM THE ORDER              
* RECORD PASSED.                                                                
*                                                                               
* PARAMETER 1 - A(ORDER RECORD)                                                 
* PARAMETER 2 - A(ORDER STATUS OBJECT BLOCK)                                    
*                                                                               
BUILDOS  NTR1  ,                                                                
         LM    R2,R3,0(R1)         R2 = A(ORDER RECORD)                         
*                                  R3 = A(ORDER STATUS OBJECT BLOCK)            
*                                                                               
*                                  SET ORDER STATUS OBJECT LENGTH/TYPE          
         MVC   0(2,R3),=Y(PROSOBJL+4)                                           
         MVC   2(4,R3),=A(ITPROSRT)                                             
*                                                                               
         USING PROSOBJD,R3         BUMP TO OBJECT DATA AND PRECLEAR             
         LA    R3,6(R3)                                                         
         MVC   0(PROSOBJL,R3),BLANKS                                            
*                                                                               
         USING ORDRECD,R2          EXTRACT RECORD KEY DATA                      
         MVC   PROSORDN,ORDKORD    ORDER NUMBER                                 
         MVI   PROSSTAT,C'O'       ORDER STATUS                                 
         TM    ORDRSTAT,ORDSFMCH                                                
         BZ    *+8                                                              
         MVI   PROSSTAT,C'C'                                                    
*                                                                               
         MVC   PROSSTA2,PROSSTAT   FOR PRESTO 2.00 OR >                         
         CLI   PROSSTAT,C'C'       STATUS IS O, P, OR C                         
         BE    BOS50                                                            
*                                                                               
         USING ORDELD,R6                                                        
         LR    R6,R2                                                            
         MVI   ELCODE,ORDELQ                                                    
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
*                                                                               
         CLI   ORDLN,ORDSTAT-ORDELD  TEST ELEMENT BIG ENOUGH                    
         BNH   BOS50                                                            
         TM    ORDSTAT,ORDSPART    TEST FOR PARTIAL PAY                         
         BZ    *+8                                                              
         MVI   PROSSTA2,C'P'                                                    
*                                                                               
BOS50    LA    R3,PROSOBJL(R3)     BUMP R3 TO PAST OBJECT                       
*                                                                               
BOSX     XC    0(2,R3),0(R3)       SET END OF OBJECT BLOCK                      
         B     YES                                                              
         DROP  R2,R3,R6                                                         
         EJECT                                                                  
* THIS ROUTINE BUILDS THE PRICE LIST OBJECT BLOCK FROM THE PRICE LIST           
* RECORD PASSED.                                                                
*                                                                               
* PARAMETER 1 - A(PRICE LIST RECORD)                                            
* PARAMETER 2 - A(PRICE LIST OBJECT BLOCK)                                      
*                                                                               
* NOTE - OFFICE CODE IS NOT PART OF THE KEY - ITS A SECURITY DEVICE             
*                                                                               
BUILDPL  NTR1  ,                                                                
         LM    R2,R3,0(R1)         R2 = A(PRICE LIST RECORD)                    
*                                  R3 = A(PRICE LIST OBJECT BLOCK)              
*                                                                               
*                                  SET PRICE LIST OBJECT LENGTH/TYPE            
         MVC   0(2,R3),=Y(PRPLOBJL+4)                                           
         MVC   2(4,R3),=A(ITPRPLRT)                                             
*                                                                               
         USING PRPLOBJD,R3         BUMP TO OBJECT DATA AND PRECLEAR             
         LA    R3,6(R3)                                                         
         MVC   0(PRPLOBJL,R3),BLANKS                                            
*                                                                               
         USING PRLRECD,R2          EXTRACT RECORD KEY DATA                      
         MVC   PRPLCODE,PRLKNUM    PRICE LIST NUMBER                            
*                                                                               
         USING NAMELD,R6           EXTRACT NAME ELEMENT DATA                    
         LR    R6,R2                                                            
         MVI   ELCODE,NAMELQ                                                    
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         ZIC   RF,NAMLN            NAME                                         
         SH    RF,=Y(NAMLN1Q+1)                                                 
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   PRPLNAME(0),NAMEREC                                              
*                                                                               
* TWO PRICES FOR EURO ARE HELD ON A SCIEL                                       
*                                                                               
BPL10    MVI   BYTE,SCITCOKE                                                    
         GOTO1 CHELLO,DMCB,(C'G',ACCMST),('SCIELQ',(R2)),(1,BYTE),0             
         L     R6,12(R1)                                                        
         USING SCIELD,R6                                                        
         CLI   12(R1),0            TEST IF ELEMENT FOUND                        
         BNE   BPL20                                                            
*                                                                               
         ZAP   DUB,SCIAMNT         GET FIRST PRICE                              
         CVB   RF,DUB                                                           
         ST    RF,FULL                                                          
         GOTO1 CHEXOUT,DMCB,FULL,PRPLAMNT,4,MIXED                               
         ZAP   DUB,SCIADMN         GET SECOND PRICE                             
         CVB   RF,DUB                                                           
         ST    RF,FULL                                                          
         GOTO1 CHEXOUT,DMCB,FULL,PRPLAMN2,4,MIXED                               
         B     BPL50                                                            
*                                                                               
         USING FFNELD,R6           EXTRACT FREE FORM NUMBER DATA                
BPL20    LR    R6,R2                                                            
         MVI   ELCODE,FFNELQ                                                    
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         PACK  DUB,FFNUMBER        AMOUNT                                       
         CVB   R1,DUB                                                           
         ST    R1,FULL                                                          
         GOTO1 CHEXOUT,DMCB,FULL,PRPLAMNT,4,MIXED                               
         XC    FULL,FULL           SECOND PRICE IS ZERO                         
         GOTO1 CHEXOUT,DMCB,FULL,PRPLAMN2,4,MIXED                               
*                                                                               
         USING XNMELD,R6           GET EXTRA NAME ELEMENT                       
BPL50    LR    R6,R2                                                            
         MVI   ELCODE,XNMELQ                                                    
         BAS   RE,GETEL                                                         
         BNE   BPL90                                                            
*                                                                               
         ZIC   RF,XNMSUBL          FOREIGN NAME                                 
         BCTR  RF,0                LENGTH VALUE INCLUDES LENGTH BYTE            
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   PRPLFNAM(0),XNMSUBN                                              
*                                                                               
BPL90    LA    R3,PRPLOBJL(R3)     BUMP R3 TO PAST OBJECT                       
*                                                                               
BPLX     XC    0(2,R3),0(R3)       SET END OF OBJECT BLOCK                      
         B     YES                                                              
         DROP  R2,R3,R6                                                         
         EJECT                                                                  
* THIS ROUTINE BUILDS THE TAX RULE AND TAX TYPE OBJECTS FROM THE                
* TAX RECORD PASSED.                                                            
*                                                                               
* PARAMETER 1 - A(TAX RECORD)                                                   
* PARAMETER 2 - A(TAX RULE/TYPE OBJECT BLOCK)                                   
*                                                                               
BUILDTX  NTR1  ,                                                                
         LM    R2,R3,0(R1)         R2 = A(TAX RECORD)                           
*                                  R3 = A(TAX OBJECT BLOCK)                     
*                                                                               
*                                  SET TAX RULE OBJECT LENGTH/TYPE              
         MVC   0(2,R3),=Y(PRTROBJL+4)                                           
         MVC   2(4,R3),=A(ITPRRTTR)                                             
*                                                                               
         USING PRTROBJD,R3         BUMP TO OBJECT DATA AND PRECLEAR             
         LA    R3,6(R3)                                                         
         MVC   0(PRTROBJL,R3),BLANKS                                            
*                                                                               
         USING TAXRECD,R2          EXTRACT RECORD KEY DATA                      
*&&US                                                                           
         OC    TAXKPRV,TAXKPRV     DON'T NEED PROVINCIAL RECORDS                
         BNZ   NO                                                               
*&&                                                                             
         CLC   TAXKOFF,EFFS        TEST FOR COMPANY LEVEL RECORD                
         BE    *+10                YES                                          
         MVC   PRTROFF,TAXKOFF     NO-RETURN THE OFFICE CODE                    
*                                                                               
         MVC   THREE,TAXKDATE      GET THE START DATE                           
         XC    THREE,EFFS          REVERSE THE COMPLEMENT                       
         GOTO1 CDATCON,DMCB,(1,THREE),(X'20',PRTRSTDT)                          
*                                                                               
         MVI   ELCODE,FFTELQ       GET THE REGISTRATION NUMBER                  
         LR    R6,R2                                                            
         BAS   RE,GETEL                                                         
         BNE   BTX20                                                            
*                                                                               
         USING FFTELD,R6                                                        
         ZIC   R1,FFTDLEN          GET STRING LENGTH                            
         LA    R0,L'PRTRREG        L'REGISTRATION NUMBER                        
         CR    R1,R0                                                            
         BNH   *+6                                                              
         LR    R1,R0               NO MORE THAN MAX LEN                         
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     BTX20                                                            
         MVC   PRTRREG(0),FFTDATA                                               
*                                                                               
BTX20    MVI   PRTRDEL,C'0'                                                     
         TM    TAXRSTA,TAXSDELT    TEST FOR DELETED RULE                        
         BNO   BTX30               NO                                           
*                                                                               
         MVI   PRTRDEL,C'1'        YES                                          
         LA    R3,PRTROBJL(R3)     EXIT NOW                                     
         B     BTXX                                                             
*                                                                               
BTX30    LA    R3,PRTROBJL(R3)                                                  
         LR    R6,R2                                                            
         MVI   ELCODE,TAXOELQ      LOOK FOR OUTPUT TAX ELEMENTS                 
         BAS   RE,GETEL                                                         
         BE    BTX50                                                            
*                                                                               
         MVI   PRTRDEL,C'1'        IF THERE ARE NO OUTPUT TAX ELS               
         LA    R3,PRTROBJL(R3)     SEND A DELETE DOWN TO PRESTO                 
         B     BTXX                                                             
*                                                                               
         USING TAXELD,R6                                                        
BTX50    MVC   0(2,R3),=Y(PRTTOBJL+4)                                           
         MVC   2(4,R3),=A(ITPRRTTT)                                             
         LA    R3,6(R3)                                                         
         USING PRTTOBJD,R3                                                      
         MVC   0(PRTTOBJL,R3),BLANKS                                            
         MVC   PRTTCODE,TAXCODE    GET TAX TYPE CODE                            
         MVC   PRTTNAME,TAXTYPE    GET TAX TYPE NAME                            
         SR    R1,R1                                                            
         ICM   R1,3,TAXRATE        GET TAX RATE                                 
         ST    R1,FULL                                                          
         GOTO1 CHEXOUT,DMCB,FULL,PRTTRATE,4,MIXED                               
*                                                                               
BTX60    LA    R3,PRTTOBJL(R3)                                                  
         BAS   RE,NEXTEL                                                        
         BE    BTX50                                                            
*                                                                               
BTXX     XC    0(2,R3),0(R3)                                                    
         B     YES                                                              
         DROP  R2,R3,R6                                                         
         EJECT                                                                  
* THIS ROUTINE BUILDS THE OPTION KEY AND OPTION VALUE OBJECTS FROM              
* THE PRODUCTION OPTION RECORD PASSED                                           
*                                                                               
* PARAMETER 1 - A(OPTION RECORD)                                                
* PARAMETER 2 - A(OPTION KEY/OPTION VALUE OBJECT BLOCK)                         
*                                                                               
BUILDOP  NTR1  ,                                                                
         LM    R2,R3,0(R1)         R2 = A(OPTION RECORD)                        
*                                  R3 = A(OPTION OBJECT BLOCK)                  
*                                                                               
*                                  SET OPTION KEY OBJ LENGTH/TYPE               
         MVC   0(2,R3),=Y(PROKOBJL+4)                                           
         MVC   2(4,R3),=A(ITPRRTOK)                                             
*                                                                               
         USING PROKOBJD,R3         BUMP TO OBJECT DATA AND PRECLEAR             
         LA    R3,6(R3)                                                         
         MVC   0(PROKOBJL,R3),BLANKS                                            
*                                                                               
         USING POPRECD,R2          EXTRACT RECORD KEY DATA                      
         MVC   PROKOGR,POPKOFG     OFFICE GROUP CODE                            
         OC    PROKOGR,BLANKS                                                   
         MVC   PROKOFF,POPKOFC     OFFICE CODE                                  
         OC    PROKOFF,BLANKS      OFFICE CODE                                  
         MVC   PROKCLI,POPKCLI     CLIENT CODE                                  
         OC    PROKCLI,BLANKS      CLIENT CODE                                  
         MVC   PROKPRO,POPKPRO     PRODUCT CODE                                 
         OC    PROKPRO,BLANKS      PRODUCT CODE                                 
         MVC   PROKJOB,POPKJOB     JOB CODE                                     
         OC    PROKJOB,BLANKS      JOB CODE                                     
*                                                                               
         MVC   PROKMGR,POPKMGR     MEDIA GROUP CODE                             
         CLI   PROKMGR,X'FF'                                                    
         BNE   *+8                                                              
         MVI   PROKMGR,C' '                                                     
         OI    PROKMGR,C' '                                                     
         MVC   PROKMED,POPKMED                                                  
         OI    PROKMED,C' '                                                     
*                                                                               
         MVC   PROKWGR,POPKWGR     WORK GROUP                                   
         CLI   PROKWGR,X'FF'                                                    
         BNE   *+8                                                              
         MVI   PROKWGR,C' '                                                     
         OI    PROKWGR,C' '                                                     
         MVC   PROKWC,POPKWRK                                                   
         OC    PROKWC,BLANKS                                                    
*                                                                               
         MVI   PROKDEL,C'0'                                                     
         TM    POPRSTA,ACTSDELT                                                 
         BNO   BOP20                                                            
*                                                                               
         CLI   ACPRLOAD,C'Y'       TEST IN LOAD MODE                            
         BE    NO                  YES-SKIP DELETED RECORD                      
         MVI   PROKDEL,C'1'                                                     
         LA    R3,PROKOBJL(R3)                                                  
         B     BOPX                                                             
*                                                                               
BOP20    LR    R6,R2                                                            
         LA    R3,PROKOBJL(R3)                                                  
         MVI   ANYOPT,C'N'         SET FLAG FOR ANY OPT VALS TO NO              
         MVI   ELCODE,OPDELQ                                                    
         BAS   RE,GETEL                                                         
*                                                                               
         USING OPDELD,R6                                                        
BOP30    BNE   BOP99                                                            
         LA    R0,NOPTVTAB         R0=LOOP COUNTER                              
         LA    R4,OPTVTAB          R4=A(OPTION TABLE)                           
         USING OPTVTABD,R4                                                      
         CLC   OPDNUM,OPTVNUM      MATCH ON OPTION NUMBER                       
         BE    BOP32                                                            
         LA    R4,L'OPTVTAB(R4)                                                 
         BCT   R0,*-14                                                          
         B     BOP90                                                            
*                                                                               
BOP32    MVC   0(2,R3),=Y(PROVOBJL+4)                                           
         MVC   2(4,R3),=A(ITPRRTOV)                                             
         MVI   ANYOPT,C'Y'         SET FLAG FOR 1 OPT VALUE                     
*                                                                               
         USING PROVOBJD,R3         BUMP TO OBJECT DATA AND PRECLEAR             
         LA    R3,6(R3)                                                         
         MVC   0(PROVOBJL,R3),BLANKS                                            
         ZIC   R1,OPTVPNUM         GET PRESTO OPTION NUMBER                     
         STH   R1,HALF                                                          
         GOTO1 CHEXOUT,DMCB,HALF,PROVNUM,2,MIXED                                
         ICM   RF,15,OPTVARTN      GET A(ROUTINE)                               
         A     RF,RELO                                                          
         B     0(RF)               JUMP TO ROUTINE                              
*                                                                               
BOP40    EDIT  (P4,OPDDATA),(7,PROVDATA),ALIGN=LEFT,ZERO=NOBLANK                
         LA    R3,PROVOBJL(R3)                                                  
         B     BOP90                                                            
*                                                                               
BOP42    ZIC   R1,OPTVMAXL         GET MAXIMUM STRING LENGTH                    
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   PROVDATA(0),OPDDATA CHARACTER STRING                             
         LA    R3,PROVOBJL(R3)                                                  
         B     BOP90                                                            
*                                                                               
BOP44    MVC   PROVDATA(1),OPDDATA 1 BYTE OPTION VALUE                          
         LA    R3,PROVOBJL(R3)                                                  
         B     BOP90                                                            
*                                                                               
BOP46    MVC   PROVDATA(1),OPDDATA YES/NO OPTIONS                               
         LA    R3,PROVOBJL(R3)                                                  
         B     BOP90                                                            
*                                                                               
BOP48    ZIC   R2,OPDDATA          NUMBER OF P ESTIMATES TO ADD                 
         EDIT  (R2),(3,PROVDATA),ALIGN=LEFT,ZERO=NOBLANK                        
         LA    R3,PROVOBJL(R3)                                                  
         B     BOP90                                                            
*                                                                               
BOP90    BAS   RE,NEXTEL                                                        
         B     BOP30                                                            
*                                                                               
BOP99    CLI   ACPRLOAD,C'Y'       TEST IN LOAD MODE                            
         BNE   BOPX                NO                                           
         CLI   ANYOPT,C'Y'         TEST ANY OPTION VALUES FOUND                 
         BNE   NO                  NO-SKIP THIS RECORD                          
*                                                                               
BOPX     XC    0(2,R3),0(R3)                                                    
         B     YES                                                              
         DROP  R2,R3,R4,R6                                                      
         SPACE 2                                                                
* TABLE OF OPTION VALUES OF INTEREST TO PRESTO                                  
* BYTE 0  = PRODUCTION OPTION NUMBER                                            
* BYTE 1  = PRESTO OPTION NUMBER                                                
* BYTE 2 =  MAXIMUM STRING LENGTH                                               
* BYTES 3-6 = A(PROCESSING ROUTINE)                                             
*                                                                               
         DS    0F                                                               
OPTVTAB  DS    0XL(OPTVTABL)                                                    
         DC    AL1(OPNCOM),AL1(PROPCOM),AL1(0),AL4(BOP40)                       
         DC    AL1(OPNSCH),AL1(PROPSCH),AL1(L'SCHKCODE),AL4(BOP42)              
*&&US*&& DC    AL1(OPNGST),AL1(PROPTAX),AL1(0),AL4(BOP44)                       
*&&UK*&& DC    AL1(OPNTAX),AL1(PROPTAX),AL1(0),AL4(BOP44)                       
         DC    AL1(OPNEEDAE),AL1(PROPNAE),AL1(0),AL4(BOP46)                     
         DC    AL1(OPNOVPER),AL1(PROPMPE),AL1(0),AL4(BOP40)                     
         DC    AL1(OPNADDP),AL1(PROPADDP),AL1(0),AL4(BOP48)                     
         DC    AL1(OPNADDR),AL1(PROPADDR),AL1(0),AL4(BOP46)                     
*&&US*&& DC    AL1(OPNEPER),AL1(PROPEPER),AL1(0),AL4(BOP40)                     
*&&UK*&& DC    AL1(OPNBLCUR),AL1(PROPBCUR),AL1(3),AL4(BOP42)                    
         DC    AL1(OPNEAPP),AL1(PROPEAPP),AL1(0),AL4(BOP44)                     
*&&UK*&& DC    AL1(OPNPROV),AL1(PROPPROV),AL1(0),AL4(BOP40)                     
         DC    AL1(OPNORDER),AL1(PROPORDR),AL1(0),AL4(BOP46)                    
NOPTVTAB EQU   (*-OPTVTAB)/L'OPTVTAB                                            
         EJECT                                                                  
*                                                                               
YES      SR    R2,R2                                                            
NO       LTR   R2,R2                                                            
XIT      XIT1                                                                   
*                                                                               
         GETEL (R6),DATADISP,ELCODE                                             
         EJECT                                                                  
*                                                                               
*        CONSTANTS AND EQUATES                                                  
*                                                                               
DATADISP DC    Y(ACCRFST-ACCRECD)                                               
EFFS     DC    8X'FF'                                                           
ACCMST   DC    CL8'ACCMST'                                                      
MIXED    DC    C'MIX'                                                           
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
* THIS ROUTINE BUILDS THE USER FIELD KEY AND USER FIELD OBJECTS                 
* FROM THE USER FIELD SELECTION RECORD PASSED                                   
*                                                                               
* PARAMETER 1 - A(USER FIELD RECORD)                                            
* PARAMETER 2 - A(USER KEY/USER FIELD OBJECT BLOCK)                             
*                                                                               
BUILDUS  NTR1  BASE=*                                                           
         B     *+12                                                             
         DC    CL8'*BUILDUS'                                                    
         LM    R2,R3,0(R1)         R2 = A(USER FIELD RECORD)                    
*                                  R3 = A(USER FIELD OBJECT BLOCK)              
*                                                                               
*                                  SET USER FIELD OBJ LENGTH/TYPE               
         MVC   0(2,R3),=Y(PRUKOBJL+4)                                           
         MVC   2(4,R3),=A(ITPRRTUK)                                             
*                                                                               
         USING PRUKOBJD,R3         BUMP TO OBJECT DATA AND PRECLEAR             
         LA    R3,6(R3)                                                         
         MVC   0(PRUKOBJL,R3),BLANKS                                            
*                                                                               
         USING UFSRECD,R2          EXTRACT RECORD KEY DATA                      
         MVC   PRUKOGR,UFSKOFG     OFFICE GROUP                                 
         OC    PRUKOGR,BLANKS                                                   
         MVC   PRUKOFF,UFSKOFC     OFFICE                                       
         OC    PRUKOFF,BLANKS                                                   
         MVC   PRUKCLI,UFSKCLI     CLIENT                                       
         OC    PRUKCLI,BLANKS                                                   
         MVC   PRUKPRO,UFSKPRO     PRODUCT                                      
         OC    PRUKPRO,BLANKS                                                   
         MVC   PRUKMGR,UFSKMGR     MEDIA GROUP                                  
         OC    PRUKMGR,BLANKS                                                   
         MVC   PRUKMED,UFSKMED     MEDIA                                        
         OC    PRUKMED,BLANKS                                                   
         MVC   PRUKJOB,UFSKJOB     JOB                                          
         OC    PRUKJOB,BLANKS                                                   
*                                                                               
         MVI   PRUKDEL,C'0'                                                     
         TM    UFSRSTA,ACTSDELT    TEST FOR DELETED RECORD                      
         BNO   BUS10                                                            
*                                                                               
         CLI   ACPRLOAD,C'Y'       TEST FOR INITIAL LOAD                        
         BE    NO                  YES-SKIP RECORD                              
         MVI   PRUKDEL,C'1'                                                     
*                                                                               
BUS10    LA    R3,PRUKOBJL(R3)                                                  
         TM    UFSRSTA,ACTSDELT                                                 
         BO    BUSX                                                             
*                                                                               
         LR    R6,R2                                                            
         LA    R5,1                R5=POSITION                                  
         MVI   ELCODE,UFSELQ                                                    
         BAS   RE,GETEL                                                         
*                                                                               
         USING UFSELD,R6                                                        
BUS20    BNE   BUSX                                                             
         MVC   0(2,R3),=Y(PRUSOBJL+4)                                           
         MVC   2(4,R3),=A(ITPRRTUF)                                             
         LA    R3,6(R3)                                                         
         USING PRUSOBJD,R3                                                      
         MVC   PRUSOBJD(PRUSOBJL),BLANKS                                        
         MVC   PRUSCODE,UFSCODE    USER FIELD CODE                              
         MVC   PRUSNAME,UFSDESC    USER FIELD NAME                              
         MVC   PRUSERUL,UFSEDIT    EDIT RULE (C, D, N)                          
         GOTO1 CHEXOUT,DMCB,UFSMXLN,PRUSMAX,1,MIXED  MAX LENGTH                 
*                                                                               
         MVI   PRUSREQD,C'0'                                                    
         TM    UFSSTAT,UFSSREQD    REQUIRED (1=YES, 0=NO)                       
         BZ    *+8                                                              
         MVI   PRUSREQD,C'1'                                                    
*                                                                               
         MVI   PRUSEST,C'0'        SHOW ON ESTIMATES                            
         TM    UFSSTAT,UFSSSHES                                                 
         BZ    *+8                                                              
         MVI   PRUSEST,C'1'                                                     
*                                                                               
         MVI   PRUSAUTH,C'0'       SHOW ON AUTHORIZATION                        
         TM    UFSSTAT,UFSSAUTH                                                 
         BZ    *+8                                                              
         MVI   PRUSAUTH,C'1'                                                    
*                                                                               
         STC   R5,BYTE             POSITION                                     
         GOTO1 CHEXOUT,DMCB,BYTE,PRUSPOS,1,MIXED                                
*                                                                               
         OC    UFSCUT,UFSCUT                                                    
         BZ    BUS30                                                            
         GOTO1 CDATCON,DMCB,(1,UFSCUT),(20,PRUSCUT)                             
*                                                                               
BUS30    LA    R3,PRUSOBJL(R3)     ADVANCE OBJECT POINTER                       
         LA    R5,1(R5)            INCREMENT POSITION NUMBER                    
         BAS   RE,NEXTEL                                                        
         B     BUS20                                                            
*                                                                               
BUSX     XC    0(2,R3),0(R3)                                                    
         B     YES                                                              
         DROP  R2,R3,R6                                                         
         EJECT                                                                  
*                                                                               
* THIS ROUTINE BUILDS THE LEDGER STRUCTURE OBJECT BLOCK FROM THE LEDGER         
* RECORD PASSED.                                                                
*                                                                               
* PARAMETER 1 - A(LEDGER RECORD)                                                
* PARAMETER 2 - A(LEDGER OBJECT BLOCK)                                          
*                                                                               
BUILDLS  NTR1  BASE=*                                                           
         B     *+12                                                             
         DC    CL8'*BUILDLS'                                                    
         LM    R2,R3,0(R1)         R2 = A(LEDGER RECORD)                        
*                                  R3 = A(LEDGER STRUCTURE OBJ BLOCK)           
*                                                                               
*                                                                               
         USING LDGRECD,R2          EXTRACT RECORD KEY DATA                      
         MVC   HALF,LDGKUNT        SAVE UNIT/LEDGER                             
*                                                                               
         USING ACLELD,R6           EXTRACT NAME ELEMENT DATA                    
         LR    R6,R2                                                            
         MVI   ELCODE,ACLELQ                                                    
         BAS   RE,GETEL                                                         
         BNE   NO                                                               
         ZIC   R5,ACLLN            LENGTH OF ELEMENT                            
         SHI   R5,2                SUBTRACT 2 FOR ELCODE & LEN                  
         LA    R4,1                FIRST LEVEL                                  
         MVI   BYTE,0              USE FOR PREVIOUS LEVEL'S LENGTH              
*                                                                               
*                                  SET LEDGER STR OBJ LENGTH/TYPE               
BLS10    MVC   0(2,R3),=Y(PRLSOBJL+4)                                           
         MVC   2(4,R3),=A(ITPRRTLS)                                             
*                                                                               
         USING PRLSOBJD,R3         BUMP TO OBJECT DATA AND PRECLEAR             
         LA    R3,6(R3)                                                         
         MVC   0(PRLSOBJL,R3),BLANKS                                            
*                                                                               
         MVC   PRLSLC,LDGKUNT      LEDGER CODE                                  
         EDIT  (R4),(3,PRLSLNUM),ZERO=NOBLANK,FILL=0  LEVEL NUMBER              
         ZIC   RE,BYTE                                                          
         ZIC   RF,ACLVLEN                                                       
         SR    RF,RE                                                            
         EDIT  (RF),(3,PRLSLLN),ZERO=NOBLANK,FILL=0   LEVEL LENGTH              
         MVC   BYTE,ACLVLEN                                                     
         MVC   PRLSLNAM,ACLVDESC   LEVEL NAME                                   
*                                                                               
         LA    R3,PRLSOBJL(R3)     BUMP R3 TO NEXT OBJECT                       
         LA    R6,L'ACLVALS(R6)    NEXT LEVEL                                   
         AHI   R4,1                BUMP LEVEL NUMBER                            
         SHI   R5,L'ACLVALS        INCREMENT BY LENGTH OF LEN & DESC            
         CHI   R5,0                CHECK IF ANYMORE LEFT                        
         BNH   BLSX                                                             
         CLI   ACLVLEN,0                                                        
         BNE   BLS10                                                            
*                                                                               
BLSX     XC    0(2,R3),0(R3)       SET END OF OBJECT BLOCK                      
         B     YES                                                              
         DROP  R2,R3,R6                                                         
         EJECT                                                                  
*                                                                               
* THIS ROUTINE BUILDS THE 1R LEDGER NAMES OBJECT BLOCK FROM THE LEDGER          
* RECORD PASSED.                                                                
*                                                                               
* PARAMETER 1 - A(LEDGER RECORD)                                                
* PARAMETER 2 - A(LEDGER OBJECT BLOCK)                                          
*                                                                               
BUILDLN  NTR1  BASE=*                                                           
         B     *+12                                                             
         DC    CL8'*BUILDLN'                                                    
         LM    R2,R3,0(R1)         R2 = A(LEDGER RECORD)                        
*                                  R3 = A(1R LEDGER NAME OBJ BLOCK)             
*                                                                               
*                                  SET LEDGER STR OBJ LENGTH/TYPE               
         MVC   0(2,R3),=Y(PRLNOBJL+4)                                           
         MVC   2(4,R3),=A(ITPRRTLN)                                             
*                                                                               
         USING PRLNOBJD,R3         BUMP TO OBJECT DATA AND PRECLEAR             
         LA    R3,6(R3)                                                         
         MVC   0(PRLNOBJL,R3),BLANKS                                            
*                                                                               
         USING LDGRECD,R2          EXTRACT RECORD KEY DATA                      
         LA    R5,LDGKEY+LDGKEND   OFFICE CODE                                  
*                                                                               
         ZIC   R1,ACPRLOFF                                                      
         SHI   R1,1                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   PRLNLC(0),0(R5)     OFFICE CODE                                  
*                                                                               
         AR    R5,R1               R1 = (LENGTH OF OFFICE CODE - 1)             
         AHI   R5,1                SO ADD 1 TO POINT TO DEPARTMENT              
         ZIC   R1,ACPRLDEP         LENGTH OF DEPARTMENT                         
         SHI   R1,1                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         CLC   0(0,R5),BLANKS      CHECK IF THERE IS A DEPARTMENT               
         BE    BLN10               NO, SO JUST SEND LEDGER NAME                 
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   PRLNLNUM(0),0(R5)   DEPT CODE                                    
*                                                                               
         AR    R5,R1               R1 = (LENGTH OF DEPT CODE - 1)               
         AHI   R5,1                SO ADD 1 TO POINT TO SUB-DEPT                
         ZIC   R1,ACPRLSUB         LENGTH OF SUB-DEPT                           
         SHI   R1,1                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         CLC   0(0,R5),BLANKS      CHECK IF THERE IS A SUB-DEPT                 
         BE    BLN10               NO, SO JUST SEND LEDGER NAME                 
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   PRLNLLN(0),0(R5)    SUB-DEPT CODE                                
*                                                                               
         USING NAMELD,R6           EXTRACT NAME ELEMENT DATA                    
BLN10    LR    R6,R2                                                            
         MVI   ELCODE,NAMELQ                                                    
         BAS   RE,GETEL                                                         
         BNE   BLN20                                                            
*                                                                               
         ZIC   R1,NAMLN            LENGTH OF ELEMENT                            
         SHI   R1,3                SUBTRACT 3 FOR ELCODE & LEN + 1              
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   PRLNLNAM(0),NAMEREC LEDGER NAME                                  
*                                                                               
BLN20    LA    R3,PRLNOBJL(R3)     BUMP R3 TO NEXT OBJECT                       
*                                                                               
BLNX     XC    0(2,R3),0(R3)       SET END OF OBJECT BLOCK                      
         B     YES                                                              
         DROP  R2,R3,R6                                                         
         EJECT                                                                  
*                                                                               
* THIS ROUTINE BUILDS THE PERSON AND PERSON ASSIGNMENT OBJECT FROM THE          
* PERSON RECORD PASSED.                                                         
*                                                                               
* PARAMETER 1 - A(PERSON RECORD)                                                
* PARAMETER 2 - A(PERSON OBJECT BLOCK)                                          
*                                                                               
BUILDPE  NTR1  BASE=*                                                           
         B     *+12                                                             
         DC    CL8'*BUILDPE'                                                    
         LM    R2,R3,0(R1)         R2 = A(PERSON RECORD)                        
*                                  R3 = A(PERSON OBJ BLOCK)                     
*                                                                               
         USING PERRECD,R2          EXTRACT RECORD KEY DATA                      
*                                                                               
*                                  SET PERSON OBJ LENGTH/TYPE                   
         MVC   0(2,R3),=Y(PRPEOBJL+4)                                           
         MVC   2(4,R3),=A(ITPRRTPE)                                             
                                                                                
         USING PRPEOBJD,R3         BUMP TO OBJECT DATA AND PRECLEAR             
         LA    R3,6(R3)                                                         
         MVC   0(PRPEOBJL,R3),BLANKS                                            
*                                                                               
         MVC   PRPEPERS,PERKCODE   PERSON CODE                                  
         MVI   PRPEDEL,C'0'        IS DELETED (0/1)                             
         TM    PERRSTA,X'80'                                                    
         BZ    *+8                                                              
         MVI   PRPEDEL,C'1'                                                     
*                                                                               
         USING GPNELD,R6           NAME ELEMENT DATA                            
         LR    R6,R2                                                            
         MVI   ELCODE,GPNELQ                                                    
         BAS   RE,GETEL                                                         
         BNE   BPE40                                                            
BPE10    ZIC   R5,GPNLN            LENGTH OF ELEMENT                            
         SHI   R5,4                SUBTRACT 4 FOR OTHER FIELDS IN ELEM          
         LA    R4,PRPELN           LAST NAME                                    
         CLI   GPNTYP,GPNTLST      CHECK IF LAST NAME                           
         BE    BPE20                                                            
         LA    R4,PRPEFN           FIRST NAME                                   
         CLI   GPNTYP,GPNTFST      CHECK IF FIRST NAME                          
         BNE   BPE30                                                            
BPE20    EX    R5,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),GPNNME                                                   
BPE30    BAS   RE,NEXTEL                                                        
         BE    BPE10                                                            
         LA    R3,PRPEOBJL(R3)     BUMP R3 TO NEXT OBJECT                       
         CLI   PRPEDEL,C'1'        IF PERSON DELETED                            
         BE    BPEX                THEN SKIP PERSON ASSIGNMENT                  
*                                                                               
         USING PRPAOBJD,R3         SWITCH USING TO PERSON ASSIGNMENT            
         USING LOCELD,R6           LOCATION ELEMENT                             
BPE40    LR    R6,R2                                                            
         MVI   ELCODE,LOCELQ                                                    
         BAS   RE,GETEL                                                         
         BNE   BPEX                                                             
*                                                                               
BPE50    MVC   0(2,R3),=Y(PRPAOBJL+4)                                           
         MVC   2(4,R3),=A(ITPRRTPA)                                             
         LA    R3,6(R3)                                                         
         MVC   0(PRPAOBJL,R3),BLANKS                                            
         MVC   PRPAPRCD,PERKCODE   PERSON CODE                                  
         GOTO1 CDATCON,DMCB,(1,LOCSTART),(20,PRPASTDT),0   START DATE           
         GOTO1 CDATCON,DMCB,(1,LOCEND),(20,PRPAENDT),0     END DATE             
         MVC   PRPAOFCD,LOCOFF                             OFFICE               
         MVC   PRPADEP,LOCDEPT                             DEPARTMENT           
         MVC   PRPASUB,LOCSUB                              SUB-DEPT             
         LA    R3,PRPAOBJL(R3)     BUMP R3 TO NEXT OBJECT                       
         BAS   RE,NEXTEL                                                        
         BE    BPE50                                                            
*                                                                               
BPEX     XC    0(2,R3),0(R3)       SET END OF OBJECT BLOCK                      
         B     YES                                                              
         DROP  R2,R3,R6                                                         
         EJECT                                                                  
*                                                                               
* THIS ROUTINE BUILDS THE RATE OBJECT BLOCK FROM THE RATE RECORD                
* PASSED.                                                                       
*                                                                               
* PARAMETER 1 - A(RATE RECORD)                                                  
* PARAMETER 2 - A(RATE OBJECT BLOCK)                                            
*                                                                               
BUILDRA  NTR1  BASE=*                                                           
         B     *+12                                                             
         DC    CL8'*BUILDRA'                                                    
         LM    R2,R3,0(R1)         R2 = A(RATE RECORD)                          
*                                  R3 = A(RATE OBJ BLOCK)                       
*                                                                               
*                                                                               
         USING TCIELD,R6           EXTRACT NAME ELEMENT DATA                    
         LR    R6,R2                                                            
         MVI   ELCODE,TCIELQ                                                    
         BAS   RE,GETEL                                                         
         BNE   BRAX                                                             
*                                                                               
BRA10    MVC   0(2,R3),=Y(PRRAOBJL+4)                                           
         MVC   2(4,R3),=A(ITPRRTRA)                                             
                                                                                
         USING PRRAOBJD,R3         BUMP TO OBJECT DATA AND PRECLEAR             
         LA    R3,6(R3)                                                         
         MVC   0(PRRAOBJL,R3),BLANKS                                            
*                                                                               
*&&US                                                                           
         USING PCHRECD,R2          EXTRACT RECORD KEY DATA                      
         MVC   PRRAOFC(L'PCHKDOF),PCHKDOF  OFFICE CODE                          
         MVC   PRRADEP(L'PCHKDEP),PCHKDEP  DEPT CODE                            
         MVC   PRRASUB(L'PCHKSUB),PCHKSUB  SUB-DEPT CODE                        
         MVC   PRRAPER(L'PCHKSTF),PCHKSTF  PERSON CODE                          
         MVC   PRRATASK(L'PCHKTSK),PCHKTSK TASK CODE                            
         MVC   PRRACLOF(L'PCHKOFF),PCHKOFF CLIENT OFFICE CODE                   
         MVC   PRRACLI(L'PCHKCLI),PCHKCLI  CLIENT CODE                          
         MVC   PRRAPRD(L'PCHKPRO),PCHKPRO  PRODUCT CODE                         
*&&                                                                             
*&&UK                                                                           
         USING PCRRECD,R2                                                       
         MVC   PRRAOFC(1),PCRKDEP OFFICE CODE IS B1 OF DEPT                     
         MVC   PRRADEP(2),PCRKDEP+1 DEPARTMENT CODE IS B2&3 OF DEPT             
         MVC   PRRASUB(L'PCRKSUB),PCRKSUB    SUB DEPT                           
         MVC   PRRAPER(L'PCRKSTF),PCRKSTF     PERSON CODE                       
         MVC   PRRATASK(L'PCRKTSK),PCRKTSK    TASK CODE                         
         MVC   PRRACLOF(L'PCRKOFF),PCRKOFF    CLIENT OFFICE                     
*                                                                               
         CLC   =C'JW',ACPRAGY       JWAL                                        
         BE    BRA20                                                            
         CLC   =C'BB',ACPRAGY       BHA                                         
         BE    BRA20                                                            
         CLC   =C'CH',ACPRAGY       BARHUM                                      
         BNE   BRA30                                                            
*                                                                               
BRA20    MVC   PRRACLI(5),PCRKJOB   CLIENT  ALL HAVE SJ LEDGER AS 5,2,5         
         MVC   PRRAPRD(2),PCRKJOB+5 PRODUCT                                     
         B     BRA40                                                            
*                                                                               
BRA30    MVC   PRRACLI(3),PCRKJOB   CLIENT  ALL OTHERS ARE 3,2,7                
         MVC   PRRAPRD(2),PCRKJOB+3 PRODUCT                                     
*&&                                                                             
*                                                                               
BRA40    MVI   PRRADEL,C'0'        IS DELETED (0/1)                             
*&&US*&& TM    PCHRSTA,X'80'                                                    
*&&UK*&& TM    PCRKSTA,X'80'                                                    
         BZ    *+8                                                              
         MVI   PRRADEL,C'1'                                                     
*                                                                               
         EDIT  TCIAMT,(10,PRRARATE),ZERO=NOBLANK,FILL=0   RATE                  
         GOTO1 CDATCON,DMCB,(1,TCIDTE),(20,PRRAEFDT),0                          
         LA    R3,PRRAOBJL(R3)     BUMP R3 TO NEXT OBJECT                       
         BAS   RE,NEXTEL                                                        
         BE    BRA10                                                            
*                                                                               
BRAX     XC    0(2,R3),0(R3)       SET END OF OBJECT BLOCK                      
         B     YES                                                              
         DROP  R2,R3,R6                                                         
         EJECT                                                                  
*                                                                               
* THIS ROUTINE BUILDS THE EST APPROVAL OBJECT BLOCK FROM THE EST RECORD         
* PASSED.                                                                       
*                                                                               
* PARAMETER 1 - A(EST RECORD)                                                   
* PARAMETER 2 - A(EST OBJECT BLOCK)                                             
*                                                                               
BUILDEA  NTR1  BASE=*                                                           
         B     *+12                                                             
         DC    CL8'*BUILDEA'                                                    
         LM    R2,R3,0(R1)         R2 = A(EST RECORD)                           
*                                  R3 = A(EST OBJ BLOCK)                        
*                                                                               
*                                                                               
         USING EAPELD,R6           EXTRACT NAME ELEMENT DATA                    
BEA10    MVC   0(2,R3),=Y(PRAPOBJL+4)                                           
         MVC   2(4,R3),=A(ITPRRTEA)                                             
                                                                                
         USING PRAPOBJD,R3         BUMP TO OBJECT DATA AND PRECLEAR             
         LA    R3,6(R3)                                                         
         MVC   0(PRAPOBJL,R3),BLANKS                                            
*                                                                               
         USING EVERECD,R2          EXTRACT RECORD KEY DATA                      
         MVC   PRAPCLCD,EVEKCLI    CLIENT CODE                                  
         MVC   PRAPPRCD,EVEKPRO    PRODUCT CODE                                 
         MVC   PRAPJBCD,EVEKJOB    JOB CODE                                     
         EDIT  EVEKVER,(3,PRAPESTN),ZERO=NOBLANK,FILL=0   EST NUMBER            
*                                                                               
         LR    R6,R2               IF APPROVED, ADD APPROVAL INFO               
         MVI   ELCODE,EAPELQ                                                    
         BAS   RE,GETEL                                                         
         BNE   BEA20                                                            
         GOTO1 CDATCON,DMCB,(1,EAPDATE),(X'20',PRAPAPDT),0                      
         MVC   PRAPAPPR,EAPPBY     APPROVER                                     
*                                                                               
BEA20    LA    R3,PRAPOBJL(R3)     BUMP R3 TO NEXT OBJECT                       
*                                                                               
BEAX     XC    0(2,R3),0(R3)       SET END OF OBJECT BLOCK                      
         B     YES                                                              
         DROP  R2,R3,R6                                                         
         EJECT                                                                  
* LOCAL WORKING STORAGE                                                         
*                                                                               
ACPRLOCD DSECT                                                                  
RELO     DS    A                                                                
DMCB     DS    6F                                                               
FULL     DS    F                                                                
DUB      DS    D                                                                
HALF     DS    H                                                                
THREE    DS    XL3                                                              
BYTE     DS    X                                                                
ELCODE   DS    X                   USED FOR GETEL CALLS                         
WORK     DS    XL64                                                             
*                                                                               
WORKSTAT DS    XL1                                                              
WORKSTA2 DS    XL1                                                              
WORKGRP  DS    CL1                                                              
ANYOPT   DS    CL1                                                              
NARRFLAG DS    CL1                                                              
FIRST    DS    CL1                                                              
COMMENT  DS    CL6                                                              
COMMLEN  DS    H                                                                
*                                                                               
BLANKS   DS    CL256                                                            
ACPRLOCL EQU   *-ACPRLOCD                                                       
         SPACE 2                                                                
* DSECT TO COVER OPTION VALUE TABLE                                             
*                                                                               
OPTVTABD DSECT                                                                  
OPTVNUM  DS    XL1                 OPTION NUMBER                                
OPTVPNUM DS    XL1                 PRESTO OPTION NUMBER                         
OPTVMAXL DS    XL1                 MAXIMUM STRING LENGTH                        
OPTVARTN DS    AL4                 ADDRESS OF ROUTINE                           
OPTVTABL EQU   *-OPTVTABD          TABLE LENGTH                                 
         EJECT                                                                  
* ACPRESTOD                                                                     
       ++INCLUDE ACPRESTOD                                                      
         EJECT                                                                  
* ACPRESTOQ                                                                     
       ++INCLUDE ACPRESTOQ                                                      
         EJECT                                                                  
* DDCOMFACSD                                                                    
       ++INCLUDE DDCOMFACSD                                                     
* ACGENFILE                                                                     
* ACOPTEQUS                                                                     
* ACTOBACCOD                                                                    
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
       ++INCLUDE ACOPTEQUS                                                      
*&&UK                                                                           
       ++INCLUDE ACTOBACCOD                                                     
*&&                                                                             
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'107ACPRESTO  08/26/08'                                      
         END                                                                    
