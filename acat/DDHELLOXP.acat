*          DATA SET DDHELLOXP  AT LEVEL 011 AS OF 05/01/02                      
*CATALP HELLOXP                                                                 
         TITLE 'GENERAL ELEMENT GET/ADD/DELETE MODULE'                          
* INPUT PARAMETER  1  BYTE 0       ACTION (A,P,D,G)                             
*                          1-3     P,D,G -- A(FILE NAME)                        
*                                  A -- C'FIL' RETURNS A(HELEN)                 
*                                       C'DIR' RETURNS A(HELENDIR)              
*                  2       0       ELEMENT CODE (D OR G)                        
*                          1-3     A(RECORD)                                    
*                  3       0       L'SEARCH ARGUMENT                            
*                          1-3     A(ELEMENT)-P,A(SRCH ARG)-D OR G              
*                  4       1-3     A(C'ADD=CODE' OR C'ADD=END')                 
*                                                                               
* OUTPUT PARAMETER 4       0       0=SUCCESS OR ERROR INDICATOR                 
*                          1-3     L'RECORD AFTER HELLO OR A(ELEMENT)           
*                  5       1-3     A(ELEMENT AFTER ADD)                         
*                                                                               
HELLO    CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 HELWRKX-HELWRK,**HELLO                                           
         USING HELWRK,RC                                                        
         XC    HELWRK(HELWRKX-HELWRK),HELWRK                                    
*                                                                               
         CLI   0(R1),C'A'          TEST ACTION A (FOR ADDRESS)                  
         BNE   HELB                                                             
         MVI   12(R1),0            ZERO ERROR CODE                              
         CLC   1(3,R1),=C'FIL'     TEST RETURN A(HELEN)                         
         BNE   HELA                                                             
         LA    R8,HELREL                                                        
         S     R8,HELREL                                                        
         A     R8,=V(HELEN)                                                     
         ST    R8,0(R1)            RETURN A(HELEN) IN FIRST PARAMETER           
         B     HELEX                                                            
HELA     CLC   1(3,R1),=C'DIR'     TEST RETURN A(HELENDIR)                      
         BE    *+12                                                             
         MVI   12(R1),7            INVALID ADDRESS REQUESTED                    
         B     HELEX                                                            
         LA    R8,HELREL                                                        
         S     R8,HELREL                                                        
         A     R8,=V(HELENDIR)                                                  
         ST    R8,0(R1)            RETURN A(HELENDIR) IN FIRST PARAM            
         B     HELEX                                                            
*                                                                               
HELB     LM    R2,R5,0(R1)                                                      
         CLI   0(R1),C'P'          TEST ELEMENT PUT                             
         BNE   HEL0                                                             
         LTR   R5,R5               TEST PARAMETER 4 PASSED                      
         BZ    HEL0                                                             
         MVI   ADDSW,C'Y'                                                       
         CLC   0(7,R5),=C'ADD=END'                                              
         BNE   *+8                                                              
         MVI   ADDSW,C'E'                                                       
         CLC   0(8,R5),=C'ADD=CODE'                                             
         BNE   *+8                                                              
         MVI   ADDSW,C'C'                                                       
         B     HEL0                                                             
         EJECT                                                                  
* FIND FILE DEFINITION ENTRY IN LIST                                            
*                                                                               
HEL0     XC    12(4,R1),12(R1)                                                  
         LA    R8,HELREL                                                        
         S     R8,HELREL                                                        
         A     R8,=V(HELEN)                                                     
         USING HELEND,R8                                                        
         SR    R9,R9                                                            
*                                                                               
HEL2     CLI   HELFLEN,X'FF'       END OF FILE TABLE                            
         BNE   *+12                                                             
         MVI   12(R1),1            INVALID FILE NAME                            
         B     HELEX                                                            
         IC    R9,HELFLEN                                                       
         EX    R9,HELC                                                          
         BE    HEL4                                                             
         LA    R8,L'HELENL(R8)                                                  
         B     HEL2                                                             
*                                                                               
HELC     CLC   HELNAME(0),0(R2)                                                 
         EJECT                                                                  
* VALIDATE ACTION                                                               
*                                                                               
HEL4     CLI   0(R1),C'P'          ADD                                          
         BNE   *+12                                                             
         LA    R2,0                                                             
         B     HEL6                                                             
         CLI   0(R1),C'D'          DELETE                                       
         BNE   *+12                                                             
         LA    R2,4                                                             
         B     HEL6                                                             
         CLI   0(R1),C'G'          GET                                          
         BNE   *+12                                                             
         LA    R2,8                                                             
         B     HEL6                                                             
         MVI   12(R1),2            INVALID ACTION                               
         B     HELEX                                                            
         SPACE 1                                                                
* SET UP SOME PARAMETERS AND GO TO ROUTINES                                     
*                                                                               
HEL6     LA    R3,0(R3)                                                         
         ST    R3,ARECORD                                                       
         LR    R5,R3                                                            
         IC    R9,HELEDIS          DISP OF FIRST ELEMENT                        
         LA    R3,0(R9,R3)                                                      
         ST    R3,AFIRSTEL                                                      
         IC    R9,HELLDIS          DISP OF LENGTH                               
         LA    R5,0(R9,R5)                                                      
         ST    R5,ALENGTH                                                       
         XC    MAXLEN,MAXLEN                                                    
         MVC   MAXLEN+2(2),HELMSIZE                                             
         B     HELTAB(R2)                                                       
*                                                                               
HELTAB   B     HELADD                                                           
         B     HELDEL                                                           
         B     HELGET                                                           
         EJECT                                                                  
* ROUTINE TO ADD AN ELEMENT (R4 POINTS TO ADD)                                  
*                                                                               
HELADD   L     R2,AFIRSTEL                                                      
         SR    R5,R5                                                            
         SR    R6,R6                                                            
         LTR   R4,R4                                                            
         BNZ   *+12                                                             
         MVI   12(R1),3            INVALID ADD ELEMENT                          
         B     HELEX                                                            
         CLI   1(R4),2                                                          
         BNL   HELADD2                                                          
         MVI   12(R1),4            INVALID ADD ELEMENT LENGTH                   
         B     HELEX                                                            
*                                                                               
HELADD2  CLI   0(R2),0             INSERT IF END OF RECORD                      
         BE    HELADD8                                                          
         CLI   ADDSW,C'E'          ADD TO END OF RECORD                         
         BE    HELADD4                                                          
         CLC   0(1,R4),0(R2)       IF LOWER - INSERT ELEMENT                    
         BL    HELADD10                                                         
         BE    HELADD6             IF SAME - COMPARE DATA                       
*                                                                               
HELADD4  IC    R5,1(R2)            IF HIGHER - GET NEXT ELEMENT                 
         AR    R2,R5                                                            
         B     HELADD2                                                          
         SPACE 1                                                                
* CHECK FOR CORRECT SEQUENCE ON ADD                                             
*                                                                               
HELADD6  CLI   ADDSW,C'C'          ADD IN CODE SEQ ONLY                         
         BE    HELADD4                                                          
         MVC   BYTE,1(R2)          FIND SHORTEST ELEMENT LENGTH                 
         CLC   1(1,R4),1(R2)                                                    
         BH    *+10                                                             
         MVC   BYTE,1(R4)                                                       
         IC    R5,BYTE                                                          
         SH    R5,=H'3'                                                         
         BNM   *+10                                                             
         SR    R5,R5                                                            
         B     HELADD10                                                         
         EX    R5,HELADDC                                                       
         BE    *+12                DUPLICATE DATA IN ADD/REC                    
         BNH   HELADD10                                                         
         B     HELADD4                                                          
         CLC   1(1,R4),1(R2)       COMPARE ELEMENT LENGTHS                      
         BH    HELADD4                                                          
         B     HELADD10                                                         
*                                                                               
HELADDC  CLC   2(0,R4),2(R2)       COMPARE DATA IN ADD TO THIS                  
         EJECT                                                                  
* INSERT ELEMENT INTO RECORD                                                    
*                                                                               
HELADD8  IC    R6,1(R4)            L'ADD ELEMENT                                
         LA    R7,0(R6,R2)         R7 POINTS TO NEW END                         
         B     HELADD16                                                         
*                                                                               
HELADD10 LR    R8,R2                                                            
         IC    R6,1(R4)                                                         
*                                                                               
HELADD12 IC    R5,1(R8)                                                         
         AR    R8,R5                                                            
         CLI   0(R8),0                                                          
         BNE   HELADD12                                                         
         LA    R7,0(R6,R8)                                                      
         BCTR  R8,0                                                             
         LA    R9,0(R6,R8)                                                      
*                                                                               
HELADD14 MVC   0(1,R9),0(R8)                                                    
         CR    R8,R2                                                            
         BE    HELADD16                                                         
         BCTR  R9,0                                                             
         BCTR  R8,0                                                             
         B     HELADD14                                                         
*                                                                               
HELADD16 BCTR  R6,0                                                             
         EX    R6,HELADDM                                                       
         MVI   0(R7),0                                                          
         LA    R7,1(R7)                                                         
         S     R7,ARECORD                                                       
         ST    R7,12(R1)           PASS BACK NEW LENGTH                         
         ST    R7,FULL                                                          
         L     R6,ALENGTH                                                       
         MVC   0(2,R6),FULL+2                                                   
         ST    R2,16(R1)           RETURN A(ELEMENT)                            
         CLC   FULL,MAXLEN                                                      
         BNH   HELEX                                                            
         MVI   12(R1),5            RECORD TOO LONG                              
         B     HELEX                                                            
*                                                                               
HELADDM  MVC   0(0,R2),0(R4)                                                    
         EJECT                                                                  
* ROUTINE TO DELETE AN ELEMENT                                                  
*                                                                               
HELDEL   L     R2,ARECORD                                                       
         L     R3,AFIRSTEL                                                      
         SR    R4,R4                                                            
         B     HELDEL4                                                          
*                                                                               
HELDEL2  IC    R4,1(R3)                                                         
         AR    R3,R4                                                            
         CLI   0(R3),0                                                          
         BNE   HELDEL4                                                          
         LA    R3,1(R3)                                                         
         SR    R3,R2                                                            
         ST    R3,FULL                                                          
         ST    R3,12(R1)           PASS BACK NEW LENGTH                         
         L     R2,ALENGTH                                                       
         MVC   0(2,R2),FULL+2                                                   
         B     HELEX                                                            
*                                                                               
HELDEL4  BAS   RE,HELDCOM                                                       
         BNE   HELDEL2                                                          
         IC    R4,1(R3)                                                         
         LA    R5,0(R3,R4)                                                      
         SR    R6,R6                                                            
*                                                                               
HELDEL6  CLI   0(R5),0                                                          
         BE    HELDEL8                                                          
         IC    R6,1(R5)                                                         
         BCTR  R6,0                                                             
         EX    R6,HELDELS                                                       
         LA    R6,1(R6)                                                         
         AR    R3,R6                                                            
         AR    R5,R6                                                            
         B     HELDEL6                                                          
*                                                                               
HELDEL8  BCTR  R4,0                                                             
         EX    R4,HELDELC                                                       
         B     HELDEL                                                           
*                                                                               
HELDELS  MVC   0(0,R3),0(R5)                                                    
*                                                                               
HELDELC  XC    0(0,R3),0(R3)                                                    
         EJECT                                                                  
* ROUTINE TO GET AN ELEMENT                                                     
*                                                                               
HELGET   L     R2,ARECORD                                                       
         L     R3,AFIRSTEL                                                      
         SR    R4,R4                                                            
         B     HELGET3                                                          
*                                                                               
HELGET2  IC    R4,1(R3)                                                         
         AR    R3,R4                                                            
HELGET3  CLI   0(R3),0                                                          
         BNE   HELGET4                                                          
         MVI   12(R1),6            NOT FOUND                                    
         B     HELEX                                                            
*                                                                               
HELGET4  BAS   RE,HELDCOM                                                       
         BNE   HELGET2                                                          
         ST    R3,12(R1)                                                        
         B     HELEX                                                            
         SPACE 1                                                                
* ROUTINE TO CHECK FOR ELEMENT FOUND                                            
*                                                                               
HELDCOM  IC    R9,8(R1)                                                         
         L     RA,8(R1)                                                         
         CLC   4(1,R1),0(R3)                                                    
         BNER  RE                                                               
         LTR   R9,R9                                                            
         BZR   RE                                                               
         BCTR  R9,0                                                             
         EX    R9,*+6                                                           
         BR    RE                                                               
         CLC   2(0,R3),0(RA)                                                    
*                                  RETURN TO CALLER                             
HELEX    XIT1  ,                                                                
         EJECT                                                                  
* LITERALS ETC.                                                                 
*                                                                               
         LTORG                                                                  
HELREL   DC    A(*)                                                             
         SPACE 1                                                                
* W/S DSECT                                                                     
*                                                                               
HELWRK   DSECT                                                                  
ARECORD  DS    A                                                                
AFIRSTEL DS    A                                                                
ALENGTH  DS    A                                                                
MAXLEN   DS    F                                                                
FULL     DS    F                                                                
BYTE     DS    CL1                                                              
ADDSW    DS    CL1                                                              
HELWRKX  EQU   *                                                                
         SPACE 1                                                                
* DSECT TO COVER FILE DEFINITION TABLE                                          
*                                                                               
HELEND   DSECT                                                                  
HELENL   DS    0CL13                                                            
HELFLEN  DS    CL1                 L'FILE NAME - 1                              
HELNAME  DS    CL8                 NAME                                         
HELMSIZE DS    CL2                 MAXIMUM RECORD SIZE                          
HELEDIS  DS    CL1                 DISPLACEMENT OF FIRST ELEMENT                
HELLDIS  DS    CL1                 DISPLACEMENT OF LENGTH                       
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'011DDHELLOXP 05/01/02'                                      
         END                                                                    
