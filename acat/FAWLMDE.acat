*          DATA SET FAWLMDE    AT LEVEL 001 AS OF 04/26/10                      
*CATALP FAWLMDE                                                                 
         TITLE 'WLMDE - INTERFACE TO IBM WLM DELETE ENCLVE'                     
********************************************************************            
* FACPACK - INTERFACE TO WORKLOAD MANAGER ENCLAVE SERVICES                      
* MODULE:   FAWLMDE                                                             
*                                                                               
* PROPERTIES:                                                                   
*    PROBLEM STATE                                                              
*    ADDRESSING MODE= 31                                                        
*    PRIMARY ASC MODE SAC 0                                                     
*                                                                               
* INVOCATION:                                                                   
*        SAM31                                                                  
*        SAC   0                                                                
*        GOTO1 VWLMCE,WLMPARM,C'WLDE'                                           
*                                                                               
* PROCESSING:                                                                   
* - ESTABLISH STANDEAD DDS LINKAGE                                              
* - UPON ENTRY REGISTER 1 CONTAINS THE ADDRESS OF THE PARAMETER                 
*   REQUEST BLOCK.                                                              
* - ESTABLISH REGISTER 7 AS THE REQUEST BLOCK BASE REGISTER                     
* - SWITCH TO SUPERVISOR STATE                                                  
* - ISSUE IWM4EDEL TO DELETE AN ENCLAVE                                         
* - SWITCH BACK TO PROBLEM STATE                                                
* - RETURN TO CALLER                                                            
*                                                                               
* IWM4EDEL                                                                      
* THE PURPOSE OF THIS SERVICE IS TO DELETE AN ENCLAVE, SO THAT                  
* NO SRBS OR TCBS EXIST WITHIN THE ENCLAVE AND NO NEW SRBS MAY                  
* BE SCHEDULED INTO THE ENCLAVE, NOR MAY ANY TCBS JOIN THE                      
* ENCLAVE.                                                                      
*                                                                               
* SOME RESIDUAL ENCLAVE RELATED CPU TIME WILL NOT BE                            
* ACCOUNTED BACK TO THE WORK REQUEST WHENEVER ACTIVE ENCLAVE                    
* SRBS/TCBS WERE PRESENT AT THE TIME IWM4EDEL IS INVOKED.  SRBS                 
* SCHEDULED TO THE ENCLAVE WHICH HAVE NOT COMPLETED WILL BE                     
* CONVERTED TO ORDINARY PREEMPTABLE SRBS.  TCBS JOINED TO THE                   
* ENCLAVE WHICH HAVE NOT COMPLETED WILL BE CONVERTED TO ORDINAR                 
* TCBS.                                                                         
*                                                                               
* WHEN AN ENCLAVE IS DELETED, THE WORK REQUEST IS CONSIDERED TO                 
* HAVE FINISHED AND ALL RELATED RESOURCE ACCOUNTING WILL BE                     
* FINALIZED.                                                                    
*                                                                               
*                                                                               
* 4   NAME: IWMRETCODEWARNING                                                   
*                                                                               
*     MEANING: SUCCESSFUL COMPLETION, UNUSUAL CONDITIONS NOTED.                 
*                                                                               
* XXXX0411    NAME: IWMRSNCODEENCLACTIVE                                        
*                                                                               
*             MEANING:  INPUT ENCLAVE HAD 1 OR MORE                             
*             SRBS SCHEDULED OR RUNNING, OR 1 OR MORE                           
*             TCBS JOINED TO THE ENCLAVE.                                       
*                                                                               
*             ACTION: NONE REQUIRED.                                            
*                                                                               
*                                                                               
*                                                                               
********************************************************************            
         USING WLMD,R7                                                          
FAWLMDE  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,**WLMDE                                                        
         LR    R7,R1               Parameter list                               
         ICM   R0,15,WLMETOKEN                                                  
         BZ    EXIT                Do nothing                                   
*                                                                               
* Switch to supervisor state                                                    
* Save the current ENCLAVE PSW KEY                                              
* Switch to connect PSW KEY                                                     
*                                                                               
         MODESET MODE=SUP                                                       
         MODESET SAVEKEY=WLMEKEY,WORKREG=1                                      
*                                                                               
         IWM4EDEL ETOKEN=WLMETOKEN,                                    +        
               RETCODE=WLMRETC,                                        +        
               RSNCODE=WLMRSNC,                                        +        
               MF=(E,DELLIST,COMPLETE)                                          
*                                                                               
* Switch back to problem state                                                  
*                                                                               
         MODESET MODE=PROB                                                      
         ICM   RF,15,WLMRETC       Check return codes                           
         JZ    WLMDE10             Okay, now check reason code                  
         CHI   RF,4                Check warning                                
         JE    WARN10              Yes                                          
         J     EXIT                                                             
                                                                                
WLMDE10  OC    WLMRSNC,WLMRSNC     Check reason code                            
         JNZ   ERROR10             NO, ABORT                   WLM              
         J     EXIT                                                             
*                                                                               
WARN10   AP    WARNS,=P'1'                                                      
         J     EXIT                                                             
*                                                                               
ERROR10  STM   R0,RF,DUMPREGS                                                   
         MVC   ABEND#,=CL3'915'                                                 
         AP    ERRORS,=P'1'                                                     
         LA    R0,8                                                             
         LA    R1,MSGRTC           Format return code                           
ERROR12  XR    RE,RE                                                            
         SAM24 ,                   32 bit shift between regs                    
         SLDL  RE,4                                                             
         SAM31 ,                                                                
         LA    RE,HEX2CHR(RE)                                                   
         MVC   0(1,R1),0(RE)                                                    
         LA    R1,1(,R1)                                                        
         BRCT  R0,ERROR12                                                       
                                                                                
         L     RF,WLMRSNC          Reason code                                  
         LA    R0,8                                                             
         LA    R1,MSGRSNC          Format reason code                           
ERROR14  XR    RE,RE                                                            
         SAM24 ,                   32 bit shift between regs                    
         SLDL  RE,4                                                             
         SAM31 ,                                                                
         LA    RE,HEX2CHR(RE)                                                   
         MVC   0(1,R1),0(RE)                                                    
         LA    R1,1(,R1)                                                        
         BRCT  R0,ERROR14                                                       
                                                                                
         LA    R3,MSG1                                                          
         WTO   TEXT=(R3)                                                        
*                                                                               
EXIT     XMOD1 1                                                                
                                                                                
         LTORG ,                  GENERTE LOCAL LITTERL POOL                    
         EJECT ,                                                                
***********************************************************************         
* Constants                                                                     
***********************************************************************         
         DS    0D                                                               
         DC    CL4'WR#='                                                        
WARNS    DC    PL8'0'                                                           
         DC    CL4'ER#='                                                        
ERRORS   DC    PL8'0'                                                           
         DC    CL6'ABEND='                                                      
ABEND#   DC    CL3' '                                                           
         DC    CL3' '                                                           
         DC    C'DUMPREGS'                                                      
DUMPREGS DC    18A(0)                                                           
                                                                                
HEX2CHR  DC    C'0123456789ABCDEF'                                              
                                                                                
MSG1     DC    AL2(MSG1LNQ)                                                     
MSG1TXT  DC    C'**U917** FAWLMDE DELETE ENCLAVE'                               
         DC    C' RTC='                                                         
MSGRTC   DC    C'00000000'                                                      
         DC    C' RSN='                                                         
MSGRSNC  DC    C'00000000'                                                      
MSG1LNQ  EQU   *-MSG1TXT                                                        
*                                                                               
         DS    0D                                                               
         IWM4EDEL PLISTVER=IMPLIED_VERSION,MF=(L,DELLIST)                       
         EJECT                                                                  
***********************************************************************         
* DSECTS                                                                        
***********************************************************************         
* FASSB                                                                         
*        PRINT OFF                                                              
*      ++INCLUDE FASSB                                                          
*        PRINT ON                                                               
* FATCB                                                                         
*        PRINT OFF                                                              
*      ++INCLUDE FATCB                                                          
*        PRINT ON                                                               
* FAWLMD                                                                        
       ++INCLUDE FAWLMD                                                         
*                                                                               
* CVT                                                                           
         CVT DSECT=YES                                                          
* IWMYCON                                                                       
         IWMYCON                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'001FAWLMDE   04/26/10'                                      
         END                                                                    
