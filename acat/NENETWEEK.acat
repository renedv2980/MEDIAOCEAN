*          DATA SET NENETWEEK  AT LEVEL 024 AS OF 11/12/13                      
*PROCESS USING(WARN(15))                                                        
*CATALP NETWEEK                                                                 
         TITLE '-  MODULE TO DEDUCE HUT WEEK NUMBER FROM DATE'                  
NETWEEK  RSECT                     P1 A(6-BYTE DATE)                            
*                                     HUT WEEK NO RETURNED IN P1 BYTE 1         
*                                  P2 A(GETDAY)                                 
*                                     YEAR NO RETURNED IN P2 BYTE 1             
*                                  P3 A(ADDAY)                                  
*                                     RTG WEEK NO RETURNED IN P3 BYTE 1         
*                                  P4                                           
*                                     ACTIVITY WEEK RETURNED IN P4 B 1          
         PRINT NOGEN                                                            
         NMOD1 HUTDX-HUTD,**HTNO**                                              
         USING HUTD,RC                                                          
         LM    R2,R4,0(R1)                                                      
         LR    R9,R1                                                            
         STM   R3,R4,GETDAY                                                     
         MVC   WEEKTYPE,8(R9)                                                   
* TO FIX Y2K PROBLEMS WITHOUT DATCON, ADD ZERO TO THE DATE !                    
         GOTO1 ADDAY,PARA,0(R2),DUB,0                                           
         MVC   DATE,DUB                                                         
         MVC   DATEORG,DUB                                                      
*                                                                               
         GOTO1 GETDAY,PARA,DATE,DUB FIND SUNDAY DATE                            
         ZIC   R3,PARA                                                          
         LA    R4,7                                                             
         SR    R4,R3                                                            
         GOTO1 ADDAY,PARA,DATE,DUB,(R4)                                         
         MVC   DATE,DUB                                                         
         CLI   DATE,X'FA'          YEAR 2000 OR GREATER                         
         BL    HUT1                                                             
*                                                                               
*  YEAR 2000 DATE FIX                                                           
*  DEIS (NOV/2013): THIS IS NON Y2K COMPLIANT.                                  
*                   DO NOT ASSUME THAT THIS CODE WILL WORK PAST 2027.           
*                                                                               
         MVI   DUB+4,X'F1'         START AT 100 YEARS (2000)                    
         MVC   DUB+6(1),DATE+1     MOVE IN SECOND HALF OF YEAR                  
         CLI   DATE,X'FA'                                                       
         BNE   *+8                                                              
         MVI   DUB+5,X'F0'                                                      
         CLI   DATE,X'FB'                                                       
         BNE   *+8                                                              
         MVI   DUB+5,X'F1'                                                      
         CLI   DATE,X'FC'                                                       
         BNE   *+8                                                              
         MVI   DUB+5,X'F2'                                                      
         CLI   DATE,X'FD'                                                       
         BNE   *+8                                                              
         MVI   DUB+5,X'F3'                                                      
         PACK  DUB,DUB+4(3)                                                     
         CVB   R1,DUB                                                           
         STC   R1,4(R9)            THE DDS BINARY YEAR                          
         B     HUT1A                                                            
*                                                                               
HUT1     PACK  DUB,DATE(2)                                                      
         CVB   R1,DUB                                                           
         CHI   R1,27               YEARS LESS THAN 27 ARE Y2K                   
         BH    *+8                                                              
         AHI   R1,100                                                           
         STC   R1,4(R9)            THE DDS BINARY YEAR                          
*                                                                               
HUT1A    MVC   DATE+2(4),=C'0101'  FIND DAY NUMBER OF JAN01                     
         GOTO1 GETDAY,PARA,DATE,DUB                                             
         ZIC   R3,PARA             DAY 1-7                                      
         LA    R4,1                                                             
         SR    R4,R3                   0 TO -6                                  
         BZ    HUT2                BACK UP TO PREVIOUS MONDAY                   
         GOTO1 ADDAY,PARA,DATE,DUB,(R4)                                         
         MVC   DATE,DUB                                                         
*                                                                               
HUT2     SR    R3,R3                                                            
*                                                                               
HUT4     LA    R3,1(R3)                                                         
         GOTO1 ADDAY,PARA,DATE,DUB,7                                            
         MVC   DATE,DUB                                                         
         CLC   DATE,DATEORG                                                     
         BNH   HUT4                                                             
*                                                                               
         STC   R3,ACTWEEK                                                       
         MVC   12(1,R9),ACTWEEK                                                 
         LA    R1,HUTLIST                                                       
         CLI   4(R9),YR_1984       DIFFERENT HUTS FOR 84                        
         BE    HUT4B                                                            
         CLI   4(R9),YR_1989                 AND FOR 89                         
         BE    HUT4D                                                            
         BNE   HUT5                                                             
*                                                                               
HUT4B    CLI   WEEKTYPE,C'H'       UNLESS OLD HUT OPTION                        
         BNE   *+8                 **** SHOULD                                  
HUT4D    LA    R1,HUT84                                                         
*                                                                               
HUT5     AR    R3,R1               LOOK UP WEEK TRANSLATION TABLE               
*                                  TO ACCOUNT FOR BLACK WEEKS                   
         MVC   0(1,R9),0(R3)                                                    
                                                                                
         MVC   8(1,R9),ACTWEEK     STARTING W/ 1994, PASS BACK                  
         CLI   4(R9),YR_1994        SEQUENTIAL RATINGS WEEK (1-53)              
         BNL   NETWEEKX                                                         
*                                                                               
         LA    R1,HUTLIST                                                       
         CLI   4(R9),YR_1984       DIFFERENT RATING WEEKS FOR 84                
         BNE   *+8                                                              
         LA    R1,HUT84                                                         
         CLI   4(R9),YR_1989                          AND FOR 89                
         BNE   *+8                                                              
         LA    R1,HUT84                                                         
         ZIC   R3,ACTWEEK                                                       
         AR    R3,R1                                                            
         MVC   8(1,R9),0(R3)       ASSUME RATING WEEK = HUT WEEK                
         LA    R3,BLCKLIST         UNLESS ITS A BLACK WEEK                      
         CLI   4(R9),YR_1984       DIFFERENT BLACK WEEKS FOR 84                 
         BNE   *+8                                                              
         LA    R3,BLACK84                                                       
         CLI   4(R9),YR_1989                         AND FOR 89                 
         BNE   *+8                                                              
         LA    R3,BLACK84                                                       
         LA    R4,5                                                             
*                                                                               
HUT6     CLC   ACTWEEK,0(R3)                                                    
         BNE   *+10                                                             
         MVC   8(1,R9),1(R3)                                                    
         LA    R3,2(R3)                                                         
         BCT   R4,HUT6                                                          
*                                                                               
NETWEEKX XIT1                                                                   
         SPACE 2                                                                
* NOTE: THE INDEX INTO HUTLIST SHOULD NEVER BE ZERO, BUT JUST IN CASE           
*       IT IS (E.G., DUE TO A BUG SOMEWHERE), THE FIRST BYTE OF THE             
*       ARRAY IS SET TO ZERO. SAME FOR HUT84.                                   
HUTLIST  DC    X'00'                                                            
         DC    AL1(1,2,3,4,5,6,7,8,9,10,11,12,13)                               
         DC    AL1(14,15,16,16,17,18,19,20,21,22,23,24,24)                      
         DC    AL1(25,26,27,28,29,30,31,32,32,33,34,35,36)                      
         DC    AL1(37,38,39,40,41,42,43,44,45,46,47,48,48,48)                   
*                                                                               
HUT84    DC    X'00'                                                            
         DC    AL1(1,2,3,4,5,6,7,8,9,10,11,12,13)                               
         DC    AL1(14,15,16,16,17,18,19,20,21,22,23,24,24)                      
         DC    AL1(25,26,27,28,29,30,31,32,32,32,33,34,35,36)                   
         DC    AL1(37,38,39,40,41,42,43,44,45,46,47,48,48,48)                   
*                                                                               
BLCKLIST DC    AL1(17,49)                                                       
         DC    AL1(26,50)                                                       
         DC    AL1(35,51)                                                       
         DC    AL1(52,52)                                                       
         DC    AL1(53,53)                                                       
*                                                                               
BLACK84  DC    AL1(17,49)                                                       
         DC    AL1(26,50)                                                       
         DC    AL1(35,51)                                                       
         DC    AL1(36,53)                                                       
         DC    AL1(53,52)                                                       
*                                                                               
         LTORG                                                                  
         SPACE 2                                                                
HUTD     DSECT                                                                  
DUB      DS    D                                                                
DATE     DS    CL6                                                              
DATEORG  DS    CL6                                                              
ACTWEEK  DS    CL1                                                              
WEEKTYPE DS    CL1                                                              
PARA     DS    6F                                                               
GETDAY   DS    V                                                                
ADDAY    DS    V                                                                
HUTDX    EQU   *                                                                
         SPACE 2                                                                
* ++INCLUDE DDMONYREQU                                                          
         PRINT OFF                                                              
       ++INCLUDE DDMONYREQU                                                     
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'024NENETWEEK 11/12/13'                                      
         END                                                                    
