*          DATA SET TALUPGUA   AT LEVEL 011 AS OF 05/01/02                      
*CATALP TALUPGUA                                                                
         TITLE 'TALUPGUA - LOOK UP GUARANTEE FROM TALFIL'                       
TALUPGUA CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 MYEND-MYSTART,**LUPGUA,R9,R8                                     
         LR    RA,RC                                                            
         USING MYD,RA                                                           
         CLI   0(R1),0             PASS UTL OPTIONALLY IN P1                    
         BE    *+10                                                             
         MVC   TALUTL,0(R1)                                                     
         ST    R1,APARMS                                                        
         L     RC,0(R1)                                                         
         USING GEND,RC                                                          
         SPACE 1                                                                
         LM    R2,R3,4(R1)                                                      
         CLI   4(R1),C'O'          P2 BYTE 1:O=FILE OPENED ALREADY              
         BNE   *+8                                                              
         MVI   OPENSW,1                                                         
         PACK  DUB,0(9,R2)         P1=A(SSN) P2=A(H'SEQ')                       
         CVB   R1,DUB                                                           
         ST    R1,BSSN             BINARY SSN                                   
         CLI   OPENSW,0                                                         
         BNE   *+8                                                              
         BAS   RE,OPENTAL                                                       
         MVI   OPENSW,1                                                         
         EJECT                                                                  
*              LOOK UP SSN POOL TO SEE IF WE HAVE THIS GUY                      
         SPACE 3                                                                
         LA    R5,POOLSSN                                                       
         L     R0,POOLMAX                                                       
         SPACE 1                                                                
LUPOOL   LH    R1,4(R5)            EACH ENTRY IS SSN(4) GUA(2)                  
         CLC   BSSN,0(R5)                                                       
         BE    POOLHIT                                                          
         OC    0(4,R5),0(R5)                                                    
         BZ    POOLMISS                                                         
         LA    R5,6(R5)                                                         
         BCT   R0,LUPOOL                                                        
         B     POOLMISS                                                         
         SPACE 1                                                                
POOLMISS XC    TALKEY,TALKEY       CHECK TALFIL FOR HIGHEST TO DATE             
         LA    R4,TALKEY                                                        
         USING TLGUD,R4                                                         
         MVI   TLGUCD,TLGUCDQ                                                   
         MVC   TLGUSSN,0(R2)                                                    
         SR    R1,R1               ASSUME ZERO IF MISS                          
         BAS   RE,HIGHTAL                                                       
         CLC   TALSAVE(24),TALKEY  CHECK MATCH OF SS#                           
         BNE   POOLHIT                                                          
         MVC   FULL,TLGUGUA                                                     
         XC    FULL,=X'FFFFFFFF'   COMP GUA CODE                                
         PACK  DUB,FULL                                                         
         CVB   R1,DUB                                                           
         SPACE 1                                                                
POOLHIT  MVC   0(4,R5),BSSN                                                     
         LA    R1,1(R1)                                                         
         STH   R1,4(R5)                                                         
         STH   R1,0(R3)            RETURN IN BINARY                             
         B     XIT                                                              
         EJECT                                                                  
*              TALENT FILE IO                                                   
         SPACE 3                                                                
OPENTAL  NTR1                                                                   
         L     R6,ATWA                                                          
         USING CONHEADH-64,R6                                                   
         L     R1,TWAMASTC                                                      
         DROP  R6                                                               
         USING MASTD,R1                                                         
         L     R1,MCUTL                                                         
         ST    R1,AUTL                                                          
         MVC   SAVEUTL4,4(R1)                                                   
         MVC   4(1,R1),TALUTL                                                   
         GOTO1 DATAMGR,DMCB,=C'OPEN',                                  X        
               =C'TALENT',=C'NTALFIL NTALDIR X',IO                              
         L     R1,AUTL                                                          
         MVC   4(1,R1),SAVEUTL4                                                 
         B     XIT                                                              
         SPACE 1                                                                
HIGHTAL  NTR1                                                                   
         MVC   COMMAND,=C'DMRDHI  '                                             
         MVC   TALSAVE,TALKEY                                                   
         B     ALLTAL                                                           
         SPACE 1                                                                
READTAL  NTR1                                                                   
         MVC   COMMAND,=C'DMREAD  '                                             
         B     ALLTAL                                                           
         SPACE 1                                                                
SEQTAL   NTR1                                                                   
         MVC   COMMAND,=C'DMRSEQ  '                                             
         SPACE 1                                                                
ALLTAL   L     R1,AUTL                                                          
         MVC   4(1,R1),TALUTL                                                   
         GOTO1 DATAMGR,DMCB,COMMAND,=C'TALDIR  ',TALKEY,TALKEY,0                
         B     TALXIT                                                           
         SPACE 1                                                                
GETTAL   NTR1                                                                   
         MVC   COMMAND,=C'GETREC  '                                             
         L     R1,AUTL                                                          
         MVC   4(1,R1),TALUTL                                                   
         L     R2,=A(TALIO)                                                     
         GOTO1 DATAMGR,DMCB,COMMAND,=C'TALFIL  ',TALKEY+34,            X        
               (R2),TALWORK                                                     
         SPACE 1                                                                
TALXIT   CLI   DMCB+8,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         L     R1,AUTL                                                          
         MVC   4(1,R1),SAVEUTL4                                                 
         B     XIT                                                              
         SPACE 1                                                                
XIT      XIT1                                                                   
         EJECT                                                                  
*              CONSTANTS, TABLES, LTORG                                         
         SPACE 3                                                                
OPENSW   DC    X'00'                                                            
AUTL     DC    A(0)                                                             
SAVEUTL4 DC    X'00'                                                            
TALUTL   DC    X'10'                                                            
         LTORG                                                                  
         DS    0D                                                               
TALIO    DC    2000X'00'                                                        
         SPACE 1                                                                
POOLMAX  DC    F'4000'                                                          
POOLSSN  DC    24000X'00'                                                       
         EJECT                                                                  
*              MY WORKING STORAGE                                               
         SPACE 3                                                                
MYD      DSECT                                                                  
MYSTART  DS    0C                                                               
BSSN     DS    F                                                                
APARMS   DS    A                                                                
         DS    0F                                                               
TALKEY   DS    CL48                                                             
TALSAVE  DS    CL48                                                             
TALWORK  DS    CL96                                                             
         SPACE 1                                                                
*                                  AGENCY FIELDS                                
         SPACE 1                                                                
MYEND    DS    0D                                                               
         EJECT                                                                  
         EJECT                                                                  
*              OTHER DSECTS ARE HIDDEN IN HERE                                  
         SPACE 3                                                                
*TAGENFILE                                                                      
*DDSPLWORKD                                                                     
*DDMASTD                                                                        
*CTGENFILE                                                                      
*FAFACTS                                                                        
*FATIOB                                                                         
*ACWRIFFD                                                                       
         PRINT OFF                                                              
       ++INCLUDE TAGENFILE                                                      
       ++INCLUDE DDSPLWORKD                                                     
       ++INCLUDE DDMASTD                                                        
       ++INCLUDE CTGENFILE                                                      
       ++INCLUDE FAFACTS                                                        
       ++INCLUDE FATIOB                                                         
       ++INCLUDE ACWRIFFD                                                       
         PRINT ON                                                               
         ORG   CONTAGH                                                          
         EJECT                                                                  
       ++INCLUDE DDGENTWA                                                       
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'011TALUPGUA  05/01/02'                                      
         END                                                                    
