*          DATA SET FAGETFACT  AT LEVEL 011 AS OF 11/18/20                      
*PROCESS USING(WARN(15))                                                        
*CATALP GETFACT                                                                 
*&&NOP   SET   N                                                                
***********************************************************************         
* P1 0   X'00' IF STANDARD TIME FORMAT REQUIRED (P'0HHMMSSC')         *         
*        X'01' IF TIME IN BINARY SECONDS REQUIRED                     *         
*        X'02' IF TIME IN TU'S REQUIRED                               *         
*        X'80' IF P2 CONTAINS EXTRACT VALUE                           *         
* P1 1-3 A(OUTPUT BLOCK) ONLY VALID FOR SPECIFIC VALUES IN US         *         
*        IF ZERO A(FABLOCK) IN MY WORKING STORAGE IS PASSED BACK      *         
*                                                                     *         
* P2 0   SE NUMBER IF SPECIAL                                         *         
*                                                                     *         
* P3     A(UTL) IF SSBTKADR=ZERO                                      *         
*                                                                     *         
* P3 1-3 00NNNN VALUE FOR EXTRACT                                     *         
***********************************************************************         
         TITLE 'FORMAT FACPAK SYSTEM INFO BLOCK'                                
         PRINT NOGEN                                                            
GETFACT  CSECT                                                                  
         ENTRY SYSLST                                                           
         NMOD1 WORKX-WORKD,**GETFAC,RA,RR=R2                                    
         USING WORKD,RC            RC=A(W/S)                                    
         SAM24                     SET IN 24-BIT MODE                           
*                                                                               
         ST    R2,RELO                                                          
         LR    R2,R1               R2=A(PARM LIST)                              
         SR    R3,R3                                                            
         ICM   R3,7,1(R2)          R3=A(OUTPUT BLOCK)                           
         BZ    GETF002                                                          
*&&UK*&& B     GETF003                                                          
*&&US                                                                           
         TM    0(R2),X'80'         TEST CALL TO RETURN SPECIFIC VALUE           
         BO    GETF003             YES-OK TO PASS ADDRESS                       
         DC    H'0'                DIE IF PASS ADDRESS FOR ALL VALUES           
*&&                                                                             
GETF002  LA    R3,FABLOCK          SET ADDRESS OF LOCAL OUTPUT BLOCK            
         STCM  R3,7,1(R2)          AND RETURN ADDRESS TO CALLER                 
*                                                                               
GETF003  STAM  ARE,AR1,DMCB                                                     
         TIME  TU                                                               
         ST    R0,TIMETUS          TIME IN TU'S (1/38400 SEC)                   
         LAM   ARE,AR1,DMCB                                                     
*                                                                               
         L     R1,=V(SYSFAC)       EXTRACT A TYPES                              
         L     R1,VSSB-SYSFACD(R1)                                              
         ST    R1,ASSB                                                          
         ICM   R1,15,SSBTKADR-SSBD(R1)                                          
         BZ    GETF004                                                          
         ST    R1,ATCB                                                          
         L     R1,TCBUTL-TCBD(R1)                                               
         MVC   ASELIST+1(3),TASYS-UTLD(R1)                                      
         ST    R1,AUTL                                                          
         B     GETF005                                                          
*                                                                               
GETF004  MVC   AUTL,8(R2)          A(UTL) IN P3 IF NO TASK                      
*                                                                               
GETF005  TM    0(R2),X'80'         TEST FOR EXTRACT CALL                        
         BO    GETX010             GET EXTRACTS                                 
         B     GETF010                                                          
*                                                                               
XMOD     XMOD1 1                                                                
         EJECT                                                                  
***********************************************************************         
* CLEAR FACTSD BLOCK AND COMPLETE IT                                  *         
***********************************************************************         
         USING FACTSD,R3           R3=A(SYSTEM INFO BLOCK)                      
GETF010  XC    FACTSD(FACTSLEN),FACTSD                                          
*                                                                               
         MVC   FATWASVR,=V(TWASVR) VALUES FROM V TYPES                          
         MVC   FAXLATES,=V(CTRYXLAT)                                            
*                                                                               
GETSYSF  L     R4,=V(SYSFAC)       VALUES FROM SYSFACS                          
         MVC   FAPOWWOW,VPOWWOW-SYSFACD(R4)                                     
*                                                                               
GETSSB   L     R4,VSSB-SYSFACD(R4) VALUES FROM SSB                              
         USING SSBD,R4                                                          
         MVC   FADATE,SSBDATE                                                   
         MVC   FADATEB,SSBDATEB                                                 
         MVC   FASYSID,SSBSYSID                                                 
         MVC   FAACTRY,SSBACTRY                                                 
         MVC   FAALANG,SSBALANG                                                 
         MVC   FADSPACE,SSBDSPAC                                                
         LH    R1,SSBTWAS                                                       
         SRL   R1,1                                                             
         AHI   R1,-2                                                            
         STH   R1,FATWAS           NUMBER OF EXTRA TWAS                         
         MVC   FATMAXIO,SSBMAXIO                                                
*                                                                               
         MVC   SVMODE,SSBMODE                                                   
         MVC   FATMPRL,SSBTWAL                                                  
         MVC   FATMSRL,SSBTMSL                                                  
*                                                                               
GETTCB   L     R4,SSBTKADR         VALUES FROM TCB                              
         USING TCBD,R4                                                          
         MVC   FASYM,TCBSYM                                                     
         ICM   R1,15,TCBSCRPT      IF SCRIPT RUNNING LOG CALLERS TCB            
         BZ    GETTCB2                                                          
         MVC   FASYM,TCBSYM-TCBD(R1)                                            
*                                                                               
GETTCB2  MVC   FATIME,TIMETUS      TIME IN TU'S (1/38400 SEC)                   
         MVC   FATIOCNT(2),TCBIOCNT+1                                           
         MVC   FATIOCNX(1),TCBIOCNT+0                                           
         MVC   FAPGMADR,TCBPGMA                                                 
*                                                                               
         TM    SVMODE,SSBMODIO     TEST 31-BIT MODE                             
         BO    GETUTL              YES - IOA INACCESSABLE                       
         L     R1,TCBFILES         R1=A(TCB IOA)                                
         SR    R0,R0                                                            
         ICM   R0,3,6(R1)          GET NUM OF DBL WDRS USED IN IOA              
         BZ    GETUTL                                                           
         SLL   R0,3                R0=NUM OF BYTES USED IN IOA                  
         L     RF,TCBFILEX                                                      
         SR    RF,R1               GET TOTAL LENGTH OF IOA                      
         SR    RF,R0               MINUS THAT PART USED FOR FILES               
         BM    GETUTL                                                           
         ST    RF,FAIOALEN         SET LEN AND ADR OF AVAIL AREA                
         AR    R1,R0                                                            
         ST    R1,FAIOASTR                                                      
*                                                                               
GETUTL   L     R4,TCBUTL           VALUES FROM UTL                              
         USING UTLD,R4                                                          
         MVC   FASYS,TSYS                                                       
         MVC   FAOVSYS,TOVSYS                                                   
         MVC   FATTYPE,TTYPE                                                    
         MVC   FATSTAT,TSTAT                                                    
         MVC   FAPASSWD,TPASSWD                                                 
         MVC   FATSTAT8,TSTAT8                                                  
         MVC   FATSTAT9,TSTAT9                                                  
         SR    RE,RE                                                            
         ICM   RE,7,TASYS                                                       
         BZ    *+10                                                             
         MVC   FAFILSET,SEFILSET-SELISTD(RE)                                    
         MVC   FAOFFCDE,TOFFCODE                                                
         MVC   FACTRY,TAGCTRY                                                   
         MVC   FALANG,TLANG                                                     
         MVC   FATSTAT6,TSTAT6                                                  
         MVC   FATAGYSC,TAGYSEC                                                 
         MVC   FASIN,TSIN                                                       
         MVC   FATFLAG,TFLAG                                                    
         NI    FATFLAG,X'FF'-TFLAGHLP                                           
         MVC   FAJOBINF(3),TJOBINFO                                             
         LA    R0,SRJOBMAX                                                      
         STC   R0,FAJOBMAX                                                      
         LA    R1,SYSLST                                                        
         ST    R1,FASYSLST                                                      
         XR    RE,RE                                                            
         ICM   RE,7,TARSYS                                                      
         BZ    *+16                                                             
         MVC   FATARSE,SESYS-SELISTD(RE)                                        
         MVC   FATAROVS,SEOVSYS-SELISTD(RE)                                     
         CLI   0(R2),1                                                          
         BH    XMOD                                                             
*                                                                               
GETTIM   L     R1,TIMETUS          TIME IN TU'S (1/38400 SEC)                   
         SR    R0,R0                                                            
         D     R0,=F'38400'        CONVERT TO SECONDS                           
         ST    R1,FATIME                                                        
         CLI   0(R2),0                                                          
         BH    XMOD                                                             
*                                                                               
         ZAP   FATIME,=P'0'        CONVERT SECONDS TO STANDARD FORMAT           
         SR    R0,R0                                                            
         D     R0,=F'60'                                                        
         CVD   R0,DUB                                                           
         AP    FATIME,DUB                                                       
         SR    R0,R0                                                            
         D     R0,=F'60'                                                        
         CVD   R0,DUB                                                           
         MP    DUB,=P'100'                                                      
         AP    FATIME,DUB                                                       
         CVD   R1,DUB                                                           
         MP    DUB,=P'10000'                                                    
         AP    FATIME,DUB                                                       
         B     XMOD                                                             
         DROP  R3,R4                                                            
         EJECT                                                                  
***********************************************************************         
* GET SELIST ENTRY FROM SE NUMBER                                     *         
***********************************************************************         
GETSE    NTR1                                                                   
         L     R5,=V(SELIST)       SEARCH SELIST FOR NUMBER                     
         LH    R6,0(R5)                                                         
         L     R7,2(R5)                                                         
         LA    R5,6(R5)                                                         
         USING SELISTD,R5                                                       
         CLC   SESYS,4(R2)                                                      
         BE    *+10                                                             
         BXLE  R5,R6,*-10                                                       
         SR    R5,R5                                                            
         ST    R5,ASELIST                                                       
         XIT1                                                                   
         DROP  R5                                                               
         EJECT                                                                  
***********************************************************************         
* EXTRACT VALUES FROM UTL/TCB/SSB                                     *         
***********************************************************************         
GETX010  SR    R1,R1               P2+2(2) HAS EXTRACT FIELD NUMBER             
         ICM   R1,3,6(R2)                                                       
         BZ    GETXERR             ERROR IF ZERO                                
         CH    R1,GETXMAX                                                       
         BH    GETXERR             ERROR IF GREATER THAN MAX                    
*                                                                               
         CLI   4(R2),0             TEST SE NUMBER PASSED                        
         BE    GETX020                                                          
         BAS   RE,GETSE            SEARCH SELIST FOR SE NUMBER                  
         OC    ASELIST,ASELIST                                                  
         BZ    GETXERR             ERROR IF SE NUMBER INVALID                   
*                                                                               
GETX020  STH   R1,HALF             SAVE R1                                      
         BCTR  R1,0                SUB 1 FOR START                              
         SLL   R1,3                R1=R1*8                                      
         A     R1,=A(GETXTAB)      INDEX INTO TABLE                             
         A     R1,RELO                                                          
*                                                                               
         CLC   HALF,0(R1)          VERIFY INDEXING                              
         JNE   *+2                 DIE IF TABLE IS OUT OF SEQUENCE              
*                                                                               
         CLI   3(R1),C'T'          TEST TERMINAL UTL SOURCE                     
         BE    SOURCET                                                          
         CLI   3(R1),C'B'          TEST TCB SOURCE                              
         BE    SOURCEB                                                          
         CLI   3(R1),C'S'          TEST SSB SOURCE                              
         BE    SOURCES                                                          
         CLI   3(R1),C'E'          TEST SELIST SOURCE                           
         BE    SOURCEE                                                          
         CLI   3(R1),C'*'          TEST EXECUTE ROUTINE                         
         BE    SOURCE                                                           
         DC    H'0'                DIE IF UNKNOWN SOURCE                        
*                                                                               
GETXERR  LA    R0,FABLOCK          TEST IF CALLER PASSED RETURN AREA            
         CR    R0,R3                                                            
         BNE   XMOD                EXIT IF CALLER PASSED OWN AREA               
         XC    0(FACTSLEN,R3),0(R3)                                             
         B     XMOD                CLEAR MY RETURN AREA AND EXIT                
*                                                                               
SOURCET  L     R4,AUTL             SET R4 TO UTL                                
         B     GETX050                                                          
SOURCEB  L     R4,ATCB             SET R4 TO TCB                                
         B     GETX050                                                          
SOURCES  L     R4,ASSB             SET R4 TO SSB                                
         B     GETX050                                                          
SOURCEE  L     R4,ASELIST          SET R4 TO A(SELIST ENTRY)                    
         B     GETX050                                                          
*                                                                               
SOURCE   SR    RF,RF               PICK UP LENGTH (RF)                          
         ICM   RF,3,4(R1)                                                       
         BZ    SOURCE1                                                          
         BCTR  RF,0                                                             
         EX    RF,*+8              ZERO OUTPUT BLOCK                            
         B     *+10                                                             
         XC    0(0,R3),0(R3)                                                    
*                                                                               
SOURCE1  SR    RF,RF               RF=ROUTINE NUMBER                            
         ICM   RF,3,6(R1)                                                       
         SLL   RF,2                                                             
         EX    0,*(RF)             EXECUTED BRANCH                              
*                                                                               
EXUTLQ   EQU   1                   ROUTINES & EQUATES                           
         B     EXUTL                                                            
EXTCBQ   EQU   2                                                                
         B     EXTCB                                                            
EXSSBQ   EQU   3                                                                
         B     EXSSB                                                            
EXSELQ   EQU   4                                                                
         B     EXSEL                                                            
EXPEEKQ  EQU   5                                                                
         B     EXPEEK                                                           
EXPEEK1Q EQU   6                                                                
         B     EXPEEK1                                                          
EXTRIOQ  EQU   7                                                                
         B     EXTRIO                                                           
EXDDSTQ  EQU   8                                                                
         B     EXDDSTAB                                                         
EXDDSEQ  EQU   9                                                                
         B     EXDDSENT                                                         
EXSESAVQ EQU   10                                                               
         B     EXSESAV                                                          
EXSESPRQ EQU   11                                                               
         B     EXSESPR                                                          
EXSESUNQ EQU   12                                                               
         B     EXSESUN                                                          
EXDEBQ   EQU   13                                                               
         B     EXDEBUG                                                          
EXMIOSTQ EQU   14                                                               
         B     EXMIOST                                                          
EXMIOUNQ EQU   15                                                               
         B     EXMIOUN                                                          
EXWRITEQ EQU   16                                                               
         B     EXWRITE                                                          
EXWAITQ  EQU   17                                                               
         B     EXWAIT                                                           
SETUSERQ EQU   18                                                               
         B     SETUSER                                                          
EXWSSVRQ EQU   19                                                               
         B     EXWSSVR                                                          
EXUPDNQ  EQU   20                                                               
         B     EXUPDN                                                           
EXUPDYQ  EQU   21                                                               
         B     EXUPDY                                                           
EXBTBSQ  EQU   22                                                               
         B     EXBTBS                                                           
EXBTBUQ  EQU   23                                                               
         B     EXBTBU                                                           
EXTUTMQ  EQU   24                                                               
         B     EXTUTM                                                           
EXELTMQ  EQU   25                                                               
         B     EXELTM                                                           
EXRETMQ  EQU   26                                                               
         B     EXRETM                                                           
EXMAXIO  EQU   27                                                               
         B     EXMAXI                                                           
EXTKTSQ  EQU   28                                                               
         B     EXTKTS                                                           
BCASTSET EQU   29                                                               
         B     BCASTS                                                           
GETSYSAD EQU   30                                                               
         B     GETSYSA                                                          
SETTTEST EQU   31                                                               
         B     SETTTES                                                          
EXXTSETQ EQU   32                                                               
         B     EXXTSET                                                          
EXLOCKER EQU   33                                                               
         B     EXLOCKR                                                          
EXUPDVQ  EQU   34                                                               
         B     EXUPDV                                                           
EXOSIMQ  EQU   35                                                               
         B     EXOSIM                                                           
EXOSISQ  EQU   36                                                               
         B     EXOSIS                                                           
EXTAROVQ EQU   37                                                               
         B     EXTAROV                                                          
EXTARSEQ EQU   38                                                               
         B     EXTARSE                                                          
CHKITSKQ EQU   39                                                               
         B     CHKITASK            CHECK TO SEE ADDRESS WITH IN TASK            
SETDDLPQ EQU   40                                                               
         B     SETDDLP                                                          
UNSDDLPQ EQU   41                                                               
         B     UNSDDLP                                                          
FAL1STEQ EQU   42                                                               
         B     FAL1STEL            GET FIRST ELEMENT FOR FALINK TBUFF           
FARUNQUE EQU   43                                                               
         B     FARUNQ              FIND NUM OF JOBS IN CLASS FOR USER           
FAIOADDR EQU   44                                                               
         B     FAIOADD             RETURN ADDR OF TCB I/O COUNT                 
FAXA9ADR EQU   45                                                               
         B     FAXA9AD             RETURN ADDR/LEN OF XA9 AREA                  
FASEADV  EQU   46                                                               
         B     FASEAD              SE SYSTEM NATIVE TO THIS ADV Y/N             
EXDDUMPQ EQU   47                                                               
         B     FADDUMP             ALLOW DUP DUMP FOR TASK THIS TIME            
EXSCRIPT EQU   48                                                               
         B     FASCRIPT            GET EXECUTING SCRIPT NAME                    
EXVSAM#S EQU   49                                                               
         B     VSAM#S              GET A(VSAM COUNTER ARRAY)                    
         EJECT                                                                  
***********************************************************************         
* EXTRACT VALUE AND EXIT                                              *         
* R4=A(SOURCE) / R3=A(OUTPUT) / R1=A(TABLE ENTRY)                     *         
***********************************************************************         
GETX050  SR    RF,RF               PICK UP LENGTH (RF)                          
         ICM   RF,3,4(R1)                                                       
         BCTR  RF,0                SUB 1 FOR EXECUTE                            
*                                                                               
         SR    RE,RE               PICK UP OFFSET (RE)                          
         ICM   RE,3,6(R1)                                                       
         AR    RE,R4               ADD R4 FOR SOURCE                            
*                                                                               
         EX    RF,*+8              MOVE TO OUTPUT AREA                          
         B     *+10                                                             
         MVC   0(0,R3),0(RE)                                                    
*                                                                               
         B     XMOD                AND EXIT                                     
         EJECT                                                                  
***********************************************************************         
* EXTRACT UTL BLOCK                                                   *         
***********************************************************************         
EXUTL    L     R4,AUTL                                                          
         USING UTLD,R4             R4=UTL                                       
         USING F@UTLD,R3           R3=OUTPUT BLOCK                              
*                                                                               
         MVC   F@TSTAT1,TSTAT1                                                  
         MVC   F@TSTAT2,TSTAT2                                                  
         MVC   F@TSYS,TSYS                                                      
         MVC   F@TPRG,TPRG                                                      
         MVC   F@TPASS,TPASSWD                                                  
         MVC   F@TPERS,TPERSON                                                  
         MVC   F@TTYPE,TTYPE                                                    
         MVC   F@TSYM,TSYM                                                      
         MVC   F@TSIN,TSIN                                                      
         MVC   F@TFLAG,TFLAG                                                    
         MVC   F@TLANG,TLANG                                                    
         MVC   F@TCTRY,TCTRY                                                    
         MVC   F@TSTAT6,TSTAT6                                                  
         MVC   F@TSTAT8,TSTAT8                                                  
         MVC   F@TSTAT9,TSTAT9                                                  
         MVC   F@TSTATU,TSTATU                                                  
         MVC   F@TAGYSC,TAGYSEC                                                 
         MVC   F@TOVSYS,TOVSYS                                                  
         MVC   F@TAGYB,TAGYB                                                    
         MVC   F@TTEST,TTEST                                                    
         MVC   F@TACCS,TACCS                                                    
         MVC   F@TACCS2,TACCS2                                                  
         MVC   F@TNUM,TNUM                                                      
         MVC   F@TUSER,TUSER                                                    
         MVC   F@TAUTH,TAUTH                                                    
         MVC   F@TSAGN,TSAGN                                                    
         MVC   F@TAGY,TAGY                                                      
         MVC   F@TOFFCD,TOFFCODE                                                
         MVC   F@TAGCTR,TAGCTRY                                                 
         MVC   F@TAGCOP,TAGCOPT                                                 
         MVC   F@TAGCUR,TAGCURR                                                 
         MVC   F@TSTAT5,TSTAT5                                                  
         MVC   F@TSTATB,TSTATB                                                  
         MVC   F@TAGYPE,TAGYPER                                                 
         MVC   F@TICKET,TTICKET                                                 
         MVC   F@TXPINF,TXPINFO                                                 
         XR    RE,RE                                                            
         ICM   RE,7,TARSYS                                                      
         BZ    *+16                                                             
         MVC   F@TARSE,SESYS-SELISTD(RE)                                        
         MVC   F@TAROVS,SEOVSYS-SELISTD(RE)                                     
         MVC   F@TSTATC,TSTATC                                                  
         MVC   F@RUNTAB,TRUNTAB                                                 
*                                                                               
         B     XMOD                                                             
         DROP  R3,R4                                                            
         EJECT                                                                  
***********************************************************************         
* EXTRACT TCB BLOCK                                                   *         
***********************************************************************         
EXTCB    L     R4,ATCB                                                          
         USING TCBD,R4             R4=TCB                                       
         USING F@TCBD,R3           R3=OUTPUT BLOCK                              
*                                                                               
         MVC   F@BIOCNT,TCBIOCNT+1                                              
         MVC   F@BRCVC,TCBRCVC                                                  
         MVC   F@BBILL,TCBBILL                                                  
         MVC   F@BSWNUM,TCBSWNUM                                                
         MVC   F@BOVSYS,TCBOVSYS                                                
         MVC   F@BIOCNA,TCBIOCNT                                                
         MVC   F@BTWA,TCBTWA                                                    
         MVC   F@BXTINF,TCBXTINF                                                
*                                                                               
EXTCB01  LA    R0,F@BSWMAX         SWITCH TAB LOOP                              
         LA    RF,TCBSWTAB                                                      
         LA    RE,F@BSWTAB                                                      
*                                                                               
EXTCB02  MVC   F@BSWSYS-F@BSWTAB(1,RE),TCBSWSYS-TCBSWTAB(RF)                    
         MVC   F@BSWSOV-F@BSWTAB(1,RE),TCBSWSOV-TCBSWTAB(RF)                    
         MVC   F@BSWAGB-F@BSWTAB(1,RE),TCBSWAGB-TCBSWTAB(RF)                    
         MVC   F@BSWACS-F@BSWTAB(4,RE),TCBSWACS-TCBSWTAB(RF)                    
         MVC   F@BSWAC2-F@BSWTAB(4,RE),TCBSWAC2-TCBSWTAB(RF)                    
*                                                                               
         LA    RF,TCBSWLEN(RF)                                                  
         LA    RE,F@BSWLEN(RE)                                                  
         BCT   R0,EXTCB02          END SWITCH TAB LOOP                          
*                                                                               
         B     XMOD                                                             
         DROP  R3,R4                                                            
         EJECT                                                                  
***********************************************************************         
* CHECK TO SEE IF ADDRESS PASSED IS IN RANGE                          *         
***********************************************************************         
CHKITASK L     R4,ASSB             A(SSB)                                       
         USING SSBD,R4                                                          
         LHI   RF,-1               SET NOT IN TASK                              
         ICM   R3,15,SSBTKADR                                                   
         BZ    CHKITSKX            EXIT WITH CC=LOW IF NO TASK                  
         USING TCBD,R3                                                          
         LHI   RF,1                SET NOT IN TASK AREA                         
         L     R5,TCBWRKA                                                       
         C     R5,8(R2)            TEST ADDRESS WITH START OF TASK              
         BH    CHKITSKX                                                         
         L     R5,TCBPGMX                                                       
         C     R5,8(R2)            TEST ADDRESS WITH END OF TASK                
         BL    CHKITSKX                                                         
         XR    RF,RF               EXIT WITH CC=EQL IF IN TASK AREA             
*                                                                               
CHKITSKX CHI   RF,0                SET CC                                       
         B     XMOD                                                             
         DROP  R3,R4                                                            
***********************************************************************         
* EXTRACT SSB BLOCK                                                   *         
***********************************************************************         
EXSSB    L     R4,ASSB                                                          
         USING SSBD,R4             R4=SSB                                       
         USING F@SSBD,R3           R3=OUTPUT BLOCK                              
*                                                                               
         MVC   F@SDATEB,SSBDATEB                                                
         MVC   F@SDATE,SSBDATE                                                  
         MVC   F@SSYSID,SSBSYSID                                                
         MVC   F@SSYSNA,SSBSYSNA                                                
         MVC   F@SMAXIO,SSBMAXIO                                                
         MVC   F@SSTAT3,SSBSTAT3                                                
         MVC   F@SSYSFL,SSBSYSFL                                                
         MVC   F@STSAR,SSBTSAR                                                  
         MVC   F@SCTACT,SSBCTACT                                                
         MVC   F@SSMAX,SSBSSMAX                                                 
         MVC   F@SAFID,SSBAFID                                                  
         MVC   F@SSYSN4,SSBSYSN4                                                
         MVC   F@SSYSN1,SSBSYSN1                                                
         MVC   F@SSYSCH,SSBSYSCH                                                
         MVC   F@DSPACE,SSBDSPAC                                                
*                                                                               
         B     XMOD                                                             
         DROP  R3,R4                                                            
         EJECT                                                                  
***********************************************************************         
* EXTRACT SELIST BLOCK                                                *         
***********************************************************************         
EXSEL    L     R4,ASELIST                                                       
         USING SELISTD,R4          R4=SELIST                                    
         USING F@SELISD,R3         R3=OUTPUT BLOCK                              
*                                                                               
         MVC   F@SENAME,SENAME                                                  
         MVC   F@SESYS,SESYS                                                    
         MVC   F@SEOV,SEOVSYS                                                   
         MVC   F@SEFILS,SEFILSET                                                
         MVC   F@SEIND,SEIND                                                    
*                                                                               
         B     XMOD                                                             
         DROP  R3,R4                                                            
         EJECT                                                                  
***********************************************************************         
* EXTRACT A(IAMTAB)                                                   *         
***********************************************************************         
EXPEEK   MVC   0(4,R3),=V(IAMTAB)  RETURN IAMTAB                                
         B     XMOD                                                             
EXPEEK1  ICM   R1,15,=V(IAMTAB)                                                 
         LH    RE,0(R1)            SET BXLE                                     
         L     RF,2(R1)                                                         
         LA    R1,6(R1)                                                         
         L     R0,AUTL                                                          
EXPK010  C     R0,0(R1)            FIND LINKED TERMINAL                         
         BE    EXPK020                                                          
         BXLE  R1,RE,EXPK010                                                    
         XC    0(4,R3),0(R3)       EXIT WITH ZERO IF NOT FOUND                  
         B     XMOD                                                             
*                                                                               
EXPK020  L     RF,4(R1)            TEST TERMINAL FOR IAMD FLAG                  
         TM    TSTAT6-UTLD(RF),TST6IAMD                                         
         BZ    EXPK800             CLEAR LINK IF NOT FLAGGED                    
*                                                                               
         TM    TSTAT1-UTLD(RF),TSTATDDS                                         
         BNZ   EXPK990             OK IF DDS TERMINAL DEST                      
*                                                                               
         L     RE,0(R1)            GET A(SENDER UTL)                            
         CLC   TUSER-UTLD(2,RE),TUSER-UTLD(RF)                                  
         BE    EXPK990             OK IF SAME USERID                            
*                                                                               
EXPK800  NI    TSTAT6-UTLD(RF),255-TST6IAMD                                     
         SR    RF,RF                                                            
         XC    0(8,R1),0(R1)       CLEAR LINK                                   
*                                                                               
EXPK990  ST    RF,0(R3)            RETURN A(LINKED UTL)                         
         B     XMOD                                                             
         EJECT                                                                  
***********************************************************************         
* EXTRACT ACTUAL IO COUNT - RETURN AS FULL WORD                       *         
***********************************************************************         
EXTRIO   L     R4,ATCB                                                          
         USING TCBD,R4                                                          
         SR    R1,R1                                                            
         ICM   R1,7,TCBIOCNT                                                    
         ST    R1,0(R3)                                                         
         B     XMOD                                                             
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
* EXTRACT ADDRESS OF TCB IO COUNT FOR HEAVY USERS LIKE UK DEMAND      *         
***********************************************************************         
FAIOADD  L     R4,ATCB                                                          
         USING TCBD,R4                                                          
         LA    R1,TCBIOCNT                                                      
         ST    R1,0(R3)                                                         
         B     XMOD                                                             
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
* PASS ADDRESS AND LEN OF XA9 AREA                                    *         
***********************************************************************         
FAXA9AD  L     R4,ATCB                                                          
         USING TCBD,R4             R4=TCB                                       
         L     R5,ASSB                                                          
         USING SSBD,R5             R5=SSB                                       
         MVC   0(4,R3),TCBAXA9     SET A(XA9 AREA)                              
         XR    R1,R1                                                            
         ICM   R1,1,SSBXA9MB       LEN IN MB                                    
         SLL   R1,20               CONVERT TO BYTES                             
         ST    R1,4(R3)            SAVE AT +4                                   
         B     XMOD                                                             
         DROP  R4,R5                                                            
         EJECT                                                                  
***********************************************************************         
* SE SYSTEM NATIVE TO THIS ADV Y/N                                    *         
***********************************************************************         
FASEAD   LLC   R1,4(R2)            PASSED SE NUMBER                             
         L     R4,=V(SYSSTAB)                                                   
         AR    R4,R1               INDEX INTO SSYSTAB                           
*                                                                               
         MVI   0(R3),C'N'                                                       
*                                                                               
         L     R1,ASSB                                                          
*                                                                               
         CLI   SSBDSPAC-SSBD(R1),C'C'                                           
         BE    FASEAD10                                                         
         CLI   SSBDSPAC-SSBD(R1),C'Q'                                           
         BE    FASEAD10                                                         
*                                                                               
         CLI   0(R4),X'FF'         CHECK IF OPEN EVERYWHERE                     
         BE    FASEAD10                                                         
*                                                                               
         CLC   SSBSYSID-SSBD(1,R1),0(R4)  SYSTEM WE'RE ON RIGHT NOW             
         BNE   XMOD                                                             
*                                                                               
FASEAD10 MVI   0(R3),C'Y'                                                       
         B     XMOD                                                             
         EJECT                                                                  
***********************************************************************         
* EXTRACT DDS SECURITY TABLE ADDR / ENTRY                             *         
***********************************************************************         
EXDDSTAB MVC   0(4,R3),=V(DDSDLTAB) RETURN A(DDSDLTAB)                          
         B     XMOD                                                             
*                                                                               
EXDDSENT L     R4,AUTL             RETURN DDSDLTAB ENTRY FOR USER               
         USING UTLD,R4             R4=UTL                                       
         LLC   R1,TDDSDPLV         R1=DEPT LEVEL                                
         SLL   R1,3                *8                                           
         A     R1,=V(DDSDLTAB)     +TABLE ADDR                                  
         MVC   0(8,R3),0(R1)       RETURN 64 SECURITY BITS                      
         B     XMOD                                                             
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
* SESSION EXTRACTS                                                    *         
***********************************************************************         
EXSESAV  L     R4,AUTL             RETURN NUMBER AVAILABLE SESSIONS             
         USING UTLD,R4             R4=UTL                                       
         L     R5,ASSB                                                          
         USING SSBD,R5             R5=SSB                                       
         SR    R1,R1                                                            
         LLC   R0,TSSBITS                                                       
         LH    RF,SSBSSMAX         RF=MAX SESSIONS                              
         LA    R6,8                                                             
EXSESAV1 SRDL  R0,1                TEST EACH SESSION                            
         LTR   R1,R1                                                            
         BZ    *+6                                                              
         BCTR  RF,0                DEC RF BY 1 FOR EACH ACTIVE                  
         SR    R1,R1                                                            
         BCT   R6,EXSESAV1                                                      
         ST    RF,0(R3)            RETURN NUMBER OF FREE SESSIONS               
         B     XMOD                                                             
*                                                                               
EXSESPR  L     R4,AUTL             PROTECT THIS SESSION                         
         GOTO1 VPROTOFF            DISABLE CALLER'S STORAGE PROTECTION          
         LLC   R1,TSESSION                                                      
         IC    RE,SSACBITS(R1)                                                  
         EX    RE,*+8                                                           
         B     *+8                                                              
         OI    TSSXBITS,0                                                       
         GOTO1 VPROTON             RESTORE CALLER'S STORAGE PROTECTION          
         B     XMOD                                                             
*                                                                               
EXSESUN  L     R4,AUTL             UNPROTECT THIS SESSION                       
         GOTO1 VPROTOFF            DISABLE CALLER'S STORAGE PROTECTION          
         LLC   R1,TSESSION                                                      
         IC    RE,SSNABITS(R1)                                                  
         EX    RE,*+8                                                           
         B     *+8                                                              
         NI    TSSXBITS,0                                                       
         GOTO1 VPROTON             RESTORE CALLER'S STORAGE PROTECTION          
         B     XMOD                                                             
*                                                                               
SSACBITS DC    X'0102040810204080'                                              
SSNABITS DC    X'FEFDFBF7EFDFBF7F'                                              
         EJECT                                                                  
***********************************************************************         
*MISCELLANEOUS SERVICES                                               *         
***********************************************************************         
EXMIOST  L     R4,AUTL             SET ALLOWED TO EXCEED MAXIOS                 
         GOTO1 VPROTOFF            DISABLE CALLER'S STORAGE PROTECTION          
         OI    TSTATU,TSTATMIO                                                  
         GOTO1 VPROTON             RESTORE CALLER'S STORAGE PROTECTION          
         B     XMOD                                                             
*                                                                               
EXMIOUN  L     R4,AUTL             TURN OFF ALLOWED TO EXCEED MAXIOS            
         GOTO1 VPROTOFF            DISABLE CALLER'S STORAGE PROTECTION          
         NI    TSTATU,255-TSTATMIO                                              
         GOTO1 VPROTON             RESTORE CALLER'S STORAGE PROTECTION          
         B     XMOD                                                             
*                                                                               
EXUPDN   L     R4,AUTL             SET UPDATE=NO                                
         GOTO1 VPROTOFF            DISABLE CALLER'S STORAGE PROTECTION          
         OI    TTEST,TTESTNOU+TTESTUEN                                          
         NI    TTEST,255-TTESTUEV                                               
         GOTO1 VPROTON             RESTORE CALLER'S STORAGE PROTECTION          
         B     XMOD                                                             
*                                                                               
EXUPDV   L     R4,AUTL             SET UPDATE=V                                 
         GOTO1 VPROTOFF            DISABLE CALLER'S STORAGE PROTECTION          
         OI    TTEST,TTESTNOU+TTESTUEN+TTESTUEV                                 
         GOTO1 VPROTON             RESTORE CALLER'S STORAGE PROTECTION          
         B     XMOD                                                             
*                                                                               
EXUPDY   L     R4,AUTL             SET UPDATE=Y                                 
         GOTO1 VPROTOFF            DISABLE CALLER'S STORAGE PROTECTION          
         NI    TTEST,255-TTESTNOU-TTESTUEN-TTESTUEV                             
         GOTO1 VPROTON             RESTORE CALLER'S STORAGE PROTECTION          
         B     XMOD                                                             
*                                                                               
EXOSIM   L     R4,AUTL             MAKE ORIGINAL SYSTEM INPUT NUMBER            
         MVC   0(4,R3),TSIN                                                     
         L     R5,ASSB                                                          
         MVC   0(1,R3),SSBSYSID                                                 
         B     XMOD                                                             
*                                                                               
EXOSIS   L     R4,AUTL             SET ORIGINAL SYSTEM INPUT NUMBER             
         GOTO1 VPROTOFF            DISABLE CALLER'S STORAGE PROTECTION          
         MVC   TOSIN,0(R3)                                                      
         GOTO1 VPROTON             RESTORE CALLER'S STORAGE PROTECTION          
         B     XMOD                                                             
*                                                                               
EXTAROV  L     R4,AUTL             GET GET CONNECTED (TARGET) OVSYS             
         XR    RE,RE                                                            
         ICM   RE,7,TARSYS                                                      
         BZ    XMOD                                                             
         MVC   0(1,R3),SEOVSYS-SELISTD(RE)                                      
         B     XMOD                                                             
*                                                                               
EXTARSE  L     R4,AUTL             GET GET CONNECTED (TARGET) SE                
         XR    RE,RE                                                            
         ICM   RE,7,TARSYS                                                      
         BZ    XMOD                                                             
         MVC   0(1,R3),SESYS-SELISTD(RE)                                        
         B     XMOD                                                             
*                                                                               
EXWSSVR  L     RF,=V(SYSFAC)       GET WSSSVR ADDRESS                           
         L     RF,VWSSVR-SYSFACD(RF)                                            
         ST    RF,0(R2)                                                         
         B     XMOD                                                             
*                                                                               
         DROP  R4,R5                                                            
                                                                                
         EJECT                                                                  
***********************************************************************         
* TURN ON FLAG IN TCB TO ALLOW DUPLICATE DUMP                         *         
***********************************************************************         
FADDUMP  L     R4,ATCB                                                          
         USING TCBD,R4                                                          
         GOTO1 VPROTOFF            DISABLE CALLER'S STORAGE PROTECTION          
         OI    TCBINDS1,TCBFABDD                                                
         GOTO1 VPROTON             RESTORE CALLER'S STORAGE PROTECTION          
         B     XMOD                                                             
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
* GET EXECUTING SCRIPT NAME                                           *         
***********************************************************************         
                                                                                
FASCRIPT L     R4,ATCB                                                          
         USING TCBD,R4                                                          
         GOTO1 VPROTOFF            DISABLE CALLER'S STORAGE PROTECTION          
         LT    R4,TCBSCRPT                                                      
         JZ    *+2                 NOT A SCRIPT                                 
         L     R4,TCBWRKA                                                       
         LA    R0,20                                                            
FASCRIP1 CLC   0(4,R4),=C'SCRP'    FIND SCRIPT WORK AREA                        
         JE    FASCRIP2                                                         
         L     R4,8(R4)                                                         
         JCT   R0,FASCRIP1                                                      
         DC    H'0'                SCRIPT WS NOT FOUND                          
*                                                                               
FASCRIP2 AHI   R4,(18*4)           BUMP TO WS ADDR                              
         USING SCPWORKD,R4                                                      
         CLC   SCPATCB,ATCB        CONFIRM MY TCB ADDRESS                       
         JZ    *+2                                                              
         MVC   0(8,R3),SVKCODE     COPY NAME                                    
*                                                                               
         GOTO1 VPROTON             RESTORE CALLER'S STORAGE PROTECTION          
         B     XMOD                                                             
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
* EXTRACT VSAM COUNTERS FROM DMVSDEM (FOR DEBUGGING)                  *         
***********************************************************************         
VSAM#S   DS    0H                                                               
         MVC   0(4,R3),=V(DMVSCTRS) RETURN A(VSAM COUNTER TABLE)                
         B     XMOD                                                             
         EJECT                                                                  
***********************************************************************         
* SET TCB REGISTERS FROM CALLERS REGISTERS                            *         
***********************************************************************         
EXDEBUG  L     R4,ATCB                                                          
         USING TCBD,R4                                                          
         GOTO1 VPROTOFF            DISABLE CALLER'S STORAGE PROTECTION          
         LR    R1,RD               LOCATE CALLERS REGS                          
         L     R1,4(R1)                                                         
         CLC   0(4,R1),=C'GETF'                                                 
         JNE   *+2                 DIE IF INVALID CALL                          
         MVC   TCBREGS+00(52),20(R1)                                            
         MVC   TCBREGS+52(4),4(R1)                                              
         MVC   TCBREGS+56(8),12(R1)                                             
         GOTO1 VPROTON             RESTORE CALLER'S STORAGE PROTECTION          
         L     RF,=V(ADNEXT)                                                    
         BR    RF                                                               
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
* GET VTAM BUFFER AND WRITE SCREEN TO TERMINAL                        *         
***********************************************************************         
EXWRITE  L     R4,ATCB             WRITE SCREEN TO TERMINAL                     
         USING TCBD,R4                                                          
         L     R5,AUTL                                                          
         USING UTLD,R5                                                          
         GOTO1 =V(LCM),DMCB,VTGETBUF AQUIRE A FREE BUFFER                       
         LTR   R8,R1                                                            
         BZ    XMOD                EXIT IF NONE AVAILABLE                       
*                                                                               
         OI    TSTAT1,TSTATMWT     ALLOW MULTI WRITE                            
         LA    R0,SPRBUFF                                                       
         LHI   R1,TBUFFL+32                                                     
         XR    RE,RE                                                            
         XR    RF,RF                                                            
         MVCL  R0,RE                                                            
         XC    WORK,WORK                                                        
         GOTO1 =V(TO3270),DMCB,TCBTWA,SPRBUFF,TCBUTL,WORK                       
*                                                                               
         SAM31                                                                  
         LA    R0,SPRBUFF                                                       
         LHI   R1,TBUFFL+32                                                     
         LR    RE,R8                                                            
         AHI   RE,-32                                                           
         AHI   R0,-32                                                           
         LR    RF,R1                                                            
         MVCL  RE,R0                                                            
         SAM24                                                                  
*                                                                               
         GOTO1 =V(LCM),DMCB,VTWRITE,AUTL,(R8)                                   
*&&UK*&& NI    TSTAT1,255-TSTATMWT                                              
         B     XMOD                                                             
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
* DO TICTOC TIMER WAIT TO WAIT FOR N TIMER UNITS                      *         
***********************************************************************         
EXWAIT   ICM   R4,15,0(R3)         WAIT FOR N TIMER UNITS                       
         BZ    XMOD                                                             
**NOP**  TM    0(R3),X'80'         TEST FOR ECB MODE                            
**NOP**  BO    EXWAITEC                                                         
         GOTO1 VPROTOFF            DISABLE CALLER'S STORAGE PROTECTION          
         GOTO1 =V(TICTOC),DMCB,C'WAIT',(R4)                                     
         GOTO1 VPROTON             RESTORE CALLER'S STORAGE PROTECTION          
         B     XMOD                                                             
*                                                                               
EXWAITEC LR    R1,R4               R1=A(ECB)                                    
         GOTO1 =V(ADWAIT)                                                       
         B     XMOD                                                             
         EJECT                                                                  
***********************************************************************         
* SET USER DATA IN TCB SO THAT IT IS OUTPUT IN ADRFILE LOG RECORD     *         
***********************************************************************         
SETUSER  L     R4,ATCB             SET USER DATA                                
         USING TCBD,R4                                                          
         SR    R1,R1                                                            
         GOTO1 VPROTOFF            DISABLE CALLER'S STORAGE PROTECTION          
         MVC   TCBUDATA,0(R3)                                                   
         GOTO1 VPROTON             RESTORE CALLER'S STORAGE PROTECTION          
         B     XMOD                                                             
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
* SET/UNSET/RETURN TIME BOUND TRANSACTION DATA                        *         
***********************************************************************         
EXBTBS   L     R4,ATCB             SET TIME BOUND TRANSACTION                   
         USING TCBD,R4                                                          
         ICM   R1,15,0(R3)         GET F'TIME IN SECONDS'                       
         LTR   R1,R1                                                            
         BZ    XMOD                                                             
         CHI   R1,255                                                           
         BH    XMOD                                                             
         GOTO1 VPROTOFF            DISABLE CALLER'S STORAGE PROTECTION          
         STC   R1,TCBTBSEC         SET TIME IN SECONDS                          
         OI    TCBFLAG3,TCBTBND    SET TIME BOUND FLAG                          
         GOTO1 VPROTON             RESTORE CALLER'S STORAGE PROTECTION          
         B     XMOD                                                             
         DROP  R4                                                               
*                                                                               
EXBTBU   L     R4,ATCB             UNSET TIME BOUND TRANSACTION                 
         USING TCBD,R4                                                          
         GOTO1 VPROTOFF            DISABLE CALLER'S STORAGE PROTECTION          
         MVI   TCBTBSEC,0                                                       
         NI    TCBFLAG3,255-TCBTBND                                             
         GOTO1 VPROTON             RESTORE CALLER'S STORAGE PROTECTION          
         B     XMOD                                                             
         DROP  R4                                                               
*                                                                               
EXTUTM   MVC   0(4,R3),TIMETUS     TIME IN TU'S (1/38400 SEC)                   
         B     XMOD                                                             
*                                                                               
EXELTM   L     R4,ATCB             ELAPSED TIME OF TRANSACTION                  
         USING TCBD,R4                                                          
         L     R0,TIMETUS                                                       
         S     R0,TCBSTTM                                                       
         ST    R0,0(R3)                                                         
         B     XMOD                                                             
         DROP  R4                                                               
*                                                                               
EXRETM   L     R4,ATCB             TIME REMAINING FOR BOUND TRANSACTION         
         USING TCBD,R4                                                          
         XC    0(4,R3),0(R3)       RETURN ZERO                                  
         TM    TCBFLAG3,TCBTBND                                                 
         BZ    XMOD                                                             
         SR    R1,R1                                                            
         ICM   R1,1,TCBTBSEC       CONVERT MAX TANSACTION SECS TO TUS           
         BZ    XMOD                                                             
         M     R0,=F'38400'                                                     
         A     R1,TCBSTTM                                                       
         S     R1,TIMETUS          R0=TIME REMAINING IN TU'S                    
         BM    XMOD                                                             
         ST    R1,0(R3)                                                         
         B     XMOD                                                             
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
* RETURN CURRENT MAXIO VALUE                                          *         
***********************************************************************         
EXMAXI   L     R4,ATCB             RETURN CURRENT MAXIO VALUE                   
         USING TCBD,R4                                                          
         L     R5,AUTL                                                          
         USING UTLD,R5                                                          
         L     R1,ASSB             GET BASIC MAX FROM SSB                       
         XR    R0,R0               USING ICM SO CAN BE > X'7FFF'                
         ICM   R0,3,SSBMAXIO-SSBD(R1)                                           
         SR    RF,RF                                                            
         ICM   RF,7,TAPRG          GET TAPRG DISPLACEMENT                       
         XR    RE,RE                                                            
         ICM   RE,7,TARSYS                                                      
         BZ    EXMAXI1                                                          
         ICM   RE,15,SEPGMS-SELISTD(RE)                                         
         AR    RF,RE                                                            
         TM    PGMIND-PGMLSTD(RF),PGMIIOB TEST IF PGM I/O BOUND                 
         BZ    EXMAXI1                                                          
         L     R0,MAXMAXIO         NORMAL MAXIMUM I/O COUNT                     
         CLI   TCBSWSOV,6                                                       
         BNE   *+8                                                              
         L     R0,ACCMAXIO         ACCOUNT MAXIMUM I/O COUNT                    
EXMAXI1  ST    R0,0(R3)                                                         
         DROP  R4,R5                                                            
         B     XMOD                                                             
         EJECT                                                                  
***********************************************************************         
* SET TICKET VALUE IN UTL                                             *         
***********************************************************************         
EXTKTS   L     R4,AUTL             SET TICKET VALUE                             
         USING UTLD,R4                                                          
         GOTO1 VPROTOFF            DISABLE CALLER'S STORAGE PROTECTION          
         MVC   TTICKET,0(R3)                                                    
         GOTO1 VPROTON             RESTORE CALLER'S STORAGE PROTECTION          
         B     XMOD                                                             
         EJECT                                                                  
***********************************************************************         
* ENABLE/DISABLE BROADCAST MESSAGES                                   *         
***********************************************************************         
BCASTS   L     R4,AUTL             ENABLE DISABLE BROADCASTS                    
         USING UTLD,R4                                                          
         GOTO1 VPROTOFF            DISABLE CALLER'S STORAGE PROTECTION          
*                                                                               
         MVI   BYTE,C'E'           SAVE INITIAL STATUS E OR D                   
         TM    TFLAG,TFLAGIRB                                                   
         BZ    *+8                                                              
         MVI   BYTE,C'D'                                                        
*                                                                               
         CLI   0(R3),C'E'          E - ENABLE BROADCASTS                        
         BNE   *+8                                                              
         NI    TFLAG,255-TFLAGIRB                                               
         CLI   0(R3),C'D'          D - DISABLE BROADCASTS                       
         BNE   *+8                                                              
         OI    TFLAG,TFLAGIRB                                                   
*                                                                               
         MVC   0(1,R3),BYTE        RETURN INITIAL STATUS                        
*                                                                               
         GOTO1 VPROTON             RESTORE CALLER'S STORAGE PROTECTION          
         B     XMOD                                                             
*                                                                               
GETSYSA  L     R1,=A(SYSADS)       PASS ADDRESS OF SYSADS                       
         ST    R1,0(R3)                                                         
         B     XMOD                                                             
         EJECT                                                                  
***********************************************************************         
* SET TTSET AND TACCS                                                 *         
***********************************************************************         
SETTTES  L     R4,AUTL             SET TTEST AND TACCS                          
         GOTO1 VPROTOFF            DISABLE CALLER'S STORAGE PROTECTION          
         L     R5,=V(TSTTAB)                                                    
         LH    R6,0(R5)                                                         
         L     R7,2(R5)                                                         
         LA    R5,6(R5,R6)                                                      
*                                                                               
SETTES1  CLC   2(4,R5),0(R3)       FIND TABLE ENTRY                             
         BNE   SETTES2                                                          
         MVC   TSTTACCS-TSTTABD(4,R5),TACCS                                     
         STCM  R5,15,TACCS         SET IAM DETAIL                               
         OI    TTEST,TTESTTAC                                                   
         OI    TSTAT5,TST5IOTR     DEFAULT TO TRACE ON                          
         B     SETTES3                                                          
*                                                                               
SETTES2  BXLE  R5,R6,SETTES1                                                    
*                                                                               
SETTES3  GOTO1 VPROTON             RESTORE CALLER'S STORAGE PROTECTION          
         B     XMOD                                                             
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
* SET TEMPEST NUMBER AND INDEX                                        *         
***********************************************************************         
EXXTSET  L     R4,ATCB             SET TEMPEST NUMBER AND INDEX                 
         USING TCBD,R4             R3=FLG(1)/NUM(2)/NDX(4)                      
         GOTO1 VPROTOFF            DISABLE CALLER'S STORAGE PROTECTION          
         OC    TCBXTNDX,TCBXTNDX   TEST IF ALREADY ASSIGNED                     
         BZ    EXXTSET1                                                         
         TM    TCBXTFLG,X'80'      OK IF NOT MY TEMPEST                         
         BO    EXXTSET1                                                         
         CLC   TCBXTNDX,3(R3)      TEST IF SAME AS NEW VALUE                    
         BE    EXXTSET1                                                         
         DC    H'0'                DIE IF ALREADY HAVE TEMPEST ASSIGNED         
EXXTSET1 MVC   TCBXTINF,0(R3)      SET TEMPEST ALLOCATION INFO                  
         MVI   TCBXTFLG,X'80'      SET NOT MY TEMPEST FLAG                      
         GOTO1 VPROTON             RESTORE CALLER'S STORAGE PROTECTION          
         B     XMOD                                                             
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
* RETURN NUMBER OF FREE LOCKTAB ENTRIES                               *         
***********************************************************************         
EXLOCKR  L     R4,ATCB             RETURN NUM OF FREE LOCKTAB ENTRIES           
         USING TCBD,R4                                                          
         XC    0(4,R3),0(R3)       CLEAR RETURN AREA                            
         L     R1,ASSB                                                          
         GOTO1 VPROTOFF            DISABLE CALLER'S STORAGE PROTECTION          
         LAM   AR2,AR2,SSBALET-SSBD(R1)                                         
         SAC   512                 ACCESS DMGR DATASPACE                        
         LLC   R2,TCBSEN           SYSTEM NUMBER                                
         SLL   R2,6                INDEX TO SE                                  
         SR    R1,R1                                                            
         ICM   R1,7,DSPECB+1-DMSPACED(R2)                                       
         BZ    EXLOCKX                                                          
         LR    R2,R1               LOAD SE FULL AREA                            
         ICM   R1,15,DSYALOCK-DMSYSHDR(R2)                                      
         BZ    EXLOCKX                                                          
         LR    R2,R1               R2=A(LOCKTAB)                                
         SR    R1,R1                                                            
         ICM   R1,3,0(R2)          R1=MAX LOCKS                                 
         SH    R1,2(,R2)           R1=REMAINING NUMBER OF LOCKS                 
         ST    R1,0(R3)                                                         
*                                                                               
EXLOCKX  SAC   0                   BACK TO NORMAL MODE                          
         GOTO1 VPROTON             RESTORE CALLER'S STORAGE PROTECTION          
         B     XMOD                                                             
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
* SET/UNSET DDLINK POLLING MODE                                       *         
***********************************************************************         
SETDDLP  L     R4,AUTL             SET DDLINK POLL MODE                         
         USING UTLD,R4                                                          
         GOTO1 VPROTOFF            DISABLE CALLER'S STORAGE PROTECTION          
         OI    TSTATC,TSTCDDLP     TERMINAL IN DDLINK POLL MODE                 
         MVC   TRUNTAB,0(R3)       RUNNER DISP TO TABS QUEUE ENTRY              
         GOTO1 VPROTON             RESTORE CALLER'S STORAGE PROTECTION          
         B     XMOD                                                             
         DROP  R4                                                               
*                                                                               
UNSDDLP  L     R4,AUTL             UNSET DDLINK POLL MODE                       
         USING UTLD,R4                                                          
         GOTO1 VPROTOFF            DISABLE CALLER'S STORAGE PROTECTION          
         NI    TSTATC,255-TSTCDDLP                                              
         XC    TRUNTAB,TRUNTAB                                                  
         GOTO1 VPROTON             RESTORE CALLER'S STORAGE PROTECTION          
         B     XMOD                                                             
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
* SUPPORT FOR MULTIPLE SCREEN UPLOAD FOR FALINK                       *         
***********************************************************************         
FAL1STEL LT    R4,ATCB                                                          
         JZ    *+2                 DIE IF NO TCB ADDRESS                        
         USING TCBD,R4                                                          
         L     RE,AUTL                                                          
         TM    TSTATC-UTLD(RE),TSTATFUP TEST BULK UPLOAD                        
         BZ    FAL1ST30                                                         
*                                                                               
         SAM31                                                                  
         L     RE,TCBRBUFF         FIND IN XA TBUFF                             
         USING TBHD,RE                                                          
         AHI   RE,-TBHL                                                         
         LLH   RF,TBHMSGL          LENGTH OF MESSAGE                            
         LA    R6,TBHDATA+2(RF)    POINT TO UPLOAD DATA                         
         LA    R0,80               MAX SEARCH                                   
         B     FAL1ST50                                                         
         DROP  RE                                                               
*                                                                               
FAL1ST30 L     R6,TCBTWA           EXTRACT FROM TWA                             
         USING TWAD,R6                                                          
         LA    R6,TWABEGIN                                                      
         USING FHD,R6                                                           
         CLC   FALINKH,FHDA        TEST IF DATA STARTS WITH <FALINK>            
         BNE   FAL1STX                                                          
         LLC   R0,L'FALINKH+FHDA   GET ROW OF CONTROL FIELD                     
         NILL  GR0,X'000F'                                                      
         AHI   R0,-1               (ROW-1)*80+(COL-1)                           
         MHI   R0,80                                                            
         AHI   R0,1                SET COL=2                                    
                                                                                
FAL1ST32 LLC   RF,FHLN             BUMP THROUGH FIELDS TO FIND MATCH            
         AR    R6,RF                                                            
         CH    R0,FHAD             START OF DATA SCREEN                         
         BH    FAL1ST32                                                         
         BL    FAL1STX             ERROR                                        
*                                                                               
         LLC   R0,FHLN             EQUAL SO FOUND IT                            
         AHI   R0,-FHDAD                                                        
         TM    FHAT,FHATXH                                                      
         BZ    *+8                                                              
         AHI   R0,-8               EXTENDED FIELD HEADER LENGTH                 
         LA    R6,FHDA             POINT TO DATA                                
*                                                                               
FAL1ST50 CLI   0(R6),C'='          FALINK - ELEMIDQ                             
         BE    FAL1ST54                                                         
FAL1ST52 LA    R6,1(R6)                                                         
         BCT   R0,FAL1ST50                                                      
         B     FAL1STX                                                          
                                                                                
FAL1ST54 AHI   R6,-1               BUMP BACK FROM C'='                          
         CLC   0(4,R6),=C'B=FD'    IGNORE VERSION MAP CODE                      
         BE    FAL1ST56                                                         
         CLC   0(4,R6),=C'B=FE'    IGNORE VERSION MAP CODE                      
         BNE   FAL1ST60                                                         
FAL1ST56 LA    R6,1(R6)            GET BACK TO EQUAL                            
         B     FAL1ST52                                                         
*                                                                               
FAL1ST60 LLC   RF,0(R6)                                                         
         AHI   RF,-C'A'                                                         
         LA    RF,2(RF)            ADD TWO FOR CHARACTER + EQUALS               
         CHI   RF,8                                                             
         BL    *+8                                                              
         LA    RF,7                MAX OUT AT 8                                 
         EX    RF,FAL1ST                                                        
FAL1STX  B     XMOD                                                             
                                                                                
FAL1ST   MVC   0(0,R3),0(R6)                                                    
FALINKH  DC    C'<FALINK> '                                                     
         EJECT                                                                  
***********************************************************************         
* EXTRACT COUNTS OF WAITING JOBS ON RUNQ FOR A USER                   *         
***********************************************************************         
FARUNQ   L     R4,ATCB             RETURN NUM OF WAITING JOBS FOR USER          
         USING TCBD,R4                                                          
         MVC   FULL,0(R3)          FULL=CLASS,USERID                            
         XC    4(4,R3),4(R3)       CLEAR RETURN AREA                            
         GOTO1 VPROTOFF                                                         
         L     R1,ASSB                                                          
         LAM   AR2,AR2,SSBTBLET-SSBD(R1)                                        
         SAC   512                 ACCESS TABS DATASPACE                        
         LA    R2,5                                                             
         SLL   R2,6                                                             
         USING DMSPACED,R2                                                      
         L     R5,DSPTEND          EOT                                          
         L     R2,DSPTFRST                                                      
         NC    DSPTFRST,=XL4'3FFFFFFF'                                          
*                                                                               
         USING TABJOBS,R2                                                       
         XR    RE,RE                                                            
         ICM   RE,3,TBJLNTRY       RE=ENTRY LEN                                 
         ICM   RF,15,TBJNUM        RF=NUMBER ACTIVE                             
         BZ    FARUNQ99                                                         
         XR    R1,R1                                                            
         ICM   R1,3,TBJLHEAD       R1=OFFSET TO START                           
         AR    R2,R1                                                            
         USING TBJNTRY,R2                                                       
*                                                                               
FARUNQ10 CLC   0(4,R2),=X'00000000'  INACTIVE ENTRY                             
         BE    FARUNQ90                                                         
         CLC   FULL(2),=X'FFFF'    FFFF CLASS IS ALL                            
         BE    *+14                                                             
         CLC   TBJCLASS(1),FULL    IS IT MY CLASS                               
         BNE   FARUNQ80                                                         
*                                                                               
         CLC   FULL+2(2),=X'FFFF'  FFFF USER IS ALL                             
         BE    *+14                                                             
         CLC   TBJPQUSR,FULL+2     IS IT MY USER                                
         BNE   FARUNQ80                                                         
*                                                                               
         CLC   TBJETIME,ZEROS                                                   
         BE    FARUNQ20                                                         
         ICM   R1,3,4(R3)          COUNT COMPLETRED                             
         LA    R1,1(R1)                                                         
         STCM  R1,3,4(R3)                                                       
         B     FARUNQ80                                                         
*                                                                               
FARUNQ20 ICM   R1,3,6(R3)          COUNT NON COMPLETED                          
         LA    R1,1(R1)                                                         
         STCM  R1,3,6(R3)                                                       
         B     FARUNQ80                                                         
*                                                                               
FARUNQ80 BCT   RF,FARUNQ90         DEC ACTIVE COUNT                             
         B     FARUNQ99            UNTIL ALL DONE                               
*                                                                               
FARUNQ90 AR    R2,RE               NEXT ENTRY                                   
         CR    RE,R5                                                            
         BL    FARUNQ10            CONTINUE IF < EOT                            
*                                                                               
FARUNQ99 SAC   0                                                                
         GOTO1 VPROTON             RESTORE CALLER'S STORAGE PROTECTION          
         B     XMOD                                                             
         EJECT                                                                  
***********************************************************************         
* EXTRACT TABLE AL2(NUMBER),FLAGS,SOURCE,LENGTH,OFFSET                *         
* NOTE: IF SOURCE IS * OFFSET IS A ROUTINE                            *         
***********************************************************************         
GETXTAB  DS    0H                                                               
         DC    AL2(001),X'00',C'*',Y(F@UTLL),AL2(EXUTLQ)                        
         DC    AL2(002),X'00',C'T',Y(L'F@TSTAT1),AL2(TSTAT1-UTLD)               
         DC    AL2(003),X'00',C'T',Y(L'F@TSTAT2),AL2(TSTAT2-UTLD)               
         DC    AL2(004),X'00',C'T',Y(L'F@TSYS),AL2(TSYS-UTLD)                   
         DC    AL2(005),X'00',C'T',Y(L'F@TPRG),AL2(TPRG-UTLD)                   
         DC    AL2(006),X'00',C'T',Y(L'F@TPASS),AL2(TPASSWD-UTLD)               
         DC    AL2(007),X'00',C'T',Y(L'F@TPERS),AL2(TPERSON-UTLD)               
         DC    AL2(008),X'00',C'T',Y(L'F@TTYPE),AL2(TTYPE-UTLD)                 
         DC    AL2(009),X'00',C'T',Y(L'F@TSYM),AL2(TSYM-UTLD)                   
         DC    AL2(010),X'00',C'T',Y(L'F@TSIN),AL2(TSIN-UTLD)                   
         DC    AL2(011),X'00',C'T',Y(L'F@TFLAG),AL2(TFLAG-UTLD)                 
         DC    AL2(012),X'00',C'T',Y(L'F@TLANG),AL2(TLANG-UTLD)                 
         DC    AL2(013),X'00',C'T',Y(L'F@TCTRY),AL2(TCTRY-UTLD)                 
         DC    AL2(014),X'00',C'*',Y(F@TCBL),AL2(EXTCBQ)                        
         DC    AL2(015),X'00',C'B',Y(L'F@BIOCNT),AL2(TCBIOCNT+1-TCBD)           
         DC    AL2(016),X'00',C'B',Y(L'F@BSWNUM),AL2(TCBSWNUM-TCBD)             
         DC    AL2(017),X'00',C'*',Y(F@SSBL),AL2(EXSSBQ)                        
         DC    AL2(018),X'00',C'S',Y(L'F@SDATEB),AL2(SSBDATEB-SSBD)             
         DC    AL2(019),X'00',C'S',Y(L'F@SDATE),AL2(SSBDATE-SSBD)               
         DC    AL2(020),X'00',C'S',Y(L'F@SSYSID),AL2(SSBSYSID-SSBD)             
         DC    AL2(021),X'00',C'S',Y(L'F@SSYSNA),AL2(SSBSYSNA-SSBD)             
         DC    AL2(022),X'00',C'S',Y(L'F@SMAXIO),AL2(SSBMAXIO-SSBD)             
         DC    AL2(023),X'00',C'B',Y(L'F@BRCVC),AL2(TCBRCVC-TCBD)               
         DC    AL2(024),X'00',C'B',Y(L'F@BBILL),AL2(TCBBILL-TCBD)               
         DC    AL2(025),X'00',C'T',Y(L'F@TSTAT6),AL2(TSTAT6-UTLD)               
         DC    AL2(026),X'00',C'T',Y(L'F@TAGYSC),AL2(TAGYSEC-UTLD)              
         DC    AL2(027),X'00',C'T',Y(L'F@TOVSYS),AL2(TOVSYS-UTLD)               
         DC    AL2(028),X'00',C'T',Y(L'F@TAGYB),AL2(TAGYB-UTLD)                 
         DC    AL2(029),X'00',C'B',Y(L'F@BOVSYS),AL2(TCBOVSYS-TCBD)             
         DC    AL2(030),X'00',C'S',Y(L'F@SSTAT3),AL2(SSBSTAT3-SSBD)             
         DC    AL2(031),X'00',C'*',Y(F@SELISL),AL2(EXSELQ)                      
         DC    AL2(032),X'00',C'E',Y(L'F@SENAME),AL2(SENAME-SELISTD)            
         DC    AL2(033),X'00',C'E',Y(L'F@SESYS),AL2(SESYS-SELISTD)              
         DC    AL2(034),X'00',C'E',Y(L'F@SEOV),AL2(SEOVSYS-SELISTD)             
         DC    AL2(035),X'00',C'E',Y(L'F@SEFILS),AL2(SEFILSET-SELISTD)          
         DC    AL2(036),X'00',C'*',AL2(4),AL2(EXPEEKQ)                          
         DC    AL2(037),X'00',C'*',AL2(4),AL2(EXPEEK1Q)                         
         DC    AL2(038),X'00',C'*',AL2(4),AL2(EXTRIOQ)                          
         DC    AL2(039),X'00',C'*',AL2(4),AL2(EXDDSTQ)                          
         DC    AL2(040),X'00',C'*',AL2(8),AL2(EXDDSEQ)                          
         DC    AL2(041),X'00',C'*',AL2(4),AL2(EXSESAVQ)                         
         DC    AL2(042),X'00',C'*',AL2(0),AL2(EXSESPRQ)                         
         DC    AL2(043),X'00',C'*',AL2(0),AL2(EXSESUNQ)                         
         DC    AL2(044),X'00',C'S',Y(L'F@SSYSFL),AL2(SSBSYSFL-SSBD)             
         DC    AL2(045),X'00',C'*',AL2(0),AL2(EXDEBQ)                           
         DC    AL2(046),X'00',C'*',AL2(0),AL2(EXMIOSTQ)                         
         DC    AL2(047),X'00',C'*',AL2(0),AL2(EXMIOUNQ)                         
         DC    AL2(048),X'00',C'E',Y(L'F@SEIND),AL2(SEIND-SELISTD)              
         DC    AL2(049),X'00',C'B',Y(L'F@BIOCNA),AL2(TCBIOCNT-TCBD)             
         DC    AL2(050),X'00',C'T',Y(L'F@TTEST),AL2(TTEST-UTLD)                 
         DC    AL2(051),X'00',C'*',AL2(0),AL2(EXWRITEQ)                         
         DC    AL2(052),X'00',C'*',AL2(0),AL2(EXWAITQ)                          
         DC    AL2(053),X'00',C'B',Y(L'F@BTWA),AL2(TCBTWA-TCBD)                 
         DC    AL2(054),X'00',C'S',Y(L'F@STSAR),AL2(SSBTSAR-SSBD)               
         DC    AL2(055),X'00',C'S',Y(L'F@SCTACT),AL2(SSBCTACT-SSBD)             
         DC    AL2(056),X'00',C'*',AL2(0),AL2(SETUSERQ)                         
         DC    AL2(057),X'00',C'T',Y(L'F@TSTAT8),AL2(TSTAT8-UTLD)               
         DC    AL2(058),X'00',C'T',Y(L'F@TSTAT9),AL2(TSTAT9-UTLD)               
         DC    AL2(059),X'00',C'T',Y(L'F@TSTATU),AL2(TSTATU-UTLD)               
         DC    AL2(060),X'00',C'*',AL2(4),AL2(EXWSSVRQ)                         
         DC    AL2(061),X'00',C'S',AL2(8),AL2(SSBMEDTB-SSBD)                    
         DC    AL2(062),X'00',C'T',Y(L'F@TACCS),AL2(TACCS-UTLD)                 
         DC    AL2(063),X'00',C'T',Y(L'F@TACCS2),AL2(TACCS2-UTLD)               
         DC    AL2(064),X'00',C'*',AL2(0),AL2(EXUPDNQ)                          
         DC    AL2(065),X'00',C'*',AL2(0),AL2(EXUPDYQ)                          
         DC    AL2(066),X'00',C'T',Y(L'F@TNUM),AL2(TNUM-UTLD)                   
         DC    AL2(067),X'00',C'T',Y(L'F@TUSER),AL2(TUSER-UTLD)                 
         DC    AL2(068),X'00',C'T',Y(L'F@TAUTH),AL2(TAUTH-UTLD)                 
         DC    AL2(069),X'00',C'T',Y(L'F@TSAGN),AL2(TSAGN-UTLD)                 
         DC    AL2(070),X'00',C'T',Y(L'F@TAGY),AL2(TAGY-UTLD)                   
         DC    AL2(071),X'00',C'T',Y(L'F@TOFFCD),AL2(TOFFCODE-UTLD)             
         DC    AL2(072),X'00',C'T',Y(L'F@TAGCTR),AL2(TAGCTRY-UTLD)              
         DC    AL2(073),X'00',C'T',Y(L'F@TAGCOP),AL2(TAGCOPT-UTLD)              
         DC    AL2(074),X'00',C'T',Y(L'F@TAGCUR),AL2(TAGCURR-UTLD)              
         DC    AL2(075),X'00',C'*',AL2(0),AL2(EXBTBSQ)                          
         DC    AL2(076),X'00',C'*',AL2(0),AL2(EXBTBUQ)                          
         DC    AL2(077),X'00',C'*',AL2(0),AL2(EXTUTMQ)                          
         DC    AL2(078),X'00',C'B',Y(L'F@BSTTM),AL2(TCBSTTM-TCBD)               
         DC    AL2(079),X'00',C'*',AL2(0),AL2(EXELTMQ)                          
         DC    AL2(080),X'00',C'*',AL2(0),AL2(EXRETMQ)                          
         DC    AL2(081),X'00',C'*',AL2(0),AL2(EXMAXIO)                          
         DC    AL2(082),X'00',C'T',Y(L'F@TSTAT5),AL2(TSTAT5-UTLD)               
         DC    AL2(083),X'00',C'T',Y(L'F@TSTATB),AL2(TSTATB-UTLD)               
         DC    AL2(084),X'00',C'T',Y(L'F@TAGYPE),AL2(TAGYPER-UTLD)              
         DC    AL2(085),X'00',C'T',Y(L'F@TICKET),AL2(TTICKET-UTLD)              
         DC    AL2(086),X'00',C'*',AL2(0),AL2(EXTKTSQ)                          
         DC    AL2(087),X'00',C'T',Y(L'F@TXPINF),AL2(TXPINFO-UTLD)              
         DC    AL2(088),X'00',C'*',AL2(0),AL2(BCASTSET)                         
         DC    AL2(089),X'00',C'*',AL2(0),AL2(GETSYSAD)                         
         DC    AL2(090),X'00',C'*',AL2(0),AL2(SETTTEST)                         
         DC    AL2(091),X'00',C'B',AL2(8),AL2(TCBXTINF-TCBD)                    
         DC    AL2(092),X'00',C'*',AL2(0),AL2(EXXTSETQ)                         
         DC    AL2(093),X'00',C'*',AL2(0),AL2(EXLOCKER)                         
         DC    AL2(094),X'00',C'*',AL2(0),AL2(EXUPDVQ)                          
         DC    AL2(095),X'00',C'*',AL2(0),AL2(EXOSIMQ)                          
         DC    AL2(096),X'00',C'T',Y(L'F@OSIN),AL2(TOSIN-UTLD)                  
         DC    AL2(097),X'00',C'*',AL2(0),AL2(EXOSISQ)                          
         DC    AL2(098),X'00',C'*',AL2(0),AL2(EXTAROVQ)                         
         DC    AL2(099),X'00',C'*',AL2(0),AL2(EXTARSEQ)                         
         DC    AL2(100),X'00',C'*',AL2(0),AL2(CHKITSKQ)                         
         DC    AL2(101),X'00',C'*',AL2(0),AL2(SETDDLPQ)                         
         DC    AL2(102),X'00',C'*',AL2(0),AL2(UNSDDLPQ)                         
         DC    AL2(103),X'00',C'*',AL2(8),AL2(FAL1STEQ)                         
         DC    AL2(104),X'00',C'*',AL2(0),AL2(FARUNQUE)                         
         DC    AL2(105),X'00',C'*',AL2(4),AL2(FAIOADDR)                         
         DC    AL2(106),X'00',C'*',AL2(8),AL2(FAXA9ADR)                         
         DC    AL2(107),X'00',C'*',AL2(1),AL2(FASEADV)                          
         DC    AL2(108),X'00',C'*',AL2(0),AL2(EXDDUMPQ)                         
         DC    AL2(109),X'00',C'*',AL2(0),AL2(EXSCRIPT)                         
         DC    AL2(110),X'00',C'*',AL2(4),AL2(EXVSAM#S)                         
*                                                                               
GETXMAX  DC    AL2((*-GETXTAB)/8)                                               
         EJECT                                                                  
***********************************************************************         
* EQUATES,LITERALS, AND CONSTANTS                                     *         
***********************************************************************         
VPROTOFF DC    V(PROTOFF)                                                       
VPROTON  DC    V(PROTON)                                                        
MAXMAXIO DC    A(FAMAXMIO)         EXTENDED MAX NUM OF I/O'S PER TASK           
ACCMAXIO DC    A(FAMAXAIO)         ACCOUNT  MAX NUM OF I/O'S PER TASK           
*                                                                               
ZEROS    DC    32X'00'                                                          
FABLKLN  EQU   300                                                              
         LTORG                                                                  
       ++INCLUDE FASYSLST                                                       
         EJECT                                                                  
***********************************************************************         
* SYSTEM ADDRESSES - MUST UPDATE SYSADSD IN FAFACTS TOO               *         
***********************************************************************         
SYSADS   CSECT                                                                  
         DC    V(TMPTABL)                                                       
         DC    V(TMPBUFF)                                                       
         DC    V(EXITAREA)                                                      
         DC    V(AORALL)                                                        
         DC    46A(0)                                                           
         EJECT                                                                  
***********************************************************************         
* WORKING STORAGE                                                     *         
***********************************************************************         
WORKD    DSECT                                                                  
DUB      DS    D                                                                
DMCB     DS    6F                                                               
*                                                                               
SVMODE   DS    C                                                                
         DS    CL7                                                              
FABLOCK  DS    ((FABLKLN+7)/8)D                                                 
*                                                                               
TIMETUS  DS    F                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    C                                                                
FLAG     DS    X                                                                
*                                                                               
RELO     DS    A                                                                
AUTL     DS    A                                                                
ATCB     DS    A                                                                
ASSB     DS    A                                                                
ASELIST  DS    A                                                                
*                                                                               
WORK     DS    CL64                                                             
SPRBUFF  DS    (TBUFFL+32)X                                                     
*                                                                               
WORKX    EQU   *                                                                
         EJECT                                                                  
* FAFACTS                                                                       
       ++INCLUDE FAFACTS                                                        
* FASSB                                                                         
       ++INCLUDE FASSB                                                          
* FASELIST                                                                      
       ++INCLUDE FASELIST                                                       
* FASRS                                                                         
       ++INCLUDE FASRS                                                          
* FASYSFAC                                                                      
       ++INCLUDE FASYSFAC                                                       
* FATSTTAB                                                                      
       ++INCLUDE FATSTTAB                                                       
* FATWA                                                                         
       ++INCLUDE FATWA                                                          
* FATCB                                                                         
       ++INCLUDE FATCB                                                          
* FATBHD                                                                        
       ++INCLUDE FATBHD                                                         
* FAUTL                                                                         
       ++INCLUDE FAUTL                                                          
* FAPGMLST                                                                      
       ++INCLUDE FAPGMLST                                                       
* DMSPACED                                                                      
       ++INCLUDE DMSPACED                                                       
* FATABSJOB                                                                     
       ++INCLUDE FATABSJOB                                                      
* DMDSYSHDR                                                                     
       ++INCLUDE DMDSYSHDR                                                      
* DDFH                                                                          
       ++INCLUDE DDFH                                                           
* FASCRIPTW                                                                     
       ++INCLUDE FASCRIPTW                                                      
                                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'011FAGETFACT 11/18/20'                                      
         END                                                                    
