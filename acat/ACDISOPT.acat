*          DATA SET ACDISOPT   AT LEVEL 027 AS OF 07/01/20                      
*CATALP ACDISOPT                                                                
*                                                                               
*******************************************************************             
* ACDISOPT  -  DISPLAY OPTION VALUE                               *             
*                                                                 *             
* AT ENTRY,  P1  = A(DOBLOCK)                                     *             
*                                                                 *             
* CALLER MUST PASS ADDRESS OF AN OPTION ELEMENT OR THE GOBLOCK AND*             
* AN OPTION NUMBER.  MODULE OUTPUTS TO THE ADDRESS OF A FIELD     *             
* HEADER PASSED BY THE CALLER.  MODULE USES ADDRESS OF THE OPTION *             
* TABLE PASSED BY THE CALLER TO PERFORM DISPLAY.  IF THE CALLER   *             
* DOES NOT PASS AN OPTION TABLE, MODULE WILL USE AN INTERNAL      *             
* TABLE INSTEAD.                                                  *             
*******************************************************************             
         TITLE 'ACDISOPT - MODULE TO DISPLAY OPTION VALUES'                     
*GHOA 13FEB18 026 DSRD-18081 NEW OPT/MAIN 'EMAIL IF PERSN ASSIGN 2 JOB'         
*ASAX 26MAY20 027 DSRD-25987 NEW OPT/MAIN 'DATE EST IN LAST FISCAL YR'          
                                                                                
ACDISOPT CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,**DOPT**,RA,CLEAR=YES                                
         USING WORKD,RC                                                         
         MVC   PARMS(PARML),0(R1)                                               
         MVI   SPACES,C' '                                                      
         MVC   SPACES+1(L'SPACES-1),SPACES                                      
         L     R9,ADOBLOCK                                                      
         USING DOBLOCK,R9                                                       
         L     R2,DOAFLDH          R2=A(FIELD HEADER)                           
         ICM   R5,15,DOAOPTAB      R5=A(OPTION TABLE)                           
         BNZ   *+8                                                              
         LA    R5,OPTTAB                                                        
*                                                                               
         XC    DOAOPTEN,DOAOPTEN   MAKE SURE A(OPTION ENTRY) CLEARED            
         USING OPTBD,R5                                                         
         CLI   DOOPTNUM,0          TEST OPTION ELEMENT PASSED                   
         BE    DISOPT6             YES                                          
*                                                                               
         L     R4,DOAGOBLK         R4=A(GOBLOCK)                                
         USING GOBLOCK,R4                                                       
*                                                                               
DISOPT2  CLI   OPTBOPN,0                                                        
         BE    DOX                                                              
         CLC   DOOPTNUM,OPTBOPN    MATCH ON OPTION NUMBER                       
         BE    DISOPT4                                                          
         LA    R5,OPTBL(R5)                                                     
         B     DISOPT2                                                          
*                                                                               
DISOPT4  LA    R3,GOPTIONS                                                      
         TM    OPTBIND,OPTBPAN     TEST PANEL BLOCK VALUE                       
         BZ    *+12                NO                                           
         ICM   R3,15,GOAPAN                                                     
         BZ    DOX                 NO PANEL BLOCK PASSED                        
         TM    OPTBIND,OPTBEXT     TEST VALUE IN EXTENSION BLOCK                
         BZ    *+12                NO                                           
         ICM   R3,15,GOAEXT                                                     
         BZ    DOX                 NO EXTENSION BLOCK PASSED                    
         TM    OPTBIND,OPTBBIL     TEST VALUE IN BILLING BLOCK                  
         BZ    *+12                NO                                           
         ICM   R3,15,GOABEXT                                                    
         BZ    DOX                 NO BILLING BLOCK PASSED                      
*                                                                               
         AH    R3,OPTBDISP         R3=A(OPTION VALUE FIELD)                     
         ZIC   R1,OPTBMAX                                                       
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         OC    0(0,R3),0(R3)       TEST FOR ANY VALUE                           
         BNZ   DISOPT5             YES                                          
         TM    OPTBIND,OPTBZERO    TEST IF BINARY ZERO ALLOWED                  
         BZ    DOX                 NO                                           
*                                                                               
DISOPT5  XC    ELEMENT,ELEMENT     BUILD AN ELEMENT WITH DATA                   
         LA    R6,ELEMENT                                                       
         USING OPDELD,R6                                                        
         MVI   OPDEL,OPDELQ                                                     
         MVC   OPDNUM,OPTBOPN                                                   
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   OPDDATA(0),0(R3)                                                 
         LA    R1,OPDLN1Q+1(R1)                                                 
         STC   R1,OPDLN                                                         
         B     DISOPT8                                                          
*                                                                               
DISOPT6  L     R6,DOAOPTEL         GET A(OPTION ELEMENT)                        
*                                                                               
DISOPT7  CLI   OPTBOPN,0           TEST FOR EOT                                 
         BE    DOX                 COULD NOT FIND OPTION NUMBER                 
         CLC   OPDNUM,OPTBOPN      MATCH ON OPTION NUMBER                       
         BE    DISOPT8                                                          
         LA    R5,OPTBL(R5)                                                     
         B     DISOPT7                                                          
*                                                                               
DISOPT8  ST    R5,DOAOPTEN         RETURN A(OPTION ENTRY)                       
         ZIC   RF,OPTBDRTN         GET DISPLAY ROUTINE NUMBER                   
         BCTR  RF,0                                                             
         SLL   RF,2                                                             
         B     DOBRANCH(RF)                                                     
*                                                                               
DOBRANCH DS    0F                                                               
         B     DO1                 PERCENTAGE EXPRESSION (4 DEC)                
         B     DO2                 6 WORK CODES SEPARATED BY COMMAS             
         B     DO3                 BILLING TYPE                                 
         B     DO4                 NUMERIC 0-255                                
         B     DO5                 CASH/AMOUNT FIELD                            
         B     DO6                 DEFAULT=NO YES/NO                            
         B     DO7                 OUTPUT CHARACTERS                            
         B     DO8                 SINGLE CHARACTER OUTPUT                      
         B     DO9                 YES/NO/COMPULSORY                            
         B     DO10                PERCENTAGE EXPRESSION (2 DEC)                
         B     DO11                DAYS TO ADD FOR AUTO JOB CLOSE               
         B     DO12                PST PROVINCE/OUTPUT CODE                     
         B     DO13                NONE OR PUT CHARS OUT                        
         B     DO14                YES/NO/CHK/APPR/MAX/TOT/ZERO                 
         B     DO15                DISPLAY ACCOUNT NO COMPANY                   
         B     DO16                TEXT SCROLLING                               
         B     DO17                CBILL COLUMNS                                
         B     DO18                PERCENTAGE EXPRESSION (6 DEC)                
         B     DO19                ESTIMATE PROGRAM CODE                        
         B     DO20                FORCE NARRATIVE ON TIME                      
         B     DO21                ENTRY SEPARATED BY COMMAS                    
         B     DO22                                                             
         DC    F'0'                                                             
         B     DO24C               NONE/IN PROGRESS/SUB/PAP/APP                 
         B     DO25                NONE/ERROR/WARNING                           
         B     DO26                NUMBER OUTPUT (UP TO 100000)                 
         B     DO27                TEXT APP/APP/PART APP/NO                     
         B     DO28                APPROVED/SUBMITTER/NONE                      
         B     DO29                MEDIA/WORK CODE                              
         B     DO30                YES/NO/DRAFT                                 
         B     DO31                NO OR OUTPUT CHAR WITH , 1 CHA ENTRY         
         B     DO32                NO OR OUTPUT CHAR WITH , 2 CHA ENTRY         
         B     DO33                SALES OR COST                                
         B     DO34                CONVERT HEX NUMBER TO OUTPUT                 
         B     DO24D               IN PROGRESS/SUB/PAP/APP                      
         B     DO24                NONE/IN PRO/SUB/APP/CLI APP/MAN APP          
         B     DO37                IN PRO/SUB/APP/CLI APP/MAN APP               
         B     DO38                YES/NO/CONTINGENCY                           
         B     DO39                YES/NO/AMOUNT                                
         B     DO40                DISPLAY CHARACTERS FROM FIRST CHAR           
*                                                                               
DO1      MVC   8(5,R2),=C'ZERO%'   PERCENTAGE EXPRESSION (4 DEC)                
         CP    OPDDATA(4),=P'0'                                                 
         BE    DOX                                                              
         EDIT  (P4,OPDDATA),(9,8(R2)),4,ALIGN=LEFT                              
         LA    R1,17(R2)           R1 TO END OF EXPRESSION                      
*                                                                               
DO1B     BCTR  R1,0                POSITION R1 TO DEC POINT                     
         CLI   0(R1),X'41'                                                      
         BL    DO1B                                                             
         CLI   0(R1),C'0'          GET RID OF TRAILING ZERO                     
         BNE   *+12                                                             
         MVI   0(R1),C' '                                                       
         B     DO1B                                                             
         CLI   0(R1),C'.'                                                       
         BE    *+8                                                              
         LA    R1,1(R1)                                                         
         MVI   0(R1),C'%'                                                       
         B     DOX                                                              
*                                                                               
DO2      CLI   DOOPTNUM,0          TEST IF OPTION ELEM PASSED                   
         BE    *+10                YES                                          
         OC    OPDDATA(12),SPACES MAKE SURE DATA IS SPACE PADDED                
         ZIC   R1,OPDLN            UNBILLABLE WORKCODES                         
         SHI   R1,OPDLN1Q+1                                                     
         EX    R1,*+8                                                           
         B     *+10                                                             
         CLC   OPDDATA(0),SPACES   TEST FOR ALL SPACES FIELD                    
         BNE   DO2A                NO                                           
         MVC   8(4,R2),=C'NONE'    INDICATE NONE                                
         B     DOX                                                              
*                                                                               
DO2A     LA    R1,1(R1)                                                         
         SRL   R1,1                COMPUTE N'UNBILLABLE WORKCODES               
         LA    R0,6                NO MORE THAN 6                               
         CR    R1,R0                                                            
         BH    *+6                                                              
         LR    R0,R1               R0=LOOP COUNTER                              
         MVC   WORK,SPACES                                                      
         LA    R3,OPDDATA          R3=A(WORKCODE LIST)                          
         LA    R4,WORK             R4=A(OUTPUT)                                 
*                                                                               
DO2B     MVC   0(2,R4),0(R3)       EXTRACT NEXT WORKCODE                        
         OC    0(2,R4),SPACES      MAKE SURE ITS SPACE PADDED                   
         CLC   0(2,R4),SPACES      TEST FOR EARLY EOL                           
         BE    DO2C                YES                                          
*                                                                               
         LA    R4,1(R4)                                                         
         CLI   0(R4),C' '          TEST FOR ONE-CHARACTER WORKCODE              
         BE    *+8                 YES                                          
         LA    R4,1(R4)                                                         
         MVI   0(R4),C','          PLACE A COMMA AFTER                          
         LA    R4,1(R4)            BUMP OUTPUT POINTER                          
         LA    R3,2(R3)            NEXT WORKCODE                                
         BCT   R0,DO2B                                                          
*                                                                               
DO2C     BCTR  R4,0                BACK UP TO ERASE LAST COMMA                  
         MVI   0(R4),C' '                                                       
         LA    RE,WORK                                                          
         SR    R4,RE               COMPUTE L'OUTPUT STRING                      
         BCTR  R4,0                                                             
         EX    R4,*+8                                                           
         B     DOX                                                              
         MVC   8(0,R2),WORK                                                     
*                                                                               
DO3      CLI   OPDDATA,C'E'        BILLING TYPE                                 
         BE    DO3E                                                             
         CLI   OPDDATA,C'F'                                                     
         BE    DO3F                                                             
         CLI   OPDDATA,C'S'                                                     
         BE    DO3S                                                             
         MVC   8(4,R2),=C'AUTO'                                                 
         CLI   OPDDATA,C'A'                                                     
         BE    DOX                                                              
         MVC   8(5,R2),=C'TOTAL'                                                
         CLI   OPDDATA,C'T'                                                     
         BE    DOX                                                              
         MVC   8(6,R2),=C'1 LINE'                                               
         CLI   OPDDATA,C'1'                                                     
         BE    DOX                                                              
         MVC   8(6,R2),=C'CLIENT'                                               
         CLI   OPDDATA,C'C'                                                     
         BE    DOX                                                              
         MVC   8(10,R2),=C'UNBILLABLE'                                          
         CLI   OPDDATA,C'U'                                                     
         BE    DOX                                                              
         MVC   8(11,R2),=C'PROGRESSIVE'                                         
         B     DOX                                                              
*                                                                               
DO3E     LA    R3,OPDDATA+1        ESTIMATE PERCENT(S)                          
         ZIC   R5,OPDLN                                                         
         SHI   R5,OPDLN1Q+1                                                     
         SRA   R5,2                DIVIDE LENGTH OF DATA BY 4                   
         BZ    DOX                 NO DATA PRESENT                              
         LA    RE,3                NO MORE THAN 3 PERCENTS                      
         CR    R5,RE                                                            
         BL    *+6                                                              
         LR    R5,RE                                                            
         LA    R4,8(R2)                                                         
*                                                                               
         LR    RE,R5               STRIP OUT TRAILING ZEROES (ELEMENT           
         BCTR  RE,0                BUILT FROM GOBLOCK)                          
         SLL   RE,2                                                             
         LA    RE,0(R3,RE)                                                      
*                                                                               
DO3E1    OC    0(4,RE),0(RE)                                                    
         BNZ   DO3E2                                                            
         SH    RE,=H'4'                                                         
         BCT   R5,DO3E1                                                         
         B     DOX                                                              
*                                                                               
DO3E2    EDIT  (4,(R3)),(7,0(R4)),2,ALIGN=LEFT,FLOAT=-                          
         LR    R1,R4                                                            
         BCTR  R1,0                                                             
*                                                                               
DO3E4    LA    R1,1(R1)                                                         
         CLI   0(R1),C'.'          LOOK FOR THE DECIMAL POINT                   
         BNE   DO3E4                                                            
         LA    R4,3(R1)                                                         
         CLC   1(2,R1),=C'00'      GET RID OF SUPERFLUOUS .00                   
         BNE   DO3E6                                                            
         MVC   0(3,R1),=C'   '                                                  
         LR    R4,R1                                                            
*                                                                               
DO3E6    MVI   0(R4),C'%'          POP IN THE PERCENT SIGN                      
         CH    R5,=H'1'                                                         
         BE    DOX                                                              
         MVI   1(R4),C','          AND COMMA IF NOT LAST                        
         LA    R4,2(R4)                                                         
         LA    R3,4(R3)                                                         
         BCT   R5,DO3E2                                                         
*                                                                               
DO3F     MVC   8(3,R2),=C'FEE'     FEE BILLING                                  
         LA    R3,OPDDATA+1                                                     
         EDIT  (4,(R3)),(12,11(R2)),2,ALIGN=LEFT                                
         B     DOX                                                              
*                                                                               
DO3S     LA    R3,OPDDATA+1        SPECIAL AMOUNT                               
         EDIT  (4,(R3)),(12,8(R2)),2,ALIGN=LEFT,FLOAT=-                         
         B     DOX                                                              
*                                                                               
DO4      DS    0H                  NUMERIC 0-255                                
         CLI   OPDDATA,0           TEST FOR BINARY ZERO                         
         BE    DO4A                                                             
         CLI   OPDNUM,OPNBFMT                                                   
         BE    DO4B                                                             
         EDIT  (1,OPDDATA),(3,8(R2)),ALIGN=LEFT,FLOAT=-                         
         B     DOX                                                              
*                                                                               
DO4A     EDIT  (1,OPDDATA),(3,8(R2)),ALIGN=LEFT,ZERO=NOBLANK                    
         B     DOX                                                              
*                                                                               
DO4B     SR    RF,RF                                                            
         IC    RF,OPDDATA                                                       
         EDIT  (RF),(3,8(R2)),ALIGN=LEFT                                        
         B     DOX                                                              
*                                                                               
DO5      DS    0H                  CASH FIELD                                   
         EDIT  (P4,OPDDATA),(12,8(R2)),2,ALIGN=LEFT,FLOAT=-                     
         B     DOX                                                              
*                                                                               
DO6      MVC   8(2,R2),=C'NO'      Y(ES) N(O) FIELD                             
         CLI   OPDDATA,C'N'                                                     
         BE    DOX                                                              
         MVC   8(3,R2),=C'YES'                                                  
         B     DOX                                                              
*                                                                               
DO7      ZIC   R1,OPDLN            CHARACTERS NO VALIDATION                     
         SHI   R1,OPDLN1Q+1                                                     
         EX    R1,*+8                                                           
         B     DOX                                                              
         MVC   8(0,R2),OPDDATA                                                  
*                                                                               
DO8      MVC   8(1,R2),OPDDATA     NEW BILL TYPE                                
         B     DOX                                                              
*                                                                               
DO9      CLI   OPDDATA,C'C'        COMPULSORY?                                  
         BNE   DO6                                                              
         MVC   8(4,R2),=C'COMP'                                                 
         B     DOX                                                              
*                                                                               
DO10     MVC   8(5,R2),=C'ZERO%'   PERCENTAGE EXPRESSION (2 DEC)                
         CP    OPDDATA(4),=P'0'                                                 
         BE    DOX                                                              
         EDIT  (P4,OPDDATA),(9,8(R2)),2,ALIGN=LEFT                              
         LA    R1,17(R2)           R1 TO END OF EXPRESSION                      
         B     DO1B                                                             
*                                                                               
DO11     DS    0H                  DAYS TO ADD FOR AUTO JOB CLOSE               
         EDIT  (2,OPDDATA),(3,8(R2)),ALIGN=LEFT,ZERO=NOBLANK                    
         B     DOX                                                              
*                                                                               
DO12     DS    0H                  PST PROVINCE/OUTPUT CODE                     
         MVC   8(2,R2),OPDDATA     PROVINCE                                     
         MVI   10(R2),C'/'                                                      
         MVC   11(1,R2),OPDDATA+2                                               
         B     DOX                                                              
*                                                                               
DO13     CLC   OPDDATA(3),SPACES   TEST FOR SPACES                              
         BNE   DO7                                                              
         MVC   8(4,R2),=C'NONE'    YES-ITS A SPECIAL                            
         B     DOX                                                              
*                                                                               
DO14     MVC   8(2,R2),=C'NO'      Y(YES), N(O), A(PP), Z(ERO) FIELD            
         CLI   OPDDATA,C'N'                                                     
         BE    DOX                                                              
         MVC   8(3,R2),=C'YES'                                                  
         CLI   OPDDATA,C'Y'                                                     
         BE    DOX                                                              
         MVC   8(3,R2),=C'CHK'                                                  
         CLI   OPDDATA,C'C'                                                     
         BE    DOX                                                              
         MVC   8(3,R2),=C'APP'                                                  
         CLI   OPDDATA,C'A'                                                     
         BE    DOX                                                              
         MVC   8(3,R2),=C'MAX'                                                  
         CLI   OPDDATA,C'M'                                                     
         BE    DOX                                                              
         MVC   8(3,R2),=C'TOT'                                                  
         CLI   OPDDATA,C'T'                                                     
         BE    DOX                                                              
         MVC   8(4,R2),=C'ZERO'    Y(YES), N(O), A(PP), Z(ERO) FIELD            
         B     DOX                                                              
*                                                                               
DO15     ZIC   R1,OPDLN                                                         
         SHI   R1,OPDLN1Q+2                                                     
         EX    R1,*+8                                                           
         B     DOX                                                              
         MVC   8(0,R2),OPDDATA+1   DON'T MOVE COMPANY CODE                      
*                                                                               
DO16     DS    0H                  TEXT SCROLL SEQUENCE                         
         MVC   WORK,SPACES                                                      
         CLI   DOOPTNUM,0          TEST IF ELEMENT PASSED                       
         BE    *+10                YES                                          
         OC    OPDDATA(L'GOTXTSCR),SPACES                                       
         LA    R4,WORK             R4=OUTPUT POINTER                            
         LA    R7,OPDDATA          R7=DATA POINTER                              
         ZIC   R3,OPDLN                                                         
         AHI   R3,-OPDLN1Q                                                      
         SRL   R3,1                R3=LOOP COUNTER                              
*                                                                               
DO16A    MVC   0(2,R4),0(R7)                                                    
         CLC   0(2,R4),SPACES      TEST FOR EARLY EOL                           
         BE    DO16B                                                            
         LA    R4,1(R4)                                                         
         CLI   1(R7),C' '          TEST FOR ONE BYTE CODE                       
         BE    *+8                 YES                                          
         LA    R4,1(R4)                                                         
         MVI   0(R4),C','          ADD A COMMA AFTERWARDS                       
         LA    R4,1(R4)                                                         
         LA    R7,2(R7)                                                         
         BCT   R3,DO16A                                                         
*                                                                               
DO16B    BCTR  R4,0                BACK UP AND ERASE LAST COMMA                 
         MVI   0(R4),C' '                                                       
         LA    R1,WORK                                                          
         SR    R4,R1               COMPUTE LENGTH OF OUTPUT                     
         BCTR  R4,0                                                             
         EX    R4,*+8                                                           
         B     DOX                                                              
         MVC   8(0,R2),WORK                                                     
*                                                                               
DO17     SR    R0,R0               VALID COLUMNS                                
         IC    R0,OPDLN                                                         
         SHI   R0,OPDLN1Q                                                       
         LA    RF,8(R2)                                                         
         LA    R1,OPDDATA                                                       
DO1702   TM    0(R1),X'40'         TEST OPEN COLUMN                             
         BO    *+12                                                             
         MVI   0(RF),C'&&'                                                      
         LA    RF,1(RF)                                                         
         MVC   0(1,RF),0(R1)                                                    
         OI    0(RF),X'40'                                                      
         LA    RF,1(RF)                                                         
         LA    R1,1(R1)                                                         
         BCT   R0,DO1702                                                        
         B     DOX                                                              
*                                                                               
DO18     MVC   8(5,R2),=C'ZERO%'   PERCENTAGE EXPRESSION (6 DEC)                
         CP    OPDDATA(5),=P'0'                                                 
         BE    DOX                                                              
         EDIT  (P5,OPDDATA),(11,8(R2)),6,ALIGN=LEFT                             
         LA    R1,19(R2)           R1 TO END OF EXPRESSION                      
         B     DO1B                                                             
*                                                                               
DO19     DS    0H                                                               
         LA    R4,8(R2)                                                         
         LA    R3,OPDDATA                                                       
         MVI   MYBYTE,0                                                         
*                                                                               
DO19A    CLI   0(R3),0                                                          
         BE    DOX                                                              
         CLI   MYBYTE,0                                                         
         BE    DO19B                                                            
         MVI   0(R4),C','                                                       
         AHI   R4,1                                                             
*                                                                               
DO19B    MVI   MYBYTE,1                                                         
         EDIT  (B1,0(R3)),(3,0(R4)),0,ALIGN=LEFT,WRK=MYWORK,DUB=MYDUB           
         AR    R4,R0                                                            
         AHI   R3,1                                                             
         B     DO19A                                                            
*                                                                               
DO20     SR    R0,R0                                                            
         IC    R0,OPDLN                                                         
         AHI   R0,-OPDLN1Q                                                      
         LA    RF,8(R2)                                                         
         LA    RE,OPDDATA                                                       
         B     DO20B                                                            
*                                                                               
DO20A    CLI   0(RE),C' '                                                       
         BNH   DOX                                                              
         MVI   1(RF),C','                                                       
         AHI   RF,2                                                             
*                                                                               
DO20B    MVC   0(1,RF),0(RE)                                                    
         AHI   RE,1                                                             
         BCT   R0,DO20A                                                         
         B     DOX                                                              
*                                                                               
DO21     SR    R0,R0                                                            
         IC    R0,OPDLN                                                         
         AHI   R0,-OPDLN1Q                                                      
         LA    RF,8(R2)                                                         
         LA    RE,OPDDATA                                                       
         B     DO21B                                                            
DO21A    CLI   0(RE),C' '                                                       
         BNH   DOX                                                              
         MVI   1(RF),C','                                                       
         AHI   RF,2                                                             
DO21B    MVC   0(1,RF),0(RE)                                                    
         AHI   RE,1                                                             
         BCT   R0,DO21A                                                         
         B     DOX                                                              
*                                                                               
DO22     MVC   8(4,R2),=C'ZERO%'   PERCENTAGE EXPRESSION (2 DEC + PL3)          
         MVI   8+4(R2),C'%'                                                     
         CP    OPDDATA(3),=P'0'                                                 
         BE    DOX                                                              
         EDIT  (P3,OPDDATA),(9,8(R2)),2,ALIGN=LEFT                              
         LA    R1,17(R2)           R1 TO END OF EXPRESSION                      
         B     DO1B                                                             
*                                                                               
DO24     CLI   OPDDATA,X'06'                                                    
         BNE   DO24A                                                            
         MVC   8(15,R2),=CL15'CLIENT APPROVED'                                  
         B     DOX                                                              
*                                                                               
DO24A    CLI   OPDDATA,X'07'                                                    
         BNE   DO24B                                                            
         MVC   8(16,R2),=CL16'MANAGER APPROVED'                                 
         B     DOX                                                              
*                                                                               
DO24B    CLI   OPDDATA,X'01'                                                    
         BNE   DO24E                                                            
         MVC   8(4,R2),=CL4'NONE'                                               
         B     DOX                                                              
*                                                                               
DO24C    CLI   OPDDATA,X'01'                                                    
         BNE   DO24D                                                            
         MVC   8(4,R2),=CL4'NONE'                                               
         B     DOX                                                              
*                                                                               
DO24D    CLI   OPDDATA,X'05'                                                    
         BNE   DO24E                                                            
         MVC   8(13,R2),=CL13'PART APPROVED'                                    
         B     DOX                                                              
*                                                                               
DO24E    CLI   OPDDATA,X'02'                                                    
         BNE   DO24F                                                            
         MVC   8(8,R2),=CL8'APPROVED'                                           
         B     DOX                                                              
*                                                                               
DO24F    CLI   OPDDATA,X'03'                                                    
         BNE   DO24G                                                            
         MVC   8(11,R2),=CL11'IN PROGRESS'                                      
         B     DOX                                                              
*                                                                               
DO24G    CLI   OPDDATA,X'04'                                                    
         BNE   DOX                                                              
         MVC   8(9,R2),=CL9'SUBMITTED'                                          
         B     DOX                                                              
*                                                                               
DO25     CLI   OPDDATA,X'01'                                                    
         BNE   DO25A                                                            
         MVC   8(4,R2),=C'NONE'                                                 
         B     DO25X                                                            
*                                                                               
DO25A    CLI   OPDDATA,X'02'                                                    
         BNE   DO25B                                                            
         MVC   8(6,R2),=CL6'ERROR'                                              
         B     DO25X                                                            
*                                                                               
DO25B    CLI   OPDDATA,X'03'                                                    
         BNE   DO25X                                                            
         MVC   8(7,R2),=C'WARNING'                                              
*                                                                               
DO25X    B     DOX                                                              
*                                                                               
DO26     EDIT  (3,OPDDATA),(6,8(R2)),0,ALIGN=LEFT,ZERO=NOBLANK                  
         B     DOX                                                              
*                                                                               
DO27     CLI   OPDDATA,C'K'                                                     
         BNE   DO27A                                                            
         MVC   8(3,R2),=C'KEY'                                                  
         B     DOX                                                              
*                                                                               
DO27A    CLI   OPDDATA,C'A'                                                     
         BNE   DO27B                                                            
         MVC   8(3,R2),=C'ALL'                                                  
         B     DOX                                                              
*                                                                               
DO27B    CLI   OPDDATA,C'N'                                                     
         BNE   DOX                                                              
         MVC   8(2,R2),=C'NO'                                                   
         B     DOX                                                              
*                                                                               
DO28     CLI   OPDDATA,X'01'                                                    
         BNE   DO28A                                                            
         MVC   8(8,R2),=C'APPROVER'                                             
         B     DOX                                                              
*                                                                               
DO28A    CLI   OPDDATA,X'02'                                                    
         BNE   DO28B                                                            
         MVC   8(9,R2),=C'SUBMITTER'                                            
         B     DOX                                                              
*                                                                               
DO28B    CLI   OPDDATA,X'03'                                                    
         BNE   DOX                                                              
         MVC   8(4,R2),=C'NONE'                                                 
         B     DOX                                                              
*                                                                               
DO29     CLI   OPDDATA,X'01'                                                    
         BNE   DO29A                                                            
         MVC   8(8,R2),=C'MEDIA SI'                                             
         B     DOX                                                              
*                                                                               
DO29A    CLI   OPDDATA,X'02'                                                    
         BNE   DO29B                                                            
         MVC   8(8,R2),=C'MEDIA SK'                                             
         B     DOX                                                              
*                                                                               
DO29B    CLI   OPDDATA,X'03'                                                    
         BNE   DO29C                                                            
         MVC   8(12,R2),=C'WORK CODE SI'                                        
         B     DOX                                                              
*                                                                               
DO29C    CLI   OPDDATA,X'04'                                                    
         BNE   DOX                                                              
         MVC   8(12,R3),=C'WORK CODE SK'                                        
         B     DOX                                                              
*                                                                               
DO30     CLI   OPDDATA,C'D'        DRAFT?                                       
         BNE   DO6                                                              
         MVC   8(5,R2),=C'DRAFT'                                                
         B     DOX                                                              
*                                                                               
DO31     MVI   MYBYTE,C'1'         1 CHAR ENTRIES                               
*                                                                               
DO31A    LLC   R1,OPDLN                                                         
         SHI   R1,OPDLN1Q+1                                                     
         MVC   MYWORK(0),OPDDATA                                                
         EX    R1,*-6                                                           
         AHI   R1,1                                                             
         LA    RE,MYWORK                                                        
         LA    RF,8(R2)                                                         
*                                                                               
DO31C    XR    R4,R4               SEPARATE DATA BY COMMAS                      
         CLI   MYBYTE,C'2'                                                      
         BNE   *+8                                                              
         AHI   R4,1                                                             
         MVC   0(0,RF),0(RE)                                                    
         EX    R4,*-6                                                           
         BCT   R1,DO31D                                                         
         B     DOX                                                              
*                                                                               
DO31D    AHI   R4,1                                                             
         AR    RF,R4                                                            
         AR    RE,R4                                                            
         CLI   0(RE),C' '          ANY MORE ENTRIES?                            
         BNH   DO31C                                                            
         MVI   0(RF),C','                                                       
         AHI   RF,1                                                             
         B     DO31C                                                            
*                                                                               
DO32     MVI   MYBYTE,C'2'         2 BYTE ENTRIES                               
         B     DO31A                                                            
*                                                                               
DO33     CLI   OPDDATA,X'01'                                                    
         BNE   DO33A                                                            
         MVC   8(5,R2),=C'COST'                                                 
         B     DOX                                                              
*                                                                               
DO33A    CLI   OPDDATA,X'02'                                                    
         BNE   DOX                                                              
         MVC   8(5,R2),=C'SALES'                                                
         B     DOX                                                              
*                                                                               
DO34     CLI   OPDDATA,C' '                                                     
         BE    DOX                                                              
         OC    OPDDATA(1),OPDDATA                                               
         BZ    DOX                                                              
         LLC   RE,OPDDATA                                                       
         CVD   RE,MYWORK                                                        
         OI    MYWORK+7,X'0F'                                                   
         UNPK  MYWORK+10(4),MYWORK+6(2)                                         
         LA    RF,MYWORK+10                                                     
         LHI   RE,4                                                             
DO34A    CLI   0(RF),X'F0'                                                      
         BH    DO34B                                                            
         AHI   RF,1                                                             
         BCT   RE,DO34A                                                         
         DC    H'0'                                                             
DO34B    SHI   RE,1                                                             
         MVC   8(0,R2),0(RF)                                                    
         EX    RE,*-6                                                           
         B     DOX                                                              
*                                                                               
DO37     CLI   OPDDATA,X'06'                                                    
         BNE   DO37A                                                            
         MVC   8(15,R2),=C'CLIENT APPROVED'                                     
         B     DOX                                                              
*                                                                               
DO37A    CLI   OPDDATA,X'07'                                                    
         BNE   DO24D                                                            
         MVC   8(16,R2),=C'MANAGER APPROVED'                                    
         B     DOX                                                              
*                                                                               
DO38     CLI   OPDDATA,C'C'         CONTINGENCY                                 
         BNE   DO38A                                                            
         MVC   8(11,R2),=C'CONTINGENCY'                                         
         B     DOX                                                              
*                                                                               
DO38A    CLI   OPDDATA,C'Y'         YES                                         
         BNE   DO38B                                                            
         MVC   8(3,R2),=C'YES'                                                  
         B     DOX                                                              
*                                                                               
DO38B    CLI   OPDDATA,C'N'         NO                                          
         BNE   DOX                                                              
         MVC   8(2,R2),=C'NO'                                                   
         B     DOX                                                              
*                                                                               
DO39     CLI   OPDDATA,C'A'         AMOUNT                                      
         BNE   DO38A                                                            
         MVC   8(6,R2),=C'AMOUNT'                                               
         B     DOX                                                              
*                                                                               
DO40     ZIC   R1,OPDLN            CHARACTERS - LEFT ALIGN                      
         LA    RE,OPDDATA                                                       
         CLI   0(RE),C' '          DISPLAY FROM FIRST NON-BLANK                 
         BH    DO40A                                                            
         LA    RE,1(RE)                                                         
         BCT   R1,*-12                                                          
         DC    H'0'                                                             
DO40A    SHI   R1,OPDLN1Q+1                                                     
         EX    R1,*+8                                                           
         B     DOX                                                              
         MVC   8(0,R2),0(RE)                                                    
*                                                                               
DOX      XMOD1 1                                                                
         EJECT                                                                  
* LITERAL POOL                                                                  
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
       ++INCLUDE ACOPTTAB                                                       
         EJECT                                                                  
* DSECT TO COVER WORKING STORAGE                                                
*                                                                               
WORKD    DSECT                                                                  
PARMS    DS    0C                                                               
ADOBLOCK DS    A                                                                
PARML    EQU   *-PARMS                                                          
*                                                                               
WORK     DS    CL64                                                             
DUB      DS    D                                                                
SPACES   DS    CL80                                                             
*                                                                               
BYTE     DS    X                                                                
ELEMENT  DS    CL256                                                            
*                                                                               
MYDUB    DS    D                                                                
MYWORK   DS    CL64                                                             
MYBYTE   DS    XL1                                                              
WORKX    EQU   *                                                                
         EJECT                                                                  
DOBLOCKD DSECT                                                                  
       ++INCLUDE ACDOBLOCK                                                      
         EJECT                                                                  
       ++INCLUDE ACOPTTABD                                                      
         EJECT                                                                  
       ++INCLUDE ACOPTEQUS                                                      
         EJECT                                                                  
GOBLOCKD DSECT                                                                  
       ++INCLUDE ACGOBLOCK                                                      
         EJECT                                                                  
GOPANBKD DSECT                                                                  
       ++INCLUDE ACPANBLOCK                                                     
         EJECT                                                                  
GOXBLKD  DSECT                                                                  
       ++INCLUDE ACGOXBLOCK                                                     
GOBBLKD  DSECT                                                                  
       ++INCLUDE ACGOBBLOCK                                                     
*                                                                               
* ACGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
         PRINT ON                                                               
* DDCOMFACS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'027ACDISOPT  07/01/20'                                      
         END                                                                    
