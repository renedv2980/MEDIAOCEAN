*          DATA SET DDREQTWA   AT LEVEL 005 AS OF 06/09/17                      
*CATALP REQTWA                                                                  
REQTWA   TITLE '- TWA TO REQUEST TO TWA'                                        
*==================================BYTE 1=========BYTES 2-4===========*         
* TWA TO REQUESTS       P1        0              A(TWA)               *         
*  (OR SPOON IF         P2                       A(106 BYTE REQUEST)  *         
*   TWA+25=X'02')       P3        SYSTEM         A(DATAMGR) OR A(HOOK)*         
*                                                OR A(10K RFP BLK) IF *         
*                                                P6 PRESENT           *         
*  (OR LINKED IF        P4        X'80'          A(COMFACS)           *         
*   REQ+15=X'01')                 = P6 PRESENT                        *         
*                       P5                       A(SPOOK) IF SYSTEM=0 *         
*                       P6                       A(REQUESTOR FLD)     *         
*                                                                     *         
* NOTE P3 SET TO RETURN A(PQKEY)                                      *         
*      P6 IS OPTIONAL - USED ONLY FOR RFP REQUESTS                    *         
*==================================BYTE 1=========BYTES 2-4===========*         
* REQUEST TO TWA        P1        1              A(TWA)               *         
*  (HEADERS)            P2        N'HEADERS      A(80 BYTE REQUEST)   *         
*==================================BYTE 1=========BYTES 2-4===========*         
* REQUEST TO TWA        P1        2              A(TWA)               *         
*  (REST OF FIELDS)     P2                       A(80 BYTE REQUEST)   *         
*==================================BYTE 1=========BYTES 2-4===========*         
* PRINT TWA             P1        3              A(TWA)               *         
*                       P2                       X'FF',AL3(COMFACS)   *         
*                       P3                       V(PRINT)             *         
*                       P4        C'B'           V(BOXAREA)           *         
*==================================BYTE 1=========BYTES 2-4===========*         
* WRITE OUT REQUESTS    P1        4              A(TWA)               *         
*                       P2                       A(106 BYTE REQUEST)  *         
*                       P3        SYSTEM         V(DATAMGR)           *         
*                       P4                       V(COMFACS)           *         
*                       P5        SEE NOTE BELOW A(SPOOK) IF SYSTEM=0 *         
*                       P6                       A(RFHDR IF P5 X'40'  *         
* NOTES ON P5 BYTE 0                                                  *         
* IF X'80' THE SPOOK DATA OVERRIDES THE UTL DATA (FOR STEP2 REQUESTS) *         
* IF X'40' THEN P6 IS A(RFHDR) FOR STEP 2 REQUESTS                    *         
* THIS IS USED WHEN SRUPD00 GENERATES A SECOND JOB AFTER FILE UPDATE  *         
*==================================BYTE 1=========BYTES 2-4===========*         
* WRITE OUT LINKED REQ  P1        5              A(TWA)               *         
*                       P2                       A(26+(N*80) BYTE REQ)*         
*                       P3        SYSTEM         V(DATAMGR)           *         
*                       P4                       V(COMFACS)           *         
*                       P5                       A(SPOOK) IF SYSTEM=0 *         
*                                                                     *         
* NOTE:  RFP IS DISABLED FOR THE UK - TO ENABLE REMOVE ALL THE *&&US  *         
*        CARDS                                                        *         
*=====================================================================*         
                                                                                
REQTWA   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKL,**RQTW**,CLEAR=YES                                         
         USING WORKD,RC                                                         
         LR    R9,R1               R9=A(PARAMETER LIST)                         
         SAM24 ,                   24 bit mode                                  
                                                                                
         MVI   SPACES,C' '                                                      
         MVC   SPACES+1(L'SPACES-1),SPACES                                      
         EJECT                                                                  
***********************************************************************         
* INTERPRET PARAMETERS                                                *         
***********************************************************************         
                                                                                
         USING SPOOK,SPOKAREA                                                   
         XC    SPOOK(SPOOKXL),SPOOK                                             
                                                                                
         L     R2,0(R9)                                                         
         MVC   VPRINT,8(R9)                                                     
         MVC   ACOMFACS,12(R9)                                                  
         MVC   SPOOKSYS,8(R9)                                                   
                                                                                
         CLI   0(R9),0             SPOOK NEEDED FOR 0 (TWA TO REQ)              
         BE    *+12                                                             
         CLI   0(R9),4             AND 4,5 (WRITE REQUESTS)                     
         BL    SYS12                                                            
         TM    16(R9),X'80'        TEST USE SPOOK DATA REQUESTED                
         BO    SYS2                                                             
         CLI   SPOOKSYS,0          WAS ZERO SYSTEM PASSED                       
         BNE   SYS4                NO - USE OLD CODE                            
*                                                                               
SYS2     L     R1,16(R9)           YES - COPY SPOOK                             
         MVC   SPOOK(SPOOKXL),0(R1)                                             
         B     SYS12                                                            
                                                                                
SYS4     LA    R1,SYSTWO           START OF OLD CODE                            
         LHI   R0,SYSTWON                                                       
SYS6     CLC   0(1,R1),SPOOKSYS                                                 
         BE    SYS10                                                            
         AHI   R1,L'SYSTWO                                                      
         BCT   R0,SYS6                                                          
         DC    H'0'                                                             
                                                                                
SYS10    MVC   SPOOKSYS,0(R1)                                                   
         MVC   SPOOKWEN,25(R2)                                                  
SYS12    MVC   ATWA,0(R9)                                                       
         SR    RF,RF                                                            
         IC    RF,0(R9)            SELECT ROUTINE                               
         SLL   RF,2                                                             
         B     GOROUT(RF)                                                       
                                                                                
GOROUT   B     INPUT                                                            
         B     OUTPUT                                                           
         B     OUTPUT                                                           
         B     PRINT                                                            
         B     QOUT                                                             
         B     LQOUT                                                            
                                                                                
SYSTWO   DS    0CL2                                                             
         DC    C'SPPPMEACRECTTA'                                                
SYSTWON  EQU   (*-SYSTWO)/L'SYSTWO                                              
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO CONVERT TWAS TO REQUEST(S)                               *         
***********************************************************************         
                                                                                
INPUT    L     R3,4(R9)            A(REQUEST)                                   
         MVC   QHDR,0(R3)          MOVE REQUEST HEADER                          
         MVC   QINFO,26(R3)        MOVE REQUEST/AGY/SIN                         
         MVI   QCTR,0              RESET CARD COUNTER                           
         SR    R5,R5                                                            
         SR    R4,R4                                                            
         AHI   R2,CONHEADH-TWAD    R2=A(FIRST TWA FIELD HEADER)                 
*&&US                                                                           
         SR    RA,RA                                                            
         TM    12(R9),X'80'        RFP REQUEST?                                 
         BZ    INPUT02                                                          
         MVI   INFLAG,0            SET DOING FIELD ZERO                         
         LHI   R7,L'SCREEN                                                      
         LHI   R6,L'SCREEN+5                                                    
         TM    12(R9),X'FF'-X'80'  TEST OFFLINE RFP REQUEST?                    
         BNO   *+18                                                             
         L     RF,0(R9)                                                         
         MVC   SCREEN,0(RF)        YES - SCREEN PHASE IS AT TWA+0               
         B     INPUT10                                                          
                                                                                
         L     RF,ACOMFACS                                                      
         L     RF,CSWITCH-COMFACSD(RF)                                          
         ICM   R0,15,EFFS                                                       
         GOTOR (RF),PARAS,(R0)                                                  
         L     RF,0(R1)                                                         
         SR    R1,R1                                                            
         ICM   R1,1,TSCRNE-UTLD(RF)                                             
         BZ    INPUT02                                                          
         CHI   R1,1                MUST NOT BE VIRGIN SCREEN                    
         BE    INPUT02                                                          
         MHI   R1,L'TSCRNUM                                                     
         LA    R1,TSCRNUM-L'TSCRNUM-UTLD(R1,RF)                                 
         MVC   WORK+0(1),TCOSYS-UTLD(RF)                                        
         MVC   WORK+1(1),TPRG-UTLD(RF)                                          
         MVC   WORK+2(1),0(R1)                                                  
         XOUT  WORK,SCREEN,3                                                    
         MVI   SCREEN,C'T'         SCREEN NOW CONTAINS TSPPOO                   
         B     INPUT10                                                          
*&&                                                                             
INPUT02  MVI   INFLAG,1            SET NOT DOING FIELD ZERO                     
         TM    1(R2),X'20'         UNPROTECTED FIELD                            
         BO    INPUT20                                                          
         AHI   R5,1                BUMP FIELD NUMBER COUNTER                    
         CHI   R5,1                IGNORE THE FIRST AS IT IS THE                
         BE    INPUT20             SERVICE REQUEST FIELD                        
                                                                                
         SR    R8,R8               R8=NOT AN EXTENDED FIELD HEADER              
         SR    R1,R1                                                            
         IC    R1,0(R2)            CHECK LENGTH NOT GREATER                     
         SHI   R1,8                THAN MAXIMUM LENGTH                          
         TM    1(R2),X'02'         TEST EXTENDED HEADER IS PRESENT              
         BZ    *+12                                                             
         LA    R8,0(R1,R2)         R8=A(EXTENDED FIELD HEADER)                  
         SHI   R1,8                (ADJUST FIELD LENGTH)                        
*&&US                                                                           
         SR    RA,RA               CLEAR FIELD INDEX NUMBER                     
         TM    12(R9),X'80'        TEST RFP REQUEST                             
         BZ    INPUT06             NO                                           
         LTR   R8,R8               TEST EXTENDED FIELD HEADER PRESENT           
         BZ    INPUT06             NO                                           
         CLI   0(R8),0             TEST FIELD NUMBER IS PRESENT                 
         BE    INPUT06                                                          
                                                                                
         MVC   THISFNUM,0(R8)      SET CURRENT FIELD NUMBER                     
         SR    RE,RE                                                            
         IC    RE,0(R8)                                                         
         LA    RE,FLDOCCUR(RE)                                                  
         IC    RA,0(RE)            INCREMENT FIELD OCCURRENCES                  
         AHI   RA,1                                                             
         STC   RA,0(RE)                                                         
         CHI   R5,7                IGNORE FIELDS 1 THROUGH 7                    
         BH    INPUT06             (BUT DO COUNT OCCURRENCES)                   
         SR    RA,RA                                                            
*&&                                                                             
INPUT06  SR    R7,R7                                                            
         ICM   R7,1,5(R2)          GET DATA LENGTH                              
         BZ    INPUT20             NO DATA                                      
                                                                                
         CR    R7,R1               MAKE SURE DATA LENGTH IS VALID               
         BNH   *+6                                                              
         DC    H'0'                                                             
                                                                                
         LA    R6,5(R7)            CARD COLUMNS REQUIRED                        
         CR    R6,R4               IS THERE ROOM FOR ENTIRE FIELD?              
         BNH   INPUT12             YES                                          
                                                                                
         CHI   R7,66               COULD THE FIELD REQUIRE 3 CARDS?             
         BL    INPUT08             NO, IT WILL FIT IN 2 CARDS                   
         LR    R1,R7                                                            
         SHI   R1,64               R1=L'DATA BEYOND FIRST CARD                  
         AHI   R1,5                ADD OVERHEAD FOR FIELD                       
         CR    R4,R1               WILL FIELD FIT ON TWO CARDS?                 
         BL    INPUT10             NO, START NEW CARD (NO 3-CARD SPLIT)         
                                                                                
INPUT08  CHI   R4,6                IS THERE ROOM TO BEGIN A NEW FIELD           
         BL    INPUT10             NO                                           
         CHI   R6,9                IS IT WORTH SPLITTING UP THE FIELD?          
         BNH   INPUT10             NO                                           
                                                                                
         CLM   R2,7,21(R9)         DON'T SPLIT REQUESTOR FIELD                  
         BE    INPUT10                                                          
*&&US                                                                           
         CLC   =C'&&&&OUT',8(R2)   AND OUTPUT TYPE FIELD                        
         BE    INPUT10                                                          
         CLC   =C'&&&&DST',8(R2)   AND DESTINATION FIELD                        
         BE    INPUT10                                                          
         LR    R0,R7               AND FIELDS THAT CONTAIN RFP SYMBOLS          
         LA    R1,8(R2)                                                         
         BASR  RE,0                                                             
         CLI   0(R1),X'20'                                                      
         BL    *+12                                                             
         CLI   0(R1),X'30'                                                      
         BNH   INPUT10                                                          
         AHI   R1,1                                                             
         BCTR  R0,RE                                                            
*&&                                                                             
         B     INPUT12             ALL OTHER FIELDS MAY BE SPLIT                
                                                                                
INPUT10  SR    R3,R3                                                            
         IC    R3,QCTR             USE A NEW REQUEST CARD                       
         LA    R0,1(R3)            BUMP CARD COUNT                              
         STC   R0,QCTR             AND SAVE                                     
                                                                                
         MHI   R3,CCOLUMNS                                                      
         LA    R3,QINFO(R3)        POINT TO NEW REQUEST                         
         MVC   0(HCOLUMNS,R3),QINFO MOVE REQ/AGY/SIN                            
         MVC   HCOLUMNS(DCOLUMNS,R3),SPACES                                     
                                                                                
         AHI   R3,HCOLUMNS         POINT TO OUTPUT COLUMN                       
         LHI   R4,DCOLUMNS         AND SET REMAINING COLUMNS                    
                                                                                
INPUT12  CVD   R5,DUB              FIELD NUMBER                                 
         OI    DUB+L'DUB-1,X'0F'                                                
         UNPK  0(2,R3),DUB                                                      
*&&US                                                                           
         LTR   RA,RA               TEST FIELD NUMBER PRESENT                    
         BZ    INPUT14                                                          
         MVC   0(1,R3),THISFNUM    YES - SET FIELD NUMBER                       
         CLI   0(R3),64            CONVERT FIELD#64 TO NULL                     
         BNE   *+8                                                              
         MVI   0(R3),0                                                          
         STC   RA,1(R3)            SET FIELD INDEX VALUE                        
*&&                                                                             
INPUT14  CVD   R7,DUB              LENGTH                                       
         OI    DUB+L'DUB-1,X'0F'                                                
         UNPK  2(2,R3),DUB                                                      
         CR    R6,R4               IS THERE ROOM FOR ENTIRE FIELD?              
         BNH   INPUT16             YES                                          
                                                                                
         SHI   R4,6                4 FOR OVERHEAD, 1 FOR EX, 1 FOR EOR          
         EX    R4,*+8                                                           
         B     *+10                                                             
         MVC   4(0,R3),8(R2)       MOVE IN AS MUCH AS WILL FIT                  
         AHI   R4,1                R4=NUMBER OF CHARACTERS MOVED                
         CVD   R4,DUB              LENGTH                                       
         OI    DUB+L'DUB-1,X'0F'                                                
         UNPK  2(2,R3),DUB                                                      
                                                                                
         SR    R3,R3                                                            
         IC    R3,QCTR             WE NEED A NEW REQUEST CARD                   
         LA    R0,1(R3)            BUMP CARD COUNT                              
         STC   R0,QCTR             AND SAVE                                     
                                                                                
         MHI   R3,CCOLUMNS                                                      
         LA    R3,QINFO(R3)        POINT TO NEW REQUEST                         
         MVC   0(HCOLUMNS,R3),QINFO MOVE REQ/AGY/SIN                            
         MVC   HCOLUMNS(DCOLUMNS,R3),SPACES                                     
         AHI   R3,HCOLUMNS         POINT TO OUTPUT COL                          
                                                                                
         LA    RF,8(R4,R2)         RF=A(TWA FLD DATA AT CONTINUATION)           
         SR    R7,R4               R7=NUMBER OF CHARACTERS REMAINING            
                                                                                
         CVD   R5,DUB              FIELD NUMBER                                 
         OI    DUB+L'DUB-1,X'0F'                                                
         UNPK  0(2,R3),DUB                                                      
*&&US                                                                           
         LTR   RA,RA               TEST FIELD NUMBER PRESENT                    
         BZ    *+14                                                             
         MVC   0(1,R3),THISFNUM    YES - SET FIELD NUMBER                       
         STC   RA,1(R3)            AND FIELD INDEX VALUE                        
*&&                                                                             
         CVD   R7,DUB              LENGTH                                       
         OI    DUB+L'DUB-1,X'0F'                                                
         UNPK  2(2,R3),DUB                                                      
         BCTR  R7,0                                                             
         EX    R7,*+8                                                           
         B     *+10                                                             
         MVC   4(0,R3),0(RF)       MOVE IN REMAINDER OF FIELD                   
         AHI   R7,6                                                             
         LHI   R4,DCOLUMNS         SET REMAINING COLS                           
         SR    R4,R7                                                            
         AR    R3,R7                                                            
         B     INPUT20                                                          
                                                                                
INPUT16  BCTR  R7,0                                                             
*&&US                                                                           
         LA    RF,SCREEN                                                        
         CLI   INFLAG,0                                                         
         BE    *+8                                                              
*&&                                                                             
         LA    RF,8(R2)                                                         
         EX    R7,*+8                                                           
         B     *+10                                                             
         MVC   4(0,R3),0(RF)                                                    
*&&US                                                                           
         TM    12(R9),X'80'        RFP REQUEST?                                 
         BZ    INPUT18                                                          
         CLM   R2,7,21(R9)         IS THIS REQUESTOR FLD?                       
         BNE   INPUT18                                                          
         CLI   INFLAG,0                                                         
         BE    INPUT18                                                          
         LHI   RF,CCOLUMNS+1       GET CURRENT COL                              
         SR    RF,R4               R4=REMAINING COLS                            
         LR    RE,R3               RE=A(REQ CARD FLD)                           
                                                                                
         CLI   0(RE),C','          DELIMITER?                                   
         BE    *+16                                                             
         AHI   RF,1                                                             
         AHI   RE,1                                                             
         B     *-16                                                             
         AHI   RF,1                COL # OF 1ST CHAR AFTER C','                 
         STC   RF,RQCOL            SAVE COL OF REQUESTOR FLD                    
         MVC   RQCARD,QCTR         SAVE CARD # OF REQUESTOR FLD                 
*&&                                                                             
INPUT18  AR    R3,R6                                                            
         SR    R4,R6                                                            
                                                                                
INPUT20  CLI   INFLAG,0            DON'T BUMP IF SCREEN ID                      
         BE    INPUT02                                                          
         GOTOR NXTFLD                                                           
         CLI   0(R2),0             TEST END OF TWA                              
         BNE   INPUT02                                                          
                                                                                
         BCTR  R3,0                BACK UP ONE COL                              
         MVI   0(R3),C'*'          INDICATE EOD                                 
                                                                                
         SR    RE,RE                                                            
         ICM   RE,1,QCTR           GET CARD COUNT                               
         BNZ   *+6                                                              
         DC    H'0'                                                             
         STC   RE,REQCON           SAVE CARD COUNT FOR SPOON                    
         OI    REQCON,X'10'        INDICATE MULTIPLE REQUEST CARD               
         BCTR  RE,0                HAVE TO PASS COUNT - 1                       
         SLL   RE,4                LEFT ALIGN                                   
         STC   RE,QHDR+15          SET IN REQUEST                               
         OI    QHDR+15,X'09'       SET LINKED/REQTWA FLAGS ON                   
         B     PUTREQ              GO AND WRITE REQUEST                         
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO CONVERT REQUEST TO TWA                                   *         
***********************************************************************         
                                                                                
OUTPUT   L     R3,4(R9)            A(REQ)                                       
         L     R8,0(R9)            A(TWA)                                       
         USING TWAD,R8                                                          
                                                                                
         MVC   REQ(CCOLUMNS),0(R3)                                              
         LA    R3,REQ+HCOLUMNS                                                  
         MVC   REQ+CCOLUMNS(26),SPACES                                          
         CLI   REQ+4,C'*'          TEST TURNAROUND REQUEST                      
         BE    OUTPUT50                                                         
*&&US                                                                           
         CLC   =C'00',0(R3)        TEST RFP SCREEN ID (FIELD#0)                 
         BNE   OUTPUT02                                                         
         PACK  DUB,2(2,R3)         L'SCREEN ID                                  
         CVB   RE,DUB                                                           
         LA    R3,5(R3,RE)         POINT TO FIRST REAL DATA                     
*&&                                                                             
OUTPUT02 DS    0H                                                               
*&&US                                                                           
         TM    1(R3),X'F0'         TEST FIELD NUMBER/INDEX PRESENT              
         BO    OUTPUT04                                                         
         SR    R4,R4                                                            
         ICM   R4,1,0(R3)          R4=FIELD NUMBER                              
         BNZ   *+8                                                              
         LHI   R4,64               NULL MEANS FIELD#64                          
         SR    RA,RA                                                            
         ICM   RA,1,1(R3)          RA=FIELD INDEX NUMBER                        
         BNZ   OUTPUT06                                                         
         DC    H'0'                                                             
*&&                                                                             
OUTPUT04 PACK  DUB,0(2,R3)         FIELD NUMBER                                 
         CVB   R4,DUB                                                           
*&&US*&& SR    RA,RA               CLEAR FIELD INDEX NUMBER                     
                                                                                
OUTPUT06 CLI   0(R9),1             1=CONVERT FIELDS 2 AND 3 ONLY                
         BNE   OUTPUT14                                                         
                                                                                
         MVI   TWAFLDNM,0          CLEAR THE CONTINUATION NUMBER                
*&&US*&& MVI   TWAFLDNX,0          AND THE FIELD INDEX NUMBER                   
                                                                                
         CLI   4(R9),1             SOME APPLICATIONS NEED ONE FIELD             
         BE    OUTPUT12                                                         
         CLI   4(R9),4             (NEW CONTROLLERS WANT 4 AND 5 ALSO)          
         BE    OUTPUT08                                                         
         CLI   4(R9),5             SOME NEED 6                                  
         BE    OUTPUT10                                                         
         CLC   0(2,R3),=C'03'                                                   
         BH    XIT                                                              
         B     OUTPUT14                                                         
                                                                                
OUTPUT08 CLC   0(2,R3),=C'05'                                                   
         BH    XIT                                                              
         B     OUTPUT14                                                         
                                                                                
OUTPUT10 CLC   0(2,R3),=C'06'                                                   
         BH    XIT                                                              
         B     OUTPUT14                                                         
                                                                                
OUTPUT12 CLC   0(2,R3),=C'02'                                                   
         BH    XIT                                                              
                                                                                
OUTPUT14 DS    0H                                                               
*&&US                                                                           
         LTR   RA,RA               TEST FIELD INDEX PRESENT                     
         BZ    OUTPUT18                                                         
         CLM   R4,1,TWAFLDNM       TEST SAME FIELD AS LAST TIME                 
         BNE   OUTPUT22                                                         
         CLM   RA,1,TWAFLDNX       AND SAME INDEX NUMBER                        
         BNE   OUTPUT22                                                         
         LA    R2,CONHEADH         YES - LOCATE PARTIAL FIELD                   
         SR    R0,R0                                                            
         LR    R5,RA                                                            
         BASR  RE,0                                                             
         ICM   R0,1,0(R2)                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         TM    1(R2),X'20'         TEST PROTECTED                               
         BNZ   OUTPUT16                                                         
         TM    1(R2),X'02'         TEST EXTENDED FIELD HEADER                   
         BZ    OUTPUT16                                                         
         LR    R1,R0                                                            
         SHI   R1,8                                                             
         LA    R1,0(R2,R1)                                                      
         CLM   R4,1,0(R1)          MATCH FIELD NUMBER                           
         BNE   OUTPUT16                                                         
         BCT   R5,OUTPUT16         YES - DO UNTIL INDEX SATISFIED               
         B     OUTPUT20                                                         
OUTPUT16 AR    R2,R0                                                            
         BR    RE                                                               
*&&                                                                             
OUTPUT18 CLM   R4,1,TWAFLDNM       DOES FIELD NUMBER MATCH PREVIOUS?            
         BNE   OUTPUT22            NO                                           
                                                                                
         LA    R2,CONHEADH                                                      
         LR    R5,R4                                                            
         SR    R0,R0                                                            
         BASR  RE,0                                                             
         ICM   R0,1,0(R2)          LOCATE FIELD IN TWA                          
         BNZ   *+6                                                              
         DC    H'0'                                                             
         TM    1(R2),X'20'         TEST PROTECTED FIELD                         
         BNZ   *+12                                                             
         BCT   R5,*+8                                                           
         B     OUTPUT20                                                         
         AR    R2,R0                                                            
         BR    RE                                                               
                                                                                
OUTPUT20 SR    RE,RE                                                            
         IC    RE,7(R2)            ACTUAL PARTIAL INPUT LENGTH                  
         LR    R1,RE                                                            
         PACK  DUB,2(2,R3)         LENGTH                                       
         CVB   R6,DUB                                                           
         AR    R1,R6                                                            
         STC   R1,5(R2)            NEW INPUT LENGTH                             
         LA    RE,8(RE,R2)                                                      
         BCTR  R6,0                                                             
         EX    R6,*+8                                                           
         B     *+10                                                             
         MVC   0(0,RE),4(R3)                                                    
         BCTR  R1,0                                                             
         EX    R1,*+8              TEST FIELD IS ALL SPACES                     
         BNE   OUTPUT40                                                         
         CLC   8(0,R2),SPACES                                                   
         MVI   5(R2),0             YES - SET NO INPUT                           
         B     OUTPUT40                                                         
                                                                                
OUTPUT22 CLC   =C'**',2(R3)        TEST FOR SOFT LENGTH '**'                    
         BNE   OUTPUT30            NO                                           
                                                                                
         LA    R6,4(R3)            A(1ST DATA CHARACTER)                        
OUTPUT24 CLC   =C'* ',0(R6)        TEST END OF DATA                             
         BE    OUTPUT28            YES                                          
         CLI   0(R6),C' '          TEST END OF CARD                             
         BNE   OUTPUT26                                                         
         CLI   1(R6),C'0'          ONLY ACCEPT ALPHA                            
         BNL   OUTPUT28                                                         
         CLI   1(R6),C' '          INCLUDING MIXED CASE                         
         BNH   OUTPUT28                                                         
OUTPUT26 AHI   R6,1                INCREMENT INPUT LENGTH                       
         B     OUTPUT24                                                         
                                                                                
OUTPUT28 SR    R6,R3                                                            
         SHI   R6,4                R6=INPUT LENGTH                              
         BP    OUTPUT32            YES                                          
         DC    H'0'                NO - INPUT LENGTH OF 0 NOT ALLOWED           
                                                                                
OUTPUT30 PACK  DUB,2(2,R3)         LENGTH                                       
         CVB   R6,DUB                                                           
                                                                                
OUTPUT32 DS    0H                                                               
*&&US                                                                           
         LTR   RA,RA               TEST FIELD INDEX PRESENT                     
         BZ    OUTPUT36                                                         
         LA    R2,CONHEADH                                                      
         SR    R0,R0                                                            
         LR    R5,RA                                                            
         BASR  RE,0                                                             
         ICM   R0,1,0(R2)                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         TM    1(R2),X'20'         TEST PROTECTED                               
         BNZ   OUTPUT34                                                         
         TM    1(R2),X'02'         TEST EXTENDED FIELD HEADER                   
         BZ    OUTPUT34                                                         
         LR    R1,R0                                                            
         SHI   R1,8                                                             
         LA    R1,0(R2,R1)                                                      
         CLM   R4,1,0(R1)          YES - MATCH FIELD NUMBER                     
         BNE   OUTPUT34                                                         
         BCT   R5,OUTPUT34         DO UNTIL INDEX SATISFIED                     
         B     OUTPUT38                                                         
OUTPUT34 AR    R2,R0                                                            
         BR    RE                                                               
*&&                                                                             
OUTPUT36 LA    R2,CONHEADH         A(FIRST TWA FIELD)                           
         LR    R5,R4                                                            
         SR    R0,R0                                                            
         BASR  RE,0                                                             
         ICM   R0,1,0(R2)                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         TM    1(R2),X'20'                                                      
         BNZ   *+12                                                             
         BCT   R5,*+8                                                           
         B     OUTPUT38                                                         
         AR    R2,R0                                                            
         BR    RE                                                               
                                                                                
OUTPUT38 STC   R4,TWAFLDNM         SAVE FIELD NUMBER                            
*&&US*&& STC   RA,TWAFLDNX         SAVE FIELD INDEX NUMBER                      
                                                                                
         LA    RE,REQ+HCOLUMNS     IF WE'RE AT FIRST FIELD                      
         CR    RE,R3                                                            
         BNE   *+8                                                              
         GOTOR CLRSCR              CLEAR ALL UNPROT FROM HERE DOWN              
                                                                                
         STC   R6,5(R2)            SET INPUT LENGTH                             
         SR    R1,R1                                                            
         IC    R1,0(R2)            CHECK LENGTH NOT GREATER                     
         SHI   R1,8                THAN MAXIMUM LENGTH                          
         TM    1(R2),X'02'         TEST EXTENDED HEADER                         
         BZ    *+8                                                              
         SHI   R1,8                                                             
         CR    R6,R1                                                            
         BNH   *+6                                                              
         DC    H'0'                                                             
                                                                                
         BCTR  R6,0                                                             
         EX    R6,*+8                                                           
         B     *+10                                                             
         MVC   8(0,R2),4(R3)       SET IN INPUT DATA                            
         EX    R6,*+8                                                           
         BNE   OUTPUT40                                                         
         CLC   8(0,R2),SPACES                                                   
         MVI   5(R2),0                                                          
                                                                                
OUTPUT40 GOTOR SETHDR                                                           
                                                                                
         LA    R3,5(R6,R3)                                                      
         CLC   0(2,R3),SPACES      TAKE OUT THIS CRAP                           
         BNE   *+8                                                              
         AHI   R3,2                                                             
         CLI   0(R3),C'*'          HAVE WE FINISHED DATA                        
         BE    OUTPUT42                                                         
         AHI   R3,1                                                             
         CLI   0(R3),C' '          HAVE WE FINISHED CARD                        
         BNE   OUTPUT02                                                         
         B     XIT                 YES - BACK TO CALLER FOR MORE                
                                                                                
OUTPUT42 MVI   0(R9),C'*'          SET NO MORE CARDS TO COME                    
                                                                                
         L     R2,0(R9)            SET FIELD HEADER VALUES                      
         AHI   R2,CONHEADH-TWAD                                                 
         SR    RF,RF                                                            
OUTPUT44 ICM   RF,1,0(R2)                                                       
         BZ    XIT                                                              
         GOTOR SETHDR                                                           
         AR    R2,RF                                                            
         B     OUTPUT44                                                         
         DROP  R8                                                               
         EJECT                                                                  
***********************************************************************         
* TURNAROUND REQUEST FORMAT IS IDENTIFIED BY '*' IN COL 5             *         
* FIELD DELIMITER IS C'.' AND TERMINATOR IS CL2'* '                   *         
*                                                                     *         
* Q Q A G * . P A T . L I S T . *                                     *         
* 0 1 2 3 4 5 6 7 8 9 0 1 2 3 4 5 6 7 8 9 0 . . .                     *         
***********************************************************************         
                                                                                
OUTPUT50 L     R2,0(R9)            TWA ADDRESS                                  
         AHI   R2,CONHEADH-TWAD                                                 
         GOTOR CLRSCR              CLEAR ALL UNPROTECTED FIELDS                 
         LA    R3,REQ+5            POINT TO FIRST INPUT FIELD                   
         LHI   R4,1                SET FIELD NUMBER COUNTER                     
                                                                                
OUTPUT52 GOTOR NXTFLD                                                           
         TM    1(R2),X'20'         FIND NEXT UNP FIELD IN TWA                   
         BZ    OUTPUT54                                                         
         CLI   0(R2),0             IF REACH END, STOP                           
         BE    OUTPUT42                                                         
         B     OUTPUT52                                                         
                                                                                
OUTPUT54 CLI   0(R3),C'.'          TEST DATA IN FIELD                           
         BNE   OUTPUT56            YES                                          
         AHI   R3,1                                                             
         B     OUTPUT58                                                         
                                                                                
OUTPUT56 LR    RE,R3               WORK OUT INPUT LENGTH                        
         SR    RF,RF                                                            
                                                                                
         CLI   0(RE),C'.'                                                       
         BE    *+12                                                             
         AHI   RE,1                                                             
         BCT   RF,*-12                                                          
                                                                                
         LPR   RF,RF                                                            
         SR    RE,RE                                                            
         IC    RE,0(R2)                                                         
         SHI   RE,8                                                             
         TM    1(R2),X'02'         TEST EXTENDED HEADER                         
         BZ    *+8                                                              
         SHI   RE,8                                                             
         CR    RF,RE               COMPARE ACTUAL LEN TO MAX                    
         BNH   *+6                                                              
         DC    H'0'                                                             
         STC   RF,5(R2)            SET INPUT LENGTH                             
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   8(0,R2),0(R3)                                                    
         GOTOR SETHDR              SET INPUT INDICATORS                         
         LA    R3,2(RF,R3)         POINT TO FIRST CHAR OF NEXT FIELD            
                                                                                
OUTPUT58 CLC   =C'* ',0(R3)        TEST END OF REQUEST                          
         BE    OUTPUT42            YES - GO SET EOD FLAG                        
         AHI   R4,1                BUMP FIELD NUMBER                            
         CLI   0(R9),1             TEST REQUEST FOR HEADERS ONLY                
         BNE   OUTPUT52                                                         
         CHI   R4,5                IF SO, PROCESS 5 FIELDS MAX                  
         BNH   OUTPUT52                                                         
         B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO CLEAR ALL UNPROTECTED FIELDS                             *         
***********************************************************************         
                                                                                
CLRSCR   SR    R0,R0                                                            
         LR    RF,R2                                                            
CLRSCR02 ICM   R0,1,0(RF)                                                       
         BZR   RE                                                               
         TM    1(RF),X'20'         TEST PROTECTED                               
         BNZ   CLRSCR04                                                         
         LR    R1,R0                                                            
         SHI   R1,9                                                             
         TM    1(RF),X'02'         TEST EXTENDED HEADER                         
         BZ    *+8                                                              
         SHI   R1,8                                                             
         EX    R1,*+8              CLEAR ALL UNP FIELDS                         
         B     CLRSCR04                                                         
         XC    8(0,RF),8(RF)                                                    
CLRSCR04 AR    RF,R0                                                            
         B     CLRSCR02                                                         
         EJECT                                                                  
***********************************************************************         
* PRINTING ROUTINES                                                   *         
***********************************************************************         
                                                                                
PRINT    CLI   12(R9),C'B'         TURN OFF BOXES ETC                           
         BNE   *+12                                                             
         L     RF,12(R9)                                                        
         MVI   BOXOFF-BOXD(RF),C'Y'                                             
         BRAS  RE,PIDINF           GET AND SET PID INFO                         
         MVC   P,SPACES                                                         
         GOTOR VPRINT,PARAS,P-1,BC01                                            
         L     RE,0(R9)                                                         
         SR    RF,RF                                                            
         ICM   RE,15,TWAMASTC-TWAD(RE)                                          
         BZ    *+8                                                              
         IC    RF,MCLANG-MASTD(RE)                                              
         MHI   RF,L'LANGDOR        INDEX INTO TEXT ARRAY                        
         LA    RF,LANGDOR(RF)                                                   
         SR    RE,RE               GET LENGTH OF TEXT                           
         IC    RE,0(RF)            GET LENGTH OF TEXT                           
         BCTR  RE,0                                                             
         LR    R0,RE               SAVE LENGTH OF TEXT-1                        
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   P(0),1(RF)                                                       
         OC    P(32),SPACES        CONVERT TO UPPER CASE                        
         GOTOR VPRINT,PARAS,P-1,BL01                                            
         LR    RE,R0                                                            
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   P(0),=31C'-'                                                     
         BASR  RE,RF                                                            
         MVC   P(L'PIDLINE),PIDLINE                                             
         BASR  RE,RF                                                            
         MVC   P,SPACES                                                         
         BASR  RE,RF                                                            
         LA    R3,PSTACK                                                        
         LHI   R4,24                                                            
                                                                                
PRINT02  MVC   0(CCOLUMNS,R3),SPACES CLEAR STACK                                
         AHI   R3,CCOLUMNS                                                      
         BCT   R4,PRINT02                                                       
         L     R2,0(R9)                                                         
         AHI   R2,CONHEADH-TWAD                                                 
                                                                                
PRINT04  CLI   0(R2),0             POST FROM TWA TO PRINT STACK                 
         BE    PRINT08                                                          
         TM    1(R2),X'0C'         TEST ZERO INTENSITY FIELD                    
         BO    PRINT06                                                          
         LH    R3,2(R2)                                                         
         LA    R3,PSTACK(R3)                                                    
         SR    R1,R1                                                            
         IC    R1,0(R2)                                                         
         SHI   R1,9                                                             
         TM    1(R2),X'02'         TEST EXTENDED HEADER                         
         BZ    *+8                                                              
         SHI   R1,8                                                             
         EX    R1,*+8                                                           
         B     PRINT06                                                          
         MVC   0(0,R3),8(R2)                                                    
PRINT06  GOTOR NXTFLD                                                           
         B     PRINT04                                                          
                                                                                
PRINT08  MVI   P,C'*'                                                           
         MVC   P+1(CCOLUMNS+1),P                                                
         GOTOR VPRINT,PARAS,P-1,BL01                                            
         LA    R3,PSTACK                                                        
         LHI   R4,24                                                            
                                                                                
PRINT10  MVC   P+1(CCOLUMNS),0(R3) PRINT THE STACK                              
         ICM   RF,15,=V(DICTATE)                                                
         BNZ   PRINT12                                                          
         CLI   4(R9),X'FF'         TEST CALLER PASSED A(COMFACS)                
         BNE   PRINT14                                                          
         ICM   RF,7,5(R9)                                                       
         ICM   RF,15,CDICTATE-COMFACSD(RF)                                      
         BZ    PRINT14                                                          
                                                                                
PRINT12  GOTOR (RF),PARAS,C'TU  ',('CCOLUMNS',P+1)                              
                                                                                
PRINT14  CHI   R4,24               TEST FIRST SCREEN LINE IN TWA                
         BNE   PRINT20                                                          
         CLI   P+2,C' '            ANY DATA IN SCREEN HEADER FIELD              
         BNE   PRINT20                                                          
*NOP*    MVC   P+2(78),PIDLINE     REPLACE FIRST LINE WITH PID INFO             
                                                                                
PRINT20  OC    P+1(CCOLUMNS),SPACES CONVERT TO UPPER CASE                       
         GOTOR VPRINT,PARAS,P-1,BL01                                            
         AHI   R3,CCOLUMNS                                                      
         BCT   R4,PRINT10                                                       
         MVC   P+1(CCOLUMNS+1),P                                                
         BASR  RE,RF                                                            
         B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
* GET AND SET PID INFO IN PIDINF                                      *         
***********************************************************************         
                                                                                
PIDINF   NTR1                                                                   
         MVC   PIDLINE,SPACES                                                   
         L     RF,0(R9)            TEST IF EXTRA DATA AREA DEFINED              
         ICM   RF,15,TWAMASTC-TWAD(RF)                                          
         BZ    PIDINFX                                                          
         ICM   RE,15,MCAEXTRA-MASTD(RF)                                         
         BZ    PIDINFX                                                          
                                                                                
PIDINF1  CLI   MCPID-MCEXTRA(RE),C' ' TEST IF ANY PID DATA DEFINED              
         BNH   PIDINF3                                                          
         MVC   PIDPIDH,=C'PID='                                                 
         MVC   PIDPID,MCPID-MCEXTRA(RE)                                         
         MVC   PIDFNAME,MCFNAME-MCEXTRA(RE)                                     
         MVC   PIDLNAME,MCLNAME-MCEXTRA(RE)                                     
                                                                                
PIDINF3  CLI   MCREQWHY-MASTD(RF),C' '                                          
         BNH   PIDINFX                                                          
         MVC   PIDWHYH,=C'WHY='                                                 
         MVC   PIDWHY,MCREQWHY-MASTD(RF)                                        
                                                                                
PIDINFX  XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
* PUT REQUEST RECORD(S) TO REQUEST FILE                               *         
***********************************************************************         
                                                                                
QOUT     MVI   REQCON,3            TYPE 4 = PUT SINGLE REQUEST CARD             
         L     R3,4(R9)                                                         
         MVC   QHDR(106),0(R3)     MOVE REQUEST CARD                            
         B     PUTREQ                                                           
                                                                                
LQOUT    L     R3,4(R9)            TYPE 5 = PUT LINKED REQUESTS                 
         MVC   REQCON,15(R3)       4 HOBS=N'CARDS-1  4 LOBS=1                   
         SR    R1,R1                                                            
         IC    R1,REQCON                                                        
         SRL   R1,4                SHIFT HOBS TO LOBS                           
         AHI   R1,1                R1=N'CARDS                                   
         CLI   SPOOKWEN,5          FOR UPDATIVE SOON REQUESTS                   
         BE    LQOUT02                                                          
         CLI   SPOOKWEN,6          AND FOR LUNATIC SOON REQUESTS                
         BE    LQOUT02                                                          
         CLI   SPOOKWEN,2          AND FOR SOON REQUESTS                        
         BNE   LQOUT04                                                          
                                                                                
LQOUT02  STC   R1,REQCON           STORE N'CARDS IN LOBS                        
         OI    REQCON,X'10'        AND SET LINKED REQUEST                       
                                                                                
LQOUT04  MHI   R1,CCOLUMNS         N'CARDS * L'CARD                             
         AHI   R1,26               + L'HDR                                      
         LA    R0,QHDR                                                          
         LR    RE,R3                                                            
         LR    RF,R1                                                            
         MVCL  R0,RE               MOVE HEADER AND REQUEST CARD(S)              
                                                                                
PUTREQ   L     R2,ATWA                                                          
         CLI   SPOOKWEN,2          TEST SOON                                    
         BE    SOON                                                             
         CLI   SPOOKWEN,5          TEST UPDATIVE SOON                           
         BE    SOON                                                             
         CLI   SPOOKWEN,6          TEST LUNATIC SOON                            
         BE    SOON                                                             
         CLI   SPOOKWEN,7          TEST LATE                                    
         BE    PUTREQ02                                                         
*&&US*&& TM    12(R9),X'80'        TEST RFP REQUEST                             
*&&US*&& BNZ   RFP                                                              
         B     PUTREQ04                                                         
                                                                                
PUTREQ02 GOTOR GOSOON              PASS CONTROL TO SPOON                        
                                                                                
         L     RE,8(R9)            RETURNED JESKEY                              
         OC    0(7,RE),0(RE)       TEST VALID KEY RETURNED (JCL ERROR)          
         BZ    XIT                                                              
*NOP*    MVC   PHEADER+RQHOSID-RQHHDR(L'RQHOSID),2(RE) SET REP ID               
*NOP*    MVC   PHEADER+RQHOSNO-RQHHDR(L'RQHOSNO),6(RE) SET REP NO               
                                                                                
PUTREQ04 GOTOR VDATAMGR,PARAS,(X'20',DMADD),REQUEST,PHEADER,PHEADER             
         B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
* CALL SPOON TO SUBMIT JOB                                            *         
***********************************************************************         
                                                                                
GOSOON   NTR1  ,                                                                
                                                                                
SOON     MVC   JCLBOOK,SPACES      SET UP JCL BOOK FOR SOON                     
         MVC   JCLBOOK(2),SPOOKSYS                                              
         MVI   JCLBOOK+2,C'0'                                                   
         NC    SPOOKJCL,SPOOKJCL                                                
         BNZ   *+10                                                             
         MVC   SPOOKJCL,QHDR+26                                                 
*                                                                               
SOON1    OC    SPOOKDES,SPOOKDES   SET DESTINATION FROM REQ HDR                 
         BNZ   *+10                                                             
         MVC   SPOOKDES,QHDR+11                                                 
*                                                                               
SOON2    OC    QHDR+21(4),QHDR+21  TEST IF PID OR PIN VALUE SET                 
         BZ    SOON3                                                            
         CLI   QHDR+20,2           TEST IF PIN SET                              
         BNE   *+12                                                             
         MVI   SPOOKSF1,X'10'      SET PIN PROTECTED REPORT                     
         B     SOON2A                                                           
         CLI   QHDR+20,3           TEST IF PID SET                              
         BNE   SOON3                                                            
         MVI   SPOOKSF1,X'20'      SET PID PROTECTED REPORT                     
SOON2A   MVC   SPOOKXT(3),=C'XT='                                               
         MVI   SPOOKSF2,0                                                       
         MVC   SPOOKPSW,QHDR+21                                                 
                                                                                
SOON3    MVC   JCLBOOK+3(2),SPOOKJCL                                            
*                                                                               
         L     RF,ACOMFACS         GET A(SPOON) FROM CALLOV                     
         L     RF,CCALLOV-COMFACSD(RF)                                          
         LHI   R0,QSPOON                                                        
         ICM   R0,B'1110',T00A                                                  
         GOTOR (RF),PARAS,0,(R0),0                                              
         MVC   PARAS+16(8),16(R9)  MOVE CALLING P5/P6                           
         LA    RE,SPOOK                                                         
         STCM  RE,7,PARAS+17       SET A(LOCAL SPOOK)                           
*                                                                               
         L     RF,0(R1)            GET A(SPOON)                                 
         GOTOR (RF),PARAS,(X'FF',(R2)),(REQCON,QHDR+26),               *        
               (8(R9),ACOMFACS),JCLBOOK                                         
         MVC   8(4,R9),PARAS+20    RETURN PRTQUE KEY TO USER                    
         B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
* WRITES REQUEST TO GENDIR/GENFIL VIA RFPIO                           *         
***********************************************************************         
* The RFPVREQH may not be used because it looks like you would have   *         
* to turn on bit RFPFLAGS,RFPUSERH to say use it instead of building  *         
* it.                                                                           
***********************************************************************         
*&&US                                                                           
RFP      L     R4,8(R9)            A(RFPBLK)                                    
         USING RFPBLK,R4                                                        
         MVC   RFPFRQID,QINFO      REQUEST #                                    
         SR    R1,R1                                                            
         IC    R1,QCTR             #REQCRDS                                     
         AHI   R1,1                ADD 1 FOR HEADER                             
         STC   R1,RFPFNUMR                                                      
         MVC   RFPVRQSC,RQCOL                                                   
         MVC   RFPVRQSN,RQCARD                                                  
         MVI   RFPMODE,RFPADDRQ    ADD REQUEST                                  
         MVC   RFPVREQH+54(L'QHDR),QHDR                                         
         CLI   QHDR+2,0            REQCTRL VALUE SET?                           
         BE    *+10                NO                                           
         MVC   RFPCTRL,QHDR+2      YES SO MOVE IN REQCTRL VALUE                 
         LA    R0,QINFO                                                         
         BCTR  R1,0                DON'T COUNT HEADER CARD                      
         MHI   R1,L'RFPVREQC                                                    
         LR    RF,R1                                                            
         LA    RE,RFPVREQC                                                      
         MVCL  RE,R0                                                            
         TM    12(R9),X'FF'-X'80'  TEST OFFLINE RFP REQUEST?                    
         BO    XIT                 YES - DON'T UPDATE GROUP HERE                
         L     RF,ACOMFACS                                                      
         L     RF,CCALLOV-COMFACSD(RF)                                          
         LHI   R0,QRFPIO                                                        
         ICM   R0,B'1110',T00A                                                  
         GOTOR (RF),PARAS,0,(R0),0 GET ADDRESS OF RFPIO                         
         L     RF,0(R1)                                                         
         GOTOR (RF),PARAS,RFPBLK                                                
         B     XIT                 CALLER MUST CHECK FOR RFPIO ERRORS           
         DROP  R4                                                               
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* SETS NUM/ALPHA/HEX BITS AND INPUT FIELD LENGTH                      *         
***********************************************************************         
                                                                                
SETHDR   SR    R0,R0                                                            
         IC    R0,5(R2)                                                         
         LA    R1,8(R2)            TEST FIELD CONTAINS ESCAPE SEQUENCE          
SETHDR02 CLI   0(R1),X'20'                                                      
         BL    *+10                                                             
         CLI   0(R1),X'30'         LEAVE FIELD LENGTH ALONE IF TRUE             
         BNHR  RE                                                               
         AHI   R1,1                                                             
         BCT   R0,SETHDR02                                                      
                                                                                
         MVC   7(1,R2),5(R2)       SAVE ACTUAL INPUT LENGTH                     
         XC    4(2,R2),4(R2)       CLEAR INDICATORS & LENGTH                    
         SR    R0,R0               CALCULATE LENGTH OF INPUT FIELD              
         IC    R0,0(R2)                                                         
         SHI   R0,8                                                             
         TM    1(R2),X'02'         TEST EXTENDED HEADER SET                     
         BZ    *+8                                                              
         SHI   R0,8                                                             
                                                                                
         LA    R1,7(R2)                                                         
         AR    R1,R0               R1=A(END OF INPUT FIELD)                     
SETHDR04 CLI   0(R1),C' '          LOOK FOR LAST VALID CHARACTER                
         BH    SETHDR06                                                         
         BCTR  R1,0                                                             
         BCT   R0,SETHDR04                                                      
         BR    RE                                                               
                                                                                
SETHDR06 STC   R0,5(R2)            SET ADJUSTED FIELD LENGTH                    
         OI    4(R2),B'11001110'   SET FIELD VALID NUM/ALPHA/HEX                
         TM    5(R2),X'01'         TEST EVEN LENGTH                             
         BZ    SETHDR08                                                         
         NI    4(R2),B'11001100'   NO - SET NOT VALID HEX                       
                                                                                
SETHDR08 TM    1(R2),X'40'         TEST LOWER CASE                              
         BZ    SETHDR10                                                         
         CLI   0(R1),X'81'         TEST LITTLE A                                
         BL    SETHDR12                                                         
         CLI   0(R1),X'A9'         TEST LITTLE Z                                
         BH    SETHDR10                                                         
         NI    4(R2),B'11110101'   FLD NOT NUM/HEX                              
         B     SETHDR18                                                         
                                                                                
SETHDR10 CLI   0(R1),C'A'                                                       
         BNL   SETHDR14                                                         
SETHDR12 NI    4(R2),B'11110001'   FLD NOT NUM/ALPHA/HEX                        
         B     SETHDR20                                                         
SETHDR14 CLI   0(R1),C'Z'                                                       
         BNH   SETHDR16                                                         
         NI    4(R2),B'11111011'   FLD NOT ALPHA                                
         B     SETHDR18                                                         
SETHDR16 NI    4(R2),B'11110111'   FLD NOT NUM                                  
         CLI   0(R1),C'F'                                                       
         BNH   SETHDR18                                                         
         NI    4(R2),B'11111101'   FLD NOT HEX                                  
                                                                                
SETHDR18 BCTR  R1,0                                                             
         BCT   R0,SETHDR08                                                      
                                                                                
SETHDR20 TM    1(R2),X'10'         REQUIRED NUM FIELD                           
         BZR   RE                                                               
         TM    4(R2),X'08'         IS IT NUM                                    
         BOR   RE                                                               
         OI    4(R2),X'10'         NO SET FLD INV                               
         BR    RE                                                               
                                                                                
NXTFLD   SR    R1,R1                                                            
         IC    R1,0(R2)                                                         
         AR    R2,R1                                                            
         BR    RE                                                               
                                                                                
XIT      XIT1  ,                                                                
         EJECT                                                                  
         LTORG                                                                  
                                                                                
CCOLUMNS EQU   L'QDATA                                                          
HCOLUMNS EQU   L'QINFO                                                          
DCOLUMNS EQU   CCOLUMNS-HCOLUMNS                                                
                                                                                
EFFS     DC    X'FFFFFFFF'                                                      
                                                                                
DMADD    DC    C'DMADD   '                                                      
REQUEST  DC    C'REQUEST '                                                      
BC01     DC    C'BC01'                                                          
BL01     DC    C'BL01'                                                          
T00A     DC    X'D9000A'                                                        
                                                                                
LANGDOR  DS    0XL32                                                            
         DC    AL1(18),CL31'Details of request             '                    
         DC    AL1(18),CL31'Details of request             '                    
         DC    AL1(18),CL31'Details of request             '                    
         DC    AL1(20),CL31'Anforderungs-Details           '                    
         DC    AL1(18),CL31'Details of request             '                    
         DC    AL1(18),CL31'Details of request             '                    
         DC    AL1(18),CL31'Details of request             '                    
         DC    AL1(18),CL31'Details of request             '                    
         EJECT                                                                  
WORKD    DSECT                                                                  
DUB      DS    D                                                                
WORK     DS    CL64                                                             
PARAS    DS    6F                                                               
                                                                                
VDATAMGR DS    0V                                                               
VPRINT   DS    V                                                                
ACOMFACS DS    A                                                                
ATWA     DS    A                                                                
                                                                                
JCLBOOK  DS    CL10                                                             
REQCON   DS    C                                                                
SPOKAREA DS    XL(SPOOKXL)         SPOOK AREA                                   
FLDOCCUR DS    XL256               FIELD OCCURRENCES                            
                                                                                
THISFNUM DS    X                   LAST FIELD NUMBER                            
                                                                                
INFLAG   DS    X                                                                
*&&US                                                                           
SCREEN   DS    CL6                 TSPPOO OF SCREEN                             
RQCARD   DS    X                   REQUESTOR FLD CARD  (FOR RFPIO)              
RQCOL    DS    X                   REQUESTOR FLD COL   (FOR RFPIO)              
*&&                                                                             
PIDLINE  DS    0CL78                                                            
PIDPIDH  DS    CL4                 PID=                                         
PIDPID   DS    CL8                                                              
         DS    CL1                                                              
PIDFNAME DS    CL9                                                              
         DS    CL1                                                              
PIDLNAME DS    CL18                                                             
         DS    CL11                                                             
PIDWHYH  DS    CL4                 WHY=                                         
PIDWHY   DS    CL22                                                             
                                                                                
P        DS    CL132                                                            
SPACES   DS    CL132                                                            
REQ      DS    CL106                                                            
PFILL    DS    C                                                                
                                                                                
PHEADER  DS    XL54                REQUEST HEADER BUILD AREA                    
PSTACK   DS    24CL80                                                           
         ORG   PSTACK                                                           
QHDR     DS    XL26                                                             
QINFO    DS    CL12                                                             
QDATA    DS    15CL80                                                           
QCTR     DS    X                                                                
         ORG                                                                    
WORKL    EQU   *-WORKD                                                          
                                                                                
* INCLUDE DDCOREQUS                                                             
         PRINT OFF                                                              
       ++INCLUDE DDCOREQUS                                                      
                                                                                
         PRINT ON                                                               
* INCLUDE DMREQHDRA                                                             
         PRINT OFF                                                              
       ++INCLUDE DMREQHDRA                                                      
                                                                                
         PRINT ON                                                               
* INCLUDE DDBIGBOX                                                              
         PRINT OFF                                                              
       ++INCLUDE DDBIGBOX                                                       
                                                                                
         PRINT ON                                                               
* INCLUDE DDCOMFACS                                                             
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
                                                                                
TWAD     DSECT                                                                  
         DS    XL64                                                             
CONHEADH EQU   *                                                                
       ++INCLUDE DDGENTWA                                                       
*&&US                                                                           
         ORG   TWAFLDNM+L'TWAFLDNM                                              
TWAFLDNX DS    X                   FIELD INDEX NUMBER                           
         ORG                                                                    
*&&                                                                             
       ++INCLUDE DDMASTD                                                        
       ++INCLUDE FAUTL                                                          
       ++INCLUDE DDSPOOK                                                        
*&&US                                                                           
       ++INCLUDE GERFPIOD                                                       
*&&                                                                             
         PRINT ON                                                               
                                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'005DDREQTWA  06/09/17'                                      
         END                                                                    
