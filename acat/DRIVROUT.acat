*          DATA SET DRIVROUT   AT LEVEL 022 AS OF 08/30/96                      
*CATALP DRIVROUT                                                                
         TITLE 'DRIVROUT - DRIVER ROUTINE HANDLER'                              
DRIVROUT CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 100,**ROUT**                                                     
         USING DROUTD,RC                                                        
         L     RA,0(R1)                                                         
         USING GLOBALD,RA                                                       
         CLI   GLHOOK,GLRESOLV                                                  
         BE    RESOLVE                                                          
         CLI   GLHOOK,GLROUT                                                    
         BE    ROUTINE                                                          
         B     XIT                                                              
         EJECT                                                                  
*              RESOLVE ADDRESSES                                                
         SPACE 3                                                                
RESOLVE  LA    R1,ROUTLIST                                                      
         LA    R2,GLLABEL                                                       
         SPACE 1                                                                
RESOLVE2 CLC   0(8,R1),0(R2)                                                    
         BE    RESOLVE4                                                         
         LA    R1,12(R1)                                                        
         CLI   0(R1),X'FF'                                                      
         BE    XIT                                                              
         B     RESOLVE2                                                         
         SPACE 1                                                                
RESOLVE4 MVC   GLAROUT,8(R1)                                                    
         B     XIT                                                              
         SPACE 1                                                                
ROUTLIST DS    0F                                                               
         DC    C'RANK    ',A(RANK)                                              
         DC    C'COLRANK ',A(COLRANK)                                           
         DC    C'RECORD  ',A(RECORD)                                            
         DC    C'RECLABEL',A(RECLABEL)                                          
         DC    C'COUNT   ',A(COUNT)                                             
         DC    C'OUTCOUNT',A(OUTCOUNT)                                          
         DC    C'WEIGHT  ',A(WEIGHT)                                            
         DC    C'DAY     ',A(DAY)                                               
         DC    C'TIME    ',A(TIME)                                              
         DC    C'DEMO    ',A(DEMO)                                              
         DC    X'FF'                                                            
         EJECT                                                                  
*              ROUTINES                                                         
         SPACE 3                                                                
ROUTINE  L     RF,GLAROUT                                                       
         L     R2,GLAIFLD          R2=A(INPUT)                                  
         L     R3,GLAOFLD          R3=A(OUTPUT)                                 
         BR    RF                                                               
         SPACE 1                                                                
RANK     CLI   GLMODE,GLOUTPUT     RANK IN                                      
         BE    RANKOUT                                                          
         MVI   3(R2),1             (NEED SOMETHING SIGNIFICANT)                 
         B     XIT                                                              
         SPACE 1                                                                
RANKOUT  LTR   R3,R3                    RANK OUT                                
         BZ    XIT        (TO SURPRESS PRINTING OF RANK/EXIT HERE)              
         SPACE 1                                                                
         L     R1,GLADTENT                                                      
         USING DROD,R1                                                          
         CLI   DROLEN,6              IF LENGTH IS 6                             
         BNE   RANKOUT4                                                         
         EDIT  (4,RANKEQU),(5,0(R3)) SHOW 5 FOR RANK NUMBER                     
         LA    R3,5(R3)                                                         
         B     RANKOUTX                                                         
         SPACE 1                                                                
RANKOUT4 EDIT  (4,RANKEQU),(3,0(R3))  ELSE SHOW 3 FOR RANK NUMBER               
         LA    R3,3(R3)                                                         
         SPACE 1                                                                
RANKOUTX CLI   EQUSW,C'Y'                                                       
         BNE   XIT                                                              
         MVI   0(R3),C'='                                                       
         B     XIT                                                              
         DROP  R1                                                               
         SPACE 1                                                                
COLRANK  CLI   GLMODE,GLOUTPUT     COLUMN RANKING                               
         BE    COLRANKO                                                         
         MVI   3(R2),1             (NEED SOMETHING SIGNIFICANT)                 
         B     XIT                                                              
         SPACE 1                                                                
COLRANKO L     R1,0(R2)            COLUMN RANKING OUTPUT                        
         SLL   R1,1                                                             
         SRL   R1,1                                                             
         CH    R1,=H'1000'                                                      
         BL    COLRANK2                                                         
         MVC   0(3,R3),=C' HI'                                                  
         B     XIT                                                              
COLRANK2 CH    R1,=H'0'                                                         
         BH    COLRANK3                                                         
         MVC   0(3,R3),=C'***'                                                  
         B     XIT                                                              
COLRANK3 EDIT  (R1),(3,0(R3)),ALIGN=LEFT                                        
         TM    0(R2),X'80'         X'80' MEANS =                                
         BNO   XIT                                                              
         AR    R3,R0                                                            
         MVI   0(R3),C'='                                                       
         B     XIT                                                              
         SPACE 1                                                                
RECORD   MVC   0(1,R2),GLRECNO     RECORD NUMBER                                
         B     XIT                                                              
         SPACE 1                                                                
RECLABEL MVC   0(8,R2),GLRECLAB    RECORD LABEL                                 
         B     XIT                                                              
         SPACE 1                                                                
COUNT    CLI   GLMODE,GLOUTPUT     COUNT IN                                     
         BE    COUNTOUT                                                         
         MVC   0(4,R2),=F'1'                                                    
         B     XIT                                                              
         SPACE 1                                                                
COUNTOUT EDIT  (4,0(R2)),(5,0(R3)) COUNT OUT                                    
         B     XIT                                                              
         SPACE 1                                                                
OUTCOUNT TM    GLINDS,GLTOTLIN     OUT COUNT (DETAILS)                          
         BO    OUTCTOT                                                          
         LA    R2,OUTCACCS                                                      
         LA    R0,13                                                            
         SPACE 1                                                                
OUTCDET2 L     R1,0(R2)            ADD 1 TO ALL LEVELS                          
         LA    R1,1(R1)                                                         
         ST    R1,0(R2)                                                         
         LA    R2,4(R2)                                                         
         BCT   R0,OUTCDET2                                                      
         B     XIT                                                              
         SPACE 1                                                                
OUTCTOT  ZIC   R1,GLLEVEL          OUTCOUNT TOTAL - PICK UP LEVEL               
         SLL   R1,2                                                             
         LA    R1,OUTCACCS(R1)                                                  
         L     R2,0(R1)            FIND COUNT AT THAT LEVEL                     
         XC    0(4,R1),0(R1)       AND CLEAR                                    
         EDIT  (R2),(5,0(R3))                                                   
         B     XIT                                                              
         SPACE 1                                                                
WEIGHT   CLI   GLMODE,GLOUTPUT     WEIGHT IN                                    
         BE    WTOUT                                                            
         MVC   0(4,R2),GLWTFACT                                                 
         B     XIT                                                              
         SPACE 1                                                                
WTOUT    EDIT  (4,0(R2)),(6,0(R3)) WEIGHT OUT                                   
         B     XIT                                                              
         SPACE 1                                                                
DAY      ZIC   R1,0(R2)            DAY                                          
         MH    R1,=H'3'                                                         
         LA    R1,DAYLIST(R1)                                                   
         MVC   0(3,R3),0(R1)                                                    
         B     XIT                                                              
         SPACE 1                                                                
TIME     GOTO1 UNTIME,DMCB,(R2),(R3)    TIME                                    
         B     XIT                                                              
         SPACE 1                                                                
DEMO     LA    R5,DAREA                                                         
         USING DBLOCK,R5                                                        
         XC    DBLOCK,DBLOCK                                                    
         MVC   DBCOMFCS,GLCOMFAC                                                
         MVC   DBFILE,=C'TP'                                                    
         MVI   DBSELMED,C'T'                                                    
         GOTO1 DEMOCON,DMCB,(R2),(7,WORK),DBLOCK                                
         MVC   0(7,R3),WORK                                                     
         SPACE 1                                                                
XIT      XIT1                                                                   
         EJECT                                                                  
*              LTORG ETC                                                        
         SPACE 3                                                                
         LTORG                                                                  
         SPACE 1                                                                
DAYLIST  DC    C'M-FMONTUEWEDTHUFRISATSUNM-S'                                   
RANKCNT  DC    F'0'                                                             
         SPACE 1                                                                
         ENTRY OUTCACCS                                                         
*                                  MAY BE CLEARED BY DRIVOCON                   
OUTCACCS DC    13F'0'                                                           
         SPACE 1                                                                
         EJECT                                                                  
*              DSECT FOR ROUTINE                                                
         SPACE 3                                                                
DROUTD   DSECT                                                                  
         SPACE 1                                                                
DAREA    DS    CL256                                                            
         SPACE 1                                                                
         PRINT OFF                                                              
       ++INCLUDE DRGLOBAL                                                       
       ++INCLUDE DRLOCAL                                                        
       ++INCLUDE DRIVETABLE                                                     
       ++INCLUDE DEDBLOCK                                                       
         PRINT ON                                                               
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'022DRIVROUT  08/30/96'                                      
         END                                                                    
