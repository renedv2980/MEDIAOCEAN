*          DATA SET PP6602AN   AT LEVEL 061 AS OF 05/01/02                      
*CATALP PP6602AN                                                                
         TITLE 'PP6602A - CROSS AGY RPT'                                        
*                                                                               
PP6602A  CSECT                                                                  
         NMOD1 0,PP6602A                                                        
         L     RA,0(R1)                                                         
         USING PPWORKD,RA                                                       
         L     RC,PPFILEC                                                       
         LR    R9,RC                                                            
         AH    R9,=H'4096'                                                      
         USING PPFILED,RC,R9                                                    
         LA    R8,SPACEND                                                       
         USING PP66WRKD,R8                                                      
*                                                                               
         XC    PROF,PROF                                                        
         OC    PTCHPROF,PTCHPROF                                                
         BE    ES2                                                              
         MVC   PROF,PTCHPROF                                                    
         B     ES2                                                              
PTCHPROF DC    XL32'00'                                                         
         EJECT                                                                  
*                                                                               
ES2      DS    0H                                                               
         CLI   LASTMODE,LBUYPUB                                                 
         BNE   *+8                                                              
         BAS   RE,ENDPUB           FINISH PUB                                   
         CLI   MODE,PROCBUY                                                     
         BE    BUY                                                              
         CLI   MODE,FBUYEST                                                     
         BE    NEWEST                                                           
         CLI   MODE,FBUYREG                                                     
         BE    NEWREG                                                           
         CLI   MODE,FBUYDST                                                     
         BE    NEWDST                                                           
         CLI   MODE,FBUYPUB                                                     
         BE    NEWPUB                                                           
         CLI   MODE,LBUYCNT                                                     
         BE    ENDCNT                                                           
         CLI   MODE,LBUYMKT                                                     
         BE    ENDMKT                                                           
         CLI   MODE,LBUYPRO            ASPO                                     
         BE    ENDASPO                                                          
         CLI   MODE,DISKERR                                                     
         BNE   *+12                                                             
         MVI   MODE,0                                                           
         B     EXIT                                                             
         GOTO1 AENDBRKS                                                         
*                                                                               
EXIT     DS    0H                                                               
         MVC   LASTMODE,MODE                                                    
         XIT1                                                                   
         EJECT                                                                  
NEWEST   GOTO1 AESMTHS                                                          
         B     EXIT                                                             
         SPACE 2                                                                
NEWREG   DS    0H                                                               
         NI    RDPACT,X'7F'         SET NO REG ACTIVITY                         
         CLC   QREGION,=C'ALL'                                                  
         BNE   EXIT                                                             
         MVI   WFREGSW,C'R'                                                     
         B     EXIT                                                             
*                                                                               
NEWDST   DS    0H                                                               
         NI    RDPACT,X'BF'         SET NO DST ACTIVITY                         
         CLC   QDIST,=C'ALL'                                                    
         BNE   EXIT                                                             
         MVI   WFDSTSW,C'D'                                                     
         B     EXIT                                                             
         EJECT                                                                  
NEWPUB   DS    0H                                                               
*                                                                               
         LA    R3,PDUMREC                                                       
         USING PPDUM00,R3                                                       
*                                                                               
         NI    RDPACT,X'D0'        CLEAR PUB ACTIVITY                           
         MVC   SVPNAME(40),PUBNAME SAVE NAME,ZONE,CITY,STATE                    
         MVC   SVPCITY(18),PUBCITY                                              
         MVC   SVPKPUB,PUBKPUB     AND PUB NO.                                  
         MVC   P,SPACES                                                         
         MVC   PSECOND,SPACES                                                   
         IC    R2,PAGYPROF+12                                                   
         GOTO1  PUBOUT,DMCB,PUBREC,LTLREC,(3,P+1),PCLTKCLT,            X        
               ((R2),PUBEDIT)                                                   
*                                                                               
         MVC   SAVPUBN3,SPACES                                                  
         CLI   QPUB,C' '           SEE IF DOING ONE PUB                         
         BNE   NEWPUB2                                                          
         CLC   QPUB+1(3),=C'   '                                                
         BNE   NEWPUBY             ASPO REPORT - NO CIRC OR FREQ                
NEWPUB2  LA    R7,SAVPUBN3+2                                                    
*                                                                               
         LA    R2,PUBREC+33                                                     
         MVI   ELCODE,X'30'                                                     
         BAS   RE,NEXTEL                                                        
         BNE   NEWPUB4                                                          
         USING PPDUMD06,R2                                                      
*                                                                               
         CP    PUBCIR1,=P'0'                                                    
         BZ    NEWPUB4                                                          
         CLI   ESCCIRC,C'S'        TEST SUPPRESS CIRC                           
         BE    NEWPUB4                                                          
         MVC   0(5,R7),=C'CIRC='                                                
         EDIT  PUBCIR1,(10,5(R7)),COMMAS=YES,ALIGN=LEFT                         
         AR    R7,R0                                                            
         LA    R7,6(R7)                                                         
         DROP  R2                                                               
*                                                                               
NEWPUB4  DS    0H                                                               
*                                                                               
         CLI   QMEDIA,C'N'                                                      
         BE    NEWPUBY                                                          
         CLI   QMEDIA,C'O'                                                      
         BE    NEWPUBY                                                          
*                                                                               
         LA    R2,PUBREC+33        DISPLAY FREQ                                 
         MVI   ELCODE,X'20'                                                     
         CLC   ELCODE,0(R2)                                                     
         BE    *+8                                                              
         BAS   RE,NEXTEL                                                        
         BNE   NEWPUBY                                                          
         USING PPDUMD05,R2                                                      
         CLI   PUBMFREQ,C' '                                                    
         BNH   NEWPUBY                                                          
         MVC   0(5,R7),=C'PUBL='                                                
         MVC   5(2,R7),PUBMFREQ                                                 
         DROP  R2                                                               
*                                                                               
NEWPUBY  DS    0H                                                               
*                                                                               
NEWPUBZ  DS    0H                                                               
         SPACE 2                                                                
*                                  REG/DST SHARE                                
         MVI   RDSHRLNC,0          NO LINES TO PRINT                            
         MVI   RDSHRFST,1          PRIMARY REG/DST                              
         CLC   QREGION(6),SPACES                                                
         BE    NEWPUBZ2                                                         
         XC    WORK(12),WORK                                                    
         MVC   WORK(3),PCLTKCLT                                                 
         MVC   WORK+3(3),PDIVKDIV                                               
         CLI   QDIV,C' '                                                        
         BNE   *+10                                                             
         MVC   WORK+3(3),PPRDDIV                                                
         CLC   QPUB+1(3),=C'RD='   REG/DST CLT OVERRIDE                         
         BNE   *+16                                                             
         MVC   WORK(3),QPUB+4                                                   
         MVC   WORK+3(3),=C'000'                                                
         CLI   QREGION,C' '                                                     
         BE    *+10                                                             
         MVC   WORK+6(3),PREGKREG                                               
         CLI   QDIST,C' '                                                       
         BE    *+10                                                             
         MVC   WORK+9(3),PDSTKDST                                               
*                                                                               
         GOTO1 APPRDSHR,DMCB,LTLREC,(1,WORK),RDSHROUT                           
*                                                                               
NEWPUBZ2 DS    0H                                                               
         MVI   SPACING,2                                                        
         CLI   ESCCONC,C'N'                                                     
         BNE   NEWPUBZ4                                                         
         CLI   ESCPBAD,C'0'                                                     
         BNE   NEWPUBZ4                                                         
         CLI   RDSHRLNC,0                                                       
         BNE   NEWPUBZ4                                                         
         MVI   SPACING,1                                                        
*                                                                               
NEWPUBZ4 DS    0H                                                               
         B     EXIT                                                             
         EJECT                                                                  
* POINT R7 TO LAST NONBLANK POSITION IN FIELD                                   
* THAT ENDS AT 0(R7)                                                            
*                                                                               
ESFLOAT  OI    0(R7),C' '                                                       
         CLI   0(R7),C' '                                                       
         BNE   ESFLOATX                                                         
         BCT   R7,ESFLOAT                                                       
ESFLOATX BR    RE                                                               
         SPACE 2                                                                
         EJECT                                                                  
BUY      DS    0H                                                               
         TM    PBUYCNTL,X'80'      TEST DELETED                                 
         BZ    BUY1P               NO                                           
         XC    GROSS(12),GROSS                                                  
*                                                                               
BUY1P    DS    0H                                                               
*                                                                               
         MVI   BUDSW,0             1ST TIME SW FOR INS COUNT IN BUYACC          
*                                                                               
         MVI   RDPACT,X'E0'        SET REG/DST/PUB ACTIVE                       
         MVC   SAVBKEY,PBUYKEY                                                  
         SR    R0,R0                                                            
         CLI   PAGYPROF+1,C'L'     TEST PRINT LINE NO.                          
         BE    BUY1P2                                                           
         LA    R0,1                NO                                           
BUY1P2   DS    0H                                                               
         GOTO1 APPUNBUY,DMCB,PBUYREC,GROSS,((R0),PPUNBPRT),DTCNV    X           
*                                                                               
         MVC   SVGROSS(12),GROSS     SAVE TOTAL G/AC/CD                         
BUY2     DS    0H                                                               
         CLI   QOPT6,C'A'           SEE IF DOING AN AUDIT                       
         BNE   BUY2B2                                                           
*                                                                               
*                                                                               
BUY2B    DS    0H                                                               
         GOTO1 ABUYINV              GO BUILD INVOICE LINES                      
BUY2B2   CLI   ESCCONC,C'N'                                                     
         BE    BUY2X                                                            
*                                                                               
         GOTO1 ABUYCON                                                          
         EJECT                                                                  
*                                                                               
BUY2X    DS    0H                                                               
         SR    RE,RE                                                            
         IC    RE,LINE             GET LINE COUNTER                             
         CLI   P+1,C' '            TEST PUB NAME PRINTED YET                    
         BE    BUY2Y               YES                                          
* NO - NEED 4 LINES FOR PUB + CONTRACT LINES REQUIRED                           
         LA    RE,4(RE)                                                         
         CLI   ESCPBAD,C'0'        TEST PUB ADDR TO PRINT                       
         BE    *+8                 NO                                           
         LA    RE,3(RE)            3 LINES FOR PUB ADDR                         
         SR    R0,R0                                                            
         CLI   RDSHRLNC,0                                                       
         BE    *+14                                                             
         IC    R0,RDSHRLNC                                                      
         AR    RE,R0               LINES FOR RD SHARE                           
         LA    RE,1(RE)                                                         
         IC    R0,ACURKLN          SET BY BUYCON                                
         AR    RE,R0                                                            
         LTR   R0,R0               IF NO CON LINES, USE BUY DATA                
         BNZ   BUY2Z                                                            
* PUB NAME PRINTED. BUY MUST FIT THIS PAGE.                                     
BUY2Y    AH    RE,PPUNBPRT                                                      
         CLI   QMEDIA,C'N'                                                      
         BNE   *+12                                                             
         CLI   PPUNBPR,C' '                                                     
         BNH   BUY2Z               NO PREM                                      
         LA    RE,1(RE)            1 MORE LINE                                  
*                                                                               
BUY2Z    DS    0H                                                               
         CLI   ESCJOB,C'0'                                                      
         BE    BUY2Z1                                                           
         LA    RE,1(RE)            1 FOR COPY NUMBER                            
         CLI   ESCJOB,C'2'                                                      
         BNE   *+8                                                              
         LA    RE,3(RE)            3 MORE FOR CAPTIONS + SPACE                  
         LR    RF,RE                                                            
         MVI   ELCODE,X'66'                                                     
         LA    R2,PBUYREC+33                                                    
BUY2ZA   DS    0H                                                               
         BAS   RE,NEXTEL                                                        
         BNE   BUY2ZB                                                           
         LA    RF,1(RF)                                                         
         B     BUY2ZA                                                           
*                                                                               
BUY2ZB   DS    0H                                                               
         LR    RE,RF                                                            
*                                                                               
*                                                                               
BUY2Z1   DS    0H                                                               
         STC   RE,BYTE                                                          
         CLC   BYTE,MAXLINES                                                    
         BNH   *+8                                                              
         MVI   LINE,99                                                          
*                                                                               
         CLI   P+1,C' '            TEST PUB NAME PRINTED YET                    
         BE    BUY3F               YES                                          
*                                                                               
         MVC   SAVPUBN1,P          SAVE PUB LINES                               
         MVC   SAVPUBN2,PSECOND                                                 
         LA    R7,SAVPUBN2+70                                                   
         BAS   RE,ESFLOAT                                                       
         MVC   3(11,R7),=C'(CONTINUED)'                                         
         IC    R6,SPACING                                                       
         CLC   SAVPUBN3,SPACES                                                  
         BE    BUY2Z2                                                           
*                                  SEE IF SAVPUBN3 CAN PRINT                    
*                                  ON PSECOND                                   
         LA    R7,SAVPUBN3+30                                                   
         BAS   RE,ESFLOAT                                                       
         LA    R0,SAVPUBN3-4                                                    
         SR    R7,R0               R7=LEN+3                                     
         EX    R7,*+8                                                           
         B     *+10                                                             
         CLC   PSECOND(0),SPACES                                                
         BNE   BUY2Z1B             WILL NOT FIT                                 
         SH    R7,=H'4'                                                         
         EX    R7,*+8                                                           
         B     BUY2Z2                                                           
         MVC   PSECOND(0),SAVPUBN3                                              
BUY2Z1B  DS    0H                                                               
         MVI   SPACING,1                                                        
         BAS   RE,ESPRT2                                                        
         STC   R6,SPACING                                                       
         MVC   P,SAVPUBN3                                                       
BUY2Z2   DS    0H                                                               
         BAS   RE,ESPRT2                                                        
*                                                                               
         CLI   ESCPBAD,C'0'        TEST PUB ADDR OPTION                         
         BE    BUY3B                NO                                          
         CLI   CONSW,0             TEST CONT TO PRINT                           
         BNE   BUY3                YES                                          
         L     R6,AKLINES          NO - PUT ADDR THERE                          
         MVI   0(R6),C' '                                                       
         MVC   30(30,R6),PUBLINE1                                               
         MVC   60(30,R6),PUBLINE2                                               
         B     BUY3B                                                            
*                                                                               
BUY3     DS    0H                                                               
         MVC   P+1(30),PUBLINE1                                                 
         MVC   PSECOND+1(30),PUBLINE2                                           
         CLI   RDSHRLNC,0                                                       
         BNE   BUY3B                                                            
         MVI   SPACING,2                                                        
         BAS   RE,ESPRT                                                         
*                                                                               
BUY3B    DS    0H                                                               
         CLI   RDSHRLNC,0          NO REG/DIST LINES                            
         BE    BUY4                                                             
         LA    R4,P+41             LINE UP OVER DATE                            
         CLI   QMEDIA,C'N'                                                      
         BE    *+8                                                              
         LA    R4,P+39                                                          
*                                  FIRST 2 LINES                                
         MVC   0(45,R4),RDSHRLNS                                                
         MVC   132(45,R4),RDSHRLNS+45                                           
         BAS   RE,ESPRT                                                         
         SR    R6,R6                                                            
         IC    R6,RDSHRLNC                                                      
         SH    R6,=H'2'                                                         
         BNP   BUY3E                                                            
         LA    R5,RDSHRLNS+45                                                   
         LA    R5,45(R5)                                                        
BUY3D    DS    0H                                                               
         MVC   0(45,R4),0(R5)                                                   
         BAS   RE,ESPRT                                                         
         LA    R5,45(R5)                                                        
         BCT   R6,BUY3D                                                         
BUY3E    DS    0H                                                               
         BAS   RE,ESPRT                                                         
         B     BUY4                                                             
*                                                                               
BUY3F    DS    0H                                                               
         CLI   CONSW,0                                                          
         BE    BUY4                                                             
         CLC   ACURKLN+1(3),AKLINES+1                                           
         BNE   BUY4                                                             
         MVI   P,0                                                              
         BAS   RE,ESPRT2           SKIP BETWEEN CONTRACTS                       
*                                                                               
*                                                                               
         SPACE 2                                                                
*                                                                               
BUY4     DS    0H                  SET PRD-EST OR FLAGS                         
         CLI   PRDESTSW,C'T'       T/A                                          
         BNE   BUY4C                                                            
         MVI   P+36,C'D'           DELETED                                      
         TM    PBUYCNTL,X'80'                                                   
         BNZ   BUY6                                                             
         MVI   P+36,C'B'           NEW BUY                                      
         CLC   PBDBUYDT,BTODAY                                                  
         BE    BUY6                                                             
         MVI   P+36,C'C'           CHANGE                                       
         CLC   PBDDATE,BTODAY                                                   
         BE    BUY6                                                             
         MVI   P+36,C' '                                                        
         B     BUY6                                                             
*                                                                               
BUY4C    DS    0H                                                               
         CLI   QOPT6,C'A'        SEE IF DOING ASPO AUDIT                        
         BE    BUY4F                                                            
         TM    PBUYCNTL,X'80'                                                   
         BZ    BUY4C2                                                           
         MVC   P+78(5),=C'*DEL*'   DELETED                                      
         B     BUY4F                                                            
BUY4C2   DS    0H                                                               
         CLI   QPUBCLAS+1,C' '           SEE IF FLAGING CHANGES                 
         BE    BUY4F                                                            
         CLC   PBDCHGDT,QPUBCLAS+1                                              
         BNH   *+10                                                             
         MVC   P+78(5),=C'*CHA*'        CHANGED                                 
         CLC   PBDBUYDT,QPUBCLAS+1                                              
         BNH   *+10                                                             
         MVC   P+78(5),=C'*NEW*'        NEW                                     
         B     BUY4F                                                            
*                                                                               
BUY4F    DS    0H                                                               
         CLI   PRDESTSW,C'E'            EST                                     
         BNE   BUY4J                                                            
         MVC   HALF,PBUYKEST                                                    
         LH    R0,HALF                                                          
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  P+33(3),DUB                                                      
         B     BUY4P                                                            
*                                                                               
BUY4J    DS    0H                                                               
         CLI   PRDESTSW,C'P'            PRD                                     
         BNE   BUY4M                                                            
         MVC   P+33(3),PBUYKPRD                                                 
         B     BUY6                                                             
*                                                                               
BUY4M    DS    0H                                                               
         CLI   PRDESTSW,C'B'       BOTH, PRD-EST                                
         BNE   BUY4P                                                            
         MVC   P+32(2),PBUYKAGY                                                 
         MVI   P+34,C'-'                                                        
         MVC   P+35(3),PBUYKPRD    AGY-PRD                                      
         B     BUY6                                                             
*                                                                               
BUY4P    DS    0H                                                               
         CLC   PBUYKPRD,=C'ZZZ'                                                 
         BNE   BUY6                                                             
         CLC   PPRDKPRD,=C'ZZZ'                                                 
         BE    BUY6                                                             
         MVI   P+37,C'Z'                ZZZ INDICATOR                           
*                                                                               
         EJECT                                                                  
*                                                                               
BUY6     DS    0H                                                               
         CLI   QMEDIA,C'N'                                                      
         BNE   BUY9                                                             
*                                  NEWSPAPER FORMAT                             
         LA    R1,P+39                                                          
         CLI   PAGYPROF+1,C'L'     TEST PRINT LINE NO.                          
         BE    BUY6A                                                            
         LA    R1,2(R1)            NO                                           
BUY6A    DS    0H                                                               
         MVC   0(8,R1),PPUNBDT                                                  
         MVC   P+48(5),PPUNBLNS                                                 
         MVC   HOLDSPAC(5),PPUNBLNS                                             
         CLC   PPUNBRT(2),=C'IN'     PPUNBUY WILL PUT 'IN' IN PPUNBRT           
         BNE   BUY6D                 WHEN THERE IS NO RATE                      
         MVC   P+53(2),=C'IN'                                                   
         B     BUY9F                                                            
*                                                                               
BUY6D    MVC   P+56(8),PPUNBRT                                                  
         B     BUY9F                                                            
*                                  MAG FORMAT                                   
BUY9     DS    0H                                                               
         CLI   PBDSPACE,C'*'                                                    
         BE    BUY9A                                                            
         LA    R1,P+39                                                          
         CLI   PAGYPROF+1,C'L'     TEST PRINT LINE NO.                          
         BE    BUY9A0                                                           
         LA    R1,2(R1)            NO                                           
BUY9A0   DS    0H                                                               
         MVC   0(8,R1),PPUNBDT                                                  
BUY9A    DS    0H                                                               
         GOTO1 AOUTER,DMCB,PBUYREC,(1,P+48)                                     
         MVC   HOLDSPAC,P+48                                                    
*                                                                               
BUY9F    CLI   QOPT6,C'A'        SEE IF DOING AUDIT                             
         BE    BUY40                                                            
*                                                                               
         LA    R4,P+72                                                          
         LA    R5,ESCDETS                                                       
         LA    R6,3                                                             
BUY2A    CLI   0(R5),0                                                          
         BE    BUY2A1                                                           
         SR    R7,R7                                                            
         IC    R7,0(R5)                                                         
         SLL   R7,2                                                             
         BAS   RE,BUYBTAB-4(R7)                                                 
BUY2A1   LA    R5,1(R5)                                                         
         LA    R4,12(R4)                                                        
         BCT   R6,BUY2A                                                         
         B     BUY40                                                            
*                                                                               
*                                                                               
BUYBTAB  B     BUYG                                                             
         B     BUYN                                                             
         B     BUYC                                                             
         B     BUYGLC                                                           
         B     BUYNLC                                                           
         B     BILNLCD             BILLED NET LESS CD                           
         DC    4X'00'                                                           
*                                                                               
BUYG     L     R0,GROSS                                                         
         BAS   R7,EDITAM                                                        
         BR    RE                                                               
*                                                                               
BUYN     L     R0,GROSS                                                         
         S     R0,AGYCOM                                                        
         BAS   R7,EDITAM                                                        
         BR    RE                                                               
*                                                                               
BUYC     L     R0,CSHDSC                                                        
         BAS   R7,EDITAM                                                        
         BR    RE                                                               
*                                                                               
BUYGLC   L     R0,GROSS                                                         
         S     R0,CSHDSC                                                        
         BAS   R7,EDITAM                                                        
         BR    RE                                                               
*                                                                               
BUYNLC   L     R0,GROSS                                                         
         S     R0,AGYCOM                                                        
         S     R0,CSHDSC                                                        
         BAS   R7,EDITAM                                                        
         BR    RE                                                               
*                                                                               
BILNLCD  L     R0,BGROSS                                                        
         S     R0,BAGYCOM                                                       
         S     R0,BCSHDSC                                                       
         BAS   R7,EDITAM                                                        
         BR    RE                                                               
         SPACE 2                                                                
BUY40    MVC   P+66(9),BUYASPO                                                  
         BAS   RE,ESPRT                                                         
         MVI   PLINSW,0                                                         
         CLI   QMEDIA,C'N'         FOR NEWS SHOW PREM/COST                      
         BNE   BUY46               UNDER RATE                                   
         CLI   PPUNBPR,C' '                                                     
         BNH   BUY46                                                            
         MVC   P+56(10),PPUNBPR                                                 
         BAS   RE,ESPRT                                                         
         MVI   PLINSW,1                                                         
BUY46    DS    0H                                                               
*                                                                               
* BFD OR W/O COMMENT                                                            
         LA    R4,P+52                                                          
         CLI   QMEDIA,C'N'                                                      
         BE    *+8                                                              
         LA    R4,P+48                                                          
*                                                                               
         CLC   PPUNBBFD,SPACES                                                  
         BE    BUY47                                                            
         MVC   0(L'PPUNBBFD,R4),PPUNBBFD                                        
         BAS   RE,ESPRT                                                         
         MVI   PLINSW,1                                                         
*                                                                               
BUY47    DS    0H                                                               
* REGULAR COMMENT                                                               
         LA    R2,PBUYREC+33                                                    
         MVI   ELCODE,X'66'                                                     
BUY47B   DS    0H                                                               
         BAS   RE,NEXTEL                                                        
         BNE   BUY48                                                            
         SR    R5,R5                                                            
         IC    R5,1(R2)                                                         
         SH    R5,=H'2'                                                         
*                      COMMENTS TO PRINT IN P AND PSECOND                       
         GOTO1 ACHOPPER,DMCB,((R5),2(R2)),(28,0(R4)),(C'P',2)                   
         BAS   RE,ESPRT                                                         
         MVI   PLINSW,1                                                         
         B     BUY47B                                                           
*                                                                               
*                                                                               
BUY48    DS    0H                                                               
         EJECT                                                                  
* FORMAT POOL ALLOCATIONS                                                       
*                                                                               
BUY50    CLC   PBUYKPRD,=C'ZZZ'                                                 
         BNE   BUY60                                                            
         CLC   PPRDKPRD,=C'ZZZ'    ALWAYS SHOW ALLOCATIONS FOR POL              
         BE    BUY51                                                            
* FIRST CHECK FOR 2 PRDS OR MORE                                                
         MVI   ELCODE,X'21'                                                     
         LA    R2,PBUYREC+33                                                    
         BAS   RE,NEXTEL                                                        
         BAS   RE,NEXTEL           MUST GET HERE SINCE AT LEAST 1 PRD           
         BNE   BUY60               BUT IF NO MORE GET OUT                       
* ELSE FORMAT ALLOCATIONS                                                       
BUY51    DS    0H                                                               
         CLI   QEST,C' '                                                        
         BE    BUY51A                                                           
         CLI   QESTEND,C' '                                                     
         BNE   BUY51A                                                           
         CLC   PESTZZZ,PPUNBZZZ                                                 
         BE    BUY60                                                            
BUY51A   DS    0H                                                               
         MVC   0(50,R4),PPUNBZZZ                                                
         BAS   RE,ESPRT                                                         
         MVI   PLINSW,1                                                         
         SPACE 3                                                                
*                                  COPY NO. AND CAPTION                         
BUY60    DS    0H                                                               
         OC    PBDJOB,PBDJOB                                                    
         BZ    BUY61                                                            
         CLI   ESCJOB,C'0'                                                      
         BE    BUY61                                                            
         CLI   ESCJOB,C'A'         AD NO. ONLY                                  
         BE    BUY60B                                                           
         GOTO1 AFNDJOB                                                          
*                                                                               
*                                                                               
         L     R3,DMCB             A(JOB INFO)                                  
         LTR   R3,R3                                                            
         BZ    BUY61                                                            
*                                                                               
BUY60B   DS    0H                                                               
         TM    ESCJOB,X'30'                                                     
         BNZ   BUY60C                                                           
         MVC   0(8,R4),=C'AD NO. ='                                             
         MVC   9(6,R4),PBDJOB                                                   
         B     BUY60D                                                           
*                                                                               
BUY60C   DS    0H                                                               
         MVC   0(6,R4),=C'COPY ='                                               
         MVC   7(17,R4),9(R3)                                                   
BUY60D   DS    0H                                                               
         BAS   RE,ESPRT                                                         
         MVI   PLINSW,1                                                         
*                                                                               
         TM    ESCJOB,X'02'                                                     
         BZ    BUY61                                                            
*                                                                               
         MVC   000(25,R4),26(R3)        CAP1                                    
         BAS   RE,ESPRT                                                         
         CLC   51(25,R3),SPACES                                                 
         BE    *+14                                                             
         MVC   000(25,R4),51(R3)   CAP 2                                        
*                                                                               
         BAS   RE,ESPRT                                                         
         MVI   PLINSW,1                                                         
*                                                                               
BUY61    DS    0H                                                               
*                            FINISH PRINTING INVOICE LINES                      
         L     RF,ACURILN                                                       
         CLI   0(RF),0                                                          
         BE    BUY61A          NO MORE LINES                                    
         BAS   RE,ESPRT                                                         
         MVI   PLINSW,1           TO SKIP A LINE WHEN DONE                      
         B     BUY61                                                            
*                                                                               
BUY61A   DS    0H                                                               
         CLI   PLINSW,0                                                         
         BE    BUY62                                                            
         BAS   RE,ESPRT            SKIP A LINE IF MULT LINES FOR BUY            
*                                                                               
* POST TO ACCUMULATORS                                                          
*                                                                               
BUY62    DS    0H                                                               
         CLI   QOPT6,C'A'             SEE IF DOING AUDIT                        
         BE    EXIT             YES - POSTING WAS DONE IN BUYINV                
         TM    PBUYCNTL,X'80'      SEE IF DELETED                               
*              YES THEN STILL POST BILLED NET-CD TO ACCUMS                      
*              BUT LEAVE GROSS/AC/CD AS ZERO                                    
         BNZ   BUY63                                                            
         MVC   GROSS(12),SVGROSS     RESTORE TOTAL G/AC/CD                      
BUY63    GOTO1 ABUYACC                                                          
*                                                                               
         B     EXIT                                                             
         EJECT                                                                  
* LAST BUY FOR CONTRACT                                                         
*                                                                               
ENDCNT   DS    0H                                                               
         B     EXIT                                                             
         SPACE 2                                                                
* LAST BUY FOR PUB                                                              
*                                                                               
*                                                                               
*                                  NOTE - AT LBUYPUB KEY IS THE                 
*                                         KEY OF THE NEXT BUY RECORD            
*                                         SO IT CAN BE USED TO                  
*                                         DETERMINE 'VENDOR' BREAKS             
*                                                                               
ENDPUB   NTR1                                                                   
         MVI   CONSW,0                                                          
         TM    RDPACT,X'20'        TEST PUB ACTIVITY                            
         BZ    ENDP2               NO                                           
ENDP1    DS    0H                                                               
         L     RE,ACURKLN          PRINT ANY REMAINING K LINES                  
         CLI   0(RE),0                                                          
         BNE   ENDP1A                                                           
         L     RE,ACURILN          PRINT REMAINING INV LINES                    
         CLI   0(RE),0                                                          
         BE    ENDP1B                                                           
*                                                                               
ENDP1A   DS    0H                                                               
         BAS   RE,ESPRT                                                         
         B     ENDP1                                                            
ENDP1B   DS    0H                                                               
         CLI   QOPT5,C'C'          IF PRINTING ONLY CHANGES                     
         BNE   *+8                                                              
         BAS   RE,ESPRT            SKIP A LINE                                  
ENDP2    DS    0H                                                               
         L     RE,AKLINES          CLEAR K LINES                                
         LA    RF,3600             120 X 30 COLS                                
         XCEF                                                                   
         MVC   ACURKLN,AKLINES                                                  
         L     RE,AILINES                                                       
         LA    RF,1200                                                          
         XCEF                                                                   
         MVC   ACURILN,AILINES                                                  
         XC    CONWRK,CONWRK                                                    
         LH    RF,PBRCNT                                                        
         LA    RF,1(RF)                                                         
         STH   RF,PBRCNT           BUMP PUB COUNT                               
         IC    R5,LINE             SAVE LINE COUNT                              
         MVI   LINE,0                                                           
         TM    ESCPBTOT,X'04'       TEST ED/VEN TOTALS                          
         BZ    ENDP4               ONLY VENDOR                                  
         LH    RF,EDITCNT          BUMP EDITION COUNT                           
         LA    RF,1(RF)                                                         
         STH   RF,EDITCNT                                                       
         CLI   MODE,LBUYVEN                                                     
         BNE   ENDP6                                                            
         CLI   EDITCNT+1,1         YES- TEST MULTIPLE EDITS                     
         BE    ENDP4               NO                                           
         MVI   EDVENSW,C'E'        YES                                          
         GOTO1 AENDPUB                                                          
*                                                                               
         BAS   RE,ENDPPRT            SKIP A LINE                                
ENDP4    DS    0H                                                               
         MVI   EDVENSW,C'V'                                                     
         GOTO1 AENDPUB                                                          
*                                                                               
         XC    EDITCNT,EDITCNT                                                  
         B     ENDP8                                                            
ENDP6    DS    0H                                                               
         MVI   EDVENSW,C'E'                                                     
         GOTO1 AENDPUB                                                          
*                                                                               
ENDP8    DS    0H                                                               
         SR    R0,R0                                                            
         IC    R0,LINE                                                          
         AR    R0,R5                                                            
         STC   R0,LINE             RESTORE LINE COUNT                           
         B     EXIT                                                             
*                                                                               
ENDPPRT  DS    0H                                                               
         CLI   QOPT5,C'C'          IF PRINTING ONLY CHANGES                     
         BER   RE                  NO PUB TOTS                                  
         B     ESPRT                                                            
*                                                                               
         SPACE 2                                                                
ENDMKT   DS    0H                                                               
         CLI   PBRCNT+1,1          TEST MULTIPLE PUBS                           
         MVI   PBRCNT+1,0                                                       
         BNH   ENDMKT2                NO                                        
         MVI   EDVENSW,C'M'                                                     
         MVI   FORCEHED,C'N'       NO AUTO NEW PAGE                             
         BAS   RE,ENDPPRT            SKIP A LINE                                
         GOTO1 AENDPUB                                                          
*                                                                               
ENDMKT2  DS    0H                                                               
         L     R2,AMKTTOT                                                       
         LA    R3,ACCN                                                          
ENDMKT5  ZAP   0(8,R2),=P'0'                                                    
         LA    R2,8(R2)                                                         
         BCT   R3,ENDMKT5                                                       
         XC    0(40,R2),0(R2)      CLEAR SPARE AT ACCUM END                     
         ZAP   32(8,R2),=P'0'                                                   
         B     EXIT                                                             
*                                                                               
ENDASPO  DS    0H                                                               
         IF    QPUB,EQ,C' ',AND,QPUB+1,NE,C' ',ENDASPOA                         
         B     ENDASPOX                                                         
ENDASPOA MVI   EDVENSW,C'A'                                                     
         MVI   FORCEHED,C'N'                                                    
         BAS   RE,ENDPPRT                                                       
         GOTO1 AENDPUB                                                          
ENDASPOX L     R2,APRDTOT                                                       
         LA    R3,ACCN                                                          
ENDASP5  ZAP   0(8,R2),=P'0'                                                    
         LA    R2,8(R2)                                                         
         BCT   R3,ENDASP5                                                       
         XC    0(40,R2),0(R2)      CLEAR SPARE AT ACCUM END                     
         ZAP   32(8,R2),=P'0'                                                   
         B     EXIT                                                             
         EJECT                                                                  
NEXTEL   DS    0H                                                               
         SR    R0,R0                                                            
         IC    R0,1(R2)                                                         
         AR    R2,R0                                                            
         CLI   0(R2),0                                                          
         BE    NEXTELX                                                          
         CLC   0(1,R2),ELCODE                                                   
         BCR   8,RE                                                             
         B     NEXTEL                                                           
NEXTELX  LTR   R2,R2                                                            
         BR    RE                                                               
         SPACE 2                                                                
*                                                                               
         SPACE 2                                                                
* LINK TO REPORT WITH HEADLINES ALWAYS SET                                      
*                                                                               
ESPRT    DS    0H                                                               
         CLC   P(3),SPACES         CAN CON DATA GO ON THIS LINE                 
         BNE   ESPRT1              NO                                           
* SEE IF MORE CONTRACT DATA TO PRINT                                            
         L     RF,ACURKLN                                                       
         CLI   0(RF),0             IS DATA THERE                                
         BE    ESPRT1              NO                                           
         MVC   P+1(30),0(RF)                                                    
         LA    RF,30(RF)                                                        
         ST    RF,ACURKLN                                                       
*                                                                               
ESPRT1   DS    0H                                                               
*        SEE   IF THER ARE ANY INVOICE LINES                                    
*                                                                               
         L     RF,ACURILN                                                       
         CLI   0(RF),0                                                          
         BE    ESPRT2              NO DATA                                      
         MVC   P+77(40),0(RF)                                                   
         LA    RF,40(RF)                                                        
         ST    RF,ACURILN                                                       
*                                                                               
ESPRT2   DS    0H                                                               
*                                                                               
         MVC   HEAD8,MYHEAD8                                                    
         MVC   HEAD9,MYHEAD9                                                    
         MVC   HEAD10,MYHEAD10                                                  
*                                                                               
ESPRT4   LR    R0,RE                                                            
         CLI   COMSW,0                                                          
         BNE   ESPRT6                                                           
         MVI   COMSW,C'E'                                                       
         GOTO1 ACOMPRT                                                          
         SPACE 1                                                                
ESPRT6   DS    0H                                                               
         CLC   P(6),SAVPUBN1                                                    
         BE    *+16                                                             
         MVC   MID1,SAVPUBN1                                                    
         MVC   MID2,SAVPUBN2                                                    
         GOTO1 APRNT                                                            
         LR    RE,R0                                                            
         BR    RE                                                               
         SPACE 3                                                                
CLRTOT   LA    RF,ACCN                                                          
CLRTOT5  ZAP   0(8,R2),=P'0'                                                    
         LA    R2,8(R2)                                                         
         BCT   RF,CLRTOT5                                                       
         XC    0(40,R2),0(R2)         CLEAR SPARE AT ACCUM END                  
         ZAP   32(8,R2),=P'0'                                                   
         BR    RE                                                               
*                                                                               
*                                                                               
EDITAM   DS    0H                                                               
         EDIT  (R0),(12,(R4)),2,FLOAT=-                                         
         BR    R7                                                               
*                                                                               
         SPACE 3                                                                
         LTORG                                                                  
*                                                                               
PDUMREC  DS    CL1000                                                           
*                                                                               
         TITLE 'PP6602A - POST TO ACCUMULATORS'                                 
BUYACC   CSECT                                                                  
         NMOD1 0,BUYACC                                                         
         SPACE 2                                                                
         USING PPWORKD,RA                                                       
         L     RC,PPFILEC                                                       
         USING PPFILED,RC,R9                                                    
         USING PP66WRKD,R8                                                      
         SPACE 2                                                                
* POST TO ACCUMULATORS                                                          
*                                                                               
BUYX     LA    R2,RMTAB                                                         
         LA    R4,PBUYKDAT                                                      
         MVI   IMDSPL,X'FF'                                                     
         MVI   BMDSPL,X'FF'                                                     
BUYX1    CLC   0(2,R4),0(R2)       MATCH MONTH                                  
         BE    BUYX2                                                            
         LA    R2,2(R2)                                                         
         OC    0(2,R2),0(R2)                                                    
         BNZ   BUYX1                                                            
BUYX2    LA    R0,RMTAB                                                         
         SR    R2,R0               GET DSPL X 2                                 
         MH    R2,=Y(ROWL/2)                                                    
         ST    R2,IMDSPL                                                        
*                                                                               
         MVC   WORK2(12),GROSS     SAVE ORIG VALUES                             
*                                                                               
* POST TO INSERTION MONTH ACCUMS                                                
*                                                                               
         LA    R0,9                PUB/VEN/MKT/DST/REG/EST/PRD/DIV/CLT          
         L     R4,APUBTOT                                                       
*                                                                               
BUYX4    DS    0H                                                               
         CH    R0,=H'6'            TEST DST TOTALS                              
         BNE   BUYX4A                                                           
         CLI   ESCCENTS,C'$'       NO CENTS IN DIST + HIGHER ACCUMS             
         BNE   *+8                                                              
         BAS   RE,BCONDOL                                                       
*                                                                               
BUYX4A   LR    R5,R4                                                            
         AR    R5,R2                                                            
         L     RE,GROSS                                                         
         CVD   RE,DUB                                                           
         AP    0(8,R5),DUB                                                      
         L     RE,CSHDSC                                                        
         CVD   RE,DUB                                                           
         AP    8(8,R5),DUB                                                      
         L     RE,AGYCOM                                                        
         CVD   RE,DUB                                                           
         AP    16(8,R5),DUB                                                     
         L     RE,BGROSS                                                        
         S     RE,BAGYCOM                                                       
         S     RE,BCSHDSC                                                       
         CVD   RE,DUB                                                           
         AP    32(8,R5),DUB                                                     
*                                                                               
         CLI   PBDSPACE,C'*'                                                    
         BE    BUYX5                                                            
         TM    PBUYCNTL,X'80'      SEE IF DELETED                               
         BNZ   BUYX5               YES - DON'T COUNT AS INSERTION               
         CLI   BUDSW,0             SO I WON'T COUNT INS AGAIN                   
         BNE   BUYX5                                                            
         CLI   QMEDIA,C'N'                                                      
         BNE   BUYX4C                                                           
         L     R5,UNITS                                                         
         CVD   R5,DUB                                                           
         CLI   PBDUIND,C'I'                                                     
         BE    BUYX4B                                                           
         CLI   PBDUIND,X'89'       LOWER CASE I - 2 DECIMALS                    
         BNE   BUYX4B5                                                          
BUYX4B   MH    R5,=H'14'           14 LINES/INCH                                
         CVD   R5,DUB                                                           
         CLI   PBDUIND,X'89'       LOWER CASE I - 2 DECIMALS                    
         BNE   BUYX4B5                                                          
         CVD   R5,DUB                                                           
         AP    DUB,=P'50'          ROUND TO NEAREST LINE                        
         DP    DUB,=P'100'                                                      
         ZAP   DUB,DUB(6)                                                       
BUYX4B5  LR    R5,R4                                                            
         AR    R5,R2                                                            
         AP    24(8,R5),DUB                                                     
         B     BUYX5                                                            
*                                                                               
BUYX4C   DS    0H                                                               
         CLI   RDSHRFST,1                                                       
         BNE   BUYX5               NOT PRIMARY REG/DST                          
         LR    R5,R4                                                            
         AR    R5,R2                                                            
         AP    24(8,R5),=P'1'                                                   
*                                                                               
BUYX5    DS    0H                                                               
         CH    R0,=H'6'            TEST DISTRICT TOTALS                         
         BNE   BUYX5A             NO                                            
         CLI   QMEDIA,C'N'                                                      
         BE    BUYX5A    FOR NEWS OS = INS MONTH                                
         CLI   ESCOS,C'0'          TEST OS OPT                                  
         BNE   BUYX5A1       YES - USE OS DATE FOR ALL BUT PUBTOTS              
BUYX5A   DS    0H                                                               
         CLI   IMDSPL,X'FF'                                                     
         BE    BUYX5E                                                           
         LA    R4,ACCL(R4)                                                      
         BCT   R0,BUYX4                                                         
         B     BUYX5E                                                           
*                                                                               
BUYX5A1  DS    0H                                                               
         LA    RF,PBDSDATE                                                      
         LA    R2,RMTAB                                                         
BUYX5B   CLC   0(2,RF),0(R2)                                                    
         BE    BUYX5C                                                           
         LA    R2,2(R2)                                                         
         CLI   0(R2),0                                                          
         BNE   BUYX5B                                                           
         L     R2,IMDSPL                                                        
         B     BUYX5A                                                           
BUYX5C   LA    RF,RMTAB                                                         
         SR    R2,RF                                                            
         MH    R2,=Y(ROWL/2)                                                    
         ST    R2,IMDSPL                                                        
         B     BUYX5A                                                           
* ADD TO BILL MONTH ACCUMS IF NOT NEWSPAPERS                                    
*                                                                               
BUYX5E   DS    0H                                                               
*                                                                               
         LA    R2,RMTAB                                                         
BUYX6    CLC   PBDBDATE(2),0(R2)                                                
         BE    BUYX7                                                            
         LA    R2,2(R2)                                                         
         OC    0(2,R2),0(R2)                                                    
         BNZ   BUYX6                                                            
         B     BUYX10                                                           
*                                                                               
BUYX7    LA    R0,RMTAB                                                         
         SR    R2,R0               GET DSPL X 2                                 
         MH    R2,=Y(ROWL/2)                                                    
         ST    R2,BMDSPL                                                        
*                                                                               
         LA    R0,6                DST/REG/EST/PRD/DIV/CLT                      
         L     R4,ADSTTOTB                                                      
*                                                                               
BUYX8    LR    R5,R4                                                            
         AR    R5,R2                                                            
         L     RE,GROSS                                                         
         CVD   RE,DUB                                                           
         AP    0(8,R5),DUB                                                      
         L     RE,CSHDSC                                                        
         CVD   RE,DUB                                                           
         AP    8(8,R5),DUB                                                      
         L     RE,AGYCOM                                                        
         CVD   RE,DUB                                                           
         AP    16(8,R5),DUB                                                     
         L     RE,BGROSS                                                        
         S     RE,BAGYCOM                                                       
         S     RE,BCSHDSC                                                       
         CVD   RE,DUB                                                           
         AP    32(8,R5),DUB                                                     
*                                                                               
         CLI   PBDSPACE,C'*'                                                    
         BE    BUYX9                                                            
         TM    PBUYCNTL,X'80'      SEE IF DELETED                               
         BNZ   BUYX9               YES - DON'T COUNT AS INSERTION               
         CLI   BUDSW,0             SO I WON'T COUNT INS AGAIN                   
         BNE   BUYX9                                                            
         CLI   QMEDIA,C'N'                                                      
         BNE   BUYX8E                                                           
         L     R5,UNITS                                                         
         CVD   R5,DUB                                                           
         CLI   PBDUIND,C'I'                                                     
         BE    BUYX8B                                                           
         CLI   PBDUIND,X'89'       LOWER CASE I - 2 DECIMALS                    
         BNE   BUYX8C                                                           
BUYX8B   MH    R5,=H'14'           14 LINES/INCH                                
         CVD   R5,DUB                                                           
         CLI   PBDUIND,X'89'       LOWER CASE I - 2 DECIMALS                    
         BNE   BUYX8C                                                           
         CVD   R5,DUB                                                           
         AP    DUB,=P'50'          ROUND TO NEAREST LINE                        
         DP    DUB,=P'100'                                                      
         ZAP   DUB,DUB(6)                                                       
BUYX8C   LR    R5,R4                                                            
         AR    R5,R2                                                            
         AP    24(8,R5),DUB                                                     
         B     BUYX9                                                            
*                                                                               
BUYX8E   DS    0H                                                               
         CLI   RDSHRFST,1                                                       
         BNE   BUYX9               NOT PRIMARY REG/DST                          
         LR    R5,R4                                                            
         AR    R5,R2                                                            
         AP    24(8,R5),=P'1'                                                   
*                                                                               
BUYX9    DS    0H                                                               
*                                                                               
         LA    R4,ACCL(R4)                                                      
         BCT   R0,BUYX8                                                         
*                                                                               
BUYX10   B     BZ                                                               
         EJECT                                                                  
* BUCKET INSERTION DATA FOR EACH AGENCY                                         
BZ       DS    0H                                                               
*              FIND AGENCY INS/BILL MTH ACCUMS OR ADD AGENCY TO TABLE           
*                                                                               
BZ3      L     R3,ABRDT                                                         
         LA    R4,ACCL                                                          
         L     R5,ABRDTX                                                        
BZ4      OC    ACCL-13(2,R3),ACCL-13(R3)      TEST ACCUM USED                   
         BZ    BZ6                 NO.                                          
         CLC   ACCL-13(2,R3),PBUYKAGY        MATCH AGY                          
         BE    BZ6                                                              
         BXLE  R3,R4,BZ4                                                        
         DC    H'0'                NEED MORE ACCUMS                             
*                                                                               
BZ6      L     R4,ABRDT                                                         
         SR    R4,R3               GIVES BRAND DISPLACEMNT                      
         LPR   R4,R4                                                            
         A     R4,ABRDTB           GIVES BILL MNTH ACCUM ADDRESS                
*                                                                               
         MVC   ACCL-13(2,R3),PBUYKAGY                                           
         MVC   ACCL-13(2,R4),PBUYKAGY                                           
*                                                                               
         A     R3,IMDSPL           POINT TO MONTH BUCKETS                       
         A     R4,BMDSPL                                                        
*                                                                               
         CLI   IMDSPL,X'FF'                                                     
         BE    BZ7B                                                             
         L     RE,GROSS                                                         
         CVD   RE,DUB                                                           
         AP    0(8,R3),DUB                                                      
         L     RE,CSHDSC                                                        
         CVD   RE,DUB                                                           
         AP    8(8,R3),DUB                                                      
         L     RE,AGYCOM                                                        
         CVD   RE,DUB                                                           
         AP    16(8,R3),DUB                                                     
         L     RE,BGROSS                                                        
         S     RE,BAGYCOM                                                       
         S     RE,BCSHDSC                                                       
         CVD   RE,DUB                                                           
         AP    32(8,R3),DUB                                                     
         CLI   PBDSPACE,C'*'                                                    
         BE    BZ7B                                                             
         TM    PBUYCNTL,X'80'      SEE IF DELETED                               
         BNZ   BZ7B                YES - DON'T COUNT AS INSERTION               
         CLI   BUDSW,0             SO I WON'T COUNT INS AGAIN                   
         BNE   BZ7B                                                             
         CLI   QMEDIA,C'N'                                                      
         BNE   BZ7                                                              
         L     R5,UNITS                                                         
         CVD   R5,DUB                                                           
         CLI   PBDUIND,C'I'                                                     
         BE    BZ6B                                                             
         CLI   PBDUIND,X'89'       LOWER CASE I - 2 DECIMALS                    
         BNE   BZ6C                                                             
BZ6B     MH    R5,=H'14'           14 LINES/INCH                                
         CVD   R5,DUB                                                           
         CLI   PBDUIND,X'89'       LOWER CASE I - 2 DECIMALS                    
         BNE   BZ6C                                                             
         CVD   R5,DUB                                                           
         AP    DUB,=P'50'          ROUND TO NEAREST LINE                        
         DP    DUB,=P'100'                                                      
         ZAP   DUB,DUB(6)                                                       
BZ6C     AP    24(8,R3),DUB                                                     
         B     BZ7B                                                             
*                                                                               
BZ7      DS    0H                                                               
         AP    24(8,R3),=P'1'                                                   
*                                                                               
*                                                                               
BZ7B     DS    0H                                                               
         CLI   BMDSPL,X'FF'                                                     
         BE    BZ8                                                              
*                                                                               
         L     RE,GROSS                                                         
         CVD   RE,DUB                                                           
         AP    0(8,R4),DUB                                                      
         L     RE,CSHDSC                                                        
         CVD   RE,DUB                                                           
         AP    8(8,R4),DUB                                                      
         L     RE,AGYCOM                                                        
         CVD   RE,DUB                                                           
         AP    16(8,R4),DUB                                                     
         L     RE,BGROSS                                                        
         S     RE,BAGYCOM                                                       
         S     RE,BCSHDSC                                                       
         CVD   RE,DUB                                                           
         AP    32(8,R4),DUB                                                     
         XC    BGROSS(12),BGROSS      SO I WON'T ADD IT AGAIN                   
         CLI   PBDSPACE,C'*'                                                    
         BE    BZ8                                                              
         TM    PBUYCNTL,X'80'      SEE IF DELETED                               
         BNZ   BZ8                 YES - DON'T COUNT AS INSERTION               
         CLI   BUDSW,0             SO I WON'T COUNT INS AGAIN                   
         BNE   BZ8                                                              
         CLI   QMEDIA,C'N'                                                      
         BNE   BZ7D                                                             
         L     R5,UNITS                                                         
         CVD   R5,DUB                                                           
         CLI   PBDUIND,C'I'                                                     
         BE    BZ7C                                                             
         CLI   PBDUIND,X'89'       LOWER CASE I - 2 DECIMALS                    
         BNE   BZ7C5                                                            
BZ7C     MH    R5,=H'14'           14 LINES/INCH                                
         CVD   R5,DUB                                                           
         CLI   PBDUIND,X'89'       LOWER CASE I - 2 DECIMALS                    
         BNE   BZ7C5                                                            
         CVD   R5,DUB                                                           
         AP    DUB,=P'50'          ROUND TO NEAREST LINE                        
         DP    DUB,=P'100'                                                      
         ZAP   DUB,DUB(6)                                                       
BZ7C5    AP    24(8,R4),DUB                                                     
         B     BZ8                                                              
*                                                                               
BZ7D     DS    0H                                                               
         AP    24(8,R4),=P'1'                                                   
*                                                                               
BZ8      DS    0H                                                               
         EJECT                                                                  
* BUCKET FOR SPACE DESCRIPTION                                                  
*                                                                               
BS       DS    0H                                                               
         CLI   ESCPBREC,C'0'                                                    
         BE    BSX                 NO RECAP                                     
         CLI   QMEDIA,C'N'                                                      
         BE    BSX                 NOT FOR NEWSPAPERS                           
         MVC   GROSS(12),WORK2     RESTORE ORIG VALUES                          
*                                  SAVED AT  BUYX2                              
         L     R3,ASPCTAB                                                       
         LA    R4,40                                                            
         L     R5,ASPCTABX                                                      
*                                                                               
         OC    HOLDSPAC,SPACES                                                  
BS2      CLC   0(17,R3),HOLDSPAC                                                
         BE    BS4                                                              
         CLI   0(R3),0                                                          
         BE    BS4                                                              
         BXLE  R3,R4,BS2                                                        
         SH    R3,=H'40'                                                        
         MVC   0(17,R3),=CL17'*ALL OTHER*'                                      
         B     *+10                                                             
BS4      DS    0H                                                               
         MVC   0(17,R3),HOLDSPAC                                                
         L     R6,20(R3)                                                        
         A     R6,GROSS                                                         
         ST    R6,20(R3)                                                        
         L     R6,24(R3)                                                        
         A     R6,CSHDSC                                                        
         ST    R6,24(R3)                                                        
         L     R6,28(R3)                                                        
         A     R6,AGYCOM                                                        
         ST    R6,28(R3)                                                        
         CLI   HOLDSPAC,C'*'      *SPACES DONT COUNT AS INSERTIONS              
         BE    BSX                                                              
         TM    PBUYCNTL,X'80'         SEE IF DELETED                            
         BNZ   BSX                 DON'T COUNT AS INSERTION                     
         CLI   BUDSW,0             SO I WON'T COUNT INS AGAIN                   
         BNE   BSX                                                              
         L     R6,32(R3)                                                        
         LA    R6,1(R6)                                                         
         ST    R6,32(R3)                                                        
BSX      DS    0H                                                               
BUYACCX  MVI   BUDSW,1             SO I WON'T COUNT THIS INS AGAIN              
*                                  WILL BE RESET TO O FOR NEXT BUY              
         XMOD1 1                                                                
         SPACE 2                                                                
BNXTEL   DS    0H                                                               
         SR    R0,R0                                                            
         IC    R0,1(R2)                                                         
         AR    R2,R0                                                            
         CLI   0(R2),0                                                          
         BE    BNXTELX                                                          
         CLC   0(1,R2),ELCODE                                                   
         BCR   8,RE                                                             
         B     BNXTEL                                                           
BNXTELX  LTR   R2,R2                                                            
         BR    RE                                                               
         SPACE 2                                                                
BCONDOL  NTR1                      CONVER TO $ AMTS                             
         LA    R6,3                GROSS AGYCOM/CSHDSC                          
         LA    R7,GROSS                                                         
*                                                                               
BCOND5   L     R3,0(R7)                                                         
         SR    R2,R2                                                            
         M     R2,=F'2'                                                         
         D     R2,=F'100'                                                       
         LTR   R3,R3                                                            
         BM    *+8                                                              
         AH    R3,=H'1'                                                         
         SRA   R3,1                                                             
         ST    R3,0(R7)                                                         
         LA    R7,4(R7)                                                         
         BCT   R6,BCOND5                                                        
*                                                                               
         LA    R6,3               BGROSS/BAGYCOM/BCSHDSC                        
         LA    R7,BGROSS                                                        
*                                                                               
BCOND8   L     R3,0(R7)                                                         
         SR    R2,R2                                                            
         M     R2,=F'2'                                                         
         D     R2,=F'100'                                                       
         LTR   R3,R3                                                            
         BM    *+8                                                              
         AH    R3,=H'1'                                                         
         SRA   R3,1                                                             
         ST    R3,0(R7)                                                         
         LA    R7,4(R7)                                                         
         BCT   R6,BCOND8                                                        
*                                                                               
BCONDX   XIT1                                                                   
         LTORG                                                                  
         TITLE 'PP6602A - PUB TOTALS'                                           
ENDPUBC  CSECT                                                                  
         NMOD1 0,ENDPUB                                                         
         SPACE 2                                                                
         USING PPWORKD,RA                                                       
         L     RC,PPFILEC                                                       
         USING PPFILED,RC,R9                                                    
         USING PP66WRKD,R8                                                      
         SPACE 2                                                                
*                                  SUM MONTHLY ACCUMS                           
         L     R2,APUBTOT                                                       
         CLI   EDVENSW,C'V'                                                     
         BNE   *+8                                                              
         L     R2,AVENTOT                                                       
         CLI   EDVENSW,C'M'                                                     
         BNE   *+8                                                              
         L     R2,AMKTTOT                                                       
         CLI   EDVENSW,C'A'            ASPO                                     
         BNE   *+8                                                              
         L     R2,APRDTOT                                                       
         LA    R0,21                                                            
         LA    R1,ROWL/8                                                        
         LA    R4,WORK2                                                         
ENDPUB1  ZAP   0(8,R4),=P'0'                                                    
         LA    R4,8(R4)                                                         
         BCT   R1,ENDPUB1                                                       
ENDPUB1A LA    R1,ROWL/8       PACKED LENGTH 8                                  
         LA    R4,WORK2                                                         
ENDPUB1B AP    0(8,R4),0(8,R2)                                                  
         LA    R4,8(R4)                                                         
         LA    R2,8(R2)                                                         
         BCT   R1,ENDPUB1B                                                      
         BCT   R0,ENDPUB1A                                                      
         CLI   ESCLINS,C'0'        TEST LINE TOTS TO PRINT                      
         BZ    ENDPUB3                                                          
         MVC   DUB,WORK2+24                                                     
         CVB   R7,DUB                                                           
         TM    ESCLINS,X'20'       LINES VS INCHES                              
         BNZ   ENDPUB1C                                                         
         SR    R6,R6                                                            
         AH    R7,=H'7'                                                         
         D     R6,=F'14'           INCH = 14 LINES                              
ENDPUB1C DS    0H                                                               
         LA    R6,P+47                                                          
         EDIT  (R7),(9,(R6)),COMMAS=YES                                         
         OI    8(R6),C'0'                                                       
         CLI   QMEDIA,C'N'                                                      
         BNE   ENDPUB2                                                          
         MVC   9(2,R6),=C'L*'                                                   
         TM    ESCLINS,X'20'       LINES VS INCHES                              
         BNZ   *+10                                                             
         MVC   9(3,R6),=C'IN*'                                                  
         B     ENDPUB2A                                                         
ENDPUB2  DS    0H                                                               
         MVC   10(10,R6),=C'INSERTIONS'                                         
         CLI   QMEDIA,C'O'                                                      
         BNE   *+10                                                             
         MVC   10(10,R6),=C'POSTINGS  '                                         
         C     R7,=F'1'            SEE IF ONE                                   
         BNE   ENDPUB2A                                                         
         MVI   19(R6),C' '         BLANK 'S' FOR ONE INSERTION                  
         CLI   QMEDIA,C'O'                                                      
         BNE   *+8                                                              
         MVI   17(R6),C' '         BLANK 'S' FOR ONE POSTING                    
ENDPUB2A DS    0H                                                               
         CLI   EDVENSW,C'M'                                                     
         BE    ENDPUB3                                                          
         CLI   ESCDETS,0                                                        
         BE    *+8                                                              
         BAS   RE,EPPRT            IF 3 $ - PRINT INS TOTAL NOW                 
*                                                                               
ENDPUB3  DS    0H                                                               
         LA    R4,P+57                                                          
         CLI   ESCDETS,0                                                        
         BNE   *+8                                                              
         LA    R4,12(R4)                                                        
         MVC   0(12,R4),=C'VENDOR TOTAL'                                        
         CLI   EDVENSW,C'V'                                                     
         BE    ENDPUB3A                                                         
         MVC   0(6,R4),=C'MARKET'                                               
         CLI   EDVENSW,C'M'                                                     
         BE    EPMKT                                                            
         MVC   0(6,R4),=C'* ASPO'                                               
         MVI   13(R4),C'*'                                                      
         CLI   EDVENSW,C'A'                                                     
         BE    ENDPUB3A                                                         
         MVI   13(R4),C' '                                                      
         MVC   0(6,R4),SPACES                                                   
         CLI   QMEDIA,C'O'                                                      
         BE    ENDPUB3A                                                         
         BCTR  R4,R0                                                            
         MVC   0(7,R4),=C'EDITION'                                              
         LA    R4,1(R4)                                                         
ENDPUB3A DS    0H                                                               
         CLI   ESCDETS+2,X'3'           TEST SEP CD TOTAL NEEDED                
         BNE   *+10                                                             
         MVC   132(09,R4),=C'LESS C.D.'                                         
         LA    R4,P+72                                                          
         LA    R5,ESCDETS                                                       
         LA    R6,3                                                             
ENDPUB4  CLI   0(R5),0                                                          
         BE    ENDPUB5                                                          
         ZAP   MYDUB,WORK2(8)                                                   
         CLI   0(R5),1                                                          
         BE    ENDPUB4D                                                         
         SP    MYDUB,WORK2+16(8)                                                
         CLI   0(R5),2                                                          
         BE    ENDPUB4D                                                         
         ZAP   MYDUB,WORK2+8(8)                                                 
         CLI   0(R5),3                                                          
         BE    ENDPUB4D                                                         
         ZAP   MYDUB,WORK2(8)                                                   
         CLI   0(R5),4                                                          
         BE    ENDPUB4B                                                         
         SP    MYDUB,WORK2+16(8)   LESS AGYCOM                                  
ENDPUB4B SP    MYDUB,WORK2+8(8)    LESS CD                                      
         CLI   0(R5),6                                                          
         BNE   ENDPUB4D                                                         
         ZAP   MYDUB,WORK2+32(8)   BILLED NET LESS C/D                          
*                                                                               
ENDPUB4D BAS   R7,EPEDT                                                         
         CLI   0(R5),3                                                          
         BE    ENDPUB5                                                          
         CLI   ESCDETS+2,3                                                      
         BNE   ENDPUB5                                                          
         SP    MYDUB,WORK2+8(8)    LESS C.D.                                    
         LA    R4,132(R4)                                                       
         BAS   R7,EPEDT                                                         
         SH    R4,=H'132'                                                       
*                                                                               
ENDPUB5  LA    R4,12(R4)                                                        
         LA    R5,1(R5)                                                         
         BCT   R6,ENDPUB4                                                       
         LA    R4,P+72                                                          
         LA    R0,3                                                             
ENDPUB6  CLI   11(R4),C' '                                                      
         BE    ENDPUB7                                                          
         CLC   12(2,R4),SPACES                                                  
         BNE   ENDPUB7                                                          
         MVI   12(R4),C'*'                                                      
ENDPUB7  LA    R4,12(R4)                                                        
         BCT   R0,ENDPUB6                                                       
         LA    R4,PSECOND+72                                                    
         LA    R0,2                                                             
ENDPUB7A DS    0H                                                               
         CLI   11(R4),C' '                                                      
         BE    ENDPUB8                                                          
         CLC   12(2,R4),SPACES                                                  
         BNE   ENDPUB8                                                          
         MVI   12(R4),C'*'                                                      
ENDPUB8  DS    0H                                                               
         LA    R4,12(R4)                                                        
         BCT   R0,ENDPUB7A                                                      
*                                                                               
         CLC   LINE,MAXLINES       MAKE SURE IT FITS ON PAGE                    
         BNL   *+12                                                             
         BAS   RE,EPPRT                                                         
         B     ENDPUB9                                                          
*                                                                               
         IC    R5,LINE             SUPPRESS PAGING                              
         MVI   LINE,0                                                           
         BAS   RE,EPPRT                                                         
         LA    R5,2(R5)            THEN ADJUST LINE COUNT                       
         STC   R5,LINE                                                          
*                                                                               
ENDPUB9  DS    0H                                                               
         CLI   QOPT1,C'Y'          SEE IF DOING ONE PUB PER PAGE                
         BNE   *+8                                                              
         MVI   FORCEHED,C'Y'                                                    
         TM    ESCPBTOT,X'01'       TEST PUB TOTAL OPBTION                      
         BZ    ENDPUB10            NO                                           
         CLI   QOPT5,C'C'          IF PRINTING ONLY CHANGES                     
         BE    ENDPUB10            NO PUB TOTS                                  
         GOTO1 AENDPTOT                                                         
*                                                                               
ENDPUB10 CLI   ESCPBREC,C'0'       TEST PUB RECAP OPTION                        
         BNE   ENDPUB11                                                         
         CLC   QREGION,=C'ALL'                                                  
         BE    ENDPUB11                                                         
         CLC   QDIST,=C'ALL'                                                    
         BNE   ENDPUBX                                                          
ENDPUB11 DS    0H                                                               
         MVC   WRITTOTS,WORK2                                                   
         GOTO1 AWFMOD                                                           
*                                                                               
ENDPUBX  DS    0H                                                               
         CLI   EDVENSW,C'M'                                                     
         BE    ENDPUBXX                                                         
         CLI   EDVENSW,C'A'                                                     
         BE    ENDPUBXX                                                         
ENDPUBX2 DS    0H                                                               
         L     R2,APUBTOT                                                       
         LA    R3,ACCN                                                          
ENDPUBX4 ZAP   0(8,R2),=P'0'                                                    
         LA    R2,8(R2)                                                         
         BCT   R3,ENDPUBX4                                                      
         XC    0(40,R2),0(R2)       CLEAR SPARE AT ACCUM END                    
         ZAP   32(8,R2),=P'0'                                                   
         CLI   EDVENSW,C'V'                                                     
         BNE   ENDPUBXX                                                         
         L     R2,AVENTOT                                                       
         LA    R3,ACCN                                                          
ENDPUBX6 ZAP   0(8,R2),=P'0'                                                    
         LA    R2,8(R2)                                                         
         BCT   R3,ENDPUBX6                                                      
         XC    0(40,R2),0(R2)       CLEAR SPARE AT ACCUM END                    
         ZAP   32(8,R2),=P'0'                                                   
ENDPUBXX DS    0H                                                               
         XIT1                                                                   
         SPACE 2                                                                
EPMKT    DS    0H                  END OF MARKET                                
         MVC   P+3(2),=C'**'                                                    
         MVC   P+6(20),PUBZNAME                                                 
         CLI   QMEDIA,C'O'                                                      
         BE    EPMKT2                                                           
         MVC   P+6(2),PUBSTATE                                                  
         MVI   P+8,C','                                                         
         MVI   P+9,C' '                                                         
         MVC   P+10(16),PUBCITY                                                 
EPMKT2   DS    0H                                                               
         MVC   PSECOND+3(15),=C'** MARKET TOTALS'                               
         LA    R7,P+28                                                          
EPMKT3   CLI   0(R7),C' '                                                       
         BH    EPMKT4                                                           
         CLI   132(R7),C' '                                                     
         BH    *+8                                                              
         BCT   R7,EPMKT3                                                        
EPMKT4   DS    0H                                                               
         MVC   2(2,R7),=C'**'                                                   
         MVC   132+2(2,R7),=C'**'                                               
         MVI   SPACING,2                                                        
         B     ENDPUB3A                                                         
         SPACE 2                                                                
EPEDT    DS    0H                                                               
         EDIT  (P8,MYDUB),(12,(R4)),2,FLOAT=-                                   
         BR    R7                                                               
         SPACE 3                                                                
EPPRT    DS    0H                                                               
         CLC   P(3),SPACES         CAN CON DATA GO HERE                         
         BNE   EPPRT1              NO                                           
*                                                                               
         L     RF,ACURKLN                                                       
         CLI   0(RF),0                                                          
         BE    EPPRT1                                                           
         MVC   P+1(30),0(RF)                                                    
         LA    RF,30(RF)                                                        
         ST    RF,ACURKLN                                                       
*                                                                               
EPPRT1   DS    0H                                                               
         CLI   QOPT5,C'C'          IF PRINTING ONLY CHANGES                     
         BZR   RE                  NO PUB TOTS                                  
         MVC   HEAD8,MYHEAD8                                                    
         MVC   HEAD9,MYHEAD9                                                    
         MVC   HEAD10,MYHEAD10                                                  
*                                                                               
         LR    R0,RE                                                            
         GOTO1 APRNT                                                            
*                                                                               
         LR    RE,R0                                                            
         BR    RE                                                               
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
BUYINV   CSECT                                                                  
         NMOD1 0,BUYINV                                                         
         USING PPWORKD,RA                                                       
         L     RC,PPFILEC                                                       
         USING PPFILED,RC,R9                                                    
         USING PP66WRKD,R8                                                      
*                                                                               
         L     R6,AILINES                                                       
         LA    R7,30                                                            
BUYI1    XC    0(40,R6),0(R6)                                                   
         LA    R6,40(R6)                                                        
         BCT   R7,BUYI1                                                         
         L     R3,AILINES                                                       
         ST    R3,ACURILN                                                       
         LA    R2,PBUYREC+33                                                    
         MVI   ELCODE,X'26'                                                     
BUYI2    BAS   RE,INXTEL                                                        
         BNE   BUYIX                                                            
         USING PPDUMD02,R2                                                      
         MVC   HALF,PBLDATE+3        INVOICE NUMBER                             
         LH    R0,HALF                                                          
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  3(4,R3),DUB                                                      
         SR    R0,R0                                                            
         IC    R0,PBLDATE+1       MONTH                                         
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  0(2,R3),DUB                                                      
         MVI   2(R3),C'-'                                                       
         GOTO1 DTCNV,DMCB,(1,PBLDATE),(3,8(R3))                                 
         LA    R4,19(R3)                                                        
         MVC   GROSS(12),PBGROSS                                                
*                                                                               
         TM    PBUYCNTL,X'80'         DON'T POST DELETES                        
         BZ    BUY09                                                            
         MVI   17(R3),C'D'          DELETED FLAG                                
         B     BUY10                                                            
*                                                                               
BUY09    DS    0H                                                               
         GOTO1 ABUYACC                                                          
*                                                                               
BUY10    LA    R5,ESCDETS                                                       
         LA    R6,3                                                             
BUY23    CLI   0(R5),0                                                          
         BE    BUY24                                                            
         SR    R7,R7                                                            
         IC    R7,0(R5)                                                         
         SLL   R7,2                X4                                           
         BAS   RE,BUYBRTAB-4(R7)                                                
BUY24    LA    R5,1(R5)                                                         
         BCT   R6,BUY23                                                         
         B     BUYI30                                                           
BUYBRTAB B     BUYGRS                                                           
         B     BUYNET                                                           
         B     BUYCD                                                            
         B     BUYGLCD                                                          
         B     BUYNLCD                                                          
         DC    4X'00'                                                           
*                                                                               
BUYGRS   L     R0,GROSS                                                         
         BAS   R7,EDTAMT                                                        
         BR    RE                                                               
*                                                                               
BUYNET   L     R0,GROSS                                                         
         S     R0,AGYCOM                                                        
         BAS   R7,EDTAMT                                                        
         BR    RE                                                               
*                                                                               
BUYCD    L     R0,CSHDSC                                                        
         BAS   R7,EDTAMT                                                        
         BR    RE                                                               
*                                                                               
BUYGLCD  L     R0,GROSS                                                         
         S     R0,CSHDSC                                                        
         BAS   R7,EDTAMT                                                        
         BR    RE                                                               
*                                                                               
BUYNLCD  L     R0,GROSS                                                         
         S     R0,AGYCOM                                                        
         S     R0,CSHDSC                                                        
         BAS   R7,EDTAMT                                                        
         BR    RE                                                               
*                                                                               
*                                                                               
BUYI30   LA    R3,40(R3)                                                        
         B     BUYI2                                                            
*                                                                               
*                                                                               
INXTEL   DS    0H                                                               
         SR    R0,R0                                                            
         IC    R0,1(R2)                                                         
         AR    R2,R0                                                            
         CLI   0(R2),0                                                          
         BE    INXTELX                                                          
         CLC   0(1,R2),ELCODE                                                   
         BCR   8,RE                                                             
         B     INXTEL                                                           
*                                                                               
INXTELX  LTR   R2,R2                                                            
         BR    RE                                                               
         SPACE 2                                                                
EDTAMT   DS    0H                                                               
         EDIT  (R0),(12,(R4)),2,FLOAT=-                                         
         BR    R7                                                               
*                                                                               
*                                                                               
BUYIX    XMOD1 1                                                                
         LTORG                                                                  
         EJECT                                                                  
PRNT     CSECT                                                                  
         NMOD1 0,PRNT                                                           
         USING PPWORKD,RA                                                       
         L     RC,PPFILEC                                                       
         USING PPFILED,RC,R9                                                    
         USING PP66WRKD,R8                                                      
         MVI   ESTACTIV,C'A'       SET ACTIVITY FOR REQ                         
         CLI   PRNTSW,C'N'                                                      
         BE    PRNT1                                                            
         CLI   PRNTSW,C'S'                                                      
         BE    PRNT2                                                            
         CLI   QOPT3,C'S'      SUMMARIES ONLY                                   
         BNE   PRNT2                                                            
*                                                                               
PRNT1    DS    0H                                                               
         MVC   P,SPACES                                                         
         B     PRNTX                                                            
*                                                                               
PRNT2    MVC   HEAD1+42(25),MEDIANM                                             
         MVC   HEAD2+42(25),MEDIAUL                                             
         MVC   HEAD4+42(25),PERHD                                               
         CLI   QPRODUCT,C' '                                                    
         BNE   *+8                                                              
         MVI   PPRDKEY+1,0                                                      
         GOTO1 REPORT                                                           
*                                                                               
PRNTX    XMOD1 1                                                                
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
       ++INCLUDE PP66WRKN                                                       
*                                                                               
         PRINT OFF                                                              
*                                                                               
       ++INCLUDE PPWORKD                                                        
       ++INCLUDE PPNEWFILE                                                      
       ++INCLUDE PPMODEQU                                                       
*                                                                               
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'061PP6602AN  05/01/02'                                      
         END                                                                    
