*          DATA SET ACSRCHAC   AT LEVEL 027 AS OF 04/06/10                      
*CATALP ACSRCHAC                                                                
ACSRCHAC TITLE '- ACCOUNT SEARCH SUBROUTINES FOR ACCOUNT RECORDS'               
* DPEA 018 USE NEW SBCALLER INDICATOR FOR STAND-ALONE TEST                      
* FPEA 019 REPLACE UTLD BY THAT DESCRIBED IN DCGETFACT                          
* DPEA 020 05SEP05 <LO01-4465> DON'T DIE IF HIGH LEVEL ACCOUNT MISSING          
* TKLU 021 22SEP06 <DU01-5577> (UN)APPROVED ACCOUNT FILTER                      
* NSHE 022 06FEB07 <LO01-6033> USE ALTERNATIVE NAMES                            
* NSHE 023 05NOV08 <LO01-8294> USE OFFICE LIST IN ACCOUNT CODE                  
         PRINT NOGEN                                                            
ACSRCHAC CSECT                                                                  
         NMOD1 WORKL,ACSRCHAC,RA,R9                                             
         USING WORKD,RC            RC=A(LOCAL W/S)                              
         ST    R1,APARMS                                                        
         L     R8,0(R1)                                                         
         USING SEARCHD,R8          R8=A(SEARCH CONTROL BLOCK)                   
         LH    R7,=Y(SVBLK-TWAD)                                                
         A     R7,SBATWA                                                        
         USING SVBLKD,R7           R7=A(SAVE AREA IN TWA)                       
         USING OFFALD,SVOFFAL                                                   
*                                                                               
         MVI   SBRETURN,0          CLEAR ERROR FLAG                             
         CLI   0(R1),SDSSYPPQ                                                   
         BNE   *+6                                                              
         DC    H'0'                                                             
         OC    SBASYS,SBASYS                                                    
         BNZ   *+12                                                             
         MVI   SBRETURN,SBENITQ    SEARCH BLOCK NOT INITIALIZED                 
         B     EXITN                                                            
*                                                                               
         L     RF,SBCOMFAC                                                      
         USING COMFACSD,RF                                                      
         MVC   CUREDIT,CCUREDIT                                                 
         MVC   VDATAMGR,CDATAMGR                                                
         MVC   VGETFACT,CGETFACT                                                
         MVC   VCALLOV,CCALLOV                                                  
         MVC   VDICTATE,CDICTATE                                                
         MVC   VSCANNER,CSCANNER                                                
         MVC   VSWITCH,CSWITCH                                                  
         DROP  RF                                                               
*                                                                               
         L     RF,SBASRCH                                                       
         MVC   SDSRCH,0(RF)        COPY SEARCH TABLE ENTRY INTO W/S             
         USING SDSRCHD,SDSRCH                                                   
*                                                                               
         CLI   0(R1),ROUTSN                                                     
         BNL   EXITY               UNKNOWN ROUTINE                              
         XR    RF,RF                                                            
         IC    RF,0(R1)                                                         
         SLL   RF,2                                                             
         B     ROUTS(RF)                                                        
*                                                                               
ROUTS    DS    0XL4                                                             
         B     EXITY               SYSTEM KEY SET                               
         B     SETKEY              SET KEY                                      
         B     SEC                 SECURITY CHECK                               
         B     FLT                 FILTER                                       
         B     DIS                 DISPLAY                                      
         B     BITSET              PRESET HITS IN BITLIST                       
ROUTSN   EQU   (*-ROUTS)/L'ROUTS                                                
         EJECT                                                                  
***********************************************************************         
* SECURITY ROUTINES                                                   *         
***********************************************************************         
         SPACE 1                                                                
SEC      CLI   SBMODE,1            DIRECTORY LEVEL SECURITY                     
         BE    DIRSEC                                                           
         CLI   SBMODE,2            RECORD LEVEL SECURITY                        
         BE    RECSEC                                                           
         CLI   SBMODE,0            INITIALIZE                                   
         BE    INITSEC                                                          
         B     EXITY               UNKNOWN MODE                                 
         SPACE 1                                                                
***********************************************************************         
* FILTER ROUTINES                                                     *         
***********************************************************************         
         SPACE 1                                                                
FLT      CLI   SBMODE,1            DIRECTORY LEVEL FILTERS                      
         BE    DIRFLT                                                           
         CLI   SBMODE,2            RECORD LEVEL FILTERS                         
         BE    RECFLT                                                           
         CLI   SBMODE,0            INITIALIZE                                   
         BE    INITFLT                                                          
         B     EXITY               UNKNOWN MODE                                 
         SPACE 1                                                                
***********************************************************************         
* DISPLAY ROUTINES                                                    *         
***********************************************************************         
         SPACE 1                                                                
DIS      CLI   SBMODE,1            DISPLAY RECORD                               
         BE    RECDIS                                                           
         CLI   SBMODE,0            DISPLAY HEADERS                              
         BE    INITDIS                                                          
         B     EXITY               UNKNOWN MODE                                 
         SPACE 1                                                                
***********************************************************************         
* EXITS                                                               *         
***********************************************************************         
         SPACE 1                                                                
EXITH    CLI   *,0                 CC=HIGH                                      
         B     EXIT                                                             
*                                                                               
EXITN    DS    0H                                                               
EXITL    CLI   *,FF                CC=LOW                                       
         B     EXIT                                                             
*                                                                               
EXITY    CR    RB,RB               CC=EQUAL                                     
EXIT     XIT1  ,                                                                
         EJECT                                                                  
***********************************************************************         
* SET UP SEARCH RECORD                                                *         
***********************************************************************         
         SPACE 1                                                                
SETKEY   L     R2,4(R1)                                                         
         USING SRCRECD,R2                                                       
         XC    SRCKEY,SRCKEY                                                    
         MVI   SRCKTYP,SRCKTYPQ                                                 
         MVC   SRCKCPY,SBAGYB                                                   
         MVC   SRCKUL,SDSRUL                                                    
         B     EXITY                                                            
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* INITIALIZE SECURITY                                                 *         
***********************************************************************         
         SPACE 1                                                                
INITSEC  LA    RE,SVBLKD           CLEAR SAVE AREA                              
         LA    RF,SVBLKL                                                        
         XR    R1,R1                                                            
         MVCL  RE,R0                                                            
*                                                                               
         L     RF,SBATWA           TEST STAND ALONE CALL                        
         USING TWAD,RF                                                          
         TM    SRVIDH+FHATD,FHATPR                                              
         BO    *+8                                                              
         OI    SVINDS,SVIALONE                                                  
*&&DO                                                                           
         TM    SBCALLER,SBCALSOQ   TEST STAND ALONE CALL                        
         BZ    *+8                                                              
         OI    SVINDS,SVIALONE                                                  
*&&                                                                             
         BAS   RE,SETOFF           SET OFFICE SECURITY                          
*                                                                               
         BAS   RE,READLDG          READ LEDGER RECORD                           
         BNE   EXITN                                                            
         GOTO1 GETEL,DMCB,('RSTELQ',IOREC)                                      
         LA    RF,SVLVLUL                                                       
         USING LVLD,RF             SET DEFAULT LEDGER LEVEL VALUES              
         MVC   LVLSECY,RSTSECY-RSTELD(R1)                                       
         CLC   SVSECY,LVLSECY      TEST USER HAS SECURITY FOR LEDGER            
         BNL   *+12                                                             
         MVI   SBRETURN,SBRSECQ                                                 
         B     EXITN                                                            
         MVI   LVLAF,C'.'                                                       
         MVC   LVLAF+1(L'LVLAF-1),LVLAF                                         
         DROP  RF                                                               
         GOTO1 GETEL,DMCB,('LDGELQ',IOREC)                                      
         MVC   SVOPOS,LDGOPOS-LDGELD(R1)                                        
*                                                                               
ISEC02   GOTO1 GETEL,DMCB,('ACLELQ',IOREC)                                      
         XC    WORK,WORK                                                        
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   WORK(0),0(R1)       COPY ELEMENT TO WORK                         
         LA    R1,WORK+(ACLVALS-ACLELD)                                         
         USING ACLVALS,R1          R1=A(ACCOUNT LEVEL VALUES)                   
*                                                                               
         XR    RE,RE               SET ACCOUNT LENGTHS FOR EACH LEVEL           
         LA    R3,SVLVL1                                                        
         USING LVLD,R3                                                          
ISEC04   LA    RE,1(RE)                                                         
         STC   RE,LVL#                                                          
         MVC   LVLLEN,ACLVLEN                                                   
         LA    R3,LVLL(R3)                                                      
         LA    R1,L'ACLVALS(R1)                                                 
         CLI   ACLVLEN,0                                                        
         BNE   ISEC04                                                           
         STC   RE,SV#LVLS          SAVE NO. OF LEVELS OF LEDGER                 
         DROP  R1,R3                                                            
*                                                                               
INITSECX B     EXITY                                                            
         EJECT                                                                  
***********************************************************************         
* INITIALIZE FILTERS                                                  *         
***********************************************************************         
         SPACE 1                                                                
INITFLT  L     R2,4(R1)                                                         
         USING FHD,R2              R2=A(INPUT FIELD)                            
*                                                                               
         OI    SBSTYLO,SBSTDFLQ    FORCE DIRECTORY FILTERING                    
*                                                                               
         MVC   SVPRELEN,SBLENPRE   TEST FOR PREFIX TO ACCOUNT CODE              
         TM    SVINDS,SVIALONE                                                  
         BO    *+12                                                             
         CLI   SVPRELEN,0          CALLER SET'S LENGTH                          
         BNE   IFLT02                                                           
         LA    R0,L'SBACTPRE         OR JUST SET'S THE CODE                     
         LA    RF,SBACTPRE+L'SBACTPRE-1                                         
         CLI   0(RF),C' '                                                       
         BH    *+10                                                             
         BCTR  RF,0                                                             
         BCT   R0,*-10                                                          
         STC   R0,SVPRELEN                                                      
*                                                                               
IFLT02   MVI   ACLVLMIN,1          ALL LEVELS ALLOWED IF STAND ALONE            
         MVC   ACLVLMAX,SV#LVLS                                                 
         TM    SVINDS,SVIALONE                                                  
         BO    IFLT04                                                           
         MVC   ACLVLMIN,SBLVLMIN   SET MINIMUM LEVEL ALLOWED                    
         CLI   ACLVLMIN,0                                                       
         BE    *+14                                                             
         CLC   ACLVLMIN,SV#LVLS                                                 
         BNH   *+10                                                             
         MVC   ACLVLMIN,SV#LVLS                                                 
         MVC   ACLVLMAX,SBLVLMAX   SET MAXIMUM LEVEL ALLOWED                    
         CLI   ACLVLMAX,0                                                       
         BE    *+14                                                             
         CLC   ACLVLMAX,SV#LVLS                                                 
         BNH   *+10                                                             
         MVC   ACLVLMAX,SV#LVLS                                                 
*                                                                               
IFLT04   CLI   FHIL,0              TEST FOR NO INPUT                            
         BNE   *+12                                                             
         MVI   SBRETURN,SBEMIFQ                                                 
         B     IFLTER3                                                          
*                                                                               
         XC    FTVALS,FTVALS                                                    
         MVI   SBLFILT,FTVALSL                                                  
         MVI   SBERRFLD,2          INDICATE KEY FIELD                           
         MVI   SBNNAMES,0          INDICATE NO NAMES FOUND YET                  
         GOTO1 DICTATE,DMCB,C'LU  ',DI,DO                                       
*                                                                               
         LA    R5,IOREC                                                         
         USING SCANBLKD,R5         R5=A(SCAN BLOCK)                             
         GOTO1 VSCANNER,DMCB,(L'SCFLD2,FHD),('SCANBLKN',SCANBLKD),     *        
               C',=  '                                                          
         MVC   SCANN,4(R1)                                                      
         CLI   SCANN,SCANBLKN                                                   
         BNH   *+12                                                             
         MVI   SBRETURN,SBETMNQ                                                 
         B     IFLTER2                                                          
*                                                                               
         MVI   INDX,1              SET FIELD INDEX NO.                          
         XC    BTNOFILT,BTNOFILT                                                
*                                                                               
IFLT12   CLI   SC1STLEN,0          TEST NO 1ST FIELD                            
         BE    IFLT28                                                           
         CLI   SC2NDLEN,0          TEST NO 2ND FIELD                            
         BNE   IFLT14                                                           
*                                                                               
         CLI   SCFLD1,C'?'         TEST HELP WANTED                             
         BE    HELPFLT                                                          
         GOTO1 CMPWORD,DMCB,(SC1STLEN,SCFLD1),(L'AC@HELP,AC@HELP)               
         BE    HELPFLT                                                          
*                                                                               
         CLI   SC1STLEN,SBNALENQ   PARSE NAME                                   
         BNH   *+12                                                             
         MVI   SBRETURN,SBEFTLQ    NAME TOO LONG                                
         B     IFLTER2                                                          
         GOTO1 SBPARSER,DMCB,('SBPARSKQ',SEARCHD),(SC1STLEN,SCFLD1)             
         BNE   IFLTER2                                                          
         B     IFLT28                                                           
*                                                                               
IFLT14   CLI   SC1STLEN,FLTLMINQ   TEST FIELD LONG ENOUGH                       
         BNL   *+12                                                             
         MVI   SBRETURN,SBEFTSQ                                                 
         B     IFLTER1                                                          
*                                                                               
         XR    R3,R3                                                            
         USING FLTTABD,R3          R3=A(FILTER TABLE ENTRY)                     
IFLT16   BAS   RE,NXTFLT                                                        
         BE    *+12                                                             
         MVI   SBRETURN,SBEIKWQ    E-O-T SO INVALID KEYWORD                     
         B     IFLTER1                                                          
*                                                                               
         XR    RF,RF               COMPARE LHS                                  
         ICM   RF,3,FLTLNAM                                                     
         GOTO1 CMPWORD,DMCB,(SC1STLEN,SCFLD1),(FLTLMAX,DO(RF))                  
         BNE   IFLT16                                                           
*                                                                               
         TM    FLTINDS1,FLTIDUP    TEST DUPLICATES ALLOWED                      
         BO    IFLT18                                                           
         GOTO1 TESTBIT,DMCB,(FLTCODE,FTUSED)                                    
         BZ    IFLT18                                                           
         MVI   SBRETURN,SBEDKWQ                                                 
         B     IFLTER1                                                          
IFLT18   GOTO1 SETBIT,DMCB,(FLTCODE,FTUSED)                                     
*                                                                               
         TM    FLTINDS1,FLTINOT    TEST NOTS ALLOWED                            
         BZ    IFLT20                                                           
         CLI   SCFLD2,C'*'                                                      
         BNE   IFLT20                                                           
         GOTO1 SETBIT,DMCB,(FLTCODE,FTNOT)                                      
         MVC   SCFLD2(L'SCFLD2-1),SCFLD2+1                                      
         MVI   SCFLD2+L'SCFLD2-1,C' '                                           
         IC    RF,SC2NDLEN                                                      
         BCTR  RF,0                                                             
         STC   RF,SC2NDLEN                                                      
*                                                                               
IFLT20   CLC   SC2NDLEN,FLTRMIN    TEST FIELD TOO SHORT                         
         BNL   *+12                                                             
         MVI   SBRETURN,SBEFTSQ                                                 
         B     IFLTER1                                                          
         CLC   SC2NDLEN,FLTRMAX    TEST FIELD TOO LONG                          
         BNH   *+12                                                             
         MVI   SBRETURN,SBEFTLQ                                                 
         B     IFLTER1                                                          
*                                                                               
         XR    RF,RF               TEST FOR VALIDATION ROUTINE                  
         ICM   RF,3,FLTVAL                                                      
         BZ    IFLT28                                                           
         B     ACSRCHAC(RF)        GO TO VALIDATION ROUTINE                     
*                                                                               
VFX      BE    VFYES               RETURN POINTS FOR ROUTINES                   
VFNO     B     IFLTER1                                                          
VFYES    DS    0H                                                               
*                                                                               
IFLT28   CLC   INDX,SCANN          TEST ALL FIELDS DONE                         
         BNL   IFLT30                                                           
         IC    RE,INDX             INCREMENT INDEX NO.                          
         LA    RE,1(RE)                                                         
         STC   RE,INDX                                                          
         LA    R5,SCANBLKL(R5)                                                  
         B     IFLT12                                                           
*                                                                               
IFLT30   XR    R3,R3               SET ANY DEFAULT VALUES                       
IFLT32   BAS   RE,NXTFLT                                                        
         BNE   IFLT40                                                           
         GOTO1 TESTBIT,DMCB,(FLTCODE,FTUSED)                                    
         BO    IFLT32              FILTER HAS BEEN USED                         
         XR    RF,RF                                                            
         ICM   RF,3,FLTDEF                                                      
         BZ    IFLT32                                                           
         B     ACSRCHAC(RF)        GO TO SET DEFAULT ROUTINE                    
DFYES    GOTO1 SETBIT,DMCB,(FLTCODE,FTUSED)                                     
DFNO     B     IFLT32                                                           
*                                                                               
IFLT40   XC    FTUSED,BTNOFILT     TURN OFF NO FILTERS                          
*                                                                               
         XR    RE,RE               SET MINIMUM LENGTH OF ACCOUNT CODE           
         IC    RE,FTLVLMIN                                                      
         MH    RE,=Y(LVLL)                                                      
         LA    RF,SVLVLUL-LVLL(RE)                                              
         MVC   SVLENMIN,LVLLEN-LVLD(RF)                                         
         CLC   SVLENMIN,SVPRELEN                                                
         BNL   *+10                                                             
         MVC   SVLENMIN,SVPRELEN                                                
         IC    RE,FTLVLMAX         SET MAXIMUM LENGTH OF ACCOUNT CODE           
         MH    RE,=Y(LVLL)                                                      
         LA    RF,SVLVLUL(RE)                                                   
         MVC   SVLENMAX,LVLLEN-LVLD(RF)                                         
*                                                                               
IFLTYES B      EXITY                                                            
*                                                                               
IFLTER1  DS    0H                  INSERT LHS INTO ERROR MESSAGE                
         LA    RF,SBTXTBLK                                                      
         USING GETTXTD,RF          RF=A(GETTXT BLOCK)                           
         CLI   GTLTXT,0                                                         
         BNE   *+16                                                             
         MVC   GTLTXT,SC1STLEN                                                  
         MVC   SBOPTTXT(L'SCFLD1),SCFLD1                                        
         LA    RE,SBOPTTXT                                                      
         STCM  RE,7,GTATXT                                                      
*                                                                               
IFLTER2  DS    0H                  DISPLAY INDEX # AFTER ERROR MESSAGE          
         MVC   SBTXTBLK+(GTINDX-GETTXTD)(L'INDX),INDX                           
*                                                                               
IFLTER3  CLI   SBRETURN,0                                                       
         BNE   EXITN                                                            
         MVI   SBRETURN,SBEIFVQ                                                 
         B     EXITN                                                            
         DROP  RF                                                               
         EJECT                                                                  
***********************************************************************         
* DISPLAY HELP FOR FILTERS                                            *         
***********************************************************************         
         SPACE 1                                                                
HELPFLT  MVC   WORK,SPACES                                                      
         LA    R4,WORK                                                          
         XR    R3,R3                                                            
*                                                                               
HFLT02   BAS   RE,NXTFLT           GET NEXT TABLE ENTRY                         
         BNE   HFLT10                                                           
         XR    RE,RE                                                            
         ICM   RE,3,FLTLNAM        COPY KEYWORD                                 
         LA    RE,DO(RE)                                                        
         XR    RF,RF                                                            
         IC    RF,FLTLMAX                                                       
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),0(RE)                                                    
         AR    R4,RF                                                            
         CLI   0(R4),C' '          FIND END OF KEYWORD                          
         BH    *+8                                                              
         BCT   R4,*-8                                                           
         MVI   1(R4),C','          APPEND COMMA                                 
         LA    R4,2(R4)                                                         
         B     HFLT02                                                           
*                                                                               
HFLT10   BCTR  R4,0                REMOVE LAST COMMA                            
         MVI   0(R4),C' '                                                       
         IC    RF,FHLN             COPY LIST TO OUTPUT                          
         SH    RF,=Y(FHDAD+FHDAD+1)                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   FHDA(0),WORK                                                     
*                                                                               
HELPFLTX MVI   SBRETURN,SBEHLPQ                                                 
         B     EXITN                                                            
         EJECT                                                                  
***********************************************************************         
* VALIDATE FILTER ROUTINES                                            *         
***********************************************************************         
         SPACE 1                                                                
VFRNAM   XR    RF,RF               ** = KEYWORD **                              
         ICM   RF,3,FLTRNAM                                                     
         GOTO1 CMPWORD,DMCB,(SC2NDLEN,SCFLD2),(FLTRMAX,DO(RF))                  
         B     VFX                                                              
         SPACE 1                                                                
*                                  ** =YES/ONLY/NO **                           
VFYON    GOTO1 CMPWORD,DMCB,(SC2NDLEN,SCFLD2),(L'AC@YES,AC@YES)                 
         BNE   VFYON02                                                          
         GOTO1 SETBIT,(R1),(FLTCODE,BTNOFILT)                                   
         B     VFYES                                                            
VFYON02  GOTO1 (RF),(R1),,(L'AC@ONLY,AC@ONLY)                                   
         BE    VFYES                                                            
         GOTO1 (RF),(R1),,(L'AC@NO,AC@NO)                                       
         BNE   VFNO                                                             
         GOTO1 SETBIT,(R1),(FLTCODE,FTNOT)                                      
         B     VFYES                                                            
         SPACE 1                                                                
*                                  ** NAME **                                   
VFNAME   GOTO1 SBPARSER,DMCB,('SBPARSKQ',SEARCHD),(SC2NDLEN,SCFLD2)             
         B     VFX                                                              
         SPACE 1                                                                
VFAF1    MVC   FTAF1,SCFLD2        ** ACCOUNT FILTERS 1-5 **                    
         B     VFYES                                                            
VFAF2    MVC   FTAF2,SCFLD2                                                     
         B     VFYES                                                            
VFAF3    MVC   FTAF3,SCFLD2                                                     
         B     VFYES                                                            
VFAF4    MVC   FTAF4,SCFLD2                                                     
         B     VFYES                                                            
VFAF5    MVC   FTAF5,SCFLD2                                                     
         B     VFYES                                                            
         SPACE 1                                                                
*                                  ** LEVEL=X-Y **                              
VFLEV    GOTO1 PARTINP,DMCB,(SC2NDLEN,SCFLD2)                                   
         LA    R1,FTLVLMIN                                                      
         LA    RF,PARTALN                                                       
         BAS   RE,VALFIG                                                        
         LA    R1,FTLVLMAX                                                      
         LA    RF,PARTBLN                                                       
         BAS   RE,VALFIG                                                        
         CLC   FTLVLMIN,FTLVLMAX                                                
         BH    VFNO                                                             
         CLC   FTLVLMIN,ACLVLMIN                                                
         BL    *+14                                                             
         CLC   FTLVLMAX,ACLVLMAX                                                
         BNH   VFYES                                                            
         LA    RF,SBTXTBLK                                                      
         USING GETTXTD,RF                                                       
         MVI   GTMSYS,ACCSYSQ                                                   
         MVC   GTMSGNO,=AL2(AE$ALVLA)                                           
         MVC   SBOPTTXT(1),ACLVLMIN                                             
         OI    SBOPTTXT,C'0'                                                    
         MVI   SBOPTTXT+1,C'-'                                                  
         MVC   SBOPTTXT+2(1),ACLVLMAX                                           
         OI    SBOPTTXT+2,C'0'                                                  
         MVI   GTLTXT,3                                                         
         CLC   ACLVLMIN,ACLVLMAX                                                
         BNE   *+8                                                              
         MVI   GTLTXT,1                                                         
         MVI   SBRETURN,SBETXTQ                                                 
         B     VFNO                                                             
         DROP  RF                                                               
*                                                                               
VALFIG   CLI   0(RF),1             VALIDATE FIGURE                              
         BH    VFNO                                                             
         BE    VFIG02                                                           
         MVC   0(1,R1),ACLVLMIN    THE MIN                                      
         CLI   1(RF),X'00'                                                      
         BER   RE                                                               
         MVC   0(1,R1),ACLVLMAX    THE MAX                                      
         BR    RE                                                               
VFIG02   CLI   1(RF),C'1'                                                       
         BL    VFNO                                                             
         MVC   0(1,R1),1(RF)                                                    
         NI    0(R1),X'0F'                                                      
         BR    RE                                                               
         SPACE 1                                                                
*                                  ** ADDRESS = YES/NO **                       
VFADR    GOTO1 CMPWORD,DMCB,(SC2NDLEN,SCFLD2),(FLTRMAX,AC@YES)                  
         BNE   *+12                                                             
         OI    SVINDS,SVIADR                                                    
         B     VFYES                                                            
         GOTO1 (RF),(R1),,(FLTRMAX,AC@NO)                                       
         BNE   VFNO                                                             
         NI    SVINDS,FF-SVIADR                                                 
         B     VFYES                                                            
         SPACE 1                                                                
*                                  ** CODE=ABC-XYZ **                           
VFCODE   GOTO1 PARTINP,DMCB,(SC2NDLEN,SCFLD2)                                   
         MVC   FTCODMIN,PARTA                                                   
         MVC   FTCODMAX,PARTB                                                   
         CLC   FTCODMIN,FTCODMAX                                                
         BNH   VFYES                                                            
         B     VFNO                                                             
         EJECT                                                                  
***********************************************************************         
* SET DEFAULT FILTER VALUES                                           *         
***********************************************************************         
         SPACE 1                                                                
*                                  ** CLOSED=NO **                              
DFCLOSED TM    SVINDS,SVIALONE     DON'T BOTHER IF STAND-ALONE CALL             
         BO    DFNO                                                             
         GOTO1 SETBIT,DMCB,(FLTCODE,FTNOT)                                      
         B     DFYES                                                            
         SPACE 1                                                                
DFLEV    MVC   FTLVLMIN,ACLVLMIN   ** LEVEL=LOWEST - HIGHEST **                 
         MVC   FTLVLMAX,ACLVLMAX                                                
         B     DFYES                                                            
         SPACE 1                                                                
DFCODE   XC    FTCODMIN,FTCODMIN   ** CODE=LOWEST - HIGHEST **                  
         MVC   FTCODMAX,EFFS                                                    
         B     DFYES                                                            
         SPACE 1                                                                
DFADR    NI    SVINDS,FF-SVIADR    ** ADDRESS = NO **                           
         B     DFYES                                                            
         SPACE 2                                                                
         DROP  R2,R3,R5                                                         
         EJECT                                                                  
***********************************************************************         
* INITIALIZE DISPLAY - SET UP HEADLINES                               *         
***********************************************************************         
         SPACE 1                                                                
INITDIS  L     RF,SBATWA           TEST FOR A REPORT                            
         ICM   R1,15,REPACOM-TWAD(RF)                                           
         BZ    *+8                                                              
         OI    SVINDS,SVIREP                                                    
*                                                                               
         XC    SBEXHEAD,SBEXHEAD                                                
         MVI   SBEXHNHD,2          SET NO. OF HEADLINES                         
*                                                                               
         LA    R6,SBEXHTIT         R6=A(TITLE)                                  
         LR    R0,R6                                                            
         MVC   0(L'SDSRUL,R6),SDSRUL                                            
         LA    R6,L'SDSRUL(R6)                                                  
         XR    RE,RE                                                            
         ICM   RE,1,SVPRELEN                                                    
         BZ    IDIS01                                                           
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R6),SBACTPRE                                                 
         LA    R6,1(R6,RE)                                                      
IDIS01   MVI   1(R6),C'-'                                                       
         LA    R6,3(R6)                                                         
*                                                                               
         LR    RE,R6                                                            
         SR    RE,R0               RE=L'TILE SO FAR                             
         LA    RF,L'SRVID                                                       
         SR    RF,RE               RF=L'TITLE LEFT                              
         GOTO1 TITLE,DMCB,(RF),SVLVLUL                                          
         LM    RE,RF,0(R1)                                                      
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R6),0(RF)                                                    
         LA    R6,1(R6,RE)         R6=A(FIELD HEADER ENTRIES)                   
         LR    RE,R6                                                            
         SR    RE,R0                                                            
         STC   RE,SBEXHTIL                                                      
*                                                                               
         XR    R5,R5               R5=ITEM DISPLACEMENT                         
         LA    R1,SDSRCLMS         R1=A(COLUMN LIST)                            
         XR    R2,R2               R2=COUNT OF NO. OF COLUMNS                   
*                                                                               
IDIS02   BAS   RE,NXTCLM                                                        
         BNE   IDIS10                                                           
         USING CLMTABD,R3                                                       
         SH    RF,=Y(CLMWDTH-CLMTABD+1)                                         
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   2(0,R6),CLMWDTH                                                  
         LA    RF,3(RF)                                                         
         STC   RF,0(R6)            SET ENTRY LENGTH                             
         STC   R4,1(R6)            SET ITEM DISPLACEMENT                        
*                                                                               
         TM    CLMINDS1,CLMIKEY+CLMINAME                                        
         BZ    IDIS08                                                           
         LA    RE,SBEXHEAD                                                      
         SR    RE,R6                                                            
         LPR   RE,RE                                                            
         TM    CLMINDS1,CLMIKEY                                                 
         BZ    *+12                                                             
         STC   RE,SBEXHDKY         SET DISPLACEMENT TO KEY                      
         B     *+8                                                              
         STC   RE,SBEXHDNA         SET DISPLACEMENT TO NAME                     
*                                                                               
IDIS08   LA    R2,1(R2)                                                         
         BXH   R6,RF,IDIS02                                                     
         DROP  R3                                                               
*                                                                               
IDIS10   MVI   0(R6),EOT                                                        
*                                                                               
         TM    SVINDS,SVIREP+SVIADR                                             
         BNO   INITDISX            INSERT 'ADDRESS' HEADLINE ON REPORT          
         L     R3,SBATWA                                                        
         L     R3,REPAMIDS-TWAD(R3)                                             
         LA    R3,L'ACTKACT(R3,R4)                                              
         AR    R3,R2                                                            
         MVCDD 0(L'ADRADD1,R3),AC#ADR                                           
         GOTO1 DICTATE,DMCB,C'SL  ',(R3)                                        
         MVCDD REPWREGQ(L'ADRADD1,R3),AC#ADR,LU                                 
         GOTO1 (RF),(R1),,REPWREGQ(R3)                                          
INITDISX B     EXITY                                                            
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO BUILD TITLE                                              *         
*                                                                     *         
* NTRY: P1=MAX. LENGTH OF OUTPUT                                      *         
*       P2=A(LVLD)                                                    *         
* EXIT: P1=L(OUTPUT)                                                  *         
*       P2=A(OUTPUT)                                                  *         
***********************************************************************         
         SPACE 1                                                                
TITLE    NTR1  WORK=(R6,TWORKL)                                                 
         USING TWORKD,R6                                                        
         ST    R1,TAOPARMS                                                      
         MVC   TIPARMS,0(R1)                                                    
         L     R4,TILVL                                                         
         USING LVLD,R4             R4=A(LEVEL INFORMATION)                      
*                                                                               
         CLC   SVPRELEN,LVLLEN                                                  
         BE    TITLE02                                                          
         BH    TITLE04                                                          
*                                                                               
         BAS   RE,TCODE            RETURN THE REMAINDER OF THE CODE             
         B     TITLEX                                                           
TITLE02  BAS   RE,TNAME            RETURN THE NAME OF THE LAST LEVEL            
         B     TITLEX                                                           
*                                                                               
TITLE04  BAS   RE,TCODE                                                         
         L     RF,TILOUT                                                        
         S     RF,TOLOUT                                                        
         LA    RE,L'TITLEDIV                                                    
         SR    RF,RE               RF=LENGTH OF TITLE LEFT AFTER CODE           
         GOTO1 TITLE,DMCB,(RF),LVLD+LVLL                                        
         MVC   TOUT2LN,0(R1)                                                    
         L     RF,4(R1)                                                         
         MVC   TOUT2,0(RF)                                                      
*                                                                               
         BAS   RE,TNAME                                                         
         L     RE,TOUT2LN          RE=L(OUTPUT FOR NEXT)                        
         A     RE,TOLOUT              +L(NAME)                                  
         LA    RE,L'TITLEDIV(RE)      +L(DIVIDER)                               
         C     RE,TILOUT           TEST NAME WOULD FIT                          
         BNH   *+8                                                              
         BAS   RE,TCODE            IF NOT DISPLAY CODE INSTEAD OF NAME          
*                                                                               
         LA    R1,TOUT             APPEND NEXT TO CURRENT                       
         A     R1,TOLOUT                                                        
         MVC   0(L'TITLEDIV,R1),TITLEDIV                                        
         MVC   L'TITLEDIV(L'TOUT,R1),TOUT2                                      
         L     RE,TOUT2LN                                                       
         A     RE,TOLOUT           UPDATE L(OUTPUT)                             
         LA    RE,L'TITLEDIV(RE)                                                
         ST    RE,TOLOUT                                                        
*                                                                               
TITLEX   LA    RE,TOUT                                                          
         ST    RE,TOAOUT                                                        
         L     R1,TAOPARMS                                                      
         MVC   0(L'TOPARMS,R1),TOPARMS                                          
         B     EXIT                                                             
         SPACE 1                                                                
***********************************************************************         
* SET OUTPUT TO NAME                                                  *         
***********************************************************************         
         SPACE 1                                                                
TNAME    NTR1  ,                                                                
         LA    R2,IOKEY                                                         
         USING ACTRECD,R2                                                       
         MVC   ACTKEY,SPACES                                                    
         MVC   ACTKCPY,SBAGYB                                                   
         MVC   ACTKUNT(L'SDSRUL),SDSRUL                                         
         ICM   RE,1,LVLLEN                                                      
         BZ    TNAME02                                                          
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   ACTKACT(0),SBACTPRE                                              
TNAME02  GOTO1 VDATAMGR,DMCB,DMREAD,ACCMST,ACTKEY,IOREC,WORK                    
         BNE   TNAMEN                                                           
         TM    SVCPYSTC,CPYSALTN                                                
         BZ    TNAME04                                                          
         GOTO1 GETEL,DMCB,('XNMELQ',IOREC)                                      
         BNE   TNAME06                                                          
         SH    RF,=Y(XNMLN1Q+1)                                                 
         ST    RF,TOLOUT                                                        
         MVC   TOUT,XNMSUBN-XNMELD(R1)                                          
         B     EXITY                                                            
*                                                                               
TNAME04  TM    SVCPYSTC,CPYSMEDN                                                
         BZ    TNAME06                                                          
         GOTO1 GETEL,DMCB,('SNMELQ',IOREC)                                      
         BNE   TNAME06                                                          
         SH    RF,=Y(SNMLN1Q)                                                   
         ST    RF,TOLOUT                                                        
         MVC   TOUT,SNMNAME-SNMELD(R1)                                          
         B     EXITY                                                            
*                                                                               
TNAME06  GOTO1 GETEL,DMCB,('NAMELQ',IOREC)                                      
         BNE   TNAMEN                                                           
         SH    RF,=Y(NAMLN1Q)                                                   
         ST    RF,TOLOUT                                                        
         MVC   TOUT,NAMEREC-NAMELD(R1)                                          
         B     EXITY                                                            
*                                                                               
TNAMEN   BAS   RE,TCODE                                                         
         B     EXITN                                                            
         DROP  R2                                                               
         SPACE 1                                                                
***********************************************************************         
* SET OUTPUT TO CODE                                                  *         
***********************************************************************         
         SPACE 1                                                                
TCODE    CLI   LVL#,0              TEST UNIT/LEDGER REQUIRED                    
         BNE   TCODE02                                                          
         MVC   TOUT(L'SDSRUL),SDSRUL                                            
         LA    RF,L'SDSRUL                                                      
         ST    RF,TOLOUT                                                        
         B     TCODEX                                                           
*                                                                               
TCODE02  LA    RF,LVLD                                                          
         LA    R1,LVLL                                                          
         SR    RF,R1               RF=A(LVLD) FOR PREVIOUS LEVEL                
         IC    R1,LVLLEN-LVLD(RF)  R1=D(TO START OF CODE)                       
         XR    R0,R0                                                            
         IC    R0,LVLLEN                                                        
         CLC   SVPRELEN,LVLLEN                                                  
         BNL   *+8                                                              
         IC    R0,SVPRELEN         R0=D(TO END OF CODE)                         
         SR    R0,R1                                                            
         ST    R0,TOLOUT           SAVE L(CODE)                                 
         LA    R1,SBACTPRE(R1)                                                  
         MVC   TOUT(L'SBACTPRE),0(R1)                                           
*                                                                               
TCODEX   BR    RE                                                               
         SPACE 1                                                                
         DROP  R4,R6                                                            
         SPACE 1                                                                
TWORKD   DSECT                     * TITLE W/S *                                
TIPARMS  DS    0XL8                INPUT PARAMETERS                             
TILOUT   DS    A                   MAXIMUM LENGTH OF OUTPUT                     
TILVL    DS    A                   A(LVLD)                                      
TOPARMS  DS    0XL8                OUTPUT PARAMETERS                            
TOLOUT   DS    A                   LENGTH OF OUTPUT                             
TOAOUT   DS    A                   A(OUTPUT)                                    
TAOPARMS DS    A                   A(OUTPUT PARAMETERS)                         
TOUT2LN  DS    A                                                                
TOUT     DS    XL100                                                            
TOUT2    DS    XL100                                                            
TWORKL   EQU   *-TWORKD                                                         
ACSRCHAC CSECT                                                                  
         EJECT                                                                  
***********************************************************************         
* PRESET BIT LIST                                                     *         
***********************************************************************         
         SPACE 1                                                                
BITSET   CLI   SBNNAMES,0          TEST NO NAME FILTERS                         
         BNE   EXITY                                                            
         CLI   FTLVLMIN,1          TEST ACCOUNT LEVEL FILTER                    
         BNE   BSET00                                                           
         CLC   FTLVLMAX,SV#LVLS                                                 
         BNE   BSET00                                                           
         CLI   SVPRELEN,0          TEST PREFIX FILTER                           
         BNE   BSET00                                                           
         CLI   FTCODMIN,0          TEST ACCOUNT CODE RANGE FILTER               
         BH    BSET00                                                           
         CLI   FTCODMAX,FF                                                      
         BNL   EXITY               EVERY ACCOUNT IS VALID                       
*                                                                               
BSET00   L     R6,4(R1)                                                         
         USING SRCHLSTD,R6                                                      
         CLC   SRCHLTYP,=AL2(SRCHLTBQ)                                          
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    R2,KEYMIN           SET UP MINUMUM KEY                           
         USING SAPRECD,R2                                                       
         XC    SAPKEY,SAPKEY                                                    
         MVI   SAPKTYP,SAPKTYPQ                                                 
         MVC   SAPKCPY,SBAGYB                                                   
         MVC   SAPKUL,SDSRUL                                                    
         MVI   SAPKSUB,SAPKSUBQ                                                 
         MVC   SAPKLVL,FTLVLMIN                                                 
         MVC   SAPKACT(L'SBACTPRE),SBACTPRE                                     
         XR    RE,RE                                                            
         IC    RE,SVPRELEN                                                      
         LA    RF,L'SAPKACT                                                     
         SR    RF,RE                                                            
         BCTR  RF,0                                                             
         LA    R1,SAPKACT(RE)                                                   
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R1),FTCODMIN                                                 
         MVC   KEYMAX,KEYMIN       SET UP MAXIMUM KEY                           
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   KEYMAX-KEYMIN(0,R1),FTCODMAX                                     
*                                                                               
BSET02   LA    R2,IOKEY                                                         
         MVC   SAPKEY,KEYMIN                                                    
         LA    RF,DMRDHI                                                        
         B     *+8                                                              
BSET04   LA    RF,DMRSEQ                                                        
         GOTO1 VDATAMGR,DMCB,(RF),ACCDIR,SAPRECD,SAPRECD                        
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
         CLC   SAPKEY,KEYMAX       TEST RECORD LESS THAN MAXIMUM KEY            
         BH    BSET08                                                           
*                                                                               
         XR    RE,RE                                                            
         BCTR  RE,0                                                             
         ICM   RE,7,SAPKSEQ                                                     
         LCR   RE,RE               RE=UNCOMPLEMENTED SEQUENCE NUMBER            
         BCTR  RE,0                                                             
         SRDL  RE,3                                                             
         SRL   RF,32-3                                                          
         LA    R1,X'80'                                                         
         SRL   R1,0(RF)                                                         
         LA    RE,SRCHLLST(RE)                                                  
         EX    R1,*+8                                                           
         BZ    *+10                                                             
         TM    0(RE),0                                                          
         DC    H'0'                                                             
         EX    R1,*+8                                                           
         B     *+8                                                              
         OI    0(RE),0             SET BIT                                      
*                                                                               
         ICM   RE,7,SRCHLNAC       INCREMENT COUNT                              
         LA    RE,1(RE)                                                         
         STCM  RE,7,SRCHLNAC                                                    
         CLC   SRCHLNAC,=AL3(4000) TEST NOT TOO MANY                            
         BL    BSET04                                                           
         MVI   SBRETURN,SBELTSQ    ASK USER TO BE MORE SPECIFIC                 
         B     EXITN                                                            
*                                                                               
BSET08   LA    R2,KEYMIN                                                        
         IC    RE,SAPKLVL          INCREMENT ACCOUNT LEVEL NUMBER               
         LA    RE,1(RE)                                                         
         STC   RE,SAPKLVL                                                       
         STC   RE,KEYMAX+(SAPKLVL-SAPRECD)                                      
         CLC   SAPKLVL,FTLVLMAX    TEST WITHIN MAXIMUM                          
         BNH   BSET02                                                           
*                                                                               
BSET10   OC    SRCHLNAC,SRCHLNAC                                                
         BNZ   EXITY                                                            
         OI    SRCHLNAC,X'80'      SET FLAG FOR NO MATCHED CODES                
         B     EXITY                                                            
         DROP  R2,R6                                                            
         EJECT                                                                  
***********************************************************************         
* SECURITY ON SEARCH DIRECTORY RECORD                                 *         
***********************************************************************         
         SPACE 1                                                                
DIRSEC   CLI   SVOPOS,LDGONONE     TEST ANY OFFICE SECURITY ON LEDGER           
         BE    EXITY                                                            
         CLI   SVOPOS,LDGOOFLS     TEST OFFICE LIST IN ACCOUNT CODE             
         BE    DSEC02                                                           
         CLI   SVOPOS,LDGOKEY      TEST OFFICE(1) IN ACCOUNT CODE               
         BNH   DSEC02                                                           
         CLI   SVOPOS,LDGONKLO     TEST OFFICE(2) IN ACCOUNT CODE               
         BL    EXITY                                                            
         CLI   SVOPOS,LDGONKHI                                                  
         BH    EXITY                                                            
*                                                                               
DSEC02   L     R2,4(R1)            TEST ACCESS FOR OFFICE                       
         USING SRCRECD,R2                                                       
         IC    R1,SVOPOS                                                        
         CLI   SVOPOS,LDGOOFLS     TEST OFFICE LIST IN ACCOUNT CODE             
         BNE   *+12                                                             
         OI    OFFACTRL,OFFOFLIS                                                
         LA    R1,1                TREAT AS OFFPOS=1                            
         LA    RE,X'0F'                                                         
         NR    R1,RE                                                            
         GOTO1 TSTOFF,SRCKACT-1(R1)                                             
         B     EXIT                                                             
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* FILTER ON SEARCH DIRECTORY RECORD                                   *         
***********************************************************************         
         SPACE 1                                                                
DIRFLT   L     R2,4(R1)                                                         
         USING SRCRECD,R2                                                       
*                                                                               
         XR    RE,RE               MATCH ON PREFIX TO CODE                      
         ICM   RE,1,SVPRELEN                                                    
         BZ    DFLT02                                                           
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         BNE   DIRFLTN                                                          
         CLC   SBACTPRE(0),SRCKACT                                              
         LA    RE,1(RE)                                                         
*                                                                               
DFLT02   LA    RF,L'SRCKACT        TEST CODE WITHIN MINUMUM AND MAXIMUM         
         SR    RF,RE                                                            
         BCTR  RF,0                                                             
         LA    R1,SRCKACT(RE)                                                   
         EX    RF,DFLTMIN                                                       
         BL    DIRFLTN                                                          
         EX    RF,DFLTMAX                                                       
         BH    DIRFLTN                                                          
*                                                                               
DFLT04   LA    R0,L'SRCKACT        FIND LENGTH OF CODE                          
         LA    RF,SRCKACT+L'SRCKACT-1                                           
         CLI   0(RF),C' '                                                       
         BNE   *+10                                                             
         BCTR  RF,0                                                             
         BCT   R0,*-10                                                          
         CLM   R0,1,SVLENMIN       TEST LENGTH > MINIMUM                        
         BNH   DIRFLTN                                                          
         CLM   R0,1,SVLENMAX       TEST LENGTH <= MAXIMUM                       
         BH    DIRFLTN                                                          
*                                                                               
DIRFLTY  B     EXITY                                                            
DIRFLTN  MVI   SBRETURN,SBRFLTQ                                                 
         B     EXITN                                                            
*                                                                               
DFLTMIN  CLC   0(0,R1),FTCODMIN                                                 
DFLTMAX  CLC   0(0,R1),FTCODMAX                                                 
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* SECURITY ON ACCOUNT RECORD                                          *         
***********************************************************************         
         SPACE 1                                                                
RECSEC   L     R2,4(R1)                                                         
         USING ACTRECD,R2          R2=A(RECORD)                                 
*                                                                               
         LA    R3,SVLVL1           FIND LEVEL OF RECORD                         
         USING LVLD,R3                                                          
         XR    RF,RF                                                            
         LA    R0,LVL#MAX                                                       
RSEC02   IC    RF,LVLLEN                                                        
         LA    R1,ACTKACT(RF)                                                   
         CLC   0(L'ACTKACT,R1),SPACES                                           
         BE    RSEC04                                                           
         LA    R3,LVLL(R3)                                                      
         BCT   R0,RSEC02                                                        
         DC    H'0'                                                             
RSEC04   GOTO1 GETLVL,DMCB,(R2),(R3)                                            
         LA    R3,SVCURLVL         R3=A(CURRENT LEVEL VALUES)                   
*                                                                               
         CLC   SVSECY,LVLSECY      TEST ACCOUNT LEVEL SECURITY                  
         BL    RECSECN                                                          
*                                                                               
         CLI   SVOPOS,LDGOPROF     TEST OFFICE SECURITY ON PROD PROFILE         
         BNE   RSEC06                                                           
         GOTO1 TSTOFF,LVLGAOFF                                                  
         BNE   RECSECN                                                          
         B     RECSECY                                                          
*                                                                               
RSEC06   CLI   SVOPOS,LDGOFLT1     TEST OFFICE SECURITY ON FILTERS              
         BL    RECSECY                                                          
         CLI   SVOPOS,LDGOFLT4+1                                                
         BH    RECSECY                                                          
         IC    R1,SVOPOS                                                        
         LA    RE,X'0F'                                                         
         NR    R1,RE                                                            
         GOTO1 TSTOFF,LVLAF-1(R1)                                               
         BNE   RECSECN                                                          
*                                                                               
RECSECY  B     EXITY                                                            
RECSECN  MVI   SBRETURN,SBRSECQ                                                 
         B     EXITN                                                            
         DROP  R2,R3                                                            
         SPACE 3                                                                
***********************************************************************         
* GET ACCOUNT LEVEL VALUES                                            *         
*                                                                     *         
* NTR1: P1=(0, A(ACCOUNT RECORD)) OR (1, A(ACCOUNT KEY))              *         
*       P2=A(LVLD)                                                    *         
***********************************************************************         
         SPACE 1                                                                
         PUSH  USING                                                            
GETLVL   NTR1  WORK=(R6,GLWORKL)                                                
         USING GLWORKD,R6                                                       
         MVC   GLFLAG,0(R1)                                                     
         LM    R2,R3,0(R1)                                                      
         USING ACTRECD,R2          R2=A(RECORD)                                 
         USING LVLD,R3             R3=A(LEVEL INFORMATION)                      
         LR    R4,R3                                                            
         AHI   R4,-LVLL            R4=A(LEVEL INFO FOR PREVIOUS LEVEL)          
PREV     USING LVLD,R4                                                          
*                                                                               
         IC    RF,LVLLEN           TEST ACCOUNT CODE=SAVED ACCOUNT CODE         
         BCTR  RF,0                  FOR THIS LEVEL                             
         EX    RF,*+8                                                           
         BE    GETLVLX                                                          
         CLC   ACTKACT(0),SVCURACT                                              
*                                                                               
         CLI   LVL#,1              TEST LEVEL 1 ACCOUNT                         
         BE    GLVL02                                                           
         MVC   GLKEY,SPACES        GET VALUES FOR PREVIOUS LEVEL                
         IC    RF,PREV.LVLLEN                                                   
         LA    RF,LDGKEND-1(RF)                                                 
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   GLKEY(0),ACTKEY                                                  
         GOTO1 GETLVL,DMCB,(1,GLKEY),PREV.LVLD                                  
*                                                                               
GLVL02   DS    0H                  INITIALISE VALUES                            
         MVC   LVLAF,SPACES                                                     
         XC    LVLSECY,LVLSECY                                                  
         MVC   LVLGAOFF,SPACES                                                  
*                                                                               
         CLI   GLFLAG,0            READ RECORD IF ONLY HAVE KEY                 
         BE    GLVL04                                                           
         GOTO1 VDATAMGR,DMCB,DMREAD,ACCMST,ACTKEY,IOREC,WORK                    
         BNE   GLVL10                                                           
         LA    R2,IOREC            R2=A(RECORD)                                 
*                                                                               
GLVL04   DS    0H                                                               
         MVC   LVLAF,ACTRSAF1      COPY FILTERS                                 
         GOTO1 GETEL,DMCB,('RSTELQ',ACTRECD)  GET SEC. LEVEL                    
         BNE   *+10                                                             
         MVC   LVLSECY,RSTSECY-RSTELD(R1)                                       
         CLI   SVOPOS,LDGOPROF     GET PRODUCTION OFFILE                        
         BNE   GLVL10                                                           
         GOTO1 GETEL,DMCB,('PPRELQ',ACTRECD)                                    
         BNE   *+10                                                             
         MVC   LVLGAOFF,PPRGAOFF-PPRELD(R1)                                     
*                                                                               
GLVL10   DS    0H                                                               
         CLI   LVL#,1                                                           
         BE    GLVL20                                                           
*                                                                               
         LA    R0,L'LVLAF          MERGE ACCOUNT FILTERS                        
         LA    RF,PREV.LVLAF                                                    
GLVL12   CLI   LVLL(RF),C' '                                                    
         BH    *+10                                                             
         MVC   LVLL(1,RF),0(RF)                                                 
         LA    RF,1(RF)                                                         
         BCT   R0,GLVL12                                                        
*                                                                               
         CLC   LVLSECY,PREV.LVLSECY                                             
         BNL   *+10                MERGE SECURITY LEVEL                         
         MVC   LVLSECY,PREV.LVLSECY                                             
*                                                                               
         CLI   SVOPOS,LDGOPROF     TEST OFFICE ON PRODUCTION PROFILE            
         BNE   GLVL20                                                           
         CLI   LVLGAOFF,C' '       MERGE OFFICE                                 
         BH    *+10                                                             
         MVC   LVLGAOFF,PREV.LVLGAOFF                                           
*                                                                               
GLVL20   DS    0H                                                               
         MVC   SVCURACT,ACTKACT    UPDATE CURRENT ACCOUNT CODE                  
         MVC   SVCURLVL,LVLD       UPDATE CURRENT VALUES                        
*                                                                               
GETLVLX  B     EXIT                                                             
         POP   USING                                                            
*                                                                               
GLWORKD  DSECT                     * GETLVL W/S *                               
GLKEY    DS    CL(L'ACTKEY)                                                     
GLFLAG   DS    XL1                                                              
GLWORKL  EQU   *-GLWORKD                                                        
ACSRCHAC CSECT                                                                  
         EJECT                                                                  
***********************************************************************         
* FILTER ON ACCMST RECORD                                             *         
***********************************************************************         
         SPACE 1                                                                
RECFLT   L     R2,4(R1)                                                         
         USING ACTRECD,R2          R2=A(ACCOUNT RECORD)                         
         XR    R3,R3                                                            
         USING FLTTABD,R3          R3=FILTER TABLE ENTRY                        
RFLT02   BAS   RE,NXTFLT                                                        
         BNE   RECFLTY             E-O-T                                        
*                                                                               
         GOTO1 TESTBIT,DMCB,(FLTCODE,FTUSED)                                    
         BZ    RFLT02                                                           
         XR    RF,RF                                                            
         ICM   RF,3,FLTREC                                                      
         BZ    RFLT02                                                           
         B     ACSRCHAC(RF)        GO TO FILTER RECORD ROUTINE                  
*                                                                               
RFX      BNE   RFNO                RETURN POINTS FOR ROUTINES                   
RFYES    GOTO1 TESTBIT,DMCB,(FLTCODE,FTNOT)                                     
         BZ    RFLT02              MATCH FOR '=' FILTER                         
         B     RECFLTN                                                          
RFNO     GOTO1 TESTBIT,DMCB,(FLTCODE,FTNOT)                                     
         BO    RFLT02              NO MATCH FOR 'NOT=' FILTER                   
*                                                                               
RECFLTN  MVI   SBRETURN,SBRFLTQ                                                 
         B     EXITN                                                            
RECFLTY  B     EXITY                                                            
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
* FILTER ROUTINES                                                     *         
***********************************************************************         
         SPACE 1                                                                
*                                  ** ACCOUNT FILTERS 1-5 **                    
RFAF1    CLC   FTAF1,SVCURLVL+(LVLAF1-LVLD)                                     
         B     RFX                                                              
RFAF2    CLC   FTAF2,SVCURLVL+(LVLAF2-LVLD)                                     
         B     RFX                                                              
RFAF3    CLC   FTAF3,SVCURLVL+(LVLAF3-LVLD)                                     
         B     RFX                                                              
RFAF4    CLC   FTAF4,SVCURLVL+(LVLAF4-LVLD)                                     
         B     RFX                                                              
RFAF5    CLC   FTAF5,SVCURLVL+(LVLAF5-LVLD)                                     
         B     RFX                                                              
         SPACE 1                                                                
*                                  ** BAL=OUT **                                
RFBOUT   GOTO1 GETEL,DMCB,('ABLELQ',ACTRECD)                                    
         BNE   RFNO                                                             
         USING ABLELD,R1                                                        
         ZAP   DUB,ABLFRWD                                                      
         SP    DUB,ABLDR                                                        
         AP    DUB,ABLCR                                                        
         CLI   ABLLN,ABLLN1Q                                                    
         BNH   *+10                                                             
         AP    DUB,ABLURG                                                       
         CP    DUB,PZERO                                                        
         BE    RFNO                                                             
         B     RFYES                                                            
         DROP  R1                                                               
         SPACE 1                                                                
RFLOCKED TM    ACTRSTAT,ACTSLOCK   ** LOCKED=YES/NO **                          
         BZ    RFNO                                                             
         B     RFYES                                                            
         SPACE 1                                                                
RFUNAPPR TM    ACTRSTAT,ACTSDRFT   ** UNAPPROVED=YES/NO **                      
         BZ    RFNO                                                             
         B     RFYES                                                            
         SPACE 1                                                                
RFCLOSED TM    ACTRSTAT,ACTSCLOS   ** CLOSED=YES/NO **                          
         BZ    RFNO                                                             
         B     RFYES                                                            
         SPACE 1                                                                
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* DISPLAY RECORD                                                      *         
***********************************************************************         
         SPACE 1                                                                
RECDIS   L     R2,8(R1)                                                         
         USING ACTRECD,R2          R2=A(ACCOUNT RECORD)                         
         L     R6,4(R1)            R6=A(OUTPUT AREA)                            
         MVI   0(R6),C' '                                                       
         MVC   1(255,R6),0(R6)                                                  
*                                                                               
         XR    R5,R5               R5=ITEM DISPLACEMENT                         
         LA    R1,SDSRCLMS         R1=A(COLUMN LIST)                            
*                                                                               
RDIS02   BAS   RE,NXTCLM                                                        
         BNE   RDIS10                                                           
         USING CLMTABD,R3                                                       
         AR    R4,R6               R4=A(OUTPUT AREA)                            
         BAS   RE,DISPLAY                                                       
         B     RDIS02                                                           
*                                                                               
RDIS10   B     EXITY                                                            
         EJECT                                                                  
***********************************************************************         
* DIPSLAY ROUTINES                                                    *         
*                                                                     *         
* NTRY: R4=A(OUTPUT)                                                  *         
*       R3=A(COLUMN TABLE ENTRY)                                      *         
***********************************************************************         
         SPACE 1                                                                
DISPLAY  NTR1  ,                                                                
         XR    RF,RF                                                            
         IC    RF,CLMRTN                                                        
         SLL   RF,2                                                             
         B     *(RF)                                                            
*                                                                               
DISRTNS  DS    0XL4                                                             
         B     DISACC                                                           
         B     DISNAM                                                           
         B     DISFLT                                                           
         B     DISADR                                                           
         B     DISDSC                                                           
         EJECT                                                                  
***********************************************************************         
* DISPLAY ACCOUNT CODE                                                *         
***********************************************************************         
         SPACE 1                                                                
DISACC   XR    RE,RE                                                            
         IC    RE,SVPRELEN                                                      
         LA    RE,ACTKACT(RE)                                                   
         MVC   0(L'ACTKACT,R4),0(RE)                                            
         B     EXIT                                                             
         SPACE 1                                                                
***********************************************************************         
* DISPLAY NAME                                                        *         
***********************************************************************         
         SPACE 1                                                                
DISNAM   TM    SVCPYSTC,CPYSMEDN                                                
         BZ    DISNAM02                                                         
         GOTO1 GETEL,DMCB,('SNMELQ',ACTRECD)                                    
         BNE   DISNAM08                                                         
         SH    RF,=Y(SNMLN1Q+1)                                                 
         BM    EXIT                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),SNMNAME-SNMELD(R1)                                       
         B     EXIT                                                             
DISNAM02 TM    SVCPYSTC,CPYSALTN                                                
         BZ    DISNAM08                                                         
         GOTO1 GETEL,DMCB,('XNMELQ',ACTRECD)                                    
         BNE   DISNAM08                                                         
         SH    RF,=Y(XNMLN1Q+2)                                                 
         BM    EXIT                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),XNMSUBN-XNMELD(R1)                                       
         B     EXIT                                                             
DISNAM08 GOTO1 GETEL,DMCB,('NAMELQ',ACTRECD)                                    
         BNE   EXIT                                                             
         SH    RF,=Y(NAMLN1Q+1)                                                 
         BM    EXIT                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),NAMEREC-NAMELD(R1)                                       
         B     EXIT                                                             
         SPACE 1                                                                
***********************************************************************         
* DISPLAY ACCOUNT FILTERS/STATUS                                      *         
***********************************************************************         
         SPACE 1                                                                
         USING CFLTSD,R4                                                        
DISFLT   MVC   CFLTAF,SVCURLVL+(LVLAF-LVLD)                                     
         GOTO1 GETEL,DMCB,('RSTELQ',ACTRECD)                                    
         BNE   EXIT                                                             
         USING RSTELD,R1                                                        
         MVC   CFLTA,RSTCOSTG                                                   
         TM    RSTSTAT1,RSTSGPEI                                                
         BZ    *+8                                                              
         MVI   CFLTS,C'S'                                                       
         TM    RSTSTAT1,RSTSEADD                                                
         BZ    *+8                                                              
         MVI   CFLTD,C'D'                                                       
         TM    ACTRSTAT,ACTSCLOS                                                
         BZ    *+8                                                              
         MVI   CFLTC,C'C'                                                       
         TM    ACTRSTAT,ACTSLOCK                                                
         BZ    *+8                                                              
         MVI   CFLTL,C'L'                                                       
*                                                                               
         MVC   CFLTG,AC@YES                                                     
         TM    ACTRSTAT,ACTSDRFT                                                
         BZ    *+8                                                              
         MVI   CFLTG,C'N'                                                       
         DROP  R1                                                               
*                                                                               
         LA    R0,CFLTSL                                                        
         LA    RF,CFLTSD                                                        
DISFLT02 CLI   0(RF),C' '                                                       
         BH    *+8                                                              
         MVI   0(RF),C'.'                                                       
         LA    RF,1(RF)                                                         
         BCT   R0,DISFLT02                                                      
         B     EXIT                                                             
         DROP R4                                                                
         SPACE 1                                                                
CFLTSD   DSECT                     * OUTPUT FOR FILTERS *                       
CFLTAF   DS    CL5                 ACCOUNT FILTERS 1-5                          
CFLTA    DS    CL1                 COSTING GROUP                                
CFLTS    DS    CL1                 GENERATE PERSON EXPENSE ITEM                 
CFLTD    DS    CL1                 EXPENSE A/C DEMANDS DEPT.                    
CFLTC    DS    CL1                 ACCOUNT IS CLOSED                            
CFLTL    DS    CL1                 ACCOUNT IS LOCKED                            
CFLTG    DS    CL1                 ACCOUNT IS APPROVED                          
CFLTSL   EQU   *-CFLTSD                                                         
ACSRCHAC CSECT                                                                  
         SPACE 1                                                                
***********************************************************************         
* DISPLAY ADDRESS                                                     *         
***********************************************************************         
         SPACE 1                                                                
DISADR   TM    SVINDS,SVIADR                                                    
         BZ    EXIT                                                             
         GOTO1 GETEL,DMCB,('ADRELQ',ACTRECD)                                    
         BNE   EXIT                                                             
         MVC   L'ACTKACT+1(L'ADRADD1,R4),ADRADD1-ADRELD(R1)                     
         B     EXIT                                                             
         SPACE 1                                                                
***********************************************************************         
* DISPLAY DISCOUNT RATE                                               *         
***********************************************************************         
         SPACE 1                                                                
DISDSC   GOTO1 GETEL,DMCB,('RATEDSCQ',ACTRECD)                                  
         BNE   EXIT                                                             
         XC    FULL,FULL                                                        
         MVC   FULL+2(2),RATRATE-RATELD(R1)                                     
         CURED (B4,FULL),(CLMDSCLQ,(R4)),2                                      
         B     EXIT                                                             
CLMDSCLQ EQU   8                   LENGTH OF COLUMN                             
         SPACE 2                                                                
         DROP  R2,R3                                                            
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO GET A(NEXT COLUMN TABLE ENTRY)                           *         
*                                                                     *         
* NTRY: R1=A(COLUMN CODE LIST)                                        *         
*       R5=CURRENT DISPLACEMENT IN LIST LINE                          *         
* EXIT: IF END OF LIST, CC=NOT EQUAL                                  *         
*       R1=A(NEXT COLUMN CODE IN LIST)                                *         
*       R3=A(COLUMN TABLE ENTRY), RF=L(COLUMN TABLE ENTRY)            *         
*       R4=OLD LINE DISPLACEMENT                                      *         
*       R5=NEW LINE DISPLACEMENT                                      *         
***********************************************************************         
         SPACE 1                                                                
NXTCLM   CLI   0(R1),EOT                                                        
         BE    NXTCLMN                                                          
*                                                                               
         LA    R3,CLMTAB           LOCATE COLUMN TABLE ENTRY                    
         USING CLMTABD,R3                                                       
         XR    RF,RF                                                            
NCLM02   IC    RF,CLMLEN                                                        
         CLC   CLMRTN,0(R1)                                                     
         BE    NCLM10                                                           
NCLM08   BXH   R3,RF,NCLM02                                                     
*                                                                               
NCLM10   LR    R4,R5               R4=OLD DISPLACEMENT                          
         IC    R5,CLMWDTH                                                       
         AR    R5,R4               R5=NEW DISPLACEMENT                          
*                                                                               
         LA    R1,1(R1)            BUMP R1 TO NEXT CODE IN LIST                 
*                                                                               
NXTCLMY  CR    RB,RB                                                            
         BR    RE                                                               
*                                                                               
NXTCLMN  LTR   RB,RB                                                            
         BR    RE                                                               
         SPACE 1                                                                
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO GO TO NEXT ENTRY OF THE FILTER TABLE                     *         
*                                                                     *         
* NTRY: R3=A(CURRENT ENTRY) OR 0 TO START                             *         
* EXIT: R3=A(NEXT ENTRY)                                              *         
*       CC=LOW IF END-OF TABLE REACHED                                *         
***********************************************************************         
         SPACE 1                                                                
         USING FLTTABD,R3                                                       
NXTFLT   LR    R0,RE                                                            
         LTR   R3,R3                                                            
         BNZ   NFLT08                                                           
         LA    R3,FLTTAB                                                        
*                                                                               
NFLT02   CLI   FLTTABD,EOT         TEST E-O-T                                   
         BE    NXTFLTN                                                          
         CLI   FLTFOR,FLTFALL      MATCH ON UNIT/LEDGER                         
         BE    NFLT06                                                           
         CLI   FLTFOR,FLTFNOT                                                   
         BE    NFLT04                                                           
         CLC   FLTFUL,SDSRUL                                                    
         BE    NFLT06                                                           
         B     NFLT08                                                           
NFLT04   CLC   FLTFUL,SDSRUL                                                    
         BE    NFLT08                                                           
*                                                                               
NFLT06   GOTO1 TESTCTRY,FLTCTRY    MATCH ON COUNTRY                             
         BE    NXTFLTY                                                          
*                                                                               
NFLT08   LA    R3,FLTTABL(R3)                                                   
         B     NFLT02                                                           
*                                                                               
NXTFLTN  LTR   RB,RB                                                            
         B     *+6                                                              
NXTFLTY  CR    RB,RB                                                            
         LR    RE,R0                                                            
         BR    RE                                                               
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO PARTITION INPUT                                          *         
*                                                                     *         
* NTRY: P1=(L'INPUT,A(INPUT))                                         *         
*                                                                     *         
***********************************************************************         
         SPACE 1                                                                
PARTINP  NTR1  ,                                                                
         MVI   PARTALN,0                                                        
         XC    PARTA,PARTA                                                      
         MVI   PARTBLN,0                                                        
         MVI   PARTB,FF                                                         
         MVC   PARTB+1(L'PARTB-1),PARTB                                         
         XR    R2,R2                                                            
         ICM   R2,7,1(R1)          R2=A(INPUT)                                  
*                                                                               
         XR    RE,RE                                                            
         IC    RE,0(R1)            RE=L(INPUT)                                  
         LR    RF,RE                                                            
         LR    R1,R2               R1=A(INPUT)                                  
         CLI   0(R1),C'-'                                                       
         BE    *+12                                                             
         LA    R1,1(R1)                                                         
         BCT   RE,*-12                                                          
*                                                                               
         SR    RF,RE               COPY PART A                                  
         BZ    PINP02                                                           
         STC   RF,PARTALN                                                       
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   PARTA(0),0(R2)                                                   
         LTR   RE,RE               TEST PARTITION CHARACTER FOUND               
         BNZ   PINP02                                                           
         MVC   PARTBLN,PARTALN     NO SO PARTB=PARTA                            
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   PARTB(0),0(R2)                                                   
         B     PARTINPX                                                         
*                                                                               
PINP02   BCT   RE,*+8                                                           
         B     PARTINPX                                                         
         STC   RE,PARTBLN                                                       
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   PARTB(0),1(R1)                                                   
*                                                                               
PARTINPX B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO COMPARE INPUT WITH WORD                                  *         
*                                                                     *         
* NTRY: P1=(L'INPUT, A(INPUT))                                        *         
*       P2=(L'WORD, A(WORD))                                          *         
***********************************************************************         
         SPACE 1                                                                
CMPWORD  STM   RE,R3,12(RD)                                                     
         LM    R2,R3,0(R1)                                                      
         IC    RF,0(R1)                                                         
         CLC   0(1,R1),4(R1)                                                    
         BNH   *+8                                                              
         IC    RF,4(R1)                                                         
         BCTR  RF,0                                                             
         EX    RF,CMPWORDC                                                      
CMPWORDX LM    RE,R3,12(RD)                                                     
         BR    RE                                                               
*                                                                               
CMPWORDC CLC   0(0,R2),0(R3)                                                    
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO SET A BIT OF A BIT TABLE                                 *         
*                                                                     *         
* NTRY: P1=(BIT CODE, A(BIT TABLE))                                   *         
***********************************************************************         
         SPACE 1                                                                
SETBIT   XR    RF,RF                                                            
         IC    RF,0(R1)                                                         
         BCTR  RF,0                                                             
         L     R0,0(R1)                                                         
         LA    R1,X'07'                                                         
         NR    R1,RF                                                            
         LA    R1,MASKS(R1)        R1=A(MASK)                                   
         SRL   RF,3                                                             
         AR    RF,R0               RF=A(BYTE OF BIT)                            
         OC    0(1,RF),0(R1)                                                    
         BR    RE                                                               
         SPACE 3                                                                
***********************************************************************         
* ROUTINE TO TEST A BIT OF A BIT TABLE                                *         
*                                                                     *         
* NTRY: P1=(BIT CODE, A(BIT TABLE))                                   *         
***********************************************************************         
         SPACE 1                                                                
TESTBIT  XR    RF,RF                                                            
         IC    RF,0(R1)                                                         
         BCTR  RF,0                                                             
         L     R0,0(R1)                                                         
         LA    R1,X'07'                                                         
         NR    R1,RF                                                            
         IC    R1,MASKS(R1)        R1=MASK                                      
         SRL   RF,3                                                             
         AR    RF,R0               RF=A(BYTE OF BIT)                            
         EX    R1,TESTBITM         EX A TM                                      
         BR    RE                                                               
         SPACE 1                                                                
TESTBITM TM    0(RF),0                                                          
         SPACE 3                                                                
MASKS    DC    X'8040201008040201'                                              
         EJECT                                                                  
***********************************************************************         
* TEST COUNTRY FILTER                                                 *         
*                                                                     *         
* NTRY: R1=A(COUNTRY)                                                 *         
***********************************************************************         
         SPACE 1                                                                
TESTCTRY CLI   0(R1),ALL           TEST ALL COUNTRIES                           
         BE    TCTRYYES                                                         
         CLC   SBCTRY,0(R1)        MATCH ON COUNTRY                             
         BE    TCTRYYES                                                         
         TM    0(R1),NOT                                                        
         BZ    TCTRYNO                                                          
         MVC   BYTE,0(R1)          MATCH ON NOT A COUNTRY                       
         XI    BYTE,NOT                                                         
         CLC   BYTE,SBCTRY                                                      
         BE    TCTRYNO                                                          
*                                                                               
TCTRYYES CR    RE,RE                                                            
         BR    RE                                                               
TCTRYNO  LTR   RE,RE                                                            
         BR    RE                                                               
         SPACE 1                                                                
***********************************************************************         
* CALL DICTATE                                                        *         
***********************************************************************         
         SPACE 1                                                                
DICTATE  NTR1  ,                                                                
         MVI   2(R1),ACCSYSQ       SET ACCOUNT SYSTEM                           
         GOTO1 VDICTATE                                                         
         XR    RE,RE                                                            
         ICM   RE,7,EFFS                                                        
         GOTO1 VSWITCH,(R1),(RE)   TEST DICTATE SWITCHED                        
         L     RE,0(R1)                                                         
         CLC   SVSYS,TCBSYS-TCBD(RE)                                            
         BE    EXIT                SWITCH BACK IF IT DID                        
         GOTO1 (RF),(R1),(SVSYS,0),0                                            
         BE    EXIT                                                             
         DC    H'0'                SWITCH FAILED                                
         EJECT                                                                  
***********************************************************************         
* GET ELEMENT ROUTINE                                                 *         
*                                                                     *         
* NTRY: P1=(ELEMENT CODE, A(RECORD))                                  *         
* EXIT: R1=A(ELEMENT), RF=L(ELEMENT)                                  *         
***********************************************************************         
         SPACE 1                                                                
GETEL    ST    RE,12(RD)                                                        
         LR    RE,R1                                                            
*                                                                               
         L     R1,0(RE)                                                         
         LA    R1,ACCRFST-ACCRECD(R1)                                           
         LA    R0,100                                                           
         XR    RF,RF                                                            
*                                                                               
GETEL02  CLI   0(R1),0                                                          
         BE    GETELNO                                                          
         IC    RF,1(R1)                                                         
         CLC   0(1,R1),0(RE)                                                    
         BE    GETELYES                                                         
         AR    R1,RF                                                            
         BCT   R0,GETEL02                                                       
*                                                                               
GETELNO  LTR   RB,RB                                                            
         B     *+6                                                              
GETELYES CR    RB,RB                                                            
         L     RE,12(RD)                                                        
         BR    RE                                                               
         SPACE 1                                                                
***********************************************************************         
* ROUTINE TO READ LEDGER RECORD INTO IOREC                            *         
***********************************************************************         
         SPACE 1                                                                
READLDG  NTR1  ,                                                                
         LA    R2,IOKEY                                                         
         USING LDGRECD,R2                                                       
         MVC   LDGKEY,SPACES                                                    
         MVC   LDGKCPY,SBAGYB                                                   
         MVC   LDGKUNT(L'SDSRUL),SDSRUL                                         
         GOTO1 VDATAMGR,DMCB,DMREAD,ACCMST,LDGKEY,IOREC,WORK                    
         BE    EXIT                                                             
         MVI   SBRETURN,SBETXTQ    RETURN 'LEDGER NOT ON FILE'                  
         LA    RF,SBTXTBLK                                                      
         USING GETTXTD,RF                                                       
         MVI   GTMSYS,ACCSYSQ                                                   
         MVC   GTMSGNO,=AL2(AE$LDGNF)                                           
         B     EXITN                                                            
         DROP  R2,RF                                                            
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO INITIALIZE OFFICE SECURITY FOR USER                      *         
***********************************************************************         
         SPACE 1                                                                
SETOFF   NTR1  ,                                                                
         LA    R2,IOKEY            READ THE COMPANY RECORD                      
         USING CPYRECD,R2                                                       
         MVC   CPYKEY,SPACES                                                    
         MVC   CPYKCPY,SBAGYB                                                   
         GOTO1 VDATAMGR,DMCB,DMREAD,ACCMST,CPYKEY,IOREC,WORK                    
         BE    *+6                                                              
         DC    H'0'                                                             
         DROP  R2                                                               
         XR    RF,RF                                                            
         LA    R3,IOREC+(CPYRFST-CPYRECD)                                       
         USING CPYELD,R3           R3=A(COMPANY ELEMENT)                        
         CLI   CPYEL,CPYELQ                                                     
         BE    *+12                                                             
         IC    RF,CPYLN                                                         
         BXH   R3,RF,*-12                                                       
*                                                                               
         MVC   DMCB,EFFS                                                        
         GOTO1 VGETFACT,DMCB,(X'80',0),F#UTLD  REPLACE UTLD BY FA               
         L     R4,0(R1)                                                         
         USING F@UTLD,R4           R4=A(UTL)                                    
         MVC   SVSECY+1(1),F@TAUTH+1                                            
         MVC   SVSYS,F@TSYS        EXTRACT                                      
*                                                                               
         MVI   OFFACTRL,OFFACNEW                                                
         MVC   OFFACOMF,SBCOMFAC                                                
         MVC   OFFAALPH,SBAGY                                                   
         MVC   OFFACPY,SBAGYB                                                   
         MVC   OFFACST1,CPYSTAT1                                                
         MVC   OFFACST2,CPYSTAT2                                                
         MVC   OFFACST3,CPYSTAT3                                                
         MVC   OFFACST4,CPYSTAT4                                                
         MVC   OFFACST5,CPYSTAT5                                                
         MVC   OFFACST6,CPYSTAT6                                                
         MVC   OFFACST7,CPYSTAT7                                                
         MVC   OFFACST8,CPYSTAT8                                                
         MVC   SVCPYSTC,CPYSTATC                                                
         MVC   OFFALIMA,F@TACCS                                                 
         MVC   OFFAAUTH,F@TAUTH                                                 
         MVI   OFFAINDS,OFFAIOFF   OFFICE SECURITY CHECK                        
         MVI   OFFAACT,OFFAINI     INITIALIZE                                   
         GOTO1 VCALLOV,DMCB,0,X'D9000A62'                                       
         MVC   VOFFAL,0(R1)                                                     
         GOTO1 VOFFAL,OFFALD                                                    
         BE    EXIT                                                             
         DC    H'0'                                                             
         DROP  R3,R4                                                            
         SPACE 1                                                                
***********************************************************************         
* ROUTINE TO TEST OFFICE SECURITY                                     *         
*                                                                     *         
* NTRY: R1 = A(OFFICE CODE)                                           *         
***********************************************************************         
         SPACE 1                                                                
TSTOFF   NTR1  ,                                                                
         MVI   OFFAACT,OFFAPST                                                  
         MVC   OFFAOPOS,SVOPOS                                                  
         MVC   OFFAOFFC,0(R1)                                                   
         GOTO1 VOFFAL,OFFALD                                                    
         BE    EXITY                                                            
         MVI   SBRETURN,SBRSECQ                                                 
         B     EXITN                                                            
         EJECT                                                                  
FF       EQU   X'FF'                                                            
EOT      EQU   X'00'               END-OF-TABLE                                 
ALL      EQU   X'00'               ALL                                          
NOT      EQU   X'80'               NOT                                          
ACCSYSQ  EQU   6                   ACCOUNT SYSTEM                               
         SPACE 1                                                                
         LTORG                                                                  
         SPACE 1                                                                
SPACES   DC    CL80' '                                                          
PZERO    DC    PL1'0'                                                           
EFFS     DC    12X'FF'                                                          
         SPACE 1                                                                
DMREAD   DC    C'DMREAD  '                                                      
DMRSEQ   DC    C'DMRSEQ  '                                                      
DMRDHI   DC    C'DMRDHI  '                                                      
ACCMST   DC    C'ACCMST  '                                                      
ACCDIR   DC    C'ACCDIR  '                                                      
TITLEDIV DC    C' / '                                                           
         SPACE 1                                                                
DI       DS    0X                  ** DICTIONARY IN **                          
         DCDDL AC#FLT1,2                                                        
         DCDDL AC#FLT2,2                                                        
         DCDDL AC#FLT3,2                                                        
         DCDDL AC#FLT4,2                                                        
         DCDDL AC#FLT5,2                                                        
         DCDDL AC#NAME,8                                                        
         DCDDL AC#BAL,8                                                         
         DCDDL AC#LCKED,8                                                       
         DCDDL AC#UNAPR,8                                                       
         DCDDL AC#CLSD,8                                                        
         DCDDL AC#LEVEL,8                                                       
         DCDDL AC#ADR,8                                                         
         DCDDL AC#OUT,4                                                         
         DCDDL AC#YES,4                                                         
         DCDDL AC#NO,4                                                          
         DCDDL AC#ONLY,8                                                        
         DCDDL AC#HELP,4                                                        
         DCDDL AC#CODE1,8                                                       
DIX      DC    AL1(0)                                                           
         EJECT                                                                  
***********************************************************************         
* COLUMN TABLE                                                        *         
***********************************************************************         
         SPACE 1                                                                
CLMTABD  DSECT                                                                  
CLMLEN   DS    XL1                 LENGTH OF TABLE ENTRY                        
CLMRTN   DS    XL1                 DISPLAY ROUTINE NUMBER                       
CLMINDS1 DS    XL1                 * INDICATORS *                               
CLMIKEY  EQU   X'80'               COLUMN IS KEY                                
CLMINAME EQU   X'40'               COLUMN IS NAME                               
CLMWDTH  DS    XL1                 COLUMN WIDTH                                 
CLMNHD   DS    XL1                 NO. OF HEADINGS                              
CLMHEADS DS    0X                  LIST OF COLUMN HEADINGS                      
CLMHWDTH DS    XL1                 WIDTH OF HEADING                             
CLMHEAD  DS    0X                  HEADING                                      
         SPACE 1                                                                
ACSRCHAC CSECT                                                                  
CLMTAB   DS    0X                                                               
*                                                                               
CACC     DC    AL1(CACCX-CACC,CLMACCQ,CLMIKEY)                                  
         DC    AL1(L'ACTKACT,2)                                                 
         DC    AL1(4)                                                           
         DCDD  AC#CODE,L'ACTKACT,PAD=NONE                                       
         DC    AL1(4)                                                           
         DCDD  AC#CODE,L'ACTKACT,LU,PAD=NONE                                    
CACCX    DS    0X                                                               
*                                                                               
CNAM     DC    AL1(CNAMX-CNAM,CLMNAMQ,CLMINAME)                                 
         DC    AL1(L'NAMEREC,2)                                                 
         DC    AL1(4)                                                           
         DCDD  AC#NAME,L'NAMEREC,PAD=NONE                                       
         DC    AL1(4)                                                           
         DCDD  AC#NAME,L'NAMEREC,LU,PAD=NONE                                    
CNAMX    DS    0X                                                               
*                                                                               
CFLT     DC    AL1(CFLTX-CFLT,CLMFLTQ,0)                                        
         DC    AL1(CFLTSL,2)                                                    
         DC    AL1(4)                                                           
         DCDD  AC#FSLN1,CFLTSL,PAD=NONE                                         
         DC    AL1(4)                                                           
         DCDD  AC#FSLN2,CFLTSL,PAD=NONE                                         
CFLTX    DS    0X                                                               
*                                                                               
CADR     DC    AL1(CADRX-CADR,CLMADRQ,0)                                        
         DC    AL1(L'ADRADD1+L'ACTKACT+1,0)                                     
CADRX    DS    0X                                                               
*                                                                               
CDSC     DC    AL1(CDSCX-CDSC,CLMDSCQ,0)                                        
         DC    AL1(CLMDSCLQ,2)                                                  
         DC    AL1(4)                                                           
         DCDD  AC#DISS,CLMDSCLQ,R,PAD=NONE                                      
         DC    AL1(4)                                                           
         DCDD  AC#RATE,CLMDSCLQ,R,PAD=NONE                                      
CDSCX    DS    0X                                                               
*                                                                               
CLMTABX DC     AL1(EOT)                                                         
         EJECT                                                                  
***********************************************************************         
* FILTER TABLE                                                        *         
***********************************************************************         
         SPACE 1                                                                
FLTTABD  DSECT                                                                  
FLTCODE  DS    XL1                 A NUMBER (1-32)                              
FLTFOR   DS    XL1                 UNIT/LEDGER FILTER FOR FILTER                
FLTFALL  EQU   C'A'                FILTER FOR ALL UNIT/LEDGERS                  
FLTFONE  EQU   C' '                FILTER JUST FOR FLTFUL                       
FLTFNOT  EQU   C'*'                FILTER FOR ALL BUT FLTFUL                    
FLTFUL   DS    CL2                 UNIT/LEDGER CODE                             
FLTCTRY  DS    XL1                 COUNTRY                                      
FLTINDS1 DS    XL1                 INDICATORS BYTE 1                            
FLTINOT  EQU   X'80'               'NOT EQUAL' IS VALID                         
FLTIDUP  EQU   X'40'               FILTER CAN BE DUPLICATED                     
FLTINDS2 DS    XL1                 INDICATORS BYTE 2                            
FLTLNAM  DS    AL2                 DISPLACEMENT TO LHS NAME                     
FLTLMINQ EQU   2                   MINIMUM INPUT LENGTH FOR LHS                 
FLTLMAX  DS    XL1                 MAXIMUM INPUT LENGTH FOR LHS                 
FLTRNAM  DS    AL2                 DISPLACEMENT TO RHS (IF KEYWORD)             
FLTRMIN  DS    XL1                 MINIMUM INPUT LENGTH FOR RHS                 
FLTRMAX  DS    XL1                 MAXIMUM INPUT LENGTH FOR RHS                 
FLTVAL   DS    AL2                 DISP. TO VALIDATION ROUTINE                  
FLTDEF   DS    AL2                 DISP. TO SET DEFAULT VALUE ROUTINE           
FLTREC   DS    AL2                 DISP. TO FILTER RECORD ROUTINE               
FLTTABL  EQU   *-FLTTABD                                                        
         SPACE 1                                                                
ACSRCHAC CSECT                                                                  
*                                                                               
FLTTAB   DS    0H                                                               
*                                                                               
         DC    AL1(((*-FLTTAB)/FLTTABL)+1)                                      
         DC    C'ALL',AL1(ALL,FLTIDUP,0)                                        
         DC    AL2(AC@NAME-DO),AL1(L'AC@NAME)                                   
         DC    AL2(0),AL1(1,SBNALENQ)                                           
         DC    AL2(VFNAME-ACSRCHAC,0,0)                                         
*                                                                               
         DC    AL1(((*-FLTTAB)/FLTTABL)+1)                                      
         DC    C'ALL',AL1(ALL,FLTINOT,0)                                        
         DC    AL2(AC@FLT1-DO),AL1(L'AC@FLT1)                                   
         DC    AL2(0),AL1(1,1)                                                  
         DC    AL2(VFAF1-ACSRCHAC,0,RFAF1-ACSRCHAC)                             
*                                                                               
         DC    AL1(((*-FLTTAB)/FLTTABL)+1)                                      
         DC    C'ALL',AL1(ALL,FLTINOT,0)                                        
         DC    AL2(AC@FLT2-DO),AL1(L'AC@FLT2)                                   
         DC    AL2(0),AL1(1,1)                                                  
         DC    AL2(VFAF2-ACSRCHAC,0,RFAF2-ACSRCHAC)                             
*                                                                               
         DC    AL1(((*-FLTTAB)/FLTTABL)+1)                                      
         DC    C'ALL',AL1(ALL,FLTINOT,0)                                        
         DC    AL2(AC@FLT3-DO),AL1(L'AC@FLT3)                                   
         DC    AL2(0),AL1(1,1)                                                  
         DC    AL2(VFAF3-ACSRCHAC,0,RFAF3-ACSRCHAC)                             
*                                                                               
         DC    AL1(((*-FLTTAB)/FLTTABL)+1)                                      
         DC    C'ALL',AL1(ALL,FLTINOT,0)                                        
         DC    AL2(AC@FLT4-DO),AL1(L'AC@FLT4)                                   
         DC    AL2(0),AL1(1,1)                                                  
         DC    AL2(VFAF4-ACSRCHAC,0,RFAF4-ACSRCHAC)                             
*                                                                               
         DC    AL1(((*-FLTTAB)/FLTTABL)+1)                                      
         DC    C'ALL',AL1(ALL,FLTINOT,0)                                        
         DC    AL2(AC@FLT5-DO),AL1(L'AC@FLT5)                                   
         DC    AL2(0),AL1(1,1)                                                  
         DC    AL2(VFAF5-ACSRCHAC,0,RFAF5-ACSRCHAC)                             
*                                                                               
         DC    AL1(((*-FLTTAB)/FLTTABL)+1)                                      
         DC    C'ALL',AL1(ALL,FLTINOT,0)                                        
         DC    AL2(AC@BAL-DO),AL1(L'AC@BAL)                                     
         DC    AL2(AC@OUT-DO),AL1(1,L'AC@OUT)                                   
         DC    AL2(VFRNAM-ACSRCHAC,0,RFBOUT-ACSRCHAC)                           
*                                                                               
         DC    AL1(((*-FLTTAB)/FLTTABL)+1)                                      
         DC    C'ALL',AL1(ALL,0,0)                                              
         DC    AL2(AC@LCKED-DO),AL1(L'AC@LCKED)                                 
         DC    AL2(0),AL1(1,5)                                                  
         DC    AL2(VFYON-ACSRCHAC,0,RFLOCKED-ACSRCHAC)                          
*                                                                               
         DC    AL1(((*-FLTTAB)/FLTTABL)+1)                                      
         DC    C'ALL',AL1(ALL,0,0)                                              
         DC    AL2(AC@UNAPR-DO),AL1(L'AC@UNAPR)                                 
         DC    AL2(0),AL1(1,5)                                                  
         DC    AL2(VFYON-ACSRCHAC,0,RFUNAPPR-ACSRCHAC)                          
*                                                                               
         DC    AL1(((*-FLTTAB)/FLTTABL)+1)                                      
         DC    C'ALL',AL1(ALL,0,0)                                              
         DC    AL2(AC@CLSD-DO),AL1(L'AC@CLSD)                                   
         DC    AL2(0),AL1(1,5)                                                  
         DC    AL2(VFYON-ACSRCHAC,DFCLOSED-ACSRCHAC,RFCLOSED-ACSRCHAC)          
*                                                                               
         DC    AL1(((*-FLTTAB)/FLTTABL)+1)                                      
         DC    C'ALL',AL1(ALL,0,0)                                              
         DC    AL2(AC@LEVEL-DO),AL1(L'AC@LEVEL)                                 
         DC    AL2(0),AL1(1,3)                                                  
         DC    AL2(VFLEV-ACSRCHAC,DFLEV-ACSRCHAC,0)                             
*                                                                               
         DC    AL1(((*-FLTTAB)/FLTTABL)+1)                                      
         DC    C'ALL',AL1(ALL,0,0)                                              
         DC    AL2(AC@CODE1-DO),AL1(L'AC@CODE1)                                 
         DC    AL2(0),AL1(1,L'ACTKACT*2+1)                                      
         DC    AL2(VFCODE-ACSRCHAC,DFCODE-ACSRCHAC,0)                           
*                                                                               
         DC    AL1(((*-FLTTAB)/FLTTABL)+1)                                      
         DC    C'ALL',AL1(ALL,FLTINOT,0)                                        
         DC    AL2(AC@ADR-DO),AL1(L'AC@ADR)                                     
         DC    AL2(0),AL1(1,L'AC@YES)                                           
         DC    AL2(VFADR-ACSRCHAC,DFADR-ACSRCHAC,0)                             
*                                                                               
FLTTABX  DC    AL1(EOT)                                                         
         EJECT                                                                  
***********************************************************************         
* LOCAL WORKING STORAGE                                               *         
***********************************************************************         
         SPACE 1                                                                
WORKD    DSECT                                                                  
DUB      DS    D                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
DMCB     DS    6F                                                               
         SPACE 1                                                                
CUREDIT  DS    A                                                                
VDATAMGR DS    A                                                                
VCALLOV  DS    A                                                                
VDICTATE DS    A                                                                
VGETFACT DS    V                                                                
VSCANNER DS    A                                                                
VSWITCH  DS    A                                                                
         SPACE 1                                                                
APARMS   DS    A                   ADDRESS CALLERS PARAMETER LIST               
WORK     DS    XL256                                                            
BYTE     DS    X                                                                
BTNOFILT DS    XL(L'FTUSED)        NO FILTERS                                   
ACLVLMIN DS    XL1                 MINIMUM ACCOUNT LEVEL                        
ACLVLMAX DS    XL1                 MAXIMUM ACCOUNT LEVEL                        
*                                                                               
PARTALN  DS    XL1                                                              
PARTA    DS    XL32                                                             
PARTBLN  DS    XL1                                                              
PARTB    DS    XL32                                                             
*                                                                               
SDSRCH   DS    XL64                COPY OF CURRENT SDSCRHD ENTRY                
*                                                                               
DO       DS    0X                  ** DICTIONARY OUT **                         
         DSDDL PRINT=YES                                                        
DOX      DS    0X                                                               
*                                                                               
SCANN    DS    XL1                 NO. OF ENTRIES IN SCAN BLOCK                 
INDX     DS    XL1                 CURRENT FIELD INDEX NO.                      
SCANBLKN EQU   (L'IOREC)/SCANBLKL  MAX. NO OF ENTRIES IN SCAN BLOCK             
*                                                                               
KEYMIN   DS    XL42                MINIMUM KEY FOR BIT SET                      
KEYMAX   DS    XL42                MAXIMUM KEY FOR BIT SET                      
*                                                                               
IOKEY    DS    XL64                                                             
IOREC    DS    XL2048              IO AREA                                      
*                                                                               
WORKL    EQU   *-WORKD                                                          
         EJECT                                                                  
* GESRCHBLKD                                                                    
         PRINT OFF                                                              
       ++INCLUDE GESRCHBLKD                                                     
         PRINT ON                                                               
         SPACE 1                                                                
***********************************************************************         
* FILTERS/SAVED STORAGE                                               *         
***********************************************************************         
         SPACE 1                                                                
FTVALS   DS    0XL(FTVALSL)        ** FILTERS **                                
FTUSED   DS    XL4                 BIT TABLE OF FILTERS USED                    
FTNOT    DS    XL4                 BIT TABLE OF FILTERS WITH 'NOT ='            
FTAF1    DS    CL1                 ACCOUNT FILTER 1                             
FTAF2    DS    CL1                 ACCOUNT FILTER 2                             
FTAF3    DS    CL1                 ACCOUNT FILTER 3                             
FTAF4    DS    CL1                 ACCOUNT FILTER 4                             
FTAF5    DS    CL1                 ACCOUNT FILTER 5                             
FTLVLMIN DS    XL1                 MINIMUM ACCOUNT LEVEL                        
FTLVLMAX DS    XL1                 MAXIMUM ACCOUNT LEVEL                        
FTCODMIN DS    CL(L'ACTKACT)       START CODE                                   
FTCODMAX DS    CL(L'ACTKACT)       END CODE                                     
FTVALSL  EQU   *-FTUSED                                                         
*                                                                               
         ORG   SBASAVE+L'SBASAVE                                                
*                                                                               
         ORG   SBOTHER                                                          
SBACTPRE DS    CL(L'ACTKACT-1)     ACCOUNT CODE PREFIX                          
SBLVLMIN DS    XL1                 MINIMUM ACCOUNT LEVEL ALLOWED                
SBLVLMAX DS    XL1                 MAXIMUM ACCOUNT LEVEL ALLOWED                
SBLENPRE DS    XL1                 SBACTPRE LENGTH (IF SET BY CALLER)           
         ORG   SBOTHER+L'SBOTHER                                                
         SPACE 1                                                                
LVLD     DSECT                     * ACCOUNT LEVEL DSECT *                      
LVL#     DS    XL1                 LEVEL NUMBER (1-4) OR 0 FOR LEDGER           
LVL#MAX  EQU   4                   MAXIMUM ACCOUNT LEVEL                        
LVLLEN   DS    XL1                 MAX. LENGTH OF ACCOUNT AT LEVEL              
LVLAF    DS    0CL5                ACCOUNT FILTERS 1-5                          
LVLAF1   DS    CL1                                                              
LVLAF2   DS    CL1                                                              
LVLAF3   DS    CL1                                                              
LVLAF4   DS    CL1                                                              
LVLAF5   DS    CL1                                                              
LVLSECY  DS    XL2                 SECURITY LEVEL                               
LVLGAOFF DS    CL2                 PRODUCTION PROFILE OFFICE (PPRGAOFF)         
LVLL     EQU   *-LVLD                                                           
         EJECT                                                                  
***********************************************************************         
* TWAD                                                                *         
***********************************************************************         
         SPACE 1                                                                
* FATWA                                                                         
         PRINT OFF                                                              
       ++INCLUDE FATWA                                                          
         PRINT ON                                                               
         DS    0X                                                               
* SRSRCFFD                                                                      
         PRINT OFF                                                              
       ++INCLUDE SRSRCFFD                                                       
         PRINT ON                                                               
         DS    0X                                                               
* FAREPBLK                                                                      
         PRINT OFF                                                              
       ++INCLUDE FAREPBLK                                                       
         PRINT ON                                                               
         SPACE 1                                                                
         ORG   TWAD+X'3B00'        SAVE AREA                                    
SVBLK    DS    XL(SVBLKL)                                                       
         ORG   TWAD+X'4000'                                                     
         SPACE 1                                                                
***********************************************************************         
* TWA SAVE AREA                                                       *         
***********************************************************************         
         SPACE 1                                                                
SVBLKD   DSECT                                                                  
SVINDS   DS    XL1                 INDICATORS BYTE                              
SVIALONE EQU   X'80'               STAND-ALONE SEARCH ($SEARCH ENTERED)         
SVIREP   EQU   X'40'               OUTPUT IS FOR A REPORT                       
SVIADR   EQU   X'20'               DISPLAY ADDRESS                              
SVSECY   DS    XL2                 USER SECURITY LEVEL                          
SVSYS    DS    XL1                 CONNECTED SYSTEM                             
SVLAOFFC DS    CL1                 LIMITED ACCESS OFFICE CODE                   
SVOPOS   DS    XL1                 OFFICE POSITION                              
SV#LVLS  DS    XL1                 MAX. NO OF LEVELS FOR LEDGER                 
SVCURLVL DS    XL(LVLL)            CURRENT ACCOUNT VALUES                       
SVCURACT DS    CL(L'ACTKACT)       CURRENT ACCOUNT CODE                         
SVLVLUL  DS    XL(LVLL)            LEDGER LEVEL VALUES                          
SVLVL1   DS    XL(LVLL)            LEVEL 1 ACCOUNT VALUES                       
SVLVL2   DS    XL(LVLL)            LEVEL 2 ACCOUNT VALUES                       
SVLVL3   DS    XL(LVLL)            LEVEL 3 ACCOUNT VALUES                       
SVLVL4   DS    XL(LVLL)            LEVEL 4 ACCOUNT VALUES                       
SVPRELEN DS    XL1                 LENGTH OF SBACTPRE                           
SVLENMIN DS    XL1                 MINIMUM ACCOUNT CODE LENGTH ALLOWED          
SVLENMAX DS    XL1                 MAXIMUM ACCOUNT CODE LENGTH ALLOWED          
SVCPYSTC DS    XL1                 COMPANY STATUS BYTE 12                       
*                                                                               
VOFFAL   DS    A                   A(OFFAL)                                     
SVOFFAL  DS    XL(OFFALLEN)        OFFAL BLOCK                                  
*                                                                               
SVBLKL   EQU   *-SVBLKD                                                         
*                                                                               
* ACOFFALD                                                                      
       ++INCLUDE ACOFFALD                                                       
         EJECT                                                                  
* GESRCHDIRD                                                                    
         PRINT OFF                                                              
       ++INCLUDE GESRCHDIRD                                                     
         PRINT ON                                                               
         SPACE 1                                                                
* ACSRCHDIRD                                                                    
         PRINT OFF                                                              
       ++INCLUDE ACSRCHDIRD                                                     
         PRINT ON                                                               
         SPACE 1                                                                
* ACGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* DDCOMFACS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* FAGETTXTD                                                                     
         PRINT OFF                                                              
       ++INCLUDE FAGETTXTD                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* DDSCANBLKD                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDSCANBLKD                                                     
         PRINT ON                                                               
         ORG   SCONEFLD                                                         
SCFLDS   DS    0XL(L'SCFLD1+L'SCFLD2)                                           
SCFLD1   DS    XL10                                                             
SCFLD2   DS    XL(SBNALENQ)                                                     
SCANBLKL EQU   *-SCANBLKD                                                       
         SPACE 1                                                                
* ACDDEQUS                                                                      
         PRINT OFF                                                              
       ++INCLUDE ACDDEQUS                                                       
         PRINT ON                                                               
         SPACE 1                                                                
* ACMSGEQUS                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACMSGEQUS                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* DDFH                                                                          
         PRINT OFF                                                              
       ++INCLUDE DDFH                                                           
         PRINT ON                                                               
         SPACE 1                                                                
* FAUTL                                                                         
         PRINT OFF                                                              
       ++INCLUDE FAUTL                                                          
         PRINT ON                                                               
       ++INCLUDE FAFACTS                                                        
         SPACE                                                                  
* FATCB                                                                         
         PRINT OFF                                                              
       ++INCLUDE FATCB                                                          
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'027ACSRCHAC  04/06/10'                                      
         END                                                                    
