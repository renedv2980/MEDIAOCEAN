*          DATA SET FAVIRTRM   AT LEVEL 003 AS OF 06/04/03                      
*CATALP FAVIRTRM                                                                
         TITLE 'FAVIRTRM - VIRTUAL TERMINAL ROUTINES'                           
*                                                                               
*PARM1   XL1   1=GETTOK,2=DELTOK,3=RETTOK,4=SETWEB,5=UNSWEB                     
*              6=ADDVIR,7=DELVIR,8=DELALL                                       
*        AL3   N/D - RETURN A(TOKEN) OR A(ERROR MESSAGE)                        
*PARM2   AL4   A(TOKEN) OR A(UTL IF NOT CALLERS UTL)                            
*PARM3   AL4   V(SYSFACS) IF *INCLUDED VERSION                                  
         PRINT NOGEN                                                            
VIRTRM   CSECT                                                                  
         NMOD1 WRKX-WRKD,**VIRT**                                               
         USING WRKD,RC                                                          
         LA    RE,*+10                                                          
         O     RE,=XL4'80000000'                                                
         BSM   0,RE                THIS IS NOW IN XA MODE                       
*                                                                               
VT1      ST    R1,APARM            SAVE A(PARAMETER LIST)                       
         MVC   P1(12),0(R1)                                                     
         MVC   VIRACT,0(R1)        EXTRACT ACTION                               
         LA    RE,VIRTOK                                                        
         ST    RE,0(R1)            SET ADDRESS OF RETURN AREA                   
         MVC   VIRTOK,SPACES                                                    
         MVI   VIRRSLT,0                                                        
*                                                                               
VT2      ICM   RA,15,=V(SYSFAC)    RA=A(SYSFAC)                                 
         BNZ   *+8                                                              
         L     RA,8(R1)            IF NOT LINKED SET P3=VSYSFACS                
         USING SYSFACD,RA                                                       
         L     R5,VSSB                                                          
         MVC   SYSID,SSBSYSID-SSBD(R5)                                          
         L     R5,SSBTKADR-SSBD(R5)                                             
         L     R5,TCBUTL-TCBD(R5)                                               
         USING UTLD,R5             R5=A(CALLERS UTL ENTRY)                      
         ST    R5,MYUTL                                                         
         ST    R5,THEUTL                                                        
         CLI   VIRACT,2            TEST IF A(UTL) CAN BE PASSED IN P2           
         BNH   VT3                                                              
         ICM   RF,15,P2            TEST IF SET                                  
         ST    RF,INUTL                                                         
         BZ    *+6                                                              
         LR    R5,RF               SWITCH TO PASSED UTL ENTRY                   
         ST    R5,THEUTL                                                        
*                                                                               
VT3      CLI   VIRACT,1            LOCATE VIRTUAL TERMINAL VIA TOKEN            
         BE    VTTOK                                                            
         CLI   VIRACT,2            DELETE VIRTUAL TERMINAL VIA TOKEN            
         BE    VTTOK                                                            
         CLI   VIRACT,3            RETURN TOKEN OF A VIRTUAL TERMINAL           
         BE    VTRET                                                            
         CLI   VIRACT,4            SET WEB BASED LU                             
         BE    VTWEBS                                                           
         CLI   VIRACT,5            UNSET WAB BASED LU                           
         BE    VTWEBU                                                           
         CLI   VIRACT,6            ADD VIRTUAL TERMINAL                         
         BE    VTADD                                                            
         CLI   VIRACT,7            DELETE VIRTUAL TERMINAL                      
         BE    VTDEL                                                            
         CLI   VIRACT,8            DELETE ALL VIRTUAL TERMINALS OWNED           
         BE    VTDALL                                                           
         DC    H'0'                                                             
*                                                                               
VTEXIT   L     R1,APARM            RETURN RESULT                                
         MVC   0(1,R1),VIRRSLT                                                  
         XMOD1 1                                                                
         EJECT                                                                  
***********************************************************************         
* A(TOKEN) PASSED IN P2. VALIDATE AND LOCATE UTL ENTRY                *         
* RETURN AL3(TOKEN) IN P1+1(3) AND A(UTL) IN P2                       *         
***********************************************************************         
         SPACE 1                                                                
VTTOK    ICM   RE,15,P2            VALIDATE TOKEN                               
         BZ    VTTOKER1                                                         
         MVC   VIRTOK,0(RE)        SAVE IN RETURN TOKEN AREA                    
         CLI   VIRTOK,C'>'                                                      
         BNE   VTTOKER2                                                         
         MVI   VIRTOK,C'0'                                                      
         L     RF,VSYSFAC0                                                      
         L     RF,CHEXIN-COMFACSD(RF)                                           
         GOTO1 (RF),DMCB,VIRTOK,TOKEN,10                                        
         OC    12(4,R1),12(R1)                                                  
         BZ    VTTOKER2                                                         
         MVI   VIRTOK,C'>'                                                      
*                                                                               
VTTOK1   BAS   RE,VALTOK           VALIDATE TOKEN                               
         BNE   VTTOKER2                                                         
         LR    R5,RF               R5=A(UTL) ENTRY FOR PASSED TOKEN             
         ST    R5,INUTL                                                         
         ST    R5,THEUTL                                                        
*                                                                               
VTTOK3   L     R1,APARM            PASS BACK A(UTL) ENTRY FOR TOKEN             
         ST    R5,4(R1)                                                         
         CLI   VIRACT,2            TEST TOKEN ACTION                            
         BE    VTDEL               DELETE                                       
         B     VTEXIT              LOCATE                                       
*                                                                               
VTTOKER1 MVI   VIRRSLT,1           MUST PASS TOKEN                              
         MVC   VIRTOK,=CL10'*NO TOKEN'                                          
         B     VTEXIT                                                           
VTTOKER2 MVI   VIRRSLT,2           INVALID TOKEN                                
         MVC   VIRTOK,=CL10'*INV TOKEN'                                         
         B     VTEXIT                                                           
         EJECT                                                                  
***********************************************************************         
* RETURN THE TOKEN OF A VIRTUAL TERMINAL                              *         
* P2=A(VIRTUAL UTL) OR ZERO FOR THIS UTL ENTRY WHICH MUST BE VIRTUAL  *         
* P1+1(3)=A(TOKEN)                                                    *         
***********************************************************************         
                                                                                
VTRET    TM    TVIFLAG,TVIVIRT     IT MUST BE A VIRTUAL TERMINAL                
         BZ    VTRETER1                                                         
         BAS   RE,DSPTOK                                                        
         B     VTEXIT                                                           
*                                                                               
VTRETER1 MVI   VIRRSLT,1           MUST BE A VIRTUAL TERMINAL                   
         MVC   VIRTOK,=CL10'*NOT VIRT'                                          
         B     VTEXIT                                                           
         EJECT                                                                  
***********************************************************************         
* SET ON/OFF TTYPEWEB TO SHOW THIS LU IS A WEB BASED LU               *         
* P2 A(REAL UTL) OR ZERO FOR THIS UTL ENTRY                           *         
***********************************************************************         
                                                                                
VTWEBS   TM    TVIFLAG,TVIVIRT     MUST BE REAL TERMINAL                        
         BO    VTWEBER1                                                         
         OI    TTYPE,TTYPEWEB                                                   
         B     VTEXIT                                                           
*                                                                               
VTWEBU   NI    TTYPE,255-TTYPEWEB                                               
         B     VTEXIT                                                           
*                                                                               
VTWEBER1 MVI   VIRRSLT,1           ONLY REAL TERM CAN BE SET WEB                
         MVC   VIRTOK,=CL10'*NOT REAL'                                          
         B     VTEXIT                                                           
         EJECT                                                                  
***********************************************************************         
* ADD A VIRTUAL TERMINAL                                              *         
* P2=A(REAL UTL) OR ZERO FOR THIS UTL ENTRY                           *         
* RETURN A(NEW UTL) IN P2                                             *         
***********************************************************************         
                                                                                
VTADD    L     RF,THEUTL           RF=A(MY UTL ENTRY)                           
         TM    TVIFLAG-UTLD(RF),TVIVIRT                                         
         BO    VTADDER1            MUST BE A REAL UTL ENTRY                     
         TM    TTYPE-UTLD(RF),TTYPEWEB                                          
         BO    VTADD1                                                           
         CLI   TVICOUNT-UTLD(RF),4                                              
         BE    VTADDER2            CANT EXCEED THE MAXIMUM                      
*                                                                               
VTADD1   GOTO1 VLCM,DMCB,VTGETUTL  GET A NEW UTL ENTRY                          
         LTR   R5,R1                                                            
         BZ    VTADDER3                                                         
         ST    R5,NEWUTL           R5=A(NEW UTL ENTRY)                          
         L     RF,THEUTL           RF=A(MY UTL ENTRY)                           
         SR    R1,R1                                                            
         ICM   R1,3,TNUM                                                        
         CVD   R1,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         MVC   VIRLUID,=C'VT.....V'                                             
         UNPK  VIRLUID+2(5),DUB                                                 
         TM    TTYPE-UTLD(RF),TTYPEWEB                                          
         BZ    VTADD2                                                           
         MVI   VIRLUID+1,C'W'      SHOW WEB BASED IN LUID                       
*                                                                               
VTADD2   NI    TSTAT2,255-TSTATNIT BUILD VIRTUAL UTL FIXED VALUES               
         MVI   TTYPE,TTYPE327                                                   
         MVC   TLUID,VIRLUID                                                    
         MVI   TVIFLAG,TVIVIRT                                                  
         MVC   TVIOWNER,TNUM-UTLD(RF)                                           
         MVC   TVICID,TSIN+2-UTLD(RF)                                           
*                                                                               
         MVI   TSESSION,0          INITIALISE SESSION INFO                      
         MVI   TNAHNAH,0           *** SAME AS TERMBLD ***                      
         MVI   TSSXBITS,0                                                       
         MVI   TSSBITS,0                                                        
         MVI   TSSSWAP,X'FF'                                                    
         MVI   TSSRSRV,X'FF'                                                    
*                                                                               
VTADD3   TM    TSTAT1-UTLD(RF),TSTATDDS COPY INHERITED VALUES                   
         BZ    *+8                                                              
         OI    TSTAT1,TSTATDDS                                                  
         TM    TTYPE-UTLD(RF),TTYPEWEB                                          
         BZ    *+8                                                              
         OI    TTYPE,TTYPEWEB                                                   
         TM    TSTAT8-UTLD(RF),TST8STSS                                         
         BZ    *+8                                                              
         OI    TSTAT8,TST8STSS                                                  
         TM    TSTAT8-UTLD(RF),TST8BINT                                         
         BZ    *+8                                                              
         OI    TSTAT8,TST8BINT                                                  
         MVC   TLVLDRTY,TLVLDRTY-UTLD(RF)                                       
         TM    TSTAT9-UTLD(RF),TSTNVRSN                                         
         BZ    *+8                                                              
         OI    TSTAT9,TSTNVRSN                                                  
         TM    TSTAT9-UTLD(RF),TST9CMAD                                         
         BZ    *+8                                                              
         OI    TSTAT9,TST9CMAD                                                  
         TM    TTYPE2-UTLD(RF),TTYPE2SF                                         
         BZ    *+8                                                              
         OI    TTYPE2,TTYPE2SF                                                  
         TM    TTYPE2-UTLD(RF),TTYPE2SC                                         
         BZ    *+8                                                              
         OI    TTYPE2,TTYPE2SC                                                  
*                                                                               
VTADD4   SR    R1,R1               BUMP COUNT OF VIRTUAL TERMS OWNED            
         IC    R1,TVICOUNT-UTLD(RF)                                             
         LA    R1,1(R1)                                                         
         STC   R1,TVICOUNT-UTLD(RF)                                             
*                                                                               
VTADD5   BAS   RE,DSPTOK           BUILD EBCDIC VERSION OF TOKEN                
         L     R1,APARM                                                         
         ST    R5,4(R1)            PASS BACK A(NEW UTL) IN P2                   
         B     VTADDX                                                           
*                                                                               
VTADDER1 MVI   VIRRSLT,1           ONLY REAL TERM CAN ADD A VIRTUAL             
         MVC   VIRTOK,=CL10'*NOT REAL'                                          
         B     VTADDX                                                           
VTADDER2 MVI   VIRRSLT,2           EXCEEDED MAX VIRTUAL TERMINALS               
         MVC   VIRTOK,=CL10'*TOO MANY'                                          
         B     VTADDX                                                           
VTADDER3 MVI   VIRRSLT,3           NO MORE SPACE IN UTL                         
         MVC   VIRTOK,=CL10'*NO SPACE'                                          
         B     VTADDX                                                           
*                                                                               
VTADDX   CLI   VIRRSLT,0                                                        
         B     VTEXIT                                                           
         EJECT                                                                  
***********************************************************************         
* DELETE A VIRTUAL TERMINAL                                           *         
* P2=A(REAL UTL) OR ZERO FOR THIS UTL ENTRY. P2 CAN ALSO BE A(TOKEN)  *         
* P2 SET TO A(DELETED UTL) ON RETURN                                  *         
***********************************************************************         
                                                                                
VTDEL    L     R5,INUTL            R5=A(INPUT VIRTUAL TERMINAL)                 
         L     RF,MYUTL            RF=A(MY TERMINAL)                            
         TM    TVIFLAG-UTLD(RF),TVIVIRT                                         
         BZ    VTDEL2                                                           
*                                                                               
VTDEL1   LTR   R5,R5               I AM A VIRTUAL UTL ENTRY                     
         BZ    *+10                                                             
         CR    RF,R5                                                            
         BNE   VTDELER3            I CAN ONLY DELETE MYSELF                     
         LR    R5,RF               R5=A(MYSELF)                                 
         SR    R0,R0                                                            
         ICM   R0,3,TVIOWNER       GET MY OWNERS NUMBER                         
*                                                                               
         MVI   TSTAT2,TSTATNIT     DELETE MYSELF                                
         MVI   TSTATU,TSTATAVA                                                  
         MVI   TLUID,C'*'                                                       
         MVI   TVIFLAG,0                                                        
         XC    TVIOWNER,TVIOWNER                                                
*                                                                               
         LTR   RF,R0               DO I HAVE AN OWNER                           
         BZ    VTDEL5              NO                                           
         AHI   RF,-1                                                            
         L     RE,VUTL                                                          
         MH    RF,0(RE)                                                         
         LA    RF,6(RE,RF)         RF=A(OWNER UTL ENTRY)                        
         CLM   R0,3,TNUM-UTLD(RF)                                               
         BNE   VTDEL5              OWNER DOESNT LOOK TOO GOOD                   
         TM    TVIFLAG-UTLD(RF),TVIVIRT                                         
         BO    VTDEL5              OWNER HAS TO BE REAL                         
         B     VTDEL4              DECREMENT OWNER'S COUNT                      
*                                                                               
VTDEL2   LTR   R5,R5               I AM A REAL UTL ENTRY                        
         BZ    VTDELER1            I MUST PASS A(UTL ENTRY) IN P2               
         TM    TVIFLAG,TVIVIRT                                                  
         BZ    VTDELER2            IT MUST BE A VIRTUAL TERMINAL                
         TM    TSTAT2,TSTATTIP                                                  
         BO    VTDELER4            TERMINAL IN PROCESS                          
         CLC   TVIOWNER,TNUM-UTLD(RF)                                           
         BE    VTDEL3                                                           
         TM    TTYPE-UTLD(RF),TTYPEWEB                                          
         BZ    VTDELER3            IF NOT WEB I MUST BE THE OWNER               
         TM    TTYPE,TTYPEWEB                                                   
         BZ    VTDELER3            WEB CAN ONLY DELETE VIRTUAL WEB              
*                                                                               
VTDEL3   MVI   TSTAT2,TSTATNIT     DELETE THE VIRTUAL TERMINAL                  
         MVI   TSTATU,TSTATAVA                                                  
         MVI   TLUID,C'*'                                                       
         MVI   TVIFLAG,0                                                        
         XC    TVIOWNER,TVIOWNER                                                
*                                                                               
VTDEL4   SR    R1,R1               DECR COUNT OF VIRTUAL TERMS OWNED            
         IC    R1,TVICOUNT-UTLD(RF)                                             
         AHI   R1,-1                                                            
         BNM   *+6                                                              
         SR    R1,R1                                                            
         STC   R1,TVICOUNT-UTLD(RF)                                             
*                                                                               
VTDEL5   BAS   RE,DSPTOK           BUILD TOKEN TO PASS BACK IN P1               
         L     R1,APARM                                                         
         ST    R5,4(R1)            PASS BACK A(DELETED UTL) IN P2               
         B     VTDELX                                                           
*                                                                               
VTDELER1 MVI   VIRRSLT,1           ONLY REAL TERM CAN DEL A VIRTUAL             
         MVC   VIRTOK,=CL10'*NOT REAL'                                          
         B     VTDELX                                                           
VTDELER2 MVI   VIRRSLT,2           CAN ONLY DELETE A VIRTUAL TERMINAL           
         MVC   VIRTOK,=CL10'*NOT VIRT'                                          
         B     VTDELX                                                           
VTDELER3 MVI   VIRRSLT,3           I AM NOT THE OWNER OF VIRTUAL TERM           
         MVC   VIRTOK,=CL10'*NOT MINE'                                          
         B     VTDELX                                                           
VTDELER4 MVI   VIRRSLT,4           VIRTUAL TERMINAL IN PROCESS                  
         MVC   VIRTOK,=CL10'*IS ACTIVE'                                         
         B     VTDELX                                                           
*                                                                               
VTDELX   CLI   VIRRSLT,0                                                        
         B     VTEXIT                                                           
         EJECT                                                                  
***********************************************************************         
* DELETE ALL VIRTUAL TERMINALS OWNED                                  *         
* P2=A(REAL UTL) OR ZERO FOR THIS REAL UTL ENTRY                                
***********************************************************************         
                                                                                
VTDALL   L     R5,THEUTL           R5=A(INPUT OR CALLERS UTL)                   
         TM    TVIFLAG,TVIVIRT                                                  
         BO    VTDALER1            I MUST BE A REAL UTL ENTRY                   
         TM    TTYPE,TTYPEWEB                                                   
         BO    VTDALER2            I CANT BE A WEB LU                           
         MVC   DUB(2),TNUM         SAVE OWNER TERMINAL NUMBER                   
         SR    R0,R0               R0=DELETED COUNT                             
         SR    RF,RF                                                            
         ICM   RF,1,TVICOUNT       NOTHING TO DO IF NONE OWNED                  
         BZ    VTDAL6                                                           
         STH   RF,DUB+2            SAVE OWNER VIRTUAL TERMINAL COUNT            
*                                                                               
VTDAL1   L     R5,VUTL             SET BXLE REGS TO SEARCH UTL                  
         LH    R6,0(R5)                                                         
         L     R7,2(R5)                                                         
         AHI   R5,6                                                             
         USING UTLD,R5                                                          
*                                                                               
VTDAL2   TM    TVIFLAG,TVIVIRT     ONLY VIRTUAL UTL ENTRIES                     
         BZ    VTDAL5                                                           
         TM    TTYPE,TTYPEWEB      SKIP CREATED BY WEB LUS                      
         BO    VTDAL5                                                           
         CLC   TVIOWNER,DUB        TEST IF CREATED BY REAL UTL                  
         BNE   VTDAL5                                                           
*                                                                               
VTDAL3   MVI   TSTAT2,TSTATNIT     DELETE THE VIRTUAL TERMINAL                  
         MVI   TSTATU,TSTATAVA                                                  
         MVI   TLUID,C'*'                                                       
         MVI   TVIFLAG,0                                                        
         XC    TVIOWNER,TVIOWNER                                                
*                                                                               
VTDAL4   AHI   R0,1                BUMP DELETED COUNT                           
*                                                                               
VTDAL5   BXLE  R5,R6,VTDAL2        BACK FOR NEXT UTL ENTRY                      
*                                                                               
VTDAL6   L     R5,THEUTL           R5=A(INPUT OR CALLERS UTL)                   
         MVI   TVICOUNT,0          CLEAR COUNT                                  
         B     VTDALX                                                           
*                                                                               
VTDALER1 MVI   VIRRSLT,1           MUST BE A REAL LU                            
         MVC   VIRTOK,=CL10'*NOT REAL'                                          
         B     VTDALX                                                           
VTDALER2 MVI   VIRRSLT,2           CANT BE A WEB LU                             
         MVC   VIRTOK,=CL10'*NOT WEB'                                           
         B     VTDALX                                                           
*                                                                               
VTDALX   CLI   VIRRSLT,0                                                        
         B     VTEXIT                                                           
         EJECT                                                                  
***********************************************************************         
* ROUTINES TO VALIDATE AND DISPLAY A TOKEN                            *         
***********************************************************************         
         SPACE 1                                                                
VALTOK   ST    RE,SAVERE           VALIDATE TOKEN                               
*                                                                               
         CLC   TOKSYSID,SYSID      FIRST CHR IS FACPAK ID                       
         BNE   VALTOKER                                                         
         SR    RE,RE               NEXT TWO CHRS ARE VIRTUAL TERM NUM           
         ICM   RE,3,TOKTNUM                                                     
         BZ    VALTOKER                                                         
         L     RF,VUTL                                                          
         AHI   RF,-6                                                            
         C     RE,0(RF)            COMPARE WITH MAXIMUM SO FAR                  
         BH    VALTOKER                                                         
         AHI   RF,6                                                             
         AHI   RE,-1               TNUM-1                                       
         MH    RE,0(RF)                                                         
         LA    RF,6(RE,RF)         RF=A(VIRTUAL UTL ENTRY)                      
*                                                                               
VALTOK2  TM    TVIFLAG-UTLD(RF),TVIVIRT                                         
         BZ    VALTOKER                                                         
         CLC   TOKCID,TVICID-UTLD(RF)                                           
         BE    VALTOKOK                                                         
*                                                                               
VALTOKER CR    RB,RD              SET CC NEQ IF INVALID TOKEN                   
         B     *+6                                                              
VALTOKOK CR    RB,RB              SET CC EQL IF VALID                           
         L     RE,SAVERE                                                        
         BR    RE                                                               
                                                                                
DSPTOK   ST    RE,SAVERE           BUILD TOKEN FROM R5=A(UTL ENTRY)             
         MVC   DUB+0(1),SYSID                                                   
         MVC   DUB+1(2),TNUM                                                    
         MVC   DUB+3(2),TVICID                                                  
         L     RF,VSYSFAC0                                                      
         L     RF,CHEXOUT-COMFACSD(RF)                                          
         GOTO1 (RF),DMCB,DUB,VIRTOK,5,=C'TOG'                                   
         MVI   VIRTOK,C'>'                                                      
         L     RE,SAVERE                                                        
         BR    RE                                                               
         EJECT                                                                  
SPACES   DC    10C' '                                                           
         LTORG                                                                  
         EJECT                                                                  
WRKD     DSECT                                                                  
DMCB     DS    CL24                                                             
DUB      DS    D                                                                
APARM    DS    A                                                                
P1       DS    A                                                                
P2       DS    A                                                                
P3       DS    A                                                                
SAVERE   DS    A                                                                
MYUTL    DS    A                                                                
INUTL    DS    A                                                                
THEUTL   DS    A                                                                
NEWUTL   DS    A                                                                
SYSID    DS    X                                                                
VIRACT   DS    C                                                                
VIRRSLT  DS    C                                                                
VIRLUID  DS    CL8                                                              
VIRTOK   DS    CL10                                                             
*                                                                               
TOKEN    DS    0XL5                VIRTUAL TERMINAL TOKEN                       
TOKSYSID DS    XL1                 VIRTUAL TERMINAL FAKPAK ID                   
TOKTNUM  DS    XL2                 VIRTUAL TERMINAL NUMBER                      
TOKCID   DS    XL2                 VIRTUAL TERMINAL CONTROL ID                  
*                                                                               
WRKX     EQU   *                                                                
         EJECT                                                                  
* FADSECTS                                                                      
       ++INCLUDE FADSECTS                                                       
* DDCOMFACS                                                                     
       ++INCLUDE DDCOMFACS                                                      
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003FAVIRTRM  06/04/03'                                      
         END                                                                    
