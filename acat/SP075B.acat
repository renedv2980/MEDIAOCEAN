*          DATA SET SP075B     AT LEVEL 078 AS OF 05/01/02                      
*CATALP RSP075B                                                                 
         TITLE 'TRAFFIC NEW BUY ANALYSIS REPORT'                                
SP75E15  CSECT                                                                  
         REQUS                                                                  
         NMOD1 0,SP75E15                                                        
         L     RA,=V(RPWORKC)                                                   
         USING RPWORK,RA                                                        
         L     RC,=V(SP75WKC)                                                   
         USING SP75WKD,RC                                                       
         L     4,STORE4                                                         
         ST    R1,STORE1                                                        
         SR    R0,R0                                                            
         IC    R0,BAGY                                                          
         SLL   R0,4                                                             
         STC   R0,BAGYMD                                                        
         CLI   QMED,C'R'                                                        
         BNE   *+8                                                              
         OI    BAGYMD,2                                                         
         CLI   QMED,C'T'                                                        
         BNE   *+8                                                              
         OI    BAGYMD,1                                                         
         CLI   FRSTIM,X'02'                                                     
         BE    TRFIRST                                                          
         CLI   SORTSW,X'01'        CHECK FOR END CHANGGS                        
         BE    TRRCHA                                                           
         CLI   SORTSW,X'02'        CHECK FOR END DELETES                        
         BE    TRREADEL                                                         
         CLI   SORTSW,X'03'                                                     
         BE    TRWRCHA                                                          
         CLI   NEWREC,X'FF'                                                     
         BE    TRIEND                                                           
         CLI   FRSTIM,X'02'                                                     
         BE    *+8                 CHECK FOR RECORD IN BUFFER                   
GETTAP   BAS   9,READACT           READ ACTIVITY TAPE                           
TRPROC   XC    SORTSW,SORTSW                                                    
         OC    RSIN,RSIN           CHECK FOR ACTIVITY RECORD                    
         BZ    GETTAP                                                           
         CLI   NEWREC,X'FF'                                                     
         BE    TRFLCH                                                           
         TM    NEWREC,X'F0'        CHECK FOR BUY RECORD                         
         BZ    GETTAP                                                           
         MVC   DUB(1),NEWREC       CHECK IF AVIAL                               
         NI    DUB,X'0F'                                                        
         CLI   DUB,2                                                            
         BH    GETTAP                                                           
         CLI   FRSTIM,X'00'        CHECK FOR FIRST                              
         BNE   TRFIRST                                                          
         CLI   NEWREC+3,X'FF'      CHECK FOR POOL                               
         BE    *+12                                                             
         TM    NEWREC+3,X'80'      BYPASS OTHER AGENCY BUYS                     
         BO    GETTAP                                                           
         CLC   NEWREC(13),GETWORK  CHECK FOR LINE BEAK                          
         BNE   TRFLCH                                                           
         CLI   RRECTY,X'02'        CHECK FOR CHANGE RECORD                      
         BNE   GETTAP                                                           
         TM    NEWREC+15,X'80'     CHECKFOR DELETED RECORD                      
         BO    TRDEL                                                            
         BAS   9,BPRDLST           BUILD PRODUCT LIST                           
         B     GETTAP                                                           
TRFLCH   L     4,=V(TRCCTAB)                                                    
         LA    5,TRWRKTAB                                                       
TRCNG    CLI   0(5),X'00'                                                       
         BE    NXTBUY                                                           
         CLC   0(7,5),0(4)         THIS ROUTINE FILLS                           
         BNE   TRCNG1              CHANGE TABLE FOR STATION                     
         LA    5,7(5)                                                           
         L     R4,=V(TRCCTAB)                                                   
         B     TRCNG                                                            
TRCNG1   CLI   0(4),0                                                           
         BE    *+12                                                             
         LA    4,7(4)                                                           
         B     TRCNG                                                            
         MVC   0(7,4),0(5)                                                      
         LA    5,7(5)                                                           
         CLI   0(5),X'00'                                                       
         BE    NXTBUY                                                           
         L     R4,=V(TRCCTAB)                                                   
         B     TRCNG                                                            
NXTBUY   CLC   NEWREC(9),GETWORK   CHECK FOR STATION BREAK                      
         BNE   TRGENREC                                                         
         MVC   GETWORK(13),NEWREC                                               
         CLI   RRECTY,X'03'        CHECK FOR ADD                                
         BNE   *+12                                                             
         BAS   9,BPRDLST                                                        
         B     GETTAP                                                           
         CLI   RRECTY,X'01'         CHECK FOR COPY                              
         BE    *+8                                                              
         B     GETTAP                                                           
         LA    R8,NEWREC                                                        
         USING BUYREC,R8                                                        
         CLC   BDCHG,TODAY         BYPASS COPIES OF BUYS MADE TODAY             
         BE    GETTAP                                                           
         DROP  R8                                                               
         BAS   9,BPRDLST                                                        
         L     R4,=V(TRSTTAB)                                                   
         LA    5,TRWRKTAB                                                       
TRCOPY   CLI   0(5),X'00'          THIS ROUTINE FILLS STATION TABLE             
         BE    GETTAP                                                           
         CLC   0(7,5),0(4)                                                      
         BNE   *+16                                                             
         LA    5,7(5)                                                           
         L     R4,=V(TRSTTAB)                                                   
         B     TRCOPY                                                           
         CLI   0(4),X'00'                                                       
         BE    *+12                                                             
         LA    4,7(4)                                                           
         B     TRCOPY                                                           
         MVC   0(7,4),0(5)                                                      
         LA    5,7(5)                                                           
         CLI   0(5),X'00'                                                       
         BE    GETTAP                                                           
         L     R4,=V(TRSTTAB)                                                   
         B     TRCOPY                                                           
TRDEL    BAS   9,BPRDLST           THIS ROUTINE FILLS DELETE TABLE              
         L     R4,=V(TRDELTAB)                                                  
         LA    5,TRWRKTAB                                                       
TRDELFL  CLI   0(5),X'00'                                                       
         BE    GETTAP                                                           
         CLC   0(7,5),0(4)                                                      
         BNE   TRDELFL1                                                         
         LA    5,7(5)                                                           
         L     R4,=V(TRDELTAB)                                                  
         B     TRDELFL                                                          
TRDELFL1 CLI   0(4),0                                                           
         BE    *+12                                                             
         LA    4,7(4)                                                           
         B     TRDELFL                                                          
         MVC   0(7,4),0(5)                                                      
         LA    5,7(5)                                                           
         CLI   0(5),X'00'                                                       
         BE    GETTAP                                                           
         L     R4,=V(TRDELTAB)                                                  
         B     TRDELFL                                                          
*                                                                               
*              THIS ROUTINE BUILDS A PRD/LEN/EST LIST FOR BRAND                 
*                                                                               
BPRDLST  LA    RE,TRWRKTAB                                                      
         LA    RF,18                                                            
         XC    0(200,RE),0(RE)                                                  
         LA    RE,200(RE)                                                       
         BCT   RF,*-10                                                          
         USING BUYREC,8                                                         
         LA    8,NEWREC                                                         
         CLC   TODAY,BDCHG                                                      
         BH    *+10                                                             
         MVC   TODAY,BDCHG                                                      
         CLI   NEWREC+3,X'FF'      CHECK FOR POOL                               
         BE    POLCHG                                                           
         LA    R4,TRWRKTAB                                                      
         CLI   NEWREC+3,X'00'                                                   
         BNE   *+6                                                              
         DC    H'0'                                                             
         XC    STAREC,STAREC                                                    
         GOTO1 =V(BRDWK),DUB,(X'01',BDSTART),(X'02',STAREC)                     
         GOTO1 =V(DTCNV),DUB,(X'01',BDEND),(X'02',TRPREC)                       
         LA    15,STAREC-2                                                      
BBWEEKS1 LA    15,2(15)                                                         
         CLI   0(15),0                                                          
         BCR   8,9                                                              
         CLC   0(2,RF),TRPREC                                                   
         BCR   2,R9                                                             
         MVC   0(1,4),NEWREC+3                                                  
         MVC   4(1,4),NEWREC+9                                                  
         MVC   5(2,R4),0(RF)                                                    
         MVC   1(1,4),BDSEC                                                     
         LA    4,7(4)                                                           
         CLI   BDTIME,X'00'        CHECK FOR PIGGY                              
         BE    BBWEEKS1                                                         
         SH    4,=H'7'                                                          
         LA    3,BDELEM                                                         
         XR    0,0                                                              
TRGTPIG  IC    0,1(3)              BUILD PIGGYBACK LIST                         
         AR    3,0                                                              
         CLI   0(3),X'00'                                                       
         BE    BBWEEKS1                                                         
         CLI   0(3),X'04'                                                       
         BNE   TRGTPIG                                                          
         MVC   1(1,4),BDTIME                                                    
         MVC   0(1,4),NEWREC+3                                                  
         MVC   4(1,4),NEWREC+9                                                  
         MVC   2(1,4),2(3)                                                      
         MVC   3(1,4),4(3)                                                      
         LA    4,7(4)                                                           
         B     TRGTPIG                                                          
*                                                                               
*        THIS ROUTINE BUILDS PRD/LEN/EST FOR POOL                               
*                                                                               
POLCHG   LA    3,BDELEM                                                         
         XR    0,0                                                              
         LA    15,28                                                            
         LA    4,TRWRKTAB                                                       
         B     *+10                                                             
TRGETRG  IC    0,1(3)                                                           
         AR    3,0                                                              
         CLI   0(3),X'00'                                                       
         BCR   8,9                                                              
         CLI   1(R3),X'00'                                                      
         BCR   8,9                                                              
         CLI   0(3),X'0B'                                                       
         BNE   TRGETRG                                                          
         CLI   1(3),X'0E'                                                       
         BL    TRGETRG                                                          
         TM    6(3),X'04'                                                       
         BO    TRGETRG                                                          
         MVC   FULL(2),2(3)                                                     
         MVC   FULL+2(2),2(3)                                                   
         XC    STAREC,STAREC                                                    
         GOTO1 =V(BRDWK),DMCB,(X'02',FULL),(X'02',STAREC)                       
         MVC   5(2,4),STAREC                                                    
         MVC   0(1,4),10(3)                                                     
         MVC   1(1,4),11(3)                                                     
         MVC   4(1,4),NEWREC+9                                                  
         CLI   1(3),X'11'                                                       
         BH    *+14                                                             
         LA    4,7(4)                                                           
         BCT   15,TRGETRG                                                       
         BR    9                                                                
         MVC   2(1,4),14(3)                                                     
         MVC   3(1,4),15(3)                                                     
         LA    4,7(4)                                                           
         BCT   15,TRGETRG                                                       
         BR    9                                                                
         DROP  8                                                                
*                                                                               
*              THIS ROUTINE MATCHS TABLES BEFORE READING FILE                   
*                                                                               
TRGENREC L     R4,=V(TRSTTAB)                                                   
         L     R5,=V(TRCCTAB)                                                   
TRSUMLP  CLC   0(7,5),0(4)                                                      
         BNE   TRSUMUP                                                          
         OI    0(5),X'80'                                                       
         LA    5,7(5)                                                           
         L     R4,=V(TRSTTAB)                                                   
         CLI   0(5),X'00'                                                       
         BE    TRCHK                                                            
         B     TRSUMLP                                                          
TRSUMUP  LA    4,7(4)                                                           
         CLI   0(4),X'00'                                                       
         BNE   TRSUMLP                                                          
         LA    5,7(5)                                                           
         CLI   0(5),X'00'                                                       
         BE    TRCHK                                                            
         L     R4,=V(TRSTTAB)                                                   
         B     TRSUMLP                                                          
*                                                                               
*        THIS ROUTINE READS BUY FILE FOR CHANGES                                
*                                                                               
TRCHK    L     RE,=V(TRSTTAB)                                                   
         LA    RF,100                                                           
         XC    0(200,RE),0(RE)                                                  
         LA    RE,200(RE)                                                       
         BCT   RF,*-10                                                          
         L     R4,=V(TRCCTAB)                                                   
TRRCHA   CLI   0(4),X'00'          END OF CHANGES PROCESS DELETES               
         BE    TRRDEL                                                           
         TM    0(4),X'80'                                                       
         BZ    *+12                                                             
         LA    4,7(4)                                                           
         B     TRRCHA                                                           
CHANXTP  XC    KEY,KEY             GET BUY FOR NEXT PRD                         
         MVI   HALF2,X'00'                                                      
         MVC   KEY(9),GETWORK                                                   
         MVC   KEY+3(1),0(4)                                                    
         MVC   KEY+9(1),4(4)                                                    
         L     1,=V(GTBYWKC)                                                    
         USING GTBYWK,1                                                         
         MVI   RHISW,X'01'                                                      
         MVI   DELOPT,X'01'                                                     
         MVI   PSVSW,X'01'                                                      
         DROP  1                                                                
CHANXTB  BAS   9,TRGETBUY                                                       
         CLC   KEY(1),BAGYMD                                                    
         BNE   DELCHA                                                           
         CLC   KEY+3(1),0(4)      PRODUCT NOT FOUND-WRITE DELETE                
         BNE   DELCHA                                                           
         CLC   KEY+4(5),GETWORK+4                                               
         BNE   DELCHA                                                           
         CLC   KEY+9(1),4(4)       ESTIMATE NOT FOUND-DELETE                    
         BNE   DELCHA                                                           
         CLC   BDCHG,TODAY         RECORD CHANGE NOT TODAY                      
         BL    CHALEN                                                           
         TM    BUYCNTL,X'80'       RECORD DELETE TODAY-BYPASS                   
         BO    CHANXTB                                                          
         MVI   HALF2,1                                                          
         B     CHANXTB                                                          
CHALEN   BAS   9,BRSTPRD           STORE DELETED BRANDS                         
         TM    BUYCNTL,X'80'                                                    
         BO    CHANXTB                                                          
         XR    1,1                                                              
         XR    2,2                                                              
         IC    2,1(4)                                                           
         IC    1,3(4)                                                           
         AR    1,2                                                              
         STC   1,HALF                                                           
         CLC   HALF(1),BDSEC       CHECK FOR SAME SPOT LENGTH                   
         BNE   CHANXTB                                                          
         L     R1,=V(TRSTTAB)                                                   
CHASRCH  CLI   0(1),X'00'          CHECK FOR PIGGYBACK BRANDS                   
         BE    CHANXTB                                                          
         CLC   0(7,4),0(R1)                                                     
         BNE   *+12                                                             
         OI    0(4),X'80'          DELETE CHANGE                                
         B     TRRCHA                                                           
         LA    1,7(1)                                                           
         B     CHASRCH                                                          
*                                                                               
*              THIS ROUTINE READS BUY FILE FOR DELETES                          
*                                                                               
TRRDEL   XC    TRIREC,TRIREC                                                    
         L     R4,=V(TRDELTAB)                                                  
TRREADEL TM    0(4),X'80'                                                       
         BZ    *+12                                                             
         LA    4,7(4)                                                           
         B     TRREADEL                                                         
         CLI   0(4),X'00'          END OF DELETES-PROCESS CHANGES               
         BE    TRWRCHA                                                          
DELNXTP  XC    KEY,KEY             GET BUYREC FOR DELETES                       
         MVC   KEY(9),GETWORK                                                   
         MVC   KEY+3(1),0(4)                                                    
         MVC   KEY+9(1),4(4)                                                    
         L     1,=V(GTBYWKC)                                                    
         USING GTBYWK,1                                                         
         MVI   RHISW,X'01'                                                      
         MVI   DELOPT,X'00'                                                     
         DROP  1                                                                
DELNXTB  BAS   9,TRGETBUY                                                       
         CLC   KEY+3(1),0(4)       PRODUCT DELETED                              
         BNE   TRPDEL                                                           
         CLC   KEY+9(1),4(4)       ESTIMATE DELETE                              
         BNE   TRPDEL                                                           
         BAS   9,BBRPLST           STORE PRODUCT                                
         LA    1,TRWRKTAB                                                       
DELSRCH  CLI   0(1),X'00'          PRODUCT NOT IN BUYREC                        
         BE    DELNXTB                                                          
         CLC   0(7,1),0(4)                                                      
         BE    *+12                                                             
         LA    1,7(1)                                                           
         B     DELSRCH                                                          
         OI    0(4),X'80'                                                       
         B     TRRDEL                                                           
*                                                                               
*                                                                               
*        THIS ROUTINE SETS UP DELETE SORT RECORD                                
*                                                                               
TRPDEL   MVC   TRIAMC,GETWORK                                                   
         MVC   TRIMS,GETWORK+4                                                  
         MVC   TRIP1(7),0(4)                                                    
         OI    0(4),X'80'                                                       
         MVI   TRITYP,C'3'                                                      
         MVI   SORTSW,X'02'                                                     
         B     TRRELSE                                                          
DELCHA   XC    TRIREC,TRIREC                                                    
         CLI   HALF2,1                                                          
         BNE   *+12                                                             
         LA    4,7(4)                                                           
         B     TRRCHA                                                           
         MVC   TRIAMC,GETWORK                                                   
         CLC   TRIAMC(1),BAGYMD                                                 
         BNE   TRIEND                                                           
         MVC   TRIMS,GETWORK+4                                                  
         MVC   TRIP1(7),0(4)                                                    
         OI    0(4),X'80'                                                       
         MVI   SORTSW,X'01'                                                     
         MVI   TRITYP,C'3'                                                      
         B     TRRELSE                                                          
*                                                                               
*        THIS ROUTINE WRITES CHANGES TO OUTPUT                                  
*                                                                               
TRWRCHA  XC    TRIREC,TRIREC                                                    
         L     R4,=V(TRCCTAB)                                                   
TRREADCH TM    0(4),X'80'                                                       
         BZ    *+12                                                             
         LA    4,7(4)                                                           
         B     TRREADCH                                                         
         CLI   0(4),X'00'                                                       
         BNE   TROKWR              WRITE CHANGES                                
         MVC   GETWORK(13),NEWREC                                               
         CLI   NEWREC,X'FF'                                                     
         BE    TRIEND                                                           
         LA    RE,18                                                            
         LA    RF,TRWRKTAB                                                      
         XC    0(200,RF),0(RF)                                                  
         LA    RF,200(RF)                                                       
         BCT   RE,*-10                                                          
         LA    RE,100                                                           
         L     RF,=V(TRCCTAB)                                                   
         XC    0(200,RF),0(RF)                                                  
         LA    RF,200(RF)                                                       
         BCT   RE,*-10                                                          
         L     RF,=V(TRDELTAB)                                                  
         LA    RE,100                                                           
         XC    0(200,RF),0(RF)                                                  
         LA    RF,200(RF)                                                       
         BCT   RE,*-10                                                          
         LA    RE,100                                                           
         L     RF,=V(TRSTTAB)                                                   
         XC    0(200,RF),0(RF)                                                  
         LA    RF,200(RF)                                                       
         BCT   RE,*-10                                                          
         CLC   NEWREC(1),BAGYMD                                                 
         BNE   TRIEND                                                           
         MVC   GETWORK(13),NEWREC                                               
         B     NXTBUY                                                           
TROKWR   MVC   TRIAMC,GETWORK                                                   
         CLC   TRIAMC(1),BAGYMD                                                 
         BNE   TRIEND                                                           
         MVC   TRIMS,GETWORK+4                                                  
         MVC   TRIP1(7),0(4)                                                    
         OI    0(4),X'80'                                                       
         MVI   SORTSW,X'03'                                                     
         MVI   TRITYP,C'1'                                                      
         B     TRRELSE                                                          
*        THIS ROUTINE STORES BUYREC BRANDS IN WRKTAB                            
*                SEE BPRDLST AND POLCHG                                         
*                                                                               
BBRPLST  LA    15,TRWRKTAB                                                      
         LA    RE,18                                                            
         XC    0(200,RF),0(RF)                                                  
         LA    RF,200(RF)                                                       
         BCT   14,*-10                                                          
         CLI   BUYREC+3,X'FF'                                                   
         BE    BRPOLCHG                                                         
         LA    R6,TRWRKTAB                                                      
         XC    STAREC,STAREC                                                    
         GOTO1 =V(BRDWK),DMCB,(X'01',BDSTART),(X'02',STAREC)                    
         LA    15,STAREC-2                                                      
BBWEEKS2 LA    15,2(15)                                                         
         CLI   0(15),0                                                          
         BCR   8,9                                                              
         MVC   0(1,R6),KEY+3                                                    
         MVC   4(1,R6),KEY+9                                                    
         MVC   1(1,R6),BDSEC                                                    
         MVC   5(2,R6),0(RF)                                                    
         LA    R6,7(R6)                                                         
         CLI   BDTIME,0                                                         
         BE    BBWEEKS2                                                         
         SH    R6,=H'7'                                                         
         LA    3,BDELEM                                                         
         XR    0,0                                                              
BRGTPIG  IC    0,1(3)                                                           
         AR    3,0                                                              
         CLI   0(3),X'00'                                                       
         BE    BBWEEKS2                                                         
         CLI   0(3),X'04'                                                       
         BNE   BRGTPIG                                                          
         MVC   1(1,R6),BDTIME                                                   
         MVC   2(1,R6),2(3)                                                     
         MVC   3(1,R6),4(R3)                                                    
         LA    R6,7(R6)                                                         
         B     BRGTPIG                                                          
BRPOLCHG LA    3,BDELEM                                                         
         XR    0,0                                                              
         LA    7,TRWRKTAB                                                       
         LA    15,28                                                            
         B     *+10                                                             
BRGETRG  IC    0,1(3)                                                           
         AR    3,0                                                              
         CLI   0(3),X'00'                                                       
         BCR   8,9                                                              
         CLI   0(3),X'0B'                                                       
         BNE   BRGETRG                                                          
         CLI   1(3),X'0E'                                                       
         BL    BRGETRG                                                          
         TM    6(3),X'04'                                                       
         BO    BRGETRG                                                          
         XC    STAREC,STAREC                                                    
         MVC   FULL(2),2(3)                                                     
         MVC   FULL+2(2),2(3)                                                   
         GOTO1 =V(BRDWK),DMCB,(X'02',FULL),(X'02',STAREC)                       
         MVC   5(2,7),STAREC                                                    
         MVC   0(1,7),10(3)                                                     
         MVC   1(1,7),11(3)                                                     
         MVC   4(1,7),KEY+9                                                     
         CLI   1(3),X'11'                                                       
         BH    *+16                                                             
         LA    7,7(7)                                                           
         BCT   15,BRGETRG                                                       
         B     BRGETRG                                                          
         MVC   2(1,7),14(3)                                                     
         MVC   3(1,7),15(3)                                                     
         LA    7,7(7)                                                           
         B     BRGETRG                                                          
*                                                                               
*              THIS ROUTINE BUILDS STATION TABLE FOR BUY RECORDS                
*                                                                               
         DC    F'0'                                                             
BRSTPRD  ST    9,*-4                                                            
         BAS   9,BBRPLST                                                        
         L     9,BRSTPRD-4                                                      
         LA    7,TRWRKTAB                                                       
         L     R8,=V(TRSTTAB)                                                   
BRSTRLP  CLI   0(7),X'00'                                                       
         BCR   8,9                                                              
         CLC   0(7,7),0(8)                                                      
         BNE   *+12                                                             
         LA    7,7(7)                                                           
         B     BRSTRLP-4                                                        
         CLI   0(8),X'00'                                                       
         BNE   *+18                                                             
         MVC   0(7,8),0(7)                                                      
         LA    7,7(7)                                                           
         B     BRSTRLP-4                                                        
         LA    8,7(8)                                                           
         B     BRSTRLP                                                          
TRGETBUY L     15,=V(GETBUY)                                                    
         BASR  14,15                                                            
         BR    9                                                                
READACT  LA    1,10                                                             
         CLI   NEWREC,X'FF'                                                     
         BE    TRIEND                                                           
         LA    7,NEWREC                                                         
         OC    RSIN,RSIN                                                        
         BZ    *+10                                                             
         MVC   GETWORK(13),NEWREC                                               
         XC    0(200,7),0(7)                                                    
         LA    7,200(7)                                                         
         BCT   1,*-10                                                           
         LA    1,TRRDPAR                                                        
         L     15,=V(R00310)                                                    
         BASR  14,15                                                            
         MVC   DUB(1),NEWREC                                                    
         MVC   DUB+1(1),BAGYMD                                                  
         NI    DUB,X'F0'                                                        
         NI    DUB+1,X'F0'                                                      
         CLC   DUB(1),DUB+1                                                     
         BH    *+10                                                             
         TM    9(1),X'04'                                                       
         BCR   8,9                                                              
         MVI   NEWREC,X'FF'                                                     
         MVC   NEWREC+1(12),NEWREC                                              
CHKEND   CLI   FRSTIM,0                                                         
         BNE   TRIEND                                                           
         BR    9                                                                
TRFIRST  SR    R0,R0                                                            
         IC    R0,BAGY                                                          
         SLL   R0,4                                                             
         STC   R0,BAGYMD                                                        
         XC    TODAY,TODAY                                                      
         CLI   QMED,C'R'                                                        
         BNE   *+8                                                              
         OI    BAGYMD,X'02'                                                     
         CLI   QMED,C'T'                                                        
         BNE   *+8                                                              
         OI    BAGYMD,X'01'                                                     
         LA    RE,18                                                            
         LA    RF,TRWRKTAB                                                      
         XC    0(200,RF),0(RF)                                                  
         LA    RF,200(RF)                                                       
         BCT   RE,*-10                                                          
         LA    RE,100                                                           
         L     RF,=V(TRCCTAB)                                                   
         XC    0(200,RF),0(RF)                                                  
         LA    RF,200(RF)                                                       
         BCT   RE,*-10                                                          
         L     RF,=V(TRDELTAB)                                                  
         LA    RE,100                                                           
         XC    0(200,RF),0(RF)                                                  
         LA    RF,200(RF)                                                       
         BCT   RE,*-10                                                          
         CLC   NEWREC(4),=X'FFFFFFFF'                                           
         BE    TRIEND                                                           
         CLC   NEWREC(1),BAGYMD                                                 
         BNE   GETTAP                                                           
         XC    FRSTIM,FRSTIM                                                    
         XC    GETWORK,GETWORK                                                  
         MVC   GETWORK(9),NEWREC                                                
         B     TRPROC                                                           
TRRELSE  L     R1,STORE1           RELEASE RECORD TO SORT                       
         CLI   TRIEST,0                                                         
         BNE   *+6                                                              
         DC    H'0'                                                             
         MVI   TRIBRND,X'00'                                                    
         CLI   QOPT1,C'Y'                                                       
         BNE   *+10                                                             
         MVC   TRIBRND,TRIP1                                                    
         ST    4,STORE4                                                         
         LA    R5,TRIREC                                                        
         ST    R5,0(R1)                                                         
         L     R8,4(R1)                                                         
         MVI   3(R8),X'0C'                                                      
TREXIT   XMOD  1                                                                
TRIEND   L     R1,STORE1           END                                          
         LA    R5,0                                                             
         ST    R5,0(1)                                                          
         L     R4,4(R1)                                                         
         MVI   3(R4),X'08'                                                      
         B     TREXIT                                                           
         LTORG                                                                  
STORE4   DC    F'0'                                                             
TRSTTAB  CSECT                                                                  
         DS    20000C                                                           
TRDELTAB CSECT                                                                  
         DS    20000C                                                           
TRCCTAB  CSECT                                                                  
         DS    20000C                                                           
GTBYWK   DSECT                                                                  
RHISW    DC    X'01'                                                            
DELOPT   DC    X'00'                                                            
PSVSW    DC    X'00'                                                            
LNKEY    DS    CL13                                                             
LNKEL    DC    XL40'00'                                                         
       ++INCLUDE SP75WKD                                                        
         PRINT OFF                                                              
       ++INCLUDE RPWORK                                                         
