*          DATA SET FATOICC    AT LEVEL 045 AS OF 05/01/02                      
*CATALP FATOICC                                                                 
         TITLE 'TOICC - ICC TERMINAL OUTPUT TRANSLATOR'                         
TOICC    CSECT                                                                  
         DS    2048C               FIX PROGRAM LENGTH                           
         ORG   *-2048                                                           
*                                                                               
         NMOD1 10,**TOICC                                                       
         USING OUTD,RC                                                          
         XC    OUTD(80),OUTD       CLEAR WORK AREA                              
         LM    R2,R4,0(R1)         P1 A(TWA)                                    
         USING UTLD,R4                                                          
         MVC   SVTTYPE,TTYPE                                                    
         OC    TSVCREQ,TSVCREQ     TEST SERVICE REQ                             
         BZ    TC1                 NO                                           
         TM    SVTTYPE,X'20'       TEST ADDS TWX                                
         BZ    TC1                 NO                                           
         LA    R4,64(R2)                                                        
         ZIC   R0,0(R4)                                                         
         AR    R4,R0                                                            
         OI    1(R4),X'01'         SET MODIFIED FLAG IN FIRST UNP FLD           
TC1      DC    0H'0'                                                            
         DROP  R4                                                               
*                                  A(OUTPUT BUFFER)                             
*                                     AFTER                                     
*                                  P1 BYTE 0 X'FF' = TWAERROR                   
         EJECT                                                                  
*                  CHECK INTEGRITY OF TWA                                       
         SPACE 3                                                                
         LA    R4,64(R2)                                                        
         SR    R5,R5                                                            
         SPACE 2                                                                
TC2      CLI   0(R4),0                                                          
         BE    TC3                                                              
         IC    R5,0(R4)                                                         
         CH    R5,=H'9'                                                         
         BNL   *+6                                                              
         DC    H'0'                                                             
         CLC   2(2,R4),=H'1920'    CHECK ADDRESS NOT GREATER THAN               
         BNH   *+6                                                              
         DC    H'0'                                                             
         CLC   2(2,R4),CHECK       CHECK THAT ADDRESSES ASCEND                  
         BH    *+6                                                              
         DC    H'0'                                                             
         TM    7(R4),X'80'         IS THIS FIELD TO BE FORMATTED                
         BZ    TCNOFORM                                                         
         NI    7(R4),X'7F'         RESET IND                                    
         LH    RE,2(R4)            WORK OUT WHAT LINE ITS ON                    
         SRDA  RE,32                                                            
         D     RE,=F'80'                                                        
         LA    RF,FORMLINE(RF)                                                  
         MVI   0(RF),1             LEAVE A NOTE OF IT                           
*                                                                               
TCNOFORM MVC   CHECK(2),2(R4)                                                   
         AR    R4,R5                                                            
         B     TC2                                                              
         SPACE 2                                                                
TWAERROR MVI   0(R1),X'FF'                                                      
         B     TCEXT                                                            
         EJECT                                                                  
*                  CHECK IF ALL FIELDS ARE TO BE TRANSMITTED                    
         SPACE 3                                                                
TC3      MVC   INDICS(2),1(R4)     SAVE GENCER INDICS                           
         OC    FORMLINE,FORMLINE                                                
         BZ    *+8                                                              
         BAS   RE,REFORM                                                        
         CLI   0(R1),1             CHECK OPTION TO OUTPUT ALL                   
         BNE   CHKIND                                                           
         MVI   INDICS,1                                                         
         B     TRANSALL                                                         
         SPACE 2                                                                
CHKIND   EQU   *                                                                
         OC    1(2,R4),1(R4)       ARE GENCER INDICS ON                         
         BZ    TC4                                                              
         XC    1(2,R4),1(R4)                                                    
         MVI   0(R1),1             TELL LCM TO WRITE ERASE                      
         MVI   0(R2),0                                                          
         SPACE 2                                                                
TRANSALL LA    R4,64(R2)                                                        
         B     YES                 ALWAYS XMT FIRST FIELD ON ALL                
         SPACE 2                                                                
ALLOOP   CLI   0(R4),0             NOW LOOP AND TURN ON TRANSMIT BITS           
         BE    TC4                 FOR ALL UNPROTECTED FIELDS AND               
         NI    6(R4),X'7F'         PROTECTED FIELDS WITH SIGNIFICANT            
         TM    1(R4),X'20'         DATA (NOT ZEROS OR BLANKS)                   
         BZ    YES                                                              
         TM    SVTTYPE,X'20'       TEST ADDS TWX                                
         BO    YES                                                              
         SR    R6,R6                                                            
         IC    R6,7(R4)                                                         
         LTR   R6,R6                                                            
         BZ    NO                                                               
         BCTR  R6,R0                                                            
         EX    R6,ZERCOMP                                                       
         BE    NO                                                               
         CLI   8(R4),C' '                                                       
         BNE   YES                                                              
         LTR   R6,R6                                                            
         BZ    NO                                                               
         BCTR  R6,R0                                                            
         EX    R6,BLNKCOMP                                                      
         BE    NO                                                               
         SPACE 2                                                                
YES      OI    6(R4),X'80'                                                      
         TM    SVTTYPE,X'20'       TEST ADDS TWX                                
         BZ    *+18                                                             
         OC    SVSVCREQ,SVSVCREQ   TEST SVC REQ                                 
         BZ    *+8                                                              
         OI    6(R4),X'01'         SET MODIFY ON                                
         SPACE 2                                                                
NO       EQU   *                                                                
         IC    R5,0(R4)                                                         
         AR    R4,R5                                                            
         B     ALLOOP                                                           
         SPACE 3                                                                
TC4      LR    R5,R3                                                            
         MVI   0(R5),STX                                                        
         LA    R5,1(R5)                                                         
*                                                                               
         OC    INDICS,INDICS       TEST XMT ALL                                 
         B     TC5       ***** NOP *****                                        
         BNZ   *+12                YES - NO LINE ADDRESS REQUIRED               
         MVI   0(R5),AL1           LINE ADDRESS CHAR                            
         LA    R5,1(R5)                                                         
*                                                                               
TC5      MVI   0(R5),ESC           SET FMT MODE                                 
         MVI   1(R5),C'C'                                                       
         TM    SVTTYPE,X'20'       TEST ADDS TWX                                
         BZ    *+8                                                              
         MVI   1(R5),C'R'          SET FORMS MODE                               
         LA    R5,2(R5)                                                         
*                                                                               
TC5X     OC    INDICS,INDICS       TEST XMT ALL FIELDS                          
         BZ    TC6                 NO                                           
         TM    SVTTYPE,X'20'       TEST ADDS TWX                                
         BZ    TC5Y                                                             
         MVI   0(R5),FF            ERASE ALL                                    
         MVI   1(R5),ESC           SET MUST TAB ENABLE                          
         MVI   2(R5),C'H'                                                       
         LA    R5,3(R5)                                                         
         MVI   0(R5),ESC           SET ROW 0/COL 0 PROTECTED                    
         MVI   1(R5),C'Y'                                                       
         IC    R0,ADDRTAB                                                       
         STC   R0,2(R5)                                                         
         STC   R0,3(R5)                                                         
         MVI   4(R5),ESC                                                        
         MVI   5(R5),C'0'                                                       
         MVI   6(R5),C'H'                                                       
         LA    R5,7(R5)                                                         
         B     TC6                                                              
* SEND FORM FEED TO ROW 0/COL 0                                                 
TC5Y     MVI   0(R5),ESC                                                        
         MVI   1(R5),C'Y'                                                       
         IC    R0,ADDRTAB                                                       
         STC   R0,2(R5)                                                         
         STC   R0,3(R5)                                                         
         MVI   4(R5),FF                                                         
         LA    R5,5(R5)                                                         
*                                                                               
TC6      DS    0H                                                               
         EJECT                                                                  
* TRANSMIT THE FIELDS                                                           
*                                                                               
         LA    R4,64(R2)                                                        
         MVI   CURSW,0             RESET SWITCH                                 
*                                                                               
         OC    INDICS,INDICS       TEST TRANSMIT ALL                            
         BNZ   XMT8                YES                                          
*                                                                               
         LA    RF,=F'0'            POINT TO A ZERO                              
         BAS   RE,CURSOR           NO - SEND CURSOR ADDR AND FIRST FLD          
         TM    6(R4),X'80'         TEST XMT THIS TIME                           
         BZ    *+8                 NO - JUST ADDRESS CURSOR                     
         BAS   RE,TRANS                                                         
*                                                                               
XMT2     SR    R0,R0               RESET TAB COUNTER                            
*                                                                               
XMT4     SR    R7,R7                                                            
         IC    R7,0(R4)                                                         
         AR    R4,R7               POINT TO NEXT FIELD                          
         CLI   0(R4),0             TEST END                                     
         BE    XMT20                                                            
         OC    INDICS,INDICS       TEST TRANSMIT ALL                            
         BNZ   XMT8                YES                                          
*                                                                               
         OC    FORMLINE,FORMLINE   TEST FORMATTING                              
         BZ    XMT5                NO                                           
         LH    RE,2(R4)                                                         
         SRDA  RE,32                                                            
         D     RE,=F'80'                                                        
         LR    R7,RF               SAVE ROW                                     
         LA    RF,FORMLINE(RF)                                                  
         CLI   0(RF),1                                                          
         BL    XMT5                0 = NOT A FORMAT LINE                        
         BH    XMT8                2 = ALREADY CLEARED                          
* CLEAR THE LINE NOW                                                            
         MVI   0(RF),2                                                          
         MVI   0(R5),ESC           POSN CURSOR TO START OF LINE                 
         MVI   1(R5),C'Y'                                                       
         IC    RE,ADDRTAB(R7)      R7 HAS ROW                                   
         STC   RE,2(R5)                                                         
         MVC   3(1,R5),ADDRTAB     COL = 0                                      
         LA    R5,4(R5)                                                         
         TM    SVTTYPE,X'20'       TEST ADDS TWX                                
         BZ    XMT4A               NO                                           
         MVI   0(R5),ESC                                                        
         MVI   1(R5),X'92'         LOWER CASE K - ERASE TO E-O-S                
         LA    R5,2(R5)                                                         
         B     XMT8                                                             
XMT4A    MVI   0(R5),ESC                                                        
         MVI   1(R5),C'B'          ERASE ONLY WORKS IF FMT OFF                  
         MVI   2(R5),ESC                                                        
         MVI   3(R5),C'F'          ERASE TO END-OF-LINE                         
         MVI   4(R5),ESC                                                        
         MVI   5(R5),C'C'          FMT ON                                       
         LA    R5,6(R5)                                                         
         B     XMT8                                                             
*                                                                               
XMT5     TM    1(R4),X'20'         TEST PROTECTED                               
         BO    *+6                 FIELD IS PROTECTED                           
         BCTR  R0,0                INCREMENT COUNTER                            
         TM    6(R4),X'80'         TEST XMT THIS TIME                           
         BZ    XMT4                NO                                           
         TM    1(R4),X'20'         TEST PROTECTED                               
         BO    XMT8                FIELD IS PROTECTED                           
*                                                                               
* FIELD IS UNPROTECTED                                                          
         LPR   R0,R0               SET COUNTER POSITIVE                         
         CH    R0,=H'4'            IF GT 4, USE CURSOR ADDR                     
         BH    XMT8                                                             
         TM    SVTTYPE,X'20'       TEST ADDS TWX                                
         BO    XMT8                YES - CANT TAB IN FORMS MODE                 
*                                                                               
XMT6     MVI   0(R5),HT            USE TABS                                     
         LA    R5,1(R5)                                                         
         BCT   R0,*-8                                                           
         B     XMT10                                                            
*                                                                               
XMT8     TM    6(R4),X'80'         TEST TRANSMIT THIS TIME                      
         BZ    XMT2                                                             
*                                                                               
         BAS   RE,CURSOR                                                        
         OC    INDICS,INDICS       TEST XMT ALL                                 
         BZ    XMT10               NO                                           
         TM    SVTTYPE,X'20'       TEST ADDS TWX                                
         BZ    XMT10                                                            
         TM    1(R4),X'20'                                                      
         BO    XMT10                                                            
         CLI   0(R4),12            MORE THAN 4 DATA CHARS                       
         BNH   XMT10                                                            
         ZIC   RE,0(R4)                                                         
         SH    RE,=H'9'                                                         
         EX    RE,ZERCOMP                                                       
         BE    XMT9                                                             
         CLI   8(R4),C' '                                                       
         BNE   XMT10                                                            
         BCTR  RE,0                                                             
         EX    RE,BLNKCOMP                                                      
         BNE   XMT10                                                            
         EJECT                                                                  
* DONT NEED TO XMT BLANK FIELD BUT MUST CALC ADDR FOR E-O-F                     
XMT9     MVI   0(R5),ESC                                                        
         MVI   1(R5),C'0'                                                       
         MVI   2(R5),X'C1'                                                      
         TM    6(R4),X'01'         TEST MODIFY ON                               
         BZ    *+8                                                              
         MVI   2(R5),X'81'                                                      
         TM    1(R4),X'01'                                                      
         BZ    *+8                                                              
         MVI   2(R5),X'81'                                                      
         LA    R5,3(R5)                                                         
         MVI   0(R5),ESC                                                        
         MVI   1(R5),C'Y'                                                       
         LH    R6,2(R4)                                                         
         SRDA  R6,32                                                            
         D     R6,=F'80'                                                        
         IC    R7,ADDRTAB(R7)                                                   
         STC   R7,2(R5)                                                         
         ZIC   R0,0(R4)            GET LEN OF FIELD + HDR                       
         SH    R0,=H'8'            LEAVES FIELD LEN                             
         AR    R6,R0                                                            
         IC    R6,ADDRTAB(R6)                                                   
         STC   R6,3(R5)                                                         
         MVI   4(R5),ESC                                                        
         MVI   5(R5),C'0'                                                       
         MVI   6(R5),C'H'                                                       
         LA    R5,7(R5)                                                         
         NI    6(R4),X'7F'         RESET XMT BIT                                
         NI    4(R4),X'BF'         RESET 'PREV INPUT'                           
         B     *+8                                                              
*                                                                               
XMT10    BAS   RE,TRANS                                                         
*                                                                               
         B     XMT2                                                             
         EJECT                                                                  
* IF FORMATTING, NEED TO CLEAR LINES WE DIDN'T USE THIS TIME                    
*                                                                               
XMT20    OC    FORMLINE,FORMLINE   TEST FORMATTING                              
         BZ    CURS                NO                                           
         OC    INDICS,INDICS       TEST XMT ALL                                 
         BNZ   XMT26               YES                                          
*                                                                               
         SR    R4,R7               BACK UP TO LAST FIELD                        
         LH    R6,2(R4)            GET SCREEN ADDR                              
         SRDA  R6,32                                                            
         D     R6,=F'80'           GIVES ROW IN R7                              
         ZIC   R0,0(R2)            GET PREVIOUS LAST ROW                        
         B     XMT24                                                            
*                                                                               
XMT22    MVI   0(R5),ESC                                                        
         MVI   1(R5),C'Y'                                                       
         IC    RE,ADDRTAB(R7)      SET ROW                                      
         STC   RE,2(R5)                                                         
         MVC   3(1,R5),ADDRTAB     COL=0                                        
         TM    SVTTYPE,X'20'       TEST ADDS TWX                                
         BZ    XMT22A                                                           
         MVI   4(R5),ESC           SET ERASE TO E-O-S                           
         MVI   5(R5),X'92'         LOWER CASE K                                 
         LA    R5,6(R5)                                                         
         B     XMT26                                                            
XMT22A   MVI   4(R5),ESC                                                        
         MVI   5(R5),C'B'          FMT OFF                                      
         MVI   6(R5),ESC                                                        
         MVI   7(R5),C'F'          ERASE TO E-O-L                               
         LA    R5,8(R5)                                                         
*                                                                               
XMT24    LA    R7,1(R7)                                                         
         CR    R0,R7               MORE LINES LAST TIME                         
         BNH   XMT26               NO - DONE                                    
         CH    R7,=H'24'                                                        
         BNH   XMT22                                                            
*                                                                               
* SAVE LAST FORMATTED ROW NUMBER IN TWA                                         
*                                                                               
XMT26    LA    R0,24                                                            
         LA    R1,FORMLINE+23                                                   
         CLI   0(R1),0                                                          
         BNE   *+10                                                             
         BCTR  R1,0                                                             
         BCT   R0,*-10                                                          
         STC   R0,0(R2)                                                         
         TM    SVTTYPE,X'20'       TEST ADDS TWX                                
         BO    CURS                                                             
         MVI   0(R5),ESC                                                        
         MVI   1(R5),C'C'          TURN FMT ON                                  
         LA    R5,2(R5)                                                         
         EJECT                                                                  
*              POSITION THE CURSOR                                              
         SPACE 3                                                                
CURS     LA    R4,64(R2)                                                        
         SPACE 2                                                                
CURS2    CLI   0(R4),0                                                          
         BE    CURS6                                                            
         TM    1(R4),X'20'                                                      
         BO    CURS4                                                            
         TM    6(R4),X'40'                                                      
         BO    CURS8                                                            
         SPACE 2                                                                
CURS4    SR    R7,R7                                                            
         IC    R7,0(R4)                                                         
         AR    R4,R7                                                            
         B     CURS2                                                            
         SPACE 2                                                                
CURS6    LA    R4,64(R2)           CURSOR POSITIONING NOT SPECIFIED             
         SR    R7,R7                                                            
         IC    R7,0(R4)                                                         
         AR    R4,R7               DEFAULT TO THE SECOND SCREEN POS             
         SPACE 2                                                                
CURS8    MVI   CURSW,C'C'          SET SWITCH FOR NO SOF/EOF                    
         BAS   RE,CURSOR                                                        
         NI    6(R4),X'BF'         RESET CURSOR SWITCH  (X'40')                 
*                                                                               
         TM    SVTTYPE,X'20'       TEST ADDS TWX                                
         BZ    CURS10                                                           
         MVI   0(R5),ESC                                                        
         MVI   1(R5),C'C'          SET MODIFY MODE                              
         LA    R5,2(R5)                                                         
*                                                                               
CURS10   MVI   0(R5),ETX                                                        
         LA    R5,1(R5)                                                         
         SR    R5,R3                                                            
         SH    R3,=H'2'                                                         
         STH   R5,0(R3)            STORE L'OUTPUT BEHIND BUFFER                 
         SPACE 2                                                                
TCEXT    XMOD1 1                                                                
         EJECT                                                                  
*              ROUTINE TO SET THE CURSOR ADDRESS                                
         SPACE 3                                                                
CURSOR   MVI   0(R5),ESC                                                        
         MVI   1(R5),C'Y'                                                       
         LH    R6,2(R4)                                                         
         CLI   CURSW,0             IS THIS POISTION CURSOR                      
         BNE   CURSOR2             YES - IGNORE FIELD TYPE                      
         OC    FORMLINE,FORMLINE   TEST FORMATTING                              
         BZ    *+12                NO                                           
         CLI   0(RF),0             TEST FORMAT THIS LINE                        
         BNE   CURSOR1             YES                                          
         OC    INDICS,INDICS       TEST TRANSMIT ALL                            
         BNZ   CURSOR1             YES                                          
         TM    SVTTYPE,X'20'       TEST ADDS TWX                                
         BZ    CURSOR2             NO                                           
CURSOR1  TM    1(R4),X'20'         TEST PROTECTED                               
         BO    CURSOR2                                                          
         BCTR  R6,0                                                             
CURSOR2  SRDA  R6,32                                                            
         D     R6,=F'80'           ROW IN R7, COL IN R6                         
         IC    R7,ADDRTAB(R7)                                                   
         STC   R7,2(R5)                                                         
         IC    R6,ADDRTAB(R6)                                                   
         STC   R6,3(R5)                                                         
         LA    R5,4(R5)                                                         
         BR    RE                                                               
         EJECT                                                                  
ADDRTAB  DS    0F                                                               
*                                                                               
*ROW/COL*      *                                                                
*                                                                               
         DC    X'405A7F7B5B6C507D4D5D'                                          
**00-09**      * 40414243444546474849 *  MOD USASCII                            
*              * 20212223242526272829 *  ASCII                                  
         SPACE 1                                                                
         DC    X'5C4E6B604B61F0F1F2F3'                                          
**10-19**      * 4A4B4C4D4E4F50515253 *  MOD USASCII                            
*              * 2A2B2C2D2E2F30313233 *  ASCII                                  
         SPACE 1                                                                
         DC    X'F4F5F6F7F8F97A5E4C7E'                                          
**20-29**      * 5455565758595A5B5C5D *  MOD USADCII                            
*              * 3435363738393A3B3C3D *  ASCII                                  
         SPACE 1                                                                
         DC    X'6E6FC0C1C2C3C4C5C6C7'                                          
**30-39**      * 5E5FA0A1A2A3A4A5A6A7 *  MOD USADCII                            
*              * 3E3F4041424344454647 *  ASCII                                  
         SPACE 1                                                                
         DC    X'C8C9D1D2D3D4D5D6D7D8'                                          
**40-49**      * A8A9AAABACADAEAFB0B1 *  MOD USASCII                            
*              * 48494A4B4C4D4E4F5051 *  ASCII                                  
         SPACE 1                                                                
         DC    X'D9E2E3E4E5E6E7E8E9AD'                                          
**50-59**      * B2B3B4B5B6B7B8B9BABB *  MOD USASCII                            
*              * 52535455565758595A5B *  ASCII                                  
         SPACE 1                                                                
         DC    X'E0BD5F6D798182838485'                                          
**60-69**      * BCBDBEBFE0E1E2E3E4E5 *  MOD USASCII                            
*              * 5C5D5E5F606162636465 *  ASCII                                  
         SPACE 1                                                                
         DC    X'86878889919293949596'                                          
**70-79**      * E6E7E8E9EAEBECEDEEEF *  MOD USASCII                            
*              * 666768696A6B6C6D6E6F *  ASCII                                  
*                                                                               
* NON-STANDARD TRANSLATIONS  8F INTO BF                                         
         EJECT                                                                  
*              ROUTINE TO TRANSMIT THE FIELD                                    
         SPACE 3                                                                
TRANS    SR    R6,R6                                                            
         IC    R6,0(R4)                                                         
         SH    R6,=H'8'                                                         
         LTR   R6,R6                                                            
         BNZ   *+6                                                              
         DC    H'0'                                                             
         TM    SVTTYPE,X'20'       TEST ADDS TWX                                
         BO    TWXTR                                                            
*                                                                               
TWTRANSX DS    0H                                                               
         OC    FORMLINE,FORMLINE   TEST FORMATTING                              
         BZ    *+12                NO                                           
         CLI   0(RF),0             TEST THIS LINE FORMATTED                     
         BNE   TRANS1              YES                                          
         OC    INDICS,INDICS       TEST TRANSMIT ALL                            
         BZ    TRANS2              NO                                           
TRANS1   TM    1(R4),X'20'         TEST UNPROTECTED                             
         BO    TRANS2              FIELD IS PROTECTED                           
         MVI   0(R5),SOF           YES - MOVE IN SOF                            
         LA    R5,1(R5)                                                         
TRANS2   EQU   *                                                                
         LR    R7,R6                                                            
         BCTR  R6,0                                                             
         EX    R6,VARCLEAR                                                      
         EX    R6,VARMESS                                                       
         LA    R5,1(R5,R6)                                                      
*                                                                               
         OC    FORMLINE,FORMLINE   TEST FORMATTING                              
         BZ    *+12                                                             
         CLI   0(RF),0             TEST THIS LINE FORMATTED                     
         BNE   TRANS3              YES                                          
         OC    INDICS,INDICS       TEST TRANSMIT ALL                            
         BZ    TRANS4              NO                                           
TRANS3   TM    1(R4),X'20'         TEST UNPROTECTED                             
         BO    TRANS4              FIELD IS PROTECTED                           
         MVI   0(R5),EOF                                                        
         LA    R5,1(R5)                                                         
*                                                                               
TRANS4   NI    6(R4),X'7F'         RESET TRANSMIT BIT                           
         NI    4(R4),X'BF'         RESET 'PREV INPUT'                           
         BR    RE                                                               
         SPACE 2                                                                
ZERCOMP  OC    8(0,R4),8(R4)                                                    
BLNKCOMP CLC   9(0,R4),8(R4)                                                    
         SPACE 2                                                                
VARMESS  MVC   0(0,R5),8(R4)                                                    
VARCLEAR XC    0(0,R5),0(R5)                                                    
         EJECT                                                                  
TWXTR    MVI   DUB,0               RESET FORMAT FLAG                            
         OC    FORMLINE,FORMLINE   TEST FORMATTING                              
         BZ    *+12                NO                                           
         CLI   0(RF),0             THIS LINE FORMATTED                          
         BNE   TWXTR2              YES                                          
         OC    INDICS,INDICS       TEST XMT ALL                                 
         BZ    TWXTR4                                                           
*                                                                               
TWXTR2   MVI   DUB,C'F'            SET FORMAT FLAG                              
*                                                                               
TWXTR4   TM    1(R4),X'20'         TEST PROTECTED                               
         BO    TWXTR6                                                           
* SET UNP ATTRIBUTE                                                             
         MVI   0(R5),ESC                                                        
         MVI   1(R5),C'0'                                                       
         MVI   2(R5),C'A'                                                       
         TM    6(R4),X'01'         TEST MODIFIED                                
         BZ    *+8                                                              
         MVI   2(R5),X'81'                                                      
         TM    1(R4),X'01'                                                      
         BZ    *+8                                                              
         MVI   2(R5),X'81'                                                      
         LA    R5,3(R5)                                                         
*                                                                               
TWXTR6   CLI   DUB,C'F'            TEST FORMATTING                              
         BE    TWXTR10             YES                                          
* NOT FORMATTING - NEED TO ERASE PRESENT FIELD                                  
         MVI   0(R5),ESC                                                        
         MVI   1(R5),C'K'          SET ERASE TO EOF                             
         LA    R5,2(R5)                                                         
*                                                                               
* CALCULATE DATA LEN PRESENT                                                    
*                                                                               
TWXTR10  DC    0H'0'                                                            
         ZIC   R7,0(R4)                                                         
         LA    R7,0(R4,R7)                                                      
         BCTR  R7,0                POINT TO LAST CHAR IN FIELD                  
         CLI   0(R7),C' '                                                       
         BH    *+8                                                              
         BCT   R7,*-8                                                           
*                                                                               
         LA    R6,8(R4)            SET A(DATA)                                  
         SR    R7,R6               GIVES LEN-1 TO XMT                           
         BM    TWXTR15                                                          
*                                                                               
TWXTR12  EX    R7,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R5),8(R4) *EXECUTED*                                         
         LA    R5,1(R5,R7)                                                      
         EJECT                                                                  
* IF FORMATTING, MUST SEND E-O-F                                                
*                                                                               
TWXTR15  CLI   DUB,C'F'            TEST FORMAT FLAG                             
         BNE   TWXTRX                                                           
*                                                                               
TWXTR25  DC    0H'0'                                                            
         MVI   0(R5),ESC                                                        
         MVI   1(R5),C'Y'                                                       
         LH    R6,2(R4)                                                         
         SRDA  R6,32                                                            
         D     R6,=F'80'                                                        
         IC    R7,ADDRTAB(R7)                                                   
         STC   R7,2(R5)                                                         
         ZIC   R0,0(R4)            GET LEN OF FIELD + HDR                       
         SH    R0,=H'8'            LEAVES FIELD LEN                             
         AR    R6,R0                                                            
         C     R6,=F'80'           IF COL GT 80, SET EOF IN COL 80              
         BNH   *+8                                                              
         L     R6,=F'80'                                                        
         IC    R6,ADDRTAB(R6)                                                   
         STC   R6,3(R5)                                                         
         MVI   4(R5),ESC                                                        
         MVI   5(R5),C'0'                                                       
         MVI   6(R5),C'H'                                                       
         LA    R5,7(R5)                                                         
*                                                                               
TWXTRX   B     TRANS4                                                           
         EJECT                                                                  
*              ROUTINE TO REFORMAT TWA FOR NEW FIELDS                           
         SPACE 3                                                                
REFORM   NTR1                                                                   
*                                  R2=A(TWA)                                    
         LA    R4,64(R2)                                                        
         SR    R5,R5                                                            
         SPACE 2                                                                
RE2      CLI   0(R4),0             END OF SCREEN                                
         BE    XIT                                                              
         LH    RE,2(R4)                                                         
         SRDA  RE,32                                                            
         D     RE,=F'80'                                                        
         LA    RF,FORMLINE(RF)     IS THIS FIELD ON A LINE THAT IS              
         CLI   0(RF),0             GOING TO BE REFORMATTED                      
         BE    RE4                                                              
         OI    6(R4),X'80'         TURN ON TRANSMIT BIT                         
         SPACE 2                                                                
RE4      IC    R5,0(R4)                                                         
         AR    R4,R5                                                            
         B     RE2                                                              
         SPACE 2                                                                
XIT      XIT1                                                                   
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
*                  DSECT FOR OUTPUT TRANSLATOR                                  
         SPACE 3                                                                
OUTD     DSECT                                                                  
DUB      DS    D                                                                
CHECK    DS    F                                                                
LASTTYPE DS    C                                                                
         DS    1C                  SPARE                                        
SVSVCREQ DS    CL2                                                              
ANYROOM  DS    F                                                                
LINE     DS    PL2                                                              
COL      DS    PL2                                                              
FLINE    DS    PL2                                                              
FCOL     DS    PL2                                                              
INDICS   DS    CL2                                                              
CURSW    DS    C                                                                
SVTTYPE  DS    C                                                                
FORMLINE DS    CL24                                                             
         EJECT                                                                  
       ++INCLUDE FAUTL                                                          
         EJECT                                                                  
*FASCREQUS                                                                      
       ++INCLUDE FASCREQUS                                                      
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'045FATOICC   05/01/02'                                      
         END                                                                    
