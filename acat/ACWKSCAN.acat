*          DATA SET ACWKSCAN   AT LEVEL 005 AS OF 05/01/02                      
*CATALP ACWKSCAN                                                                
         TITLE 'SCAN FACWORK FOR FULL CONDITION'                                
ACWKSCAN CSECT                                                                  
*-------------------------------------------------------------------*           
*  WILL SCAN FACWK FOR FILE AVAILABILITY                            *           
*  YOU NEED TO PASS THE ADDRESS OF DATAMGR IN PARAMETER 1           *           
*                                                                   *           
*  WILL PASS BACK TO YOU:                                           *           
*       PARAMETER 1: BYTE 0      X'80' = FILE IS FULL               *           
*                    BYTE 3-4    HALFWORD CONTAINING TOTAL NUMBER   *           
*                                OF FILES IN THIS FACWK             *           
*       PARAMETER 2: BYTE 3-4    HALFWORD CONTAINING TOTAL NUMBER   *           
*                                AVAILABLE ON THIS FACWK            *           
*-------------------------------------------------------------------*           
         PRINT NOGEN                                                            
         NMOD1 WRKWKX-WRKWKD,**ACWS**                                           
         USING WRKWKD,RC                                                        
                                                                                
         ST    R1,APARM               ADDRESS OF PARAMETER LIST                 
         ICM   R5,15,0(R1)            GET ADDRESS OF DATAMGR                    
         ST    R5,ADATAMGR                                                      
         LHI   RF,CXREC-WRKWKD                                                  
         AR    RF,RC                                                            
         ST    RF,ACXREC                                                        
         LHI   RF,WKFREC-WRKWKD                                                 
         AR    RF,RC                                                            
         ST    RF,AWKFREC                                                       
         MVC   WKFILE,=CL8'FACWRK'                                              
         XC    CITOTS,CITOTS          CLEAR BLOCK FOR BUFFER CALL               
         XC    FACTOTL,FACTOTL        CLEAR TOTAL FILES                         
         XC    FACAVAIL,FACAVAIL      CLEAR TOTAL AVAILABLE FILES               
         LA    R5,L'WKINDEX           SET LENGTH OF INDEX ENTRY                 
         STH   R5,CINDXLN                                                       
                                                                                
         GOTO1 ADATAMGR,DMCB,=C'BUF',WKFILE,WKFNDX,AWKFREC,ACXREC               
         CLI   8(R1),0                INITIALIZE BUFFER                         
         BE    *+6                                                              
         DC    H'0'                                                             
         L     RE,ACXREC                                                        
         MVC   AWKTAB(52),0(RE)       SAVE FILE STATS                           
                                                                                
         BAS   RE,CXLOOPI                                                       
                                                                                
         USING WKRECD,R5              R5=A(WKFILE INDEX ENTRY)                  
SRCH0    BAS   RE,GETXAD                                                        
         GOTO1 ADATAMGR,DMCB,(0,DMREAD),WKFILE,CXADDR,ACXREC                    
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
                                                                                
SRCH2    DS    0H                                                               
         CLI   WKSTAT,WKSTPU          TEST IF PURGED                            
         BE    SRCHX                                                            
SRCH2A   TM    WKSTAT,WKSTTE          TEST IF TEMPORARY                         
         BZ    SRCH2B                                                           
         LH    R1,CIX1                BUMP PART1 NOTAVAIL                       
         LA    R1,1(R1)                                                         
         STH   R1,CIX1                                                          
         B     SRCHX                                                            
SRCH2B   ZIC   R0,WKAGES              GET SIZE OF FILE                          
         SH    R0,=H'1'                                                         
         BP    *+6                                                              
         SR    R0,R0                                                            
         TM    WKSTAT,WKSTKE                                                    
         BZ    SRCH2C                                                           
         LH    R1,CIK1                BUMP PART1 KEEP                           
         LA    R1,1(R1)                                                         
         STH   R1,CIK1                                                          
         LH    R1,CIK2                BUMP PART2 KEEP                           
         AR    R1,R0                                                            
         STH   R1,CIK2                                                          
         B     SRCH2D                                                           
SRCH2C   TM    WKSTAT,WKSTAC+WKSTHO                                             
         BZ    SRCH2D                                                           
         LH    R1,CIX1                BUMP PART1 NOTAVAIL                       
         LA    R1,1(R1)                                                         
         STH   R1,CIX1                                                          
         LH    R1,CIX2                BUMP PART2 NOTAVAIL                       
         AR    R1,R0                                                            
         STH   R1,CIX2                                                          
SRCH2D   EQU   *                                                                
                                                                                
SRCHX    BAS   RE,CXLOOPX             BUMP TO NEXT INDEX ENTRY                  
         B     SRCH2                  NEXT ENTRY                                
         B     SRCH0                  END OF PAGE                               
         B     SORT                   END OF FILE                               
                                                                                
SORT     DS    0H                                                               
SIZE     SR    RF,RF                                                            
         ICM   RF,3,CICITOT           DISPLAY FILE SIZE INFO                    
         SH    RF,CICINDX                                                       
         SH    RF,CIK1                                                          
         SH    RF,CIX1                                                          
         STCM  RF,3,CIA1              SET PART1 AVAIL CIS                       
         LH    RF,CJCITOT                                                       
         SH    RF,CIK2                                                          
         SH    RF,CIX2                                                          
         STH   RF,CIA2                SET PART2 AVAIL CIS                       
                                                                                
         ICM   RF,3,CICITOT                                                     
         AH    RF,CJCITOT                                                       
         STH   RF,FACTOTL                                                       
         ICM   RF,3,CIA1                                                        
         AH    RF,CIA2                                                          
         STH   RF,FACAVAIL                                                      
                                                                                
         L     R1,APARM                                                         
         XC    0(8,R1),0(R1)                                                    
         MVC   2(2,R1),FACTOTL                                                  
         MVC   6(2,R1),FACAVAIL                                                 
         OC    FACAVAIL,FACAVAIL                                                
         BNZ   *+8                                                              
         MVI   0(R1),X'80'                                                      
         B     EXIT                                                             
                                                                                
EXIT     XMOD1 1                                                                
         EJECT                                                                  
                                                                                
*        ROUTINE TO INIT INDEX SEARCH LOOP - SETS R5=A(FIRST ENTRY)             
CXLOOPI  XC    CXPAGE,CXPAGE                                                    
         LH    R5,CICINDX                                                       
         B     *+14                                                             
CXLOOPJ  MVC   CXPAGE,CJPAGE                                                    
         LH    R5,CJENTRY                                                       
         STH   R5,CXENTRY                                                       
         MH    R5,CINDXLN                                                       
         A     R5,ACXREC                                                        
         BR    RE                                                               
                                                                                
* ROUTINE TO TERMINATE INDEX SERCH LOOP - NEXT ENTRY RETURN - 0(RE)             
*                                       - PAGE FAULT RETURN - 4(RE)             
*                                       - END OF NDX RETURN - 8(RE)             
CXLOOPX  LH    RF,CXENTRY                                                       
         LA    RF,1(RF)                                                         
         STH   RF,CXENTRY                                                       
         CH    RF,CIENTRYS                                                      
         BNL   CXLOOPX1                                                         
         AH    R5,CINDXLN                                                       
         CLC   0(2,R5),=X'FFFF'                                                 
         BNER  RE                                                               
         B     8(RE)                  END OF INDEX                              
CXLOOPX1 XC    CXENTRY,CXENTRY                                                  
         LH    RF,CXPAGE                                                        
         LA    RF,1(RF)                                                         
         STH   RF,CXPAGE                                                        
         LA    R5,CXREC                                                         
         B     4(RE)                  END OF PAGE                               
         EJECT                                                                  
                                                                                
*        INDEX PAGE/ENTRY TO CONTROL INTERVAL DISK ADDR                         
GETCAD   LH    RF,CXPAGE              CAD=(CXP*ENT+CXE)*TRKS + 1                
         CLC   CXPAGE(4),CJPAGE                                                 
         BNL   GETCAD1                                                          
         MH    RF,CIENTRYS                                                      
         AH    RF,CXENTRY                                                       
         MH    RF,CITRKS                                                        
         LA    RF,1(RF)                                                         
         LH    R0,CITRKS                                                        
         B     GETCAD2                                                          
GETCAD1  SH    RF,CJPAGE              CI IS IN PART 2 OF INDEX                  
         MH    RF,CIENTRYS                                                      
         AH    RF,CXENTRY                                                       
         SH    RF,CJENTRY                                                       
         MH    RF,CJTRKS                                                        
         AH    RF,CJSTTRK                                                       
         LH    R0,CJTRKS                                                        
GETCAD2  SLL   RF,16                                                            
         ST    RF,CIADDR                                                        
         MVI   CIADDR+2,1                                                       
         SRL   RF,16                                                            
         AR    RF,R0                                                            
         BCTR  RF,0                                                             
         SLL   RF,16                                                            
         ST    RF,CIADDRX                                                       
         MVC   CIADDRX+2(1),CIHIREC+1                                           
         BR    RE                                                               
         EJECT                                                                  
                                                                                
*        INDEX PAGE/ENTRY TO INDEX DISK ADDR                                    
GETXAD   ST    RE,CISAVRE             XADT=CXP/TRKS + 1                         
         SR    RE,RE                  XADB=REM + 1                              
         LH    RF,CXPAGE                                                        
         LH    R0,CIHIREC                                                       
         DR    RE,R0                                                            
         LA    RE,1(RE)                                                         
         LA    RF,1(RF)                                                         
         STH   RF,CXADDR                                                        
         STC   RE,CXADDR+2                                                      
         MVI   CXADDR+3,0                                                       
         L     RE,CISAVRE                                                       
         BR    RE                                                               
         EJECT                                                                  
                                                                                
*        CONTROL INTERVAL DISK ADDR TO INDEX PAGE/ENTRY                         
GETXPE   ST    RE,CISAVRE             CXP=(CAD-1)/(CXE*TRKS)                    
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         ICM   RF,3,CIADDR                                                      
         CLM   RF,3,CJSTTRK                                                     
         BNL   GETXPE1                                                          
         BCTR  RF,0                                                             
         LH    R0,CITRKS                                                        
         MH    R0,CIENTRYS                                                      
         DR    RE,R0                                                            
         STH   RF,CXPAGE                                                        
         SRDL  RE,32                                                            
         LH    R0,CITRKS                                                        
         DR    RE,R0                                                            
         STH   RF,CXENTRY                                                       
         B     GETXPEX                                                          
GETXPE1  SH    RF,CJSTTRK             CI IS IN PART 2 OF INDEX                  
         LH    R0,CJTRKS                                                        
         MH    R0,CIENTRYS                                                      
         DR    RE,R0                                                            
         AH    RF,CJPAGE                                                        
         STH   RF,CXPAGE                                                        
         SRDL  RE,32                                                            
         LH    R0,CJTRKS                                                        
         DR    RE,R0                                                            
         AH    RF,CJENTRY                                                       
         CH    RF,CIENTRYS                                                      
         BL    *+20                                                             
         SH    RF,CIENTRYS            ADJUST FOR PART 2 INDEX OFFSET            
         LH    RE,CXPAGE                                                        
         LA    RE,1(RE)                                                         
         STH   RE,CXPAGE                                                        
         STH   RF,CXENTRY                                                       
GETXPEX  L     RE,CISAVRE                                                       
         BR    RE                                                               
         EJECT                                                                  
                                                                                
DMREAD   DC    CL8'DMREAD'                                                      
ADATAMGR DS    A                                                                
FACTOTL  DS    H                                                                
FACAVAIL DS    H                                                                
                                                                                
         LTORG                                                                  
         EJECT                                                                  
                                                                                
*SRWRKWK                                                                        
       ++INCLUDE SRWRKWK                                                        
         EJECT                                                                  
*DMWRKRD                                                                        
       ++INCLUDE DMWRKRD                                                        
         EJECT                                                                  
*DMWRKRS                                                                        
       ++INCLUDE DMWRKRS                                                        
         EJECT                                                                  
*DDCOMFACS                                                                      
       ++INCLUDE DDCOMFACS                                                      
         EJECT                                                                  
*FASRPARM                                                                       
       ++INCLUDE FASRPARM                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'005ACWKSCAN  05/01/02'                                      
         END                                                                    
