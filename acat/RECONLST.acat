*          DATA SET RECONLST   AT LEVEL 084 AS OF 09/14/99                      
*CATALP RECONLST                                                                
         TITLE 'RECONLST - CONTRACT LISTING IO MODULE'                          
*                                                                               
*********************************************************************           
*                                                                   *           
*  PARAMETERS FOR CALLING RECONLST                                  *           
*                                                                   *           
* ----------------------------------------------------------------- *           
*                                                                   *           
*   P1 -  BYTE 0 - INPUT OPTIONS                                    *           
*                  X'40' READ RECORDS FOR UPDATE                    *           
*                  X'20' OFFLINE REQUEST (DON'T PREVAL IO COUNT)    *           
*                  X'10' DON'T RETURN RECORDS (JUST TOTALS)         *           
*                                                                   *           
*         BYTES 1-3: A(RECONLSTD) PARAMETER BLOCK                   *           
*                                                                   *           
*   P2 -  NOT USED                                                  *           
*   P3 -  NOT USED                                                  *           
*                                                                   *           
*   P4 -  A(RFBLOCK)  - REPFACS PARAMETER BLOCK                     *           
*   P5 -  V(REPFACS)                                                            
*                                                                   *           
*                                                                   *           
* ----------------------------------------------------------------- *           
*                                                                   *           
*                                                                   *           
* UPDATE HISTORY:                                                   *           
*                                                                   *           
* 23JUL99 RHV --- VOILA!                                            *           
*                                                                   *           
*                    **  END TOMBSTONE  **                          *           
*********************************************************************           
*                                                                               
RECONLST CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKDX-WORKD,*RECONL*,RR=R8                                      
         USING WORKD,RC                                                         
         ST    R8,RELO                                                          
         MVC   INOPTS,0(R1)        SAVE INPUT OPTIONS                           
         L     R8,0(R1)            PASS ADDR OF RECONLSTD                       
         USING RKLD,R8                                                          
*                                                                               
         L     R2,12(R1)           A(RFBLOCK)                                   
         MVC   RFBLOCK,0(R2)                                                    
         MVC   VREPFACS,16(R1)                                                  
         L     R2,ACOMFACS                                                      
         USING COMFACSD,R2                                                      
         MVC   DATAMGR,CDATAMGR                                                 
         MVC   DATCON,CDATCON                                                   
         MVC   GETDAY,CGETDAY                                                   
         MVC   ADDAY,CADDAY                                                     
         DROP  R2                                                               
*                                                                               
         OC    RKLKEY,RKLKEY       ALREADY HAVE KEY                             
         BNZ   MAIN050             SKIP KEY BUILD                               
         BAS   RE,NEWKEY           BUILD NEW KEY                                
         BAS   RE,HIGH                                                          
         B     MAIN060                                                          
*                                                                               
MAIN050  DS    0H                                                               
         BAS   RE,SEQ                                                           
*                                                                               
MAIN060  DS    0H                                                               
         BAS   RE,CHKKEY                                                        
         BE    MAIN080                                                          
         BL    MAIN050                                                          
         BH    EXITL                                                            
*                                                                               
MAIN080  DS    0H                                                               
         BAS   RE,GETREC                                                        
*                                                                               
         BAS   RE,FILTER                                                        
         BNE   MAIN050             REJECTED - NEXT RECORD                       
*                                                                               
         BAS   RE,TOTAL            UPDATE TOTALS & BUCKETS                      
         TM    INOPTS,X'10'        RETURN TO CALLER?                            
         BO    MAIN050             NO - NEXT RECORD                             
         B     EXITOK              YES - RETURN TO CALLER CC:=                  
***********************************************************************         
* NEWKEY - BUILD NEW KEY                                              *         
***********************************************************************         
NEWKEY   NTR1                                                                   
         MVI   MODE,MNEWKEY                                                     
         MVI   KEYID,0             RESET KEY,SCORE,ORDER                        
         LA    R1,1                                                             
         ST    R1,KEYSCORE                                                      
         ST    R1,KEYORDER                                                      
         LA    R2,KEYTAB                                                        
*                                                                               
NKEY020  DS    0H                  CHECK INTEGRITY OF TABLE ENTRY               
         SR    R1,R1                                                            
         ZIC   R5,1(R2)            KEY LEN                                      
         ZIC   R3,2(R2)            KEY TYPE LEN                                 
         SR    R5,R3                                                            
         LA    R3,3(R3,R2)         OVERHEAD + TYPE LEN = 1ST KEY FIELD          
NKEY030  DS    0H                                                               
         CLI   0(R3),X'FF'         END OF KEY MARKER?                           
         BNE   NKEY035                                                          
         LTR   R5,R5                                                            
         BZ    NKEY040                                                          
         DC    H'0'                TABLE PROBLEM                                
NKEY035  DS    0H                                                               
         CLI   0(R3),CONST         CONST FIELD?                                 
         BNE   *+12                                                             
         LA    R1,1                CONST IS 1 BYTE LONG                         
         B     *+8                                                              
         IC    R1,1(R3)            KEY FIELD LENGTH                             
         SR    R5,R1                                                            
         LA    R3,2(R3)            NEXT KEY FIELD                               
         B     NKEY030                                                          
*                                                                               
NKEY040  DS    0H                                                               
         BAS   RE,EVALKEY          EVALUATE KEY SCORE                           
*                                                                               
         LA    R2,1(R3)            NEXT KEY IN TABLE                            
         CLI   0(R2),X'FF'         END OF TABLE?                                
         BNE   NKEY020                                                          
*                                                                               
         TM    INOPTS,X'20'        BYPASS IO COUNT PREVALIDATION?               
         BO    NKEY100             YES                                          
         CLC   KEYSCORE,=AL4(16)                                                
         BL    EXITL               KEY TOO WEAK                                 
*                                                                               
NKEY100  DS    0H                  BUILD KEY                                    
         LA    R2,KEYTAB                                                        
         B     *+8                                                              
NKEY110  DS    0H                                                               
         BAS   RE,NEXTKEY                                                       
         CLI   0(R2),X'FF'                                                      
         BNE   *+6                                                              
         DC    H'0'                                                             
         CLC   KEYID,0(R2)                                                      
         BNE   NKEY110                                                          
*                                                                               
         XC    RKLKEY,RKLKEY                                                    
         LA    R3,RKLKEY                                                        
         ZIC   R1,2(R2)            KEY TYPE LEN                                 
         BCTR  R1,0                                                             
         EX    R1,*+4                                                           
         MVC   0(0,R3),3(R2)       KEY TYPE                                     
         LA    R3,1(R1,R3)                                                      
         LA    R2,4(R1,R2)         1ST KEY FIELD                                
NKEY120  DS    0H                                                               
         CLI   0(R2),CONST                                                      
         BNE   NKEY125                                                          
         SR    R1,R1                                                            
         MVC   0(1,R3),1(R2)                                                    
         B     NKEY130                                                          
NKEY125  DS    0H                                                               
         ZIC   R1,1(R2)            FIELD LEN                                    
         BCTR  R1,0                                                             
         CLI   0(R2),0                                                          
         BE    NKEY130                                                          
         MVC   INFILT,0(R2)        FILTER FOR LOOKUP                            
         BAS   RE,GETFILT          POINTS R4 TO FILTER TABLE ENTRY              
         USING FLTRD,R4                                                         
         CLI   FLTRELEM,X'FF'      BYPASS                                       
         BE    NKEY128                                                          
         ZICM  R6,FLTRRKLD,2       FILTER DISPL IN RKLD                         
         BZ    NKEY130                                                          
         LA    R5,RKLD                                                          
         AR    R5,R6                                                            
         EX    R1,*+4                                                           
         MVC   0(0,R3),0(R5)                                                    
NKEY128  DS    0H                                                               
         BAS   RE,HOOK                                                          
NKEY130  DS    0H                                                               
         LA    R3,1(R1,R3)                                                      
         LA    R2,2(R2)                                                         
         CLI   0(R2),X'FF'                                                      
         BNE   NKEY120                                                          
         B     EXITOK                                                           
***********************************************************************         
* EVALKEY - EVALUATE AN INDIVIDUAL KEY SCORE                          *         
***********************************************************************         
*                                                                     *         
* INPUT:  R2 = A(KEYTAB ENTRY TO BE EVALUATED)                        *         
* OUTPUT: KEYID = KEY ID NUM OF HIGHEST RANKED KEY SO FAR             *         
*         KEYSCORE = SCORE OF HIGHEST RANKED KEY SO FAR               *         
*         KEYORDER = ORDER SCORE OF HIGHEST RANKED KEY SO FAR         *         
*                                                                     *         
* ------------------------------------------------------------------- *         
EVALKEY  NTR1                                                                   
         ZIC   R3,1(R2)            KEY LENGTH                                   
         LA    RF,3(R3,R2)         END OF KEY MARKER                            
         CLI   0(RF),X'FF'                                                      
         BNE   *+6                                                              
         DC    H'0'                TABLE PROBLEM                                
*                                                                               
         LA    R3,2(R2)            BEGINNING OF KEY                             
         ZIC   R1,2(R2)            KEY TYPE LEN                                 
         LA    R3,3(R1,R3)         FIRST KEY FIELD                              
*                                                                               
         LA    R1,1                INITIALIZE:                                  
         ST    R1,FULL             KEY SCORE ACCUMULATOR                        
         ST    R1,FULL2            ORDER SCORE ACCUMULATOR                      
         LA    R5,64               MAX INDIVIDUAL ORDER SCORE                   
*                                                                               
EKEY050  DS    0H                                                               
         CLI   0(R3),0             NULLS IN KEY?                                
         BE    EKEY060             YES - SKIP THEM                              
         CLI   0(R3),CONST         CONSTANT IN KEY?                             
         BE    EKEY060             YES - SKIP                                   
         MVC   INFILT,0(R3)        PASS KEY FIELD                               
         BAS   RE,GETFILT          TO LOOKUP FILTER                             
         USING FLTRD,R4            R4 = A(FILTER TABLE ENTRY)                   
         ZICM  R6,FLTRRKLD,2       FILTER DISPL IN RKLD                         
         BZ    EKEY060                                                          
         LA    R1,RKLD                                                          
         AR    R6,R1                                                            
         ZIC   R1,FLTRLEN                                                       
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         OC    0(0,R6),0(R6)       DO WE HAVE A FILTER?                         
         BZ    EKEY060             NO                                           
*                                                                               
         MVI   HALF,0                                                           
         MVC   HALF+1(1),FLTRSCR   ORDER SCORE FOR KEY FIELD                    
         L     R1,FULL             RUNNING SCORE                                
         MH    R1,HALF                                                          
         ST    R1,FULL             UPDATED SCORE                                
         DROP  R4                                                               
*                                                                               
         L     R1,FULL2            RUNNING ORDER SCORE                          
         AR    R1,R5                                                            
         ST    R1,FULL2            UPDATED ORDER SCORE                          
*                                                                               
EKEY060  DS    0H                                                               
         BCTR  R5,0                                                             
         LA    R3,2(R3)            NEXT FILTER IN KEY                           
         CLI   0(R3),X'FF'         END OF KEY?                                  
         BNE   EKEY050             NO - PROCESS NEXT FIELD                      
*                                                                               
         CLC   KEYSCORE,FULL       CHECK KEY SCORE                              
         BH    EXIT                                                             
         BL    EKEY080                                                          
*                                                                               
         CLC   KEYORDER,FULL2      CHECK ORDER SCORE TO BREAK TIE               
         BNL   EXIT                                                             
*                                                                               
EKEY080  DS    0H                                                               
         MVC   KEYSCORE,FULL                                                    
         MVC   KEYORDER,FULL2                                                   
         MVC   KEYID,0(R2)                                                      
         B     EXIT                                                             
***********************************************************************         
* GETFILT - READS FILTER TABLE FOR FILTER                                       
***********************************************************************         
*                                                                               
*  INPUT:   INFILT = VALID FILTER NUMBER FOR LOOKUP                             
*  OUTPUT:  R4 = A(FILTER TABLE ENTRY) - USE FLTRD                              
*                                                                               
* ------------------------------------------------------------------- *         
GETFILT  DS    0H                                                               
         LA    R4,FLTRTAB                                                       
GFILT020 CLI   0(R4),X'FF'                                                      
         BNE   *+6                                                              
         DC    H'0'                INFILT CONTAINS INVALID FILTER NUM           
         CLC   INFILT,0(R4)                                                     
         BER   RE                                                               
         LA    R4,FLTRLNQ(R4)                                                   
         B     GFILT020                                                         
***********************************************************************         
* NEXTKEY - POINTS R2 TO NEXT KEY ENTRY IN KEYTAB                               
***********************************************************************         
*                                                                               
*  INPUT:   R2 = A(VAILD KEYTAB ENTRY)                                          
*  OUTPUT:  R2 = A(NEXT KEYTAB ENTRY)                                           
*                                                                               
*  NOTE: USES RF                                                                
* ------------------------------------------------------------------- *         
NEXTKEY  DS    0H                                                               
         ZIC   RF,2(R2)            KEYTYP LEN                                   
         LA    R2,3(RF,R2)         1ST FIELD ENTRY                              
NEXTK030 DS    0H                                                               
         CLI   0(R2),X'FF'                                                      
         BE    NEXTK050                                                         
         LA    R2,2(R2)                                                         
         B     NEXTK030                                                         
NEXTK050 DS    0H                                                               
         LA    R2,1(R2)                                                         
         BR    RE                                                               
***********************************************************************         
* CHKKEY - CHECK RETRIEVED KEY AGAINST FILTER(S)                                
***********************************************************************         
*                                                                               
* INPUT:   RKLKEY = KEY                                                         
*                                                                               
* OUTPUT:  CC = EQ - KEY MATCH                                                  
*          CC = L  - NO KEY MATCH                                               
*          CC = H  - NO MATCH, LAST KEY READ                                    
*                                                                               
* ---------------------------------------------------------------------         
CHKKEY   NTR1                                                                   
         MVI   MODE,MCHKKEY                                                     
         LA    R2,KEYTAB           FIND KEY IN KEYTAB                           
         B     *+8                                                              
CHKEY110 DS    0H                                                               
         BAS   RE,NEXTKEY                                                       
         CLI   0(R2),X'FF'                                                      
         BNE   *+6                                                              
         DC    H'0'                                                             
         CLC   KEYID,0(R2)                                                      
         BNE   CHKEY110                                                         
*                                                                               
         LA    R3,RKLKEY                                                        
         ZIC   R1,2(R2)            KEY TYPE LEN                                 
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         CLC   0(0,R3),3(R2)       KEY TYPE                                     
         BNE   EXITH               DONE                                         
*                                                                               
         MVI   BYTE,X'FF'                                                       
         LA    R3,1(R1,R3)                                                      
         LA    R2,4(R1,R2)         1ST KEY FIELD                                
*                                                                               
CHKEY120 DS    0H                                                               
         CLI   0(R2),CONST                                                      
         BNE   CHKEY125                                                         
         SR    R1,R1                                                            
         CLC   0(1,R3),1(R2)                                                    
         B     CHKEY130                                                         
CHKEY125 DS    0H                                                               
         ZIC   R1,1(R2)            FIELD LEN                                    
         BCTR  R1,0                                                             
         CLI   0(R2),0                                                          
         BE    CHKEY140                                                         
         MVC   INFILT,0(R2)        FILTER FOR LOOKUP                            
         BAS   RE,GETFILT          POINTS R4 TO FILTER TABLE ENTRY              
         USING FLTRD,R4                                                         
         ZICM  R6,FLTRRKLD,2       FILTER DISPL IN RKLD                         
         BNZ   *+12                                                             
         MVI   BYTE,0                                                           
         B     CHKEY140                                                         
         LA    R5,RKLD                                                          
         AR    R5,R6                                                            
         EX    R1,*+8                                                           
         B     *+10                                                             
         OC    0(0,R5),0(R5)                                                    
         BNZ   *+12                                                             
         MVI   BYTE,0                                                           
         B     CHKEY140                                                         
         CLI   FLTRELEM,X'FF'      SKIP AUTO COMPARE?                           
         BE    CHKEY135            YES - PROCEED TO HOOK                        
         EX    R1,*+8                                                           
         B     *+10                                                             
         CLC   0(0,R3),0(R5)                                                    
CHKEY130 DS    0H                                                               
         BE    CHKEY140                                                         
         BL    EXITL                                                            
         CLI   BYTE,X'FF'                                                       
         BE    EXITH                                                            
         B     EXITL                                                            
CHKEY135 DS    0H                                                               
         BAS   RE,HOOK                                                          
         BE    CHKEY140                                                         
         BL    EXITL                                                            
         CLI   BYTE,X'FF'                                                       
         BE    EXITH                                                            
         B     EXITL                                                            
CHKEY140 DS    0H                                                               
         LA    R3,1(R1,R3)                                                      
         LA    R2,2(R2)                                                         
         CLI   0(R2),X'FF'                                                      
         BNE   CHKEY120                                                         
         B     EXITOK                                                           
***********************************************************************         
* FILTER - CHECK RETRIEVED RECORD AGAINST FILTER(S)                             
***********************************************************************         
*                                                                               
* INPUT:   RKLAIO = A(RECORD)                                                   
*                                                                               
* OUTPUT:  CC = EQ - KEEP RECORD                                                
*          CC = NEQ - REJECT RECORD                                             
*                                                                               
* ---------------------------------------------------------------------         
FILTER   NTR1                                                                   
         MVI   MODE,MFILTER                                                     
*                                                                               
         LA    R4,FLTRTAB                                                       
         USING FLTRD,R4                                                         
FILT020  DS    0H                                                               
         CLI   0(R4),X'FF'         END OF FILTER TABLE?                         
         BE    EXITOK              KEEP RECORD                                  
*                                                                               
         ZICM  R6,FLTRRKLD,2                                                    
         LA    R5,RKLD                                                          
         AR    R5,R6                                                            
         ZIC   R1,FLTRLEN                                                       
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         OC    0(0,R5),0(R5)                                                    
         BZ    FILT200             NO FILTER                                    
*                                                                               
         LA    R2,KEYTAB                                                        
         B     *+8                                                              
FILT050  DS    0H                                                               
         BAS   RE,NEXTKEY                                                       
         CLI   0(R2),X'FF'                                                      
         BNE   *+6                                                              
         DC    H'0'                                                             
         CLC   KEYID,0(R2)                                                      
         BNE   FILT050                                                          
*                                                                               
         ZIC   R6,2(R2)            KEY TYPE LEN                                 
         LA    R2,3(R6,R2)         1ST KEY FIELD                                
*                                                                               
FILT060  DS    0H                                                               
         CLI   0(R2),X'FF'         END OF KEY                                   
         BE    FILT080             YES - FILTER NOT IN KEY                      
         CLC   FLTRNUM,0(R2)       FILTER IN KEY?                               
         BE    FILT200             YES - ALREADY CHECKED, NEXT FILTER           
         LA    R2,2(R2)            NEXT KEY FIELD                               
         B     FILT060                                                          
*                                                                               
FILT080  DS    0H                  CHECK FILTER IN RECORD                       
         CLI   FLTRELEM,X'FF'      SKIP AUTO CHECK                              
         BNE   FILT090             NO                                           
         BAS   RE,HOOK             YES - PROCEED TO HOOK ROUTINE                
         BE    FILT200             PASSED - GO TO NEXT FILTER                   
         BNE   EXITL               FAILED - REJECT RECORD                       
*                                                                               
FILT090  DS    0H                                                               
         L     R6,RKLAIO           IO AREA                                      
         CLI   FLTRELEM,0          FILTER IN ACTIVE KEY?                        
         BE    FILT100                                                          
         MVC   ELCODE,FLTRELEM                                                  
         BAS   RE,GETEL                                                         
         BNE   EXITL                                                            
FILT100  DS    0H                                                               
         ZICM  R3,FLTRRCON,2                                                    
         AR    R6,R3                                                            
         EX    R1,*+8                                                           
         B     *+10                                                             
         CLC   0(0,R5),0(R6)       FILTER VS. RECORD                            
         BNE   EXITL                                                            
*                                                                               
FILT200  DS    0H                                                               
         LA    R4,FLTRLNQ(R4)    NEXT FILTER                                    
         B     FILT020                                                          
         DROP  R4                                                               
***********************************************************************         
* TOTAL - PROCESS ACCEPTED RECORD INTO RUNNING TOTALS & BUCKETS                 
***********************************************************************         
TOTAL    NTR1                                                                   
         MVI   MODE,MTOTAL                                                      
         B     EXITOK                                                           
***********************************************************************         
* HOOK - HOOK ROUTING ROUTINE                                                   
***********************************************************************         
*                                                                               
* INPUT:   R4 = A(FLTRTAB ENTRY)                                                
*                                                                               
*     MODE MNEWKEY: R2 = KEY FIELD DEFINITION IN KEYTAB ENTRY                   
*                   R3 = A(KEY FIELD) IN KEY BUILD AREA                         
*                                                                               
*     MODE MCHKKEY: R2 = KEY FIELD DEFINITION IN KEYTAB ENTRY                   
*                   R3 = A(KEY FIELD) IN RETURNED KEY                           
*                                                                               
*     MODE MFILTER: RKLAIO = A(RCONREC)                                         
*                                                                               
*                                                                               
* OUTPUT:                                                                       
*                                                                               
*     MODE MNEWKEY: KEY FIELD COMPLETED IN KEY BUILD AREA                       
*                                                                               
*     MODE MCHKKEY: CC = EQ  = MATCH KEY FIELD                                  
*                   CC = L   = FAILED MATCH KEY FIELD                           
*                   CC = H   = FAILED MATCH KEY FIELD, END OF SEQ               
*                                                                               
*     MODE MFILTER: CC = EQ  = KEEP RECORD                                      
*                   CC = EQ  = REJECT RECORD                                    
*                                                                               
* ---------------------------------------------------------------------         
HOOK     NTR1                                                                   
         USING FLTRD,R4                                                         
         L     RF,FLTRROUT                                                      
         A     RF,RELO                                                          
         BR    RF                                                               
*                                                                               
* SDATE - START DATE HOOK ROUTINE                                               
*                                                                               
SDATE    DS    0H                                                               
         ZICM  R6,FLTRRKLD,2                                                    
         LA    R5,RKLD                                                          
         AR    R5,R6                                                            
*                                                                               
         XC    WORK,WORK                                                        
         MVC   WORK(2),0(R5)                                                    
         MVI   WORK+2,1                                                         
         GOTO1 DATCON,DMCB,(3,WORK),(0,WORK+3)                                  
         GOTO1 (RFGETBRD,VREPFACS),DMCB,(1,WORK+3),WORK+12,GETDAY,ADDAY         
                                                                                
         GOTO1 DATCON,DMCB,(3,0(R5)),(2,WORK)                                   
         GOTO1 DATCON,DMCB,(3,RKLEND),(2,WORK+2)                                
*                                                                               
         CLI   MODE,MNEWKEY                                                     
         BNE   SDATE100                                                         
*                                                                               
         MVC   0(2,R3),WORK        START DATE INTO NEW KEY (COMPRESSED)         
         B     EXIT                                                             
*                                                                               
SDATE100 DS    0H                                                               
         CLI   MODE,MCHKKEY                                                     
         BNE   SDATE200                                                         
         CLC   0(2,R3),WORK        KEY START DATE VS. FILTER END DATE           
         BL    EXITL                                                            
         B     EXITOK                                                           
*                                                                               
SDATE200 DS    0H                                                               
         CLI   MODE,MFILTER                                                     
         BNE   EXITL                                                            
         L     R6,RKLAIO                                                        
         USING RCONREC,R6                                                       
         CLC   RCONDATE(3),0(R5)                                                
         BL    EXITL                                                            
         B     EXITOK                                                           
         DROP  R6                                                               
*                                                                               
* EDATE - END DATE HOOK ROUTINE                                                 
*                                                                               
EDATE    DS    0H                                                               
         ZICM  R6,FLTRRKLD,2                                                    
         LA    R5,RKLD                                                          
         AR    R5,R6                                                            
*                                                                               
         GOTO1 DATCON,DMCB,(3,0(R5)),(2,WORK)                                   
*                                                                               
         CLI   MODE,MNEWKEY                                                     
         BNE   EDATE100                                                         
         B     EXITOK                                                           
*                                                                               
EDATE100 DS    0H                                                               
         CLI   MODE,MCHKKEY                                                     
         BNE   EDATE200                                                         
         CLC   0(2,R3),WORK                                                     
         BH    EXITH                                                            
         B     EXITOK                                                           
*                                                                               
EDATE200 DS    0H                                                               
         CLI   MODE,MFILTER                                                     
         BNE   EXITL                                                            
         L     R6,RKLAIO                                                        
         USING RCONREC,R6                                                       
         CLC   RCONDATE+3(3),0(R5)                                              
         BH    EXITL                                                            
         B     EXITOK                                                           
         DROP  R6                                                               
*                                                                               
         DROP  R4                                                               
*                                                                               
*                                                                               
* KEYTAB - CONTRACT KEY DEFINITION TABLE                                        
*                                                                               
*        BYTE 1     KEY IDENTIFIER                                              
*        BYTE 2     ACTUAL KEY LENGTH ON FILE                                   
*        BYTE 3     KEY TYPE LENGTH                                             
*        BYTE 4+    KEY TYPE                                                    
*                   2-BYTE KEY FIELD LIST:                                      
*                        BYTE 1 = KEY FIELD EQUATE                              
*                        BYTE 2 = KEY FIELD LENGTH                              
                                  (OR VALUE IF CONST)                           
*                   X'FF' END OF KEY                                            
*                                                                               
KEYTAB   DS    0C                                                               
         DC    AL1(1,27,1),X'0C'             ACTIVE KEY                         
         DC    AL1(0,1)                                                         
         DC    AL1(KREP,2)                                                      
         DC    AL1(KGRP,1)                                                      
         DC    AL1(KSGRP,1)                                                     
         DC    AL1(KSTA,5)                                                      
         DC    AL1(KOFF,2)                                                      
         DC    AL1(KAGY,4)                                                      
         DC    AL1(KAOF,2)                                                      
         DC    AL1(KADV,4)                                                      
         DC    AL1(KCON,4)                                                      
         DC    X'FF'                                                            
*                                                                               
         DC    AL1(3,27,1),X'9C'                9C PASSIVE                      
         DC    AL1(0,1)                                                         
         DC    AL1(KREP,2)                                                      
         DC    AL1(KOFF,2)                                                      
         DC    AL1(KGRP,1)                                                      
         DC    AL1(KSGRP,1)                                                     
         DC    AL1(KSTA,5)                                                      
         DC    AL1(KADV,4)                                                      
         DC    AL1(KAGY,4)                                                      
         DC    AL1(KAOF,2)                                                      
         DC    AL1(KCON,4)                                                      
         DC    X'FF'                                                            
*                                                                               
         DC    AL1(4,27,1),X'9D'                 '9D' PASSIVE                   
         DC    AL1(KREP,2)                                                      
         DC    AL1(KSTA,5)                                                      
         DC    AL1(KADV,4)                                                      
         DC    AL1(KPRDN,9)                                                     
         DC    AL1(0,2)                                                         
         DC    AL1(KCON,4)                                                      
         DC    X'FF'                                                            
*                                                                               
         DC    AL1(5,27,2),X'AB01'               'AB01' PASSIVE                 
         DC    AL1(0,8)                                                         
         DC    AL1(KREP,2)                                                      
         DC    AL1(KAGY,4)                                                      
         DC    AL1(KADV,4)                                                      
         DC    AL1(KPRD,3)                                                      
         DC    AL1(KCON,4)                                                      
         DC    X'FF'                                                            
*                                                                               
         DC    AL1(6,27,1),X'8D'                                                
         DC    AL1(KREP,2)                                                      
         DC    AL1(0,5)                                                         
         DC    AL1(KSTART,2)                                                    
         DC    AL1(KEND,2)                                                      
         DC    AL1(KCON,4)                                                      
         DC    AL1(CONST,1)                                                     
         DC    AL1(KAGY,4)                                                      
         DC    AL1(KAOF,2)                                                      
         DC    AL1(KADV,4)                                                      
         DC    X'FF'                                                            
*                                                                               
         DC    AL1(7,27,1),X'8D'                                                
         DC    AL1(KREP,2)                                                      
         DC    AL1(0,5)                                                         
         DC    AL1(KSTART,2)                                                    
         DC    AL1(KEND,2)                                                      
         DC    AL1(KCON,4)                                                      
         DC    AL1(CONST,2)                                                     
         DC    AL1(KSAL,3)                                                      
         DC    AL1(KTYPE,1)                                                     
         DC    AL1(KGRP,1)                                                      
         DC    AL1(KSGRP,1)                                                     
         DC    AL1(KCTGY,2)                                                     
         DC    AL1(KTEAM,2)                                                     
         DC    X'FF'                                                            
*                                                                               
         DC    AL1(8,27,1),X'8D'                                                
         DC    AL1(KREP,2)                                                      
         DC    AL1(0,5)                                                         
         DC    AL1(KSTART,2)                                                    
         DC    AL1(KEND,2)                                                      
         DC    AL1(KCON,4)                                                      
         DC    AL1(CONST,3)                                                     
         DC    AL1(KOFF,2)                                                      
         DC    AL1(KDEM,3)                                                      
         DC    AL1(KCREA,2)                                                     
         DC    AL1(0,3)                                                         
         DC    X'FF'                                                            
*                                                                               
         DC    X'FF'               END OF TABLE                                 
*                                                                               
* FLTRTAB - FILTER DEFINITION TABLE -- SEE FLTRD FOR LAYOUT!!!                  
*                                                                               
FLTRTAB  DS    0C                                                               
         DC    AL1(KREP),AL1(2,4),AL2(RKLREP-RKLD)       REP                    
         DC    X'00',AL2(RCONKREP-RCONKEY),AL4(EXIT)                            
*                                                                               
         DC    AL1(KGRP),AL1(1,4),AL2(RKLGRP-RKLD)       GROUP                  
         DC    X'00',AL2(RCONKGRP-RCONKEY),AL4(EXIT)                            
*                                                                               
         DC    AL1(KSGRP),AL1(1,4),AL2(RKLSBGP-RKLD)     SUB GROUP              
         DC    X'00',AL2(RCONKGRP+1-RCONKEY),AL4(EXIT)                          
*                                                                               
         DC    AL1(KSTA),AL1(5,8),AL2(RKLSTA-RKLD)       STATION                
         DC    X'00',AL2(RCONKSTA-RCONKEY),AL4(EXIT)                            
*                                                                               
         DC    AL1(KOFF),AL1(2,6),AL2(RKLOFF-RKLD)       OFFICE                 
         DC    X'00',AL2(RCONKOFF-RCONKEY),AL4(EXIT)                            
*                                                                               
         DC    AL1(KAGY),AL1(4,8),AL2(RKLAGY-RKLD)       AGENCY                 
         DC    X'00',AL2(RCONKAGY-RCONKEY),AL4(EXIT)                            
*                                                                               
         DC    AL1(KAOF),AL1(2,4),AL2(RKLAGOFF-RKLD)     AGENCY OFFICE          
         DC    X'00',AL2(RCONKAOF-RCONKEY),AL4(EXIT)                            
*                                                                               
         DC    AL1(KADV),AL1(4,12),AL2(RKLADV-RKLD)      ADVERTISER             
         DC    X'00',AL2(RCONKADV-RCONKEY),AL4(EXIT)                            
*                                                                               
         DC    AL1(KPRDN),AL1(20,12),AL2(RKLPRDN-RKLD)    PRODUCT NAME          
         DC    X'05',AL2(RCONEXPR-RCONEXEL),AL4(EXIT)                           
*                                                                               
         DC    AL1(KPRD),AL1(3,12),AL2(RKLPRD-RKLD)       PRODUCT CODE          
         DC    X'01',AL2(RCONPRD-RCONELEM),AL4(EXIT)                            
*                                                                               
         DC    AL1(KCON),AL1(4,255),AL2(0)                CONTRACT              
         DC    X'00',AL2(RCONKCON-RCONKEY),AL4(EXIT)                            
*                                                                               
         DC    AL1(KSTART),AL1(2,3),AL2(RKLSTART-RKLD)    FLIGHT START          
         DC    X'FF',AL2(0),AL4(SDATE)                                          
*                                                                               
         DC    AL1(KEND),AL1(2,3),AL2(RKLEND-RKLD)        FLIGHT END            
         DC    X'FF',AL2(0),AL4(EDATE)                                          
*                                                                               
         DC    AL1(KTYPE),AL1(1,6),AL2(RKLTYPE-RKLD)   CONTRACT TYPE            
         DC    X'01',AL2(RCONTYPE-RCONELEM),AL4(EXIT)                           
*                                                                               
         DC    AL1(KTEAM),AL1(2,4),AL2(RKLTEAM-RKLD)      TEAM                  
         DC    X'01',AL2(RCONTEM-RCONELEM),AL4(EXIT)                            
*                                                                               
         DC    AL1(KDEM),AL1(3,4),AL2(RKLDEMO-RKLD)   TARGET DEMO               
         DC    X'FF',AL2(0),AL4(EXIT)                                           
*                                                                               
         DC    AL1(KCREA),AL1(2,4),AL2(RKLCDATS-RKLD)   CREATION DATE           
         DC    X'FF',AL2(0),AL4(EXIT)                                           
*                                                                               
         DC    AL1(KCTGY),AL1(2,4),AL2(RKLCAT-RKLD)   CATEGORY                  
         DC    X'01',AL2(RCONCTGY-RCONELEM),AL4(EXIT)                           
*                                                                               
         DC    AL1(KSAL),AL1(3,8),AL2(RKLSAL-RKLD)   SALESMAN                   
         DC    X'01',AL2(RCONSAL-RCONELEM),AL4(EXIT)                            
*                                                                               
         DC    X'FF'                                                            
*                                                                               
* FIELD EQUATES                                                                 
*                                                                               
CONST    EQU   1                                                                
KREP     EQU   2                                                                
KSTA     EQU   3                                                                
KOFF     EQU   4                                                                
KAGY     EQU   5                                                                
KAOF     EQU   6                                                                
KADV     EQU   7                                                                
KPRD     EQU   8                                                                
KPRDN    EQU   9                                                                
KCON     EQU   10                                                               
KGRP     EQU   11                                                               
KSTART   EQU   12                                                               
KEND     EQU   13                                                               
KSAL     EQU   14                                                               
KTYPE    EQU   15                                                               
KTEAM    EQU   16                                                               
KDEM     EQU   17                                                               
KCREA    EQU   18                                                               
KCTGY    EQU   19                                                               
KSGRP    EQU   20                                                               
***********************************************************************         
* COMMON ROUTINES & CONSTANTS                                         *         
***********************************************************************         
*                                                                               
* DATAMGR HANDLING ROUTINES                                                     
*                                                                               
HIGH     NTR1                                                                   
         MVI   MODE,MHIGH                                                       
         MVC   RKLKEYSV,RKLKEY                                                  
         GOTO1 DATAMGR,DMCB,=C'DMRDHI',=C'REPDIR',RKLKEY,RKLKEY                 
         CLI   8(R1),0                                                          
         BE    EXIT                                                             
         DC    H'0'                                                             
*                                                                               
SEQ      NTR1                                                                   
         MVI   MODE,MSEQ                                                        
         MVC   RKLKEYSV,RKLKEY                                                  
         GOTO1 DATAMGR,DMCB,=C'DMRSEQ',=C'REPDIR',RKLKEY,RKLKEY                 
         CLI   8(R1),0                                                          
         BE    EXIT                                                             
         DC    H'0'                                                             
*                                                                               
GETREC   NTR1                                                                   
         GOTO1 DATAMGR,DMCB,=C'GETREC',=C'REPFILE',RKLKEY+28,          +        
               RKLAIO,DMWORK                                                    
         CLI   8(R1),0                                                          
         BE    EXIT                                                             
         DC    H'0'                                                             
         B     EXITOK                                                           
*                                                                               
*                                                                               
*                                                                               
*                                                                               
*                                                                               
EXITH    CLI   *,0                 SET CC HIGH                                  
         B     EXIT                                                             
EXITL    CLI   *,X'FF'             SET CC LOW                                   
         B     EXIT                                                             
EXITOK   CR    RB,RB               SET CC EQUAL                                 
EXIT     XIT1                                                                   
*                                                                               
         GETEL  R6,34,ELCODE       REPFILE                                      
         LTORG                                                                  
*                                                                               
WORKD    DSECT                                                                  
DUB      DS    D                                                                
DMWORK   DS    12D                                                              
DMCB     DS    6F                                                               
FULL     DS    F                                                                
FULL2    DS    F                                                                
RELO     DS    F                                                                
KEYSCORE DS    F                   KEY SELECTION SCORE                          
KEYORDER DS    F                   KEY FIELDS ORDER SCORE                       
VREPFACS DS    A                                                                
DATCON   DS    A                                                                
DATAMGR  DS    A                                                                
ADDAY    DS    A                                                                
GETDAY   DS    A                                                                
RFBLOCK  DS    0CL6                                                             
ACOMFACS DS    A                                                                
REPALPHA DS    H                                                                
HALF     DS    H                                                                
BYTE     DS    X                                                                
KEYID    DS    X                   KEY ID NUMBER                                
INOPTS   DS    X                   P1 INPUT FLAGS                               
ELCODE   DS    X                                                                
INFILT   DS    X                   FILTER NUMBER FOR TABLE LOOKUP               
*                                                                               
MODE     DS    X                                                                
MNEWKEY  EQU   1                                                                
MCHKKEY  EQU   2                                                                
MFILTER  EQU   3                                                                
MHIGH    EQU   4                                                                
MSEQ     EQU   5                                                                
MTOTAL   EQU   6                                                                
*                                                                               
WORK     DS    CL256                                                            
MYWORK   DS    CL20                                                             
WORKDX   EQU   *                                                                
*                                                                               
* FLTRD - DSECT TO DEFINE FILTER TABLE ENTRIES                                  
*                                                                               
FLTRD    DSECT                                                                  
FLTRNUM  DS    X                   FILTER NUMBER                                
FLTRLEN  DS    X                   FILTER LENGTH                                
FLTRSCR  DS    X                   FILTER SCORE AS KEY FIELD (0-16)             
*                                  0 IF NOT KEY FIELD                           
FLTRRKLD DS    XL2                 FILTER DISPLACEMENT IN RKLD                  
FLTRELEM DS    X                   RCONREC ELEM FILTER LOCATED IN               
*                                  0 = KEY FIELD                                
*                                  FF = BYPASS THIS AUTOMATIC CHECK             
FLTRRCON DS    XL2                 FILTER DISPLACEMENT IN RCONREC ELEM          
*                                  OR KEY                                       
FLTRROUT DS    XL4                 A(FILTER CHECK HOOK ROUTINE)                 
FLTRLNQ  EQU   *-FLTRD                                                          
*                                                                               
       ++INCLUDE RECONLSTD                                                      
       ++INCLUDE REGENCON                                                       
       ++INCLUDE DDCOMFACS                                                      
       ++INCLUDE REPFACSQ                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'084RECONLST  09/14/99'                                      
         END                                                                    
