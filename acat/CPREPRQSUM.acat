*          DATA SET CPREPRQSUM AT LEVEL 005 AS OF 05/01/02                      
*CATALP CPREQSUM                                                                
         TITLE 'MODULE TO PRINT A SUMMARY OF REQUESTS'                          
CPREQSUM CSECT                                                                  
         NMOD1 6,CPREQSUM                                                       
         USING REQSUMD,RC                                                       
         L     RA,=V(CPWORKC)                                                   
         USING CPWORKD,RA                                                       
         LA    R9,2048(RA)                                                      
         LA    R9,2048(R9)                                                      
         USING CPWORKD+4096,R9                                                  
         L     R2,0(R1)            PARAMETER 1 TERMINATION CODE                 
         MVC   P,SPACES                        SEE BELOW                        
         BAS   RE,SKIP                                                          
         EJECT                                                                  
*                   MODICUM OF HEADLINES                                        
         SPACE 3                                                                
         MVC   P+24(6),=C'RUN ON'                                               
         GOTO1 DATCON,DMCB,(0,RCDATE),(5,P+31)                                  
         MVC   P+45(30),=C'SUMMARY OF REQUESTS     PAGE 1'                      
         BAS   RE,PRINT1                                                        
         MVC   P+45(30),=C'-------------------     -----'                       
         MVC   P+28(2),=C'AT'                                                   
       ++INCLUDE DDGETIME                                                       
         XC    SUMDUB,SUMDUB                                                    
         ST    R1,SUMWORK                                                       
         MVC   SUMDUB+5(3),SUMWORK                                              
         OI    SUMDUB+7,X'0F'                                                   
         CVB   R3,SUMDUB                                                        
         EDIT  (R3),(5,P+31),2,DUB=SUMDUB,WRK=SUMWORK                           
         BAS   RE,PRINT3                                                        
         EJECT                                                                  
*                   NOW PRINT REPORT                                            
         SPACE 3                                                                
         MVC   P+34(20),=C'COMPLETION OF REPORT'                                
         MVC   P+69(3),=C'CP0'                                                  
         MVC   P+72(2),RCPROG                                                   
         BAS   RE,PRINT1                                                        
         SPACE 2                                                                
         MH    R2,=H'21'           TERMINATION CODE                             
         LA    R2,TERMTAB(R2)                                                   
         MVC   P+34(21),0(R2)                                                   
         BAS   RE,PRINT2                                                        
         MVC   P+34(23),=C'FIRST REQUEST PROCESSED'                             
         LA    R3,RCSTNUM                                                       
         BAS   RE,REPLINE                                                       
         MVC   P+34(22),=C'LAST REQUEST PROCESSED'                              
         LA    R3,RCRQTOT                                                       
         BAS   RE,REPLINE                                                       
         MVC   P+34(24),=C'VALID REQUESTS PROCESSED'                            
         LA    R3,RCRQVAL                                                       
         BAS   RE,REPLINE                                                       
         MVC   P+34(17),=C'REQUESTS IN ERROR'                                   
         LA    R3,RCRQERR                                                       
         BAS   RE,REPLINE                                                       
         AP    RCRQERR,RCRQVAL                                                  
         MVC   P+34(24),=C'TOTAL REQUESTS PROCESSED'                            
         BAS   RE,REPLINE                                                       
         XMOD1 1                                                                
         EJECT                                                                  
*                   TABLE OF TERMINATIONS                                       
         SPACE 3                                                                
TERMTAB  DS    0CL21                                                            
         DC    CL21'NORMAL TERMINATION'           0                             
         DC    CL21'OPERATION EXCEPTION'          1                             
         DC    CL21'PROTECTION EXCEPTION'         2                             
         DC    CL21'ADDRESSING EXCEPTION'         3                             
         DC    CL21'DATA EXCEPTION'               4                             
         DC    CL21'OPERATOR INTERRUPTION'        5                             
         EJECT                                                                  
*                   ROUTINE TO PRINT A LINE                                     
         SPACE 3                                                                
REPLINE  NTR                                                                    
         ZAP   SUMDUB,0(3,R3)                                                   
         CVB   R4,SUMDUB                                                        
         EDIT  (R4),(4,P+69),ALIGN=LEFT,DUB=SUMDUB,WRK=SUMWORK                  
         BAS   RE,PRINT2                                                        
         XIT                                                                    
         EJECT                                                                  
*                   PRINT AIDS                                                  
         SPACE 3                                                                
PRINT1   MVC   SUMCON,=C'BL01'                                                  
         B     PRINTALL                                                         
         SPACE 2                                                                
PRINT2   MVC   SUMCON,=C'BL02'                                                  
         B     PRINTALL                                                         
         SPACE 2                                                                
PRINT3   MVC   SUMCON,=C'BL03'                                                  
         B     PRINTALL                                                         
         SPACE 2                                                                
SKIP     MVC   SUMCON,=C'BC01'                                                  
         B     PRINTALL                                                         
         SPACE 2                                                                
PRINTALL NTR                                                                    
         GOTO1 PRINT,SUMWORK,P,SUMCON                                           
         MVC   P,SPACES                                                         
         XIT                                                                    
         EJECT                                                                  
*                   DSECT FOR CPREQSUM                                          
         SPACE 3                                                                
REQSUMD  DSECT                                                                  
SUMDUB   DS    D                                                                
SUMWORK  DS    CL32                                                             
SUMCON   DS    CL5                                                              
         PRINT OFF                                                              
       ++INCLUDE CPREPWORKD                                                     
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'005CPREPRQSUM05/01/02'                                      
         END                                                                    
