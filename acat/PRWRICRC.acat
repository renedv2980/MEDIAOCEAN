*          DATA SET PRWRICRC   AT LEVEL 024 AS OF 04/22/03                      
*CATALP PRWRICRC                                                                
         TITLE 'PRWRICRC - CIRCULATION RECORD KEYWORDS - ENTRY POINTS'          
         ENTRY ICRC                                                             
PRWRICRC CSECT                                                                  
         TITLE 'PRWRICRC - CHANGE LOG'                                          
***********************************************************************         
*                                                                     *         
*        PROGRAM CONTAINS THE INPUT & OUTPUT ROUTINES FOR             *         
*          CIRCULATION RECORD KEYWORDS                                *         
*                                                                     *         
*        ICRC     - RETRIEVE DATA FROM CIRCULATION ELEMENT- INPUT     *         
*                                                                     *         
***********************************************************************         
         TITLE 'PRWRICRC - DATA IN CIRCULATION ELEMENT - ICRC'                  
***********************************************************************         
*                                                                     *         
*        ROUTINE TO EXTRACT DATA FROM CIRCULATION ELEMENT             *         
*                                                                     *         
*NTRY    GLARGS   = RECORD ID                                         *         
*                   C'F' - CIRCULATION                                *         
*        GLARGS+1 = DATA TYPE                                         *         
*                   C'B' - BASE                                       *         
*                   C'D' - ISSUE DATE                                 *         
*                   C'F' - FINAL                                      *         
*                   C'P' - PRELIMINARY                                *         
*                   C'Q' - FREQUENCY                                  *         
*                   C'S' - SOURCE                                     *         
*                   C'T' - TOTAL                                      *         
*                                                                     *         
*        R2==>  DRIVER INPUT AREA                                     *         
*                                                                     *         
*EXIT   DATA                                                          *         
*                                                                     *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
         PRINT NOGEN                                                            
ICRC     NMOD1 0,**#ICRC                                                        
*                                                                               
         USING SPOOLD,R8           SPOOL WORKAREA                               
         USING SYSD,R9             PRWRITER WORKAREA                            
         USING GLOBALD,RA          DRIVER AREA                                  
*                                                                               
         L     RC,GLAWORKD         RESET WORKING STORAGE POINTER                
         USING GEND,RC                                                          
*                                                                               
         LA    R7,PBLOCK           ESTABLISH PRNTBLOCK                          
         USING PBLOCK,R7                                                        
*                                                                               
*        FIND DATA TYPE TO BE HANDLED                                           
*                                                                               
         ICM   R4,15,PBCRHELA      POINT TO CIRCULATION HEADER ELEM             
         BZ    ICRCX                 NO HEADER PRESENT                          
         USING PCRHELM,R4                                                       
*                                                                               
         CLI   GLARGS+1,C'Q'       CIRCULATION FREQUENCY                        
         BE    ICRCFRQ                                                          
*                                                                               
         CLI   GLARGS+1,C'S'       CIRCULATION SOURCE                           
         BNE   *+14                                                             
         MVC   0(L'PCRHSRC,R2),PCRHSRC                                          
         B     ICRCX                                                            
*                                                                               
         ICM   R6,15,PBCRCELA      POINT TO CIRCULATION ELEMENT                 
         BZ    ICRCX                  NONE FOUND                                
         USING PCRCELM,R6                                                       
*                                                                               
         CLI   GLARGS+1,C'D'       ISSUE DATE                                   
         BNE   *+14                                                             
         MVC   0(L'PCRCISS,R2),PCRCISS                                          
         B     ICRCX                                                            
*                                                                               
         CLI   GLARGS+1,C'T'       TOTAL CIRCULATION                            
         BNE   *+14                                                             
         MVC   0(L'PCRCTOT,R2),PCRCTOT                                          
         B     ICRCX                                                            
*                                                                               
         CLI   GLARGS+1,C'B'       RATE BASE                                    
         BNE   ICRCBASN                                                         
*                                                                               
         OC    PBCCRELA,PBCCRELA   SKIP IF NO CLIENT CIRC ELEMENT               
         BZ    ICRCBAS9                                                         
*                                                                               
         TM    PBQOPTS,PBQOXCCR    SKIP IF RPT EXCLUDING CLT CIRC               
         BO    ICRCBAS9                                                         
*                                                                               
         GOTO1 FNDFLTRA,DMCB,=AL2(PRQWRCFL),(2,=AL2(PRQCFXCC)),0                
*                                                                               
         OC    8(4,R1),8(R1)       SKIP IF EXCLUDING CLT CRC IN COL             
         BNZ   ICRCBAS9                                                         
*                                                                               
         ICM   R6,15,PBCCRELA      USE CLIENT CIRC ELEMENT                      
*                                                                               
ICRCBAS9 DS    0H                                                               
*                                                                               
         MVC   0(L'PCRCRTB,R2),PCRCRTB                                          
*                                                                               
         B     ICRCX                                                            
*                                                                               
ICRCBASN DS    0H                                                               
*                                                                               
         CLI   GLARGS+1,C'P'       PRELIMINARY                                  
         BNE   *+14                                                             
         MVC   0(L'PCRCPLIM,R2),PCRCPLIM                                        
         B     ICRCX                                                            
*                                                                               
         CLI   GLARGS+1,C'F'       FINAL                                        
         BNE   *+14                                                             
         MVC   0(L'PCRCFIN,R2),PCRCFIN                                          
         B     ICRCX                                                            
*                                                                               
         B     ICRCX               UNKNOWN DATATYPE                             
*                                                                               
*        TRANSLATE FREQUENCY                                                    
*                                                                               
ICRCFRQ  DS    0H                                                               
*                                                                               
         LA    R1,ICRCFRTB         POINT TO FREQUENCY TABLE                     
*                                                                               
         CLI   0(R1),X'FF'         DONE IF END OF TABLE REACHED                 
         BE    ICRCFRQX                                                         
         CLC   PCRHFRQ,0(R1)       MATCH FREQUENCY TO TABLE                     
         BE    *+12                                                             
         LA    R1,ICRCFRLQ(R1)     NEXT TABLE ENTRY                             
         B     *-22                                                             
*                                                                               
         MVC   0(ICRCFRLQ-1,R2),1(R1)   RETURN EXPANDED FREQUENCY               
*                                                                               
ICRCFRQX DS    0H                                                               
*                                                                               
         B     ICRCX                                                            
*                                                                               
ICRCX    DS    0H                                                               
         XIT1                                                                   
*                                                                               
ICRCFRTB DS    0H                  FREQUENCY TRANSLATE TABLE                    
         DC    C'M',CL11'MONTHLY    '                                           
ICRCFRLQ EQU   *-ICRCFRTB          ENTRY LENGTH                                 
         DC    C'Q',CL11'QUARTERLY  '                                           
         DC    C'W',CL11'WEEKLY     '                                           
         DC    C'I',CL11'ISSUE      '                                           
         DC    C'M',CL11'VARIOUS    '                                           
         DC    X'FF',CL11'EOT        '                                          
         LTORG                                                                  
*                                                                               
         DROP                                                                   
*                                                                               
         TITLE   'PRWRICRC DSECTS / STORAGE'                                    
* PRWRIWORKD                                                                    
         PRINT   OFF                                                            
       ++INCLUDE PRWRIWORKD                                                     
         PRINT   ON                                                             
* DRGLOBAL                                                                      
         PRINT   OFF                                                            
       ++INCLUDE DRGLOBAL                                                       
         PRINT   ON                                                             
* DRIVETABLE                                                                    
         PRINT   OFF                                                            
       ++INCLUDE DRIVETABLE                                                     
         PRINT   ON                                                             
* DRINTRECD2                                                                    
         PRINT   OFF                                                            
       ++INCLUDE DRINTRECD2                                                     
         PRINT   ON                                                             
* DDSPOOLD                                                                      
         PRINT   OFF                                                            
       ++INCLUDE DDSPOOLD                                                       
         PRINT   ON                                                             
* DDSPLWORKD                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDSPLWORKD                                                     
         PRINT   ON                                                             
* PRGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE PRGENFILE                                                      
         PRINT   ON                                                             
* PRWRIEQUS                                                                     
         PRINT OFF                                                              
       ++INCLUDE PRWRIEQUS                                                      
         PRINT   ON                                                             
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'024PRWRICRC  04/22/03'                                      
         END                                                                    
