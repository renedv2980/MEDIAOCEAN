*          DATA SET FATASKERS  AT LEVEL 019 AS OF 03/27/01                      
*CATALP FATASKEA                                                                
         TITLE 'COMBINED TOR/AOR SCHEDULER/TASK CONTROLLER'                     
         SPACE 1                                                                
ADNEXT   CSECT                                                                  
         PRINT NOGEN                                                            
         ENTRY ADFIRST                                                          
         ENTRY ADFIRREG                                                         
         ENTRY ADWAIT                                                           
         ENTRY SCWAIT                                                           
         ENTRY TKWAIT                                                           
         ENTRY ADIRPT                                                           
         ENTRY MQWAIT                                                           
         ENTRY TIMERECB                                                         
         ENTRY POSTECB                                                          
         ENTRY UTLZERO                                                          
         ENTRY ABNDECB                                                          
         ENTRY DSPACECB                                                         
         ENTRY DSOPSECB                                                         
         EJECT                                                                  
***********************************************************************         
* MAIN PROGRAM ENTRY POINT                                            *         
***********************************************************************         
         SPACE 1                                                                
         NBASE 0,TASKNEXT,WORK=A(TASKWORK)                                      
         SAC   0                                                                
         L     R9,=A(TASKCNST)                                                  
         USING TASKCNST,R9                                                      
         L     RA,VSYSFX                                                        
         USING SYSFACD,RA                                                       
         SPACE 1                                                                
***********************************************************************         
* SUSPEND TIMER SUPPORT AND CLEAR SSB TASK ADDRESS                    *         
***********************************************************************         
         SPACE 1                                                                
         GOTO1 VTICTOC,DUB,C'SSET'     SUSPEND ALL TIMERS                       
         L     RE,VSSB                                                          
         USING SSBD,RE                                                          
         XC    SSBTKADR,SSBTKADR       CLEAR SSBTKADR                           
         DROP  RE                                                               
*                                                                               
SEFST    SAC   0                                                                
         LAM   R0,RF,ARZERO        MAKE SURE THESE ARE RESET ALWAYS             
         CLI   TORFLAG,C'Y'                                                     
         BE    TORFST                                                           
         B     AORFST                                                           
         EJECT                                                                  
***********************************************************************         
* RESET ACCESS REGISTERS & TEST TIMER EXPIRY                          *         
***********************************************************************         
         SPACE 1                                                                
TORFST   L     RE,VSSB                                                          
         USING SSBD,RE                                                          
         MVI   MODE,0              SET NORMAL MODE                              
         NOP   *+12                                                             
         OI    *-3,X'F0'                                                        
         OI    MODE,X'80'          SET FIRST TIME MODE                          
         TM    SSBT2,X'80'                                                      
         BZ    *+8                                                              
         OI    MODE,X'02'          SET MAJOR TIMER EXPIRED MODE                 
         DROP  RE                                                               
         EJECT                                                                  
***********************************************************************         
* FIND THE MOST APPROPRIATE TRANSACTION TO PROCESS                    *         
***********************************************************************         
         SPACE 1                                                                
         GOTO1 =A(SCHEDULE)        SCHEDULING ALGORITHM                         
         USING SELISTD,R7          RETURNS A(SELIST ENTRY) IN R7                
         BNE   VTAMDSPC            NO TRANSACTIONS TO PROCESS                   
         LTR   R7,R7                                                            
         BNZ   *+6                                                              
         DC    H'0'                FUCKING HELL                                 
         EJECT                                                                  
***********************************************************************         
* FIND THE BEST AVAILABLE TASK TO PROCESS TRANSACTION                 *         
***********************************************************************         
         SPACE 1                                                                
         GOTO1 =A(TASKFILL)                                                     
         LAM   R0,RF,ARZERO        RESET ACCESS REGISTERS                       
         BE    SEFST               AOR WILL PROCESS TRANSACTION                 
         BL    VTAMDSPC            NO FREE TASKS (CHECK COMPLETIONS)            
         EJECT                                                                  
***********************************************************************         
* TOR HAS TO PROCESS TRANSACTION                                      *         
***********************************************************************         
         SPACE 1                                                                
         L     RE,VSSB             FIND EXCHANGE BLOCK FOR TOR/AOR WORK         
         LAM   R2,R2,SSBALET-SSBD(RE)                                           
         ICM   R2,15,SSBATOR-SSBD(RE)                                           
         SAC   512                                                              
         AHI   R2,TORFACLQ+6       FIRST ENTRY IS ALWAYS FOR TOR                
         USING SBEXCHD,R2                                                       
*                                                                               
         ICM   R0,15,SBTSKMAX                                                   
         CPYA  R3,R2                                                            
         LA    R3,SBTSKBLK         FIND NEW TASK TO PROCESS                     
         USING EXCHNGD,R3                                                       
*                                                                               
TFST02   CLI   EXCFLAG,EXCNEW      NEW TASK?                                    
         BE    TFST04              YES                                          
         AHI   R3,EXCHNGLQ                                                      
         BCT   R0,TFST02                                                        
         DC    H'0'                                                             
*                                                                               
TFST04   L     R6,VTCB             MATCH TCB SLOT TO EXCHANGE SLOT              
         LH    R4,0(R6)                                                         
         L     R5,2(R6)                                                         
         LA    R6,6(R6)                                                         
         USING TCBD,R6                                                          
         CLC   EXCIDENT,TCBID                                                   
         BE    TFST06                                                           
         BXLE  R6,R4,*-10                                                       
         DC    H'0'                                                             
*                                                                               
TFST06   L     RE,VSSB             SET A(TASK) IN SSB                           
         ST    R6,SSBTKADR-SSBD(RE)                                             
         MVI   EXCFLAG,EXCBUSY     SET THIS TASK NOW PROCESSING                 
*                                                                               
         LA    R5,UTLZERO                                                       
         CLC   EXCUTL,UTL0ID       SPECIAL UTL'S (UTLZERO & UTLZEROA)           
         BE    DISPATCH                                                         
         LA    R5,UTLZEROA                                                      
         CLC   EXCUTL,UTL0AID                                                   
         BE    DISPATCH                                                         
*                                                                               
TFST08   LH    RF,EXCUTL           PICK UP TNUM & INDEX INTO UTL LIST           
         BCTR  RF,0                                                             
         L     RE,VUTL                                                          
         MH    RF,0(RE)                                                         
         LA    R5,6(RE,RF)         R5 = A(UTL)                                  
         B     DISPATCH                                                         
         DROP  R2,R3,R6                                                         
         EJECT                                                                  
***********************************************************************         
* FIND AND INITIALISE NEXT UNIT OF WORK TO PROCESS                    *         
***********************************************************************         
         SPACE 1                                                                
AORFST   GOTO1 =A(TASKFIND)                                                     
         BNE   IOWAIT              NO NEW WORK                                  
*                                                                               
         L     RE,VSSB             SET REGS FOR DISPATCH ENTRY                  
         USING SSBD,RE                                                          
         L     R3,SSBTKADR                                                      
         USING TCBD,R3                                                          
         L     R5,TCBUTL                                                        
         L     R7,TCBSE                                                         
         B     DISPATCH                                                         
         DROP  R3,RE                                                            
         EJECT                                                                  
***********************************************************************         
* DISPATCH THE TASK VIA MONITOR/SCRUNCH                               *         
* NTRY: R5 = A(UTL)                                                   *         
*       R7 = A(SELIST ENTRY)                                          *         
*       A(TCB) IN SSBTKADR                                            *         
***********************************************************************         
         SPACE 1                                                                
         USING UTLD,R5                                                          
         USING SELISTD,R7                                                       
DISPATCH SAC   0                   MAKE SURE AR MODE IS OFF                     
         LAM   R0,RF,ARZERO        CLEAR DOWN ALL ACCESS REGISTERS              
*                                                                               
         L     RE,VSSB                                                          
         USING SSBD,RE                                                          
         L     R3,SSBTKADR                                                      
         USING TCBD,R3                                                          
*                                                                               
         CLI   TORFLAG,C'Y'        IS THIS THE TOR                              
         BNE   *+12                                                             
         TM    TTORAOR,TTARCVR     RECOVERED TRANSACTIONS CANNOT RUN IN         
         BO    DSPT02              THE TOR TO HELP TOR INTEGRITY                
         TM    SEIND,SEISTRT       IS SYSTEM OPERATIONAL                        
         BO    DSPT04              YES - DISPATCH                               
*                                  NO- REQUEUE TRANSACTION TO SE1               
DSPT02   MVC   TSVCREQ,$SYSNOP                                                  
         L     R7,VSELIST                                                       
         LA    R7,6(R7)            SE#1 IS ALWAYS FIRST                         
*                                                                               
DSPT04   ST    RA,TCBSYSF          A(SYSFAC)                                    
         ST    R5,TCBUTL           A(UTL ENTRY)                                 
         ST    R7,TCBSE            A(SELIST ENTRY)                              
         OI    TSTAT2,TSTATTIP                                                  
*                                                                               
         XR    RF,RF                                                            
         IC    RF,SETASK           BUMP NUMBER OF ACTIVE TASKS                  
         LA    RF,1(RF)                                                         
         STC   RF,SETASK                                                        
*                                                                               
         MVC   TCBDTFS(8),SEFILES  SET FILE INFO INTO TCB                       
         XC    TCBRCV,TCBRCV       INITIALISE RECOVERY COUNTS/DSKADRS           
         XC    TCBSWNUM(1+(TCBSWMAX*TCBSWLEN)),TCBSWNUM                         
         XC    TCBXTNUM,TCBXTNUM                                                
         XC    TCBXTNDX,TCBXTNDX                                                
         XC    TCBLKSE,TCBLKSE     INITIALISE LOCKER SE LIST                    
         XC    TCBBILL,TCBBILL     INITIALISE BILLING REF                       
         XC    TCBSRMSG,TCBSRMSG                                                
*                                                                               
         GOTO1 VDTFIOA,DUB,(C'I',(R3))    INITIALISE DTF SAVE DATA              
*                                                                               
         MVI   TCBFLAG1,0          SET NO SYSTEM GLOBAL RESOURCES USED          
         XC    TCBIRDR,TCBIRDR     INITIALISE IRDR INFO                         
*                                                                               
DSPT06   OC    TSVCREQ,TSVCREQ     TASK IS FOR A SERVICE REQUEST                
         BZ    DSPT08                                                           
         MVI   TCBSYS,1                                                         
         MVC   TCBPRG,TSVCREQ+1                                                 
         MVI   TCBOVSYS,1                                                       
         MVI   TCBPRTY,0                                                        
         MVI   TCBPRTYF,0                                                       
         B     DSPT12                                                           
*                                                                               
DSPT08   MVC   TCBSYS,TSYS         TASK IS FOR A REGULAR PROGRAM                
         MVC   TCBPRG,TPRG                                                      
         MVC   TCBOVSYS,TOVSYS                                                  
         MVI   TCBPRTY,0           DEFAULT PRIORITY                             
         MVI   TCBPRTYF,0                                                       
         XR    RF,RF               SET INITIAL PRTY TO PROGRAM VALUE            
         ICM   RF,7,TAPRG                                                       
         BZ    DSPT12                                                           
         XR    RE,RE                                                            
         ICM   RE,7,TARSYS         REAL SYSTEM                                  
         BZ    DSPT12                                                           
         ICM   RE,15,SEPGMS-SELISTD(RE)                                         
         AR    RF,RE                                                            
*                                                                               
DSPT10   MVC   TCBPRTY,PGMPRTY-PGMLSTD(RF)                                      
*                                                                               
DSPT12   XC    TCBSCRPT,TCBSCRPT   SET TASK SCRIPT DATA                         
         NI    TCBFLAG3,255-TCBSCPWT                                            
         MVI   TCBSCPTK,0                                                       
*                                                                               
         TM    TSTAT6,TST6SCRP     TEST IF GOING TO RUN A SCRIPT                
         BZ    DSPT14                                                           
*                                                                               
         LA    RE,*+10                                                          
         O     RE,=XL4'80000000'                                                
         BSM   0,RE                                                             
*                                                                               
         ICM   RF,15,TBUFF         BUFFER HAS DETAILS OF SCRIPT                 
         ICM   RF,15,TBHATCB-TBHDATA(RF)                                        
         MVC   TCBSCPTK-TCBD(1,RF),TCBID+6                                      
         ST    RF,TCBSCRPT         SET A(TASK WAITING FOR US)                   
         ICM   RF,15,TCBSIN-TCBD(RF)                                            
*                                                                               
         LA    RE,DSPT16           USE ORIGINAL SIN FOR SCRIPT                  
         BSM   0,RE                                                             
*                                                                               
DSPT14   L     RE,VSSB             GET SYSTEM INPUT NUMBER                      
         USING SSBD,RE                                                          
         L     RF,SSBSIN-SSBD(RE)                                               
*                                                                               
DSPT16   STCM  RF,15,TSIN          SET SIN IN UTL & TCB ENTRIES                 
         STCM  RF,15,TCBSIN                                                     
*                                                                               
         NI    TCBFLAG3,255-TCBMQWT CLEAR MQ WAIT FLAG                          
*                                                                               
         MVC   TCBTASK,TCBID+6                                                  
         XC    TCBOVCNT(4),TCBOVCNT CLEAR TCBOVCNT AND TCBIOCNT                 
         XC    TCBLIOC(8),TCBLIOC   CLEAR LOGICAL I/O COUNTS                    
         XC    TCBCPUTM,TCBCPUTM                                                
         XC    TCBMSGI(4),TCBMSGI   CLEAR INPUT AND OUTPUT MSG LENGTHS          
         XC    TCBINDS1(8),TCBINDS1 CLEAR STATS INDS1/INDS2/UDATA               
*                                                                               
         TM    TTYPE2,TTYPEDUM                                                  
         BO    DSPT18                                                           
*                                                                               
         LA    RE,*+10                                                          
         O     RE,=XL4'80000000'                                                
         BSM   0,RE                                                             
*                                                                               
         L     RE,TBUFF            SET INPUT MSG LEN FROM BUFFER HDR            
         AHI   RE,-TBHL                                                         
         MVC   TCBMSGI(2),TBHMSGL-TBHD(RE)                                      
*                                                                               
         LA    RE,DSPT18                                                        
         BSM   0,RE                                                             
*                                                                               
DSPT18   GOTO1 VTICTOC,DUB,C'1SET'                                              
*                                                                               
         MVC   TCBINTM,TTIMETU     SET TRANSACTION INPUT TIME                   
         L     R1,DUB                                                           
         ST    R1,TCBSTTM          SET TRANSACTION START TIME                   
         ST    R1,TCBTIME          SET CURRENT EVENT TIME                       
*                                                                               
         SL    R1,TTIME            COMPUTE TRANSACTION RATE                     
         SRL   R1,11               <== WAS 12 (FOR PRINTERS)                    
         SR    RF,RF                                                            
         IC    RF,TRATE                                                         
         SR    RF,R1               DECR RATE BY DELTA TIME FACTOR               
         BNM   *+6                                                              
         SR    RF,RF               IF NEG SET RATE ZERO                         
         LA    RF,1(RF)            BUMP RATE FOR NEW TRANSACTION                
         STC   RF,TRATE            NEW RATE                                     
*                                                                               
         MVC   TTIME,DUB           SET LAST EVENT TIME TO NOW                   
*                                                                               
         TM    TTYPE2,TTYPEDUM     DUMMY TERMINAL?                              
         BZ    *+10                NO                                           
         MVC   TCBINTM,DUB         SET ARRIVAL TIME TO NOW FOR DUMMIES          
         MVC   TCBSYM,TSYM                                                      
*                                                                               
         TM    TSTAT6,TST6DBUG     IF TERMINAL IS DEBUG                         
         BZ    *+8                                                              
         OI    TCBFLAG3,TCBDEBUG   SET TASK TO DEBUG                            
*                                                                               
         L     RD,TCBWRKA          POINT TO WORK AREA                           
         LA    R1,TCBD                                                          
         XR    R2,R2               SET NORMAL CALL TO MONITOR                   
         L     RF,VMONITR                                                       
         OC    TCBSCRPT,TCBSCRPT   TEST IF THIS TASK RUNNING A SCRIPT           
         BZ    *+8                                                              
         L     RF,VSCRNCH          YES - GO TO SCRIPT EXECUTOR                  
         BASR  RE,RF                                                            
*                                                                               
         SAC   0                                                                
         LAM   R0,RF,ARZERO                                                     
         GOTO1 =V(CRAPPER)                                                      
*                                                                               
DSPT20   TIME  TU                  SET END TIME AND CPU TIME                    
         ST    R0,TCBNDTM                                                       
         L     R1,TCBTIME                                                       
         CLR   R0,R1                                                            
         BNL   *+8                                                              
         AL    R0,MIDNIGHT         ADJUST FOR MIDNIGHT                          
         SLR   R0,R1                                                            
         AL    R0,TCBCPUTM                                                      
         ST    R0,TCBCPUTM                                                      
*                                                                               
         L     RF,TCBLIOCT         BUMP TOTAL LOGICAL I/O COUNT                 
         A     RF,TCBLIOC                                                       
         ST    RF,TCBLIOCT                                                      
         XC    TCBLIOC,TCBLIOC                                                  
*                                                                               
         NI    TTORAOR,255-TTARCVR CLEAR AOR RECOVERY FLAG                      
*                                                                               
         L     RE,VSSB             SPECIAL FOR AOR IF JUST RUN $T2              
         TM    SSBSTAT4-SSBD(RE),SSBSAOR                                        
         BZ    DSPT24                                                           
         CLC   TSVCREQ+1(1),$T2+1                                               
         BNE   DSPT24                                                           
*                                                                               
         LAM   R2,R2,SSBALET-SSBD(RE)                                           
         ICM   R2,15,SSBATOR-SSBD(RE)                                           
         SAC   512                                                              
         USING TORFACD,R2                                                       
         LA    R2,TORFACLQ(,R2)                                                 
         USING SBEXCHD,R2                                                       
         LH    RE,0(,R2)                                                        
         L     RF,2(,R2)                                                        
         LA    R2,6(,R2)                                                        
*                                                                               
         CLC   ASTOKEN,SBSTOKEN    IS THIS OUR ROW?                             
         BE    DSPT22                                                           
         BXLE  R2,RE,*-10                                                       
         DC    H'0'                WHERE IS OUR DSPACE ENTRY?                   
*                                                                               
DSPT22   MVI   SBAVLBL,SBYES       SET AOR AVAILABLE                            
         SAC   0                                                                
         LAM   R2,R2,ARZERO                                                     
*                                                                               
DSPT24   LR    R1,R3               POINT R1 TO TCB ENTRY                        
         GOTO1 VLOGGR,(R1)         PUT RECORD TO RECORDER FILE                  
*                                                                               
         OC    TCBSCRPT,TCBSCRPT                                                
         BNZ   CHECK08             THAT'S ALL FOR SCRIPTS                       
         OC    TSVCREQ,TSVCREQ                                                  
         BNZ   DSPT26                                                           
*                                                                               
         TM    TSTAT1,TSTATBIL     TEST IF BILLING ACTIVE                       
         BZ    DSPT26              NO                                           
         XC    DUB,DUB             YES OUTPUT FACPAK SYSTEM DATA                
         MVI   DUB,C'S'                                                         
         GOTO1 VBILLIT,DUB                                                      
*                                                                               
DSPT26   MVC   SESIN,TSIN          SET COMPLETED SIN                            
         NI    TSTAT2,255-TSTATTIP RESET TRM IN PROCESS                         
         CLC   TSVCREQ+1(1),$DONE+1                                             
         BE    DSPT28                                                           
         TM    TSTAT5,TST5PSWD     TEST PASSWORD INPUT PENDING                  
         BO    DSPT28                                                           
         NI    TSTAT2,255-TSTATNIT                                              
*                                                                               
DSPT28   TM    TSTAT2,TSTATBCS+TSTATBCP TEST IF BRDCAST SENT THIS TIME          
         BNO   *+12                                                             
         NI    TSTAT2,255-TSTATBCP-TSTATBCS                                     
         OI    TSTAT2,TSTATNIT     SET TRM NOT INIT TO FORCE $RE                
*                                                                               
                                                                                
         CLC   TSVCREQ+1(1),$HELP+1 TEST IF HELP SREEN ON TERMINAL              
         B     *+8                 NOP THIS IS NOT USED EVER                    
         OI    TSTAT2,TSTATNIT     YES FORCE $RE NEXT TIME                      
*                                                                               
         CLC   TSVCREQ+1(1),$EOJ+1                                              
         BNE   CHECK000                                                         
         L     RE,VSSB                                                          
         USING SSBD,RE                                                          
         OI    SSBSTAT1,SSBSEOJ    SET EOJ PENDING                              
         B     CHECK06                                                          
         EJECT                                                                  
***********************************************************************         
* WRITE CHKPT RECORDS IF FLAGGED                                      *         
***********************************************************************         
         SPACE 1                                                                
CHECK000 LR    R1,RD               POINT TO WORK AREA                           
         LA    RD,32(RD)           SET FOR NEXT NMOD                            
*                                                                               
         L     RE,VSSB             TEST CHKPT1 UPDATED                          
         TM    SSBSTAT1,SSBSCHK1                                                
         BZ    CHECK02                                                          
         NI    SSBSTAT1,255-SSBSCHK1                                            
         XC    0(24,R1),0(R1)                                                   
         MVC   0(4,R1),VWTID                                                    
         L     RE,VCHKPT1                                                       
         ST    RE,4(R1)                                                         
         L     RF,VCHKPT1X                                                      
         SR    RF,RE                                                            
         ST    RF,8(R1)                                                         
         MVC   12(4,R1),VPRGMS                                                  
         L     RE,VSSB                                                          
         SR    RF,RF                                                            
         IC    RF,SSBSYSID                                                      
         LA    RF,1(RF,RF)                                                      
         STH   RF,20(R1)                                                        
         MVC   22(2,R1),=X'0100'                                                
         LA    RE,20(R1)                                                        
         ST    RE,16(R1)                                                        
         XR    R0,R0               SAVE AND CLEAR TASK I/O COUNT                
         ICM   R0,7,TCBIOCNT                                                    
         XC    TCBIOCNT,TCBIOCNT                                                
         GOTO1 VDADDS,(R1)                                                      
         AHI   R0,1                RESTORE NEW TASK I/O COUNT                   
         STCM  R0,7,TCBIOCNT                                                    
         OC    8(2,R1),8(R1)                                                    
         BZ    *+6                                                              
         DC    H'0'                                                             
*                                                                               
CHECK02  L     RE,VSSB             TEST CHKPT2 UPDATED                          
         TM    SSBSTAT1,SSBSCHK2                                                
         BZ    CHECK04                                                          
         NI    SSBSTAT1,255-SSBSCHK2                                            
         XC    0(24,R1),0(R1)                                                   
         MVC   0(4,R1),VWTID                                                    
         L     RE,VCHKPT2                                                       
         ST    RE,4(R1)                                                         
         L     RF,VCHKPT2X                                                      
         SR    RF,RE                                                            
         ST    RF,8(R1)                                                         
         MVC   12(4,R1),VPRGMS                                                  
         L     RE,VSSB                                                          
         SR    RF,RF                                                            
         IC    RF,SSBSYSID                                                      
         LA    RF,2(RF,RF)                                                      
         STH   RF,20(R1)                                                        
         MVC   22(2,R1),=X'0100'                                                
         LA    RE,20(R1)                                                        
         ST    RE,16(R1)                                                        
         XR    R0,R0               SAVE AND CLEAR TASK I/O COUNT                
         ICM   R0,7,TCBIOCNT                                                    
         XC    TCBIOCNT,TCBIOCNT                                                
         GOTO1 VDADDS,(R1)                                                      
         AHI   R0,1                RESTORE NEW TASK I/O COUNT                   
         STCM  R0,7,TCBIOCNT                                                    
         OC    8(2,R1),8(R1)                                                    
         BZ    *+6                                                              
         DC    H'0'                                                             
*                                                                               
CHECK04  TM    TTEST,TTESTTAC      TEST IF TSTTAB REFERENCED                    
         BO    *+14                                                             
         CLC   TSVCREQ+1(1),$TEST+1                                             
         BNE   CHECK06                                                          
*                                                                               
         XC    0(24,R1),0(R1)      WRITE TSTTAB TO TSTRCVR FILE                 
         MVC   0(4,R1),VWTID                                                    
         L     RE,VTSTTAB                                                       
         ST    RE,4(R1)                                                         
         L     RF,2(RE)                                                         
         LA    RF,1(RF)                                                         
         SR    RF,RE                                                            
         ST    RF,8(R1)                                                         
         MVC   12(4,R1),VTSTRCVR                                                
         LA    RE,20(R1)                                                        
         ST    RE,16(R1)                                                        
         MVC   20(4,R1),=X'00010100'                                            
         XR    R0,R0               SAVE AND CLEAR TASK I/O COUNT                
         ICM   R0,7,TCBIOCNT                                                    
         XC    TCBIOCNT,TCBIOCNT                                                
         GOTO1 VDADDS,(R1)                                                      
         AHI   R0,1                RESTORE NEW TASK I/O COUNT                   
         STCM  R0,7,TCBIOCNT                                                    
         OC    8(2,R1),8(R1)                                                    
         BZ    *+6                                                              
         DC    H'0'                                                             
*                                                                               
CHECK06  CLI   TCBSYS,1            TEST SE 1 COMPLETION                         
         BE    *+8                                                              
         ST    R7,LASTSE           SET LAST SE COMPLETION                       
*                                                                               
CHECK08  MVI   TCBSYS,0            SET TASK IDLE                                
         NI    TCBFLAG3,255-TCBDEBUG                                            
         IC    R0,SETASK                                                        
         BCTR  R0,0                                                             
         STC   R0,SETASK           DECR NUMBER OF ACTIVE TASKS                  
*                                                                               
         CLI   TORFLAG,C'N'                                                     
         BE    CHECKAOR                                                         
         EJECT                                                                  
***********************************************************************         
* TAKE TASK OUT OF DSPACE                                             *         
***********************************************************************         
         SPACE 1                                                                
         L     RE,VSSB             FIND EXCHANGE BLOCK FOR TOR/AOR WORK         
         LAM   R0,RF,ARZERO                                                     
         LAM   R2,R2,SSBALET-SSBD(RE)                                           
         ICM   R2,15,SSBATOR-SSBD(RE)                                           
         SAC   512                                                              
         LA    R2,TORFACLQ(,R2)                                                 
         LA    R2,6(,R2)           FIRST ONE IS ALWAYS PRIMARY                  
         USING SBEXCHD,R2                                                       
         ICM   R0,15,SBTSKMAX                                                   
         CPYA  R4,R2                                                            
         LA    R4,SBTSKBLK         FIND NEW TASK TO PROCESS                     
         USING EXCHNGD,R4                                                       
*                                                                               
CHECK10  CLC   TCBID,EXCIDENT      FIND THIS TASK IN EXCHANGE BLOCK             
         BE    CHECK12                                                          
         LA    R4,EXCHNGLQ(,R4)                                                 
         BCT   R0,CHECK10                                                       
         DC    H'0'                YOU REALLY ARE MESSED UP!                    
*                                                                               
CHECK12  XC    EXCUTL,EXCUTL       FREE EXCHANGE BLOCK SLOT                     
         XC    EXCTIME,EXCTIME                                                  
         MVI   EXCFLAG,EXCFREE                                                  
*                                                                               
CHECK14  ICM   RE,15,SBTSKNOW      DECREMENT ACTIVE TASK COUNT                  
         BZ    CHECK16                                                          
         LR    RF,RE                                                            
         AHI   RF,-1                                                            
         CS    RE,RF,SBTSKNOW                                                   
         BNE   CHECK14                                                          
*                                                                               
CHECK16  SAC   0                                                                
         LAM   R0,RF,ARZERO                                                     
         BR    RB                                                               
         DROP  R2,R3,R4,R5,R7                                                   
         EJECT                                                                  
***********************************************************************         
* MOVE BACK COMPLETED TRANSACTION TO TOR UTL / TBUFF                  *         
***********************************************************************         
         SPACE 1                                                                
CHECKAOR OC    MODE,MODE           SRTIM INITIALISATION RUN?                    
         BNZR  RB                  YES - BACK TO ADNEXT                         
*                                                                               
         GOTO1 =A(TORBACK)         COPY INFO BACK TO TOR                        
         BR    RB                                                               
         EJECT                                                                  
***********************************************************************         
* NOTHING TO DISPATCH - SEE IF VTAM HAS ANYTHING                      *         
***********************************************************************         
         SPACE 1                                                                
VTAMDSPC TM    POSTECB,X'40'       CHECK AOR COMPLETIONS FIRST                  
         BZ    VTDS02                                                           
         BRAS  RE,PCOMPS           PROCESS ALL AOR COMPLETIONS                  
         BNE   SEFST               REQUEUED TRANSACTIONS                        
*                                                                               
VTDS02   TM    ABNDECB,X'40'       THEN TEST AOR ABENDED                        
         BZ    VTDS04                                                           
         NI    ABNDECB,255-X'40'                                                
         GOTO1 VTORRCV             RECOVER ABENDING AOR                         
         B     SEFST                                                            
*                                                                               
VTDS04   L     R7,=V(RPLLST)       POINT TO VTAM RPLLST                         
         USING FARPLD,R7                                                        
*                                                                               
VTDS06   L     RE,=V(LCMECB)       TEST LINE CONTROL ECB                        
         TM    0(RE),X'40'                                                      
         BO    VTDS08                                                           
*                                                                               
         L     RE,FARPLRPL         POINT TO RPL                                 
         TM    8(RE),X'40'         TEST VTAM ECB POSTED                         
         BO    VTDS08              YES                                          
         ICM   R7,15,FARPLNXT      POINT TO NEXT RPL                            
         BNZ   VTDS06                                                           
         B     VTDS10                                                           
         SPACE 1                                                                
***********************************************************************         
* COMPLETION HAS BEEN POSTED - CALL LCM TO PROCESS IT                 *         
***********************************************************************         
         SPACE 1                                                                
VTDS08   GOTO1 VLCM,DUB,VTWAIT                                                  
         B     SEFST               SEE IF ANY NEW SE'S TO DISPATCH              
         SPACE 1                                                                
***********************************************************************         
* CHECK MQIO EVENT POSTED                                             *         
***********************************************************************         
         SPACE 1                                                                
VTDS10   CLI   MQIOFLAG,0          TEST FOR MQIO ACTIVE                         
         BE    IOWAIT                                                           
         ICM   RE,15,=V(MQIOECB)   THEN TEST MQIO ECB                           
         BZ    VTDS12                                                           
         TM    0(RE),X'40'                                                      
         BZ    VTDS12                                                           
         NI    0(RE),255-X'40'                                                  
*                                  PROCESS MQIO INPUT                           
         GOTO1 VMQIO,PARM,=CL8'INPUT'                                           
         B     SEFST                                                            
*                                                                               
VTDS12   L     RF,VSSB             DETACH ANY ATTACHED SUB-TASKS                
         L     R5,SSBAATC-SSBD(RF)                                              
         LH    R6,0(R5)                                                         
         L     R7,2(R5)                                                         
         LA    R5,6(R5)                                                         
         USING ATCD,R5             R5=A(ATC)                                    
*                                                                               
VTDS14   CLI   ATCTYPE,ATCTMQIO    TEST FAMQIO SUB TASK                         
         BNE   VTDS18              NO                                           
         LA    RE,ATCOSECB         TEST FOR O/S POSTED COMPLETION               
         TM    0(RE),X'40'                                                      
         BZ    VTDS18                                                           
         NI    0(RE),255-X'40'                                                  
         TM    ATCSTAT1,ATCSRSET   TEST FAMQIO SUB TASK RESET FLAG              
         BNZ   VTDS16                                                           
         TM    ATCSTAT1,ATCSATCH   TEST FAMQIO SUB TASK ATTACHED                
         BZ    VTDS18                                                           
         NI    ATCSTAT1,X'FF'-ATCSATCH                                          
         OI    ATCSTAT1,ATCSERRS   SET ERROR OCCURRED IN TASK                   
         OI    ATCSTAT2,ATCSUDER                                                
         LA    R1,ATCOSTCB         DETACH TASK                                  
         DETACH (1),STAE=NO                                                     
*                                  PROCESS FAMQIO ENDED                         
         GOTO1 VMQIO,PARM,=CL8'END'                                             
         B     SEFST                                                            
*                                                                               
VTDS16   MVI   ATCSTAT1,0                                                       
         MVC   ATCARECB,=V(MQIOECB)                                             
         MVC   ATCAIOPL,=V(MQIOLST)                                             
         LA    R6,ATCOSECB         RE-ATTACH MQIO SCHEDULER                     
         ATTACH EP=FAMQIO,PARAM=((R5)),ECB=(R6),SZERO=YES                       
         ST    R1,ATCOSTCB         SAVE A(OPERATING SYSTEM TCB)                 
         B     SEFST                                                            
*                                                                               
VTDS18   BXLE  R5,R6,VTDS14                                                     
         B     IOWAIT                                                           
         DROP  R5                                                               
         EJECT                                                                  
***********************************************************************         
* NOTHING TO DO - WAIT ON DISK I/O COMPLETIONS IF ANY PENDING         *         
***********************************************************************         
         SPACE 1                                                                
IOWAIT   L     R3,VTCB             START AT FIRST TASK                          
         USING TCBD,R3                                                          
         LH    R4,0(R3)                                                         
         L     R5,2(R3)                                                         
         LA    R3,6(R3)                                                         
         CLI   TORFLAG,C'Y'        AOR HAS EXTRA TASK FOR OWN WORK              
         BE    *+6                                                              
         AR    R5,R4                                                            
*                                                                               
         LHI   R0,-1               R0=TIME TO WAIT FOR DSPACE OR $WAIT          
         LA    R1,ECBLST           R1=A(NEXT ECBLIST ENTRY)                     
         XC    0(4,R1),0(R1)                                                    
*                                                                               
IOWT02   ICM   RE,15,TCBSVECB      CAN I WAIT ON THIS TASK                      
         BZ    IOWT10              NO                                           
         CLI   TCBSVECB,X'FC'      TEST WHAT TYPE OF ECB                        
         BL    IOWT04              NORMAL                                       
         CLI   TCBSVECB,X'FF'      TEST WHAT TYPE OF ECB                        
         BE    IOWT06              NORMAL                                       
         B     IOWT08              DATASPACE-WAIT TYPE ECB                      
*                                                                               
IOWT04   TM    0(RE),X'40'         TEST COMPLETION ON NORMAL ECB                
         BO    TASK300                                                          
         ST    RE,0(R1)            PUT NORMAL ECBS IN ECBLIST                   
         LA    R1,4(R1)                                                         
         B     IOWT14                                                           
*                                                                               
IOWT06   TM    0(RE),X'40'         TEST COMPLETION ON DO-NOT-WAIT ECB           
         BO    TASK300             YES - PROCESS COMPLETION                     
         B     IOWT14                                                           
*                                                                               
IOWT08   LAM   R0,RF,ARZERO        RESET ACCESS REGISTERS                       
         SAC   512                 SET ACCESS REG MODE                          
         L     RE,VSSB                                                          
         USING SSBD,RE                                                          
         LAM   R2,R2,SSBALET       SET UP ACCESS REGISTER                       
*                                                                               
         CLI   TCBSVECB,X'FD'      TEST WHAT TYPE OF ECB                        
         BNE   *+8                                                              
         LAM   R2,R2,SSBTBLET      FD FOR TABS WAIT                             
*                                                                               
         CLI   TCBSVECB,X'FC'      TEST WHAT TYPE OF ECB                        
         BNE   *+8                                                              
         LAM   R2,R2,SSBPGMTA      FC FOR PGMS WAIT                             
         SR    R2,R2                                                            
         DROP  RE                                                               
*                                                                               
         XR    RE,RE               EBC CONTAINS INDEX TO DATASPACE ECB          
         ICM   RE,7,TCBSVECB+1                                                  
         AR    R2,RE               INDEX TO DATASPACE ECB                       
         TM    0(R2),X'40'         TEST POSTED COMPLETE                         
         SAC   0                                                                
         BO    TASK300             PROCESS COMPLETION                           
*                                                                               
         C     R0,T10SECS          DATASPACE WAIT 10 SECONDS                    
         BL    IOWT14                                                           
         L     R0,T10SECS          DATASPACE WAIT 10 SECONDS                    
         B     IOWT14                                                           
*                                                                               
IOWT10   ICM   RE,15,TCBTIMEW      TEST FOR TASK WAIT                           
         BZ    IOWT14                                                           
         STM   R0,R1,DUB           PROTECT R0,R1 FROM TIME MACRO                
         TIME  TU                                                               
         LR    RE,R0               RE=TIME NOW (TUS)                            
         LM    R0,R1,DUB                                                        
         ICM   RF,15,TCBTIMEW                                                   
         SR    RF,RE               RF=TUS UNTIL WAKEUP TIME                     
         BM    IOWT12                                                           
         CLR   R0,RF               R0 = TIME TO GO UNTIL POP                    
         BL    IOWT14                                                           
         LR    R0,RF               SAVE SMALLEST TIME                           
         B     IOWT14                                                           
                                                                                
IOWT12   MVC   TCBTIMEW,=F'-1'     SET TO FFS IF READY TO RUN                   
         B     TASK300                                                          
*                                                                               
IOWT14   BXLE  R3,R4,IOWT02        BACK FOR NEXT TCB ENTRY                      
*                                                                               
IOWT16   OC    ECBLST(4),ECBLST    ANY ENTRIES IN ECBLST                        
         BZ    POSTWAIT            NO                                           
*                                                                               
         XC    POSTRECB,POSTRECB                                                
         LA    RE,POSTRECB         ADD POST ECB                                 
         ST    RE,0(R1)                                                         
         LA    R1,4(R1)                                                         
*                                                                               
         L     RE,VSSB                                                          
         USING SSBD,RE                                                          
         MVC   0(4,R1),SSBOPECB    ADD OPER ECB TO LIST                         
         OI    0(R1),X'80'         SET EOL FLAG                                 
         LA    R1,ECBLST                                                        
         WAIT  ECBLIST=(1)         WAIT FOR DISK I/O COMPLETION                 
*                                                                               
         L     R1,SSBOPECB         TEST FOR OPERATOR INTERUPT                   
         TM    0(R1),X'40'                                                      
         BO    SEFST               GO PROCESS IT                                
*                                                                               
         TM    POSTRECB,X'40'      TEST POST                                    
         BO    SEFST               GO PROCESS IT                                
         B     TASK300                                                          
         DROP  RE                                                               
         EJECT                                                                  
***********************************************************************         
* NO DISK I/OS PENDING - WAIT ON VTAM, INTERVAL, DSPACE POST          *         
***********************************************************************         
         SPACE 1                                                                
POSTWAIT L     RE,VSSB                                                          
         USING SSBD,RE                                                          
         TM    SSBSTAT1,SSBSEOJ    TEST EOJ PENDING                             
         BZ    PWT02                                                            
         C     R0,=F'-1'           TEST WAITING FOR ANYTHING                    
         BNE   PWT02                                                            
         L     RF,=A(ADLAST)       COMPLETE EOJ HOUSE KEEPING                   
         BASR  RE,RF                                                            
         LM    RB,RE,ADFIRREG      RESTORE RETURN REGISTERS                     
         BR    RE                  RETURN TO FALOAD                             
*                                                                               
PWT02    OI    SSBSTAT1,SSBSVTAM   SET AWAITING VTAM                            
         C     R0,=F'-1'           FFS MEANS NO TIMER3                          
         BE    PWT04                                                            
*        XR    R0,R0                                                            
         GOTO1 VTICTOC,DUB,C'3SET',(R0)  SET T3                                 
*                                                                               
PWT04    GOTO1 VTICTOC,DUB,C'WSET' START AN EVENT TIMER                         
         CLI   MQIOFLAG,0          TEST FOR MQIO ACTIVE                         
         BE    PWT05                                                            
*                                  RESET MQIO INPUT TRIGGER MONITOR             
         GOTO1 VMQIO,PARM,=CL8'RESET'                                           
*                                                                               
PWT05    LA    R1,ECBLST           BUILD ECBLST FOR EXTERNAL WAIT               
         CLI   TORFLAG,C'N'                                                     
         BE    PWT06               TOR WAIT FOR VTAM AOR WAIT FOR POST          
*                                                                               
         L     RE,=V(LCMECB)       LCM CONTROL ECB IS 1ST ENTRY                 
         ST    RE,0(R1)                                                         
         LA    R1,4(R1)                                                         
         B     PWT07                                                            
*                                                                               
PWT06    XC    POSTRECB,POSTRECB   POST ECB IS 1ST ENTRY                        
         LA    RE,POSTRECB                                                      
         ST    RE,0(R1)                                                         
         LA    R1,4(R1)                                                         
*                                                                               
PWT07    XC    TIMERECB,TIMERECB   TIMER ECB IS 2ND ENTRY                       
         LA    RE,TIMERECB                                                      
         ST    RE,0(R1)                                                         
         LA    R1,4(R1)                                                         
*                                                                               
         XC    DSPACECB,DSPACECB   DSPACE ECB IS 3RD ENTRY                      
         LA    RE,DSPACECB                                                      
         ST    RE,0(R1)                                                         
         LA    R1,4(R1)                                                         
*                                                                               
         XC    DSOPSECB,DSOPSECB   DSPACE OPERATOR COMMAND ECB                  
         LA    RE,DSOPSECB                                                      
         ST    RE,0(R1)                                                         
         LA    R1,4(R1)                                                         
*                                                                               
         CLI   TORFLAG,C'N'        AOR COMPLETIONS FOR TOR ONLY                 
         BE    PWTMQ                                                            
*                                                                               
         XC    POSTECB,POSTECB     AOR COMPLETION ECB                           
         LA    RE,POSTECB                                                       
         ST    RE,0(R1)                                                         
         LA    R1,4(R1)                                                         
*                                                                               
         XC    ABNDECB,ABNDECB     AOR ABEND ECB                                
         LA    RE,ABNDECB                                                       
         ST    RE,0(R1)                                                         
         LA    R1,4(R1)                                                         
         EJECT                                                                  
***********************************************************************         
*        ADD MQ ECBS                                                  *         
***********************************************************************         
         SPACE 1                                                                
PWTMQ    CLI   MQIOFLAG,0          TEST MQIO ACTIVE                             
         BE    PWT20                                                            
         ICM   RE,15,=V(MQIOECB)   MQIO ECB                                     
         BZ    PWT10                                                            
         ST    RE,0(R1)                                                         
         LA    R1,4(R1)                                                         
*                                                                               
PWT10    L     RF,VSSB             ADD OS ECB OF FAMQIO ATTACHED TASK           
         L     R5,SSBAATC-SSBD(RF)                                              
         LH    R6,0(R5)                                                         
         L     R7,2(R5)                                                         
         LA    R5,6(R5)                                                         
         USING ATCD,R5             R5=A(ATC)                                    
PWT11    CLI   ATCTYPE,ATCTMQIO    TEST FAMQIO SUB-TASK                         
         BNE   PWT12               NO                                           
         TM    ATCSTAT1,ATCSATCH   TEST ATTACHED                                
         BZ    PWT12               NO                                           
         LA    RE,ATCOSECB                                                      
         ST    RE,0(R1)                                                         
         LA    R1,4(R1)                                                         
PWT12    BXLE  R5,R6,PWT11                                                      
         DROP  R5                                                               
*                                                                               
         DROP  R3                                                               
         L     R5,VTCB             ADD MQIO TASK WAIT ECB'S                     
         USING TCBD,R5                                                          
         LH    R6,0(R5)                                                         
         L     R7,2(R5)                                                         
         LA    R5,6(R5)                                                         
         CLI   TORFLAG,C'Y'        AOR HAS EXTRA TASK FOR OWN WORK              
         BE    *+6                                                              
         AR    R5,R4                                                            
*                                                                               
PWT13    TM    TCBFLAG3,TCBMQWT    TEST TASK WAITING ON MQIO                    
         BZ    PWT14                                                            
         ICM   RE,15,TCBMQERT                                                   
         BZ    PWT14                                                            
         ST    RE,0(R1)                                                         
         LA    R1,4(R1)                                                         
*                                                                               
PWT14    BXLE  R5,R6,PWT13         BACK FOR NEXT TCB ENTRY                      
         DROP  R5                                                               
         EJECT                                                                  
***********************************************************************         
*        COMPLETE ECB LIST AND WAIT                                             
***********************************************************************         
         SPACE 1                                                                
PWT20    CLI   TORFLAG,C'N'        VTAM ECBS FOR TOR ONLY                       
         BE    PWT40                                                            
*                                                                               
         L     R7,=V(RPLLST)       VTAM RPL ECB'S ARE REST OF LIST              
         USING FARPLD,R7                                                        
PWT30    L     RE,FARPLRPL         POINT TO RPL                                 
         LA    RE,8(RE)            POINT TO INTERNAL ECB                        
         ST    RE,0(R1)                                                         
         LA    R1,4(R1)                                                         
         ICM   R7,15,FARPLNXT      POINT TO NEXT RPL                            
         BNZ   PWT30                                                            
*                                                                               
PWT40    L     RE,VSSB                                                          
         MVC   0(4,R1),SSBOPECB    ADD OPERATOR ECB TO LIST AT END              
         OI    0(R1),X'80'         SET EOL FLAG                                 
         LA    R1,ECBLST                                                        
         WAIT  ECBLIST=(1)         WAIT ON VTAM/TIMER                           
*                                                                               
         NI    SSBSTAT1,255-SSBSVTAM                                            
*                                                                               
         TM    DSOPSECB,X'40'      TEST FOR DSPACE OPER                         
         BO    SEFST                                                            
         L     R1,SSBOPECB         TEST FOR OPERATOR INTERUPT                   
         TM    0(R1),X'40'                                                      
         BO    SEFST               GO PROCESS IT                                
*                                                                               
         LA    R1,TIMERECB         TEST FOR TIMER INTERUPT                      
         TM    0(R1),X'40'                                                      
         BO    SEFST               GO PROCESS IT                                
*                                                                               
         TM    POSTRECB,X'40'                                                   
         BO    SEFST                                                            
*                                                                               
         LA    R1,DSPACECB         TEST FOR DSPACE INTERUPT                     
         TM    0(R1),X'40'                                                      
         BO    SEFST               GO PROCESS IT                                
*                                                                               
         GOTO1 VTICTOC,DUB,C'SSET' SUSPEND TIMERS                               
*                                                                               
         CLI   TORFLAG,C'Y'                                                     
         BE    VTAMDSPC            GO PROCESS POSTED COMPLETION                 
         B     IOWAIT              GO PROCESS POSTED COMPLETION                 
         EJECT                                                                  
***********************************************************************         
* DISK I/O COMPLETION POSTED - FIND WHICH TASK                        *         
***********************************************************************         
         SPACE 1                                                                
         USING TCBD,R3             NOTE R4 HAS LEN,R5 HAS END ADDR              
TASK300  L     R3,LASTTCB          GET LAST TCB POSTED                          
         B     TASK350             BUMP TO NEXT                                 
*                                                                               
TASK320  ICM   R1,15,TCBSVECB      GET IOB ADDRESS                              
         BZ    TASK340                                                          
         CLI   TCBSVECB,X'FC'      TEST DATASPACE-WAIT                          
         BL    TASK330                                                          
         CLI   TCBSVECB,X'FE'      TEST DATASPACE-WAIT                          
         BH    TASK330                                                          
*                                                                               
         LAM   R0,RF,ARZERO                                                     
         SAC   512                 SET ACCESS MODE                              
         L     RE,VSSB                                                          
         USING SSBD,RE                                                          
         LAM   R2,R2,SSBALET       SET UP ACCESS REGISTER                       
         CLI   TCBSVECB,X'FD'      TEST TABS DATASPACE                          
         BNE   *+8                                                              
         LAM   R2,R2,SSBTBLET      SET UP ACCESS REGISTER                       
         CLI   TCBSVECB,X'FC'      TEST PGMS DATASPACE                          
         BNE   *+8                                                              
         LAM   R2,R2,SSBPGMTA      SET UP ACCESS REGISTER                       
         SR    R2,R2                                                            
         DROP  RE                                                               
*                                                                               
         XR    RE,RE                                                            
         ICM   RE,7,TCBSVECB+1                                                  
         AR    R2,RE               INDEX TO DATASPACE ECB                       
         TM    0(R2),X'40'         TEST POSTED COMPLETE                         
         SAC   0                                                                
         LAM   R0,RF,ARZERO        AR2 NEEDS CLEARING !                         
         BO    TASK400             DISPATCH IF FREE                             
         B     TASK350                                                          
*                                                                               
TASK330  TM    0(R1),X'40'         TEST POSTED COMPLETE                         
         BO    TASK400             YES                                          
         B     TASK350                                                          
*                                                                               
TASK340  OC    TCBSVRD,TCBSVRD     IS RD SAVED                                  
         BZ    TASK350             NO                                           
         CLC   TCBTIMEW,=F'-1'     IS IT TIME TO WAKE UP                        
         BE    TASK400                                                          
         OC    TCBTIMEW,TCBTIMEW   ARE WE WAITING FOR WAKE UP                   
         BNZ   TASK350                                                          
         OC    TCBLOCK+1(3),TCBLOCK+1                                           
         BZ    TASK400             DISPATCH IF DATA NOW FREE                    
*                                                                               
TASK350  BXLE  R3,R4,TASK320                                                    
         L     R3,VTCB             NOW START AT TOP OF LIST                     
         LA    R3,6(R3)                                                         
         B     TASK320             LOOP BACK                                    
         EJECT                                                                  
***********************************************************************         
* THIS TASK IS READY TO RUN - TEST IF CANCEL PENDING FROM TERMINAL    *         
***********************************************************************         
         SPACE 1                                                                
TASK400  L     RE,TCBUTL           TEST FOR CANCEL PENDING THIS TASK            
         TM    TSTAT2-UTLD(RE),TSTATCAN                                         
         BZ    TASK500                                                          
         CLI   TCBSYS,1            IGNORE IF S/R PROGRAM                        
         BE    TASK500                                                          
         OC    TCBRCVC,TCBRCVC     IGNORE IF RECS IN RECOVERY FILE              
         BNZ   TASK500                                                          
         TM    TCBFLAG1,TCBENQPQ+TCBENQWK IGNORE IF ENQUEUE ISSUED              
         BNZ   TASK500                                                          
         TM    TCBFLAG2,TCBENQFW+TCBENQCT+TCBENQMZ                              
         BNZ   TASK500                                                          
         TM    TCBFLAG1,TCBIRALC   IGNORE IF IRDR ALLOCATED                     
         BNZ   TASK500                                                          
*                                                                               
         L     R0,=V(MONBRPRG)                                                  
         L     RF,TCBWRKA                                                       
         L     RF,8(RF)            POINT TO MONITOR'S REGISTERS                 
         CLM   R0,7,13(RF)                                                      
         BNE   TASK500             IGNORE IF RE NOT VIA MONBRPRG                
         MVC   TCBSVRD,8(RF)       SET RD TO RETURN DIRECT TO MONITOR           
         XC    TCBPSW(72),TCBPSW   NEGATE TIMER POP EXIT EFFECTOR               
         MVC   TSVCREQ-UTLD(2,RE),$CAN                                          
         B     TASK600             EXIT BACK TO MONITOR WITH $CAN               
         EJECT                                                                  
***********************************************************************         
* THIS TASK IS READY TO RUN - TEST IF PRIORITIZED TASK                *         
***********************************************************************         
         SPACE 1                                                                
TASK500  C     R3,LASTTCB          DISPATCH IF THIS TASK SAME AS LAST           
         BE    TASK600                                                          
         ST    R3,LASTTCB          SAVE TCB ENTRY ADDRESS                       
         USING TCBD,R3                                                          
         SR    R2,R2               SET R2 TO PROGRAM PRIORITY                   
         ICM   R2,1,TCBPRTY                                                     
         BZ    TASK600                                                          
         TBIN  MILLI               GET TIME IN MILLI SECS                       
         MR    R0,R0                                                            
         ALR   R1,R0                                                            
         SRL   R1,5                SQUARE,FOLD,EXTRACT                          
         N     R1,=X'0000000F'                                                  
         L     RE,VSSB                                                          
         USING SSBD,RE                                                          
         L     RF,SSBPRTST                                                      
         LA    RF,1(RF)                                                         
         ST    RF,SSBPRTST                                                      
         CR    R1,R2               COMPARE TIME TO PRIORITY                     
         BH    TASK300             IF HIGH DONT DISPATCH THIS TIME              
         L     RF,SSBPRPAS                                                      
         LA    RF,1(RF)                                                         
         ST    RF,SSBPRPAS                                                      
         B     TASK600                                                          
         EJECT                                                                  
***********************************************************************         
* THIS TASK IS READY TO RUN - RETURN CONTROL BACK TO HIM              *         
***********************************************************************         
         SPACE 1                                                                
TASK600  L     RD,TCBSVRD          GET SAVED REG AND CLEAR SAVE AREA            
         XC    TCBSVECB(8),TCBSVECB                                             
         OC    TCBPSW,TCBPSW       TEST WAIT CAUSED BY TIMER POP                
         BNZ   TPOP                                                             
*                                                                               
         L     RE,VSSB             SET ACTIVE TCB ADDRESS IN SSB                
         USING SSBD,RE                                                          
         ST    R3,SSBTKADR                                                      
         USING TCBD,R3                                                          
         GOTO1 VDTFIOA,DUB,(C'R',(R3)) RESTORE DTF SAVE DATA                    
         L     RE,VSSB                                                          
*                                                                               
         CLC   TCBTIMEW,=F'-1'     TEST TIME TO WAKE UP                         
         BNE   *+14                                                             
         XC    TCBTIMEW,TCBTIMEW   CLEAR TIME FIELD                             
         B     TASK620                                                          
*                                                                               
         TM    TCBFLAG3,TCBSCPWT   TEST SCRIPT COMPLETION                       
         BZ    TASK610                                                          
         NI    TCBFLAG3,255-TCBSCPWT                                            
         B     TASK620                                                          
*                                                                               
TASK610  TM    TCBFLAG3,TCBMQWT    TEST MQIO COMPLETION                         
         BZ    TASK630                                                          
         NI    TCBFLAG3,255-TCBMQWT                                             
*                                                                               
TASK620  GOTO1 VTICTOC,DUB,C'1SET',1                                            
         XC    TCBIRPTS,TCBIRPTS   RESET TIMER INTERRUPT COUNTER                
         MVC   TCBTIME,DUB         SET EVENT TIME                               
         B     DIOEXIT                                                          
*                                                                               
TASK630  CLI   TCBPRTY,0           TEST IF PRIORITIZED TASK                     
         BE    TASK640                                                          
         TM    TCBPRTYF,X'80'      TEST PREVIOUSLY EXCEEDED                     
         BO    TASK640                                                          
         CLC   TCBIOCNT+1(2),SSBPRIO TEST I/O THRESHOLD EXCEEDED                
         BL    TASK640                                                          
         ZIC   RF,TCBPRTY          DECREMENT TASK PRIORITY                      
         SRL   RF,1                                                             
         CH    RF,SSBPRMIN                                                      
         BNL   *+8                                                              
         LH    RF,SSBPRMIN                                                      
         STC   RF,TCBPRTY                                                       
         OI    TCBPRTYF,X'80'      SET TASK EXCEEDED I/O THRESHOLD              
*                                                                               
TASK640  GOTO1 VTICTOC,DUB,C'1SET',1                                            
         XC    TCBIRPTS,TCBIRPTS   RESET TIMER INTERRUPT COUNTER                
         MVC   TCBTIME,DUB         SET EVENT TIME                               
         L     RE,VSSB                                                          
         SR    R0,R0                                                            
         ICM   R0,3,SSBMAXIO                                                    
         CLM   R0,7,TCBIOCNT       TEST I/O COUNT WITH NORMAL MAX               
         BNL   DIOEXIT                                                          
*                                                                               
         L     R1,TCBUTL           GET A(TASK UTL ENTRY)                        
         SR    RF,RF                                                            
         TM    TSTAT6-UTLD(R1),TST6SCRP  TEST SCRIPT EXECUTING                  
         BO    DIOEXIT                                                          
         TM    TSTATU-UTLD(R1),TSTATMIO  TEST PROGRAM WANTS TO EXCEED           
         BO    DIOEXIT                                                          
         OC    TSVCREQ-UTLD(2,R1),TSVCREQ-UTLD(R1)                              
         BZ    TASK650                                                          
         CLC   TSVCREQ-UTLD(2,R1),$WKR   TEST SPECIAL SERVICE REQUESTS          
         BE    DIOEXIT                                                          
         CLC   TSVCREQ-UTLD(2,R1),$BC                                           
         BE    DIOEXIT                                                          
         CLC   TSVCREQ-UTLD(2,R1),$$BYE                                         
         BE    DIOEXIT                                                          
         CLC   TSVCREQ-UTLD(2,R1),$UNWIND                                       
         BE    DIOEXIT                                                          
         CLC   TSVCREQ-UTLD(2,R1),$ABEND                                        
         BE    DIOEXIT                                                          
         ICM   RF,7,TASVC-UTLD(R1) IF S/R - POINT TO S/R PGMLST ENTRY           
         BNZ   TASK660                                                          
         B     TASK670                                                          
*                                                                               
TASK650  ICM   RF,7,TAPRG-UTLD(R1) GET A(PGM LIST ENTRY)                        
         BZ    TASK670                                                          
         XR    RE,RE                                                            
         ICM   RE,7,TARSYS-UTLD(R1)                                             
         BZ    TASK670                                                          
         ICM   RE,15,SEPGMS-SELISTD(RE)                                         
         AR    RF,RE                                                            
*                                                                               
TASK660  TM    PGMIND-PGMLSTD(RF),PGMIIOB TEST IF PGM I/O BOUND                 
         BZ    TASK670                                                          
         L     R0,MAXMAXIO         NORMAL MAXIMUM I/O COUNT                     
         CLI   TCBSWSOV,6                                                       
         BNE   *+8                                                              
         L     R0,ACCMAXIO         ACCOUNT MAXIMUM I/O COUNT                    
         CLM   R0,7,TCBIOCNT                                                    
         BNL   DIOEXIT                                                          
*                                                                               
TASK670  MVC   TASK680+1(3),TCBIOCNT STORE ACTUAL NUMBER OF I/OS                
         L     RF,TCBLIOCT                                                      
         A     RF,TCBLIOC                                                       
         ST    RF,TCBLIOCT         BUMP TOTAL LOGICAL I/OS                      
         XC    TCBLIOC,TCBLIOC                                                  
         XC    TCBIOCNT,TCBIOCNT                                                
*                                                                               
TASK680  DC    XL4'00'             DIE IF TASK HAS EXCEEDED MAX I/0'S           
*                                                                               
DIOEXIT  LAM   R0,RF,TCBIARS       RESTORE ACCESS REGISTERS (ADWAIT)            
         XIT1  ,                   EXIT BACK TO WHERE TASK ISSUED I/O           
         EJECT                                                                  
***********************************************************************         
* THIS PROCESSING USED WHEN WAIT CAUSED BY TIMER POP                  *         
***********************************************************************         
         SPACE 1                                                                
TPOP     L     RD,=A(TASKWORK)     POINT TO WORK AREA FOR SUBR ENTRY            
         L     RE,VSSB             SET ACTIVE TCB ADDRESS IN SSB                
         USING SSBD,RE                                                          
         ST    R3,SSBTKADR                                                      
         GOTO1 VDTFIOA,DUB,(C'R',(R3)) RESTORE DTF SAVE DATA                    
*                                                                               
         L     RE,VSSB                                                          
         CLI   TCBPRTY,0           TEST IF PRIORITIZED TASK                     
         BE    TPOP02                                                           
         TM    TCBPRTYF,X'40'      TEST PREVIOUSLY EXCEEDED                     
         BO    TPOP02                                                           
         CLC   TCBCPUTM+2(2),SSBPRCPU                                           
         BL    TPOP02                                                           
         XR    RF,RF                                                            
         IC    RF,TCBPRTY          DECREMENT TASK PRIORITY                      
         SRL   RF,1                                                             
         CH    RF,SSBPRMIN                                                      
         BNL   *+8                                                              
         LH    RF,SSBPRMIN                                                      
         STC   RF,TCBPRTY                                                       
         OI    TCBPRTYF,X'40'      SET TASK EXCEEDED CPU THRESHOLD              
*                                                                               
TPOP02   L     RE,=V(ABENDWHY)                                                  
         MVI   0(RE),C'X'          SET TASKER EXIT FLAG FOR PC ROUTINE          
         DC    H'0'                AND EXIT VIA PROGRAM CHECK                   
         DROP  R3,RE                                                            
         EJECT                                                                  
***********************************************************************         
* MISCELLANEOUS LITERALS                                              *         
***********************************************************************         
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* SCHEDULE BEST QUALIFYING ENTRY IN SELIST                            *         
* NTRY: R9     = A(TASKCNST)                                          *         
*     : RA     = V(SYSFAC)                                            *         
* EXIT: CC EQ  = ENTRY TO DISPATCH - ENTRY RETURNED IN R7             *         
*     : CC NEQ = NO ENTRY TO DISPATCH                                 *         
***********************************************************************         
         SPACE 1                                                                
SCHEDULE DS    0H                                                               
         NMOD1 0,SCHEDULE                                                       
         L     RE,VSSB                                                          
         USING SSBD,RE                                                          
*                                                                               
         L     R3,VSELIST          R3=A(FIRST SELIST ENTRY)                     
         LH    R4,0(R3)            BXLE ON R3,R4,R5                             
         L     R5,2(R3)                                                         
         LA    R3,6(R3)                                                         
         USING SELISTD,R3                                                       
         SPACE 2                                                                
***********************************************************************         
* DISPATCH SPECIAL S/R IF NECESSARY                                   *         
* TERMBLD IS THE ONLY SPECIAL S/R TO USE UTLZERO                      *         
* ALL OTHER SPECIAL S/R USE UTLZEROA  - FZSA  17APR96                 *         
***********************************************************************         
         SPACE 1                                                                
         LA    R2,UTLZERO          POINT TO DUMMY UTL ENTRY                     
         USING UTLD,R2                                                          
         TM    TSTAT2,TSTATTIP     TEST BUSY                                    
         BO    SCHD04              YUP - FUHGEDABOUDIT                          
         XC    TSVDATA1,TSVDATA1   CLEAR THIS FOR SAFETY                        
*                                                                               
         TM    SSBOPECB,X'80'      TEST SROPS IN PROGRESS                       
         BO    SCHD02                                                           
         TM    DSOPSECB,X'40'      TEST DSPACE OPS ECB POSTED                   
         BO    *+16                                                             
         L     RF,SSBOPECB         TEST OPERATOR ECB POSTED                     
         TM    0(RF),X'40'                                                      
         BZ    SCHD02                                                           
         XC    DSOPSECB,DSOPSECB                                                
         MVC   DUB+4(2),$OPS       PROCESS OPER INPUT                           
         B     SCHD06                                                           
*                                                                               
SCHD02   CLI   MODE,0              TEST TIMER EXPIRED MODE                      
         BE    *+14                                                             
         MVC   DUB+4(2),$T2                                                     
         B     SCHD06                                                           
*                                                                               
         TM    SSBVTFL1,SSBVTTBP+SSBVTTCP TEST TERMBLD PENDING                  
         BZ    SCHD04                                                           
         MVC   DUB+4(2),$VTL                                                    
         B     SCHD06                                                           
*                                                                               
SCHD04   LA    R2,UTLZEROA         POINT TO EXTRA DUMMY UTL                     
         USING UTLD,R2                                                          
         TM    TSTAT2,TSTATTIP     IGNORE IF ALREADY IN PROCESS                 
         BO    SCHD08                                                           
         XC    TSVDATA1,TSVDATA1   CLEAR THIS FOR SAFETY                        
*                                                                               
         TM    SSBJFLAG,SSBJFEXT   TEST EXTRACT UPDATE PENDING                  
         BZ    *+14                                                             
         MVC   DUB+4(2),$EXT                                                    
         B     SCHD06                                                           
*                                                                               
         TM    SSBJFLAG,SSBJFWKR   TEST FACWRK RECOVERY UPDATE PENDING          
         BZ    *+14                                                             
         MVC   DUB+4(2),$WKR                                                    
         B     SCHD06                                                           
*                                                                               
         TM    SSBJFLG2,SSBJFSCP   TEST WRKF SCRIPT PENDING                     
         BZ    *+14                                                             
         MVC   DUB+4(2),$SCR                                                    
         B     SCHD06                                                           
*&&US                                                                           
         TM    SSBDARFL,SSBDRUPD   DOES THIS SYSTEM USE $DARE?                  
         BZ    SCHD08              NO                                           
*                                                                               
         TM    SSBDARFL,SSBDRRPQ   CALL $REPDARE IF ENTRY IN TABLE              
         BZ    *+14                                                             
         MVC   DUB+4(2),$REPDARE                                                
         B     SCHD06                                                           
*                                                                               
         TM    SSBDARFL,SSBDRSDR   CALL $SPTDARE IF ENTRY IN TABLE              
         BZ    *+14                                                             
         MVC   DUB+4(2),$SPTDARE                                                
         B     SCHD06                                                           
*                                                                               
         TM    SSBDARFL,SSBDRSPQ   CALL $MKGDARE IF ENTRY IN TABLE              
         BZ    *+14                                                             
         MVC   DUB+4(2),$MKGDARE                                                
         B     SCHD06                                                           
*&&                                                                             
         B     SCHD08                                                           
*                                                                               
SCHD06   CLM   R2,7,SEFIRST+1      ARE WE AT HEAD OF QUEUE                      
         BE    SCHD08              YES - IGNORE                                 
*                                                                               
         MVC   TNEXTIN,SEFIRST+1   SET LINK TO NEXT UTL ENTRY                   
         MVC   TAGYB,MODE          SET MODE                                     
         MVC   TSVCREQ,DUB+4       SET SPECIAL S/R                              
         CLC   TSVCREQ+1(1),$T2+1                                               
         BNE   *+8                                                              
         NI    SSBT2,X'7F'                                                      
         MVC   TTIMETU,DUB         SET ARRIVAL TIME TO NOW                      
*                                                                               
         ST    R2,SEFIRST          SET AT TOP OF SE1 QUEUE                      
         SR    RF,RF                                                            
         ICM   RF,3,SEQLEN                                                      
         BNZ   *+8                                                              
         ST    R2,SELAST                                                        
         LA    RF,1(RF)                                                         
         STH   RF,SEQLEN                                                        
         B     SCHDXE              DISPATCH SPECIAL S/R                         
         DROP  RE                                                               
         EJECT                                                                  
***********************************************************************         
* CHECK SERVICE REQUESTS FIRST                                        *         
***********************************************************************         
         SPACE 1                                                                
SCHD08   OC    SEFIRST,SEFIRST     TEST IF SERVICE QUEUE EMPTY                  
         BZ    SCHD10                                                           
         TM    SEIND,SEISTRT+SEINOP SYSTEM IS INHIBITED                         
         BO    SCHD10                                                           
         CLC   SETASK,SETASKMX     DISPATCH IF LESS THAN MAX TASKS              
         BL    SCHDXE                                                           
*                                                                               
         XR    RE,RE                                                            
         ICM   RE,7,SEFIRST+1      POINT TO FIRST TERMINAL IN SE1 QUEUE         
         TM    TSTAT1-UTLD(RE),TSTATDDS                                         
         BNZ   SCHDXE              DISPATCH IF DDS TERMINAL                     
         SPACE 1                                                                
***********************************************************************         
* CHECK SE0 (SCRIPTS) NEXT                                            *         
***********************************************************************         
         SPACE 1                                                                
SCHD10   ICM   R3,15,=V(SELIST0)   POINT TO SCRIPT SYSTEM                       
         BZ    SCHD12                                                           
         OC    SEFIRST,SEFIRST     TEST IF SCRIPT QUEUE EMPTY                   
         BZ    SCHD12                                                           
         TM    SEIND,SEISTRT+SEINOP SYSTEM IS INHIBITED                         
         BO    SCHD12                                                           
         CLC   SETASK,SETASKMX     DISPATCH IF LESS THAN MAX TASKS              
         BL    SCHDXE                                                           
         SPACE 1                                                                
***********************************************************************         
* CHECK CONTROL SYSTEM NEXT                                           *         
***********************************************************************         
         SPACE 1                                                                
SCHD12   ICM   R3,15,=V(SELISTCT)  POINT TO CONTROL SYSTEM                      
         BZ    SCHD14                                                           
         OC    SEFIRST,SEFIRST     TEST IF CONTROL QUEUE EMPTY                  
         BZ    SCHD14                                                           
         TM    SEIND,SEISTRT+SEINOP SYSTEM IS INHIBITED                         
         BO    SCHD14                                                           
         CLC   SETASK,SETASKMX     DISPATCH IF LESS THAN MAX TASKS              
         BL    SCHDXE                                                           
         EJECT                                                                  
***********************************************************************         
* NO SERVICE OR CONTROL SYSTEM WORK                                   *         
* START AT SYSTEM AFTER LAST ONE TO COMPLETE                          *         
***********************************************************************         
         SPACE 1                                                                
SCHD14   XC    DUB,DUB             CLEAR OPTIMUM SYSTEM INFO                    
         L     R3,LASTSE           GET LAST ONE                                 
         B     SCHD18              BUMP TO NEXT                                 
*                                                                               
SCHD16   BAS   RE,CHKMAX                                                        
*                                                                               
SCHD18   BXLE  R3,R4,SCHD16                                                     
         SPACE 1                                                                
***********************************************************************         
* NOW START AT TOP AND GO THRU TO LASTSE                              *         
***********************************************************************         
         SPACE 1                                                                
         L     R5,LASTSE           SET BXLE END PARAM                           
         LA    R5,0(R4,R5)                                                      
         BCTR  R5,0                                                             
         L     R3,VSELIST                                                       
         LA    R3,6(R3,R4)         POINT TO SECOND ENTRY                        
*                                                                               
SCHD20   BAS   RE,CHKMAX                                                        
*                                                                               
SCHD22   BXLE  R3,R4,SCHD20                                                     
         ICM   R3,15,DUB           DISPATCH SE WITH LONGEST QUEUE               
         BNZ   SCHDXE                                                           
         SPACE 1                                                                
***********************************************************************         
* DISPATCH SYSTEM WITH LOWEST SIN IF ALL SYSTEMS ARE AT MAX TASKS     *         
***********************************************************************         
         SPACE 1                                                                
         L     R3,VSELIST                                                       
         LH    R4,0(R3)                                                         
         L     R5,2(R3)                                                         
         LA    R3,6(R3)                                                         
         LA    R3,0(R4,R3)         POINT TO SECOND ENTRY                        
*                                                                               
SCHD24   ICM   R2,15,SEFIRST                                                    
         BZ    SCHD26                                                           
         USING UTLD,R2                                                          
         CLI   SETASK,9            MAX 10 TASKS FOR VERMONT                     
         BNL   SCHD26               (ALLOW 1 TO SWITCH IN)                      
         ST    R3,DUB              SAVE A(SELIST ENTRY)                         
         MVC   DUB+4(4),TSIN       SAVE SIN                                     
         B     SCHD30                                                           
*                                                                               
SCHD26   BXLE  R3,R4,SCHD24                                                     
         B     SCHD32              NOTHING TO DISPATCH                          
*                                                                               
SCHD28   ICM   R2,15,SEFIRST                                                    
         BZ    SCHD30                                                           
         CLC   DUB+4(4),TSIN                                                    
         BL    SCHD30                                                           
         ST    R3,DUB              SAVE A(SELIST ENTRY)                         
         MVC   DUB+4(4),TSIN       SAVE SIN                                     
*                                                                               
SCHD30   BXLE  R3,R4,SCHD28                                                     
         L     R3,DUB                                                           
         B     SCHDXE                                                           
*                                                                               
SCHD32   L     RE,VSSB                                                          
         USING SSBD,RE                                                          
         XC    SSBTKADR,SSBTKADR   RESET TASK ADDRESS                           
         B     SCHDXL                                                           
         DROP  RE                                                               
         SPACE 1                                                                
***********************************************************************         
* FIND TASK WITH LONGEST INPUT QUEUE                                  *         
***********************************************************************         
         SPACE 1                                                                
CHKMAX   OC    SEFIRST,SEFIRST     ANYTHING IN QUEUE                            
         BZR   RE                  NO                                           
         CLC   SETASKMX,SETASK     MAX TASKS IN PROCESS                         
         BER   RE                  YES                                          
         CLI   SETASK,8            MORE THAN 9 IN PROCESS                       
         BHR   RE                  YES - IGNORE                                 
         TM    SEIND,SEISTRT+SEINOP SYSTEM IS INHIBITED                         
         BOR   RE                                                               
         CLI   SETASK,0            ANY TASKS IN PROCESS                         
         BE    SCHDXE              NO - DISPATCH                                
         CLC   SEQLEN,DUB+4        LONGEST QUEUE YET FOUND?                     
         BCR   12,RE               NO                                           
         MVC   DUB+4(2),SEQLEN                                                  
         ST    R3,DUB                                                           
         BR    RE                                                               
         DROP  R2                                                               
*                                                                               
SCHDXE   CR    RB,RB               SET CC EQUAL                                 
         B     SCHDX                                                            
*                                                                               
SCHDXL   CLI   *,255               SET CC LOW                                   
         B     SCHDX                                                            
*                                                                               
SCHDX    LR    R7,R3               SELIST ENTRY RETURNED IN R7                  
         XIT1  REGS=(R7)                                                        
         DROP  R3                                                               
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* FIND BEST AVAILABLE TASK                                            *         
* NTRY: R7 = SELIST ENTRY TO PROCESS                                  *         
* EXIT: CC HI = TOR MUST PROCESS TRANSACTION IN OWN TASKS             *         
*       CC EQ = AOR POSTED TO PROCESS TRANSACTION                     *         
*       CC LO = NO TASKS FREE IN EITHER TOR OR AOR                    *         
***********************************************************************         
         SPACE 1                                                                
         USING SELISTD,R7                                                       
TASKFILL DS    0H                                                               
         NMOD1 0,TASKFILL                                                       
         MVI   TASKMASK,PGMIRTOR   DEFAULT ACTION - RUN IN TOR                  
*                                                                               
         L     R3,SEFIRST                                                       
         USING UTLD,R3                                                          
         XR    RF,RF                                                            
         OC    TSVCREQ,TSVCREQ     SERVICE REQUEST?                             
         BZ    TFIL01              NO                                           
         ICM   RF,7,TASVC                                                       
         BNZ   TFIL02                                                           
         LA    RF,SVCDUM                                                        
         STCM  RF,7,TASVC                                                       
         B     TFIL02                                                           
*                                                                               
TFIL01   ICM   RF,7,TAPRG          CONNECTED TO A PROGRAM?                      
         BZ    TFIL04              NO                                           
*                                                                               
         XR    RE,RE                                                            
         ICM   RE,7,TARSYS                                                      
         BZ    TFIL04                                                           
         ICM   RE,15,SEPGMS-SELISTD(RE)                                         
         AR    RF,RE                                                            
         USING PGMLSTD,RF                                                       
*                                                                               
TFIL02   TM    PGMIND3,PGMIRAOR+PGMIRTOR                                        
         BZ    TFIL03                                                           
         MVC   TASKMASK,PGMIND3    SET ROUTING FROM PGMIND3 OVERRIDE            
         NI    TASKMASK,PGMITSKS                                                
         B     TFIL04                                                           
         DROP  RF                                                               
*                                                                               
TFIL03   ICM   R1,15,SEFILES       GLOBAL SYSTEMS CAN RUN ANYWHERE              
         BZ    TFIL04                                                           
         LA    R1,8(R1)                                                         
         USING ISDTF,R1                                                         
         TM    ISFFLAG,ISFGLOB     TEST GLOBAL FLAG                             
         BZ    TFIL04                                                           
         MVI   TASKMASK,PGMIRAOR+PGMIRTOR                                       
         DROP  R1                                                               
*                                                                               
TFIL04   TM    TTEST,TTESTTAC      TEST BUFFER?                                 
         BZ    *+8                 NO                                           
         MVI   TASKMASK,PGMIRTOR                                                
*                                                                               
         L     RE,VSSB             FIND EXCHANGE BLOCK FOR TOR/AOR WORK         
         LAM   R2,R2,SSBALET-SSBD(RE)                                           
         ICM   R2,15,SSBATOR-SSBD(RE)                                           
         SAC   512                                                              
         LA    R2,TORFACLQ(,R2)                                                 
         USING SBEXCHD,R2                                                       
*                                                                               
         LH    R4,0(,R2)           BXLE ON R4,R5                                
         L     R5,2(,R2)                                                        
         LA    R2,6(,R2)           R2=A(TOR ENTRY)                              
*                                                                               
         L     RE,VSSB             SSBGO ISSUED?                                
         TM    SSBSTAT1-SSBD(RE),SSBUII                                         
         BNO   *+12                NO                                           
         MVI   TASKMASK,PGMIRTOR   YES - MUST RUN IN TOR                        
         B     TFIL24              TRY TO SCHEDULE                              
*                                                                               
         TM    TSTATU,TSTATRER     REQUEUED DUE TO PREVIOUS ERROR?              
         BZ    TFIL06              NO                                           
         MVI   TASKMASK,PGMIRTOR   SET MUST RUN IN TOR                          
         B     TFIL24              TRY TO SCHEDULE                              
                                                                                
TFIL06   TM    TSTAT7,TST7DIR      DIRECT TO A PARTICULAR REGION?               
         BO    TFIL07              YES                                          
         OC    TSVCREQ,TSVCREQ     SERVICE REQUEST?                             
         BZ    TFIL07              NO                                           
         MVI   TASKMASK,PGMIRTOR   SET MUST RUN IN TOR                          
         B     TFIL24              TRY TO SCHEDULE                              
*                                                                               
TFIL07   TM    TSTAT7,TST7DIR      DIRECT TO A PARTICULAR REGION?               
         BZ    TFIL24              NO                                           
*                                                                               
         MVC   DIRECT,TTORAOR      FIND 'DIRECT TO' REGION                      
         NI    DIRECT,255-(TTAXTRA)                                             
         XR    RE,RE                                                            
         ICM   RE,8,DIRECT         RE HOLDS ZERO BASED REGION NUMBER            
         BZ    TFIL08              ZERO = ROUTE TO TOR                          
*                                                                               
         TM    TASKMASK,PGMIRAOR   CAN WE ROUTE THIS TO AN AOR?                 
         BO    TFIL10              YES                                          
         NI    TTORAOR,TTAXTRA                                                  
         OI    TTORAOR,1           ERROR 1 - PRG WILL NOT RUN IN REGION         
         B     TFIL18                                                           
*                                                                               
TFIL08   TM    TASKMASK,PGMIRTOR   CAN WE ROUTE THIS TO TOR?                    
         BO    TFIL10              YES                                          
         NI    TTORAOR,TTAXTRA                                                  
         OI    TTORAOR,1           ERROR 1 - PRG WILL NOT RUN IN REGION         
         B     TFIL18                                                           
*                                                                               
TFIL10   SRDL  RE,64-8                                                          
         MR    RE,R4               INDEX INTO FACPAK BLOCKS                     
         AR    RF,R2                                                            
         CPYA  RF,R2               SET RF TO ROW IN BLOCK                       
SEL      USING SBEXCHD,RF                                                       
*                                                                               
         OC    SEL.SBSTOKEN,SEL.SBSTOKEN                                        
         BNZ   TFIL12                                                           
         NI    TTORAOR,TTAXTRA                                                  
         OI    TTORAOR,2           ERROR 2 - NO FACPAK IN ROW                   
         B     TFIL18                                                           
*                                                                               
TFIL12   CLI   SEL.SBSTRTD,SBYES   FACPAK WAS INITIALISED?                      
         BE    TFIL14              YES                                          
         NI    TTORAOR,TTAXTRA                                                  
         OI    TTORAOR,3           ERROR 3 - FACPAK NOT STARTED                 
         B     TFIL18                                                           
*                                                                               
TFIL14   CLI   SEL.SBAVLBL,SBYES   FACPAK STILL RUNNING?                        
         BE    TFIL16              YES                                          
         NI    TTORAOR,TTAXTRA                                                  
         OI    TTORAOR,4           ERROR 4 - FACPAK NOT AVAILABLE               
         B     TFIL18                                                           
         DROP  SEL                                                              
*                                                                               
TFIL16   LAM   RF,RF,ARZERO        YOU HAD BETTER RESET THIS                    
         LR    R2,RF               R2=A(CORRECT REGION)                         
         CLC   SBTSKNOW,SBTSKMAX   IS THERE A FREE TASK IN THE AOR?             
         BL    TFIL32              YES                                          
*                                                                               
         NI    TTORAOR,255-(TTAXTRA)                                            
         OI    TTORAOR,5           ERROR 5 - AOR IS BUSY                        
         B     TFIL18                                                           
         SPACE 1                                                                
***********************************************************************         
* HANDLE ERRORS IN ROUTING BY REQUEUEING TO SE#1 $TOR.                *         
* FLAG TST8RERR INDICATES THAT AN ERROR CONDITION OCCURRED            *         
* BITS IN TTOTAOR INDICATE THE ERROR CONDITION WHICH WAS HANDLED      *         
***********************************************************************         
         SPACE 1                                                                
TFIL18   MVC   SEFIRST+1(3),TNEXTIN  REMOVE THIS UTL FROM CURRENT QUEUE         
         XC    TNEXTIN,TNEXTIN                                                  
         LH    RE,SEQLEN           UPDATE QUEUE COUNTER                         
         BCTR  RE,0                                                             
         LTR   RE,RE                                                            
         STH   RE,SEQLEN                                                        
         BP    *+10                                                             
         XC    SELAST,SELAST                                                    
*                                                                               
         OI    TSTATU,TSTATRER     SET ERROR & REQUEUE TO SE#1                  
         NI    TSTAT2,255-TSTATTIP                                              
         MVC   TSVCREQ,$TOR        THIS HANDLES ERROR CONDITIONS                
*                                                                               
         L     R7,VSELIST                                                       
         LA    R7,6(R7)            SE#1 IS ALWAYS FIRST                         
         OC    SEFIRST,SEFIRST     ANYTHING IN QUEUE?                           
         BNZ   TFIL20              YES                                          
*                                                                               
         ST    R3,SEFIRST                                                       
         ST    R3,SELAST                                                        
         LA    R0,1                                                             
         STH   R0,SEQLEN                                                        
         B     TFIL22              SET CC AND EXIT BACK                         
*                                                                               
TFIL20   L     RE,SELAST                                                        
         STCM  R3,7,TNEXTIN-TNUM(RE)                                            
         ST    R3,SELAST                                                        
         LH    RE,SEQLEN                                                        
         LA    RE,1(RE)                                                         
         STH   RE,SEQLEN                                                        
         SPACE 1                                                                
***********************************************************************         
* SET CC EQUAL IF ERROR OCCURS. TASKER WILL THINK THAT AN AOR WAS     *         
* POSTED AND TRY TO GET THE NEXT ENTRY FROM THE MOST VALID QUEUE      *         
***********************************************************************         
         SPACE 1                                                                
TFIL22   B     TFILE               SET CC EQUAL                                 
         EJECT                                                                  
*                                                                               
TFIL24   LAM   RF,RF,ARZERO        RESET THIS JUST IN CASE                      
         TM    TASKMASK,PGMIRAOR   TRANSACTION WILL RUN IN AOR?                 
         BZ    TFIL30              NO                                           
         SPACE 1                                                                
**********************************************************************          
* INTENTION IS TO ALWAYS TRY TO RUN IN SYSTEM WITH LOWEST ACTIVE     *          
* TASKS UNLESS PROGRAM HAS TO BE ROUTED TO A SPECIFIC REGION         *          
**********************************************************************          
         SPACE 1                                                                
         CPYA  R6,R2                                                            
         CPYA  R3,R2               SET R3 TO FIRST AOR ROW IN BLOCK             
         LR    R3,R2                                                            
AOR      USING SBEXCHD,R3                                                       
         CPYA  RF,R2                                                            
         XR    RF,RF               RF = BEST QUALIFYING AOR                     
         B     TFIL28                                                           
BEST     USING SBEXCHD,RF                                                       
*                                                                               
TFIL26   CLI   AOR.SBAVLBL,SBYES   FACPAK IS AVAILABLE                          
         BNE   TFIL28              NO                                           
         CLC   AOR.SBTSKNOW,AOR.SBTSKMAX                                        
         BNL   TFIL28              FACPAK IS BUSY                               
*                                                                               
         LTR   RF,RF                                                            
         BNZ   *+6                                                              
         LR    RF,R3               SAVE FIRST AOR ENTRY                         
         SPACE 1                                                                
***********************************************************************         
* PERFORMANCE INFORMATION PROCESSING FOR SCHEDULER SELECTION WILL     *         
* HAVE TO GO HERE EVENTUALLY                                          *         
* FOR NOW THE ASSUMPTION IS THAT THE AOR WITH THE LEAST NUMBER OF     *         
* ACTIVE TASKS IS THE LEAST BUSY - THIS MAY NOT ALWAYS BE THE CASE    *         
***********************************************************************         
         SPACE 1                                                                
         CLC   AOR.SBTSKNOW,BEST.SBTSKNOW                                       
         BNL   *+6                                                              
         LR    RF,R3               BEST FACPAK IN RF                            
*                                                                               
TFIL28   BXLE  R3,R4,TFIL26                                                     
         LTR   RF,RF               AOR AVAILABLE?                               
         BZ    TFIL30              NO                                           
*                                                                               
         TM    TASKMASK,PGMIRTOR   ABLE TO RUN IN TOR?                          
         BO    *+10                YES                                          
         LR    R2,RF                                                            
         B     TFIL32              RUN IN AOR                                   
*                                                                               
         CLC   SBTSKNOW,BEST.SBTSKNOW                                           
         BL    TFIL32              TOR IS LESS BUSY THAN AOR                    
         LR    R2,RF                                                            
         B     TFIL32              AOR IS LESS BUSY THAN TOR                    
         DROP  BEST,AOR                                                         
*                                                                               
TFIL30   TM    TASKMASK,PGMIRTOR   ABLE TO RUN IN TOR?                          
         BZ    *+14                NO                                           
         CLC   SBTSKNOW,SBTSKMAX   TOR AT MAX CAPACITY?                         
         BL    TFIL32              NO                                           
         LAM   RF,RF,ARZERO                                                     
         B     TFILL                                                            
*                                                                               
TFIL32   LAM   RF,RF,ARZERO                                                     
         ICM   RE,15,SBTSKNOW      INCREMENT ACTIVE TASK COUNT                  
         LA    RF,1(RE)                                                         
         CS    RE,RF,SBTSKNOW                                                   
         BNE   *-8                                                              
*                                                                               
         ICM   R0,15,SBTSKMAX      FIND FIRST FREE TASK SLOT                    
         CPYA  R3,R2                                                            
         LA    R3,SBTSKBLK                                                      
         USING EXCHNGD,R3                                                       
*                                                                               
TFIL34   CLI   EXCFLAG,EXCFREE     FREE TASK?                                   
         BE    TFIL36              YES                                          
         LA    R3,EXCHNGLQ(,R3)                                                 
         BCT   R0,TFIL34                                                        
         DC    H'0'                HOW CAN DISPATCH IF NOTHING FREE?            
*                                                                               
TFIL36   L     R5,SEFIRST          POINT TO UTL ENTRY                           
         USING UTLD,R5                                                          
         OI    TSTAT2,TSTATTIP     SET TRM IN PROC                              
         MVC   SEFIRST+1(3),TNEXTIN                                             
         XC    TNEXTIN,TNEXTIN                                                  
         LH    RE,SEQLEN           UPDATE QUEUE COUNTER                         
         BCTR  RE,0                                                             
         STH   RE,SEQLEN                                                        
         LTR   RE,RE                                                            
         BP    *+10                                                             
         XC    SELAST,SELAST                                                    
         NI    TSTAT8,255-TST8INQ                                               
*                                                                               
         L     RE,VSSB             BUMP SYSTEM INPUT NUMBER                     
         USING SSBD,RE                                                          
         L     RF,SSBSIN                                                        
         LA    RF,1(RF)                                                         
         ST    RF,SSBSIN                                                        
         ST    RF,TSIN             SET SYSTEM INPUT NUMBER IN UTL               
         DROP  RE                                                               
*                                                                               
         MVC   EXCUTL,TNUM         SET TERMINAL NUMBER FOR TASK                 
         MVC   EXCTIME,TTIMETU     SET ARRIVAL TIME OF TRANSACTION              
         MVI   EXCFLAG,EXCNEW      SET NEW WORK                                 
*                                                                               
         OC    TNUM,TNUM           DUMMY TERMINAL?                              
         BNZ   TFIL38              NO                                           
*                                                                               
         C     R5,=A(UTLZERO)                                                   
         BNE   *+20                                                             
         MVC   EXCUTL,UTL0ID                                                    
         MVC   EXCTIME,DUB                                                      
         B     TFIL38                                                           
*                                                                               
         C     R5,=A(UTLZEROA)                                                  
         BNE   *+20                                                             
         MVC   EXCUTL,UTL0AID                                                   
         MVC   EXCTIME,DUB                                                      
         B     TFIL38                                                           
         DC    H'0'                                                             
         DROP  R3                                                               
*                                                                               
TFIL38   CLC   SBWAKE,TORWAKE      IS THIS THE TOR?                             
         BE    TFILH               SET CC HIGH IF WORK FOR TOR                  
*                                                                               
         LH    R5,SBASID           GET ASCB FOR THE AOR                         
         SAC   0                                                                
*                                                                               
         LOCASCB ASID=(R5)                                                      
         LTR   RF,RF               ASCB RETURNED IN R1 OK?                      
         BZ    TFIL40              NO                                           
         BRAS  RE,FACDWN                                                        
         B     TFILE                                                            
         SPACE 1                                                                
***********************************************************************         
* MIGHT NEED A BETTER CHECK THAN THIS FOR AOR AVAILABILITY            *         
* IN CASE AOR IS ABENDING WHILST PROCESSING TAKING PLACE              *         
***********************************************************************         
         SPACE 1                                                                
TFIL40   SAC   512                                                              
         LR    R4,R1               SET ASCB IN R4                               
         L     R6,SBWAKE           SET A(WAKEUP ECB) IN R6                      
         LA    R5,99                                                            
         SAC   0                                                                
*                                                                               
         POST (R6),(R5),ASCB=(R4),LINKAGE=SYSTEM,ECBKEY=8,MF=(E,POSTAO)         
         LTR   RF,RF                                                            
         BZ    TFILE               SET CC EQUAL IF AOR POSTED                   
         BRAS  RE,FACDWN                                                        
         B     TFILE                                                            
*                                                                               
POSTAO   POST  ECBKEY=YES,MF=L                                                  
*                                                                               
TFILE    CR    RB,RB                                                            
         B     TFILX                                                            
*                                                                               
TFILH    CLI   *,0                                                              
         B     TFILX                                                            
*                                                                               
TFILL    CLI   *,FF                                                             
         B     TFILX                                                            
*                                                                               
TFILX    SAC   0                   RESET ALL ACCESS REGISTERS                   
         LAM   R0,RF,ARZERO                                                     
         XMOD1 ,                                                                
         DROP  R2,R5,R7                                                         
*                                                                               
TASKMASK DS    XL1                 TOR/AOR APPLICATION RUN FLAG                 
FLIPFLOP DS    XL1                 LOAD BALANCE FOR GLOBAL FILE TEST            
         EJECT                                                                  
***********************************************************************         
* POST ERROR HANDLING ROUTINE - ASSUME AOR HAS ABENDED                *         
***********************************************************************         
         SPACE 1                                                                
FACDWN   NTR1  BASE=*,LABEL=*      TURN OFF AOR DOWN FLAG                       
         L     R9,=A(TASKCNST)                                                  
         USING TASKCNST,R9                                                      
         L     RA,VSYSFX                                                        
         USING SYSFACD,RA                                                       
         NI    ABNDECB,255-X'40'   TURN OFF AOR DOWN FLAG                       
         GOTO1 VTORRCV             RECOVER ABENDING AOR TASKS                   
         XIT1  ,                                                                
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* PROCESS COMPLETED TRANSACTIONS FROM AOR FACPAKS                     *         
***********************************************************************         
         SPACE 1                                                                
PCOMPS   NTR1  BASE=*,LABEL=*                                                   
         SAC   0                                                                
         LAM   R0,RF,ARZERO        CLEAR ACCESS REGISTERS                       
         L     RA,VSYSFX                                                        
         USING SYSFACD,RA                                                       
         XC    PCMPRQ,PCMPRQ                                                    
*                                                                               
         L     RE,VSSB                                                          
         LAM   R2,R2,SSBALET-SSBD(RE)                                           
         ICM   R2,15,SSBATOR-SSBD(RE)                                           
         SAC   512                                                              
         LA    R2,TORFACLQ(,R2)                                                 
*                                                                               
         LH    R4,0(,R2)                                                        
         L     R5,2(,R2)                                                        
         LA    R2,6(,R2)                                                        
         USING SBEXCHD,R2          R2=A(FACPAK GRID)                            
         CPYA  R3,R2                                                            
         B     PCOMP16             FIRST ENTRY IS TOR                           
*                                                                               
PCOMP02  TM    SBDONE,X'40'        COMPLETION POST FOR THIS FACPAK?             
         BZ    PCOMP16             NO                                           
         NI    SBDONE,255-X'40'    TURN OFF FLAG                                
         ICM   R0,15,SBTSKMAX      THIS FACPAK HAS TASKS?                       
         BZ    PCOMP16             NO                                           
*                                                                               
         LA    R3,SBTSKBLK         REMEMBER THE AR ABOVE YOU FOOL               
         USING EXCHNGD,R3                                                       
*                                                                               
PCOMP04  CLI   EXCFLAG,EXCDONE     IS THIS TRANSACTION FINISHED?                
         BNE   PCOMP14             NO                                           
*                                                                               
PCOMP06  ICM   RE,15,SBTSKNOW      DECREMENT ACTIVE TASK COUNT                  
         BZ    PCOMP08                                                          
         LR    RF,RE                                                            
         AHI   RF,-1                                                            
         CS    RE,RF,SBTSKNOW                                                   
         BNE   PCOMP06                                                          
*                                                                               
PCOMP08  ICM   RE,15,VUTL          RE=A(UTL BLOCK)                              
         LH    RF,EXCUTL           RF=TERMINAL NUMBER                           
         BCTR  RF,0                ZERO BASED                                   
*&&US                                                                           
         CHI   RF,6000             DEBUG TEST                                   
         BNH   *+6                                                              
         DC    H'0'                                                             
*&&                                                                             
         MH    RF,0(RE)            INDEX INTO BLOCK                             
         LA    R6,6(RE,RF)                                                      
         USING UTLD,R6                                                          
*                                                                               
         XC    EXCUTL,EXCUTL       MATCH TCB SLOT TO EXCHANGE SLOT              
         XC    EXCTIME,EXCTIME                                                  
         MVI   EXCFLAG,EXCFREE     MATCH TCB SLOT TO EXCHANGE SLOT              
*                                                                               
         STAR  CLEAR=ARZERO                                                     
*                                                                               
         TM    TTORAOR,TTAGOBAK    UTL NEEDS REQUEUING?                         
         BZ    PCOMP10             NO                                           
         NI    TTORAOR,255-TTAGOBAK                                             
*                                                                               
         ICM   RF,15,TBUFF                                                      
         GOTO1 VMSGQIN,PCMPLST,(RA),(RF),(R6)                                   
         MVI   PCMPRQ,1            FLAG REQUEUED STUFF TO RUN                   
         B     PCOMP12                                                          
         SPACE 1                                                                
**********************************************************************          
* NOTE: LCM CALL FROM FAMONITOR ( @ LABEL : MONWRT ) SHOULD SCRIPT   *          
*       PROCESSING NEED TO BE HANDLED HERE ALSO                      *          
**********************************************************************          
         SPACE 1                                                                
PCOMP10  GOTO1 VLCM,PCMPLST,VTWRITE,(R6),0,0                                    
*                                                                               
PCOMP12  REAR                                                                   
*                                                                               
PCOMP14  LA    R3,EXCHNGLQ(,R3)    NEXT TASK WITHIN THIS FACPAK                 
         BCT   R0,PCOMP04                                                       
         DROP  R6                                                               
*                                                                               
PCOMP16  BXLE  R2,R4,PCOMP02                                                    
         LAM   R0,RF,ARZERO        CLEAR ACCESS REGISTERS                       
*                                                                               
PCOMPX   SAC   0                                                                
         CLI   PCMPRQ,0            SET CC EQ IF NO REQUEUED                     
         XIT1  ,                                                                
         DROP  R2,R3                                                            
*                                                                               
PCMPLST  DS    4F                  PARAMETER LIST FOR LCM WRITE                 
PCMPRQ   DS    X                                                                
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* I/O WAITS ENTER HERE                                                *         
***********************************************************************         
         SPACE 1                                                                
ADWAIT   NMOD1 0,*ADWAIT*                                                       
         SAC   0                                                                
         LA    RE,*+6              NEED TO ENSURE 24 BIT MODE                   
         BSM   0,RE                                                             
*                                                                               
         L     R9,=A(TASKCNST)                                                  
         USING TASKCNST,R9                                                      
         L     RA,VSYSFX                                                        
         USING SYSFACD,RA                                                       
         GOTO1 =V(CHAIN)                                                        
         GOTO1 =V(CRAPPER)                                                      
*                                                                               
ADWAIT01 L     RE,VSSB                                                          
         USING SSBD,RE                                                          
         CLI   SSBMTIND,0          TEST MULTI-TASKING ACTIVE                    
         BNZ   ADWAIT02                                                         
         WAIT  ECB=(1)                                                          
         XMOD1 1                                                                
*                                                                               
ADWAIT02 L     R3,SSBTKADR         POINT TO TCB ENTRY                           
         USING TCBD,R3                                                          
         ST    RD,TCBSVRD          SAVE CURRENT RD AND ECB ADDR                 
         ST    R1,TCBSVECB                                                      
         STAM  R0,RF,TCBIARS       SAVE ACCESS REGISTERS                        
*                                                                               
         CLI   TCBSVECB,X'FC'      TEST DO-NOT-WAIT/DATASPACE-WAIT              
         BNL   *+8                                                              
         MVI   TCBSVECB,0          CLEAR HOB OF NORMAL ECBS                     
*                                                                               
         GOTO1 VDTFIOA,ADWPL,(C'S',(R3))  SAVE DTF DATA                         
*                                                                               
         TIME  TU                  ACCUMULATE CPU TIME                          
         L     R1,TCBTIME                                                       
         CLR   R0,R1                                                            
         BNL   *+8                                                              
         AL    R0,MIDNIGHT         ADJUST FOR MIDNIGHT                          
         SLR   R0,R1                                                            
         AL    R0,TCBCPUTM                                                      
         ST    R0,TCBCPUTM                                                      
*                                                                               
         CLI   TCBSVECB,X'FC'      TEST DO-NOT-WAIT/DATASPACE-WAIT              
         BNL   ADWAIT04            YES                                          
         SR    R1,R1               NORMAL ECB - BUMP I/O COUNTER                
         ICM   R1,7,TCBIOCNT                                                    
         LA    R1,1(R1)                                                         
         STCM  R1,7,TCBIOCNT                                                    
         L     R1,TCBLIOCT         ADD LOGICAL I/O COUNT TO TOTAL               
         A     R1,TCBLIOC                                                       
         ST    R1,TCBLIOCT                                                      
         XC    TCBLIOC,TCBLIOC     CLEAR LOGICAL I/O COUNT                      
*                                                                               
**       L     RF,=V(TEMPIOS)      I/O TRACE                                    
**       BASR  RE,RF                                                            
*                                                                               
ADWAIT04 L     RF,=A(ADNEXT)                                                    
         BR    RF                                                               
*                                                                               
         LTORG                                                                  
*                                                                               
ADWPL    DS    4F                                                               
         EJECT                                                                  
***********************************************************************         
* SCRIPT WAITS ENTER HERE - R1 POINTS TO CALLER'S ECB                 *         
***********************************************************************         
         SPACE 1                                                                
SCWAIT   NMOD1 0,*SCWAIT*                                                       
         LA    RE,*+6              NEED TO ENSURE 24 BIT MODE                   
         BSM   0,RE                                                             
*                                                                               
         L     R9,=A(TASKCNST)                                                  
         USING TASKCNST,R9                                                      
         L     RA,VSYSFX                                                        
         USING SYSFACD,RA                                                       
         GOTO1 =V(CHAIN)                                                        
         GOTO1 =V(CRAPPER)                                                      
*                                                                               
         L     R3,VSSB                                                          
         L     R3,SSBTKADR-SSBD(R3)                                             
         USING TCBD,R3                                                          
         OI    TCBFLAG3,TCBSCPWT   SET AWAITING SCRIPT COMPLETION               
         ST    RD,TCBSVRD          SAVE CURRENT RD AND ECB ADDR                 
         ST    R1,TCBSVECB                                                      
         STAM  R0,RF,TCBIARS       SAVE ACCESS REGISTERS                        
         CLI   TCBSVECB,X'FC'      TEST DO-NOT-WAIT/DATASPACE-WAIT              
         BNL   *+8                 YES                                          
         MVI   TCBSVECB,0          CLEAR HOB OF NORMAL ECB                      
*                                                                               
         GOTO1 VDTFIOA,SCWPL,(C'S',(R3))     SAVE DTF DATA                      
*                                                                               
         TIME  TU                  ACCUMULATE CPU TIME                          
         L     R1,TCBTIME                                                       
         CLR   R0,R1                                                            
         BNL   *+8                                                              
         AL    R0,MIDNIGHT         ADJUST FOR MIDNIGHT                          
         SLR   R0,R1                                                            
         AL    R0,TCBCPUTM                                                      
         ST    R0,TCBCPUTM                                                      
*                                                                               
         L     RF,=A(ADNEXT)                                                    
         BR    RF                                                               
         DROP  R3                                                               
*                                                                               
         LTORG                                                                  
*                                                                               
SCWPL    DS    4F                                                               
         EJECT                                                                  
***********************************************************************         
* MQIO WAITS ENTER HERE - R1 POINTS TO CALLER'S ECB                   *         
***********************************************************************         
         SPACE 1                                                                
MQWAIT   NMOD1 0,*MQWAIT*                                                       
         LA    RE,*+6              NEED TO ENSURE 24 BIT MODE                   
         BSM   0,RE                                                             
*                                                                               
         L     R9,=A(TASKCNST)                                                  
         USING TASKCNST,R9                                                      
         L     RA,VSYSFX                                                        
         USING SYSFACD,RA                                                       
         GOTO1 =V(CHAIN)                                                        
         GOTO1 =V(CRAPPER)                                                      
*                                                                               
         L     R3,VSSB                                                          
         L     R3,SSBTKADR-SSBD(R3)                                             
         USING TCBD,R3                                                          
         OI    TCBFLAG3,TCBMQWT    SET AWAITING MQIO COMPLETION                 
         ST    RD,TCBSVRD          SAVE CURRENT RD AND ECB ADDR                 
         ST    R1,TCBSVECB                                                      
         STAM  R0,RF,TCBIARS       SAVE ACCESS REGISTERS                        
         CLI   TCBSVECB,X'FC'      TEST DO-NOT-WAIT/DATASPACE-WAIT              
         BNL   *+8                 YES                                          
         MVI   TCBSVECB,0          CLEAR HOB OF NORMAL ECB                      
*                                                                               
         GOTO1 VDTFIOA,MQWPL,(C'S',(R3))     SAVE DTF DATA                      
*                                                                               
         TIME  TU                  ACCUMULATE CPU TIME                          
         L     R1,TCBTIME                                                       
         CLR   R0,R1                                                            
         BNL   *+8                                                              
         AL    R0,MIDNIGHT         ADJUST FOR MIDNIGHT                          
         SLR   R0,R1                                                            
         AL    R0,TCBCPUTM                                                      
         ST    R0,TCBCPUTM                                                      
*                                                                               
         L     RF,=A(ADNEXT)                                                    
         BR    RF                                                               
         DROP  R3                                                               
*                                                                               
         LTORG                                                                  
*                                                                               
MQWPL    DS    4F                                                               
         EJECT                                                                  
***********************************************************************         
* TIMER WAITS ENTER HERE                                              *         
***********************************************************************         
         SPACE 1                                                                
TKWAIT   NMOD1 0,*TKWAIT*                                                       
         LA    RE,*+6              NEED TO ENSURE 24 BIT MODE                   
         BSM   0,RE                                                             
*                                                                               
         L     R9,=A(TASKCNST)                                                  
         USING TASKCNST,R9                                                      
         L     RA,VSYSFX                                                        
         USING SYSFACD,RA                                                       
         GOTO1 =V(CHAIN)                                                        
         GOTO1 =V(CRAPPER)                                                      
*                                                                               
TKWAIT2  L     R3,VSSB                                                          
         L     R3,SSBTKADR-SSBD(R3)                                             
         USING TCBD,R3                                                          
         ST    RD,TCBSVRD          SAVE CURRENT RD                              
         ST    R1,TCBTIMEW         SET WAKE UP IME                              
         STAM  R0,RF,TCBIARS       SAVE ACCESS REGISTERS                        
*                                                                               
         TIME  TU                  GET TIME NOW                                 
         A     R0,TCBTIMEW         ADD WAIT TIME                                
         ST    R0,TCBTIMEW         SET WAKE UP IME                              
*                                                                               
         GOTO1 VDTFIOA,TKWPL,(C'S',(R3))  SAVE DTF DATA                         
*                                                                               
         L     R1,TCBTIME                                                       
         CLR   R0,R1                                                            
         BNL   *+8                                                              
         AL    R0,MIDNIGHT         ADJUST FOR MIDNIGHT                          
         SLR   R0,R1                                                            
         AL    R0,TCBCPUTM                                                      
         ST    R0,TCBCPUTM                                                      
*                                                                               
TKWAIT3  L     RF,=A(ADNEXT)                                                    
         BR    RF                                                               
         DROP  R3                                                               
*                                                                               
         LTORG                                                                  
*                                                                               
TKWPL    DS    4F                                                               
         EJECT                                                                  
***********************************************************************         
* TIMER INTERRUPTED TASKS ENTER HERE (FROM ABENDIT)                   *         
***********************************************************************         
         SPACE 1                                                                
ADIRPT   BASR  RB,0                ESTABLISH ADDRESSABILITY                     
         USING *,RB                                                             
         LA    RE,*+6                                                           
         BSM   0,RE                ENSURE 24-BIT MODE                           
*                                                                               
         L     RD,=A(TASKWORK)     POINT TO REGSAVE AREA                        
         L     R9,=A(TASKCNST)                                                  
         USING TASKCNST,R9                                                      
         L     RA,VSYSFX                                                        
         USING SYSFACD,RA                                                       
         GOTO1 =V(CHAIN)                                                        
         GOTO1  =V(CRAPPER)                                                     
*                                                                               
ADIRP02  L     RE,VSSB                                                          
         USING SSBD,RE                                                          
         L     R3,SSBTKADR         GET TCB ENTRY ADDRESS                        
         USING TCBD,R3                                                          
         TM    TCBFLAG3,TCBDBWAI   TEST DEBUG WAIT                              
         BO    ADIRP04                                                          
*                                                                               
         L     RF,=V(ABENDITR)     GET ADDRESS OF TIMER IRPT REGS               
         MVC   TCBPSW(72),0(RF)    AND SAVE PSW/REGS IN TCB                     
         MVC   TCBIARS(64),72(RF)  AND SAVE ACCESS REGS                         
         LA    R0,TCBSVRD          NOW DUMMY UP AN ECB ADDRESS                  
         ST    R0,TCBSVECB                                                      
         MVI   TCBSVRD+0,X'7F'                                                  
*                                                                               
         LH    R1,TCBIRPTS         BUMP TIMER INTERRUPT COUNTER                 
         LA    R1,1(R1)                                                         
         STH   R1,TCBIRPTS                                                      
         CH    R1,SSBPOPMX         IF EXCEEDS MAXIMUM ASSUME LOOP               
         BNH   *+14                                                             
         L     RE,=V(ABENDWHY)     POINT TO ABEND CAUSE BYTE                    
         MVI   0(RE),C'I'          AND INDICATE LOOP (INTVL TIMER)              
         DC    H'0'                THEN CAUSE A PROGRAM CHECK                   
*                                                                               
ADIRP04  GOTO1 VDTFIOA,ADIRPL,(C'S',(R3)) SAVE DTF DATA                         
*                                                                               
         TIME  TU                  ACCUMULATE CPU TIME                          
         L     R1,TCBTIME                                                       
         CLR   R0,R1                                                            
         BNL   *+8                                                              
         AL    R0,MIDNIGHT         ADJUST FOR MIDNIGHT                          
         SLR   R0,R1                                                            
         AL    R0,TCBCPUTM                                                      
         ST    R0,TCBCPUTM                                                      
*                                                                               
         L     RF,=A(ADNEXT)                                                    
         BR    RF                                                               
         DROP  R3                                                               
*                                                                               
         LTORG                                                                  
*                                                                               
ADIRPL   DS    4F                                                               
         EJECT                                                                  
***********************************************************************         
* FASTART PASSES CONTROL TO V(ADFIRST)                                *         
* INTIALISE FOR ENTRY INTO ADNEXT                                     *         
***********************************************************************         
         SPACE 1                                                                
         USING *,RF                                                             
ADFIRST  L     R5,=A(ADFIRREG)                                                  
         STM   RB,RE,0(R5)         SAVE RETURN REGS RB,RC,RD,RE                 
         DROP  RF                                                               
*                                                                               
         NBASE 0,TASKFRST,=A(TASKWORK),SAVE=YES                                 
         SAC   0                                                                
*                                                                               
         L     R9,=A(TASKCNST)                                                  
         USING TASKCNST,R9                                                      
         L     RA,VSYSFX                                                        
         USING SYSFACD,RA                                                       
         LAM   R0,RF,ARZERO                                                     
*                                                                               
         L     RE,VSSB                                                          
         USING SSBD,RE                                                          
         TM    SSBSTAT4,SSBSAOR    ARE WE TOR OR AOR                            
         BO    AORFIRST                                                         
         EJECT                                                                  
***********************************************************************         
*        ADFIRST ACTIONS FOR TOR                                      *         
***********************************************************************         
         SPACE 1                                                                
TORFIRST MVI   TORFLAG,C'Y'        IDENTIFY TOR                                 
*                                  EXTRACT ASID & STOKEN FOR FACPAK             
         EXTRACT ASIDFLD,'S',FIELDS=(ASID)                                      
         ALESERV EXTRACTH,STOKEN=ASTOKEN                                        
*                                                                               
         L     RE,VSSB             FIND TOR DATASPACE                           
         USING SSBD,RE                                                          
         MVC   SSBSTOKN,ASTOKEN    SAVE STOKEN IN SSB                           
*                                                                               
         LAM   R2,R2,SSBALET                                                    
         ICM   R2,15,SSBATOR                                                    
         BNZ   *+6                                                              
         DC    H'0'                                                             
         SAC   512                                                              
         DROP  RE                                                               
*                                                                               
         USING TORFACD,R2      *** INITIALISE TOR INFORMATION AREA              
         MVC   TOREYE(4),=CL4'TOR-'                                             
         MVC   TOREYE+4(4),SSBSYSN4-SSBD(RE)                                    
         MVC   TORSTOKN,ASTOKEN    PUBLISH TOR STOKEN                           
         L     RF,ASIDFLD                                                       
         STH   RF,TORASID           PUBLISH TOR ASID                            
         MVC   TORDONE,=A(POSTECB)  PUBLISH A(COMPLETION POST ECB)              
         MVC   TORUTL,VUTL          PUBLISH A(UTL ENTRIES)                      
         MVC   TORAORDN,=A(ABNDECB) PUBLISH A(AOR ABEND ECB)                    
         MVI   TORSTRTD,SBYES      SET TOR STARTED                              
         MVI   TORAVLBL,SBYES      SET TOR AVAILABLE                            
*                                                                               
         LA    R2,TORFACLQ(,R2)    SET EXCHANGE GRID SIZE INFORMATION           
         USING SBEXCHD,R2                                                       
         LHI   R4,SBEXCHLQ         SET LENGTH OF SINGLE ROW                     
         STCM  R4,3,0(R2)                                                       
         LA    R5,SBFACMAX           MAX ALLOWABLE FACPAKS                      
         MSR   R5,R4               * LENGTH OF SINGLE ROW                       
         LA    R5,5(R5,R2)         + BXLE OVERHEAD - 1                          
         STCM  R5,15,2(R2)         = A(END OF GRID -1)                          
         LA    R2,6(,R2)                                                        
         LR    R0,R2               SAVE A(FIRST FACPAK ENTRY)                   
*                                                                               
         LA    R3,1(,R5)                                                        
         SR    R3,R2               R3=LENGTH OF GRID                            
         XR    RE,RE                                                            
         XR    RF,RF                                                            
         MVCL  R2,RE               CLEAR EXCHANGE GRID                          
         LR    R2,R0               RESTORE R2                                   
*                                                                               
         LA    RF,1                INITIALISE EXCHANGE GRID INFORMATION         
         STH   RF,SBFACID          SET FACPAK ID NUMBER                         
         LA    RF,1(RF)                                                         
         BXLE  R2,R4,*-8                                                        
*                              *** PUBLISH TOR FACPAK DETAILS                   
         LR    R2,R0               TOR IS ALWAYS FIRST ENTRY                    
         MVC   SBSTOKEN,ASTOKEN    PUBLISH TOR STOKEN                           
         L     RF,ASIDFLD                                                       
         STH   RF,SBASID           PUBLISH TOR ASID                             
         MVC   SBWAKE,TORWAKE      TOR REQUIRES NO WAKE-UP ECB                  
         MVI   SBSTRTD,SBYES       SET TOR STARTED                              
         MVI   SBAVLBL,SBYES       SET TOR AVAILABLE                            
*                                                                               
         CPYA  R6,R2                                                            
         LA    R6,SBTSKBLK     *** INITIALISE TOR TASK DETAILS                  
         USING EXCHNGD,R6                                                       
*                                                                               
         L     R3,VTCB                                                          
         LH    R4,0(R3)                                                         
         L     R5,2(R3)                                                         
         LA    R3,6(R3)                                                         
         USING TCBD,R3                                                          
         XR    RF,RF                                                            
*                                                                               
ADF02    TM    TCBFLAG2,TCBNOPED+TCBAORED  TEST END OF USABLE TCBS              
         BNZ   ADF22               YES                                          
         CLI   TCBID,C'*'          IGNORE IF NO *TASK HEADER                    
         BNE   ADF04                                                            
*                                                                               
         MVC   EXCIDENT,TCBID      MOVE IN TCB IDENTIFIER                       
         LA    RF,1(RF)                                                         
         ST    RF,SBTSKMAX         BUMP MAX TASK COUNT                          
         LA    R6,EXCHNGLQ(,R6)    NEXT TASK BLOCK WITHIN ROW                   
*                                                                               
ADF04    BXLE  R3,R4,ADF02                                                      
         B     ADF22                                                            
         DROP  R2,R3,R6                                                         
         EJECT                                                                  
***********************************************************************         
*        ADFIRST ACTIONS FOR AOR                                      *         
***********************************************************************         
         SPACE 1                                                                
AORFIRST L     RE,VSSB             FIND TOR DATASPACE                           
         USING SSBD,RE                                                          
         MVI   TORFLAG,C'N'        IDENTIFY AOR                                 
         MVC   NOTOR+4(3),SSBSYSNA                                              
*                                                                               
         LAM   R2,R2,SSBALET                                                    
         ICM   R2,15,SSBATOR                                                    
         BNZ   *+6                                                              
         DC    H'0'                                                             
         SAC   512                                                              
         USING TORFACD,R2                                                       
         CLI   TORAVLBL,SBYES      IS TOR AVAILABLE?                            
         BE    ADF06               YES                                          
         SAC   0                                                                
         WTO   TEXT=NOTORMSG                                                    
         ABEND 900                                                              
*                                                                               
ADF06    MVC   TSTOKEN,TORSTOKN    GET TOR STOKEN LOCALLY                       
         SAC   0                                                                
*                                                                               
         XC    ADFCARD,ADFCARD     SVC 247 NEEDS CHANGING                       
         MVC   ADFCARD+00(04),=C'PALE'                                          
         MVC   ADFCARD+04(12),=C'TESTDATAMGRT'                                  
         MVC   ADFCARD+28(08),TSTOKEN                                           
         LA    R0,21                                                            
         LNR   R0,R0                                                            
         LA    R1,ADFCARD                                                       
         SVC   247                                                              
         LTR   RF,RF                                                            
         BZ    ADF08                                                            
*                                                                               
         CVD   RF,ADFDUB                                                        
         UNPK  RCMSG+4(4),ADFDUB                                                
         OI    RCMSG+8,X'F0'                                                    
         MVC   NOTOR+60(10),RCMSG                                               
         WTO   TEXT=NOTORMSG                                                    
         ABEND 901                                                              
*                                                                               
ADF08    L     RF,ADFCARD+24       SAVE ALET FOR TOR IN SSB & LOCALLY           
         L     RE,VSSB                                                          
         USING SSBD,RE                                                          
         ST    RF,AALET                                                         
         ST    RF,SSBTLET                                                       
         DROP  RE                                                               
*                                                                               
         EXTRACT ASIDFLD,'S',FIELDS=(ASID)                                      
         ALESERV EXTRACTH,STOKEN=ASTOKEN                                        
*                                                                               
         L     RE,VSSB             FIND TOR DATASPACE                           
         USING SSBD,RE                                                          
         MVC   SSBSTOKN,ASTOKEN    SAVE STOKEN IN SSB                           
*                                                                               
         LA    R2,TORFACLQ(,R2)    INDEX TO FACPAK EXCHANGE GRID                
         SAC   512                                                              
         USING SBEXCHD,R2                                                       
         LH    R4,0(,R2)                                                        
         L     R5,2(,R2)                                                        
*                                                                               
         SR    R1,R1               USE SYSIX TO INDEX TO AOR                    
         IC    R1,SSBSYSIX                                                      
         SRL   R1,4                                                             
         MH    R1,0(,R2)                                                        
         LA    R2,6(,R2)                                                        
         AR    R2,R1                                                            
         DROP  RE                                                               
*                                                                               
         OC    SBSTOKEN,SBSTOKEN   SLOT FREE?                                   
         BZ    ADF10               THIS WILL NEED TO CHANGE (CPU++) =2          
*                                                                               
         SAC   0                                                                
         WTO   'THIS AOR IS ALREADY STARTED '                                   
         ABEND 902                                                              
*                                                                               
ADF10    MVC   SBSTOKEN,ASTOKEN    SAVE STOKEN FOR THIS AOR                     
         L     RF,ASIDFLD                                                       
         STH   RF,SBASID           SET ASID FOR THIS AOR                        
         MVC   SBWAKE,=A(POSTRECB) SET A(WAKEUP ECB) FOR THIS AOR               
         MVI   SBSTRTD,SBYES       SET FACPAK STARTED                           
         MVI   SBAVLBL,SBNO                                                     
*                                                                               
         L     R3,VSELIST                                                       
         LH    R4,0(R3)                                                         
         L     R5,2(R3)                                                         
         LA    R3,6(R3)                                                         
         USING SELISTD,R3                                                       
         CPYA  R6,R2                                                            
         LA    R6,SBSYS                                                         
         XR    RF,RF                                                            
*                                                                               
ADF12    TM    SEIND,SEISTRT       IS THE SYSTEM STARTED?                       
         BZ    ADF14               NO                                           
         LA    RF,1(RF)                                                         
         STCM  RF,1,SBSYSCNT                                                    
         MVC   0(L'SESYS,R6),SESYS                                              
         LA    R6,L'SESYS(,R6)                                                  
*                                                                               
ADF14    BXLE  R3,R4,ADF12                                                      
*                                                                               
         LA    R6,SBTSKBLK                                                      
         USING EXCHNGD,R6                                                       
         L     R3,VTCB                                                          
         LH    R4,0(R3)                                                         
         L     R5,2(R3)                                                         
         LA    R3,6(R3)                                                         
         USING TCBD,R3                                                          
         XR    RF,RF                                                            
*                                                                               
ADF16    TM    TCBFLAG2,TCBNOPED+TCBAORED                                       
         BNZ   ADF20               YES                                          
         CLI   TCBID,C'*'          IGNORE IF NO *TASK HEADER                    
         BNE   ADF18                                                            
*                                                                               
         XC    EXCHNGD(EXCHNGLQ),EXCHNGD                                        
         MVC   EXCIDENT,TCBID      MOVE IN TCB IDENTIFIER                       
         LA    RF,1(RF)                                                         
         ST    RF,SBTSKMAX         BUMP MAX TASK COUNT                          
         LA    R6,EXCHNGLQ(R6)     NEXT TASK BLOCK                              
*                                                                               
ADF18    BXLE  R3,R4,ADF16                                                      
*                                                                               
ADF20    SAC   0                                                                
         ST    R3,SPRTSK           SET A(LAST TASK) AS SPARE ($OPS)             
         L     RE,VSSB                                                          
         NI    SSBSTAT1-SSBD(RE),255-SSBUII     SET SSBGO FOR AOR               
         DROP  R6                                                               
         EJECT                                                                  
***********************************************************************         
*        COMMON ADFIRST ACTIONS                                       *         
***********************************************************************         
         SPACE 1                                                                
ADF22    EQU   *                                                                
         GOTO1 =V(AORNUM),DUB      GET AOR NUMBER                               
         L     RE,VSSB                                                          
         USING SSBD,RE                                                          
         SR    R1,R1                                                            
         IC    R1,DUB+3                                                         
         SLL   R1,4                                                             
         STC   R1,SSBSYSIX                                                      
         OC    SSBSYSIX,SSBSYSID   SET EXTERNAL SYSID                           
*                                                                               
         XC    DUB,DUB                                                          
         MVI   DUB+1,3                                                          
         GOTO1 VLOCKSPC,DUB        CALL LOCKSPC TO ENTER A FACPAK               
*                                                                               
         SAC   0                                                                
         L     R2,=V(ABENDPC)      SET MVS ESPIE EXIT                           
         ESPIE SET,(2),((1,7),9,11,12,15)                                       
*                                                                               
         L     R2,=V(ABENDES)      SET MVS ESTAE EXIT                           
         ESTAE (2),CT,ASYNCH=YES,TERM=NO                                        
*                                                                               
         L     R7,=V(ABENDIT)      SET INTERVAL TIMER EXIT                      
         L     R8,=V(ABENDITR)                                                  
         GOTO1 VTICTOC,ADFPL,C'XSET',(R7),(R8)                                  
*                                                                               
         MVI   MQIOFLAG,0          SET MQIO FLAG TO NOT ACTIVE                  
         L     RE,VSSB             RE=A(SSB)                                    
         USING SSBD,RE                                                          
         ICM   R5,15,SSBAATC                                                    
         BZ    ADF26                                                            
*                                                                               
         LH    R2,0(R5)                                                         
         L     R3,2(R5)                                                         
         LA    R5,6(R5)                                                         
         USING ATCD,R5             R5=A(ATC)                                    
*                                                                               
         ST    RA,ATCSYSFC                                                      
         CLI   SSBJESIO,C' '       ATTACH JES SUBMIT FACILITY                   
         BNH   ADF24                                                            
*                                                                               
         LA    R4,SSBJESIO                                                      
         LA    R6,ATCOSECB                                                      
         ATTACH EPLOC=(R4),PARAM=((R5)),ECB=(R6),SZERO=YES                      
         ST    R1,ATCOSTCB         SAVE A(OPERATING SYSTEM TCB)                 
         LA    R5,0(R2,R5)         BUMP A(ATCD)                                 
         ST    RA,ATCSYSFC                                                      
*                                                                               
ADF24    L     RE,VSSB             RE=A(SSB)                                    
         OC    SSBMQION,SSBMQION   TEST FOR MQSERIES FACILITY                   
         BZ    ADF26                                                            
         CLI   TORFLAG,C'Y'                                                     
         BNE   ADF26                                                            
         MVI   MQIOFLAG,X'FF'      SET MQIO FLAG TO ACTIVE                      
         MVI   ATCTYPE,ATCTMQIO                                                 
         MVC   ATCARECB,=V(MQIOECB)                                             
         MVC   ATCAIOPL,=V(MQIOLST)                                             
         LA    R6,ATCOSECB         ATTACH MQIO SCHEDULER                        
         ATTACH EP=FAMQIO,PARAM=((R5)),ECB=(R6),SZERO=YES                       
         ST    R1,ATCOSTCB         SAVE A(OPERATING SYSTEM TCB)                 
*                                                                               
ADF26    L     R7,VSELIST          POINT TO SE 1 ENTRY                          
         LA    R7,6(R7)                                                         
         ST    R7,LASTSE                                                        
*                                                                               
         L     R7,VTCB             POINT TO TASK 1 ENTRY                        
         LA    R7,6(R7)                                                         
         ST    R7,LASTTCB                                                       
*                                                                               
         CLI   TORFLAG,C'Y'        VTAM WAIT FOR TOR ONLY                       
         BNE   ADF28                                                            
         GOTO1 VLCM,ADFPL,VTWAIT                                                
*                                                                               
         L     RE,VSSB                                                          
         OC    SSBT2,SSBT2         TEST TIMER 2 ACTIVE                          
         BZ    ADF28                                                            
         OI    SSBSTAT1,SSBSVTAM   SET AWAITING VTAM                            
         LA    R7,1                                                             
         GOTO1 VTICTOC,ADFPL,C'2SET',(R7)                                       
         LA    R1,TIMERECB                                                      
         WAIT  1,ECB=(1)                                                        
*                                                                               
ADF28    L     RF,=V(ADNEXT)                                                    
         BR    RF                                                               
         DROP  R3,R5,RE                                                         
*                                                                               
         LTORG                                                                  
*                                                                               
NOTORMSG DC    AL2(0075)                                                        
NOTOR    DC    CL75'*FACPAK* TOR FACPAK UNAVAILABLE - FACPAK ABENDING'          
RCMSG    DC    CL10'(RF)=XXXX'                                                  
*                                                                               
ADFDUB   DS    D                                                                
ADFPL    DS    4F                                                               
*                                                                               
         DC    CL8'ADFCARD*'                                                    
ADFCARD  DC    XL48'00'                                                         
         EJECT                                                                  
***********************************************************************         
* EOJ PENDING - CLOSE DOWN VTAM AND DETACH SUB-TASKS                  *         
***********************************************************************         
         SPACE 1                                                                
ADLAST   NTR1  BASE=*                                                           
         L     R9,=A(TASKCNST)                                                  
         USING TASKCNST,R9                                                      
         L     RA,VSYSFX                                                        
         USING SYSFACD,RA                                                       
         SAC   0                   YOU NEVER KNOW AROUND HERE                   
         LAM   R0,RF,ARZERO        SO MAKE IT EASY ON YOURSELF                  
*                                                                               
         L     RE,VSSB                                                          
         LAM   R2,R2,SSBALET-SSBD(RE)                                           
         ICM   R2,15,SSBATOR-SSBD(RE)                                           
         SAC   512                                                              
         LA    R2,TORFACLQ(,R2)                                                 
         USING SBEXCHD,R2                                                       
         LH    R4,0(,R2)                                                        
         L     R5,2(,R2)                                                        
         LA    R2,6(,R2)                                                        
*                                                                               
         CLC   ASTOKEN,SBSTOKEN    IS THIS OUR ROW?                             
         BE    *+10                YES                                          
         BXLE  R2,R4,*-10                                                       
         DC    H'0'                BAD - WHERE IS OUR DSPACE ENTRY?             
*                                                                               
         MVI   SBSTRTD,SBNO        SET FACPAK NOT STARTED                       
         MVI   SBAVLBL,SBNO        SET FACPAK NOT AVAILABLE                     
         XC    SBSTOKEN,SBSTOKEN   CLEAR STOKEN                                 
*                                                                               
         SAC   0                                                                
         LAM   R2,R2,ARZERO                                                     
*                                                                               
         CLI   TORFLAG,C'Y'        VTAM CLOSE FOR TOR ONLY                      
         BNE   ADLST02                                                          
*                                                                               
         GOTO1 VLCM,ADLPL,VTCLOSE  CLOSE DOWN VTAM                              
*                                                                               
ADLST02  L     RF,VSSB             DETACH ANY ATTACHED SUB-TASKS                
         L     R5,SSBAATC-SSBD(RF)                                              
         LH    R6,0(R5)                                                         
         L     R7,2(R5)                                                         
         LA    R5,6(R5)                                                         
         USING ATCD,R5             R5=A(ATC)                                    
*                                                                               
ADLST04  TM    ATCSTAT1,ATCSATCH   TEST TASK ATTACHED                           
         BZ    ADLST08             NO                                           
         TM    ATCSTAT1,ATCSAACT   TEST TASK IS BUSY                            
         BNZ   ADLST06             YES - WAIT FOR TASK TO EXIT                  
         LA    R1,ATCECB                                                        
         POST  (1)                 POST COMPLETION TO FORCE EXIT                
*                                                                               
ADLST06  LA    R1,ATCOSECB         WAIT FOR O/S TO POST COMPLETION              
         WAIT  1,ECB=(1)                                                        
         LA    R1,ATCOSTCB         DETACH TASK                                  
         DETACH (1),STAE=NO                                                     
*                                                                               
ADLST08  BXLE  R5,R6,ADLST04                                                    
         DROP  R5                                                               
*                                                                               
         ESTAE 0                   CANCEL ESTAE                                 
         XIT1  ,                                                                
*                                                                               
         LTORG                                                                  
*                                                                               
ADLPL    DS    4F                                                               
         DROP  R9,RB                                                            
         EJECT                                                                  
***********************************************************************         
* FIND FIRST UNPROCESSED WORK & INITIALISE CORRESPONDING TASK         *         
* NTRY:                                                               *         
* EXIT: R7 = A(DUMMY SELIST ENTRY)                                    *         
*     : R5 = A(UTL COPY)                                              *         
*      SSBTKADR = A(NEW TASK CONTROL BLOCK)                           *         
*      TCBUTL (WITHIN SSBTKADR) = A(UTL)                              *         
***********************************************************************         
         SPACE 1                                                                
TASKFIND DS    0H                                                               
         NMOD1 0,TASKFIND                                                       
                                                                                
         L     R2,VSSB         *** SPECIALS THAT RUN IN SPRTSK HERE             
         USING SSBD,R2                                                          
         TM    SSBOPECB,X'80'      TEST SROPS IN PROGRESS                       
         BO    TFIND04             YES                                          
         LA    R5,UTLZERO          POINT TO DUMMY UTL ENTRY                     
         USING UTLD,R5                                                          
*                                                                               
         TM    DSOPSECB,X'40'      TEST DSPACE OPS ECB POSTED                   
         BO    *+16                                                             
         L     RF,SSBOPECB                                                      
         TM    0(RF),X'40'         OPERATOR REQUEST TO HANDLE                   
         BZ    TFIND02                                                          
*                                                                               
         XC    DSOPSECB,DSOPSECB                                                
         TM    TSTAT2,TSTATTIP     TEST BUSY WITH MAIN INITIALISATION           
         BO    TFIND04             YES - CAN'T DO ANYTHING ELSE                 
*                                                                               
         MVC   SSBTKADR,SPRTSK     SET A(SPARE TASK)                            
         MVC   DUB+4(2),$OPS                                                    
         B     TFIND03                                                          
*                                                                               
TFIND02  TM    TSTAT2,TSTATTIP     TEST BUSY WITH MAIN INITIALISATION           
         BO    TFINDL              YES - CAN'T DO ANYTHING ELSE                 
*                                                                               
         MVI   MODE,0              SET NORMAL MODE                              
         NOP   *+12                                                             
         OI    *-3,X'F0'                                                        
         OI    MODE,X'80'          SET FIRST TIME MODE                          
*                                                                               
         CLI   MODE,0              TIMER SET?                                   
         BE    TFIND04             NO                                           
         MVC   DUB+4(2),$T2                                                     
*                                                                               
TFIND03  MVC   SSBTKADR,SPRTSK     RUN IN SPARE TASK                            
*                                                                               
         L     R7,VSELIST          GET A(DUMMY SELIST ENTRY)                    
         USING SELISTD,R7                                                       
         LH    RE,0(R7)                                                         
         L     RF,2(R7)                                                         
         LA    R7,6(R7)                                                         
         CLI   SESYS,1             MATCH SYSTEM (SERVICE = SE#1)                
         BE    *+10                                                             
         BXLE  R7,RE,*-8                                                        
         DC    H'0'                WHERE SELIST FOR SERVICE?                    
*                                                                               
         L     R6,SSBTKADR                                                      
         USING TCBD,R6                                                          
         ST    R7,TCBSE            SET A(SELIST ENTRY)                          
*                                                                               
         ST    R5,TCBUTL           SAVE A(UTL)                                  
         MVC   TNEXTIN,SEFIRST+1                                                
         ST    R5,SEFIRST          PUT THIS UTL TO FRONT OF QUEUE               
         LH    RF,SEQLEN                                                        
         LA    RF,1(RF)                                                         
         STH   RF,SEQLEN           INCREMENT QUEUE                              
*                                                                               
         MVC   TAGYB,MODE          SET MODE                                     
         MVC   TSVCREQ,DUB+4       SET SPECIAL S/R                              
         NI    SSBT2,X'7F'                                                      
         MVC   TTIMETU,DUB         SET ARRIVAL TIME TO NOW                      
         B     TFINDE              GO PROCESS TRANSACTION                       
         DROP  R2,R5,R6,R7                                                      
*                                                                               
TFIND04  L     R2,VSSB         *** NORMAL TRANSACTIONS HERE                     
         USING SSBD,R2                                                          
         LAM   R3,R3,SSBALET                                                    
         ICM   R3,15,SSBATOR                                                    
         SAC   512                                                              
         USING TORFACD,R3                                                       
         CLI   TORSTRTD,SBYES      TOR INITIALISED?                             
         BNE   TFINDL              NO - GO TO SLEEP                             
*                                                                               
         GOTO1 VAORSLT,TFLIST      GET OUR SLOT IN TOR BLOCK                    
         L     R3,0(R1)                                                         
         SAC   512                                                              
         USING SBEXCHD,R3                                                       
*                                                                               
         ICM   R0,15,SBTSKMAX      NUMBER OF TASKS IN AOR                       
         CPYA  R4,R3                                                            
         LA    R4,SBTSKBLK                                                      
         USING EXCHNGD,R4                                                       
*                                                                               
         L     R5,VUTL                                                          
         LH    RE,0(R5)                                                         
         LA    R5,6(R5)            R5=A(LOCAL UTL BLOCK)                        
         USING UTLD,R5                                                          
*                                                                               
TFIND08  CLI   EXCFLAG,EXCNEW      NEW TRANSACTION TO PROCESS?                  
         BE    TFIND10             YES                                          
         AHI   R4,EXCHNGLQ                                                      
         AR    R5,RE                                                            
         BCT   R0,TFIND08                                                       
         B     TFINDL              NO WORK TO DO                                
         DROP  R3                                                               
*                                                                               
TFIND10  L     R6,VTCB             MATCH TCB SLOT TO EXCHANGE SLOT              
         LH    RE,0(R6)                                                         
         L     RF,2(R6)                                                         
         LA    R6,6(R6)                                                         
         USING TCBD,R6                                                          
         CLC   EXCIDENT,TCBID                                                   
         BE    *+10                                                             
         BXLE  R6,RE,*-10                                                       
         DC    H'0'                                                             
*                                                                               
         ST    R6,SSBTKADR                                                      
         MVI   EXCFLAG,EXCBUSY     SET THIS TASK IS NOW RUNNING                 
*                                                                               
         ICM   R3,15,SSBATOR       POINT R3 BACK TO TOR BLOCK                   
         USING TORFACD,R3                                                       
         ICM   R3,15,TORUTL        PICK UP A(UTL BLOCK) IN TOR                  
         DROP  R3                                                               
*                                                                               
         LAM   R3,R3,SSBTLET       R3 POINTS INTO TOR                           
         LH    RF,EXCUTL           PICK UP TNUM & INDEX INTO UTL LIST           
         BCTR  RF,0                                                             
         LH    RE,0(,R3)                                                        
         MSR   RF,RE                                                            
         LA    R3,6(RF,R3)         R3 = A(UTL IN TOR)                           
*                                                                               
         BCTR  RE,0                COPY UTL ENTRY LOCALLY                       
         EX    RE,*+4                                                           
         MVC   UTLD(0),0(R3)                                                    
         ST    R5,TCBUTL           SET A(UTL) IN TCB                            
         DROP  R6                                                               
*                                                                               
         GOTO1 VLCM,TFLIST,VTGETBUF GET FREE LOCAL BUFFER (RTN IN R1)           
         SAFE  CLEAR=ARZERO                                                     
*                                                                               
         LA    RE,*+10              SWITCH INTO XA                              
         O     RE,=XL4'80000000'                                                
         BSM   0,RE                                                             
*                                                                               
         ICM   R6,15,TBUFF         SAVE A(TBUFF) IN TOR                         
         AHI   R6,-32              POINT TO HEADER                              
         LHI   R7,32+TBUFFXL                                                    
*                                                                               
         LR    RE,R1                                                            
         STCM  RE,15,TBUFF         SET A(LOCAL TBUFF) IN TBUFF                  
         AHI   RE,-32                                                           
         LR    RF,R7                                                            
*                                                                               
         LAM   R6,R6,SSBTLET                                                    
         MVCL  RE,R6               COPY TOR TBUFF LOCALLY                       
         LAM   R6,R6,ARZERO                                                     
         SAC   0                   OUT OF ACCESS MODE                           
         DROP  R4                                                               
*                                                                               
         LA    RE,*+6              SWITCH FROM XA                               
         BSM   0,RE                                                             
*                                                                               
         L     R6,SSBTKADR                                                      
         USING TCBD,R6                                                          
         L     R7,VSELIST          GET A(SELIST ENTRY)                          
         USING SELISTD,R7                                                       
         LH    RE,0(R7)                                                         
         L     RF,2(R7)                                                         
         LA    R7,6(R7)                                                         
*                                                                               
         LA    R0,1                SE#1 FOR SERVICE REQUESTS                    
         OC    TSVCREQ,TSVCREQ     TASK IS FOR A SERVICE REQUEST                
         BNZ   *+8                                                              
         ICM   R0,1,TSYS                                                        
*                                                                               
         CLM   R0,1,SESYS          MATCH SYSTEM                                 
         BE    *+10                                                             
         BXLE  R7,RE,*-8                                                        
         DC    H'0'                                                             
*                                                                               
         ST    R7,TCBSE            SET SELIST ENTRY                             
         STCM  R7,7,TASYS          SET LOCAL SELIST ENTRY IN UTL                
         STCM  R7,7,TARSYS         SET REAL SELIST ENTRY IN UTL                 
         MVC   SSBSIN,TSIN         SET SIN TO THIS ONE                          
*                                                                               
TFINDE   CR    RB,RB               SET CC EQUAL                                 
         B     TFINDX                                                           
*                                                                               
TFINDL   CLI   *,FF                SET CC LOW                                   
         B     TFINDX                                                           
*                                                                               
TFINDX   SAC   0                                                                
         LAM   R0,RF,ARZERO                                                     
         XMOD1                                                                  
         DROP  R2,R5,R6,R7                                                      
*                                                                               
TFLIST   DS    4F                                                               
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* COPY COMPLETED TASK INFORMATION BACK INTO TOR                       *         
***********************************************************************         
         SPACE 1                                                                
TORBACK  DS    0H                                                               
         NMOD1 0,TORBACK                                                        
         SAC   0                                                                
         LAM   R0,RF,ARZERO        RESET ALL ACCESS REGISTERS                   
*                                                                               
         L     R2,VSSB                                                          
         USING SSBD,R2                                                          
         L     R6,SSBTKADR                                                      
         USING TCBD,R6                                                          
         C     R6,SPRTSK                                                        
         BE    TBACKX              NOTHING IN SPARE TASK COPIED BACK            
*                                                                               
         L     R5,TCBUTL                                                        
         USING UTLD,R5                                                          
         OC    TNUM,TNUM                                                        
         BZ    TBACKX              DUMMY TERMINALS DON'T GO BACK                
*                                                                               
         LAM   R3,R3,SSBALET                                                    
         ICM   R3,15,SSBATOR                                                    
         SAC   512                                                              
         USING TORFACD,R3          R3=A(TOR REGION)                             
         CPYA  R4,R3                                                            
         LA    R4,TORFACLQ(,R3)                                                 
         USING SBEXCHD,R4          R4=A(FACPAK GRID ENTRY)                      
         LH    RE,0(,R4)                                                        
         L     RF,2(,R4)                                                        
         LA    R4,6(,R4)                                                        
         CLC   ASTOKEN,SBSTOKEN    FIND OUR GRID ENTRY?                         
         BE    *+10                                                             
         BXLE  R4,RE,*-10                                                       
         DC    H'0'                WHERE IS OUR GRID ENTRY?                     
*                                                                               
         ICM   R0,15,SBTSKMAX      R0=# TASKS IN THIS AOR                       
         LA    R7,SBTSKBLK                                                      
         CPYA  R7,R4                                                            
         USING EXCHNGD,R7          R7=A(TASK CONTROL BLOCK)                     
*                                                                               
TBACK02  CLC   EXCUTL,TNUM         THIS TASK SLOT?                              
         BE    TBACK04             YES                                          
         AHI   R7,EXCHNGLQ                                                      
         BCT   R0,TBACK02                                                       
         DC    H'0'                WHERE SLOT?                                  
*                                                                               
TBACK04  CLI   TORAVLBL,SBYES      TOR STILL THERE?                             
         BE    *+16                YES                                          
         OI    SBDONE,X'40'                                                     
         MVI   EXCFLAG,EXCDONE     NO - FLAG TASK COMPLETE & EXIT               
         B     TBACKX                                                           
*                                                                               
         LAM   R8,R8,SSBTLET                                                    
         ICM   R8,15,TORUTL        R8=A(TOR UTL LIST)                           
*                                                                               
         XR    RF,RF                                                            
         ICM   RF,3,TNUM                                                        
         BCTR  RF,0                RF=TNUM                                      
         MH    RF,0(,R8)                                                        
         LA    R8,6(RF,R8)         R8=A(TOR UTL ENTRY)                          
TOR      USING UTLD,R8                                                          
*                                                                               
         MVC   STBUFF,TOR.TBUFF    PRESERVE A(TBUFF)                            
         MVC   STASYS,TOR.TASYS    PRESERVE TASYS                               
         MVC   STARSYS,TOR.TARSYS  PRESERVE TARSYS                              
*                                                                               
         L     RF,VUTL             COPY BACK UTL TO TOR                         
         LH    RF,0(RF)                                                         
         BCTR  RF,0                                                             
         EX    RF,*+4                                                           
         MVC   TOR.UTLD(0),UTLD                                                 
*                                                                               
         MVC   TOR.TBUFF,STBUFF    RESTORE SAVED VALUES                         
         MVC   TOR.TASYS,STASYS                                                 
         MVC   TOR.TARSYS,STARSYS                                               
*                                                                               
         LA    RE,*+10                                                          
         O     RE,=XL4'80000000'                                                
         BSM   0,RE                                                             
*                                                                               
         ICM   RE,15,TOR.TBUFF     COPY BACK TBUFF TO TOR                       
         AHI   RE,-32                                                           
         ICM   R0,15,TBUFF                                                      
         AHI   R0,-32                                                           
         LHI   R1,32+TBUFFXL                                                    
         LR    RF,R1                                                            
         CPYA  RE,R8               POINT RE INTO TOR ADDRESS SPACE              
         MVCL  RE,R0                                                            
         LAM   RE,RE,ARZERO                                                     
*                                                                               
         OI    SBDONE,X'40'        SET AOR HAS COMPLETED WORK                   
         MVI   EXCFLAG,EXCDONE     FLAG TASK COMPLETED                          
*                                                                               
         ICM   R0,15,TBUFF         GET A(TBUFF) AND FREE IT                     
         GOTO1 VLCM,TBLIST,VTRELBUF,(R0)                                        
         SAFE  CLEAR=ARZERO                                                     
*                                                                               
         ICM   R4,15,TORDONE       GET A(COMPLETION ECB IN TOR)                 
         XR    R5,R5                                                            
         ICM   R5,3,TORASID        GET ASID FOR TOR IN R4                       
*                                                                               
         SAC   0                                                                
         LAM   R0,RF,ARZERO        RESET ALL ACCESS REGISTERS                   
         DROP  R2,R3,R4,R5,TOR                                                  
*                                                                               
         LOCASCB ASID=(R5)         USE ASID TO GET ASCB                         
         LR    R2,R1                                                            
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                @@@TEMP FOR DEBUGGING@@@                     
*                                                                               
         USING ASCB,R2             MAKE SURE ASCB IS OURS                       
         CLC   ASCBASCB,=C'ASCB'                                                
         BE    *+6                                                              
         DC    H'0'                FIND A BETTER TEST!!                         
         DROP  R2                                                               
*                                                                               
         LA    R5,99                                                            
         POST (R4),(R5),ASCB=(R2),LINKAGE=SYSTEM,ECBKEY=8,MF=(E,POSTTO)         
         LTR   RF,RF                                                            
         BZ    TBACKX                                                           
*                                                                               
*              MIGHT NEED TO DO SOMETHING HERE                                  
*                                                                               
TBACKX   XMOD1 ,                   RETURN TO CALLER AFTER XM POST               
*                                                                               
TBLIST   DS    4F                                                               
*                                                                               
POSTTO   POST  ECBKEY=YES,MF=L                                                  
*                                                                               
PERR     BR    RE                                                               
*                                                                               
STBUFF   DS    A                                                                
STASYS   DS    AL3                                                              
STARSYS  DS    AL3                                                              
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* TASKER CONSTANTS AND WORKING STORAGE - R9 ADDRESSES                 *         
***********************************************************************         
         SPACE 1                                                                
         DROP  RB                                                               
TASKCNST DS    0D                                                               
         DC    CL8'TASKCNST'                                                    
ARZERO   DC    16F'0'              TO CLEAR ACCESS REGISTERS                    
*                                                                               
VTORRCV  DC    V(TORRCV)                                                        
VAORSLT  DC    V(AORSLT)                                                        
VSYSFX   DC    V(SYSFAC)                                                        
VMONITR  DC    V(MONITOR)                                                       
VSCRNCH  DC    V(SCRUNCH)                                                       
VLOGGR   DC    V(LOGGER)                                                        
VBILLIT  DC    V(BILLIT)                                                        
VMSGQIN  DC    V(MSGQIN)                                                        
VMQIO    DC    V(MQIO)                                                          
*                                                                               
ADFIRREG DS    2D                  RETURN REGISTERS RB,RC,RD,RE                 
DUB      DS    D                                                                
PARM     DS    6F                                                               
         DS    XL7                                                              
MODE     DS    X                                                                
DIRECT   DS    X                                                                
MQIOFLAG DS    X                                                                
TORFLAG  DS    X                                                                
*                                                                               
SPRTSK   DC    A(0)                A(SPARE TASK) FOR SROPS IN AOR               
*                                                                               
LASTSE   DC    A(0)                A(LAST SE ENTRY TO COMPLETE)                 
LASTTCB  DC    A(0)                A(LAST TCB POSTED COMPLETE)                  
*                                                                               
T10SECS  DC    AL4(10*38400)       10 SECONDS IN TUS                            
*                                                                               
MAXMAXIO DC    A(30000)            EXTENDED MAX NUM OF I/O'S PER TASK           
ACCMAXIO DC    A(120000)           ACCOUNT  MAX NUM OF I/O'S PER TASK           
MIDNIGHT DC    XL4'C5C10000'       TU ADJUSTMENT FACTOR FOR MIDNIGHT            
*                                                                               
$SYSNOP  DC    X'0103'                                                          
$T2      DC    X'0104'                                                          
$SUB     DC    X'0107'                                                          
$EXT     DC    X'010A'                                                          
$VTL     DC    X'010C'                                                          
$WKR     DC    X'010D'                                                          
$OPS     DC    X'010F'                                                          
$DISP    DC    X'0117'                                                          
$LOAD    DC    X'0118'                                                          
$SCR     DC    X'011F'                                                          
$CAN     DC    X'01FC'                                                          
$DONE    DC    X'0112'                                                          
$EOJ     DC    X'0113'                                                          
$HELP    DC    X'0109'                                                          
$TEST    DC    X'0136'                                                          
$BC      DC    X'0144'                                                          
*&&US                                                                           
$SPTDARE DC    X'0161'                                                          
$REPDARE DC    X'0162'                                                          
$MKGDARE DC    X'0163'                                                          
*&&                                                                             
$TOR     DC    X'016D'                                                          
$$BYE    DC    X'01EE'                                                          
$UNWIND  DC    X'01FB'                                                          
$ABEND   DC    X'01FF'                                                          
*                                                                               
TORWAKE  DC    XL4'FFFFFFFF'       TOR POST ADDRESS IDENTIFIER                  
UTL0ID   DC    XL2'FFFF'           SPECIAL NUMBERS FOR DUMMY UTLS               
UTL0AID  DC    XL2'FFFE'                                                        
         LTORG                                                                  
*                                                                               
         DS    0D                                                               
         DC    C'*ECBTIM*'                                                      
TIMERECB DC    F'0'                                                             
         DC    F'0'                                                             
*                                                                               
         DC    C'*ECBDSP*'                                                      
DSPACECB DC    F'0'                                                             
         DC    F'0'                                                             
*                                                                               
         DC    C'*ECBOPS*'                                                      
DSOPSECB DC    F'0'                                                             
         DC    F'0'                                                             
*                                                                               
         DC    C'*ECBPST*'                                                      
POSTECB  DC    F'0'                                                             
         DC    F'0'                                                             
*                                                                               
         DC    C'*ECBABD*'                                                      
ABNDECB  DC    F'0'                                                             
         DC    F'0'                                                             
*                                                                               
         DC    C'*ECBPST*'                                                      
POSTRECB DC    F'0'                                                             
         DC    F'0'                                                             
*                                                                               
         DC    CL8'TSTOKEN*'                                                    
TSTOKEN  DC    D'0'                COPY OF TOR STOKEN FOR BIND                  
         DC    CL8'ASIDFLD*'                                                    
ASIDFLD  DC    D'0'                                                             
         DC    CL8'ASTOKEN*'                                                    
ASTOKEN  DC    D'0'                                                             
         DC    CL8'AALET***'                                                    
AALET    DC    A(0)                                                             
*                                                                               
         DC    C'*ECBLST*'                                                      
ECBLST   DC    XL256'00'                                                        
         DC    XL256'00'                                                        
*                                                                               
         DC    C'*UTLZERO'                                                      
UTLZERO  DC    XL256'00'                                                        
         ORG   UTLZERO+TSYM-UTLD                                                
         DC    CL8'NONENONE'                                                    
         ORG                                                                    
         ORG   UTLZERO+TTYPE2-UTLD                                              
         DC    X'80'               DEFINE AS DUMMY TERMINAL                     
         ORG                                                                    
*                                                                               
         DS    0D                                                               
         DC    C'*SVCDUM*'                                                      
SVCDUM   DC    CL7'SVCDUM '         PGMNAME                                     
         DC    AL1(PGMIRFU)         PGMIND                                      
         DC    AL1(0,0)             PGMINUM,PGMCOSYS                            
         DC    AL1(0,PGMISWT)       PGMPRTY,PGMIND2                             
         DC    AL1(0,0,0,0)         PGMTSKMX,PGMTSK,PGMALNUM,PGMCTRY            
         DC    AL2(0)               PGMTEXT                                     
         DC    AL1(PGMIRTOR)        PGMIND3                                     
         DC    AL1(0),XL4'00000000' PGMIND4,PGMCNT1                             
         DC    AL1(0),AL3(0)        N/D,PGMAGYLA                                
         DC    XL14'00'             PGMVINFO                                    
         DC    XL32'00'             SPARE FOR PGMLST EXPANSION                  
*                                                                               
*                                                                               
         DS    0D                                                               
         DC    C'*UTLZERA'                                                      
UTLZEROA DC    XL256'00'                                                        
         ORG   UTLZEROA+TSYM-UTLD                                               
         DC    CL8'NONENONE'                                                    
         ORG                                                                    
         ORG   UTLZEROA+TBUFF-UTLD                                              
         DC    A(UTLBUF)                                                        
         ORG                                                                    
         ORG   UTLZEROA+TTYPE2-UTLD                                             
         DC    X'80'               DEFINE AS DUMMY TERMINAL                     
         ORG                                                                    
UTLBUF   DC    1024C' '                                                         
*                                                                               
TASKWORK DS    256D                                                             
*                                                                               
FF       EQU   X'FF'                                                            
         EJECT                                                                  
***********************************************************************         
* INCLUDED BOOKS                                                      *         
***********************************************************************         
         SPACE 1                                                                
       ++INCLUDE FAATC                                                          
         EJECT                                                                  
       ++INCLUDE FAPGMLST                                                       
         EJECT                                                                  
       ++INCLUDE FARPLD                                                         
         EJECT                                                                  
       ++INCLUDE FASELIST                                                       
         EJECT                                                                  
       ++INCLUDE FASSB                                                          
         EJECT                                                                  
       ++INCLUDE FATBHD                                                         
         EJECT                                                                  
       ++INCLUDE FATCB                                                          
         EJECT                                                                  
       ++INCLUDE FAUTL                                                          
         EJECT                                                                  
       ++INCLUDE FASYSFAC                                                       
         EJECT                                                                  
       ++INCLUDE DMDSHDR                                                        
         EJECT                                                                  
       ++INCLUDE DMDTFIS                                                        
         EJECT                                                                  
       ++INCLUDE FAPIGFACD                                                      
         SPACE 2                                                                
         IHAASCB                                                                
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'019FATASKERS 03/27/01'                                      
         END                                                                    
