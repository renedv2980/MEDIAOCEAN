*          DATA SET DDDATVAL   AT LEVEL 004 AS OF 07/17/19                      
*CATALP DATVAL                                                                  
***********************************************************************         
* XL1 US 0=MONTH/DAY/YEAR,1=MONTH/DAY,2=MONTH/YEAR,3=DAY              *         
*                         or                                                    
* XL1 UK 0=DAY/MONTH/YEAR,1=DAY/MONTH,2=MONTH/YEAR,3=DAY              *         
* AL3    A(INPUT DATE - MAX LENGTH 12 BYTES)                          *         
*                                                                     *         
* XL1    INPUT X'80'=DONT SET NEW CENT YEAR                                     
*              X'40'=TWO CHR YEAR ONLY                                          
*              X'20'=ALLOW DATES 1964-2059                                      
*              X'10'=ALLOW DATES 1900-2079 future use                           
*        RETN  X'80'=CALLER DIDNT WANT NEW CENT YEAR                  *         
*              X'40'=20TH CENTURY DATE                                *         
*              X'20'=C'20NN' YEAR INPUT                               *         
*              X'10'=C'19NN' YEAR INPUT                               *         
*              X'08'=AMBIGUOUS INPUT       not currently used                   
* AL3    A(OUTPUT DATE - C'YYMMDD') -  SET TO C'000000' IF INVALID    *         
*        1ST YEAR BYTE SET TO X'FA' FOR 2000 THRU 2009.               *         
*        1ST YEAR BYTE SET TO X'FB' FOR 2010 THRU 2019.               *         
*        ...                                                          *         
*        1ST YEAR BYTE SET TO X'FF' FOR 2050 THRU 2059.               *         
* XL1    Optional input length                                        *         
* XL3    Not used                                                               
*                                                                     *         
* xxxxxxx DATE IS 1928-01-01 AND MAXIMUM DATE IS 2027-12-31           *         
* MINIMUM DATE IS 1964-01-01 AND MAXIMUM DATE IS 2027-12-31           *         
* MINIMUM DATE IS 1964-01-01 AND MAXIMUM DATE IS 2059-12-31           *         
*                                                                     *         
* INPUT CAN ALSO BE A ISO DATE YYYY-MM-DD OR YYYYMMDD INDEPENDENT     *         
* OF COUNTRY FOR INPUT TYPES 0 AND 2.                                 *         
***********************************************************************         
         TITLE 'DDDATVAL - VALIDATES VARIABLE DATES'                            
         PRINT NOGEN                                                            
DATVAL   CSECT                                                                  
         NMOD1 DATX-DATD,XXDTVLXX                                               
         USING DATD,RC                                                          
         SAM24 ,                   SET 24-BIT MODE                              
         ST    R1,SAVER1                                                        
*                                                                               
         LM    R2,R3,0(R1)         R2=A(INPUT DATE),R3=A(OUTPUT DATE)           
         MVC   INDATE,0(R2)                                                     
         MVI   INDATEX,X'FF'                                                    
         LA    R2,INDATE           R2=A(COPY OF INPUT DATE IN W/S)              
         MVC   0(6,R3),ZEROS       PRESET OUTPUT TO ZEROS                       
         MVI   CENTURY,19                                                       
         MVI   RETCODE,0                                                        
         MVI   VALFLAG,0                                                        
         XC    YEAR(3),YEAR        INIT YEAR/RETCODE                            
         TM    4(R1),OPT19C        CALLER DOESNT WANT NEW DDS DATES             
         BZ    *+8                                                              
         OI    RETCODE,OPT19C      SET DONT WANT NEW 20TH CENTURY DATE          
*                                                                               
*        MVC   MINYR,MINYRO        1928 ORIGINAL RANGE                          
         MVC   MINYR,MINYRO        1964 changed  RANGE                          
         MVC   MAXYR,MAXYRO        2027 ORIGINAL RANGE                          
         TM    4(R1),OPTN6459      ALLOW NEW RANGE                              
         BZ    DATVAL10                                                         
         MVC   MINYR,MINYRN        1964 NEW RANGE                               
         MVC   MAXYR,MAXYRN        2059 NEW RANGE                               
*                                                                               
DATVAL10 CTRY  ,                   GET TERM CTRY/LANG CODE                      
         STH   R0,CTRYLANG                                                      
         CLI   LANG,7              TEST UNDEFINED LANGUAGE                      
         BNH   *+10                                                             
         MVC   LANG,DEFLANG                                                     
         CLI   LANG,0              SET FOR UK AND US DIFFERENCES                
         BNE   *+10                                                             
         MVC   LANG,DEFLANG                                                     
         L     R1,SAVER1           RESTORE R1                                   
*                                                                               
         CLI   0(R1),3             VALIDATE ALPHA DAY NAME                      
         BE    DAYOWEEK                                                         
         CLI   0(R1),1             VALIDATE DAY/MONTH OR MONTH/DAY              
         BE    DV1                                                              
         BAS   R9,DVISO            TEST ISO YYYY-MM OR YYYY-MM-DD               
         BE    ISOV                                                             
DV1      CLI   DEFLANG,2           TEST IF USA STYLE DATES                      
         BE    USAV                                                             
         EJECT                                                                  
***********************************************************************         
* EUROPEAN STYLE DATES DAY/MONTH/YEAR SEQUENCE OF FIELDS              *         
***********************************************************************         
EURV     CLI   0(R2),C' '          BYPASS LEADING SPACES                        
         BH    EURV3                                                            
         LA    R2,1(R2)                                                         
         B     EURV                                                             
*                                                                               
EURV3    CLI   0(R1),2             VALIDATE FOR M/Y ONLY ?                      
         BNE   EURV4               NO-SKIP TO DAY VALIDATION                    
                                                                                
***********************************************************************         
* Validate month for MM/YY                                                      
***********************************************************************         
         CLI   0(R2),C'0'          INPUT MONTH ALPHA ?                          
         BL    EURV20              YES-SKIP TO ALPHA VALIDATE                   
         B     EURV10              NO-SKIP TO NUMERIC VALIDATE                  
*                                                                               
EURV4    BAS   R8,VDAY             Validate day number                          
         BAS   R8,DLMCHK                                                        
*                                                                               
EURV10   BAS   R8,GETNMON          Check numeric month                          
         BNE   EURV20                                                           
         BAS   R8,DLMCHK                                                        
         B     EURV30              SKIP TO YEAR VALIDATION                      
*                                                                               
EURV20   MVC   DUB(3),0(R2)        EXTRACT MONTH                                
         OC    DUB(3),SPACES       ENSURE UPPER CASE                            
         BAS   R8,GETCMON          Charater month                               
*                                                                               
EURV30   CLI   0(R1),1             VALIDATE DAY/MONTH ONLY ?                    
         BE    DVXIT               YES-VALIDATE WHAT WE'VE GOT                  
         CLI   0(R2),C' '          BYPASS SPACES                                
         BNE   EURV40                                                           
         LA    R2,1(R2)                                                         
*                                                                               
EURV40   BAS   RE,DVYEAR           VALIDATE YY OR YYYY                          
         MVC   0(2,R3),0(R2)       MOVE VALID YEAR TO OUTPUT                    
         BAS   R9,CVYEAR           TEST IF CALLER WANTS FUNNY YEAR              
         LA    R2,2(R2)            BUMP PAST YEAR BYTES                         
         B     DVXIT                                                            
                                                                                
***********************************************************************         
* USA STYLE DATE VALIDATION - 0=M/D/Y,1=M/D,2=M/Y                     *         
***********************************************************************         
USAV     CLI   0(R2),C' '          BYPASS LEADING SPACES                        
         BH    USAVMON                                                          
         LA    R2,1(R2)                                                         
         B     USAV                                                             
                                                                                
**********************************************************************          
* Validate and process Month                                                    
**********************************************************************          
USAVMON  BAS   R8,GETNMON          Validate numeric month                       
         BE    USAVMO10                                                         
         MVC   DUB(3),0(R2)        Validate character month                     
         OC    DUB(3),SPACES       ENSURE UPPER CASE                            
         BAS   R8,GETCMON          Get character month                          
*                                                                               
USAVMO10 BAS   R8,DLMCHK                                                        
         CLI   0(R1),2             GO VALIDATE YEAR IF M/Y FORMAT               
         BE    USAVYEAR                                                         
                                                                                
***********************************************************************         
* Validate and process day for 0 or 1                                           
***********************************************************************         
USAVDAY  BAS   R8,VDAY             VALIDATE FIRST DAY BYTE                      
         CLI   0(R1),0             TEST IF THIRD FIELD REQUIRED                 
         BNE   DVXIT                                                            
         BAS   R8,DLMCHK           POSITION TO THIRD FIELD (YEAR)               
***********************************************************************         
* Validate and process year  0 or 2                                             
***********************************************************************         
USAVYEAR EQU   *                   R2=A(FIRST YEAR BYTE)                        
         BAS   RE,DVYEAR           VALIDATE YY OR YYYY                          
         MVC   0(2,R3),0(R2)       MOVE VALID YEAR TO OUTPUT                    
         BAS   R9,CVYEAR           TEST IF CALLER WANTS FUNNY YEAR              
         LA    R2,2(R2)            BUMP PAST YEAR BYTES                         
         CLI   0(R1),2             TEST IF M/Y REQUESTED                        
         BNE   DVXIT                                                            
         CLI   0(R2),C'/'          TEST IF LOOKS LIKE M/D/Y INPUT               
         BNE   DVXIT                                                            
         B     ERROR               BETTER SAFE THAN SORRY                       
                                                                                
***********************************************************************         
* NOW VALIDATE YYMMDD EXTRACTED FROM INPUT                            *         
***********************************************************************         
DVXIT    PACK  DUB,2(2,R3)         CONVERT MONTH TO BINARY                      
         CVB   R4,DUB                                                           
         CHI   R4,12               CHECK 1 THRU 12                              
         BH    ERROR                                                            
         LTR   R4,R4                                                            
         BZ    ERROR                                                            
         AHI   R4,-1               INDEX INTO MAX DAYS IN MONTH TABLE           
         SLL   R4,1                                                             
         LA    R4,MAX(R4)                                                       
         CLC   4(2,R3),0(R4)                                                    
         BH    ERROR               DAY EXCEEDS MAX IN MONTH                     
         CLC   4(2,R3),ZEROS                                                    
         BNE   DVXIT10             VALIDATE LEAP YEAR                           
         CLI   0(R1),2                                                          
         BNE   ERROR               ERROR IF YEAR MISSING                        
*                                                                               
DVXIT10  CLC   2(4,R3),=C'0229'    TEST IF FEB 29 TH                            
         BNE   DVXIT20                                                          
         OC    YEAR,YEAR           WAS A YEAR INPUT                             
         BZ    DVXIT20                                                          
         PACK  DUB,YEAR            YES-TEST IF LEAP YEAR                        
         CVB   R4,DUB                                                           
         SLL   R4,30                                                            
         LTR   R4,R4                                                            
         BNZ   ERROR               ONLY VALID IF LEAP YEAR                      
*                                                                               
DVXIT20  LA    R3,INDATE           R3 POINTS TO INPUT PARAM                     
         SR    R2,R3               R2 GETS LENGTH OF INPUT                      
         ST    R2,0(R1)            RETURN IT INTO INPUT PARAM                   
         B     EXIT                                                             
*                                                                               
ERROR    MVC   0(6,R3),ZEROS       MOVE ZEROES TO OUTPUT                        
         XC    0(4,R1),0(R1)       ZEROISE INPUT                                
*                                                                               
EXIT     MVC   4(1,R1),RETCODE     RETURN DATE INPUT FLAGS                      
         XMOD1 1                                                                
                                                                                
***********************************************************************         
* Check delimiter and bump R2 accordingly                                       
***********************************************************************         
DLMCHK   DS    0H                                                               
*&&US                                                                           
DLMCHKUS CLI   0(R2),C' '          POSITION TO SECOND FIELD                     
         BE    DLM20                                                            
         CLI   0(R2),C'/'                                                       
         BE    DLM20                                                            
         BR    R8                                                               
*&&                                                                             
*&&UK                                                                           
DLMCHKUK CLI   0(R2),C' '          LOCATE START OF SECOND FIELD                 
         BNE   DLM10                                                            
         LA    R2,1(R2)                                                         
DLM10    CLI   0(R2),C'/'          TEST IF VALID DELIMITER                      
         BE    DLM12                                                            
         CLI   0(R2),C'.'                                                       
         BE    DLM12                                                            
         CLI   0(R2),C'-'                                                       
         BNE   DLM15                                                            
DLM12    LA    R2,1(R2)                                                         
DLM15    CLI   0(R2),C' '          BYPASS SPACES                                
         BNER  R8                                                               
*&&                                                                             
DLM20    LA    R2,1(R2)                                                         
         BR    R8                                                               
                                                                                
***********************************************************************         
* Validate day number                                                           
* R2 is input 1 or 2 character                                                  
* Exit on error, no return                                                      
***********************************************************************         
VDAY     BAS   R9,DVNUM            VALIDATE DAY BYTE 1                          
         BNE   ERROR                                                            
         OI    VALFLAG,VALDAY                                                   
         LA    R2,1(R2)                                                         
         BAS   R9,DVNUM            VALIDATE DAY BYTE 2                          
         BCTR  R2,0                                                             
         BE    VDAY10                                                           
         MVC   5(1,R3),0(R2)       MOVE 1ST DIGIT ONLY                          
         LA    R2,1(R2)            BUMP PAST FIRST FIELD                        
         BR    R8                                                               
*                                                                               
VDAY10   MVC   4(2,R3),0(R2)       MOVE DAY DIGITS TO OUTPUT                    
         LA    R2,2(R2)            BUMP PAST FIRST FIELD                        
         BR    R8                                                               
                                                                                
***********************************************************************         
* RETURN DAY OF THE WEEK NUMBER: INPUT MON - SUN, OUTPUT 1 - 7        *         
* R2 points to day text                                                         
* R3 point to output area                                                       
***********************************************************************         
DAYOWEEK LLC   R6,LANG             INDEX INTO DAY TABLE FOR LANGUAGE            
         MH    R6,DAYTABL                                                       
         LA    R6,DAYTAB(R6)                                                    
         LA    R4,1                SET DAY COUNTER                              
         LA    R5,7                SET LOOP COUNTER                             
DAYOFW10 CLC   0(3,R2),0(R6)       COMPARE 3 BYTES                              
         BE    DAYOFW20                                                         
         LA    R4,1(R4)                                                         
         LA    R6,3(R6)                                                         
         BCT   R5,DAYOFW10                                                      
*                                                                               
         CLI   LANG,3              ERROR IF LANGUAGE IS ENGLISH                 
         BL    ERROR                                                            
         MVI   LANG,0              SET LANGUAGE TO ENGLISH                      
         B     DAYOWEEK            AND TRY AGAIN                                
*                                                                               
DAYOFW20 CVD   R4,DUB              CONVERT DAY NUM                              
         UNPK  0(1,R3),DUB                                                      
         OI    0(R3),X'F0'                                                      
         OI    VALFLAG,VALDAY                                                   
         B     EXIT                                                             
                                                                                
***********************************************************************         
* Extract numeric month value                                                   
* R8 = return address to caller                                                 
* R3 = output area                                                              
***********************************************************************         
GETNMON  BAS   R9,DVNUM            VALIDATE MONTH DIGIT 1                       
         BNER  R8                  Return NE and TRY ALPHA MONTH                
         LA    R2,1(R2)                                                         
         BAS   R9,DVNUM            VALIDATE MONTH DIGIT 2                       
         BCTR  R2,0                                                             
         BE    GNMON10             Looks good so far                            
         MVC   3(1,R3),0(R2)       ALLOW SINGLE DIGIT MONTH                     
         LA    R2,1(R2)            BUMP PAST MONTH                              
         B     GNMON20                                                          
*                                                                               
GNMON10  MVC   2(2,R3),0(R2)       2 BYTE MONTH FIELD                           
         LA    R2,2(R2)            BUMP PAST MONTH                              
*                                                                               
GNMON20  OI    VALFLAG,VALMONTH    Have valid month                             
         SR    R0,R0               Set CC equal                                 
         BR    R8                                                               
                                                                                
***********************************************************************         
* Extract character month value                                                 
* DUB set with 3 character value                                                
* R8 = return address to caller                                                 
* R3 = output area                                                              
***********************************************************************         
GETCMON  LLC   R6,LANG             GET LANG NUM                                 
         MH    R6,MONTABL                                                       
         LA    R6,MONTAB(R6)       POINT TO START OF MONTHS                     
         LA    R4,1                MONTH NUMBER                                 
         LA    R5,12               LOOP COUNTER                                 
*                                                                               
GCMON20  CLC   DUB(3),0(R6)        INPUT MMM = TABLE ELEMENT ?                  
         BE    GCMON30             YES-SKIP TO MMM CONVERT                      
         LA    R4,1(R4)            Count up month number                        
         LA    R6,3(R6)            Next month entry                             
         BCT   R5,GCMON20                                                       
*                                                                               
         CLI   LANG,3              ERROR IF LANGUAGE IS ENGLISH                 
         BL    ERROR               Jump out to error code                       
         XR    R6,R6                                                            
         B     GETCMON             AND TRY AGAIN                                
*                                                                               
GCMON30  CVD   R4,DUB                                                           
         UNPK  2(2,R3),DUB         UNPACK MONTH NUM                             
         OI    3(R3),X'F0'                                                      
         LA    R2,3(R2)            POINT TO YEAR OR DELIMITER                   
         OI    VALFLAG,VALMONTH    Have valid month                             
         SR    R0,R0                                                            
         BR    R8                                                               
                                                                                
***********************************************************************         
* ISO STYLE DATE VALIDATION - 0=YYYYMMDD,2=YYYYMM                     *         
***********************************************************************         
ISOV     MVI   CENTURY,19          TEST DATE RANGE                              
         CLC   INISO(2),=C'19'                                                  
         BNE   ISOV0               YES, SO USE CENTURY 20                       
         OI    RETCODE,RET19YY     DATE IS 19yy                                 
         B     ISOV1                                                            
*                                                                               
ISOV0    MVI   CENTURY,20          DATE IS 20yy                                 
         OI    RETCODE,RET20YY                                                  
*                                                                               
ISOV1    CLC   INISO(4),MINYR                                                   
         BL    ERROR                                                            
         CLC   INISO(4),MAXYR                                                   
         BH    ERROR                                                            
*                                                                               
         MVC   YEAR,INISO+2                                                     
         MVC   0(6,R3),INISO+2     SET OUTPUT DATE                              
         BAS   R9,CVYEAR           TEST IF CALLER WANTS FUNNY YEAR              
         B     DVXIT               VALIDATE DATE                                
         EJECT                                                                  
***********************************************************************         
* TEST IF LOOKS LIKE ISO DATE YYYYMMDD OR YYYY-MM-DD                  *         
***********************************************************************         
DVISO    LA    R0,1                SET NOT AN ISO DATE                          
         MVC   DUB(4),ZEROS        FIRST FOUR DIGITS MUST BE NUMERIC            
         MVZ   DUB(4),0(R2)                                                     
         CLC   DUB(4),ZEROS                                                     
         BNE   DVISOX                                                           
         MVC   INISO(4),0(R2)      SET ISO YYYY                                 
         LA    RE,4(R2)                                                         
         CLI   0(RE),C'-'                                                       
         BE    DVISO1                                                           
         CLI   0(RE),C'.'                                                       
         BE    DVISO1                                                           
         CLI   0(RE),C'/'                                                       
         BNE   DVISO2                                                           
DVISO1   LA    RE,1(RE)                                                         
*                                                                               
DVISO2   MVC   INISO+4(2),0(RE)    SET ISO MM                                   
         MVC   DUB(2),ZEROS                                                     
         MVZ   DUB(2),INISO+4                                                   
         CLC   DUB(2),ZEROS                                                     
         BNE   DVISOX                                                           
         LA    RE,2(RE)                                                         
DVISO3   CLI   0(R1),2             TEST IF YYYY-MM ONLY                         
         BNE   DVISO4                                                           
         MVC   INISO+6(2),ZEROS                                                 
         CLI   0(RE),C' '                                                       
         BH    DVISOX                                                           
         CLC   INISO+0(2),=C'12'   MMYYYY VRS YYYYMM                            
         BNH   DVISOX                                                           
         XR    R0,R0               SET DATE IS IN ISO FORMAT                    
         LR    R2,RE               POINT TO END OF ISO DATE                     
         B     DVISOX                                                           
*                                                                               
DVISO4   CLI   0(RE),C'-'          WANT DD AS WELL                              
         BE    DVISO5                                                           
         CLI   0(RE),C'.'                                                       
         BE    DVISO5                                                           
         CLI   0(RE),C'/'                                                       
         BNE   DVISO6                                                           
DVISO5   LA    RE,1(RE)                                                         
*                                                                               
DVISO6   MVC   INISO+6(2),0(RE)    SET ISO DD                                   
         MVC   DUB(2),ZEROS                                                     
         MVZ   DUB(2),INISO+6                                                   
         CLC   DUB(2),ZEROS                                                     
         BNE   DVISOX                                                           
         LA    RE,2(RE)                                                         
*                                                                               
DVISO7   EQU   *                                                                
*&&UK*&& CLC   INISO+4(2),=C'12'   UK DDMMYYYY VRS YYYYMMDD                     
*&&UK*&& BH    DVISOX                                                           
*&&US*&& CLC   INISO+0(2),=C'12'   US MMDDYYYY VRS YYYYMMDD                     
*&&US*&& BNH   DVISOX                                                           
         XR    R0,R0               SET DATE IS IN ISO FORMAT                    
         LR    R2,RE               POINT TO END OF ISO DATE                     
*                                                                               
DVISOX   LTR   R0,R0               EXIT WITH CC EQL IF ISO DATE                 
         BR    R9                                                               
         EJECT                                                                  
***********************************************************************         
* VALIDATE YEAR YY OR YYYY. IF YY GE 00 & LE 59 ASSUME 2000 THRU 2059 *         
* Warning - do not use RE, used to branch back to caller                        
***********************************************************************         
DVYEAR   MVI   CENTURY,19          DEFAULT IS 19TH CENTURY                      
         CLI   1(R2),C' '          IS THIS A SINGLE DIGIT YEAR?                 
         BH    DVYR002                                                          
                                                                                
***********************************************************************         
* Warning this is modifying the input field                                     
***********************************************************************         
         MVC   1(1,R2),0(R2)       YES-SET Y TO '0Y '                           
         MVI   0(R2),C'0'                                                       
         MVI   2(R2),C' '                                                       
*                                                                               
DVYR002  BAS   R9,DVNUM            VALIDATE YEAR DIGIT 1                        
         BNE   ERROR                                                            
         LA    R2,1(R2)                                                         
         BAS   R9,DVNUM            VALIDATE YEAR DIGIT 2                        
         BNE   ERROR                                                            
         AHI   R2,-1                                                            
                                                                                
**********************************************************************          
* Process YY only type                                                          
* old values 1964 - 2027    check 1964 - 2027                                   
* new values 1964 - 2059    check 2000 - 2059 the 1964 - 1999                   
**********************************************************************          
         TM    4(R1),OPT2YR        Test if 2 digit year only                    
         BZ    DVYR010             No                                           
*VYR004  TM    4(R1),OPTN6459      New values                                   
*        BZ    DVYR005             No, so all years valid                       
DVYR004  CLC   0(2,R2),MAXYR+2     Lower   than max year 2027 or 2059           
         BNH   DVYR008             Yes                                          
         CLC   0(2,R2),MINYR+2     Greater than min year 1964 or 1959           
         BL    ERROR               27-63 not valid or 59-63 not valid           
         B     DVYR009                                                          
*&&DO                                                                           
DVYR005  CLC   0(2,R2),MAXYR+2                                                  
         BH    DVYR009             1928-1999                                    
*&&                                                                             
DVYR008  MVI   CENTURY,20          2000-2027 or 2000-2059                       
*                                                                               
DVYR009  OI    VALFLAG,VAL2YEAR    This is a two character year                 
         B     DVYRXIT                                                          
                                                                                
**********************************************************************          
* Process possible 4 digit                                                      
**********************************************************************          
DVYR010  CLC   0(2,R2),MINYR       Might be 4 digits 19yy                       
         BE    DVYR012                                                          
         CLC   0(2,R2),MAXYR       Might be 4 digits 20yy                       
         BNE   DVYR004             No, so try 2 char yy                         
*                                                                               
DVYR012  CLI   2(R2),C'0'          TEST IF 19/20 FOLLOWED BY NUMBERS            
         BL    DVYR014                                                          
         CLI   2(R2),C'9'                                                       
         BH    DVYR014                                                          
         CLI   3(R2),C'0'          MUST HAVE A VALID PAIR OF NUMBERS            
         BL    DVYR014                                                          
         CLI   3(R2),C'9'                                                       
         BNH   DVYR020             Possible 4 byte year                         
*                                                                               
DVYR014  MVI   CENTURY,20          POINT TO FIRST PAIR OF DIGITS                
         OI    VALFLAG,VAL2YEAR    This is a two character year                 
         B     DVYRXIT                                                          
*                                                                               
DVYR020  CLC   0(2,R2),MINYR       VALIDATE 19nn INPUT YEAR                     
         BNE   DVYR030                                                          
         CLC   0(4,R2),MINYR       LOWER THAN THE MINIMUM YEAR                  
         BL    ERROR                                                            
         MVI   BYTE,RET19YY        Save as return code                          
         MVI   CENTURY,19          SET 19xx INPUT                               
         B     DVYR032                                                          
*                                                                               
DVYR030  CLC   0(4,R2),MAXYR       VALIDATE 20nn INPUT YEAR                     
         BH    ERROR                                                            
         MVI   CENTURY,20          Set 20xx input                               
         MVI   BYTE,RET20YY        Save as return code                          
*                                                                               
DVYR032  CLI   4(R2),C' '          TEST VALID DELIMITER                         
         BNH   DVYR038             best guess at valid data                     
*&&DO                                                                           
* Why would be care if it supposed to be 4 byte year?                           
         CLI   4(R2),C'0'                                                       
         BL    DVYR038                                                          
         CLI   4(R2),C'9'                                                       
         BH    DVYR038                                                          
         OI    RETCODE,RETAMBIG    Ambiguous input                              
         B     ERROR                                                            
*        CLI   4(R2),C','                                                       
*        BE    DVYR038                                                          
*        CLI   4(R2),C':'                                                       
*        BE    DVYR038                                                          
*        CLI   4(R2),C';'                                                       
*        BE    DVYR038                                                          
*        CLI   4(R2),C'#'                                                       
*        BNE   DVYRXIT                                                          
*                                                                               
*&&                                                                             
DVYR038  OI    VALFLAG,VALYEAR     4 character year                             
         OC    RETCODE,BYTE                                                     
         LA    R2,2(,R2)           return to caller pointing at year            
*                                                                               
DVYRXIT  MVC   YEAR,0(R2)          SAVE C'YY' AS INPUT                          
         BR    RE                  EXIT WITH R2 POINTING TO YEAR DIGITS         
         EJECT                                                                  
***********************************************************************         
* NUMERIC TEST SUBROUTINE                                             *         
***********************************************************************         
DVNUM    CLI   0(R2),C'9'          TEST IF A NUMERIC CHR                        
         BH    DVNO                                                             
         CLI   0(R2),C'0'                                                       
         BL    DVNO                                                             
DVYES    MVI   NUM,C'Y'            SET VALID NUMERIC AND CC EQL                 
         B     *+8                                                              
DVNO     MVI   NUM,C'N'            SET NOT NUMERIC AND CC NEQ                   
         CLI   NUM,C'Y'                                                         
         BR    R9                                                               
                                                                                
***********************************************************************         
* CONVERT 1ST YEAR BYTE TO X'FA' THRU X'FF' TO SHOW WHICH DECADE      *         
* OF 20TH CENTURY. X'FA' SHOWS DECADE 2000 THRU 2009.                 *         
***********************************************************************         
CVYEAR   CLI   CENTURY,20          RETURN 20TH CENTURY FLAG                     
         BNER  R9                                                               
         OI    RETCODE,RET20C      20 Century date                              
         TM    RETCODE,OPT19C      User doesn't want new date format            
         BOR   R9                                                               
         LLC   R4,0(R3)            CONVERT 1ST YEAR BYTE                        
         LA    R4,10(R4)                                                        
         STC   R4,0(R3)                                                         
         BR    R9                                                               
         EJECT                                                                  
***********************************************************************         
* CONSTANTS AND LITERALS PLUS WORKING STORAGE DSECT                   *         
***********************************************************************         
ZEROS    DC    8C'0'                                                            
*                                                                               
* DATVAL WORKS OLD WAY UNLESS YOU TURN ON THE X'20'                             
* =================================================                             
*INYRO   DC    C'1928'             LAST CENTURY MIN YEAR                        
MINYRO   DC    C'1964'             LAST CENTURY MIN YEAR                        
MAXYRO   DC    C'2027'             THIS CENTURY MAX YEAR                        
*                                                                               
* DATVAL WORKS WITH NEW DATE RANGE IF YOU TURN ON THE X'20'                     
* =========================================================                     
MINYRN   DC    C'1964'             LAST CENTURY MIN YEAR                        
MAXYRN   DC    C'2059'             THIS CENTURY MAX YEAR                        
*                                                                               
MAX      DC    C'312931303130313130313031'                                      
*                                                                               
MONTABL  DC    AL2(EUKMON-MONTAB)                                               
MONTAB   DC    C'JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC'  00                      
EUKMON   DC    C'JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC'  01                      
EUSMON   DC    C'JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC'  02                      
GERMON   DC    C'JANFEBMRZAPRMAIJUNJULAUGSEPOKTNOVDEZ'  03                      
FREMON   DC    C'JANFEVMARAVRMAIJUNJULAOUSEPOCTNOVDEC'  04                      
SPAMON   DC    C'ENEFEBMARABRMAYJUNJULAGOSEPOCTNOVDIC'  05                      
ITAMON   DC    C'GENFEBMARAPRMAGJUNLUGAGOSETOTTNOVDIC'  06                      
DUTMON   DC    C'JANFEBMAAAPRMEIJUNJULAUGSEPOKTNOVDEC'  07                      
*                                                                               
DAYTABL  DC    AL2(EUKDAY-DAYTAB)                                               
DAYTAB   DC    C'MONTUEWEDTHUFRISATSUN'  00                                     
EUKDAY   DC    C'MONTUEWEDTHUFRISATSUN'  01                                     
EUSDAY   DC    C'MONTUEWEDTHUFRISATSUN'  02                                     
GERDAY   DC    C'MONDIEMITDONFRESAMSON'  03                                     
FREDAY   DC    C'LUNMARMERJEUVENSAMDIM'  04                                     
SPADAY   DC    C'LUNMARMIEJUEVIESABDOM'  05                                     
ITADAY   DC    C'LUNMARMERGIOVENSABDOM'  06                                     
DUTDAY   DC    C'MAADINWOEDONVRIZATZON'  07                                     
*                                                                               
DEFLANG  DS    0C                                                               
*&&UK*&& DC    AL1(1)                                                           
*&&US*&& DC    AL1(2)                                                           
SPACES   DC    3C' '                                                            
*                                                                               
         LTORG                                                                  
                                                                                
DATD     DSECT                                                                  
DUB      DS    D                                                                
SAVER1   DS    F                                                                
HALF     DS    H                                                                
CTRYLANG DS    0H                                                               
CTRY     DS    X                                                                
LANG     DS    X                                                                
BYTE     DS    X                                                                
NUM      DS    C                                                                
CENTURY  DS    X                                                                
VALFLAG  DS    X                                                                
VALMONTH EQU   X'08'                                                            
VALDAY   EQU   X'04'                                                            
VALYEAR  EQU   X'02'                                                            
VAL2YEAR EQU   X'03'                                                            
*                                                                               
OPTIN    DS    X                                                                
OPT19C   EQU   X'80'    Don't return DDS 20 century style dates                 
OPT2YR   EQU   X'40'    2 character year input                                  
OPTN6459 EQU   X'20'    What new range of dates                                 
*                                                                               
MINYR    DS    C'0000'             PUT IN CSECT AREA FOR DEBUGGING.             
MAXYR    DS    C'0000'                                                          
*                                                                               
YEAR     DS    CL2                                                              
RETCODE  DS    X                                                                
RET19C   EQU   X'80'    User didn't want DDS 20 century style dates             
RET20C   EQU   X'40'    Date is 20th century                                    
RET20YY  EQU   X'20'    20nn input date                                         
RET19YY  EQU   X'10'    19nn input date                                         
RETAMBIG EQU   X'08'    Input ambiguous                                         
         DS    X                                                                
*                                                                               
INDATE   DS    CL12                                                             
INDATEX  DS    C                                                                
INISO    DS    CL8                                                              
DATX     EQU   *                                                                
                                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'004DDDATVAL  07/17/19'                                      
         END                                                                    
