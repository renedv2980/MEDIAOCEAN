*          DATA SET PPWRKDAY   AT LEVEL 013 AS OF 02/07/05                      
*CATALP PPWRKDAY                                                                
         TITLE 'PPWRKDAY - ADJUST NON WORK-DAY DATE TO WORK-DAY DATE'           
* INPUT                                                                         
* PARAM1  BYTE  0        C'C' IF CANADIAN, ELSE 0                               
*         BYTES 1-3      A(INPUT DATE - BIN YMD)                                
* PARAM2  BYTE  0                                                               
*         BYTES 1-3      A(OUTPUT AREA - BIN YMD DATE)                          
* PARAM3  BYTE  0                                                               
*         BYTES 1-3      A(COMFACS)                                             
*                                                                               
*                                                                               
* THIS PROGRAM ACCEPTS A DATE AND RETURNS THAT DATE IF IT IS A WORK DAY         
* OR RETURNS THE LAST WORK DAY BEFORE THE DATE. NON-WORK DAYS ARE               
* SATURDAY AND SUNDAY AND MAJOR HOLIDAYS.                                       
*                                                                               
*                                                                               
         EJECT                                                                  
PPWRKDAY CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,PPWRKDAY,CLEAR=YES                                   
         USING WORKD,RC                                                         
*                                                                               
         LM    R2,R4,0(R1)       R2=A(INPUT) R3=A(OUTPUT) R4=A(COMFACS)         
         MVC   BINDATE,0(R2)       EXTRACT INPUT DATE                           
         MVC   OPT1,0(R1)          SAVE 1ST BYTE OF PLIST                       
*                                  R4=> COMFACS                                 
         MVC   VGETDAY,CGETDAY-COMFACSD(R4)    GET GETDAY ADDR                  
         MVC   VADDAY,CADDAY-COMFACSD(R4)        AND ADDAY ADDR                 
         MVC   VDATCON,CDATCON-COMFACSD(R4)      AND DATCON ADDR                
*                                                                               
         GOTOR VDATCON,DMCB,(3,BINDATE),(0,DATE),0    BIN YMD TO                
*                                                   EBCDIC YYMMDD               
TESTLUP  DS    0H                  GET DAY OF WEEK                              
         GOTOR VGETDAY,DMCB,(0,DATE),WORK3                                      
*                                                                               
         CLI   0(R1),7             SUNDAY ?                                     
         BE    BACK2                                                            
         CLI   0(R1),6             SATURDAY ?                                   
         BE    BACK1                                                            
         CLC   DATE+2(4),=C'0101'  NEW YEAR ?                                   
         BE    BACK1                                                            
         CLC   DATE+2(4),=C'1225'  CHRISTMAS ?                                  
         BE    BACK1                                                            
         CLI   0(R1),1             MONDAY ?                                     
         BNE   PART1X              NO                                           
         CLC   DATE+2(4),=C'0901'  LABOR DAY ?                                  
         BL    PART1X              NO                                           
         CLC   DATE+2(4),=C'0907'  LABOR DAY ?                                  
         BNH   BACK3               YES                                          
PART1X   DS    0H                                                               
PART2    CLI   OPT1,C'C'           CANADIAN ?                                   
         BNE   PART2X              NO                                           
         CLI   0(R1),1             MONDAY ?                                     
         BNE   PART2D              NO                                           
         CLC   DATE+2(4),=C'1008'  CANADIAN THANKSGIVING ?                      
         BL    PART2D              NO                                           
         CLC   DATE+2(4),=C'1014'  CANADIAN THANKSGIVING ?                      
         BNH   BACK3               YES                                          
PART2D   CLC   DATE+2(4),=C'0701'  CANADA DAY ?                                 
         BE    BACK1                                                            
         CLC   DATE+2(4),=C'1226'  CHRISTMAS ?                                  
         BE    BACK2                                                            
         B     DONE                CANADIAN TESTING IS DONE                     
PART2X   DS    0H                                                               
PART3    CLC   DATE+2(4),=C'0704'  INDEPENDENCE DAY ?                           
         BE    BACK1                                                            
         CLI   0(R1),1             MONDAY ?                                     
         BNE   PART3X              NO                                           
         CLC   DATE+2(4),=C'0525'  MEMORIAL DAY ?                               
         BL    DONE                NO                                           
         CLC   DATE+2(4),=C'0531'  MEMORIAL DAY ?                               
         BNH   BACK3               YES                                          
*****    B     DONE                US DATES ARE DONE                            
PART3X   DS    0H                                                               
PART4    CLI   0(R1),4             THURSDAY ?                                   
         BNE   DONE                NO                                           
         CLC   DATE+2(4),=C'1122'  THANKSGIVING ?                               
         BL    DONE                NO                                           
         CLC   DATE+2(4),=C'1128'  THANKSGIVING ?                               
         BNH   BACK1               YES                                          
*****    B     DONE                US DATES ARE DONE                            
*                                                                               
DONE     DS    0H            CONVERT EBCDIC YYMMDD DATE TO BIN YMD DATE         
*                            AND OUTPUT TO PARAM2 ADDRESSED BY R3               
         GOTOR VDATCON,DMCB,(0,DATE),(3,0(R3)),0                                
         XMOD1 1                                                                
*                                                                               
BACK1    LHI   R6,-1               SET 1 DAY  "PRIOR"                           
         B     BACKDAY                                                          
BACK2    LHI   R6,-2               SET 2 DAYS "PRIOR"                           
         B     BACKDAY                                                          
BACK3    LHI   R6,-3               SET 3 DAYS "PRIOR"                           
*****    B     BACKDAY                                                          
BACKDAY  DS    0H                                                               
         GOTOR VADDAY,DMCB,DATE,DATE,(R6)                                       
         B     TESTLUP             BACK TO TEST CHANGED DATE                    
*                                                                               
         LTORG                                                                  
         SPACE 3                                                                
WORKD    DSECT                                                                  
VGETDAY  DS    A                                                                
VADDAY   DS    A                                                                
VDATCON  DS    A                                                                
DMCB     DS    6F                                                               
DATE     DS    CL6                 EBCDIC YYMMDD                                
BINDATE  DS    XL3                 BIN YMD                                      
OPT1     DS    X                                                                
WORK3    DS    CL3                                                              
WORKX    EQU   *                                                                
         EJECT                                                                  
       ++INCLUDE DDCOMFACS                                                      
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'013PPWRKDAY  02/07/05'                                      
         END                                                                    
