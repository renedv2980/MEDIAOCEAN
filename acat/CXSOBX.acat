*          DATA SET CXSOBX     AT LEVEL 010 AS OF 05/01/02                      
*CATALP CXSOBX                                                                  
         TITLE 'CXSOBX - CONTROL SECURITY - OCON USERID/GROUP RECORDS'          
**********************************************************************          
* AL4 A(EXTRACT RECORD)                                              *          
* AL4 A(SQL BUFFER)                                                  *          
**********************************************************************          
         SPACE 1                                                                
         PRINT NOGEN                                                            
CXSOBX   CSECT                                                                  
         NMOD1 WORKX-WORKD,*CXSOBX*,RR=RA                                       
         USING WORKD,RC                                                         
         ST    R1,APARM                                                         
         ST    RA,RELO                                                          
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         XC    0(4,R3),0(R3)                                                    
         LA    R3,4(R3)            BUMP PAST LENGTH IN BUFFER                   
         USING CXSOBD,R2           R2=A(EXTRACT RECORD)                         
         MVI   SEPARATE,DEFSEP     SET DEFAULT FIELD SEPARATOR CHAR             
         MVI   XFLAG,0             INITIALISE CONVERT INPUT FLAG                
         CLI   CXSOBACT,C'L'       TEST IF IN LOAD MODE                         
         BNE   *+12                                                             
         MVI   SEPARATE,LOADSEP    SET LOAD MODE FIELD SEPARATOR CHAR           
         OI    XFLAG,LOADMODE      SET CONVERT FLAG TO LOAD MODE                
*                                                                               
OCTYP    NI    XFLAG,REQUIRED      CONVERT RECORD TYPE - REQUIRED               
         GOTO1 XMXCNVX,DMCB,(L'CXSOBTYP,CXSOBTYP),(XFLAG,(R3)),(1,CHAR)         
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
OCACT    NI    XFLAG,REQUIRED      CONVERT RECORD ACTION - REQUIRED             
         GOTO1 XMXCNVX,DMCB,(L'CXSOBACT,CXSOBACT),(XFLAG,(R3)),(1,CHAR)         
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
OCDTM    NI    XFLAG,REQUIRED      CONVERT ACTION DATE TIME - REQUIRED          
         LA    R0,L'CXSOBDAT+L'CXSOBTIM+1                                       
         GOTO1 XMXCNVX,DMCB,((R0),CXSOBDAT),(XFLAG,(R3)),(1,DTIM)               
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
OCAID    NI    XFLAG,REQUIRED      AGENCY ALPHA ID - REQUIRED                   
         GOTO1 XMXCNVX,DMCB,(L'CXSOBAID,CXSOBAID),(XFLAG,(R3)),(1,CHAR)         
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
OCUIN    NI    XFLAG,REQUIRED      USER ID NUMBER - REQUIRED                    
         GOTO1 XMXCNVX,DMCB,(L'CXSOBUIN,CXSOBUIN),(XFLAG,(R3)),(1,INT)          
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
OCAGC    NI    XFLAG,REQUIRED      ACCESS GROUP CODE - REQUIRED                 
         GOTO1 XMXCNVX,DMCB,(L'CXSOBAGC,CXSOBAGC),(XFLAG,(R3)),(1,CHAR)         
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
OCSYS    NI    XFLAG,REQUIRED      SYSTEM CODE - REQUIRED                       
         GOTO1 XMXCNVX,DMCB,(L'CXSOBSYS,CXSOBSYS),(XFLAG,(R3)),(1,CHAR)         
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
OCPGM    NI    XFLAG,REQUIRED      PROGRAM CODE - REQUIRED                      
         GOTO1 XMXCNVX,DMCB,(L'CXSOBPGM,CXSOBPGM),(XFLAG,(R3)),(1,CHAR)         
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
OCOPT    NI    XFLAG,REQUIRED      OPTION CODE - REQUIRED                       
         CLI   CXSOBACT,C'K'       TEST IF SPECIAL DELETE KEY ACTION            
         BNE   *+8                                                              
         OI    XFLAG,OPTIONAL      SET TO OPTIONAL TO OUTPUT NULL VALUE         
         GOTO1 XMXCNVX,DMCB,(L'CXSOBOPT,CXSOBOPT),(XFLAG,(R3)),(1,CHAR)         
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
OCCOD    NI    XFLAG,REQUIRED      CONTROL CODE - REQUIRED                      
         CLI   CXSOBACT,C'K'       TEST IF SPECIAL DELETE KEY ACTION            
         BNE   *+8                                                              
         OI    XFLAG,OPTIONAL      SET TO OPTIONAL TO OUTPUT NULL VALUE         
         GOTO1 XMXCNVX,DMCB,(L'CXSOBCOD,CXSOBCOD),(XFLAG,(R3)),(1,CHAR)         
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
DXBUFLEN L     RF,ASQLBUFF         STORE BUFFER DATA LENGTH                     
         SR    R3,RF               IN TOP 2 BYTES                               
         SLL   R3,16                                                            
         STCM  R3,15,0(RF)                                                      
         B     EXIT                                                             
*                                                                               
EXIT     XMOD1 1                                                                
*                                                                               
PUTSEP   SR    RF,RF               PUT OUT SEPARATOR CHARACTER                  
         ICM   RF,1,4(R1)          GET RETURN LENGTH                            
         LA    R3,0(RF,R3)         BUMP DATA BUFFER POINTER                     
         MVC   0(1,R3),SEPARATE                                                 
         LA    R3,1(R3)                                                         
         BR    RE                                                               
         EJECT                                                                  
         LTORG                                                                  
         SPACE 1                                                                
XMXCNVX  DC    V(MXCNVX)                                                        
         SPACE 1                                                                
CHAR     DC    CL8'CHAR    '                                                    
DTIM     DC    CL8'DATETIME'                                                    
TINY     DC    CL8'TINYINT '                                                    
SMAL     DC    CL8'SMALLINT'                                                    
INT      DC    CL8'INT     '                                                    
REAL     DC    CL8'REAL    '                                                    
FLOT     DC    CL8'FLOAT   '                                                    
BIT      DC    CL8'BIT     '                                                    
MONY     DC    CL8'MONEY   '                                                    
         EJECT                                                                  
WORKD    DSECT                                                                  
PARM     DS    0XL16               PARAMETER LIST                               
AXREC    DS    A                   A(EXTRACT RECORD)                            
ASQLBUFF DS    A                   A(SQL BUFFER)                                
         DS    A                                                                
         DS    A                                                                
*                                                                               
DUB      DS    D                                                                
APARM    DS    A                   A(PARAMETER LIST)                            
RELO     DS    A                                                                
DMCB     DS    6F                                                               
SEPARATE DS    CL1                 FIELD SEPARATOR CHARACTER                    
DEFSEP   EQU   C';'                                                             
LOADSEP  EQU   C';'                                                             
XFLAG    DS    XL1                 CONVERT INPUT CONTROL FLAGS                  
LOADMODE EQU   X'80'                 LODE MODE                                  
OPTIONAL EQU   X'40'                 OPTIONAL FIELD                             
REQUIRED EQU   X'FF'-X'40'           REQUIRED FIELD                             
*                                                                               
WORKX    EQU   *                                                                
         EJECT                                                                  
       ++INCLUDE CXSOBD                                                         
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'010CXSOBX    05/01/02'                                      
         END                                                                    
