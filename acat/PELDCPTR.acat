*          DATA SET PELDCPTR   AT LEVEL 007 AS OF 05/06/85                      
*CATALP PELDCPTR                                                                
         TITLE 'LDCPTR - PERSON - CREATE DIRECTORY POINTERS'                    
         PRINT NOGEN                                                            
*        PARA1 A(RECORD)                                                        
*        PARA2 A(AREA TO CREATE PASSIVE POINTERS)                               
*              LAST POINTER FOLLOWED BY X'00'                                   
*                                                                               
LDCPTR   CSECT                                                                  
         NMOD1 WRKX-WRKD,PELDCPTR                                               
         USING WRKD,RC                                                          
         LM    R2,R3,0(R1)         R2=A(PRIME DATA RECORD)                      
         MVC   0(36,R3),0(R2)      SET ONE ACTIVE POINTER                       
         MVC   36(2,R3),38(R2)     STATUS FROM PARENT                           
         XC    38(5,R3),38(R3)                                                  
         LA    R3,42(R3)           R3=A(NEXT PASSIVE POINTER RECORD)            
         SR    RF,RF                                                            
         LA    RE,44(R2)           RE=A(NEXT RECORD ELEMENT)                    
         SPACE 2                                                                
LDPAS1   CLI   0(R2),X'01'         TEST AGENCY CODE                             
         BL    EXIT                                                             
         CLI   0(R2),X'FE'                                                      
         BH    EXIT                                                             
         CLI   1(R2),X'01'         TEST LIST NUMBER                             
         BL    LDPASA                                                           
         CLI   1(R2),X'C0'         01 TO C0 ARE MAILING LISTS                   
         BNH   LDCP1                                                            
         SPACE 2                                                                
LDPAS2   EQU   *                   NONE MAIL SYSTEMS                            
         CLI   1(R2),X'C1'         C1 IS IAMBIC SYSTEM                          
         BE    LDCP1                                                            
         SPACE 2                                                                
LDPAS3   EQU   *                                                                
         B     EXIT                                                             
         SPACE 2                                                                
LDPASA   CLI   0(R2),C'M'          TEST FOR MESSAGE SYSTEM                      
         BNE   EXIT                                                             
         TM    38(R2),X'80'           SKIP DELETED                              
         BO    EXIT                                                             
         CLI   12(R2),X'12'           ONLY USER RECORDS                         
         BNE   EXIT                                                             
LDPASB   CLI   0(RE),X'00'            SEARCH FOR NICK ELM                       
         BE    EXIT                                                             
         CLI   0(RE),X'21'                                                      
         BE    LDPASC                                                           
LDPASD   IC    RF,1(RE)               BUMP TO NEXT ELEMENT                      
         CH    RF,=H'2'                                                         
         BL    EXIT                   EXIT IF BAD LENGTH                        
         AR    RE,RF                                                            
         B     LDPASB                                                           
LDPASC   MVC   0(36,R3),0(R2)         COPY ACTIVE POINTER                       
         MVC   36(2,R3),38(R2)                                                  
         XC    38(5,R3),38(R3)                                                  
         MVI   12(R3),X'11'           SET PASSIVE TYPE CODE                     
         MVC   28(8,R3),=C'        '                                            
         IC    RF,1(RE)                                                         
         SH    RF,=H'5'                                                         
         EX    RF,*+8                                                           
         B     EXIT                                                             
         MVC   28(0,R3),4(RE)         MOVE IN NIKCNAME                          
         SPACE 2                                                                
LDCP1    CLI   0(RE),X'00'         SEARCH FOR 02 PASSIVE ELEMENTS               
         BE    EXIT                                                             
         CLI   0(RE),X'02'                                                      
         BE    LDCP3                                                            
         BH    EXIT                                                             
LDCP2    IC    RF,1(RE)            BUMP TO NEXT ELEMENT                         
         CH    RF,=H'2'                                                         
         BL    EXIT                EXIT IF BAD LENGTH                           
         AR    RE,RF                                                            
         B     LDCP1                                                            
         SPACE 2                                                                
LDCP3    MVC   0(36,R3),2(RE)      SET PASSIVE DATA                             
         MVC   36(2,R3),38(RE)     STATUS FROM PASSIVE ELEMENT                  
         XC    38(5,R3),38(R3)                                                  
         LA    R3,42(R3)           BUMP TO NEXT PASSIVE POINTER                 
         B     LDCP2                                                            
         SPACE 2                                                                
EXIT     XMOD1 1                                                                
         EJECT                                                                  
         LTORG                                                                  
         SPACE 2                                                                
WRKD     DSECT                                                                  
DUB      DS    D                                                                
WRKX     EQU   *                                                                
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'007PELDCPTR  05/06/85'                                      
         END                                                                    
