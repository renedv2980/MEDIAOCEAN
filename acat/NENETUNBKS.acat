*          DATA SET NENETUNBKS AT LEVEL 015 AS OF 01/29/91                      
*CATALP NETUNBK                                                                 
         TITLE 'NETUNBK - GENERATE HUT DATE IN MONTH WEEK QTR FORMAT'           
NETUNBK  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 UNDX-UND,**UNBK**                                                
         USING UND,RC                                                           
*                                  P1   BYTE1 1=ONE BYTE CODE W,M,Q             
*                                  P1   A(BINARY Y/W)                           
*                                       WEEK RETURNED=   YYMMDD                 
*                                       MONTH RETURNED=  YYMMDD                 
*                                       QUARTER RETURNED=QN/YY                  
*                                  P2   A(6-BYTE DATE)                          
*                                  P3   A(GETDAY)                               
*                                  P4   A(ADDAY)                                
*                                  P5   A(GETBROAD)                             
         SPACE 1                                                                
         MVC   TYPE,0(R1)                                                       
         LM    R2,R3,0(R1)                                                      
         MVC   GETDAY(12),8(R1)                                                 
         SPACE 1                                                                
         ZIC   R1,0(R2)            GENERATE JAN01 OF SPECIFIED YEAR             
         EDIT  (R1),(2,0(R3))                                                   
         OI    0(R3),X'F0'                                                      
         MVC   YEAR,0(R3)          HOLD THE YEAR                                
         MVC   2(4,R3),=C'0101'                                                 
         PRINT GEN                                                              
         GOTO1 GETDAY,PARA,0(R3),DUB                                            
         PRINT NOGEN                                                            
         ZIC   R0,0(R1)            BACK UP TO PREVIOUS MONDAY                   
         LA    R4,1                                                             
         SR    R4,R0                                                            
         GOTO1 ADDAY,PARA,0(R3),DUB,(R4)                                        
         ZIC   R5,1(R2)                                                         
         BCTR  R5,0                                                             
         MH    R5,=H'7'                                                         
         ST    R5,PARA+8                                                        
         GOTO1 ADDAY,PARA,DUB,0(R3)                                             
         GOTO1 GETBROAD,PARA,(1,0(R3)),WORK,GETDAY,ADDAY                        
         CLC   WORK+6(2),YEAR      CHECK IN THE SAME YEAR                       
         BNH   UN10                                                             
         MVI   0(R3),X'FF'                                                      
         B     UNEX                                                             
*                                                                               
UN10     CLI   TYPE,C'Q'                                                        
         BE    UN20                                                             
         CLI   TYPE,C'M'                                                        
         BNE   UNEX                                                             
*--DO MONTH LOGIC                                                               
         GOTO1 GETBROAD,PARA,(1,0(R3)),WORK,GETDAY,ADDAY                        
         LA    R4,15                                                            
         GOTO1 ADDAY,PARA,WORK,0(R3),(R4)                                       
         B     UNEX                                                             
*--DO QUARTER LOGIC                                                             
UN20     MVC   WORK+20(6),0(R3)    HOLD THE DATE                                
         LA    R4,QUARTDTE                                                      
         LA    R5,QUARTAB                                                       
         LA    R6,4                                                             
UN30     MVC   DUB+2(4),0(R4)      GET THE MONTH                                
         MVC   DUB(2),YEAR         GET THE YEAR                                 
         GOTO1 GETBROAD,PARA,(1,DUB),WORK,GETDAY,ADDAY                          
         MVC   0(6,R5),WORK+6                                                   
         LA    R4,4(R4)                                                         
         LA    R5,6(R5)                                                         
         BCT   R6,UN30                                                          
         SPACE 1                                                                
*--SET UP THIRD QUARTER                                                         
         MVC   DUB+2(4),=CL4'0911'                                              
         MVC   DUB(2),YEAR         GET THE YEAR                                 
         GOTO1 GETDAY,PARA,DUB,WORK                                             
         ZIC   R0,0(R1)            BACK UP TO PREVIOUS MONDAY                   
         LA    R4,1                                                             
         SR    R4,R0                                                            
         GOTO1 ADDAY,PARA,DUB,WORK,(R4)                                         
         MVC   QUARTAB+12(6),WORK                                               
*                                                                               
         LA    R4,QUARTERS                                                      
         LA    R5,QUARTAB                                                       
         LA    R6,4                                                             
UN40     MVC   0(3,R3),0(R4)       GET THE QUARTER                              
         CLC   WORK+20(6),0(R5)                                                 
         BNH   UN50                                                             
         LA    R4,3(R4)                                                         
         LA    R5,6(R5)                                                         
         BCT   R6,UN40                                                          
         DC    H'0'                                                             
UN50     MVC   3(2,R3),YEAR        MOVE YEAR                                    
         B     UNEX                                                             
UNEX     XIT1                                                                   
         SPACE 2                                                                
QUARTDTE DC    CL4'0315'                                                        
         DC    CL4'0615'                                                        
         DC    CL4'0911'                                                        
         DC    CL4'1215'                                                        
         DC    X'FF'                                                            
QUARTERS DC    CL3'Q1/'                                                         
         DC    CL3'Q2/'                                                         
         DC    CL3'Q3/'                                                         
         DC    CL3'Q4/'                                                         
         DC    X'FF'                                                            
         SPACE 2                                                                
UND      DSECT                                                                  
TYPE     DS    CL1                                                              
YEAR     DS    CL2                                                              
QUARTAB  DS    CL24                                                             
DUB      DS    D                                                                
WORK     DS    CL32                                                             
PARA     DS    6F                                                               
GETDAY   DS    V                                                                
ADDAY    DS    V                                                                
GETBROAD DS    V                                                                
UNDX     EQU   *                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'015NENETUNBKS01/29/91'                                      
         END                                                                    
