*          DATA SET FAMONITOR  AT LEVEL 002 AS OF 09/27/12                      
*&&      SET   ML=Y                                                             
*CATALP FAMNTR                                                                  
         TITLE 'FACILITIES MONITOR - SYSTEM EXECUTIVE CONTROLLER'               
         PRINT NOGEN                                                            
MONITOR  CSECT                                                                  
         ENTRY MONBRPRG            A(CALL TO APPLICATION CODE)                  
         ENTRY MONGORTN            RETURN POINT FROM APP                        
         ENTRY MLOG                *MLOG                                        
*                                                                               
         NMOD1 MONWORKL,**MNTR**,CLEAR=YES                                      
         USING MONWORK,RC                                                       
         L     R7,LITS             R7=A(COMMON STORAGE)                         
         USING LITERALS,R7                                                      
         LR    R6,R1               R6=A(TCB)                                    
         USING TCBD,R6                                                          
         B     MONINIT                                                          
*                                                                               
LITS     DC    A(LITERALS)                                                      
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO INITIALISE VALUES FOR MONITOR                            *         
***********************************************************************         
MONINIT  MVC   SRPARS(28),TCBPARS  SET UP S/R PARAM LIST                        
         MVC   CTPARS(28),TCBPARS  SET UP CON PARAM LIST                        
         LA    R9,COMFACS          REPLACE COMFACS WITH COPY                    
         ST    R9,CTPAR4                                                        
         LA    R9,SYSFAC           REPLACE SYSFACS WITH COPY                    
         ST    R9,CTPAR1                                                        
*                                                                               
         XC    TRNOUT(TRNOUTXC),TRNOUT  CLEAR MAGIC AREAS                       
         LA    R1,GLOBAREA                                                      
         ST    R1,GLOBWORK         SET GLOBBER WORK AREA ADDRESS                
         XC    GLOBAREA(GLOBELS-GLOBAREA+1),GLOBAREA                            
         LA    RE,TRNOUT                                                        
         ST    RE,SRPAR8                                                        
         ST    RE,CTPAR8                                                        
*                                                                               
         L     RA,SRPAR1           RA=A(SYSFAC)                                 
         USING SYSFACD,RA                                                       
         L     R3,SRPAR3           R3=A(UTL ENTRY)                              
         USING UTLD,R3                                                          
         ICM   RF,15,TBUFF         GET A(TBUFF-8 FOR USE LATER)                 
         BZ    *+12                                                             
         AHI   RF,-8                                                            
         ST    RF,TBUFFM8                                                       
*                                                                               
         XC    APRG,APRG           CLEAR A(PROGRAM TABLE ENTRY)                 
         XR    RE,RE                                                            
         ICM   RE,7,TAPRG          CONNECTED TO AN APPLICATION?                 
         JZ    MNT02               NO                                           
         XR    RF,RF                                                            
         ICM   RF,7,TARSYS         A(CONNECTED SELIST)                          
         JZ    MNT02                                                            
         ST    RE,APRG             SET A(PROGRAM TABLE ENTRY)                   
*                                                                               
         L     R1,VSSB             TAPRG IS DISPLACEMENT?                       
         TM    SSBSTAT4-SSBD(R1),SSBPGDSP                                       
         JZ    MNT02               NO-REAL ADDRESS                              
         ICM   RF,15,SEPGMS-SELISTD(RF)                                         
         AR    RE,RF                                                            
         ST    RE,APRG                                                          
*                                                                               
MNT02    MVC   SRPAR4,VSYSFAC0     FIX COMFACS ADDRESS FOR SVC REQS             
         L     RE,VSSB             EXTRACT TEMPSTR INFO FROM SSB                
         MVC   RECLEN,SSBTWAL-SSBD(RE)                                          
*                                                                               
         MVC   SSMAX,=AL2(4)                                                    
         MVC   SSMXP,=AL2(5)                                                    
         TM    TSTATC,TSTCXSES                                                  
         BZ    *+16                                                             
         MVC   SSMAX,SSBSSMAX-SSBD(RE)                                          
         MVC   SSMXP,SSBSSMXP-SSBD(RE)                                          
*                                                                               
         MVI   TSTSYS,C'N'                                                      
         TM    SSBSYSFL-SSBD(RE),X'80'                                          
         BZ    *+8                                                              
         MVI   TSTSYS,C'Y'                                                      
*                                                                               
MNT04    XR    R0,R0               FIX HOLE IF REQUIRED (AND POSSIBLE)          
         ICM   R0,8,=C'H'                                                       
         GOTO1 VCALLOV,DMCB,0,(R0),0                                            
*                                                                               
MNT06    CLC   RECLEN,=H'18432'    18K TEMPSTR DISPLACEMENTS                    
         JNE   MNT08                                                            
         MVC   CHKDSP,=Y(CHKPTDSP)                                              
         MVC   GLODSP,=Y(CHKPTGLD)                                              
         J     MONWHY                                                           
*                                                                               
MNT08    CLC   RECLEN,=H'14336'    14K TEMPSTR DISPLACEMENTS                    
         JNE   MNT10                                                            
         MVC   CHKDSP,=H'12800'    SET CHKPNT START AT 12.5K                    
         MVC   GLODSP,=H'12288'    SET GLOBAL START AT 12288                    
         J     MONWHY                                                           
*                                                                               
MNT10    DC    H'0'                WRONG LENGTH DEFN FOR TEMPSTR                
         EJECT                                                                  
***********************************************************************         
* EXAMINE DEVICE, INPUT DATA, AND UTL TO SEE WHY MONITOR WAS CALLED   *         
***********************************************************************         
MONWHY   MVI   DEVCTL,DEVPRN       SET DEVICE IS A PRINTER                      
         CLC   TPRNT,NULLS         PRINTER?                                     
         JNE   MONSR46             YES-PROCESSED VIA S/R                        
*                                                                               
         MVI   DEVCTL,DEVTRM       SET DEVICE IS A TERMINAL                     
         CLI   TRATE,250           TEST ONLY TERMINAL TRANSACTION RATE          
*NOP*    JZ    *+12                                                             
         J     *+12                                                             
         MVI   TRATE,0                                                          
         J     MONERR8                                                          
*                                                                               
         NI    TSTATU,255-TSTATMIO RESET CAN EXCEED MAX I/OS FLAG               
         TM    TSTAT6,TST6SCRP     TEST IF PROCESSING A SCRIPT                  
         JZ    MWHY08                                                           
*                                                                               
         OI    DEVCTL,DEVSCP+DEVNWT                                             
         MVC   TRINADR(8),TRNSCRP                                               
         L     RE,TBUFF            RE=A(SCRIPT INFO IN TBUFF)                   
         USING TBHDATA,RE                                                       
         MVI   TBHXIT,0                                                         
         CHI   R2,255              SCRUNCH SETS R2 FOR SPECIAL CALL             
         JNL   MWHY08                                                           
         STC   R2,TBHXIT           SET SPECIAL SCRIPT EXIT CODE                 
*                                                                               
         CLI   TBHXIT,TBHXEOS      NORMAL END OF SCRIPT                         
         JNE   MWHY02                                                           
         NI    DEVCTL,255-DEVNWT   SET TO WRITE TEMPSTR RECORD                  
         BRAS  RE,PUTTWA                                                        
         JNZ   MONERR5                                                          
         J     MONWRTX                                                          
*                                                                               
MWHY02   CLI   TBHXIT,TBHXERR      ERROR END OF SCRIPT                          
         JE    MONERR6                                                          
         CLI   TBHXIT,TBHXCMT      SCRIPT COMMAND TO COMMIT                     
         JNE   MWHY04                                                           
*                                                                               
         NI    DEVCTL,255-DEVNWT   SET TO WRITE TEMPSTR RECORD                  
         BRAS  RE,PUTTWA                                                        
         JZ    MONWRTX                                                          
         J     MONERR5                                                          
*                                                                               
MWHY04   CLI   TBHXIT,TBHXABN      SCRIPT COMMAND TO ABEND                      
         JE    MONERR7                                                          
         CLI   TBHXIT,TBHXDIE      SCRUNCH DIED AND V(SCRASH) CALLED            
         JNE   MWHY06                                                           
         NI    DEVCTL,255-DEVNWT   SET TO WRITE TEMPSTR RECORD                  
         BRAS  RE,PUTTWA                                                        
         JZ    MONWRTX                                                          
         J     MONERR5                                                          
*                                                                               
MWHY06   DC    H'0'                UNKNOWN SCRUNCH REQUEST                      
         DROP  RE                                                               
*                                                                               
MWHY08   NI    TFLAG,255-(TFLAGSSW+TFLAGNOP+TFLAGRCV)                           
         CLC   TNUM,NULLS          TEST UTLZERO                                 
         JNE   MWHY09                                                           
         OI    DEVCTL,DEVZERO      SET SPECIAL UTLZERO FLAG                     
         BRAS  RE,SETAUTO          SET SPECIAL TWA FIELDS                       
         J     MONSR48             UTLZERO PROCESSED VIA S/R                    
*                                                                               
MWHY09   L     R4,TCBTIA                                                        
         USING TWAD,R4                                                          
         ICM   R8,15,TBUFF         GET A(BUFFER)                                
         JZ    MONWRTX                                                          
*                                                                               
         L     RF,TBUFFM8                                                       
         USING TBHD,RF                                                          
         MVC   SAVE1,4(RF)         SAVE MESSAGE TYPE                            
         MVI   TBHINDS,0           CLEAR MESSAGE TYPE                           
         TM    DEVCTL,DEVSCP                                                    
         JO    MWHY30                                                           
         LH    RF,TBHMSGL          GET INPUT LENGTH                             
         DROP  RF                                                               
*                                                                               
         TM    SAVE1,X'80'                                                      
         JO    MWHY12              MESSAGE IS DATA INPUT BY $CT                 
*                                                                               
         TM    TTYPE,TTYPE327      TEST 3270                                    
         JZ    MWHY10                                                           
         AHI   R8,7                YES-MESSAGE HDR IS SEVEN CHRS LONG           
         AHI   RF,-7                                                            
         J     MWHY12                                                           
*                                                                               
MWHY10   TM    TTYPE2,TTYPEMQ      TEST MQ MESSAGE                              
         JO    MWHY12                                                           
         AHI   R8,2                POINT TO FIRST DATA BYTE                     
         AHI   RF,-2               ADJUST FOR STX/ADR                           
*                                                                               
MWHY12   STH   RF,4(R4)            SET DATA LEN IN TIA HEADER                   
         LTR   RF,RF                                                            
         JNZ   MWHY14                                                           
         XC    64(256,R4),64(R4)   MAKE SURE TIA IS CLEARED                     
         J     MWHY16                                                           
*                                                                               
MWHY14   LA    RE,64(R4)           MOVE DATA FROM TBUFF TO TIA+64               
         LR    R0,R8               SET FROM ADR                                 
         LR    R1,RF               SET FROM LEN (RF HAS TO LEN)                 
         MVCL  RE,R0                                                            
*                                                                               
MWHY16   TM    TTYPE2,TTYPEMQ      TEST MQ MESSAGE                              
         JZ    *+12                                                             
         LA    R1,TRNMQ            SET TRANSLATORS FOR MQ MESSAGES              
         J     MWHY18                                                           
         LA    R1,TRN3270          ONLY 3270                                    
*                                                                               
         TM    TSTAT6,TST6STSS     SET XLATORS FOR SPECIAL CASES                
         JO    *+12                                                             
         TM    TSTAT8,TST8STSS                                                  
         JNO   MWHY18                                                           
         MVC   TRINADR,TRN3270     SOFT FIELDS USES 3270 INPUT                  
         MVC   TROUTADR,TRNSTRO+4  BUT STEREO OUTPUT TRANSLATOR                 
         TM    SAVE1,X'40'+X'10'   TEST AUTO/GOBACK FLAGS                       
         JNZ   MWHY30              YES-USE SPECIAL JUST SET                     
         TM    TSTATC,TSTATFUP     FALINK BULK UPLOAD ON                        
         JO    MWHY30                                                           
         LA    R1,TRNSTRO          SPECIAL STEREO SCREEN TRANSLATORS            
*                                                                               
MWHY18   MVC   TRINADR(8),0(R1)                                                 
*                                                                               
MWHY20   TM    TSTAT8,TST8STSS     SESSION COMMANDS FOR SOFT FIELDS             
         JNO   MWHY30                                                           
         L     RF,VSSB                                                          
*NOP*    CLI   TSTSYS,C'Y'         TEST FACPAKS ONLY                            
*NOP*    BNE   MWHY30                                                           
         SR    R1,R1                                                            
         L     RE,TBUFF                                                         
         CLI   12(RE),C'*'         TEST THIS SESSION                            
         BNE   *+12                                                             
         IC    R1,TSESSION                                                      
         B     MWHY21                                                           
         IC    R1,12(RE)           GET SESSION LETTER                           
         N     R1,=XL4'0000000F'                                                
         AHI   R1,-1                                                            
         JM    MWHY29                                                           
         CLM   R1,1,SSMAX+1                                                     
         JH    MWHY29              INVALID SESSION IN HEADER                    
*                                                                               
MWHY21   IC    RF,SSACBITS(R1)     R1=SESSION NUMBER                            
         EX    RF,*+8                                                           
         B     *+8                                                              
         TM    TNAHNAH,0           TEST SESSION STOLEN                          
         B     MWHY22              WHO CARES                                    
*                                                                               
MWHY22   CLI   10(RE),X'C1'        STANDARD STEREO HEADER                       
         JE    MWHY24                                                           
         CLI   10(RE),X'81'                                                     
         JE    MWHY24                                                           
         CLI   10(RE),X'D5'        STANDARD STEREO HEADER WITH                  
         JE    MWHY26                                                           
         CLI   10(RE),X'95'                                                     
         JE    MWHY26                                                           
         J     MWHY29              INVALID/UNKNOWN HEADER                       
*                                                                               
MWHY24   IC    RF,SSNABITS(R1)     CLEAR UNSWAPPABLE SESSION BIT                
         EX    RF,*+8                                                           
         J     MWHY27                                                           
         NI    TSSXBITS,0                                                       
*                                                                               
MWHY26   IC    RF,SSACBITS(R1)     SET UNSWAPPABLE SESSION BIT                  
         EX    RF,*+8                                                           
         J     MWHY27                                                           
         OI    TSSXBITS,0                                                       
*                                                                               
MWHY27   CLI   13(RE),X'A7'        TEST FOR SET TRANSFER SESSION CMD            
         JNE   MWHY30                                                           
         IC    R1,14(RE)           GET SESSION LETTER PARAMETER                 
         N     R1,=X'0000000F'                                                  
         AHI   R1,-1                                                            
         JM    MWHY27A                                                          
         CLM   R1,1,SSMAX+1                                                     
         JH    MWHY27A             INVALID SESSION-UNTRANSFER                   
         NI    TSSRSRV,X'0F'                                                    
         STC   R1,SESS                                                          
         PACK  BYTE,SESS                                                        
         OC    TSSRSRV,BYTE        SET TRANSFER SESSION IN HOB                  
         J     MWHY30                                                           
MWHY27A  OI    TSSRSRV,X'F0'       UNTRANSFER SESSION SETTING                   
         J     MWHY30                                                           
*                                                                               
MWHY29   J     MWHY30              ERROR IN SESSION COMMAND                     
*                                                                               
MWHY30   CLC   TSVCREQ,NULLS       TEST SERVICE REQUEST                         
         JNE   MONSR               PROCESS SERVICE REQUEST                      
         MVI   GETWHY,0            READ TWA FROM DISK                           
         BRAS  RE,GETTWA                                                        
         JNZ   MONERR1                                                          
*                                                                               
         CLC   TSVCREQ,NULLS       TEST IF TWA HAS TIMED OUT                    
         JE    MWHY34                                                           
         L     RF,VSELIST          YES-CONVERT TO SE1                           
         LA    RF,6(RF)            POINT TO SE1 SELIST ENTRY                    
         ST    RF,TCBSE                                                         
         MVC   TCBDTFS(8),SEFILES-SELISTD(RF)                                   
         MVI   TCBSEN,1            SET PROCESSING SE1 REQUEST                   
         MVI   TCBOVSYS,1                                                       
         GOTO1 ADTFIOA,P1,(C'I',TCBD)                                           
         J     MONSR02             PROCESS SERVICE REQUEST                      
*                                                                               
MWHY34   TM    TSTAT2,TSTATCAN     PROCESS CANCEL PENDING                       
         JO    MONCAN                                                           
*                                                                               
MWHY36   TM    TSTAT8,TST8STSS     SOFT FIELD DYNAMIC SESSION CHANGE            
         JNO   MONAP                                                            
         L     RF,VSSB                                                          
         CLI   TSTSYS,C'Y'         IS IT A TEST FACPAK                          
         B     MONAP               *NOP* THE SESSION CHANGE DONT WORK           
*                                                                               
MWHY37   TM    SAVE1,X'40'+X'10'   TEST AUTO/GOBACK FLAGS INDICATING            
         JNZ   MONAP               NON STEREO FORMAT IN BUFFER                  
         L     RE,TBUFF                                                         
         CLI   12(RE),C'*'         TEST USE CURRENT SESSION                     
         JE    MONAP                                                            
*                                                                               
         IC    R1,12(RE)           GET SESSION LETTER                           
         N     R1,=X'0000000F'                                                  
         AHI   R1,-1                                                            
         JM    MWHY38              INVALID SESSION REQUEST                      
         CLM   R1,1,TSESSION                                                    
         JE    MONAP               SAME AS CURRENT SESSION                      
         CLM   R1,1,SSMAX+1                                                     
         JH    MWHY38              INVALID SESSION REQUEST                      
*                                                                               
         XC    DMCB(8),DMCB                                                     
         STC   R1,DMCB+0                                                        
         MVI   DMCB+4,X'60'        SET TWA ALREADY READ                         
         GOTO1 ATWASVR,DMCB                                                     
         CLI   4(R1),X'7E'                                                      
         JE    *+12                                                             
         CLI   4(R1),0             TEST ERRORS GOING TO NEW SESSION             
         JNE   MWHY38                                                           
*                                                                               
         L     R0,TCBTWA           COPY NEW SESSION TWA                         
         LH    R1,RECLEN                                                        
         L     RE,DMCB+4                                                        
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
*                                                                               
         L     RE,TBUFFM8          POINT TO BUFFER HEADER                       
         USING TBHD,RE                                                          
         OI    TBHINDS,TBHISTSS    FLAG TBUFF TO SHOW SESSION SWITCH            
         J     MONAP                                                            
         DROP  RE                                                               
*                                                                               
MWHY38   J     MONAP               IGNORE INVALID SESSION REQUEST               
         EJECT                                                                  
***********************************************************************         
* PROCESSING FOR APPLICATION PROGRAMS                                 *         
***********************************************************************         
MONAP    L     R4,TCBTWA           FILL IN SOME TWA HDR FIELDS                  
         USING TWAD,R4                                                          
         LA    RE,TWAHDRQ(R4)      FIND 1ST TWO UNPROTECTED FIELDS              
         USING FHD,RE                                                           
         XR    R1,R1                                                            
         XC    DUB,DUB                                                          
*                                                                               
MONAP02  TM    FHAT,FHATPR         IGNORE PROTECTED                             
         JO    MONAP04                                                          
         ST    RE,DUB+4                                                         
         CLC   DUB(4),NULLS                                                     
         JNE   MONAP06                                                          
         ST    RE,DUB                                                           
*                                                                               
MONAP04  ICM   R1,1,FHLN                                                        
         JZ    MONAP06                                                          
         BXH   RE,R1,MONAP02                                                    
         DROP  RE                                                               
*                                                                               
MONAP06  L     RE,VSSB             TEST USER INPUT INHIBITED                    
         TM    SSBSTAT1-SSBD(RE),SSBUII                                         
         JZ    MONAP08                                                          
         TM    TSTAT,TSTATDDS      BUT OK FOR DDS TERMINALS                     
         JNZ   MONAP08                                                          
         LA    R0,UIIMSG           OUTPUT USER INPUT INHIBITTED MSG             
         J     MONAP32                                                          
*                                                                               
MONAP08  XR    R1,R1               TEST IF XFR CONTROL GLOBAL PRESENT           
         ICM   R1,3,GLOBXFR                                                     
         JZ    MONAP14                                                          
*                                                                               
         LA    R1,GLOBAREA(R1)     R1=A(XFR CONTROL GLOBAL ELEMENT)             
         USING GLVXCTLD,R1                                                      
         CLI   GLVXCODE,GLVXCODQ   TEST IF STILL AN XFR ELEMENT                 
         JNE   MONAP14                                                          
*                                                                               
*&&ML*&& LHI   RF,X'01'            *MLOG 01 INPUT                               
*&&ML*&& BRAS  RE,MLOG             *MLOG                                        
*                                                                               
         TM    GLVXFLG1,GLV1IGN    TEST IF IGNORE FLAG IS SET                   
         JZ    MONAP12                                                          
         TM    GLVXFLG1,GLV1RETN+GLV1SEPS TEST GOING TO SAME SESSION            
         JNZ   MONAP12                                                          
*                                                                               
*&&ML*&& LHI   RF,X'82'            *MLOG 02 INPUT / LOAD VIRGIN FF              
*&&ML*&& BRAS  RE,MLOG             *MLOG                                        
*                                                                               
         LA    RE,16(R4)           CLEAR TWA EXCEPT HDR/GLOBALS/CHKPNT          
         LH    RF,GLODSP                                                        
         AHI   RF,-16                                                           
         XR    R1,R1                                                            
         MVCL  RE,R0                                                            
*                                                                               
         XC    DMCB(12),DMCB       LOAD APPLIC PROGRAM FF SCREEN                
         LA    RE,64(R4)                                                        
         ST    RE,DMCB             READ VIRGIN SCREEN INTO TWA+64               
         MVI   DMCB+4,C'R'                                                      
         MVC   DMCB+5(1),TCOSYS    UTL HAS CALLOV SYSTEM                        
         MVC   DMCB+6(1),TCBPRG    TCB HAS CALLOV PROGRAM                       
         MVI   DMCB+7,X'FF'                                                     
         GOTO1 VCALLOV,DMCB                                                     
*                                                                               
         L     RF,TBUFF            TEST IF S/R DATA PARKED IN TBUFF             
         CLC   1024(4,RF),=C'S/R='                                              
         JNE   MONAP10                                                          
         LA    RE,64(R4)           FIND FIRST UNPROT FIELD                      
         LLC   R0,0(RE)                                                         
         AR    RE,R0                                                            
         MVC   8(17,RE),1028(RF)   SET S/R CONNECT DATA *USER/SYS/PRG*          
         XC    1024(21,RF),1024(RF)                                             
*                                                                               
MONAP10  DS    0H                                                               
*&&US                                                                           
         LA    RE,64(R4)           FORCE ERASE WRITE                            
         XR    R0,R0                                                            
         IC    R0,0(RE)                                                         
         AR    RE,R0                                                            
         CLI   0(RE),0                                                          
         JNE   *-10                                                             
         MVI   1(RE),1             SET ERASE BEFORE AND AFTER                   
         MVI   2(RE),1                                                          
*&&                                                                             
MONAP12  OI    SAVE1,X'01'         SET TRY FOR GLOBALS                          
         DROP  R1                                                               
*                                                                               
MONAP14  TM    SAVE1,X'80'         MOVE PRE INPUT DATA FROM $CT TO TWA          
         JZ    MONAP20                                                          
*                                                                               
         L     R8,TCBTIA                                                        
         LA    R8,64(R8)           POINT TO $CT INPUT DATA SAVED IN TIA         
         ICM   R0,15,DUB+4         POINT TO 2ND UNPROT TWA FIELD                
         JZ    MONAP16                                                          
*                                                                               
*&&UK*&& LHI   RF,X'7D'            UK DELIMITER IS DINK                         
*&&US*&& LHI   RF,X'4B'            US DELIMITER IS DOT                          
         GOTO1 VSCUNKEY,DMCB,((RF),(R8)),(R0)                                   
         ORG   *-2                                                              
         BRAS  R9,MONGOTO                                                       
         CLI   4(R1),0                                                          
         JNE   MONAP18                                                          
*                                                                               
MONAP16  NI    SAVE1,X'01'         LEAVE TRY FOR GLOBALS BIT ON                 
         XC    TRNOUT,TRNOUT       TREAT AS NO INPUT IF MERGE ERROR             
         J     MONAP24                                                          
*                                                                               
MONAP18  L     R0,DUB+4            SET TRANS OUTPUT FIELDS                      
         SR    R0,R4                                                            
         STH   R0,TRNOUT           FIRST FIELD                                  
         L     R0,4(R1)                                                         
         SR    R0,R4                                                            
         STH   R0,TRNOUT+2         LAST FIELD                                   
         LLC   R0,4(R1)                                                         
         STH   R0,TRNOUT+4         NUM OF FIELDS                                
         J     MONAP24                                                          
*                                                                               
MONAP20  TM    TTYPE2,TTYPE2SC     TEST STRUCTURED CONVERSATION                 
         JO    MONAP22             YES-NO TRANSLATE                             
*                                                                               
         GOTO1 TRINADR,P1,TCBTIA,TCBTWA,(R3) CALL INPUT TRANSLATOR              
         ORG   *-2                                                              
         BRAS  R9,MONGOTO                                                       
         MVC   TRNOUT,0(R1)        EXTRACT INPUT TRANSLATOR TWA DATA            
*                                                                               
         TM    TSTAT6,TST6STSS                                                  
         JNZ   *+12                                                             
         TM    TSTAT8,TST8STSS     TEST NEW STEREO SCREENS                      
         JZ    MONAP22                                                          
*                                                                               
         LA    R1,TRNOUT                                                        
         USING TIOBD,R1                                                         
         TM    TIOBCNT,X'80'       TEST FOR FATISTR ERROR RETURN                
         JZ    MONAP22                                                          
         NI    TIOBCNT,255-X'80'                                                
         DROP  R1                                                               
***********************************************************************         
* IF FATISTR ERROR THEN GO STRAIGHT TO $DONE                          *         
***********************************************************************         
         GOTO1 VCHKOUT             WRITE TWA0 TO PRESERVE USER INPUT            
         L     RF,VSELIST                                                       
         LA    RF,6(RF)            POINT TO SE1 SELIST ENTRY                    
         ST    RF,TCBSE                                                         
         MVC   TCBDTFS(8),SEFILES-SELISTD(RF)                                   
         MVI   TCBSEN,1            SET PROCESSING SE1 REQUEST                   
         MVI   TCBOVSYS,1                                                       
         MVI   TSVCREQ,X'01'                                                    
         MVI   TSVCREQ+1,$DONE                                                  
         MVC   TCBPRG,TSVCREQ+1                                                 
         GOTO1 ADTFIOA,P1,(C'I',TCBD)                                           
         J     MONSR48             PROCESS $DONE S/R                            
*                                                                               
MONAP22  XR    RE,RE               RE=X'0000PL.R' P=PREV/L=LAST/R=RSRV          
         ICM   RE,3,TSSSWAP                                                     
         SRDL  RE,4                                                             
         SRL   RF,4                RF=X'0R......'                               
         CLM   RF,8,TSESSION       DO NOTHING IF THIS IS RESERVED SESS          
         JE    MONAP24                                                          
*                                                                               
         CLC   TSESSION,SSMAX+1    DO NOTHING IF THIS IS EXTRA SESS             
         JE    MONAP24                                                          
         SRDL  RE,8                RE=X'0000000P' PREV ACTIVE SESSION           
         SRL   RF,28               RF=X'0000000L' LAST ACTIVE SESSION           
         CLM   RF,1,TSESSION                                                    
         JE    MONAP24             DO NOTHING IF THIS SESS SAME AS LAST         
*                                                                               
         LR    RE,RF               LAST TO PREVIOUS                             
         IC    RF,TSESSION         CURRENT TO LAST                              
         SLL   RF,28                                                            
         SLDL  RE,4                RE=X'000000PL'                               
         STC   RE,TSSSWAP                                                       
*                                                                               
MONAP24  NI    TSTAT5,255-TST5PSWD MAKE SURE TST5PSWD IS OFF                    
         LA    R1,TRNOUT           TEST IF ? FOUND IN INPUT FIELD               
         USING TIOBD,R1                                                         
         MVC   TIOBLANG,TLANG      SET LANGUAGE CODE                            
         CLC   TIOBHELP,NULLS      TEST HELP REQUESTED                          
         JE    MONAP30                                                          
         TM    TSTAT6,TST6STRO+TST6STFU SKIP HELP IF ENHANCED STEREO            
         JO    MONAP30                                                          
         TM    DEVCTL,DEVSCP       NO HELP IF PROCESSING A SCRIPT               
         JO    MONAP30                                                          
         CLI   TSYS,0              TEST CONNECTED                               
         JE    MONAP30                                                          
         ICM   RE,15,APRG          TEST HELP SUPPORT FOR PROGRAM                
         JZ    MONAP30                                                          
         TM    PGMIND2-PGMLSTD(RE),PGMIHV1                                      
         JZ    MONAP30                                                          
*                                                                               
         L     RF,TCBTWA           LOCATE TWA FIELDS                            
         LA    RF,64(RF)                                                        
         USING FHD,RF                                                           
MONAP26  XR    R0,R0                                                            
         ICM   R0,1,FHLN                                                        
         JZ    MONAP28             LAST FIELD (EXIT)                            
         AR    RF,R0                                                            
         TM    FHAT,FHATPR         IGNORE PROTECTED                             
         JO    MONAP26                                                          
         TM    FHII,FHIITH         IF INPUT THIS TIME                           
         JNO   MONAP26                                                          
         OI    FHOI,FHOIMO         SET MODIFIED SO INPUT THIS TIME              
         J     MONAP26                                                          
         DROP  RF                                                               
*                                                                               
MONAP28  GOTO1 VCHKOUT             WRITE TWA0 TO PRESERVE USER INPUT            
         L     RF,VSELIST                                                       
         LA    RF,6(RF)            POINT TO SE1 SELIST ENTRY                    
         ST    RF,TCBSE                                                         
         MVC   TCBDTFS(8),SEFILES-SELISTD(RF)                                   
         MVI   TCBSEN,1            SET PROCESSING SE1 REQUEST                   
         MVI   TCBOVSYS,1                                                       
         MVI   TSVCREQ,X'01'                                                    
         MVI   TSVCREQ+1,$HELP                                                  
         MVC   TCBPRG,TSVCREQ+1                                                 
         GOTO1 ADTFIOA,P1,(C'I',TCBD)                                           
         J     MONSR48             PROCESS $HELP S/R                            
*                                                                               
MONAP30  L     RE,DUB              POINT TO FIRST UNP FIELD                     
         CLC   TIOBCNT,NULLS       WAS ANY DATA INPUT                           
         JNE   MONAP34                                                          
         TM    SAVE1,X'01'         NO-TEST TRY FOR GLOBALS                      
         JO    MONAP34                                                          
*                                                                               
         TM    TTYPE2,TTYPE2SC     TEST STRUCTURED CONVERSATION                 
         JO    MONAP34             YES-NO DATA INPUT IS ALLOWED                 
*                                                                               
         LA    R0,INVMSG                                                        
         ICM   RF,15,APRG          TEST IF PROGRAM ALLOWS NO DATA               
         JZ    MONAP32                                                          
         TM    PGMIND2-PGMLSTD(RF),PGMINOD                                      
         JO    MONAP34             OK FOR THIS PROGRAM TO HAVE NO DATA          
         DROP  R1                                                               
*                                                                               
MONAP32  MVI   GETWHY,0            RE-READ TWA TO IGNORE ALL INPUT              
         BRAS  RE,GETTWA                                                        
         JNZ   MONERR1                                                          
*                                                                               
         XC    TSVCREQ,TSVCREQ                                                  
         L     RE,TCBTWA                                                        
         LA    RE,64(RE)                                                        
         LLC   RF,0(RE)                                                         
         AHI   RF,-9                                                            
         LR    R1,R0               POINT TO MESSAGE PASSED                      
*                                                                               
         XR    R0,R0                                                            
         CLI   TLANG,8             LANGUAGE DEFINED?                            
         JH    *+8                 DEFAULT TO ENGLISH                           
         IC    R0,TLANG            GET LANGUAGE CODE                            
         SLL   R0,2                                                             
         AR    R1,R0               INDEX TO LANGUAGE                            
         L     R1,0(R1)                                                         
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   8(0,RE),0(R1)       MOVE DATA MSG TO TWA                         
         CLI   10(RE),C'/'                                                      
         JNE   *+14                                                             
         L     RF,VSSB             SET CORRECT MESSAGE CHR FOR SYSTEM           
         MVC   10(1,RE),SSBSYSCH-SSBD(RF)                                       
         GOTO1 TROUTADR,P1,(1,TCBTWA),TBUFF,(R3),TRNOUT                         
         ORG   *-2                                                              
         BRAS  R9,MONGOTO                                                       
         MVC   SVOUT,P1                                                         
         J     MONWRT1                                                          
*                                                                               
MONAP34  L     RE,DUB                                                           
         USING FHD,RE                                                           
         CLI   FHIL,0              ANY DATA IN SVCREQ FIELD                     
         JE    MONAP36                                                          
         CLI   FHDA,C'*'           YES-MUST BE CONNECT DATA                     
         JE    MONAP36                                                          
*                                                                               
         MVI   TSVCREQ,X'01'       IF IT ISNT WE HAVE A PROBLEM                 
         MVI   TSVCREQ+1,$SRERR                                                 
         J     MONSR                                                            
         DROP  RE                                                               
*                                                                               
MONAP36  XC    DMCB(12),DMCB       LOAD APPLICATION ROOT PHASE                  
         GOTO1 VCALLOV,DMCB                                                     
         CLI   4(R1),X'FF'                                                      
         JNE   *+12                                                             
         TM    TTEST,TTESTCIL      IGNORE IF IN CIL MODE                        
         JZ    MONERR3             ERROR CANT READ PHASE FROM PRGMS             
*                                                                               
         TM    TSTAT6,TST6DBUG     IN DEBUG IT MAY WELL BE FUCKED               
         JO    MONAP38                                                          
         L     RE,DMCB             SEE IF CORE PHASE IS DAMAGED                 
         CLC   0(2,RE),=X'07FE'    07FE IS OK                                   
         JE    MONAP38                                                          
         CLC   0(2,RE),=X'90EC'    90EC IS OK                                   
         JNE   MONERR9                                                          
*                                                                               
MONAP38  L     RE,TCBTWA           RESTORE S/R FIELD ON AUTO S/R                
         LA    RE,64(RE)                                                        
         LLC   R0,0(RE)                                                         
         AR    RE,R0               POINT TO FIRST UNP FIELD AND SAVE            
         MVC   SAVEFUNP,8(RE)                                                   
*                                                                               
MONAP40  MVC   DMCB(1),TOVSYS      STANDARD OVERLAY SYSTEM                      
         CLI   TCOSYS,0                                                         
         JE    *+10                                                             
         MVC   DMCB(1),TCOSYS      OVERRIDE OVERLAY SYSTEM                      
         CLI   DMCB,X'0A'                                                       
         JNE   MONAP46                                                          
*                                                                               
         MVI   TCBPROT,0           TEST STORAGE PROTECTION REQUIRED             
         L     RE,VSSB                                                          
         USING SSBD,RE                                                          
         TM    SSBPROT,SSBPONQ     TEST FACPAK ALLOWS SP                        
         JZ    MONAP44                                                          
         TM    SSBPROT,SSBPROTO    TEST PROTECTION TURNED OFF                   
         BO    MONAP44                                                          
         TM    TSTATU,TSTATEPR     TEST PROGRAM SP ENABLED BY CONNECT           
         JO    MONAP42                                                          
         TM    TSTATU,TSTATDPR     TEST PROGRAM SP DISSABLED BY CONNECT         
         JO    MONAP44                                                          
         DROP  RE                                                               
*                                                                               
         ICM   RE,15,APRG                                                       
         JZ    MONAP44                                                          
         TM    PGMIND3-PGMLSTD(RE),PGMIUNP                                      
         JO    MONAP44             TEST PROGRAM SP DISSABLED BY ABEND           
*                                                                               
MONAP42  OI    TCBPROT,TCBPKAPQ    FLAG APPLICATION TO RUN IN KEY 9             
*                                                                               
GOTOCON  EQU   *                   PASS CONTOL TO CONTROL SYSTEM APP            
MONAP44  L     RE,VSYSFAC0                                                      
         MVC   COMFACS,0(RE)       REFRESH COPY OF COMFACS TO CON               
         MVC   COMFACS1,256(RE)                                                 
*                                                                               
         LA    RE,XTRAINFO         PASS XTRA INFO DATA IN COMFACS COPY          
         ST    RE,COMFACS+CXTRAINF-COMFACSD                                     
         ST    RE,CTPAR9                                                        
         BRAS  RE,SETXTRA          SET UP EXTRA INFO FOR CONTROL APP            
*                                                                               
         BRAS  RE,SETDEBUG         SET DEBUG HOOK                               
*                                                                               
         L     RE,ASYSFAC          REFRESH COPY OF SYSFACS                      
         MVC   SYSFAC,0(RE)                                                     
         MVC   SYSFAC1,256(RE)                                                  
         GOTO1 DMCB,CTPAR1         CONTROL PROGRAMS GET CTPARM LIST             
         ORG   *-2                                                              
         BRAS  R9,MONGOTO                                                       
         MVI   TCBPROT,0           CLEAR STORAGE PROTECTION FLAGS               
         J     MONAP54                                                          
*                                                                               
MONAP46  L     RF,SRPAR5           RF=A(SELIST ENTRY)                           
         LLC   RE,SEFACSET-SELISTD(RF)                                          
         SLL   RE,2                                                             
         LR    R1,RE               SAVE DSPL FROM SYSFAC0 FOR MFACSET           
*                                                                               
         L     RE,VSYSFAC0(RE)     RE=A(SE FACILITIES LIST)                     
         MVC   SYSFACS,0(RE)       COPY FAC LISTS INTO WORKING STORAGE          
         LA    RE,COMFACS                                                       
         ST    RE,SYSFACS+252      LAST ENTRY IN SYSFACS IS A(COMFACS)          
         L     RE,VSYSFAC0                                                      
         MVC   COMFACS,0(RE)                                                    
         MVC   COMFACS1,256(RE)                                                 
         LA    RE,XTRAINFO         PASS XTRA INFO DATA IN COMFACS COPY          
         ST    RE,COMFACS+CXTRAINF-COMFACSD                                     
         LA    RE,SYSFACS          SET LOCAL SYSFAC ADDR IN COMFACS             
         ST    RE,COMFACS+CLCLFACS-COMFACSD                                     
*&&UK*&& CLI   TSYS,4              SET LOCAL SYSFAC                             
*&&UK*&& BNE   *+8                                                              
*&&UK*&& ST    RE,COMFACS+CMEDFACS-COMFACSD                                     
         TM    TTEST,X'03'         IS TERMINAL IN TEST MODE                     
         JZ    *+12                                                             
         BRAS  RE,MFACSET          T=OVERRIDES VERSION CONTROL                  
         J     MONAP48                                                          
*                                                                               
         L     RE,VSSB             LOOK IN SSB FOR VERSION CONTROL              
         TM    SSBSTAT4-SSBD(RE),SSBVCNTL                                       
         BZ    *+8                                                              
         BRAS  RE,VFACSET          TEST FOR PHASE LEVEL VERSION CTL             
*                                                                               
MONAP48  BRAS  RE,SETXTRA          SET UP EXTRA INFO FOR APPLICATION            
*                                                                               
         BRAS  RE,SETDEBUG         SET DEBUG HOOK                               
*                                                                               
         MVI   TCBPROT,0           TEST STORAGE PROTECTION REQUIRED             
         L     RE,VSSB                                                          
         USING SSBD,RE                                                          
         TM    SSBPROT,SSBPONQ     TEST FACPAK ALLOWS SP                        
         JZ    MONAP52                                                          
         TM    SSBPROT,SSBPROTO    TEST PROTECTION TURNED OFF                   
         BO    MONAP52                                                          
         TM    TSTATU,TSTATEPR     TEST PROGRAM SP ENABLED BY CONNECT           
         JO    MONAP50                                                          
         TM    TSTATU,TSTATDPR     TEST PROGRAM SP DISSABLED BY CONNECT         
         JO    MONAP52                                                          
         DROP  RE                                                               
*                                                                               
         ICM   RE,15,APRG                                                       
         JZ    MONAP52                                                          
         TM    PGMIND3-PGMLSTD(RE),PGMIUNP                                      
         JO    MONAP52             TEST PROGRAM SP DISSABLED BY ABEND           
*                                                                               
MONAP50  OI    TCBPROT,TCBPKAPQ    FLAG APPLICATION TO RUN IN KEY 9             
*                                                                               
GOTOAPP  EQU   *                   PASS CONTROL TO APPLICATION PROGRAM          
MONAP52  L     RF,DMCB                                                          
         GOTO1 (RF),DMCB,(TAGYB,TRNOUT),TCBTWA,SYSFACS,TCBTIA,COMFACS, X        
               XTRAINFO                                                         
         ORG   *-2                                                              
         BRAS  R9,MONGOTO                                                       
         MVI   TCBPROT,0           CLEAR STORAGE PROTECTION FLAG                
*                                                                               
MONAP54  ICM   RE,15,APRG          TEST IF PROGRAM GOT CONTROL ON CT            
         JZ    MONAP56                                                          
         TM    PGMIND2-PGMLSTD(RE),PGMIGOCT                                     
         JZ    MONAP56                                                          
         L     RE,TCBTWA           TEST IF PROVER MSG SAVED AT CONNECT          
         AH    RE,CHKDSP                                                        
         TM    CHPROVFL-CHKPTD(RE),X'C0'                                        
         JNO   MONAP56                                                          
         TM    DEVCTL,DEVSCP       BUT NOT FOR SCRIPT TERMINALS                 
         JO    MONAP56                                                          
*                                                                               
         L     RF,TCBTWA                                                        
         LA    RF,64(RF)                                                        
         USING FHD,RF                                                           
         MVC   FHDA(60),CHPROVTX-CHKPTD(RE)                                     
         OI    FHOI,FHOITR         SET TRANSMIT BIT                             
         TM    CHPROVFL-CHKPTD(RE),X'20'                                        
         JZ    *+8                                                              
         OI    FHOI,FHOIHI         SET HIGH INTENSITY                           
         DROP  RF                                                               
*                                                                               
MONAP56  MVI   SAVE2,0             INIT AFTER APPLICATION FLAGS                 
         TM    DEVCTL,DEVSCP       EXIT IF PROCESSING A SCRIPT                  
         JO    MONTRN                                                           
         CLC   TCBIRDR,NULLS       END IRDR IF ALLOCATED TO TASK                
         JE    MONAP58                                                          
         GOTO1 VPOWWOW,P1,0,=C'EOJ',0,0                                         
         ORG   *-2                                                              
         BRAS  R9,MONGOTO                                                       
*                                                                               
MONAP58  CLC   PRTQGBL(8),NULLS    TEST IF REPORT ADDED TO PRTQ                 
         JE    MONAP60                                                          
         CLI   PRTQGBL+15,X'FF'    TEST IF HAS BEEN TRANSFORMED                 
         JE    MONAP60                                                          
*                                                                               
         XR    R1,R1                                                            
         ICM   R1,3,PRTQGBL+5      GET REPORT SEQUENCE NUMBER                   
         CVD   R1,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         MVC   PRTQGBL+00(3),PRTQGBL+2                                          
         MVI   PRTQGBL+03,C','                                                  
         UNPK  PRTQGBL+04(5),DUB+5(3)                                           
         MVI   PRTQGBL+15,X'FF'    XXX,99999 TRANSFORMED FORMAT                 
         GOTO1 AGLOBBER,DMCB,=C'PUTD',PRTQGBL,9,1                               
         ORG   *-2                                                              
         BRAS  R9,MONGOTO          PRTQ ID IS NOW A GLOBAL                      
*                                                                               
MONAP60  XR    R1,R1               TEST IF XFR CONTROL GLOBAL PRESENT           
         ICM   R1,3,GLOBXFR                                                     
         JZ    MONAP74                                                          
         LA    R1,GLOBAREA(R1)     R1=A(XFR CONTROL GLOBAL ELEMENT)             
         USING GLVXCTLD,R1                                                      
         CLI   GLVXCODE,GLVXCODQ   TEST IF STILL AN XFR ELEMENT                 
         JNE   MONAP74                                                          
*                                                                               
*&&ML*&& LHI   RF,X'11'            *MLOG 11 OUTPUT                              
*&&ML*&& BRAS  RE,MLOG             *MLOG                                        
*                                                                               
         TM    GLVXFLG1,GLV1IGN    TEST IF IGNORE FLAG IS SET                   
         JO    MONAP74                                                          
         OI    GLVXFLG1,GLV1IGN                                                 
*                                                                               
         TM    GLVXFLG1,GLV1RETN   TEST RETURN CALL                             
         JO    *+18                                                             
         OI    GLVXFLG1,GLV1SIDR   SET SESSION ID OF CALLER                     
         MVC   GLVXSESR,TSESSION                                                
         J     MONAP62                                                          
*                                                                               
         OI    GLVXFLG1,GLV1SIDE   SET SESSION ID OF CALLEE                     
         MVC   GLVXSESE,TSESSION                                                
*                                                                               
MONAP62  L     R8,TBUFFM8          BUILD AUTO S/R IN TBUFF                      
         USING TBHD,R8                                                          
         MVI   TBHINDS,TBHIAUTO    SET AUTO S/R FLAG                            
         MVI   5(R8),0                                                          
*                                                                               
*&&ML*&& LHI   RF,X'31'            *MLOG 31 INPUT                               
*&&ML*&& BRAS  RE,MLOG             *MLOG                                        
*                                                                               
         TM    GLVXFLG1,GLV1RETN+GLV1SEPS                                       
         JNO   MONAP64                                                          
*                                                                               
         TM    GLVXFLG1,GLV1SEPD   NEW DIALOGUE MODE REQUESTED?                 
         JZ    *+12                NO                                           
         BRAS  RE,MONDIAL          GO SET LINKS AND =SS,? IF VALID              
         JE    MONAP110            =SS,? SET-JUST EXIT                          
*                                                                               
         LHI   R0,9                RETURNING FROM DIFFERENT SESSION             
         STH   R0,TBHMSGL          SET TO SWAP TO ORIGINAL SESSION              
         MVC   TBHDATA(9),=CL09'=SS,W,XCT'                                      
*                                                                               
         TM    GLVXFLG1,GLV1SIDR   TEST IF KNOW SESSION ID OF CALLER            
         JZ    MONAP110                                                         
         L     RE,VSSB             TEST IF VALID SESSION                        
         CLC   GLVXSESR,SSMAX+1                                                 
         JNL   MONAP110                                                         
         IC    RE,GLVXSESR                                                      
         LA    RE,1(RE)                                                         
         STC   RE,TBHDATA+4        SET TO SWAP BACK TO CALLERS SESSION          
         OI    TBHDATA+4,X'C0'                                                  
         J     MONAP110                                                         
*                                                                               
MONAP64  TM    GLVXFLG1,GLV1SEPD   NEW DIALOGUE MODE REQUESTED?                 
         JZ    *+12                NO                                           
         BRAS  RE,MONDIAL          GO SET LINKS AND =SS,? IF VALID              
         JE    MONAP110            =SS,? SET-JUST EXIT                          
*                                                                               
         LHI   R0,16               XFR CONTROL VIA =CT,XCTL (CONNECT)           
         STH   R0,TBHMSGL                                                       
         XC    TBHDATA(32),TBHDATA                                              
         MVC   TBHDATA(9),=CL09'=CT,XCTL,'                                      
         MVC   17(3,R8),GLVXTOSY                                                
         MVI   20(R8),C','                                                      
         MVC   21(3,R8),GLVXTOPR                                                
*                                                                               
         TM    GLVXFLG1,GLV1SEPS   XFR CONTROL TO NEW (SEPERATE) SESS           
         JZ    MONAP70                                                          
         MVI   15(R8),C'N'         SET NEW SESSION REQUIRED (=CT,XCTN)          
*                                                                               
         TM    GLVXFLG1,GLV1SNGL   TEST IF NOMINATED SINGLE SHOT CALL           
         BZ    MONAP66                                                          
         L     RF,VSSB                                                          
         CLC   SSMXP,SSMAX                                                      
         BNH   MONAP66                                                          
         MVI   15(R8),C'Z'         SET TO USE EXTRA PHYSICAL SESSION            
         J     MONAP110                                                         
*                                                                               
MONAP66  TM    GLVXFLG1,GLV1SIDE   TEST IF NOMINATED CALLEE SESSION             
         JO    MONAP68                                                          
         TM    TSTAT8,TST8STSS     TEST SOFT FIELDS                             
         JO    *+12                                                             
         TM    TSTAT6,TST6STRO     TEST RUNNING UNDER STEREO                    
         JZ    MONAP110                                                         
         TM    TSSRSRV,X'F0'       TEST IF TRANSFER SESSION DEFINED             
         JO    MONAP110                                                         
         MVI   15(R8),C'T'         SET TO USE STEREO TRANSFER SESSION           
         J     MONAP110                                                         
*                                                                               
MONAP68  CLI   GLVXSESE,GLVSRSRV   TEST IF WANT RESERVED SESSION                
         JNE   *+12                                                             
         MVI   15(R8),C'R'                                                      
         J     MONAP110                                                         
*                                                                               
         CLI   GLVXSESE,GLVSTRAN   TEST IF WANT TRANSFER SESSION                
         JNE   *+12                                                             
         MVI   15(R8),C'T'                                                      
         J     MONAP110                                                         
*                                                                               
         L     RF,VSSB             TEST VALID NOMINATED CALLEE SESSI            
         CLC   GLVXSESE,SSMAX+1                                                 
         JH    MONAP110                                                         
         CLC   GLVXSESE,TSESSION                                                
         JE    MONAP110                                                         
         IC    RF,GLVXSESE         CALLER HAS SET CALLEE SESSION ID             
         LA    RF,1(RF)                                                         
         STC   RF,15(R8)                                                        
         OI    15(R8),X'C0'        SET NOMINATED SESSION (=CT,XCT#)             
         J     MONAP110                                                         
*                                                                               
MONAP70  TM    GLVXFLG1,GLV1RETN   XFR CONTROL USING SAME SESSION               
         JZ    MONAP110                                                         
         TM    GLVXFLG1,GLV1RETG   TEST IF RETURN GLOBALS REQUIRED              
         JO    MONAP72                                                          
*                                                                               
*&&ML*&& LHI   RF,X'82'            *MLOG 12 OUTPUT DONT WRITE TEMPSTR           
*&&ML*&& BRAS  RE,MLOG             *MLOG                                        
         J     MONAP112                                                         
         DROP  R8                                                               
*                                                                               
MONAP72  XC    DMCB+8(4),DMCB+8    SET TEMPSTR PAGE=0                           
         MVC   DMCB+20(2),=C'L='   SPECIAL PARM FOR TWA0 READ/WRITES            
         MVC   DMCB+22(2),RECLEN                                                
         GOTO1 VDATAMGR,DMCB,=C'DMREAD',=C'TEMPSTR',,TCBTWA                     
         MVI   GLOBSWS,0                                                        
         LA    R0,GLOBAREA         POINT TO GLOBAL AREA IN TASK                 
         LA    R1,GLOBALEN                                                      
         L     RE,TCBTWA           POINT TO GLOBAL DATA IN TWA#0                
         AHI   RE,CHKPTGLD                                                      
         LR    RF,R1                                                            
         MVCL  RE,R0               MOVE TASK AREA GLOBALS TO TWA#0              
         J     MONAP110                                                         
         DROP  R1                                                               
*                                                                               
MONAP74  L     R1,TCBTWA           POINT TO S/R FIELD                           
         LA    R1,64(R1)                                                        
         LLC   R0,0(R1)                                                         
         AR    R1,R0                                                            
         CLI   8(R1),C'+'          TEST FOR TURN AROUND REQUEST                 
         JE    MONAP76                                                          
         CLI   8(R1),C'$'                                                       
         JE    MONAP76                                                          
         CLI   8(R1),C'='                                                       
         JNE   MONAP82                                                          
*                                                                               
MONAP76  OI    SAVE2,X'04'         SET T/A S/R FLAG                             
         LHI   R0,9                LOOK FOR ,SV OLD STYLE SWAP COMMAND          
         LA    R1,9(R1)                                                         
*                                                                               
MONAP78  CLI   0(R1),C','                                                       
         JE    MONAP80                                                          
         LA    R1,1(R1)                                                         
         BRCT  R0,MONAP78                                                       
         J     MONAP82                                                          
*                                                                               
MONAP80  CLC   1(2,R1),=C'SV'                                                   
         JNE   MONAP82                                                          
         MVI   3(R1),C'P'          SET ,SVP TO SHOW PROGRAM GENERATED           
*                                                                               
MONAP82  TM    SAVE2,X'04'         IF T/A S/R DONT NOTIFY NOW                   
         JO    MONAP102                                                         
*                                                                               
*&&US*&& BRAS  RE,DARENOTE         TEST IF DARE NOTIFY REQUIRED                 
*&&US*&& JNZ   MONTRN              NOTIFY MESSAGE BUILT IN TWA                  
*                                                                               
         TM    TSTAT2,TSTATBCP     TEST BROADCAST PENDING                       
         JZ    MONAP84                                                          
         TM    TSTAT6,TST6STFU     TEST STEREO EMULATOR MODE                    
         JZ    MONAP84                                                          
*NOP*    TM    TSTAT8,TST8STSS     TEST SOFT FIELDS                             
*NOP*    JZ    MONAP84                                                          
         J     MONAP84                                                          
*&&DO                                                                           
         TM    SAVE2,X'04'         SKIP NOTIFY IF TURNAROUND                    
         JO    MONAP100                                                         
         L     R1,TCBTWA           NOTIFY USER OF BROADCAST                     
         LA    R1,64(R1)                                                        
         LLC   R0,0(R1)                                                         
         AR    R1,R0                                                            
         OI    1(R1),X'08'         SET HIGH INTENSITY & XMIT                    
         OI    6(R1),X'80'                                                      
         LA    R1,8(R1)                                                         
         XC    0(17,R1),0(R1)      CLEAR FIELD                                  
         MVC   0(3,R1),=C'*BC '    FLAG BROADCAST SENT                          
         NI    TSTAT2,255-TSTATBCP CLEAR FLAG                                   
         J     MONAP102                                                         
*&&                                                                             
MONAP84  TM    TJOBFLAG,TJOBFOUT   TEST JOB NOTIFY PENDING                      
         JZ    MONAP102                                                         
*                                                                               
         L     R2,TCBTIA           READ SPECIAL S/R TWA PAGE                    
         USING SRSD,R2             R2=A(S/R TWA SAVE PAGE)                      
         LHI   R5,SRPAGENO                                                      
         SLL   R5,32-8                                                          
         MVC   DMCB+20(2),=C'L='                                                
         MVC   DMCB+22(2),RECLEN                                                
         GOTO1 VDATAMGR,DMCB,(X'80',=C'DMREAD'),=C'TEMPSTR',(R5),SRSD           
         ORG   *-2                                                              
         BRAS  R9,MONGOTO                                                       
*                                                                               
         TM    SAVE2,X'04'         SKIP NOTIFY IF TURNAROUND                    
         JO    MONAP100                                                         
         TM    TJOBFLAG,TJOBFOUT   TEST JOB NOTIFY PENDING                      
         JZ    MONAP100                                                         
         SR    R0,R0                                                            
         ICM   R0,1,SRJOBINQ       R0=N'ENTRIES IN JOB Q                        
         JNZ   *+12                                                             
         NI    TJOBFLAG,255-TJOBFOUT                                            
         J     MONAP90                                                          
*                                                                               
         LA    RE,SRJOBQ           SEARCH Q FOR UNNOTIFIED JOBS                 
         USING SRJOBQ,RE           RE=A(JOB Q)                                  
MONAP86  CLI   SRJOBSTA,SRJOBERR   IGNORE JOBS IN ERROR/NOT RUN ETC.            
         JE    MONAP88                                                          
         TM    SRJOBSTA,SRJOBPUT+SRJOBOUT                                       
         JNO   MONAP88                                                          
         TM    SRJOBSTA,SRJOBNOT   TEST THIS JOB NOTIFIED                       
         JNZ   MONAP88                                                          
         TM    SAVE2,X'01'         TEST THIS IS SECOND JOB FOUND                
         JNZ   MONAP90                                                          
         OI    SAVE2,X'01'         SET FIRST JOB FOUND & SAVE ENTRY             
         MVC   MONSAVE(SRJOBQLN),SRJOBQ                                         
         OI    SRJOBSTA,SRJOBNOT   SET THIS JOB NOTIFIED                        
*                                                                               
MONAP88  AHI   RE,SRJOBQLN         BUMP TO NEXT JOB Q ENTRY                     
         BRCT  R0,MONAP86                                                       
         NI    TJOBFLAG,255-TJOBFOUT                                            
*                                                                               
MONAP90  TM    SAVE2,X'01'         TEST JOB FOUND FOR NOTIFY                    
         JZ    MONAP100                                                         
*                                                                               
         LA    RE,MONSAVE          RE=A(JOB NAME)                               
         L     R1,TCBTWA           NOTIFY USER OF JOB NAME                      
         LA    R1,64(R1)                                                        
         LLC   R0,0(R1)                                                         
         AR    R1,R0                                                            
         OI    1(R1),X'08'         SET HIGH INTENSITY & XMIT                    
         OI    6(R1),X'80'                                                      
         LA    R1,8(R1)                                                         
*                                                                               
         TM    TSTAT6,TST6STRO     TEST STEREO NOTIFY                           
         JO    MONAP96                                                          
*                                                                               
         XC    0(17,R1),0(R1)      CLEAR FIELD                                  
         MVI   0(R1),C'*'          FORMAT '*AAA,NNNNN SSSSS*' IN TWA            
         MVC   1(3,R1),SRJOBSUB                                                 
         MVI   4(R1),C','                                                       
         SR    R0,R0                                                            
         ICM   R0,3,SRJOBREP                                                    
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  5(5,R1),DUB                                                      
         TM    SRJOBSTA,SRJOBINV                                                
         JNZ   *+10                                                             
         MVC   PRTQGBL(9),1(R1)                                                 
         LA    R1,5(R1)                                                         
         LA    R0,4                                                             
*                                                                               
MONAP92  CLI   0(R1),C'0'                                                       
         JNE   MONAP94                                                          
         MVC   0(5,R1),1(R1)                                                    
         BRCT  R0,MONAP92                                                       
*                                                                               
MONAP94  AR    R1,R0               R1=A(LAST DIGIT IN REPORT SEQ NUM)           
         LA    R0,RDYMSG                                                        
         TM    SRJOBSTA,SRJOBINV   CHANGE MESSAGE IF REPORT ABENDED             
         JZ    *+8                                                              
         LA    R0,RERMSG                                                        
         XR    RF,RF                                                            
         CLI   TLANG,8             SET TO ENGLISH IF UNDEFINED                  
         JH    *+8                                                              
         IC    RF,TLANG            GET LANGUAGE CODE                            
         SLL   RF,2                                                             
         AR    RF,R0               INDEX TO LANGUAGE                            
         L     RF,0(RF)                                                         
         MVC   1(7,R1),0(RF)       MOVE READY/ERROR FOR REPORT                  
         J     MONAP98                                                          
*                                                                               
MONAP96  MVC   8(3,R1),SRJOBSUB    BUILD ABC12345S (STERO NOTIFY)               
         SR    R0,R0                                                            
         ICM   R0,3,SRJOBREP                                                    
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  11(5,R1),DUB                                                     
         MVI   16(R1),C'R'         READY                                        
         TM    SRJOBSTA,SRJOBINV                                                
         JZ    *+12                                                             
         MVI   16(R1),C'E'         ERROR                                        
         J     MONAP98                                                          
         MVC   PRTQGBL(3),8(R1)    BUILD PRINT QUEUE GLOBAL                     
         MVI   PRTQGBL+3,C','                                                   
         MVC   PRTQGBL+4(5),11(R1)                                              
*                                                                               
MONAP98  TM    SRJOBSTA,SRJOBINV                                                
         JNZ   MONAP100                                                         
         DROP  RE                                                               
         GOTO1 AGLOBBER,DMCB,=C'PUTD',PRTQGBL,9,1                               
         ORG   *-2                                                              
         BRAS  R9,MONGOTO                                                       
*                                                                               
MONAP100 TM    SAVE2,X'03'         TEST TO REWRITE TWA 11                       
         JZ    MONAP102                                                         
         LA    R5,SRPAGENO                                                      
         SLL   R5,32-8                                                          
         GOTO1 VDATAMGR,DMCB,=C'DMWRT',=C'TEMPSTR',(R5),SRSD                    
         ORG   *-2                                                              
         BRAS  R9,MONGOTO                                                       
*                                                                               
MONAP102 L     R1,TCBTWA           POINT TO S/R FIELD                           
         LA    R1,64(R1)                                                        
         LLC   R0,0(R1)                                                         
         AR    R1,R0                                                            
         TM    SAVE2,X'04'         IS IT TURNAROUND                             
         JO    MONAP106                                                         
         TM    SAVE2,X'01'         IS IT NOTIFY                                 
         JO    MONTRN                                                           
*                                                                               
MONAP104 MVC   8(17,R1),SAVEFUNP   RESTORE S/R DATA                             
         J     MONTRN                                                           
*                                                                               
MONAP106 CLC   9(6,R1),=C'AUTOSW'  TEST APPLIC WANTS AUTO SWAP                  
         JE    MONAP114                                                         
         CLI   8(R1),C'+'          TEST ELIGIBLE FOR AUTO RETURN                
         JNE   *+8                                                              
         OI    TSTAT1,TSTATASW     SET AUTO SWITCH FLAG                         
*                                                                               
         GOTO1 TROUTADR,P1,TCBTWA,TBUFF,(R3),TRNOUT                             
         ORG   *-2                                                              
         BRAS  R9,MONGOTO                                                       
         MVC   SVOUT,P1                                                         
*                                                                               
         L     R8,TBUFFM8                                                       
         MVI   4(R8),X'40'         SET AUTO S/R FLAG                            
         MVI   5(R8),0                                                          
         LA    R0,17                                                            
         STH   R0,6(R8)                                                         
*                                                                               
         L     R1,TCBTWA           POINT TO S/R FIELD                           
         LA    R1,64(R1)                                                        
         LLC   R0,0(R1)                                                         
         AR    R1,R0                                                            
         MVC   8(17,R8),8(R1)      MOVE S/R DATA TO TBUFF                       
*                                                                               
MONAP108 MVC   8(17,R1),SAVEFUNP   RESTORE ORIGINAL S/R DATA                    
*                                                                               
MONAP110 CLI   TSVCREQ+1,$UNWIND   DON'T WRITE TWA ON UNWIND                    
         JNE   *+8                                                              
         OI    DEVCTL,DEVNWT                                                    
         BRAS  RE,PUTTWA                                                        
         JNZ   MONERR2                                                          
*                                                                               
MONAP112 OI    TTORAOR,TTAGOBAK    SET GOBACK FLAG                              
         J     MONWRTX                                                          
*                                                                               
MONAP114 TM    TSTAT1,TSTATASW     TEST GOT TO PROGRAM VIA AUTOSWAP             
         JZ    MONAP104            NO-RESTORE S/R FIELD AND EXIT                
         NI    TSTAT1,255-TSTATASW RESET AUTO SWITCH FLAG                       
         L     R8,TBUFFM8                                                       
         USING TBHD,R8                                                          
         MVI   TBHINDS,TBHIAUTO    SET AUTO S/R FLAG                            
         MVI   5(R8),0                                                          
         LHI   R0,3                                                             
         STH   R0,TBHMSGL                                                       
         XC    TBHDATA(17),TBHDATA                                              
         MVC   TBHDATA(04),=C'=SWU'                                             
         J     MONAP108                                                         
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO PROCESS SERVICE REQUESTS                                 *         
***********************************************************************         
MONSR    CLI   TSVCREQ+1,$RE       $RE-SPECIAL CODE                             
         JNE   MONSR22                                                          
         CLI   TSYS,0              OK IF CONNECTED                              
         JNZ   MONSR04                                                          
         MVI   TSVCREQ,X'01'                                                    
         MVI   TSVCREQ+1,$CT       IF NOT PASS TO CONNECT PGM                   
*                                                                               
MONSR02  MVC   TCBPRG,TSVCREQ+1    RESET TCBPRG TOO                             
         J     MONSR24                                                          
*                                                                               
MONSR04  MVC   PARK,TSVCREQ        PROCESS $RE FOR CONNECTED TERMINAL           
         MVC   TSVCREQ,$$BYE       FORCE NO MATCH NEXT TIME                     
         OI    TRNOUT+TIOBINDS-TIOBD,TIOBCLR SET S/R WANTS COLOUR               
         MVI   GETWHY,0                                                         
         BRAS  RE,GETTWA                                                        
         JNZ   MONERR1             ERROR CANT READ TWA FROM TEMPSTR             
*                                                                               
         CLI   TSVCREQ+1,$CT                                                    
         JE    MONSR02                                                          
         L     R1,TCBTWA           R1=A(TWA)                                    
         LA    R1,64(R1)                                                        
         LLC   RF,0(R1)            BUMP TO SRVREQ FIELD                         
         LA    R1,8(R1,RF)                                                      
*                                                                               
MONSR06  CLI   PARK+1,$RE          TEST IF $RE,S=ABC INPUT                      
         JNE   MONSR18                                                          
         L     RE,TBUFF            TEST IF INPUT SAVED AT TBUFF+1024            
         LA    RE,1024(RE)                                                      
         CLC   0(6,RE),=C'=RE,S='                                               
         JNE   MONSR18                                                          
         LA    RE,6(RE)                                                         
         CLI   0(RE),C'Y'          TEST S=Y                                     
         JNE   *+8                                                              
         OI    TSTAT6,TST6STRO                                                  
         CLI   0(RE),C'N'          TEST S=N                                     
         JNE   *+8                                                              
         NI    TSTAT6,255-TST6STRO                                              
         CLI   1(RE),C'Y'          TEST S=*Y                                    
         JNE   *+8                                                              
         OI    TSTAT6,TST6STFU                                                  
         CLI   1(RE),C'N'          TEST S=*N                                    
         JNE   *+8                                                              
         NI    TSTAT6,255-TST6STFU                                              
         CLI   2(RE),C'Y'          TEST S=**Y                                   
         JNE   MONSR08                                                          
         OI    TSTAT6,TST6STSS                                                  
         OI    TSTAT8,TST8STSS                                                  
         NI    TSTAT8,255-TST8BINT                                              
         J     MONSR16                                                          
*                                                                               
MONSR08  CLI   2(RE),C'N'          TEST S=**N                                   
         JNE   MONSR10                                                          
         NI    TSTAT6,255-TST6STSS                                              
         NI    TSTAT8,255-TST8STSS-TST8BINT                                     
         J     MONSR16                                                          
*                                                                               
MONSR10  CLI   2(RE),C'B'          TEST S=**B                                   
         JNE   MONSR12                                                          
         OI    TSTAT6,TST6STSS                                                  
         OI    TSTAT8,TST8STSS                                                  
         TM    TSTAT9,TSTNVRSN                                                  
         JZ    *+8                                                              
         OI    TSTAT8,TST8BINT                                                  
         J     MONSR16                                                          
*                                                                               
MONSR12  CLI   2(RE),C'1'          TEST S=**1                                   
         JNE   MONSR14                                                          
         OI    TSTAT6,TST6STSS                                                  
         OI    TSTAT8,TST8STSS+TST8BINT                                         
         OI    TSTAT9,TSTNVRSN                                                  
         J     MONSR16                                                          
*                                                                               
MONSR14  CLI   2(RE),C'2'          TEST S=**2                                   
         JNE   MONSR16                                                          
         OI    TSTAT6,TST6STSS                                                  
         OI    TSTAT8,TST8STSS+TST8BINT                                         
         OI    TSTAT9,TSTNVRSN+TST9CMAD                                         
*                                                                               
MONSR16  L     RE,TBUFF            CLEAR SAVED DATA AT TBUFF+1024               
         LA    RE,1024(RE)                                                      
         XC    0(10,RE),0(RE)                                                   
*                                                                               
         TM    TSTAT6,TST6SCRP     TEST IF PROCESSING A SCRIPT                  
         JO    MONSR18                                                          
         TM    TSTAT8,TST8STSS                                                  
         JNO   *+14                                                             
         MVC   TROUTADR,=V(TOSTR)  SET STEREO OUTPUT TRANSLATOR                 
         J     MONSR18                                                          
         MVC   TROUTADR,=V(TO3270) SET 3270 TRANSLATOR                          
*                                                                               
MONSR18  MVC   0(17,R1),TCBSRMSG   RESTORE SRVREQ FIELD                         
         TM    TSTAT6,TST6STRO                                                  
         JZ    MONSR20                                                          
         MVI   0(R1),C' '                                                       
         MVC   1(16,R1),0(R1)                                                   
         MVI   0(R1),C'*'          OUTPUT TRANSLATER SETS FOR STEREO            
*                                                                               
MONSR20  BRAS  RE,PUTTWA                                                        
         JNZ   MONERR2             ERROR CANT WRITE TWA TO TEMPSTR              
         J     MONSR126                                                         
*                                                                               
MONSR22  CLI   TSVCREQ+1,$CC       $CC-SPECIAL CODE                             
         JNE   MONSR24                                                          
         TM    TSTAT2,TSTATCC      TEST FIRST TIME                              
         JO    MONSR24                                                          
         OI    TSTAT2,TSTATCC      SET $CC MODE                                 
         L     RE,TBUFF            GET ADR OF OUTPUT BUFFER                     
         MVC   0(CCDL,RE),CCD      MOVE DATA TO SET FIRST FLD UNP               
         L     RE,TBUFFM8          GET ADR OF OUTPUT BUFFER                     
         MVI   5(RE),0             SET NORMAL WRITE                             
         LA    RF,CCDL                                                          
         STCM  RF,3,6(RE)          SET LEN IN BUFFER HDR                        
         STCM  RF,3,TCBMSGO        SET OUTPUT MSG LEN                           
*                                                                               
         XR    RF,RF               GET LANGUAGE CODE                            
         CLI   TLANG,8             MAKE SURE KNOWN                              
         JH    *+8                 IF NOT DEFAULT TO ENGLISH                    
         IC    RF,TLANG                                                         
         SLL   RF,2                                                             
         LA    RF,CALMSG(RF)       INDEX INTO $CC LANGUAGE TABLE                
         L     RF,0(RF)                                                         
*                                                                               
         LA    R1,CCDS-CCD+2                                                    
         L     RE,TBUFF                                                         
         AR    RE,R1               POINT TO LANGUAGE DEPENDENT DATA             
         MVC   0(17,RE),0(RF)                                                   
         J     MONWRT3                                                          
*                                                                               
MONSR24  TM    DEVCTL,DEVSCP       TWA HAS BEEN PREPARED BY SCRUNCH             
         JO    MONSR46                                                          
         TM    TSTAT2,TSTATNIT     TEST IF NEED READ OLD TEMPSTR TWA            
         JO    MONSR26                                                          
         CLI   TSVCREQ+1,$CT       $CT NEEDS OLD SAVE DATA                      
         JE    MONSR26                                                          
         CLI   TSVCREQ+1,$DONE     $DONE NEEDS OLD SAVE DATA                    
         JE    MONSR26                                                          
         CLI   TSVCREQ+1,$PCI      $PC,4 NEEDS OLD SAVE DATA                    
         JE    MONSR26                                                          
         J     MONSR28                                                          
*                                                                               
MONSR26  MVI   GETWHY,1            SET TO READ TWA TO GET SAVE DATA             
         BRAS  RE,GETTWA                                                        
         L     RE,TCBTWA                                                        
         LH    RF,GLODSP           DONT CLEAR GLOBAL+CHKPNT AREAS               
         XR    R1,R1                                                            
         MVCL  RE,R0               CLEAR TWA AREA (LEAVING SAVE DATA)           
         CLI   DMCB+8,0                                                         
         JE    MONSR30                                                          
*                                                                               
MONSR28  L     RE,TCBTWA           CLEAR TWA SAVE AND SAVE AREA ALSO            
         LH    RF,CHKDSP                                                        
         LA    RF,CHKPTLEN(RF)                                                  
         SR    R1,R1                                                            
         MVCL  RE,R0                                                            
*                                                                               
MONSR30  L     RE,TCBTWA           SET TO LOAD THE S/R FF SCREEN                
         LA    RE,64(RE)                                                        
         ST    RE,DMCB+0                                                        
         MVI   DMCB+4,C'R'                                                      
         MVI   DMCB+5,X'01'                                                     
         MVC   DMCB+6(1),TSVCREQ+1                                              
         MVI   DMCB+7,X'FF'                                                     
         XC    DMCB+8(4),DMCB+8                                                 
*                                                                               
         CLI   TSVCREQ+1,$KWX      $KWX HAS LOTS OF ROOT SCREENS                
         JNE   MONSR32                                                          
         TM    TSVCREQ,X'F0'       CHECK FOR NON FF ROOT SCREEN                 
         JZ    MONSR36                                                          
         LLC   R1,TSVCREQ          NON FF ROOT HAS BEEN SAVED HERE              
         SRL   R1,4                                                             
         STC   R1,DMCB+7                                                        
         OI    DMCB+7,X'C0'                                                     
         J     MONSR36                                                          
*                                                                               
MONSR32  CLI   TSVCREQ+1,$DUMP     $DUMP HAS MORE THAN ONE SCREEN               
         JE    MONSR34                                                          
         CLI   TSVCREQ+1,$TEST     $TEST HAS MORE THAN ONE SCREEN               
         JE    MONSR34                                                          
         J     MONSR36                                                          
*                                                                               
MONSR34  TM    TSVCREQ,X'F0'       CHECK FOR NON FF ROOT SCREEN                 
         JZ    MONSR36                                                          
         LLC   R1,TSVCREQ          NON FF ROOT HAS BEEN SAVED HERE              
         SRL   R1,4                                                             
         STC   R1,DMCB+7                                                        
         OI    DMCB+7,X'F0'                                                     
*                                                                               
MONSR36  DS    0H                                                               
*&&DO                                                                           
         CLI   TSVCREQ+1,$CT       TEST FOR CONNECT                             
         JNE   MONSR38                                                          
         CLC   TSYM,=C'????????'   TEST FOR SPECIFIC LUID                       
         JNE   *+12                                                             
         OI    TTYPE2,TTYPE2BI     SET BIAS TERMINAL                            
         MVI   DMCB+7,X'D0'        SET BIAS CONNECT SCREEN                      
*&&                                                                             
MONSR38  GOTO1 VCALLOV,DMCB        LOAD S/R ROOT SCREEN INTO TWA                
         CLI   4(R1),X'FF'                                                      
         JNE   *+12                                                             
         TM    TTEST1,TTESTSRY     IGNORE IF IN CIL MODE                        
         JZ    MONERR3             ERROR CANT READ PHASE FROM PRGMS             
*                                                                               
         CLI   TSVCREQ+1,$SRERR    TEST FOR SRERR                               
         JNE   *+14                NO                                           
         MVC   TSVCREQ,$$BYE       FORCE NO MATCH NEXT TIME IN                  
         J     MONSR126            GO TRANSLATE AND EXIT                        
*                                                                               
         TM    TSTAT2,TSTATNIT     TEST TRM NOT INITIALIZED                     
         JZ    MONSR46                                                          
*                                                                               
         L     R4,TCBTWA           CLEAR LOGO                                   
         LA    R4,64(R4)                                                        
         XR    R0,R0                                                            
         XR    R1,R1                                                            
*                                                                               
MONSR40  IC    R0,0(R4)                                                         
         AR    R4,R0                                                            
         LTR   R1,R1               TEST IF LOCATED S/R FIELD                    
         JNZ   MONSR42                                                          
*                                                                               
         L     R1,VSSB             TEST IF SHOWING SESSION ON SCREEN            
         CLI   SSMAX+1,0                                                        
         JE    MONSR42                                                          
         CLI   0(R4),X'19'         CHECK THAT S/R FIELD LOOKS OK                
         JNE   MONSR42                                                          
         CLC   2(2,R4),=X'003E'                                                 
         JNE   MONSR42                                                          
         MVC   8(8,R4),=C'=CT,*  *'                                             
         XC    16(9,R4),16(R4)     SET S/R FIELD TO =CT,*FS*                    
         MVC   13(1,R4),SSBSYSN1-SSBD(R1)                                       
         IC    R1,TSESSION                                                      
         LA    R1,1(R1)                                                         
         STC   R1,14(R4)                                                        
         OI    14(R4),X'80'        SET LOWER CASE SESSION LETTER                
*                                                                               
MONSR42  CLI   0(R4),0                                                          
         JE    MONSR44                                                          
         CLI   0(R4),9             TEST IF LOCATED ONE BYTE TAB FIELD           
         JNE   MONSR40                                                          
         MVC   9(3,R4),=X'000101'  TRUNCATE LOGO                                
*                                                                               
MONSR44  TM    TTYPE2,TTYPEMQ      TEST FOR MQ MESSAGE                          
         JO    MONSR46                                                          
         MVC   TSVCREQ,$$BYE       WRITE TWA SO $RE POSSIBLE                    
         BRAS  RE,PUTTWA                                                        
         JNZ   MONERR2             ERROR CANT WRITE TWA TO TEMPSTR              
         J     MONSR126            GO TRANSLATE AND EXIT                        
*                                                                               
MONSR46  L     R4,TCBTWA           COMPLETE TWA HDR                             
         USING TWAD,R4                                                          
         MVI   TWATASK,0                                                        
         MVC   TWATRM,TNUM                                                      
         TM    DEVCTL,DEVPRN                                                    
         JNZ   MONSR48             NO TRANSLATE FOR PRINTERS                    
         TM    TTYPE2,TTYPE2SF+TTYPE2SC                                         
         JNZ   MONSR48             OR STRUCTURED FIELDS/CONVERSATION            
*                                                                               
         GOTO1 TRINADR,P1,TCBTIA,TCBTWA,(R3)                                    
         ORG   *-2                                                              
         BRAS  R9,MONGOTO                                                       
         MVC   TRNOUT,0(R1)        EXTRACT TRANSLATOR TWA DATA                  
*                                                                               
         TM    TSTAT6,TST6STSS                                                  
         JNZ   *+12                                                             
         TM    TSTAT8,TST8STSS     TEST FOR NEW STEREO SCREENS                  
         JZ    MONSR48                                                          
         LA    R1,TRNOUT                                                        
         USING TIOBD,R1                                                         
         TM    TIOBCNT,X'80'       TEST FOR FATISTR ERROR RETURN                
         JZ    MONSR48                                                          
         NI    TIOBCNT,255-X'80'                                                
         DROP  R1                                                               
*                                                                               
* IF FATISTR ERROR THEN GO STRAIGHT TO $DONE                                    
*                                                                               
         MVI   TSVCREQ+1,$DONE                                                  
         MVC   TCBPRG,TSVCREQ+1                                                 
         L     RE,TCBTWA           SET TO LOAD THE S/R FF SCREEN                
         LA    RE,64(RE)                                                        
         ST    RE,DMCB+0                                                        
         MVI   DMCB+4,C'R'                                                      
         MVI   DMCB+5,X'01'                                                     
         MVC   DMCB+6(1),TSVCREQ+1                                              
         MVI   DMCB+7,X'FF'                                                     
         XC    DMCB+8(4),DMCB+8                                                 
         GOTO1 VCALLOV,DMCB        LOAD S/R ROOT SCREEN INTO TWA                
         CLI   4(R1),X'FF'                                                      
         JNE   *+12                                                             
         TM    TTEST1,TTESTSRY     IGNORE IF IN CIL MODE                        
         JZ    MONERR3             ERROR CANT READ PHASE FROM PRGMS             
*                                                                               
MONSR48  XC    TCBSWTAB(TCBSWMAX*TCBSWLEN),TCBSWTAB                             
         MVI   TCBSWNUM,1          SET SWITCHED TO SE1                          
         MVI   TCBSWSYS,1                                                       
         MVI   TCBSWSOV,1                                                       
*                                                                               
         XC    DMCB(12),DMCB       LOAD S/R ROOT PHASE                          
         GOTO1 VCALLOV,DMCB                                                     
         CLI   4(R1),X'FF'                                                      
         JNE   *+12                                                             
         TM    TTEST1,TTESTSRY     IGNORE IF IN CIL MODE                        
         JZ    MONERR3             ERROR CANT READ PHASE FROM PRGMS             
*                                                                               
         L     RE,TCBTWA                                                        
         LA    RE,64(RE)                                                        
         LLC   R0,0(RE)                                                         
         AR    RE,R0                                                            
         MVC   SAVEFUNP,8(RE)      SAVE S/R NAME                                
         MVC   SVSVCREQ,TSVCREQ+1  SAVE S/R NUMBER                              
         MVI   SRPAR1,0                                                         
         LA    RE,XTRAINFO                                                      
         ST    RE,SRPAR9                                                        
         BRAS  RE,SETXTRA          SET UP XTRAINFO FOR S/R                      
*                                                                               
         BRAS  RE,SETDEBUG         SET DEBUG HOOK                               
*                                                                               
GOTOSR   EQU   *                   PASS CONTOL TO S/R PROGRAM                   
         GOTO1 DMCB,SRPAR1                                                      
         ORG   *-2                                                              
         BRAS  R9,MONGOTO                                                       
*                                                                               
         XC    APRG,APRG           RESET A(PROGRAM TABLE ENTRY)                 
         XR    RE,RE                                                            
         ICM   RE,7,TAPRG          CONNECTED TO AN APPLICATION?                 
         JZ    MONSR50             NO                                           
         XR    RF,RF                                                            
         ICM   RF,7,TARSYS         A(CONNECTED SELIST)                          
         JZ    MONSR50             NOT SET                                      
         ST    RE,APRG             SET A(PROGRAM TABLE ENTRY)                   
*                                                                               
         L     R1,VSSB             TAPRG IS DISPLACEMENT?                       
         TM    SSBSTAT4-SSBD(R1),SSBPGDSP                                       
         JZ    MONSR50             NO-REAL ADDRESS                              
         ICM   RF,15,SEPGMS-SELISTD(RF)                                         
         AR    RE,RF                                                            
         ST    RE,APRG                                                          
*                                                                               
MONSR50  TM    TSTAT6,TST6SCRP     TEST IF PROCESSING A SCRIPT                  
         JO    MONSR52                                                          
         TM    TSTAT6,TST6STSS                                                  
         JO    *+12                                                             
         TM    TSTAT8,TST8STSS                                                  
         JNO   *+14                                                             
         MVC   TROUTADR,=V(TOSTR)  SET STEREO OUTPUT TRANSLATOR                 
         J     MONSR52                                                          
         MVC   TROUTADR,=V(TO3270) SET 3270 TRANSLATOR                          
*                                                                               
MONSR52  MVI   GOBACK,0            CLEAR GOBACK FLAG                            
         CLI   SVSVCREQ,$SRPSWD                                                 
         JNE   MONSR54                                                          
         TM    TSTAT5,TST5PSWD     TEST PASSWORD STILL PENDING                  
         JO    MONSR54             YES-CONTINUE                                 
         MVC   TCBPRG,TSVCREQ+1    FIX PROGAM NUMBER IN TCB                     
         JZ    MONSR               AND THEN EXECUTE NEW SVC REQ                 
*                                                                               
MONSR54  TM    TCBLOCK,X'80'       DID S/R LOCK ANYTHING                        
         JZ    MONSR56                                                          
         XC    DMCB(8),DMCB        YES-FREE LOCKTAB ENTRYS                      
         GOTO1 ALOCKER,DMCB                                                     
*                                                                               
MONSR56  TM    TCBFLAG2,TCBLOKUP                                                
         JZ    MONSR58                                                          
         GOTO1 ALOCKUP,DMCB,(C'C',0)                                            
*                                                                               
MONSR58  GOTO1 ADMRCVR,DMCB,0,0,X'00FF0000'                                     
*                                                                               
         TM    TCBFLAG2,TCBENQCT   DEQUEUE CTRL SYSTEM IF ENQUEUED              
         JZ    MONSR60                                                          
         GOTO1 AENQCTL,DMCB,(C'D',=C'CTRL')                                     
MONSR60  EQU   *                                                                
*&&UK                                                                           
         TM    TCBFLAG2,TCBENQMZ   DEQUEUE MEDZ SYSTEM IF ENQUEUED              
         JZ    MONSR62                                                          
         GOTO1 AENQCTL,DMCB,(C'D',=C'MEDZ')                                     
MONSR62  EQU   *                                                                
*&&                                                                             
*&&US                                                                           
         TM    TCBFLAG2,TCBENQEW   DEQUEUE EASI FILE IF ENQUEUED                
         JZ    MONSR64                                                          
         GOTO1 AENQDEQ,DMCB,(C'D',=C'EASI')                                     
*&&                                                                             
MONSR64  TM    TCBFLAG2,TCBENQFW   DEQUEUE FACW FILE IF ENQUEUED                
         JZ    MONSR65                                                          
         GOTO1 AENQDEQ,DMCB,(C'D',=C'FACW')                                     
*                                                                               
MONSR65  TM    TCBFLAG1,TCBENQWK   DEQUEUE WRKR FILE IF ENQUEUED                
         JZ    MONSR65A                                                         
         GOTO1 AENQDEQ,DMCB,(C'D',=C'WRKR')                                     
*                                                                               
MONSR65A OC    TCBISGQ,TCBISGQ     LET GO OF ENQUEUES FOR TASK                  
         JZ    MONSR65B                                                         
         MVI   DMCB+3,C'K'         CLEAN TASK                                   
         LA    RF,=CL5'ALL'                                                     
         ST    RF,DMCB+4                                                        
         GOTO1 =V(DMISGENQ),DMCB,,,0                                            
*                                                                               
MONSR65B TM    TCBFLAG1,TCBENQPQ   DEQUEUE PRTQ FILE IF ENQUEUED                
         JZ    MONSR66                                                          
         MVC   DMCB+16(4),=C'PRTQ'                                              
         MVC   DMCB+20(1),TCBPQCHR                                              
         GOTO1 AENQDEQ,DMCB,(C'D',DMCB+16)                                      
*                                                                               
MONSR66  TM    TCBFLAG2,TCBENQWF   DEQUEUE WRKF FILE IF ENQUEUED                
         JZ    MONSR67                                                          
         MVC   DMCB+16(4),=C'WRKF'                                              
         MVC   DMCB+20(1),TCBWFCHR                                              
         GOTO1 AENQDEQ,DMCB,(C'D',DMCB+16)                                      
*                                                                               
MONSR67  TM    TCBFLAG5,TCBENQWZ   DEQUEUE WRKZ FILE IF ENQUEUED                
         JZ    MONSR68                                                          
         MVC   DMCB+16(4),=C'WRKZ'                                              
         MVC   DMCB+20(1),TCBWZCHR                                              
         GOTO1 AENQDEQ,DMCB,(C'D',DMCB+16)                                      
*                                                                               
MONSR68  DS    0H                  SWITCH TO CONNECTED SYSTEM                   
         CLI   TSYS,0              TEST CONNECTED                               
         JE    MONSR70                                                          
         TM    TFLAG,TFLAGSSW      YES-TEST SWITCH ISSUED THIS TIME             
         JZ    MONSR70                                                          
         CLI   TCBSWSYS,1          DO NOT SWITCH TO SERVICE                     
         JE    MONSR70                                                          
         MVC   DUB(1),TCBSWSYS     SWITCH TO CONNECTED SYSTEM                   
         MVC   DUB+1(3),=X'FFFFFF' SET SPECIAL PLIST FOR FACPAK                 
         XC    DUB+4(4),DUB+4                                                   
         GOTO1 =V(FASWITCH),DUB                                                 
*                                                                               
MONSR70  CLI   SRPAR1,X'FE'        DID S/R CHANGE UTL ENTRY                     
         JL    MONSR74                                                          
         JH    MONSR72                                                          
         MVI   SRPAR1,0                                                         
         CLI   TSVCREQ+1,$CT       REQUEST TO RETURN TO $CT                     
         JE    MONSR02                                                          
*                                                                               
MONSR72  MVI   SRPAR1,0            RESET AND WRITE TWA/UTL                      
         BRAS  RE,PUTTWA                                                        
         JNZ   MONERR2             ERROR CANT WRITE TWA TO TEMPSTR              
*                                                                               
MONSR74  TM    DEVCTL,DEVPRN       IF PRINTER DO =DONE LATER                    
         JZ    *+12                                                             
         TM    TSTATU,TSTATDNE     =DONE SPECIAL ISSUED?                        
         JO    MONWRT2                                                          
*                                                                               
         TM    DEVCTL,DEVZERO      EXIT IF UTLZERO                              
         JO    MONWRTX                                                          
*                                                                               
         TM    TSTAT4,TST4SWAP     EXIT IF SWAP APPLID                          
         JO    MONWRTX                                                          
*                                                                               
         TM    DEVCTL,DEVSCP       TEST IF PROCESSING A SCRIPT                  
         JO    MONSR92                                                          
*                                                                               
         L     RE,TCBTWA           TEST IF GOBACK REQUESTED                     
         LA    RE,64(RE)                                                        
         LLC   R0,0(RE)                                                         
         AR    RE,R0               POINT TO FIRST UNP FIELD                     
         CLC   8(7,RE),=C'=GOBACK'                                              
         JNE   MONSR76             TEST S/R EXITING BACK TO PROGRAM             
         CLC   15(2,RE),=C',L'     TEST =CT EXITING WITH XFR CTRL               
         JE    MONSR78                                                          
         CLC   15(2,RE),=C',N'                                                  
         JE    MONSR80                                                          
         CLC   15(2,RE),=C',T'     TEST FOR GOBACK FROM TRANSFER XCTL           
         JE    MONSR80                                                          
*                                                                               
         CLC   15(2,RE),=C',A'     NOMINATED GOBACK                             
         JL    *+14                                                             
         CLC   15(2,RE),=C',H'     NOMINATED GOBACK                             
         JNH   MONSR80                                                          
*                                                                               
         MVI   GOBACK,C' '         SET NORMAL GOBACK                            
         MVI   8(RE),C'$'          MAKE SURE NO RANDOM MATCH NEXT TIME          
         L     R8,TBUFFM8                                                       
         MVI   4(R8),X'10'         SET GOBACK TO APPLICATION FLAG               
         LA    R8,8(R8)                                                         
         J     MONSR124            REQUEQUE DATA BACK TO APPLICATION            
*                                                                               
MONSR76  CLI   SVSVCREQ,$SW        TEST IF SWAPPING BETWEEN SESSIONS            
         JNE   MONSR92                                                          
         CLC   SAVEFUNP+1(2),=C'SS'                                             
         JNE   MONSR92                                                          
         CLC   SAVEFUNP+6(3),=C'XCT'                                            
         JNE   *+12                                                             
         MVI   GOBACK,C'W'         SET SWAP FLAG (W)                            
         J     MONSR82                                                          
*                                                                               
         CLC   SAVEFUNP+6(3),=C'XCS'                                            
         JNE   MONSR92                                                          
*                                                                               
         L     R8,TBUFFM8          SET GOBACK AND TRY FOR GLOBALS               
         USING TBHD,R8                                                          
         MVI   TBHINDS,TBHIBACK+TBHIGLOB                                        
         XC    5(3,R8),5(R8)                                                    
         XC    TBHDATA(17),TBHDATA CLEAR GOBACK FIELD DATA AREA                 
         J     MONSR122                                                         
*                                                                               
MONSR78  L     RF,SRPAR2           TIA CONTAINS =CT S/R DATA FIELD              
         L     R1,TBUFF            COPY TIA DATA TO HIGH IN TBUFF               
         MVC   1024(04,R1),=C'S/R='                                             
         MVC   1028(17,R1),0(RF)                                                
*                                                                               
MONSR80  MVC   GOBACK(1),16(RE)    SET CONNECT LAST OR NEW SESS (L/N)           
*                                                                               
MONSR82  LA    R0,GLOBAREA         POINT TO GLOBAL AREA IN TASK                 
         LA    R1,GLOBALEN                                                      
         L     RE,TCBTWA           POINT TO GLOBAL DATA IN TWA#0                
         AHI   RE,CHKPTGLD                                                      
         LR    RF,R1                                                            
         MVCL  R0,RE               MOVE TWA0 GLOBALS INTO TASK AREA             
         SR    R1,R1                                                            
         ICM   R1,3,GLOBXFR        TEST IF XFR CONTROL GLOBAL PRESENT           
         JZ    MONSR84                                                          
         LA    R1,GLOBAREA(R1)                                                  
         CLI   0(R1),GLVXCODQ      TEST IF STILL VALID                          
         JE    MONSR86                                                          
*                                                                               
MONSR84  MVI   GOBACK,C' '                                                      
         J     MONSR92                                                          
*                                                                               
         USING GLVXCTLD,R1                                                      
MONSR86  TM    GLVXFLG1,GLV1SEPD   DIALOG MODE REQUESTED?                       
         JZ    MONSR90             NO                                           
*                                                                               
*&&ML*&& LHI   RF,X'32'            *MLOG 32 DIALOG MODE CONNECT                 
*&&ML*&& BRAS  RE,MLOG             *MLOG                                        
*                                                                               
         LLC   R0,GLVXSESR         SET UP TO/FROM DIALOG PAIR IN BYTE           
         SLL   R0,4                                                             
         STC   R0,BYTE                                                          
         OC    BYTE,TSESSION                                                    
*                                                                               
         LHI   R0,L'TCNVPRS                                                     
         LA    RF,TCNVPRS                                                       
MONSR88  CLI   0(RF),0                                                          
         JNE   *+14                                                             
         MVC   0(1,RF),BYTE                                                     
         J     MONSR90                                                          
*                                                                               
         CLC   BYTE,0(RF)          SEE IF LINK ESTABLISHED ALREADY              
         JE    MONSR90                                                          
         AHI   RF,1                                                             
         BRCT  R0,MONSR88                                                       
         DC    H'0'                                                             
*                                                                               
MONSR90  CLI   GOBACK,C'W'         TEST IF RETURNING VIA =SS,?,XCT              
         JNE   MONSR92                                                          
*                                                                               
         L     R8,TBUFFM8          SET GOBACK AND TRY FOR GLOBALS               
         USING TBHD,R8                                                          
         MVI   TBHINDS,TBHIBACK+TBHIGLOB                                        
         XC    5(3,R8),5(R8)                                                    
         XC    TBHDATA(17),TBHDATA CLEAR GOBACK FIELD DATA AREA                 
         J     MONSR122                                                         
         DROP  R8                                                               
*                                                                               
MONSR92  CLC   TSVCREQ,$$IAM       EXIT IF S/R LOADED NEW SCREEN                
         JNE   MONSR94                                                          
         XC    TSVCREQ,TSVCREQ     CLEAR TO ENABLE PF12/24                      
         J     MONSR126                                                         
*                                                                               
MONSR94  CLC   TSVCREQ,NULLS       EXIT IF S/R DID NOT CONNECT                  
         JNE   MONSR126                                                         
         TM    DEVCTL,DEVPRN       PRINTER ABENDED                              
         JO    MONSR126                                                         
*                                                                               
MONSR96  OI    MONFLAGS,X'80'      SET WE HAVE JUST CONNECTED                   
         MVI   TWATASK,0           BUILD TWA HEADER ON CONNECT                  
         MVC   TWAOFFC,TOFFCODE                                                 
         MVC   TWATRM,TNUM                                                      
         MVC   TWASAGN,TSAGN                                                    
         MVC   TWAACCS,TACCS                                                    
         MVC   TWAUSRID,TUSER                                                   
         MVC   TWAAUTH,TAUTH                                                    
         MVC   TWAAGY,TAGY                                                      
         MVC   TCBOVSYS,TOVSYS     SET TCB SYS/PRG PARAMS                       
         MVC   TCBSEN,TSYS                                                      
         MVC   TCBPRG,TPRG                                                      
*                                                                               
MONSR98  TM    TTEST,TTESTTAC      CONNECT UNDER IAM TEST ID                    
         JZ    MONSR100                                                         
         MVC   DMCB(4),TACCS       YES-SET LIMIT-ACCESS FROM TSTTAB             
         L     RE,DMCB                                                          
         USING TSTTABD,RE                                                       
         MVC   TWAACCS,TSTTACCS                                                 
         DROP  RE                                                               
*                                                                               
MONSR100 L     R4,TCBTWA           CLEAR TWA EXCEPT FOR GLOBAL+CHKPNT           
         USING TWAD,R4                                                          
         LA    RE,16(R4)           AND FIRST 16 BYTES HEADER INFO               
         LH    RF,GLODSP                                                        
         AHI   RF,-16                                                           
         XR    R1,R1                                                            
         MVCL  RE,R0                                                            
         LHI   RE,TWAACCS2         POINT TO HEADER EXTENSION AREA               
         AR    RE,R4                                                            
         MVC   0(4,RE),TACCS2      SET SECOND LIMIT ACCESS CODE                 
*                                                                               
MONSR102 XC    DMCB(8),DMCB        LOAD APPLIC PROGRAM FF SCREEN                
         L     RE,TCBTWA                                                        
         LA    RE,64(RE)                                                        
         ST    RE,DMCB             READ VIRGIN SCREEN INTO TWA+64               
         MVI   DMCB+4,C'R'                                                      
         MVC   DMCB+5(1),TCOSYS    UTL HAS CALLOV SYSTEM                        
         MVC   DMCB+6(1),TCBPRG    TCB HAS CALLOV PROGRAM                       
         MVI   DMCB+7,X'FF'                                                     
         XC    DMCB+8(4),DMCB+8                                                 
         GOTO1 VCALLOV,DMCB                                                     
         CLI   4(R1),X'FF'                                                      
         JNE   *+12                                                             
         TM    TTEST,TTESTCIL      IGNORE IF IN CIL MODE                        
         JZ    MONERR3             ERROR CANT READ PHASE FROM PRGMS             
*                                                                               
MONSR104 ICM   RE,15,APRG          TEST IF PROGRAM VERSION MESSAGE              
         JZ    MONSR106                                                         
         TM    PGMVFLG-PGMLSTD(RE),PGMVDATE+PGMVHEAD                            
         JNO   MONSR106                                                         
*                                                                               
         L     RF,TCBTIA           =CT RETURNS PROVER MSG AT TIA+500            
         AHI   RF,500                                                           
         CLC   0(8,RF),NULLS       TEST IF ANY PROVER MESSAGE                   
         JE    MONSR106                                                         
*                                                                               
         L     RE,TCBTWA                                                        
         LA    RE,64(RE)           RE=A(FIRST FIELD)                            
         USING FHD,RE                                                           
         MVC   FHDA(60),0(RF)      MOVE IN PROVER MESSAGE                       
         OI    FHOI,FHOITR         SET TRANSMIT OUTPUT BIT                      
         NI    FHOI,255-FHOILO                                                  
         OI    MONFLAGS,X'40'      SET PROVER MESSAGE MOVED TO TWA              
         CLI   60(RF),C'N'         TEST FOR HIGH INTENSITY FLAG                 
         JE    MONSR106                                                         
         OI    FHOI,FHOIHI         SET HIGH INTENSITY OUTPUT BIT                
         OI    MONFLAGS,X'20'                                                   
         DROP  RE                                                               
*                                                                               
MONSR106 XR    RF,RF               FIND FIRST UNPROTECTED FIELD                 
         L     RE,TCBTWA                                                        
         LA    RE,64(RE)                                                        
         USING FHD,RE                                                           
         TM    FHAT,FHATPR                                                      
         JZ    *+14                                                             
         IC    RF,FHLN                                                          
         AR    RE,RF                                                            
         J     *-14                                                             
*                                                                               
         L     RF,SRPAR2           POINT TO TIA                                 
         OI    FHOI,FHOITR                                                      
         MVI   FHOL,17                                                          
         MVC   FHDA(17),0(RF)      MOVE CONNECT DATA TO SVCREQ FIELD            
         TM    TSTAT6,TST6STRO                                                  
         JZ    MONSR108                                                         
         MVI   FHDA,C' '                                                        
         MVC   FHDA+1(16),FHDA                                                  
         MVI   FHDA,C'*'           OUTPUT TRANSLATOR SETS STEREO DATA           
         DROP  RE                                                               
*                                                                               
MONSR108 TM    DEVCTL,DEVSCP       TEST IF HAVE CONNECTED VIA A SCRIPT          
         JZ    MONSR110            NO                                           
*                                                                               
         L     RE,TBUFF            SET THIS TRANSACTION IS A CONNECT            
         OI    TBHCNTL-TBHDATA(RE),TBHCCTTH                                     
         NI    DEVCTL,255-DEVNWT   SET TO WRITE TEMPSTR RECORD                  
         BRAS  RE,PUTTWA                                                        
         JNZ   MONERR5                                                          
         OI    DEVCTL,DEVNWT                                                    
         J     MONTRN                                                           
*                                                                               
MONSR110 DS    0H                                                               
*&&US                                                                           
         XR    R0,R0               FORCE ERASE WRITE                            
         IC    R0,0(RE)                                                         
         AR    RE,R0                                                            
         CLI   0(RE),0                                                          
         JNE   *-10                                                             
         MVI   1(RE),1             SET ERASE BEFORE AND AFTER                   
         MVI   2(RE),1                                                          
*&&                                                                             
MONSR112 TM    TTYPE2,TTYPEMQ      TEST IF MQ MESSAGE                           
         JZ    MONSR114                                                         
         L     RE,TBUFF            SET THIS TRANSACTION IS A CONNECT            
         OI    TBHCNTL-TBHDATA(RE),TBHCCTTH                                     
         NI    DEVCTL,255-DEVNWT   SET TO WRITE TEMPSTR RECORD                  
         BRAS  RE,PUTTWA                                                        
         JNZ   MONERR5                                                          
         J     MONAP               GO DIRECT TO APPLICATION PROCESSING          
*                                                                               
MONSR114 CLI   GOBACK,C'A'         NOMINATED GOBACK SESSION (1-8)               
         JL    *+12                                                             
         CLI   GOBACK,C'H'                                                      
         JNH   MONSR116                                                         
*                                                                               
         CLI   GOBACK,C'L'         TEST IF XFR CTRL TYPE CONNECT                
         JL    MONSR118                                                         
*                                                                               
MONSR116 L     R8,TBUFFM8          SET GOBACK + TRY FOR GLOBALS                 
         USING TBHD,R8                                                          
         MVI   TBHINDS,TBHIBACK+TBHIGLOB                                        
         XC    5(3,R8),5(R8)                                                    
         XC    TBHDATA(17),TBHDATA CLEAR GOBACK FIELD DATA AREA                 
         CLI   GOBACK,C'L'                                                      
         JE    MONSR124            DONT WRITE TEMPSTR IF SAME SESSION           
         J     MONSR122                                                         
*                                                                               
MONSR118 CLC   20(8,RF),NULLS      WAS DATA PASSED WITH $CT                     
         JNE   MONSR120                                                         
*                                                                               
         ICM   RE,15,APRG          TEST IF PRG GETS CONTROL ON =CT              
         TM    PGMIND2-PGMLSTD(RE),PGMIGOCT                                     
         JO    *+12                                                             
         CLI   17(RF),C'+'         TEST TO TRY FOR GLOBALS                      
         JNE   MONTRN                                                           
*                                                                               
         L     R8,TBUFFM8          SET $CT INPUT + TRY FOR GLOBALS              
         USING TBHD,R8                                                          
         MVI   TBHINDS,TBHIREIN+TBHIGLOB                                        
         XC    5(3,R8),5(R8)                                                    
         J     MONSR122                                                         
*                                                                               
MONSR120 L     R8,TBUFFM8          COPY DATA TO TERMINAL BUFFER                 
         USING TBHD,R8                                                          
         MVI   TBHINDS,TBHIREIN    SET MESSAGE TYPE                             
         MVI   5(R8),0                                                          
         LHI   R0,80                                                            
         STH   R0,TBHMSGL                                                       
         MVC   TBHDATA(80),20(RF)                                               
         DROP  R8                                                               
*                                                                               
MONSR122 ICM   R8,15,TBUFF                                                      
         BRAS  RE,PUTTWA                                                        
         JNZ   MONERR2                                                          
*                                                                               
MONSR124 OI    TTORAOR,TTAGOBAK    SET WANT GOBACK                              
         J     MONWRTX                                                          
*                                                                               
MONSR126 TM    DEVCTL,DEVPRN       NO OUTPUT TRANSLATE FOR PRINTERS             
         JNZ   MONWRT2                                                          
         TM    TTYPE2,TTYPEMQ      TEST IF MQ MESSAGE                           
         JO    MONWRT                                                           
         TM    TTYPE2,TTYPE2SF     OR FOR STRUCTURED FIELDS                     
         JO    MONWRT2                                                          
         TM    TTYPE2,TTYPE2SC     OR FOR STRUCTURED CONVERSATION               
         JO    MONWRT2                                                          
         LA    RF,1                SET PARM TO XMIT ALL FIELDS                  
         CLI   TSVCREQ+1,$CC                                                    
         JNE   *+6                 EXCEPT FOR $CC CALCULATOR                    
         SR    RF,RF                                                            
         CLI   TSVCREQ+1,$ABEND                                                 
         JNE   MONSR128                                                         
         XC    TRNOUT,TRNOUT                                                    
         TM    DEVCTL,DEVSCP       EXIT IF ABEND IN SCRIPT PROCESSING           
         JO    MONWRTX                                                          
*                                                                               
MONSR128 GOTO1 TROUTADR,P1,((RF),TCBTWA),TBUFF,(R3),TRNOUT                      
         ORG   *-2                                                              
         CLI   TSVCREQ+1,$ABEND                                                 
         JNE   *+10                                                             
         MVC   TSVCREQ,$$BYE       TRY TO PREVENT ABEND LOOP TO XLATE           
         BRAS  R9,MONGOTO          PASS CONTROL TO PROGRAM                      
*                                                                               
         MVC   SVOUT,P1                                                         
         CLI   0(R1),X'FF'                                                      
         JNE   MONWRT                                                           
         J     MONERR4             ERROR CANT TRANSLATE OUTPUT                  
         EJECT                                                                  
***********************************************************************         
* TRANSLATE OUTPUT AND WRITE TWA                                      *         
***********************************************************************         
MONTRN   TM    TSTAT5,TST5IOCT     TEST TO DISPLAY I/O COUNT                    
         JZ    MONTRN4                                                          
         SR    R0,R0                                                            
         ICM   R0,7,TCBIOCNT                                                    
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         L     RE,TCBTWA                                                        
         LA    RE,64(RE)                                                        
         LLC   R0,0(RE)                                                         
         AR    RE,R0               POINT TO S/R INPUT AREA                      
         OI    6(RE),X'80'         SET TRANSMIT FLAG                            
         MVC   16(4,RE),=C' IO='                                                
         UNPK  20(5,RE),DUB                                                     
*                                                                               
MONTRN4  TM    TSTATC,TSTCCPU      TEST TO DISPLAY CPU                          
         JZ    MONTRN5                                                          
         SR    R0,R0                                                            
         ICM   R0,15,TCBCPUTM                                                   
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         L     RE,TCBTWA                                                        
         LA    RE,64(RE)                                                        
         LLC   R0,0(RE)                                                         
         AR    RE,R0               POINT TO S/R INPUT AREA                      
         OI    6(RE),X'80'         SET TRANSMIT FLAG                            
         MVC   10(12,RE),=C' ET=00000000'                                       
         UNPK  14(8,RE),DUB                                                     
*                                                                               
MONTRN5  SR    RF,RF               SET TRANSMIT ONLY CHANGED FIELDS             
         TM    TSTAT6,TST6STSS                                                  
         JO    *+12                                                             
         TM    TSTAT8,TST8STSS                                                  
         JNO   MONTRN6                                                          
         L     RE,TBUFFM8                                                       
         TM    4(RE),TBHISTSS      TEST DYNAMIC SESSION CHANGE ON INPUT         
         JZ    MONTRN6                                                          
         LA    RF,1                YES-SHIP ALL SOFT FIELDS                     
         NI    4(RE),255-TBHISTSS                                               
*                                                                               
MONTRN6  TM    TTYPE2,TTYPE2SC     STRUCTURED CONVERSATION                      
         JO    MONTRN10                                                         
         GOTO1 TROUTADR,P1,((RF),TCBTWA),TBUFF,(R3),TRNOUT                      
         ORG   *-2                                                              
         BRAS  R9,MONGOTO                                                       
         MVC   SVOUT,P1                                                         
         CLI   0(R1),X'FF'                                                      
         JE    MONERR4             ERROR CANT TRANSLATE OUTPUT                  
*                                                                               
MONTRN10 CLI   TSVCREQ+1,$UNWIND                                                
         JNE   MONTRN12                                                         
         OI    DEVCTL,DEVNWT                                                    
         LR    R1,RC               POINT TO W/S                                 
         BRAS  RE,UNWGLBLS         READ TWA/UPDATE GLOBALS                      
*                                                                               
MONTRN12 BRAS  RE,PUTTWA                                                        
         JNZ   MONERR2             ERROR CANT WRITE TWA TO TEMPSTR              
         EJECT                                                                  
***********************************************************************         
* ISSUE WRITE TO TERMINAL                                             *         
***********************************************************************         
MONWRT   GOTO1 =V(SSBFIX),TCBD                                                  
*                                                                               
         L     RE,TBUFF                                                         
         AHI   RE,-9                                                            
         MVI   0(RE),0                                                          
*                                                                               
         TM    DEVCTL,DEVSCP       TEST IF EXECUTING A SCRIPT                   
         JO    MONWRT3                                                          
         TM    TTYPE2,TTYPEMQ      TEST MQ MESSAGE                              
         JO    MONWRT1                                                          
*                                                                               
         TM    TSTAT2,TSTATBCP     TEST BROADCAST PENDING                       
         JZ    MONWRT1                                                          
         TM    TFLAG,TFLAGIRB      TEST USER WANTS BROADCASTS                   
         JNZ   MONWRT1                                                          
         CLC   TSVCREQ,NULLS       TEST IF INPUT WAS A S/R                      
         JNE   MONWRT1                                                          
         CLC   TSYS(2),=X'0A0C'    TEST CONTROL/MAD                             
         JE    MONWRT1                                                          
*                                                                               
         TM    TSTAT6,TST6STFU     TEST STEREO EMULATOR MODE                    
         JZ    MONWRT0                                                          
         TM    TSTAT8,TST8PC32     TEST 32 BIT PC MODE                          
         JO    MONWRT1                                                          
*NOP*    TM    TSTAT8,TST8STSS     TEST STEREO SOFT FIELDS                      
*NOP*    JO    MONWRT1                                                          
*                                                                               
MONWRT0  L     R8,=V(SELISTCT)     SEND BCAST IF CTRL SYSTEM IS UP              
         TM    SEIND-SELISTD(R8),SEISTRT                                        
         JZ    MONWRT1                                                          
         TM    SEIND-SELISTD(R8),SEINOP                                         
         JO    MONWRT1                                                          
*                                                                               
         ICM   R8,15,TBUFFM8       R8=A(TBUFF)                                  
         JZ    MONWRTX                                                          
         MVI   4(R8),X'10'         SET GOBACK FLAG                              
         AHI   R8,8                                                             
         MVC   0(8,R8),BRDMSG                                                   
         OI    TTORAOR,TTAGOBAK                                                 
         J     MONWRTX                                                          
*                                                                               
MONWRT1  ICM   RE,15,TBUFFM8       GET A(BUFFER) FOR TERMINAL                   
         JZ    MONWRTX                                                          
         MVC   5(1,RE),SVOUT       SET WRITE TYPE X'01'=WRITE ERASE             
         TM    TRNOUT+TIOBINDS-TIOBD,TIOBALRM                                   
         JZ    *+8                                                              
         OI    5(RE),X'02'         SET SOUND ALARM FLAG                         
         MVC   TCBMSGO,6(RE)       SET OUTPUT MSG LEN                           
*                                                                               
MONWRT2  ICM   RE,15,TBUFFM8       GET A(BUFFER) FOR PRINTER                    
         JZ    MONWRTX                                                          
         MVC   TCBMSGO,6(RE)       SET OUTPUT MSG LEN                           
*                                                                               
MONWRT3  TM    TSTAT4,TST4SWAP     EXIT IF SWAP                                 
         JO    MONWRTX                                                          
*                                                                               
MONWRT4  TM    DEVCTL,DEVSCP       TEST IF EXECUTING A SCRIPT                   
         JO    MONWRTX                                                          
         TM    TTYPE2,TTYPEMQ      TEST IF MQ MESSAGE                           
         JO    MONWRT5             GOTO SPECIAL PROCESSING                      
*                                                                               
         L     RE,TBUFF                                                         
         AHI   RE,-9                                                            
         MVI   0(RE),X'FF'                                                      
         J     MONWRTX                                                          
*                                                                               
MONWRT5  L     RE,TBUFFM8          HERE FOR TASK IN MQ MODE                     
         USING TBHD,RE                                                          
         TM    TBHINDS,X'02'       TEST MQ RETURN DATA IN BUFFER                
         JZ    MONWRTX                                                          
         XR    RF,RF                                                            
         ICM   RF,3,6(RE)                                                       
         L     R2,TBUFF                                                         
         DROP  RE                                                               
*                                                                               
         GOTO1 AMQIO,DMCB,=CL8'REPLY',(R2),(RF)                                 
*                                                                               
MONWRTX  TM    TSTATC,TSTCREPU                                                  
         JZ    EXIT                                                             
         BRAS  RE,FORMOSA                                                       
         J     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
*SUBROUTINE TO PASS CONTROL TO TO EXTERNAL PROGRAM                    *         
*RF CONTAINS PROGRAM ENTRY POINT AND R9 CONTAINS RETURN ADDRESS       *         
***********************************************************************         
MONGOTO  STM   R2,RA,REGSAVE       CALLER'S R2-RA ARE SAVED                     
*                                                                               
         MVC   PROTFLGS,TCBPROT    SAVE STORAGE PROTECTION FLAGS                
         LA    R2,MONSPWRK                                                      
         ST    R2,TCBMONWK         SAVE A(WORK AREA IN MONITOR KEY 9)           
         LM    R2,RA,REGVALS       AND THEN SET TO FF'S FOR SAFETY              
*                                                                               
         TM    PROTFLGS,TCBPKAPQ   TEST STORAGE PROTECTION=YES                  
         JZ    MONG010                                                          
         SPKA  144                 SWITCH TO APPLCIATION KEY 9                  
*                                                                               
MONG010  BASR  RE,RF               PASS CONTROL TO PROGRAM                      
*                                  APP RUNNING IN KEY 9                         
MONGORTN TM    PROTFLGS,TCBPKAPQ   TEST STORAGE PROTECTION=YES                  
         JZ    MONG020                                                          
         IPK                                                                    
         NILL  GR2,X'00F0'                                                      
         STC   R2,SVPSWKEY                                                      
         CLI   SVPSWKEY,X'90'      OK IF STILL IN KEY 9                         
         JE    MONG016                                                          
         LM    R2,RA,REGSAVE       HAVE TO RELOAD ELSE REGS ARE CRAP            
         TM    TCBINDS1,TCBFABND   WAS IT AN ABEND                              
         JO    MONG016             YES-PROTECTION IS OFF                        
         ICM   RF,15,=V(GETEMAIL)                                               
         JZ    MONG016             DONT EMAIL IF ROUTINE NOT LINKED IN          
*                                                                               
         ICM   R2,15,APRG          HAVE PROGRAM ENTRY                           
         JZ    MONG016             CAN NOT DISABLE PROGRAM                      
         USING PGMLSTD,R2                                                       
         OI    PGMIND3,PGMIUNP     SET PROGRAM TO DISSABLE PROTECTION           
         DROP  R2                                                               
*&&US                                                                           
*TEMP                                                                           
         CLC   MONSPRB,NULLS       SOMETHING GOT BY US?                         
         JNE   MONG011             NO                                           
         MVC   MSGEMAIL,=CL40'AHYDN'                                            
         J     MONG011A                                                         
*TEMP                                                                           
*&&                                                                             
MONG011  GOTO1 ,DMCB,(R5),MSGEMAIL,(TCBOVSYS,L'MSGEMAIL)                        
         BASR  RE,RF                                                            
MONG011A LA    RF,MSGEMAIL-1       SCAN FOR END OF EMAIL TO                     
MONG011B LA    R5,L'MSGEMAIL(RF)                                                
         CLI   0(R5),X'40'                                                      
         JH    MONG012             FOUND IT                                     
         JCT   RF,MONG011B                                                      
         J     MONG016             SKIP IT                                      
*                                                                               
MONG012  MVI   MSGTXT,C' '                                                      
         MVC   MSGTXT+1(L'MSGTXT-1),MSGTXT                                      
         LA    R2,MSG2SND                                                       
         L     RF,VSSB             TAPRG IS DISPLACEMENT?                       
         MVC   4(3,R2),SSBSYSNA-SSBD(RF)                                        
         MVC   22(3,R2),TCBLNSYS                                                
         MVC   26(3,R2),TCBLNPRG                                                
*                                                                               
         CLC   MONSPRB,NULLS       SOMETHING GOT BY US?                         
         JE    MONG014                                                          
         MVC   30(8,R2),MONSPLAB   SET BY FAPROT                                
         L     RF,MONSPRB                                                       
         L     R1,MONSPPSW                                                      
         SR    R1,RF                                                            
         ST    R1,DUB                                                           
         GOTO1 AHEXOUT,DMCB,DUB+1,42(R2),3                                      
*                                                                               
MONG014  MVI   1(R5),C':'                                                       
         MVC   2(L'MSG2SND,R5),MSG2SND                                          
         LA    R2,MSGLN                                                         
         WTO   TEXT=(R2)                                                        
*                                                                               
MONG016  SPKA  128                 SWITCH BACK TO FACPAK KEY 8                  
         MVI   PROTFLGS,0          CLEAR STORAGE PROTECTION STATUS FLAG         
*                                                                               
MONG020  DS    0H                                                               
*                                                                               
MONBRPRG LM    R2,RA,REGSAVE       RE HAS THIS MAGIC ENTRY POINT                
*&&UK                                                                           
         CLC   TCBID(5),=C'*TASK'  *TEMP                                        
         JE    *+6                 *TEMP MAKE SURE TCB IS THERE                 
         DC    H'0'                *TEMP                                        
*&&                                                                             
         ICM   RF,15,TCBP1A        ANY P1 BREAKPOINT                            
         JZ    MONG025                                                          
         MVC   0(2,RF),TCBP1I      RESTORE IT                                   
         XC    TCBBREAK,TCBBREAK                                                
*                                                                               
MONG025  TM    TSTAT2,TSTATCAN     TEST IF CANCEL PENDING                       
         JZ    *+12                                                             
         CLI   TSVCREQ+1,$CAN      YES-DID TASKER CANCEL                        
         JE    MONCAN                                                           
         CLI   TSVCREQ+1,$ABEND    TEST IF TRANS ABEND'ED                       
         BNER  R9                  NO-RETURN TO CALLER                          
*                                                                               
MONG030  MVI   MONCOPY,C'Y'        MAKE SURE SCREEN COPIED BACK                 
         L     RE,TCBDTFLK                                                      
         LTR   RE,RE               WAS TASK HOLDING DNEXT                       
         JZ    *+14                                                             
         OI    2(RE),X'80'         YES-RELEASE DNEXT FOR FILE                   
         SR    RE,RE                                                            
         ST    RE,TCBDTFLK                                                      
         TM    DEVCTL,DEVZERO      CANT WRITE TWA IF UTLZERO                    
         BOR   R9                                                               
         TM    DEVCTL,DEVSCP       TEST IF SCRIPT HAS ABENDED                   
         JO    MONGOTOS                                                         
         BRAS  RE,PUTTWA                                                        
         JNZ   MONERR2             ERROR CANT WRITE TWA TO TEMPSTR              
         J     MONSR126            GO WRITE MESSAGE IN TWA TO TRM               
*                                                                               
MONGOTOS NI    DEVCTL,255-DEVNWT   SET TO WRITE TEMPSTR RECORD                  
         BRAS  RE,PUTTWA                                                        
         JNZ   MONERR2             ERROR CANT WRITE TWA TO TEMPSTR              
         J     MONSR126                                                         
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO BUILD "OK" MESSAGE TO PASS TO T17300                     *         
***********************************************************************         
FORMOSA  NTR1  BASE=*,LABEL=*                                                   
         NI    TSTATC,255-TSTCREPU                                              
*                                                                               
         GOTO1 =V(LCM),DMCB,VTGETUTL                                            
         LTR   R5,R1                                                            
         BZ    FOMO06                                                           
F        USING UTLD,R5                                                          
         SAM31                                                                  
         MVC   F.TLUID,=CL8'NONENONE'                                           
         MVC   F.TSVCREQ,=XL2'0173' SET SPECIAL S/R                             
         MVI   F.TSTATC,TSTCMQ                                                  
*                                                                               
         TIME  TU                                                               
         ST    R0,F.TTIMETU        SET ARRIVAL TIME TO NOW                      
*                                                                               
         ICM   RF,15,F.TUTLXADR                                                 
         ICM   RE,15,XAAPASSR-XAUTLD(RF)                                        
         STCM  RE,15,F.TBUFF       SET XAPASSR AREA AS DUMMY TBUFF              
         MVC   0(4,RE),=CL4'PASS'                                               
         MVC   4(4,RE),TSIN                                                     
*                                                                               
         L     R4,=V(SELIST)       SE1                                          
         AHI   R4,6                                                             
         USING SELISTD,R4                                                       
         CLC   SEFIRST,NULLS       QUEUE HAS MEMEBERS ALREADY?                  
         BNE   FOMO02              YES                                          
         STCM  R5,15,SEFIRST                                                    
         STCM  R5,15,SELAST                                                     
         LHI   RF,1                                                             
         STH   RF,SEQLEN                                                        
         B     FOMO04                                                           
*                                                                               
FOMO02   L     RF,SELAST                                                        
         STCM  R5,15,TXNEXTIN-UTLD(RF)                                          
         STCM  R5,15,SELAST                                                     
         LH    RF,SEQLEN                                                        
         AHI   RF,1                                                             
         STH   RF,SEQLEN                                                        
*                                                                               
FOMO04   J     EXIT                                                             
         DROP  F,R4                                                             
*                                                                               
FOMO06   DC    H'0'                PROBABLY NEEDS SOME ERROR HANDLING           
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO BUILD TWO TWA HEADER FIELDS TO HELP IDENTIFY AUTOMATICS  *         
***********************************************************************         
SETAUTO  NTR1  BASE=*,LABEL=*                                                   
         L     R4,TCBTWA           BUILD 2 TWA HDR FIELDS                       
         AHI   R4,64                                                            
         USING FHD,R4                                                           
         MVI   FHLN,60+FHDAD                                                    
         MVI   FHAT,FHATPR                                                      
         MVC   FHAD,=XL2'0001'     ((ROW-1)*80)+COLUMN    01,01                 
         MVI   FHOI,FHOITR                                                      
         MVI   FHOL,60                                                          
         XC    FHDA(60),FHDA                                                    
         MVC   FHDA(40),=CL40'*DDS* AUTOMATIC S/R - SEE UTL'                    
         AHI   R4,60+FHDAD                                                      
*                                                                               
         MVI   FHLN,17+FHDAD                                                    
         MVI   FHAT,FHATPR                                                      
         MVC   FHAD,=XL2'003E'     ((ROW-1)*80)+COLUMN    01,63                 
         MVI   FHOI,FHOITR                                                      
         MVI   FHOL,17                                                          
         XC    FHDA(17),FHDA                                                    
         MVC   FHDA(5),=CL5'*S/R=' SET IGNORE                                   
*                                                                               
         L     RF,TCBUTL                                                        
         MVI   DUB,1                                                            
         MVC   DUB+1(1),TSVCREQ+1                                               
         GOTO1 AHEXOUT,DMCB,DUB,FHDA+5,3                                        
         MVI   FHDA+5,C'T'                                                      
*                                                                               
         AHI   R4,17+FHDAD                                                      
         XC    0(3,R4),0(R4)       CLEAR AFTER THIS IN TWA                      
         J     EXITOK                                                           
         DROP  R4                                                               
         EJECT                                                                  
*&&US                                                                           
***********************************************************************         
* ROUTINE TO MOVE DARE NOTIFY MESSAGE TO TWA S/R FIELD                *         
***********************************************************************         
DARENOTE NTR1  BASE=*,LABEL=*                                                   
         ICM   R1,15,APRG          TEST IF PROGRAM NEEDS DARE NOTIFY            
         JZ    EXITOK                                                           
         TM    PGMIND3-PGMLSTD(R1),PGMIDARE                                     
         JZ    EXITOK                                                           
         TM    TSTATU,TSTATD4U     DARE FOR ME?                                 
         JZ    EXITOK                                                           
         NI    TSTATU,X'FF'-TSTATD4U                                            
*                                                                               
         L     R1,TCBTWA           NOTIFY USER OF JOB NAME                      
         LA    R1,64(R1)                                                        
         USING FHD,R1                                                           
         LLC   R0,FHLN                                                          
         AR    R1,R0                                                            
         OI    FHAT,FHATHI         SET HIGH INTENSITY & XMIT                    
         OI    FHOI,FHOITR                                                      
*                                                                               
         TM    TSTAT6,TST6STRO     TEST STEREO NOTIFY                           
         JO    DAREN50                                                          
         MVC   FHDA(17),=CL17'*DARE MAIL' FORMAT S/R FIELD FOR DARE             
*                                                                               
         LA    R4,DRMLTYPE                                                      
DAREN10  CLC   TDARETYP,6(R4)      DISPLAY 4-CHR DARE MAIL TYPE                 
         JE    DAREN20                                                          
         AHI   R4,L'DRMLTYPE                                                    
         CLI   0(R4),X'FF'                                                      
         JNE   DAREN10                                                          
         J     DAREN30             SKIP IF UNKNOWN TYPE                         
*                                                                               
DAREN20  MVC   FHDA+6(4),7(R4)     MOVE TYPE TO SCREEN                          
*                                                                               
DAREN30  ZAP   DUB,PZERO                                                        
         MVO   DUB,TTIMEDAR(1)                                                  
         OI    DUB+7,X'0F'                                                      
         UNPK  FHDA+12(2),DUB                                                   
         MVI   FHDA+14,C':'                                                     
         ZAP   DUB,PZERO                                                        
         MVO   DUB,TTIMEDAR+1(1)                                                
         OI    DUB+7,X'0F'                                                      
         UNPK  FHDA+15(2),DUB                                                   
         J     EXITL                                                            
*                                                                               
DAREN50  MVI   FHDA,C'*'           BUILD STERO NOTIFY                           
         MVC   FHDA+8(9),=CL9'DARE'                                             
         MVC   FHDA+12(1),TDARETYP                                              
*                                                                               
         ZAP   DUB,PZERO                                                        
         MVO   DUB,TTIMEDAR(1)                                                  
         OI    DUB+7,X'0F'                                                      
         UNPK  FHDA+13(2),DUB                                                   
         ZAP   DUB,PZERO                                                        
         MVO   DUB,TTIMEDAR+1(1)                                                
         OI    DUB+7,X'0F'                                                      
         UNPK  FHDA+15(2),DUB                                                   
         J     EXITL                                                            
         DROP  R1                                                               
*DRMAILTYP                                                                      
       ++INCLUDE DRMAILTYP                                                      
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
*SUBROUTINE TO READ TWA FROM TEMPSTR                                  *         
*TCB SYSTEM SWITCH TABLE IS REBUILT FROM TWA0 CHECKPOINT AREA         *         
*GLOBALS ARE MOVED INTO TASK AREA                                     *         
***********************************************************************         
GETTWA   NTR1  BASE=*,LABEL=*                                                   
         MVI   DMCB+8,0            SET OK RETURN CODE                           
*                                                                               
GETTWA1  TM    DEVCTL,DEVSCP       ONLY READ TWA FOR SCRIPT ON 1ST $CT          
         JZ    GETTWA1A                                                         
         L     RE,TBUFF                                                         
         TM    TBHCNTL-TBHDATA(RE),TBHCFRST+TBHCCTTH                            
         JNO   GETTWA2                                                          
*                                                                               
GETTWA1A XC    DMCB+8(4),DMCB+8    SET TEMPSTR PAGE=0                           
         MVC   DMCB+20(2),=C'L='   SPECIAL PARM FOR TWA0 READ/WRITES            
         MVC   DMCB+22(2),RECLEN                                                
         GOTO1 VDATAMGR,DMCB,=C'DMREAD',=C'TEMPSTR',,TCBTWA                     
         CLI   8(R1),0                                                          
         JNE   GETTWAX                                                          
*                                                                               
GETTWA2  CLI   GLOBSWS,0           TEST IF GLOBALS MOVED TO TASK                
         JNE   GETTWA2X                                                         
         LA    R0,GLOBAREA         POINT TO GLOBAL AREA IN TASK                 
         LA    R1,GLOBALEN                                                      
         L     RE,TCBTWA           POINT TO GLOBAL DATA IN TWA#0                
         AHI   RE,CHKPTGLD                                                      
         LR    RF,R1                                                            
         MVCL  R0,RE               MOVE TWA0 GLOBALS INTO TASK AREA             
         MVI   GLOBSWS,GLOBREAD    SET GLOBALS READ                             
*                                                                               
GETTWA2A L     RE,VSSB             CHECK GLOBALS DATE/TIME STAMP                
         CLC   GLOBDATE(8),SSBSDATE-SSBD(RE)                                    
         JNL   GETTWA2X            OK IF DATED AFTER FACPAK STARTED             
         LA    R0,GLOBAREA         INITIALISE GLOBAL INFO FOR THIS TRM          
         LA    R1,GLOBALEN                                                      
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         TIME  BIN                 R0=TIME(1/100 SEC),R1=DATE(JULIAN)           
*                                                                               
         ST    R1,GLOBDATE                                                      
         ST    R0,GLOBTIME                                                      
         MVI   GLOBSWS,GLOBREAD+GLOBUPDT                                        
*                                                                               
GETTWA2X CLI   GETWHY,1            EXIT IF SIMPLY READING FOR THESE             
         JE    GETTWAX                                                          
*                                                                               
GETTWA3  TM    DEVCTL,DEVSCP       TEST IF SCRIPT                               
         JZ    GETTWA3B                                                         
         L     RE,TBUFF                                                         
         USING TBHDATA,RE                                                       
         TM    TBHCNTL,TBHCCMT     IF COMMIT IN LAST TRANSACTION THEN           
         JZ    *+12                RESET RECOVERY INFO FOR THIS TRANS           
         NI    TBHCNTL,255-TBHCCMT                                              
         J     GETTWA3A                                                         
         TM    TBHCNTL,TBHCBALL    TEST IF ALL TRANS TREATED AS ONE             
         JO    GETTWAX             YES-DO NOTHING                               
GETTWA3A XC    TCBRCV,TCBRCV       INITIALISE FOR A NEW TRANS                   
         DROP  RE                                                               
*                                                                               
GETTWA3B XC    TCBSWNUM(1+(TCBSWMAX*TCBSWLEN)),TCBSWNUM                         
         XC    TCBXTINF,TCBXTINF                                                
         XC    TCBBILL,TCBBILL                                                  
*                                                                               
GETTWA4  L     R5,TCBTWA           RESTORE TCB SYSTEM SWITCH DATA               
         AH    R5,CHKDSP                                                        
         USING CHKPTD,R5           R5=A(TWA0 CHECKPOINT AREA)                   
         MVC   TCBXTINF,CHXTINF    RESTORE TEMPEST INFO                         
*                                                                               
         TM    TSTAT1,TSTATBIL                                                  
         JZ    *+10                                                             
         MVC   TCBBILL,CHBILL      RESTORE BILLING REF                          
*                                                                               
         SR    R0,R0                                                            
         ICM   R0,1,CHTCBNUM       R0=N'SYSTEM SWITCH ENTRIES                   
         JZ    GETTWA6                                                          
         CHI   R0,TCBSWMAX         TEST VALUE IN RANGE                          
         JNH   *+12                                                             
         MVI   DMCB+8,X'41'        RETURN FORMAT ERROR IN DMCB                  
         J     GETTWAX                                                          
         STC   R0,TCBSWNUM                                                      
         LA    RE,TCBSWTAB                                                      
         USING TCBSWTAB,RE         RE=A(TCB SYSTEM TABLE)                       
         LA    RF,CHTCBTAB                                                      
         USING CHTCBTAB,RF         RF=A(TWA CHKPT TABLE)                        
GETTWA5  MVC   TCBSWSYS,CHTCBSYS                                                
         MVC   TCBSWSOV,CHTCBSOV                                                
         MVC   TCBSWAGB,CHTCBAGB                                                
         MVC   TCBSWACS,CHTCBACS                                                
         LA    RE,TCBSWLEN(RE)                                                  
         LA    RF,CHTCBLEN(RF)                                                  
         BRCT  R0,GETTWA5                                                       
         DROP  RE,RF                                                            
*                                                                               
GETTWA6  CLC   CHUTLSYM(8),TCBSYM  CHECK THAT LUIDS MATCH                       
         JNE   GETTWA8                                                          
         MVC   TCBLNSYS,CHLNSYS    RESTORE LOGON SYSTEM/PROGRAM ID'S            
         MVC   TCBLNPRG,CHLNPRG                                                 
         MVC   TCBSRMSG,CHSRMSG                                                 
*                                                                               
GETTWA7  L     RE,VSSB             CHECK DATE/TIME WITH FACPAK START            
         CLC   CHTDDATE(8),SSBSDATE-SSBD(RE)                                    
         JNL   GETTWAX                                                          
*                                                                               
GETTWA8  LA    R1,DMCB+12          CALL V(TWASVR) TO DISCONNECT SESSION         
         XC    0(8,R1),0(R1)                                                    
         MVC   0(1,R1),TSESSION                                                 
         MVI   4(R1),X'C0'         SET DISCONNECT/TWA ALREADY READ              
         GOTO1 ATWASVR                                                          
         MVI   TSVCREQ,X'01'       SET TO PASS CONTROL TO $CT                   
         MVI   TSVCREQ+1,$CT                                                    
*                                                                               
GETTWAX  CLI   DMCB+8,0                                                         
         B     EXIT                                                             
         DROP  R5                                                               
         EJECT                                                                  
***********************************************************************         
*SUBROUTINE TO WRITE TWA TO TEMPSTR FILE                              *         
*SYSTEM IS CHECKPOINTED AT TWA0+CHKPTDSP (SEE FACHKPT)                *         
***********************************************************************         
PUTTWA   NTR1  BASE=*,LABEL=*                                                   
*                                                                               
PUTUNLK  DS    0H                  FREE LOCK TAB ENTRIES                        
         TM    TCBLOCK,X'80'       DID TASK LOCK ANYTHING                       
         JZ    PUTUNLK1                                                         
         XC    DMCB(8),DMCB        YES-FREE LOCKTAB ENTRIES                     
         GOTO1 ALOCKER,DMCB                                                     
*                                                                               
PUTUNLK1 TM    TCBFLAG2,TCBLOKUP                                                
         JZ    PUTUNLK2                                                         
         GOTO1 ALOCKUP,DMCB,(C'C',0)                                            
*                                                                               
PUTUNLK2 GOTO1 ADMRCVR,DMCB,0,0,X'00FF0000'                                     
*                                                                               
         TM    TCBFLAG2,TCBENQCT   DEQUEUE CTRL SYSTEM IF ENQUEUED              
         JZ    PUTUNLK4                                                         
         GOTO1 AENQCTL,DMCB,(C'D',=C'CTRL')                                     
PUTUNLK4 EQU   *                                                                
*&&UK                                                                           
PUTUNLK5 TM    TCBFLAG2,TCBENQMZ   DEQUEUE MEDZ SYSTEM IF ENQUEUED              
         JZ    PUTUNLK6                                                         
         GOTO1 AENQCTL,DMCB,(C'D',=C'MEDZ')                                     
PUTUNLK6 EQU   *                                                                
*&&                                                                             
*&&US                                                                           
PUTUNLK7 TM    TCBFLAG2,TCBENQEW   DEQUEUE EASI FILE IF ENQUEUED                
         JZ    PUTUNLK8                                                         
         GOTO1 AENQDEQ,DMCB,(C'D',=C'EASI')                                     
PUTUNLK8 EQU   *                                                                
*&&                                                                             
PUTUNLK9 TM    TCBFLAG2,TCBENQFW   DEQUEUE FACW FILE IF ENQUEUED                
         JZ    PUTUNLKA                                                         
         GOTO1 AENQDEQ,DMCB,(C'D',=C'FACW')                                     
*                                                                               
PUTUNLKA TM    TCBFLAG1,TCBENQWK   DEQUEUE WRKR FILE IF ENQUEUED                
         JZ    PUTUNLKB                                                         
         GOTO1 AENQDEQ,DMCB,(C'D',=C'WRKR')                                     
*                                                                               
PUTUNLKB OC    TCBISGQ,TCBISGQ     LET GO OF ENQUEUES FOR TASK                  
         JZ    PUTUNLKP                                                         
         MVI   DMCB+3,C'K'         CLEAN TASK                                   
         LA    RF,=CL5'ALL'                                                     
         ST    RF,DMCB+4                                                        
         GOTO1 =V(DMISGENQ),DMCB,,,0                                            
*                                                                               
PUTUNLKP TM    TCBFLAG1,TCBENQPQ   DEQUEUE PRTQ FILE IF ENQUEUED                
         JZ    PUTUNLKW                                                         
         MVC   DMCB+16(4),=C'PRTQ'                                              
         MVC   DMCB+20(1),TCBPQCHR                                              
         GOTO1 AENQDEQ,DMCB,(C'D',DMCB+16)                                      
*                                                                               
PUTUNLKW TM    TCBFLAG2,TCBENQWF   DEQUEUE WRKF FILE IF ENQUEUED                
         JZ    PUTUNLKZ                                                         
         MVC   DMCB+16(4),=C'WRKF'                                              
         MVC   DMCB+20(1),TCBWFCHR                                              
         GOTO1 AENQDEQ,DMCB,(C'D',DMCB+16)                                      
*                                                                               
PUTUNLKZ TM    TCBFLAG5,TCBENQWZ   DEQUEUE WRKZ FILE IF ENQUEUED                
         JZ    PUTUNLKX                                                         
         MVC   DMCB+16(4),=C'WRKZ'                                              
         MVC   DMCB+20(1),TCBWZCHR                                              
         GOTO1 AENQDEQ,DMCB,(C'D',DMCB+16)                                      
PUTUNLKX DS    0H                                                               
*                                                                               
PUTGLV   SR    R1,R1               TEST IF XFR CONTROL GLOBAL                   
         ICM   R1,3,GLOBXFR                                                     
         JZ    PUTGLVX                                                          
         LA    R1,GLOBAREA(R1)                                                  
         USING GLVXCTLD,R1                                                      
         CLI   GLVXCODE,GLVXCODQ   TEST IF STILL AN XFR ELEMENT                 
         JNE   PUTGLVX                                                          
         TM    GLVXFLG1,GLV1RETN   TEST THIS IS A RETURN CALL                   
         JZ    PUTGLVY                                                          
         TM    GLVXFLG1,GLV1RETG   TEST IF RETURNING GLOBALS                    
         JO    PUTGLVY                                                          
         TM    GLVXFLG1,GLV1SEPS   TEST SEPERATE SESSION                        
         JZ    PUTGLVN                                                          
         CLC   GLVXTOPR,=C'MAD'    IF RETURN TO $MAD DONT WRITE TWA0            
         JE    PUTGLVN                                                          
*                                                                               
PUTGLVY  DS    0H                                                               
*&&ML*&& LHI   RF,X'21'            *MLOG 21 PUTTWA WRITE                        
*&&ML*&& BRAS  RE,MLOG             *MLOG                                        
         J     PUTGLVX                                                          
*                                                                               
PUTGLVN  DS    0H                                                               
*&&ML*&& LHI   RF,X'22'            *MLOG 22 PUTTWA DONT WRITE                   
*&&ML*&& BRAS  RE,MLOG             *MLOG                                        
         J     PUTTWANO                                                         
*                                                                               
PUTGLVX  EQU   *                                                                
         DROP  R1                                                               
*                                                                               
PUTSWT   DS    0H                  SWITCH TO CONNECTED SYSTEM                   
         CLI   TSYS,0              TEST CONNECTED                               
         JE    PUTSWT2                                                          
         TM    TFLAG,TFLAGSSW      YES-TEST SWITCH ISSUED THIS TIME             
         JNZ   PUTSWT1                                                          
         MVC   TCBSWRVF(4),TCBRCVF NO-COPY RCVR D/A'S FOR $CT SYS               
         MVC   TCBSWRVL(4),TCBRCVL                                              
         J     PUTSWT2                                                          
*                                                                               
PUTSWT1  MVC   DUB(1),TCBSWSYS     SWITCH TO CONNECTED SYSTEM                   
         MVC   DUB+1(3),=X'FFFFFF' SET SPECIAL PLIST FOR FACPAK                 
         XC    DUB+4(4),DUB+4                                                   
         GOTO1 ASWITCH,DUB                                                      
*                                                                               
PUTSWT2  NI    TSTAT2,255-TSTATCAN-TSTATTIP                                     
         NI    TFLAG,255-TFLAGSSW-TFLAGNOP-TFLAGRCV                             
PUTSWTX  DS    0H                                                               
*                                                                               
PUTRULE  L     R4,SRPAR5           SET SELIST DATA                              
         USING SELISTD,R4                                                       
         TM    DEVCTL,DEVSCP       TEST SCRIPT RUNNING IN THIS TASK             
         JNO   PUTWRT                                                           
*                                                                               
PUTRUSC  L     RE,TBUFF            RULES FOR PROCESSING SCRIPTS                 
         USING TBHDATA,RE                                                       
         CLI   TBHXIT,0            TEST IS SPECIAL EXIT CALL                    
         JNE   PUTRUSC6                                                         
*                                                                               
PUTRUSC1 TM    TBHCNTL,TBHCFRST    FIRST TRANSACTION                            
         JZ    PUTRUSC2                                                         
         TM    TBHCNTL,TBHCCTTH    MUST BE CONNECT                              
         JZ    PUTRUSCE                                                         
         NI    TBHCNTL,255-TBHCCTTH-TBHCFRST                                    
         OI    TBHCNTL,TBHCCTPV                                                 
         J     PUTWRT                                                           
*                                                                               
PUTRUSC2 TM    TBHCNTL,TBHCCTTH    CONNECT TRANSACTION                          
         JZ    PUTRUSC3                                                         
         TM    TBHCNTL,TBHCCTPV    MUST HAVE DONE ONE BEFORE                    
         JZ    PUTRUSCE                                                         
         NI    TBHCNTL,255-TBHCCTTH                                             
         TM    TBHCNTL,TBHCBALL                                                 
         JO    PUTWRT                                                           
         J     PUTWRT                                                           
*                                                                               
PUTRUSC3 TM    TBHCNTL,TBHCBALL    NORMAL TRANSACTION                           
         JO    PUTWRT                                                           
         J     PUTWRT                                                           
*                                                                               
PUTRUSC6 CLI   TBHXIT,TBHXCMT      COMMIT COMMAND FROM SCRIPT                   
         JNE   PUTRUSC7                                                         
         OI    TBHCNTL,TBHCCMT     SET COMMIT DONE                              
*                                  SHOULD CLOSE VERMONT BUFFER HERE             
         J     PUTWRT                                                           
PUTRUSC7 J     PUTWRT                                                           
*                                                                               
PUTRUSCE CLI   TSVCREQ+1,$ABEND                                                 
         JNE   *+12                                                             
         OI    DEVCTL,DEVNWT                                                    
         J     PUTWRT                                                           
         CR    RB,RC                                                            
         J     PUTTWAX                                                          
         DROP  RE                                                               
*                                                                               
         USING TCBD,R6                                                          
PUTWRT   TM    DEVCTL,DEVNWT                                                    
         JO    PUTTWANO                                                         
         TM    TSTAT2,TSTATCAN                                                  
         JO    PUTTWANO                                                         
         GOTO1 VCHKOUT             WRITE CHECKPOINT TO DISK                     
         J     PUTTWAX                                                          
*                                                                               
PUTTWANO CR    R0,R0               EXIT WITH CC EQL IF DONT WRITE TWA           
*                                                                               
PUTTWAX  JNE   PUTTWAXX            EXIT WITH CC NEQ IF BAD WRITE TWA            
*                                                                               
         TM    TTEST,TTESTTAC      ANY TSTTAB ENTRY                             
         JZ    PUTTWAXX                                                         
         ICM   R5,15,TACCS         MUST HAVE A TSTTAB                           
         JZ    PUTTWAXX                                                         
         USING TSTTABD,R5                                                       
         TM    TSTFLAGS,TSTFSCRQ   TEST SCREEN TRACE REQUIRED                   
         JZ    PUTTWAXX                                                         
*                                                                               
         CLC   TNUM,TSTNUM         TEST SAME TERMINAL                           
         JE    *+10                                                             
         CR    R0,R0                                                            
         J     PUTTWAXX            EXIT IF NOT ACTIVE                           
*                                                                               
         CLC   TSTLOW,NULLS        DOES TESTER WANT RECORDS WRITTEN             
         JE    PUTTWAXX            NO                                           
         GOTO1 ADMOD000,DMCB,V(DMEXT),,,(X'F9',0)                               
*                                                                               
         MVC   DMCB(4),=A(ADDAFT)  WRITE TWA RECORD TO TSTRCVR                  
         L     RE,TCBTWA                                                        
         ST    RE,DMCB+4           P2=TWA ADDR                                  
*                                                                               
         SR    R1,R1               FIND TWA LEN                                 
         LA    RE,64(RE)                                                        
         ICM   R1,1,0(RE)                                                       
         JZ    *+10                                                             
         AR    RE,R1                                                            
         J     *-10                                                             
         L     R1,TCBTWA                                                        
         SR    RE,R1                                                            
         ST    RE,DMCB+8           SAVE IN P3                                   
*                                                                               
         LA    RE,TSTLAST          SET LAST RECORD ADDED                        
         ST    RE,DMCB+16                                                       
         MVC   DMCB+20(4),TSTHIGH  SET HIGH TRACK & CAPACITY                    
         LA    R1,DMCB                                                          
         L     RF,ADADDS                                                        
         BASR  RE,RF                                                            
         MVC   TSTCPTY,DMCB+22     SAVE TRACK CAPACITY                          
*                                                                               
         CLC   DMCB+8(2),NULLS     TEST FOR ERRORS                              
         JE    PUTTWAXX                                                         
         NI    TSTFLAGS,255-TSTFSCRQ INHIBIT LOGGING AND RETURN OK CC           
         SR    RE,RE                                                            
*                                                                               
PUTTWAXX J     EXIT                                                             
         DROP  R5                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO PROCESS CANCEL PENDING                                   *         
* P1 A(MAIN WORKING STORAGE)                                          *         
***********************************************************************         
CANCEL   NTR1                                                                   
         ICM   RE,15,TCBDTFLK      WAS TASK HOLDING DNEXT                       
         JZ    *+14                                                             
         OI    2(RE),X'80'         YES-RELEASE DNEXT FOR FILE                   
         XC    TCBDTFLK,TCBDTFLK                                                
*                                                                               
         TM    TCBLOCK,X'80'       DID TASK LOCK ANYTHING                       
         JZ    CNCL02                                                           
         XC    DMCB(8),DMCB        YES-FREE LOCKTAB ENTRIES                     
         GOTO1 ALOCKER,DMCB                                                     
*                                                                               
CNCL02   TM    TCBFLAG2,TCBENQCT   DEQUEUE CTRL SYSTEM IF ENQUEUED              
         JZ    CNCL04                                                           
         GOTO1 AENQCTL,DMCB,(C'D',=C'CTRL')                                     
CNCL04   EQU   *                                                                
*&&UK                                                                           
         TM    TCBFLAG2,TCBENQMZ   DEQUEUE MEDZ SYSTEM IF ENQUEUED              
         JZ    CNCL06                                                           
         GOTO1 AENQCTL,DMCB,(C'D',=C'MEDZ')                                     
CNCL06   EQU   *                                                                
*&&                                                                             
*&&US                                                                           
         TM    TCBFLAG2,TCBENQEW   DEQUEUE EASI FILE IF ENQUEUED                
         JZ    CNCL08                                                           
         GOTO1 AENQDEQ,DMCB,(C'D',=C'EASI')                                     
CNCL08   EQU   *                                                                
*&&                                                                             
         OC    TCBISGQ,TCBISGQ     LET GO OF ENQUEUES FOR TASK                  
         JZ    CNCL09                                                           
         MVI   DMCB+3,C'K'         CLEAN TASK                                   
         LA    RF,=CL5'ALL'                                                     
         ST    RF,DMCB+4                                                        
         GOTO1 =V(DMISGENQ),DMCB,,,0                                            
*                                                                               
CNCL09   TM    TCBFLAG1,TCBENQPQ   DEQUEUE PRTQ FILE IF ENQUEUED                
         JZ    CNCL10                                                           
         MVC   DMCB+16(4),=C'PRTQ'                                              
         MVC   DMCB+20(1),TCBPQCHR                                              
         GOTO1 AENQDEQ,DMCB,(C'D',DMCB+16)                                      
*                                                                               
CNCL10   TM    TCBFLAG2,TCBENQWF   DEQUEUE WRKF FILE IF ENQUEUED                
         JZ    CNCL11                                                           
         MVC   DMCB+16(4),=C'WRKF'                                              
         MVC   DMCB+20(1),TCBWFCHR                                              
         GOTO1 AENQDEQ,DMCB,(C'D',DMCB+16)                                      
*                                                                               
CNCL11   TM    TCBFLAG5,TCBENQWZ   DEQUEUE WRKZ FILE IF ENQUEUED                
         JZ    CNCL12                                                           
         MVC   DMCB+16(4),=C'WRKZ'                                              
         MVC   DMCB+20(1),TCBWZCHR                                              
         GOTO1 AENQDEQ,DMCB,(C'D',DMCB+16)                                      
*                                                                               
CNCL12   TM    TFLAG,TFLAGSSW      TEST SWITCH ISSUED THIS TIME                 
         JZ    CNCL14                                                           
         MVC   DUB(1),TCBSWSYS     SWITCH TO CONNECTED SYSTEM                   
         MVC   DUB+1(3),=X'FFFFFF' SET SPECIAL PLIST FOR FACPAK                 
         XC    DUB+4(4),DUB+4                                                   
         GOTO1 ASWITCH,DUB                                                      
         NI    TFLAG,255-(TFLAGSSW+TFLAGNOP)                                    
*                                                                               
CNCL14   NI    TSTAT2,255-(TSTATCAN+TSTATTIP)                                   
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* FOR DC H'0',C'$ABEND' READ OLD TWA/UPDATE GLOBALS/WRITE TWA         *         
* THIS LEAVES CURRENT TWA INTACT FOR TRANSMIT TO TERMINAL             *         
***********************************************************************         
UNWGLBLS NMOD1 TWAMAX,**UNWG**                                                  
         LR    R4,RC               SAVE W/S OBTAINED                            
         LR    RC,R1                                                            
*                                                                               
         LR    R0,R4                                                            
         LHI   R1,TWAMAX                                                        
         L     RE,TCBTWA                                                        
         LHI   RF,TWAMAX                                                        
         MVCL  R0,RE               COPY CURRENT TWA LOCALLY                     
*                                                                               
         XC    DMCB+8(4),DMCB+8    READ SAVED PAGE 0 INTO TCBTWA                
         MVC   DMCB+20(2),=C'L='                                                
         MVC   DMCB+22(2),RECLEN                                                
         GOTO1 VDATAMGR,DMCB,=C'DMREAD',=C'TEMPSTR',,TCBTWA                     
*                                                                               
         GOTO1 VCHKOUT             CHECKPOINT CURRENT INFO                      
*                                                                               
         LR    R0,R4                                                            
         LHI   R1,TWAMAX                                                        
         L     RE,TCBTWA                                                        
         LHI   RF,TWAMAX                                                        
         MVCL  RE,R0               RESTORE CURRENT TWA                          
         B     EXIT                                                             
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO LOG MONITOR XCTL EVENTS                                  *         
* RF HAS THE EVENT CODE. X'80' ON MEANS OVERWRITE LAST EVENT.         *         
* R1 A(XCTL GLOBAL)                                                   *         
***********************************************************************         
MLOG     CLI   TSTSYS,C'Y'         ONLY FOR TEST SYSTEMS                        
         BNER  RE                                                               
*                                                                               
MLOGNTR  NTR1  BASE=*,LABEL=*                                                   
         ICM   RE,15,MLOGTAB                                                    
         CHI   RF,X'80'            TEST TO GO BACK 1 SLOT                       
         BNH   MLOG02              NO                                           
*                                                                               
         AHI   RE,-(MLOGLEN)                                                    
         AHI   RF,-X'80'                                                        
*                                                                               
         LA    R0,MLOGDATA         CHECK UNDERFLOW                              
         CR    RE,R0                                                            
         BNL   MLOG02                                                           
         LA    RE,MLOGDATX-MLOGLEN SET TO A(END-L'ONE ENTRY)                    
*                                                                               
MLOG02   LA    R0,MLOGDATX         CHECK OVERFLOW                               
         CR    RE,R0                                                            
         BL    *+8                                                              
         LA    RE,MLOGDATA                                                      
*                                                                               
MLOG1    XC    0(MLOGLEN,RE),0(RE)                                              
         STC   RF,0(RE)            RECORD MONITOR EVENT CODE                    
         MVC   1(01,RE),TOVSYS      SET SYSTEM                                  
         MVC   2(01,RE),TPRG        SET PROGRAM                                 
         MVC   3(GLVXLENQ,RE),2(R1) RECORD XCTL GLOBAL                          
*                                                                               
         AHI   RE,MLOGLEN                                                       
         ST    RE,MLOGTAB                                                       
         J     EXITOK                                                           
*                                                                               
MLOGLEN  EQU   32                  LENGTH OF TABLE                              
*                                                                               
         ORG   MONITOR+((((*-MONITOR)/16)+1)*16)                                
         DC    CL08'**MLOG**'                                                   
MLOGTAB  DC    A(MLOGDATA)                                                      
         DC    XL04'00'                                                         
*                                                                               
MLOGDATA DC    64XL(MLOGLEN)'00'                                                
MLOGDATX DC    16X'FF'                                                          
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO BUILD EXTRA CONNECT INFO TABLE                           *         
***********************************************************************         
SETXTRA  NTR1  BASE=*,LABEL=*                                                   
         XC    XTRAINFO,XTRAINFO   SET UP EXTRA INFO FOR APPLICATION            
         MVC   XIAGCOPT,TAGCOPT    COUNTRY OPTIONS                              
         MVC   XIAGCTRY,TAGCTRY    AGENCY COUNTRY CODE                          
         MVC   XICTRY,TCTRY        ACTUAL COUNTRY CODE                          
         MVC   XILANG,TLANG        AGENCY LANGUAGE CODE                         
         MVC   XIAGCURR,TAGCURR    AGENCY CURRENCY CODE                         
         MVC   XIFLAG1,TSTATB      CONNECT FLAGS FOR ADV/SYS/PERSON             
         MVC   XIUSER,TUSER        USER ID NUMBER                               
*                                                                               
         TM    TTEST,TTESTUEN                                                   
         JZ    *+8                                                              
         OI    XIFLAG2,XICTUEN     SET CONNECTED WITH U=N                       
         TM    TTEST,TTESTTAC                                                   
         JZ    *+8                                                              
         OI    XIFLAG2,XICTIAM     SET CONNECTED WITH I=XXX                     
         L     R1,SRPAR5                                                        
         TM    SEIND-SELISTD(R1),SEIRONLY+SEISETRO                              
         JZ    *+8                                                              
         OI    XIFLAG1,XIROSYS     SET CONNECTED TO READ-ONLY SYSTEM            
         L     R1,VSSB                                                          
         CLI   SSBSYSID-SSBD(R1),11                                             
         JNE   *+8                                                              
         OI    XIFLAG2,XICSCADV    SET CONNECTED TO CSC SYSTEM                  
*                                                                               
         L     R1,SSBAFID-SSBD(R1) A(FACIDTAB)                                  
         LLC   R0,TUPDFAC          GET UPDATIVE FACPAK ID NUM FOR SYS           
         SLL   R0,3                                                             
         AR    R1,R0                                                            
         MVC   XIUPDFAC,0(R1)      UPDATIVE FACPAK ID NAME                      
         MVC   XIAGYALP,TAGY       AGENCY ALPHA ID                              
         MVC   XIAGYSEC,TAGYSEC    AGENCY ALPHA ID FOR SECURITY                 
         MVC   XIAGYPER,TAGYPER    AGENCY ALPHA ID FOR PERSON ID                
         MVC   XIPID,TPERSON       PERSON ID                                    
         MVC   XIAXA9,TCBAXA9      PASS A(EXTRA WORKING STORAGE)                
         L     R1,VSSB                                                          
         MVC   XIAXAMB,SSBXA9MB-SSBD(R1) PASS SIZE (MB) EXTRA WS                
         XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO HOOK INTO DEBUG MODE                                     *         
***********************************************************************         
SETDEBUG ICM   R1,15,=V(EXITAREA)  FADEBUG INSTALLED                            
         BER   RE                                                               
         LA    R0,8                SCAN EXIT AREAS                              
MONGO001 CLC   TSYM,16(R1)         FOR MY TERMINAL                              
         JE    MONGO002                                                         
*                                                                               
         CLC   16(2,R1),=X'FFFF'   CHECK FOR SA/PERS                            
         JNE   MONGO003                                                         
         CLC   18(2,R1),TAGYSEC    CHECK SEC AGENCY                             
         JNE   MONGO003                                                         
         CLC   20(2,R1),TPERSON    CHECK PERSON                                 
         JNE   MONGO003                                                         
*                                                                               
MONGO002 CLC   TCBOVSYS,24(R1)     CORRECT SYS                                  
         JNE   MONGO003                                                         
         CLC   TCBPRG,25(R1)       CORRECT PROG                                 
         JNE   MONGO003                                                         
*                                                                               
         L     RF,DMCB                                                          
         LA    RF,0(RF)                                                         
         C     RF,=V(DDWTO)        IGNORE ROUTINES WITHIN FAC                   
         JL    MONGO003                                                         
         OI    TCBFLAG3,TCBDEBUG   SET DEBUG                                    
         MVC   TCBDEXIT,28(R1)     SET EXIT                                     
         STCM  RF,7,TCBP1A+1                                                    
         MVC   TCBP1I,0(RF)                                                     
         MVC   0(2,RF),=X'0001'    SET BREAKPOINT                               
         BR    RE                                                               
*                                                                               
MONGO003 AHI   R1,1024                                                          
         BRCT  R0,MONGO001                                                      
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO PROCESS ADDRESSES OF CORE RESIDENT PHASES IN SYSFACS     *         
* AND COMFACS FOR TERMINALS SIGNED ON IN TEST MODE.                   *         
* P1 A(MAIN WORKING STORAGE)                                          *         
* P2 DISPLACEMENT OF SYSFAC ENTRY FROM VSYSFAC0                       *         
***********************************************************************         
MFACSET  NTR1  BASE=*,LABEL=*                                                   
         LA    R4,(VSYSFAC0-SYSFACD)(R1)  GET DSPL FROM VSYSFAC0                
*                                                                               
         LA    R2,X'04'            CRNDX USES 01=A,02=B,04=C                    
         TM    TTEST,X'03'         TEST FOR 'C'                                 
         JO    *+14                                                             
         IC    R2,TTEST            TTEST USES 01=A,02=B,03=C                    
         LA    R0,3                                                             
         NR    R2,R0                                                            
*                                                                               
         XC    PARM(12),PARM                                                    
         MVC   PARM+4(4),=X'D9000AFF'                                           
         GOTO1 VCALLOV,PARM                                                     
         CLI   4(R1),X'FF'                                                      
         JE    MFACSETX                                                         
         L     R5,0(R1)            R5=A(CORE RESIDENT INDEX)                    
         USING CRNDXD,R5                                                        
*                                                                               
MFACSET1 XC    PARM(12),PARM                                                    
         MVC   PARM+4(4),=X'D9000AFF'                                           
         MVC   PARM+7(1),CRNDPHS   MOVE PHASE NUMBER FOR CALLOV                 
         EX    R2,*+8                                                           
         J     *+8                                                              
         TM    CRNDINDS,0          TEST SAME TEST VERSION                       
         JO    MFACSET2                                                         
         LA    R5,CRNDTBL                                                       
         LA    R5,4(R5)                                                         
         CLI   0(R5),X'FF'                                                      
         JNE   *-8                                                              
         J     MFACSET7                                                         
MFACSET2 LA    R5,CRNDTBL                                                       
*                                                                               
MFACSET3 CLM   R4,3,0(R5)          MATCH TABLE DSPL                             
         JE    *+14                                                             
         CLC   0(2,R5),=AL2(VSYSFAC0-SYSFACD) OR IS PHASE IN COMFACS            
         JNE   MFACSET6                                                         
         GOTO1 (RF),(R1)           GET ADDRESS FROM CALLOV                      
         SR    RE,RE                                                            
         ICM   RE,3,2(R5)          GET DSPL IN TABLE                            
         LA    R0,SYSFACS          POINT TO COPY OF SYSFACS                     
         CLC   0(2,R5),=AL2(VSYSFAC0-SYSFACD) TEST COMFACS                      
         JNZ   *+8                                                              
         LA    R0,COMFACS                                                       
         AR    RE,R0                                                            
         MVC   0(4,RE),0(R1)       MOVE ADDRESS TO COPIED TABLE                 
*                                                                               
MFACSET6 LA    R5,4(R5)            BUMP TO NEXT TABLE ENTRY                     
         CLI   0(R5),X'FF'                                                      
         JNE   MFACSET3                                                         
*                                                                               
MFACSET7 LA    R5,1(R5)                                                         
         CLI   CRNDPHS,0           TEST FOR END OF TABLE                        
         JNE   MFACSET1                                                         
*                                                                               
MFACSETX B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO PROCESS ADDRESSES OF CORE RESIDENT PHASES IN SYSFACS     *         
* AND COMFACS FOR TERMINALS MATCHING PHASE VERSION CONTROL            *         
* P1 A(MAIN WORKING STORAGE)                                          *         
* P2 DISPLACEMENT OF SYSFAC ENTRY FROM VSYSFAC0                       *         
***********************************************************************         
VFACSET  NTR1  BASE=*,LABEL=*                                                   
         J     EXIT                                                             
         LA    R4,(VSYSFAC0-SYSFACD)(R1)  GET DSPL FROM VSYSFAC0                
*                                                                               
         XC    PARM(12),PARM                                                    
         MVC   PARM+4(4),=X'D9000AFF'                                           
         GOTO1 VCALLOV,PARM                                                     
         CLI   4(R1),X'FF'                                                      
         BE    VFACSETX                                                         
         L     R5,0(R1)            R5=A(CORE RESIDENT INDEX)                    
         USING CRNDXD,R5                                                        
*                                                                               
VFACSET1 MVC   PARM+7(1),CRNDPHS   MOVE PHASE NUMBER FOR CALLOV                 
         LA    R5,CRNDTBL                                                       
*                                                                               
VFACSET3 CLM   R4,3,0(R5)          MATCH TABLE DSPL                             
         BE    *+14                                                             
         CLC   0(2,R5),=AL2(VSYSFAC0-SYSFACD) OR IS PHASE IN COMFACS            
         BNE   VFACSET6                                                         
         GOTO1 (RF),(R1)           GET ADDRESS FROM CALLOV                      
         SR    RE,RE                                                            
         ICM   RE,3,2(R5)          GET DSPL IN TABLE                            
         LA    R0,SYSFACS          POINT TO COPY OF SYSFACS                     
         CLC   0(2,R5),=AL2(VSYSFAC0-SYSFACD) TEST COMFACS                      
         BNZ   *+8                                                              
         LA    R0,COMFACS                                                       
         AR    RE,R0                                                            
         MVC   0(4,RE),0(R1)       MOVE ADDRESS TO COPIED TABLE                 
*                                                                               
VFACSET6 LA    R5,4(R5)            BUMP TO NEXT TABLE ENTRY                     
         CLI   0(R5),X'FF'                                                      
         BNE   VFACSET3                                                         
*                                                                               
VFACSET7 LA    R5,1(R5)                                                         
         CLI   CRNDPHS,0           TEST FOR END OF TABLE                        
         BNE   VFACSET1                                                         
*                                                                               
VFACSETX B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* SET LINK FOR DIALOG MODE WHEN XCTL FROM APPLICATION - R1=A(GLVXCTLD)*         
* CC EQ LINK THERE - EXIT                                             *         
* CC NE LINK NOT THERE - CONTINUE TO GET FREE SESSION                 *         
***********************************************************************         
         USING GLVXCTLD,R1                                                      
MONDIAL  NTR1  BASE=*,LABEL=*                                                   
         L     R8,TBUFFM8                                                       
         USING TBHD,R8                                                          
*                                                                               
*&&ML*&& LHI   RF,X'31'            *MLOG 31 CALLED MONDIAL                      
*&&ML*&& BRAS  RE,MLOG             *MLOG                                        
*                                                                               
         TM    GLVXFLG1,GLV1RETN   RETURN CALL?                                 
         BO    MOND06              YES                                          
         CLC   TSESSION,GLVXSESE   DIALOG WITH YOURSELF NOT ALLOWED             
         BE    EXITL                                                            
         CLC   TCNVPRS,NULLS       ANY DIALOGS SET UP ALREADY?                  
         BE    EXITL               NO                                           
*                                                                               
*&&ML*&& LHI   RF,X'32'            *MLOG 32 REQUEST DIALOG MATCH                
*&&ML*&& BRAS  RE,MLOG             *MLOG                                        
*                                                                               
         LLC   R0,TSESSION         SET UP FROM/TO DIALOG PAIR IN BYTE           
         SLL   R0,4                                                             
         STC   R0,BYTE                                                          
         OC    BYTE,GLVXSESE                                                    
*                                                                               
         LHI   R0,L'TCNVPRS        TEST DIALOG MODE SET FOR THIS PAIR           
         LA    RF,TCNVPRS                                                       
MOND02   CLC   BYTE,0(RF)          TEST LINK ESTABLISHED ALREADY                
         BE    MOND04              YES                                          
         AHI   RF,1                                                             
         BCT   R0,MOND02           NEXT                                         
*                                                                               
*&&ML*&& LHI   RF,X'33'            *MLOG 33 DIDN'T MATCH DIALOG PAIR            
*&&ML*&& BRAS  RE,MLOG             *MLOG                                        
         J     EXITL                                                            
*                                                                               
MOND04   LHI   R0,5                GO TO NOMINATED SESSION VIA =SS              
         STH   R0,TBHMSGL                                                       
         XC    TBHDATA(16),TBHDATA                                              
         MVC   TBHDATA(09),=C'=SS,L,XCS'                                        
*                                                                               
*&&ML*&& LHI   RF,X'34'            *MLOG 34 MATCHED DIALOG PAIR                 
*&&ML*&& BRAS  RE,MLOG             *MLOG                                        
*                                                                               
         L     RE,VSSB             TEST IF VALID SESSION                        
         CLC   GLVXSESE,SSMAX+1                                                 
         JNL   EXITL                                                            
         LLC   RE,GLVXSESE                                                      
         AHI   RE,1                                                             
         STC   RE,TBHDATA+4        SET TO SWAP TO NOMINATED SESSION             
         OI    TBHDATA+4,X'C0'                                                  
*                                                                               
*&&ML*&& LHI   RF,X'35'            *MLOG 35 SET FOR =SS,N TO SWAP OUT           
*&&ML*&& BRAS  RE,MLOG             *MLOG                                        
         J     EXITOK                                                           
*                                                                               
MOND06   CLC   TCNVPRS,NULLS       ANY DIALOG SET UP?                           
         BE    EXITL               NO                                           
*                                                                               
*&&ML*&& LHI   RF,X'36'            *MLOG 36 DIALOG RETURN REQUESTED             
*&&ML*&& BRAS  RE,MLOG             *MLOG                                        
*                                                                               
         LLC   R0,GLVXSESR         SET UP TO/FROM DIALOG PAIR IN BYTE           
         SLL   R0,4                                                             
         STC   R0,BYTE                                                          
         OC    BYTE,TSESSION                                                    
*                                                                               
         LHI   R0,L'TCNVPRS        TEST DIALOG MODE SET FOR THIS PAIR           
         LA    RF,TCNVPRS                                                       
MOND08   CLC   BYTE,0(RF)          CHECK LINK ESTABLISHED ALREADY               
         BE    MOND10                                                           
         AHI   RF,1                                                             
         BCT   R0,MOND08           NEXT                                         
*                                                                               
*&&ML*&& LHI   RF,X'37'            *MLOG 37 DIDN'T MATCH DIALOG PAIR            
*&&ML*&& BRAS  RE,MLOG             *MLOG                                        
         J     EXITL               NO LINK-CONTINUE AS NORMAL                   
*                                                                               
MOND10   NI    DEVCTL,255-DEVNWT                                                
*NOP*    GOTO1 VCHKOUT             WRITE TWA0 TO PRESERVE USER INPUT            
*                                                                               
*&&ML*&& LHI   RF,X'38'            *MLOG 38 TRYING TO RETURN VIA =SS            
*&&ML*&& BRAS  RE,MLOG             *MLOG                                        
*                                                                               
         LHI   R0,5                GO TO NOMINATED SESSION VIA =SS              
         STH   R0,TBHMSGL                                                       
         XC    TBHDATA(16),TBHDATA                                              
         MVC   TBHDATA(09),=C'=SS,L,XCS'                                        
*                                                                               
         L     RE,VSSB             TEST IF VALID SESSION                        
         CLC   GLVXSESR,SSMAX+1                                                 
         JNL   EXITL                                                            
         LLC   RE,GLVXSESR                                                      
         AHI   RE,1                                                             
         STC   RE,TBHDATA+4        SET TO SWAP TO NOMINATED SESSION             
         OI    TBHDATA+4,X'C0'                                                  
*                                                                               
*&&ML*&& LHI   RF,X'39'            *MLOG 39 RETURN TO DIRECTED SESSION          
*&&ML*&& BRAS  RE,MLOG             *MLOG                                        
         J     EXITOK                                                           
         DROP  R1,R8                                                            
         EJECT                                                                  
***********************************************************************         
* HANDY LITTLE ROUTINES THAT NEED ADDRESSIBILITY                      *         
***********************************************************************         
LITERALS DS    0D                  EVERYTHING FROM HERE IS OFF R7               
*                                                                               
EXITOK   CR    RB,RB                                                            
         B     EXIT                                                             
*                                                                               
EXITL    CLI   *,255                                                            
         B     EXIT                                                             
*                                                                               
EXIT     XIT1                                                                   
*                                                                               
NULLS    DC    XL30'00'                                                         
MSGLN    DC    AL2(MSGEND-*-2)                                                  
         DC    C'AUTONOTE*'                                                     
MSGEMAIL DC    CL40' '                                                          
MSGTXT   DC    CL50' '                                                          
MSGEND   DS    0C                                                               
*                                                                               
MSG2SND  DC    CL50'+FACXXX+ PROT WAS OFF .../... **????** AT 000000'           
         EJECT                                                                  
***********************************************************************         
* MONITOR ERROR ROUTINES                                              *         
***********************************************************************         
MONCAN   BRAS  RE,CANCEL            CANCEL CURRENT TRANSACTION                  
         LA    R0,CANMSG                                                        
         J     MONAP32                                                          
*                                                                               
MONERR1  LA    R0,1                CANT READ TEMPSTR                            
         J     MONERRX                                                          
MONERR2  LA    R0,2                CANT WRITE TEMPSTR                           
         J     MONERRX                                                          
MONERR3  LA    R0,3                CANT READ PRGMS                              
         J     MONERRX                                                          
MONERR4  LA    R0,4                CANT TRANSLATE TWA                           
         J     MONERRX                                                          
MONERR5  LA    R0,5                MONITOR SCRIPT ERROR                         
         J     MONERRX                                                          
MONERR6  LA    R0,6                SCRUNCH SCRIPT ERROR                         
         J     MONERRX                                                          
MONERR7  LA    R0,7                SCRIPT REQUESTED ABEND                       
         J     MONERRX                                                          
MONERR8  LA    R0,8                TERM TRANASACTION RATE TOO HIGH              
         J     MONERRX                                                          
MONERR9  LA    R0,9                YOUR ROOT PHASE ISN'T THERE                  
         J     MONERRX                                                          
MONERR10 LA    R0,10               WHERE IS THE REAL TBUFF?                     
         J     MONERRX                                                          
*                                                                               
MONERRX  ST    R1,ERRNUM           DIE SAFELY WITH R0=REASON                    
         STC   R0,ERRNUM                                                        
         L     RF,=A(ERROUT)                                                    
         BRAS  R9,MONGOTO                                                       
         J     MONWRT                                                           
         EJECT                                                                  
***********************************************************************         
* LITERALS AND CONSTANTS                                              *         
***********************************************************************         
         LTORG                                                                  
*                                                                               
$HELP    EQU   X'09'                                                            
$CT      EQU   X'10'                                                            
$DONE    EQU   X'12'                                                            
$LOG     EQU   X'14'                                                            
$RE      EQU   X'20'                                                            
$PCI     EQU   X'21'                                                            
$TXT     EQU   X'44'                                                            
$KWX     EQU   X'47'                                                            
$SW      EQU   X'49'                                                            
$CC      EQU   X'56'                                                            
$DUMP    EQU   X'5D'                                                            
$TEST    EQU   X'36'                                                            
$UNWIND  EQU   X'FB'                                                            
$CAN     EQU   X'FC'                                                            
$SRPSWD  EQU   X'FD'                                                            
$SRERR   EQU   X'FE'                                                            
$ABEND   EQU   X'FF'                                                            
*                                                                               
$$BYE    DC    X'01EE'             DUMMY S/R TO SIGNIFY END OF S/R MODE         
$$IAM    DC    X'0126'             =IAM S/R                                     
*                                                                               
PZERO    DC    P'0'                                                             
*                                                                               
ALOCKER  DC    V(LOCKER)                                                        
AGLOBBER DC    V(GLOBBER)                                                       
ALOCKUP  DC    V(LOCKUP)                                                        
AENQCTL  DC    V(DMENQCTL)                                                      
AENQDEQ  DC    V(DMENQDEQ)                                                      
ADMRCVR  DC    V(DMRCVR)                                                        
AMQIO    DC    V(MQIO)                                                          
ADMOD000 DC    V(DMOD000)                                                       
ATWASVR  DC    V(TWASVR)                                                        
ASWITCH  DC    V(FASWITCH)                                                      
ADTFIOA  DC    V(DTFIOA)                                                        
ADADDS   DC    V(DADDS)                                                         
ASYSFAC  DC    V(SYSFAC)                                                        
AHEXOUT  DC    V(HEXOUT)                                                        
VSCUNKEY DC    V(SCUNKEY)                                                       
*                                                                               
REGVALS  DC    10F'-1'                                                          
*                                                                               
INVMSG   DC    A(A00,A01,A02,A03,A04,A05,A06,A07,A08)                           
CANMSG   DC    A(B00,B01,B02,B03,B04,B05,B06,B07,B08)                           
CALMSG   DC    A(C00,C01,C02,C03,C04,C05,C06,C07,C08)                           
UIIMSG   DC    A(D00,D01,D02,D03,D04,D05,D06,D07,D08)                           
RDYMSG   DC    A(E00,E01,E02,E03,E04,E05,E06,E07,E08)                           
RERMSG   DC    A(F00,F01,F02,F03,F04,F05,F06,F07,F08)                           
*                                                                               
TRN3270  DC    V(TI3270,TO3270)                                                 
TRNSCRP  DC    V(TI3270,TOSCRP)                                                 
TRNSTRO  DC    V(TISTR,TOSTR)                                                   
TRNMQ    DC    V(TIMQ,TOMQ)                                                     
*                                                                               
BRDMSG   DC    X'07003E',C'=BRD',X'00'                                          
SSACBITS DC    X'0102040810204080'                                              
SSNABITS DC    X'FEFDFBF7EFDFBF7F'                                              
*                                                                               
CCD      DC    X'114040'                       (SBA)(01,01)                     
         DC    X'1DC9',X'13',X'3C407D00'       (SF)(UHM)(IC)(...)               
CCDS     DC    X'1DC9',CL17'=CC-ENTER CALC'    (SF)(UHM)(=CC....)               
         DC    X'1D60',X'03'                   (SF)(PN )(ETX)                   
CCDL     EQU   *-CCD                                                            
*                                                                               
A00 DC C'DW/0001 Invalid or no data received - Please re-enter       '          
A01 DC C'DW/0001 Invalid or no data received - Please re-enter       '          
A02 DC C'DW/0001 Invalid or no data received - Please re-enter       '          
A03 DC C'DW/0001 Bitte neue Eingabe                                  '          
A04 DC C'DW/0001 Entrez encore s''il vous plait                       '         
A05 DC C'DW/0001 Datos invalidos -  Anote otra vez por favor         '          
A06 DC C'DW/0001 Invalid or no data received - Please re-enter       '          
A07 DC C'DW/0001 Gegevens ongeldig - s.v.p. opnieuw invoeren         '          
A08 DC C'DW/0001 Invalid or no data received - Please re-enter       '          
*                                                                               
B00 DC C'DI/0001 Transaction cancelled as requested                  '          
B01 DC C'DI/0001 Transaction cancelled as requested                  '          
B02 DC C'DI/0001 Transaction cancelled as requested                  '          
B03 DC C'DI/0001 Vorgang abgebrochen                                 '          
B04 DC C'DI/0001 La transaction est complete                         '          
B05 DC C'DI/0001 Transaccion cancelada, como pedido                  '          
B06 DC C'DI/0001 Transaction cancelled as requested                  '          
B07 DC C'DI/0001 Transactie op verzoek geannuleerd                   '          
B08 DC C'DI/0001 Transaction cancelled as requested                  '          
*                                                                               
C00      DC    CL17'=CC-Enter calc   '                                          
C01      DC    CL17'=CC-Enter calc   '                                          
C02      DC    CL17'=CC-Enter calc   '                                          
C03      DC    CL17'=CC-Rechnen      '                                          
C04      DC    CL17'=CC-Entrez calc  '                                          
C05      DC    CL17'=CC-Calculadora  '                                          
C06      DC    CL17'=CC-Enter calc   '                                          
C07      DC    CL17'=CC-Calculator   '                                          
C08      DC    CL17'=CC-Enter calc   '                                          
*                                                                               
D00 DC C'DW/0002 Input inhibited - Please wait and try later         '          
D01 DC C'DW/0002 Input inhibited - Please wait and try later         '          
D02 DC C'DW/0002 Input inhibited - Please wait and try later         '          
D03 DC C'DW/0002 Eingabesperre - bitte warten und nochmals versuchen '          
D04 DC C'DW/0002 Entree restrictee - Attendez et essayez plus tard   '          
D05 DC C'DW/0002 Entrada impedida- Pruebe mas tarde por favor        '          
D06 DC C'DW/0002 Input inhibited - Please wait and try later         '          
D07 DC C'DW/0002 Invoer niet toegastaan - probeer het later opnieuw  '          
D08 DC C'DW/0002 Input inhibited - Please wait and try later         '          
*                                                                               
E00 DC C' ready*'                                                               
E01 DC C' ready*'                                                               
E02 DC C' ready*'                                                               
E03 DC C' Bereit'                                                               
E04 DC C' pret* '                                                               
E05 DC C' listo*'                                                               
E06 DC C' ready*'                                                               
E07 DC C' gereed'                                                               
E08 DC C' ready*'                                                               
*                                                                               
F00 DC C' error*'                                                               
F01 DC C' error*'                                                               
F02 DC C' error*'                                                               
F03 DC C' Fehler'                                                               
F04 DC C' erreur'                                                               
F05 DC C' error*'                                                               
F06 DC C' error*'                                                               
F07 DC C' fout* '                                                               
F08 DC C' error*'                                                               
*                                                                               
ERRHLP   DC    CL30'Unknown error                '                              
         DC    CL30'Cant read TEMPSTR            '                              
         DC    CL30'Cant write TEMPSTR           '                              
         DC    CL30'Cant read prgms              '                              
         DC    CL30'Cant translate TWA           '                              
         DC    CL30'Monitor script error         '                              
         DC    CL30'Scrunch script error         '                              
         DC    CL30'Script requested ABEND       '                              
         DC    CL30'Transaction rate too high    '                              
         DC    CL30'Your root phase isnt there   '                              
         DC    CL30'TCBRBUFF zero in MONBACK     '                              
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO WRITE MONITOR DEATH MESSAGE                              *         
***********************************************************************         
ERROUT   NMOD1 0,*MONERR*                                                       
         L     R7,=A(LITERALS)                                                  
         USING LITERALS,R7                                                      
         STC   R0,ERROUTM+7        SET ERROR NUMBER IN DEATH MESSAGE            
         OI    ERROUTM+7,X'F0'                                                  
*                                                                               
         MHI   R0,L'ERRHLP                                                      
         LA    RF,ERRHLP                                                        
         AR    RF,R0                                                            
         MVC   ERROUTH,0(RF)                                                    
*                                                                               
ERROUTM  DC    H'0',C'MNTR#0='     SERIOUS PROBLEM ENCOUNTERED                  
ERROUTH  DC    CL30' '                                                          
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* WORKING STORAGE DSECT                                               *         
***********************************************************************         
MONWORK  DSECT                                                                  
*                                                                               
TRNOUT   DS    XL16      1ST ITEM  OUTPUT TWA DATA FROM INPUT TRANS             
PRTQGBL  DS    XL16      2ND ITEM  UPDATED BY PRTQ WHEN REPORT CLOSED           
GLOBWORK DS    A         3RD ITEM  ADDRESS OF GLOBBER WORK AREA                 
MONFLAGS DS    XL4       4TH ITEM  MISCELLANEOUS MONITOR FLAGS                  
TRNOUTXC EQU   *-TRNOUT                                                         
*                                                                               
DUB      DS    D                                                                
ERRNUM   DS    F                                                                
DMCB     DS    6F                                                               
PARM     DS    6F                                                               
TRINADR  DS    A                                                                
TROUTADR DS    A                                                                
APRG     DS    A                                                                
TBUFFM8  DS    A                   A(TBUFF-8)                                   
*                                                                               
SAVE1    DS    X                                                                
SAVE2    DS    X                                                                
GETWHY   DS    X                   CONTROL OF READING TWA#0                     
GOBACK   DS    C                   S/R GOING BACK TO APPLICATION                
PROTFLGS DS    C                   STORAGE PROTECTION FLAGS SAVE BYTE           
BYTE     DS    C                                                                
SESS     DS    C                   SAVE SESSON BYTE                             
SVOUT    DS    X                   OUTPUT TRANSLATOR WRITE/ERASE                
TSTSYS   DS    C                   C'Y' IF RUNNING IN A TEST SYSTEM             
*                                                                               
DEVCTL   DS    X                   DEVICE TYPE AND CONTROL FLAGS                
DEVTRM   EQU   X'80'               DEVICE IS A TERMINAL                         
DEVPRN   EQU   X'40'               DEVICE IS A PRINTER                          
DEVZERO  EQU   X'20'               DEVICE IS SPECIAL UTL ZERO                   
DEVNWT   EQU   X'02'               NO WRITE TEMPSTR                             
DEVSCP   EQU   X'01'               INPUT IS A SCRIPT                            
*                                                                               
SAVEFUNP DS    CL17                                                             
SVSVCREQ DS    XL1                 ACTUAL SERVICE REQUEST CALLED                
PARK     DS    XL2                                                              
RECLEN   DS    H                   TEMPSTR ACTUAL RECORD LENGTH                 
CHKDSP   DS    H                   TEMPSTR DISPLACEMENT TO CHKPNT               
GLODSP   DS    H                   TEMPSTR DISPLACEMENT TO GLOBALS              
MONCOPY  DS    X                   TBUFF COPIED OUT                             
*                                                                               
P1       DS    F                                                                
P2       DS    F                                                                
P3       DS    F                                                                
P4       DS    F                                                                
P5       DS    F                                                                
P6       DS    F                                                                
P7       DS    F                                                                
*                                                                               
SRPARS   DS    0CL36                                                            
SRPAR1   DS    A                   A(SYSFAC)                                    
SRPAR2   DS    A                   A(TIA)                                       
SRPAR3   DS    A                   A(UTL)                                       
SRPAR4   DS    A                   A(COMFACS)                                   
SRPAR5   DS    A                   A(SELIST)                                    
SRPAR6   DS    A                   A(TWA)                                       
SRPAR7   DS    A                   A(PHASE MAP)                                 
SRPAR8   DS    A                   A(TRANSLATOR TWA DATA)                       
SRPAR9   DS    A                   A(XTRAINFO BLOCK)                            
*                                                                               
CTPARS   DS    0CL36               NEW CTPARM SAFE COMFACS/SYSFACS              
CTPAR1   DS    A                   A(SYSFAC)                                    
CTPAR2   DS    A                   A(TIA)                                       
CTPAR3   DS    A                   A(UTL)                                       
CTPAR4   DS    A                   A(COMFACS) COPY                              
CTPAR5   DS    A                   A(SELIST)                                    
CTPAR6   DS    A                   A(TWA)                                       
CTPAR7   DS    A                   A(PHASE MAP)                                 
CTPAR8   DS    A                   A(TRANSLATOR TWA DATA)                       
CTPAR9   DS    A                   A(XTRAINFO BLOCK)                            
*                                                                               
XTRAINFO DS    0XL32               EXTRA INFO PASSED TO APPLICATION             
XIAGCOPT DS    X                   AGENCY COUNTRY OPTIONS                       
XIAGCTRY DS    X                   AGENCY COUNTRY                               
XICTRY   DS    X                   ACTUAL COUNTRY (WHERE TERMINAL IS)           
XILANG   DS    X                   AGENCY LANGUAGE CODE                         
XIAGCURR DS    CL3                 AGENCY CURRENCY CODE                         
*                                                                               
XIFLAG1  DS    X                   CONNECT INFO FLAG                            
XIROSYS  EQU   X'80'               CONNECTED TO READ ONLY SYSTEM                
XIROMODE EQU   X'40'               CONNECTED IN READ ONLY MODE                  
XIWRONGF EQU   X'20'               CONNECTED TO WRONG FACPAK                    
XITSTADV EQU   X'10'               CONNECTED TO TEST FACPAK                     
XIDDSPER EQU   X'08'               DDS PERSON                                   
XIDDSTRM EQU   X'04'               DDS TERMINAL                                 
*                                                                               
XIUSER   DS    XL2                 USER ID                                      
XIUPDFAC DS    CL4                 UPDATIVE FACPAK ID                           
XIAGYALP DS    CL2                 AGENCY ALPHA                                 
XIAGYSEC DS    CL2                 AGENCY SECURITY ALPHA                        
XIAGYPER DS    CL2                 AGENCY PERSONID ALPHA                        
XIPID    DS    XL2                 AGENCY PERSON ID NUMBER                      
*                                                                               
XIFLAG2  DS    XL1                 CONNECT INFO FLAG                            
XICSCADV EQU   X'80'               CONNECTED TO CSC SYSTEM                      
XICTUEN  EQU   X'40'               CONNECTED WITH U=N                           
XICTIAM  EQU   X'20'               CONNECTED WITH I=XXX                         
*                                                                               
XIAXAMB  DS    XL1                 NUMBER OF MB FOR AXA9                        
XIAXA9   DS    XL4                 ADDRESS OF KEY9 WS 1MB                       
*                                                                               
         DS    XL3                 RESERVED FOR MORE XTRAINFO                   
*                                                                               
         ORG   XTRAINFO+L'XTRAINFO                                              
*                                                                               
SSMAX    DS    XL2                 MAX SESSIONS THIS TERMINAL                   
SSMXP    DS    XL2                 MAX SESSIONS THIS TERMINAL                   
*                                                                               
REGSAVE  DS    10F                                                              
SYSFAC   DS    CL256               SYSFAC SAFE COPY                             
SYSFAC1  DS    CL256               SYSFAC SAFE COPY                             
SYSFACS  DS    CL256               SYSTEM FACILITY LIST                         
COMFACS  DS    CL256               COMMON FACILITY LIST                         
COMFACS1 DS    CL128               COMMON FACILITY LIST EXTENSION               
         ORG   SYSFACS                                                          
MONSAVE  DS    CL256                                                            
         ORG                                                                    
*                                                                               
GLOBAREA EQU   *                   GLOBAL WORK AREA                             
GLOBDATE DS    PL4                 DATE OF LAST SAVE                            
GLOBTIME DS    XL4                 TIME OF LAST SAVE                            
GLOBXFR  DS    XL2                 DISPLACEMENT TO XFR CONTROL ELEMENT          
         DS    XL1                 N/D                                          
GLOBSWS  DS    XL1                 GLOBBER SWITCHES                             
GLOBREAD EQU   X'80'               SET IF GLOBALS READ FROM DISK                
GLOBUPDT EQU   X'40'               SET IF GLOBALS UPDATED                       
GLOBCLR  EQU   X'20'               SET TO CLEAR GLOBALS                         
GLOBELS  DS    XL500               GLOBBER DATA AREA                            
GLOBELEN EQU   *-GLOBELS           LENGTH OF GLOBAL ELEMENTS                    
GLOBALEN EQU   *-GLOBAREA          LENGTH OF GLOBAL AREA                        
*                                                                               
SVPSWKEY DS    X                                                                
MONSPWRK DS    24F                 WORK AREA IN KEY 9                           
         ORG   MONSPWRK                                                         
MONSPRB  DS    A                   RB OF PROGRAM                                
MONSPPSW DS    A                   ADDRESS OF CALLED PROTOFF                    
MONSPLAB DS    CL8                 LABEL OFF RD                                 
         ORG                                                                    
MONWORKL EQU   *-MONWORK                                                        
         EJECT                                                                  
***********************************************************************         
* OTHER INCLUDED DSECTS                                               *         
***********************************************************************         
*DDCOMFACS                                                                      
       ++INCLUDE DDCOMFACS                                                      
*DDGLVXCTLD                                                                     
       ++INCLUDE DDGLVXCTLD                                                     
*DMGREQUS                                                                       
       ++INCLUDE DMGREQUS                                                       
*FACHKPT                                                                        
       ++INCLUDE FACHKPT                                                        
*DDFH                                                                           
       ++INCLUDE DDFH                                                           
*FACRNDXD                                                                       
       ++INCLUDE FACRNDXD                                                       
*FAFACTS                                                                        
       ++INCLUDE FAFACTS                                                        
*FAJOBTAB                                                                       
       ++INCLUDE FAJOBTAB                                                       
*FAPGMLST                                                                       
       ++INCLUDE FAPGMLST                                                       
*FASELIST                                                                       
       ++INCLUDE FASELIST                                                       
*FASRS                                                                          
       ++INCLUDE FASRS                                                          
*FASSB                                                                          
       ++INCLUDE FASSB                                                          
*FASYSFAC                                                                       
       ++INCLUDE FASYSFAC                                                       
*FATBHD                                                                         
       ++INCLUDE FATBHD                                                         
*FATCB                                                                          
       ++INCLUDE FATCB                                                          
*FATIOB                                                                         
       ++INCLUDE FATIOB                                                         
*FATSTTAB                                                                       
       ++INCLUDE FATSTTAB                                                       
*FATWA                                                                          
       ++INCLUDE FATWA                                                          
*FAUTL                                                                          
       ++INCLUDE FAUTL                                                          
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'002FAMONITOR 09/27/12'                                      
         END                                                                    
