*          DATA SET SXLDPRGE   AT LEVEL 005 AS OF 01/31/08                      
*CATALP SXLDPRGE                                                                
*===================================================================*           
*        PARAMS VIA R1                                              *           
*        XL1   X'00'  RETURN  X'00'=NOPURGE  OR  X'FF'=PURGE        *           
*        AL3   A(RECORD)                                            *           
*                                                                   *           
*        UPSI  00000001 PURGE X'80' DELETES                         *           
*        UPSI  00000010 PURGE X'C3' CLOSEOUTS                       *           
*===================================================================*           
         TITLE 'LDPURGE - SPOT - LOAD PURGE TEST'                               
         PRINT NOGEN                                                            
LDPURGE  CSECT                                                                  
         NMOD1 0,XSLDPRGE                                                       
*                                                                               
PU       BC    0,PU2                                                            
         OI    PU+1,X'F0'          SET BRANCH                                   
         LR    R0,R1               SAVE R1                                      
         L     R1,=V(UPSIVAL)                                                   
         MVC   UPSI,0(R1)                                                       
         LR    R1,R0               RESTORE R1                                   
*                                                                               
PU2      L     R3,0(R1)            R3 = A(RECORD)                               
         CLC   0(2,R3),=X'0D3F'    TEST AUTOPAY RECORD                          
         BE    APY                                                              
*                                                                               
PU10     TM    UPSI,X'07'          ANY PURGES                                   
         BZ    XIT                 NO                                           
         TM    34(R3),X'C0'        ANY DELETE INDS ON                           
         BNZ   ACAN                YES. A CANDIDATE                             
*                                                                               
PU20     DS    0H                                                               
*                                                                               
XIT      XMOD1 1                                                                
         EJECT                                                                  
* THIS IS A CANDIDATE FOR A PURGE                                               
ACAN     TM    UPSI,X'03'          DELETE OR CLOSEOUT PURGE                     
         BZ    XIT                 NO                                           
         BO    BOTH                DELETE BOTH X'80' AND X'C0'                  
         SPACE 2                                                                
* DELETE ONE OR THE OTHER                                                       
         TM    UPSI,X'01'                                                       
         BO    O80                 ONLY X'80'                                   
         TM    UPSI,X'02'                                                       
         BO    OC0                 ONLY X'C0'                                   
         B     XIT                                                              
         SPACE 2                                                                
* DELETE ONLY X'80'                                                             
O80      TM    34(R3),X'C0'                                                     
         BO    PU20                BOTH ON, NO DELETE                           
         TM    34(R3),X'80'                                                     
         BZ    PU20                                                             
O801     MVI   0(R1),255                                                        
         B     XIT                                                              
         SPACE 2                                                                
* DELETE ONLY X'C0'                                                             
OC0      TM    34(R3),X'C0'                                                     
         BO    O801                                                             
         B     XIT                                                              
         SPACE 2                                                                
* DELETE BOTH                                                                   
BOTH     TM    34(R3),X'C0'                                                     
         BO    O801                                                             
         TM    34(R3),X'80'                                                     
         BO    O801                                                             
         B     XIT                                                              
*                                                                               
APY      DS    0H                  DELETE AUTOPAY RECS > 2 WEEKS OLD            
         OC    APYDATE,APYDATE                                                  
         BNZ   APY2                                                             
         LR    R7,R1               SAVE R1                                      
         GOTO1 =V(DATCON),DMCB,(5,WORK),(0,WORK)       TODAY'S DATE             
         GOTO1 =V(ADDAY),DMCB,WORK,WORK,-14            MINUS 14 DAYS            
         GOTO1 =V(DATCON),DMCB,(0,WORK),(2,APYDATE)                             
         XC    APYDATE,=X'FFFF'                        COMPLEMENT DATE          
         LR    R1,R7               RESTORE R1                                   
*                                                                               
* BEALS SUGGESTS REMEMBERING DATES ARE COMPLEMENTED !!!                         
* SO BRANCHES LOOK BACKWARDS                                                    
*                                                                               
APY2     CLC   2(2,R3),APYDATE     KEEP RECS < CUTOFF DATE                      
         BL    PU10                                                             
         MVI   0(R1),X'FF'         PURGE RECS > CUTOFF                          
         B     XIT                                                              
*                                                                               
DMCB     DS    6F                                                               
WORK     DS    CL24                                                             
UPSI     DC    X'00'                                                            
ELCODE   DS    X                                                                
APYDATE  DC    XL2'00'                                                          
         LTORG                                                                  
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'005SXLDPRGE  01/31/08'                                      
         END                                                                    
