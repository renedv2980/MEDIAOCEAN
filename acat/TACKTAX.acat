*          DATA SET TACKTAX    AT LEVEL 118 AS OF 07/27/15                      
*CATALP TACKTAX                                                                 
         TITLE 'TACKTAX - TALENT GET TAX FROM ALLTAX ROUTINE'                   
TACKTAX  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 TASELQ,TACKTAX,RA                                                
         LR    R9,RC               R9 = A(TASEGUE INTERFACE BLOCK)              
         USING TASEGUED,R9                                                      
         L     RC,0(R1)            RC = A(GENCON WORKING STORAGE)               
         USING GEND,RC                                                          
         L     R8,ASPOOLD          R8 = A(SPOOL DSECT)                          
         USING SPOOLD,R8                                                        
         L     R7,ASUBSYSD         R7 = A(CONTROLLER WORKING STORAGE)           
         USING SUBSYSD,R7                                                       
         LA    R6,BUFF             R6 = A(LOCAL WORKING STORAGE)                
         LA    R6,8(R6)                                                         
         USING CHECKD,R6                                                        
         EJECT                                                                  
* MAIN DRIVER - SETS UP THE TASEGUE BLOCK WITH GLOBAL INFORMATION AND           
* THEN CALLS W4WITH, AND CANWITH TO SET UP THE WITHHOLDING ELEMENT              
* BLOCK IN THE CHECK RECORD.  THEN THE ROUTINE CALLS WITHLOOP WHICH             
* LOOPS THROUGH THE WITHHOLDING ELEMENTS AND REMOVES THE TAXES FOR              
* EACH.  THE ROUTINE FINALLY TOTALS UP THE TAXES AND SUBTRACTS THEM             
* FROM THE CHECK AMOUNT.                                                        
*                                                                               
* ASRTCHK = A(CHECK RECORD)                                                     
* AIO3 = TATU'S FOR P+ CHECKS (BUILT IN BLDTATU ROUTINE IN TACKREP)             
*                                                                               
MAIN     DS    0H                                                               
         CLI   RECNUM,EC           EXIT IF THIS IS EURO CHECKS                  
         BE    MX                                                               
*                                                                               
         GOTO1 USEVAL,DMCB,SRTUSE,SRTUTYP  SET GLOBAL USE VALUES                
*                                                                               
         CLI   RECNUM,CN           IF THIS IS CANADIAN DOLLAR CHECKS            
         BNE   *+12                                                             
         BAS   RE,CANTAX           THEN TAKE TAX FOR CANADIANS                  
         B     MX                                                               
*                                                                               
         CLI   SRTTYPE,TAW4TYFO    IF PERFORMER IS FOREIGNER                    
         BNE   MAIN05                                                           
         CLI   RECNUM,PK           PAYROLL PLUS?                                
         BNE   MAIN02                                                           
*        GOTOR TUWITH,DMCB,ASRTCHK BUILDS CERTAIN TU ELEMENTS                   
MAIN02   BAS   RE,FORTAX           THEN TAKE TAX FOR FOREIGNER                  
         B     MX                                                               
*                                                                               
MAIN05   CLI   RECNUM,PK                                                        
         BNE   MAIN10                                                           
         CLI   SRTTYPE,TAW4TYCA    ELSE IF PERFORMER IS CANADIAN                
         BNE   MAIN10                                                           
*                                                                               
         LARL  RF,SVCNET                                                        
         MVC   0(4,RF),SRTNET      SAVE NET                                     
*                                                                               
         BRAS  RE,CANWITH          ADD CANADA IF NECESSARY                      
*                                                                               
         MVI   ELCODE,TACWELQ                                                   
         MVC   AIO,ASRTCHK                                                      
         GOTO1 GETL,DMCB,(3,=C'CN ')                                            
         L     R4,TGELEM                                                        
         L     RF,SRTNET                                                        
         BAS   RE,CALCGST          CALC GST AND POSSIBLY SET GST PAID           
*                                                                               
         BRAS  RE,PKCANTAX         PAYROLL PLUS CANADIAN TAXES                  
         B     MX                                                               
*                                                                               
MAIN10   CLI   SRTTYPE,TAW4TYCA    ELSE IF PERFORMER IS CANADIAN                
         BE    MAIN20                                                           
         CLI   SRTTYPE,TAW4TYCO    OR CORPORATION                               
         BNE   MAIN30                                                           
MAIN20   CLI   RECNUM,PK           PAYROLL PLUS?                                
         BNE   MAIN22                                                           
*        GOTOR TUWITH,DMCB,ASRTCHK BUILDS CERTAIN TU ELEMENTS                   
MAIN22   BAS   RE,CORPTAX          THEN TAKE TAX FOR CORPORATION                
         B     MX                                                               
*                                                                               
*        ALL CODE FROM HERE ON HANDLES INDIVIDUALS.  INDIVIDUAL TAXES           
*        ARE CALCULATED BY CALLING THE COBOL ROUTINE, ALLTAX, FOR EACH          
*        WITHHOLDING ELEMENT IN THE CHECK RECORD.  THIS IS DONE IN THE          
*        ROUTINE, WITHLOOP.                                                     
*                                                                               
MAIN30   BRAS  RE,SETTAXBL         SET TAXABLE STATE                            
*                                                                               
         MVI   SRTFREQ,C'A'                                                     
MAIN40   MVC   TASECKDT,CHKDATEA   SAVE CHECK DATE IN TASEGUED                  
         TM    SRTINVS2,TAINSFRC   IF FORCING LAST DAY OF LAST YEAR             
         BZ    *+10                                                             
         MVC   TASECKDT,CHKDTEAL   SET DIFFERENT DATE                           
         MVC   TASEEMP,SRTEMP           EMPLOYER                                
         MVC   TASEEARN,SRTGRS          TAXABLE EARNINGS                        
         MVC   TASEFREQ,SRTFREQ         PAY FREQUENCY                           
*                                                                               
**NO-OP* MVC   TASEDEBT,=C'    '   SET DEBT STATE TO SPACES (FIELD              
*                                              CHANGED TO TASEYTDU)             
*                                                                               
         CLI   RECNUM,PK           PAYROLL PLUS?                                
         BNE   MAIN50                                                           
*                                                                               
*        GOTOR TUWITH,DMCB,ASRTCHK BUILDS TATU ELEMENTS BEFORE DUECOMP          
*        GOTOR TUWITH,DMCB,AIO3    BUILDS TATU ELEMENTS AFTER DUECOMP           
*                                                                               
         BAS   RE,W4TUWITH         BUILD TACW ELEMS                             
         BRAS  RE,ODWITH           OTHER DEDUCTIONS                             
*                                                                               
         BRAS  RE,SETFLAGS         SET SOME FLAGS FOR PROCESSING                
         BAS   RE,WITHPPLP         REMOVE TAXES FOR EACH WITHHLD ELEM           
         BRAS  RE,WITHSUI          REMOVE SUI TAXES IN EVTIME ORDER             
*                                                                               
         BRAS  RE,PROTACW          PRORATE WITHHOLDINGS                         
         B     MX                                                               
*                                                                               
MAIN50   BAS   RE,W4WITH           BUILD CW ELEMENT BLOCK FROM W4 REC           
         BAS   RE,CASTWITH         ADD CAST UNIT TO CW ELEMENT BLOCK            
         BRAS  RE,CANWITH          ADD CANADA IF NECESSARY                      
         BRAS  RE,ODWITH           OTHER DEDUCTIONS                             
*                                                                               
         BAS   RE,WITHLOOP         REMOVE TAXES FOR EACH WITHHLD ELEM           
*                                                                               
MX       B     XIT                                                              
         EJECT                                                                  
* THIS ROUTINE HANDLES TAXES FOR CANADIAN DOLLAR CHECKS.  CURRENTLY,            
* ONLY GST IS CALCULATED.  GST IS DUE TO PERFORMERS BASED ON THEIR              
* WAGES PLUS THE I&R FRATERNAL CONTRIBUTION.  UNIONS NO LONGER GET GST.         
*                                                                               
CANTAX   NTR1                                                                   
         BRAS  RE,CHKSSN           SKIP IF SOME CHECKS                          
         BNE   CTXIT                                                            
*                                                                               
         L     RF,SRTNET           SET 'PAYMENT + I&R' SUBJECT TO GST           
         A     RF,SRTINR                                                        
         BZ    CTXIT               DON'T GO ANY FURTHER IF NO BASIS             
*                                                                               
         BRAS  RE,CANWITH          ENSURE WE HAVE CN WITHHOLDING EL.            
         BNE   XIT                                                              
*                                                                               
         L     R4,TGELEM                                                        
         USING TACWD,R4            RETURNS A(ELEMENT)                           
*                                                                               
         BAS   RE,CALCGST          CALC GST AND POSSIBLY SET GST PAID           
*                                                                               
CTXIT    B     XIT                                                              
         DROP  R4                                                               
         EJECT                                                                  
* THIS ROUTINE HANDLES TAXES FOR FOREIGNERS.  IF THE WORK UNIT IS 'OT',         
* IT DOES NOTHING.  OTHERWISE, IT ADDS AN OTHER WITHHOLDING ELEMENT TO          
* THE CHECK RECORD IN WHICH IT TAKES 30% FEDERAL TAX.                           
*                                                                               
FORTAX   NTR1                                                                   
         MVI   NRESFLG,0                                                        
         MVI   FORFLAG,0                                                        
         MVC   FORGRS,SRTGRS                                                    
         MVC   FORNET,SRTNET                                                    
*                                                                               
         CLI   RECNUM,PK           PAYROLL PLUS?                                
         BE    FT50                                                             
         CLC   SRTSOW,=C'OT '      IF WORK UNIT IS NOT 'OT'                     
         BE    FTX                                                              
         TM    SRTBITS2,SRTFONTX   DON'T TAX FOREIGNER FED TAX?                 
         BO    FT7                 YES, SKIP IT                                 
         CLC   SRTSOW,=C'PR '      AND IF WORK UNIT IS PUERTO RICO              
         BE    FT7                                                              
         CLC   SRTSOW,=C'CN '      AND IF WORK UNIT IS NOT 'CN'                 
         BE    FT100                                                            
*                                                                               
FT5      BAS   RE,OTWITH           TAKE OTHER WITHHOLDING                       
*                                                                               
FT7      CLC   SRTUNIT,=C'CA '     TAKE NON-RES CA TAX?                         
         BE    *+10                                                             
         CLC   SRTUNIT,=C'LAX'                                                  
         BNE   FT9                                                              
         MVI   NRESFLG,NRESFFOR                                                 
         L     RF,SRTGRS           COMPUTE TAX = 7% EARNINGS                    
         M     RE,=F'7'                                                         
         GOTOR ADDNRES,DMCB,(RF),(RE),NRESFLG                                   
*                                                                               
FT9      CLC   SRTUNIT,=C'PR '     TAKE PUERTO RICO TAX                         
         BNE   FTX                                                              
         MVI   NRESFLG,NRESFFOR                                                 
         L     RF,SRTGRS           COMPUTE TAX = 7% EARNINGS                    
         M     RE,=F'29'                                                        
         GOTOR ADDNRES,DMCB,(RF),(RE),NRESFLG                                   
         B     FTX                                                              
*                                                                               
FT50     TM    SRTBITS2,SRTFONTX   DON'T TAX FOREIGNER FED TAX?                 
         BO    FT60                YES, SKIP IT                                 
*                                                                               
         MVC   AIO,ASRTCHK                                                      
         MVI   ELCODE,TATUELQ                                                   
         GOTO1 GETL,DMCB,(3,=C'OT ')                                            
         BE    FTX                                                              
*                                                                               
         GOTO1 GETL,DMCB,(3,=C'PR ')  WORKED IN PUERTO RICO?                    
         BNE   *+12                                                             
         MVI   FORFLAG,FORPR                                                    
         B     FT60                                                             
*                                                                               
         GOTO1 GETL,DMCB,(3,=C'CN ')  WORKED IN CANADA?                         
         BE    FT100                                                            
*                                                                               
         BAS   RE,OTWITH           TAKE OTHER WITHHOLDING                       
*                                                                               
FT60     BRAS  RE,TAXCAPR          TAX CA OR PR                                 
         J     FTX                                                              
*                                                                               
FT100    BRAS  RE,CANWITH          ADD CANADA WITHHOLD ELEM IF NEEDED           
         BNE   FTX                                                              
         L     R4,TGELEM           SET A(WITHHOLDING ELEMENT)                   
         BAS   RE,PROCWITH         REMOVE TAXES FOR CANADA ELEMENT              
         BAS   RE,CHKQC            CHECK IF ADDITIONAL 9% QUEBEC TAX            
*                                                                               
FTX      B     XIT                                                              
*        DROP  R4                                                               
         EJECT                                                                  
*                                                                               
OTWITH   NTR1                                                                   
         L     RF,SRTGRS           COMPUTE FEDERAL TAX = 30% EARNINGS           
         CLI   FORFLAG,FORPR       WORKED IN PR?                                
         BNE   *+8                                                              
         ICM   RF,15,FORGRS                                                     
         M     RE,=F'30'                                                        
         D     RE,=F'50'                                                        
         LTR   RF,RF                                                            
         BM    *+8                                                              
         AH    RF,=H'1'                                                         
         SRA   RF,1                                                             
*                                                                               
         L     RE,SRTNET           IF FEDERAL TAX > AVAILABLE BALANCE           
         CLI   FORFLAG,FORPR       WORKED IN PR?                                
         BNE   *+8                                                              
         ICM   RE,15,FORNET                                                     
         S     RE,SRTMDED                                                       
         S     RE,SRTDUES                                                       
         CR    RF,RE                                                            
         BNH   *+6                                                              
         LR    RF,RE               THEN FEDERAL TAX = AVAILABLE BALANCE         
*                                                                               
         LTR   RF,RF               DON'T GO ANY FURTHER IF NOTHING              
         BZ    OTWITHX             WITHHELD                                     
*                                                                               
         ST    RF,SRTFTAX          SAVE IN SORT RECORD                          
*                                                                               
         L     RE,SRTNET           SUBTRACT TAX FROM REMAINING BALANCE          
         SR    RE,RF                                                            
         ST    RE,SRTNET                                                        
*                                                                               
         L     RE,SRTTDED          ADD TAX TO TOTAL DEDUCTIONS                  
         AR    RE,RF                                                            
         ST    RE,SRTTDED                                                       
*                                                                               
         LA    R4,ELEM             BUILD OTHER WITHHOLDING ELEMENT              
         USING TAODD,R4                                                         
         XC    ELEM,ELEM                                                        
         MVI   TAODEL,TAODELQ                                                   
         MVI   TAODLEN,TAODLNQ                                                  
         MVI   TAODTYPE,TAODTYPF                                                
         MVC   TAODAMT,SRTFTAX     SAVE TAX IN ELEMENT                          
*                                                                               
         MVC   AIO,ASRTCHK         ADD ELEMENT TO CHECK RECORD                  
         GOTO1 ADDELEM                                                          
         MVC   AIO,AIO1                                                         
OTWITHX  B     XIT                                                              
         DROP  R4                                                               
*                                                                               
FORGRS   DS    XL4                 GROSS AMOUNT                                 
FORNET   DS    XL4                 NET AMOUNT                                   
FORFLAG  DS    XL1                 FOREIGN FLAG                                 
FORPR    EQU   X'80'               WORKED IN PR                                 
*                                                                               
* THIS ROUTINE HANDLES TAXES FOR CORPORATIONS.  SO FAR THERE ARE THREE          
* CASES WHERE TAXES ARE TAKEN FOR CORPORATIONS: 1)WHEN THE TAPDSCAN             
* BIT (TAKE CANADIAN TAXES) IS SET IN THE TAPD ELEMENT OF THE INVOICE.          
* 2) NONRESIDENT CORPS WORKING IN CALIFORNIA.                                   
* 3) US$ ACTRA PAYMENT WITH GST# ON W4                                          
* 4) NONRESIDENT CORPS WORKING IN NORTH CAROLINA                                
*                                                                               
CORPTAX  NTR1                                                                   
         TM    TGSYSTAT,TASYSFLT   NEW FLAT TAX RULES?                          
         BZ    CRPT05                                                           
         CLI   SRTTYPE,TAW4TYCA    IF PERFORMER IS CANADIAN                     
         BNE   CRPT05                                                           
         BAS   RE,FORTAX                                                        
*                                                                               
         MVC   AIO,ASRTCHK         ADDED THIS IN FORTAX?                        
         MVI   ELCODE,TACWELQ      YES, THEN ALREADY DID GST                    
         GOTO1 GETL,DMCB,(3,=C'CN ')                                            
         BE    CRPX                                                             
*                                                                               
         BRAS  RE,CANWITH          ADD CANADA WITHHLD ELEM IF NECESSARY         
*                                                                               
         MVC   AIO,ASRTCHK                                                      
         MVI   ELCODE,TACWELQ                                                   
         GOTO1 GETL,DMCB,(3,=C'CN ')                                            
         BNE   CRPX                                                             
         L     R4,TGELEM                                                        
         L     RF,SRTNET                                                        
*                                                                               
         BAS   RE,CALCGST                                                       
         B     CRPX                                                             
*                                                                               
CRPT05   MVI   NRESFLG,0                                                        
         BRAS  RE,CHKSSN           SKIP IF SOME CHECKS                          
         BNE   CRPT60                                                           
         BRAS  RE,CANWITH          ADD CANADA WITHHLD ELEM IF NECESSARY         
         BNE   CRPT10                                                           
         L     R4,TGELEM           SET A(WITHHOLDING ELEMENT)                   
         BAS   RE,PROCWITH         REMOVE TAXES FOR CANADA ELEMENT              
*        B     CRPX                                                             
*                                                                               
CRPT10   CLI   SRTW4TY,TAW4TYCO    CORPORATION, SEE IF NON-RES IN CA            
         BE    *+14                                                             
         OC    SRTFSO,SRTFSO       TEST PAYING INDIV AS CORP                    
         BZ    CRPT60              NO, QUIT                                     
         MVI   NRESFLG,NRESFCRP                                                 
*                                                                               
         CLI   RECNUM,PK           PAYROLL PLUS?                                
         BE    CRPT40                                                           
*                                                                               
         GOTOR TSTNRES,DMCB,=C'CA '  NONRES WRKING IN CA                        
         BNE   CRPT20                                                           
         L     RF,SRTGRS           COMPUTE TAX = 7% EARNINGS                    
         M     RE,=F'7'                                                         
         B     CRPT30                                                           
*                                                                               
CRPT20   GOTOR TSTNRES,DMCB,=C'NC '  NONRES WRKING IN NC                        
         BNE   CRPT60                                                           
         L     RF,SRTGRS           COMPUTE TAX = 4% EARNINGS                    
         M     RE,=F'4'                                                         
*                                                                               
CRPT30   GOTOR ADDNRES,DMCB,(RF),(RE),NRESFLG                                   
         B     CRPT60                                                           
*                                                                               
CRPT40   L     R4,ASRTCHK          PAYROLL PLUS                                 
         MVI   ELCODE,TATUELQ                                                   
         BAS   RE,GETEL                                                         
         B     *+8                                                              
CRPT45   BAS   RE,NEXTEL                                                        
         BNE   CRPT60                                                           
         USING TATUD,R4                                                         
*                                                                               
         MVC   TGFULL,=F'7'        CALCULATE ONLY CALIFORNIA                    
         CLC   TATUUNIT,=C'CA '    OR NORTH CAROLINA TAXES                      
         BE    CRPT50                                                           
         CLC   TATUUNIT,=C'NC '                                                 
         BNE   CRPT45                                                           
         MVC   TGFULL,=F'4'                                                     
*                                                                               
CRPT50   GOTOR TSTNRES,DMCB,TATUUNIT                                            
         BE    *+12                                                             
         MVI   ELCODE,TATUELQ                                                   
         B     CRPT45                                                           
*                                                                               
*        ICM   RF,15,TATUWAGE                                                   
*        ICM   RE,15,TATUSTRE                                                   
         ICM   RF,15,TATUWAAD                                                   
         ICM   RE,15,TATUTNAD                                                   
         AR    RF,RE                                                            
         XR    RE,RE                                                            
         M     RE,TGFULL                                                        
*                                                                               
         MVC   SRTUNIT,TATUUNIT                                                 
*                                                                               
         GOTOR ADDNRES,DMCB,(RF),(RE),NRESFLG                                   
         MVI   ELCODE,TATUELQ                                                   
         B     CRPT45                                                           
         DROP  R4                                                               
*                                                                               
CRPT60   BAS   RE,CHKQC            CHECK IF ADDITIONAL 9% QUEBEC TAX            
*                                                                               
CRPX     B     XIT                                                              
*                                                                               
NRESFLG  DS    XL1                                                              
NRESFCRP EQU   TACWNCRP            NON-RESIDENT CORPORATION                     
NRESFFOR EQU   TACWNFOR            NON-RESIDENT CORPORATION                     
         SPACE 3                                                                
*                                                                               
* THIS ROUTINE BUILDS CHECK WITHOLDING ELEMENTS (TACW) FOR ALLTAX               
*                                                                               
W4TUWITH NTR1                                                                   
*                                                                               
* BUILD SKELETON TACW ELEMENTS FROM TATU'S (WHAT WAS PAID)                      
*                                                                               
         L     R4,ASRTCHK          BUILD TACW ELEMENTS FROM TATU                
         MVI   ELCODE,TATUELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   W4TUWX              DONE IF NO ELEMENTS                          
         USING TATUD,R4                                                         
*                                                                               
W4TUW10  TM    TATUSTAT,TATUSCAN   CANADIAN PROVINCE?                           
         BO    W4TUW15                                                          
*                                                                               
         LA    R5,ELEM             BUILD SKELETON CHECK WITHHOLD ELEM           
         USING TACWD,R5                                                         
         XC    ELEM,ELEM                                                        
         MVI   TACWEL,TACWELQ                                                   
         OI    TACWSTAT,TACWSTAX   TAXABLE INDICATOR                            
         TM    TATUSTAT,TATUSRES   RESIDENCE ONLY?                              
         BO    *+8                                                              
         OI    TACWSTAT,TACWSWRK   WORKING INDICATOR                            
         MVC   TACWUNIT,TATUUNIT   MOVE TAX UNIT TO CW ELEMENT                  
*                                                                               
         MVI   TACWLEN,TACWLN2Q                                                 
         CLC   TACWUNIT,=C'FD '                                                 
         BE    *+10                                                             
         CLC   TACWUNIT,=C'CN '                                                 
         BE    *+12                                                             
         MVI   TACWLEN,TACWLN3Q    STATE                                        
         CLI   TACWUNIT+2,C' '                                                  
         BE    *+8                                                              
         MVI   TACWLEN,TACWLN4Q    CITY                                         
*                                                                               
         MVC   AIO,ASRTCHK         ADD CW ELEMENT TO CHECK RECORD               
         GOTO1 ADDELEM                                                          
*                                                                               
W4TUW15  BAS   RE,NEXTEL           GET NEXT WH ELEMENT                          
         BE    W4TUW10             REPEAT UNTIL NO MORE                         
*                                                                               
* UPDATE RESIDENCE TACW ELEMENTS WITH STATUS FROM TAWH AND                      
* ADD SKELETON RESIDENCE TACW ELEMENTS FROM TAWH (NOT PAID)                     
*                                                                               
         L     R4,AW4IO            ADD TACW FOR W4 WITHOLDINGS THAT             
         MVI   ELCODE,TAWHELQ      WERE NOT PAID                                
         BAS   RE,GETEL                                                         
         BNE   W4TUWX              DONE IF NO ELEMENTS                          
         USING TAWHD,R4                                                         
*                                                                               
W4TUW20  MVC   AIO,ASRTCHK         UPDATE TACW ELEMS WITH RESIDENCE             
         MVI   ELCODE,TACWELQ      INDICATOR + STATUS                           
         MVC   TGTHREE,TAWHUNIT                                                 
         GOTO1 GETL,DMCB,(3,TGTHREE)                                            
         BE    W4TUW30                                                          
*                                                                               
         LA    R5,ELEM             BUILD RESIDENT CHECK WITHHOLD ELEM           
         USING TACWD,R5                                                         
         XC    ELEM,ELEM                                                        
         MVI   TACWEL,TACWELQ                                                   
         MVC   TACWUNIT,TAWHUNIT   MOVE TAX UNIT TO CW ELEMENT                  
         OI    TACWSTAT,TACWSRES   SET RESIDENCE INDICATOR                      
*                                                                               
         MVI   TACWLEN,TACWLN2Q                                                 
         CLC   TACWUNIT,=C'FD '                                                 
         BE    *+10                                                             
         CLC   TACWUNIT,=C'CN '                                                 
         BE    *+12                                                             
         MVI   TACWLEN,TACWLN3Q    STATE                                        
         CLI   TACWUNIT+2,C' '                                                  
         BE    *+8                                                              
         MVI   TACWLEN,TACWLN4Q    CITY                                         
*                                                                               
         MVC   AIO,ASRTCHK         ADD CW ELEMENT TO CHECK RECORD               
         GOTO1 ADDELEM                                                          
         B     W4TUW40                                                          
*                                                                               
W4TUW30  L     R5,TGELEM                                                        
         OI    TACWSTAT,TACWSRES   SET RESIDENCE INDICATOR                      
         MVC   TACWMST,TAWHSTAT    MARITAL STATUS                               
         EDIT  (1,TAWHEXS),(2,TACWEXS),ALIGN=RIGHT,FILL=0                       
*                                                                               
         CLI   TACWUNIT+2,C' '     DON'T COPY FLAT TAX FOR CITY                 
         BE    *+14                                                             
         CLC   SRTCOR,TACWUNIT     CITY OF RES = WORK CITY?                     
         BNE   *+10                                                             
         MVC   TACWTAX,TAWHFLAT    SAVE FLAT RATE IN TAX IF ANY                 
*                                                                               
W4TUW40  MVI   ELCODE,TAWHELQ                                                   
         BAS   RE,NEXTEL           GET NEXT TAWH ELEMENT                        
         BE    W4TUW20             REPEAT UNTIL NO MORE                         
         DROP  R5                                                               
*                                                                               
* UPDATE NON RESIDENCE TACW ELEMENTS STATUS FROM TAWH                           
*                                                                               
         L     R4,ASRTCHK          GET CHECK WITHOLDING ELEMENTS                
         MVI   ELCODE,TACWELQ                                                   
         BAS   RE,GETEL                                                         
         B     *+8                                                              
W4TUW50  BAS   RE,NEXTEL                                                        
         BNE   W4TUWX                                                           
*                                                                               
         USING TACWD,R4                                                         
         TM    TACWSTAT,TACWSRES   RESIDENT TACW?                               
         BO    W4TUW50                                                          
*                                                                               
         L     R5,AW4IO            GET TAWH'S                                   
         USING TLW4D,R5                                                         
         LA    R5,TLW4ELEM                                                      
         USING TAWHD,R5                                                         
*                                                                               
W4TUW60  CLI   0(R5),0             END OF RECORD                                
         BE    W4TUW50                                                          
         CLI   0(R5),TAWHELQ                                                    
         BH    W4TUW50                                                          
         BNE   W4TUW90                                                          
*                                                                               
         CLC   TAWHUNIT,=C'FD '    SKIP FEDERAL                                 
         BE    W4TUW90                                                          
*                                                                               
         CLI   TACWUNIT+2,C' '     STATE?                                       
         BNE   W4TUW70                                                          
         CLI   TAWHUNIT+2,C' '                                                  
         BE    W4TUW80                                                          
         B     W4TUW90                                                          
*                                                                               
W4TUW70  CLI   TAWHUNIT+2,C' '     CITY?                                        
         BNE   W4TUW80                                                          
         MVC   TACWMST,TAWHSTAT    DEFAULT TO STATE LEVEL                       
         EDIT  (1,TAWHEXS),(2,TACWEXS),ALIGN=RIGHT,FILL=0                       
         B     W4TUW90             THEN SEE IF CITY LEVEL EXISTS                
*                                                                               
W4TUW80  MVC   TACWMST,TAWHSTAT    MARITAL STATUS                               
         EDIT  (1,TAWHEXS),(2,TACWEXS),ALIGN=RIGHT,FILL=0                       
*                                                                               
         CLI   TACWUNIT+2,C' '     DON'T COPY FLAT TAX FOR CITY                 
         BE    *+14                                                             
         CLC   SRTCOR,TACWUNIT     CITY OF RES = WORK CITY?                     
         BNE   *+10                                                             
         MVC   TACWTAX,TAWHFLAT    SAVE FLAT RATE IN TAX IF ANY                 
         B     W4TUW50                                                          
*                                                                               
W4TUW90  ZIC   RF,1(R5)                                                         
         AR    R5,RF                                                            
         B     W4TUW60                                                          
*                                                                               
W4TUWX   MVC   AIO,AIO1                                                         
         B     XIT                                                              
         DROP  R4,R5                                                            
         EJECT                                                                  
* THIS ROUTINE CONTROLS WITHHOLDING FOR THE CHECK BY CALLING WITHHOLD           
* FOR EACH TAX UNIT WHICH MAY REQUIRE WITHHOLDING.  WITHHOLD WILL GET           
* THE CORRESPONDING CHECK WITHHOLDING ELEMENT AND CALL PROCWITH TO              
* REMOVE THE TAXES.                                                             
*                                                                               
WITHPPLP NTR1                                                                   
         MVC   AIO,ASRTCHK                                                      
         MVI   ELCODE,TACWELQ                                                   
         MVC   TGTHREE,=C'FD '     ALWAYS PROCESS FEDERAL FIRST                 
         GOTO1 GETL,DMCB,(3,TGTHREE)                                            
         BNE   *+8                                                              
         BAS   RE,WITHHOLD                                                      
*                                                                               
         NI    CHKFLAGS,X'FF'-CHKFROI                                           
         TM    TGSYSTA2,TASYSROI   RETAIN ORDER OF INPUT?                       
         BO    WPPL100                                                          
*                                                                               
         USING TACWD,R4                                                         
         L     R4,ASRTCHK          A(CHECK RECORD)                              
         MVI   ELCODE,TACWELQ      GET WITHHOLDING EL.                          
         BAS   RE,GETEL                                                         
         BE    WPPL15                                                           
         B     WPPLX                                                            
*                                                                               
WPPL10   MVI   ELCODE,TACWELQ      GET NEXT WITHHOLDING EL.                     
         BAS   RE,NEXTEL                                                        
         BNE   WPPL250                                                          
*                                                                               
WPPL15   CLC   TACWUNIT,=C'FD '    ALREADY PROCESSED FEDERAL, SKIP IT           
         BE    WPPL10                                                           
         CLI   TACWUNIT+2,C' '                                                  
         BNE   WPPL20                                                           
         TM    TACWSTAT,TACWSRES   RESIDENCE?                                   
         BZ    *+10                                                             
         MVC   SRTSOR,TACWUNIT     STATE OF RESIDENCE                           
         TM    TACWSTAT,TACWSTAX   TAXABLE?                                     
         BZ    *+10                                                             
         MVC   SRTTXST,TACWUNIT    SET TAXABLE STATE                            
         B     WPPL30                                                           
*                                                                               
WPPL20   TM    TACWSTAT,TACWSRES   RESIDENCE?                                   
         BZ    *+10                                                             
         MVC   SRTCOR,TACWUNIT     CITY OF RESIDENCE                            
         TM    TACWSTAT,TACWSTAX   TAXABLE?                                     
         BZ    *+10                                                             
         MVC   SRTTXCY,TACWUNIT    SET TAXABLE CITY                             
*                                                                               
WPPL30   MVC   TGTHREE,TACWUNIT    SET UNIT                                     
         BAS   RE,WITHHOLD         REMOVE TAXES                                 
         B     WPPL10                                                           
         DROP  R4                                                               
*                                                                               
* RETAIN ORDER OF INPUT                                                         
*                                                                               
* TAKE TAXES IN EVTIME ORDER FIRST.  AFTER THAT, PROCESS ANY LEFTOVER           
* DOLLARS/UNITS.                                                                
*                                                                               
         USING SUITABD,R3                                                       
WPPL100  L     R3,ASUITAB                                                       
WPPL110  CLI   0(R3),X'FF'                                                      
         BE    WPPL140                                                          
         CLI   SUITFLAG,SUITFPRO                                                
         BE    WPPL135                                                          
*                                                                               
         USING TACWD,RF                                                         
         MVC   AIO,ASRTCHK                                                      
         MVI   ELCODE,TACWELQ                                                   
         GOTO1 GETL,DMCB,(3,SUITUNIT)                                           
         BNE   WPPL135                                                          
         L     RF,TGELEM                                                        
*                                                                               
         CLI   TACWUNIT+2,C' '                                                  
         BNE   WPPL120                                                          
*                                                                               
         TM    TACWSTAT,TACWSRES   RESIDENCE?                                   
         BZ    *+10                                                             
         MVC   SRTSOR,TACWUNIT     STATE OF RESIDENCE                           
         TM    TACWSTAT,TACWSTAX   TAXABLE?                                     
         BZ    *+10                                                             
         MVC   SRTTXST,TACWUNIT    SET TAXABLE STATE                            
         B     WPPL130                                                          
*                                                                               
WPPL120  TM    TACWSTAT,TACWSRES   RESIDENCE?                                   
         BZ    *+10                                                             
         MVC   SRTCOR,TACWUNIT     CITY OF RESIDENCE                            
         TM    TACWSTAT,TACWSTAX   TAXABLE?                                     
         BZ    *+10                                                             
         MVC   SRTTXCY,TACWUNIT    SET TAXABLE CITY                             
*                                                                               
WPPL130  MVC   TGTHREE,TACWUNIT    SET UNIT                                     
         BAS   RE,WITHHOLD         REMOVE TAXES                                 
*                                                                               
         L     RF,ASUITAB                                                       
WPPL132  CLI   0(RF),X'FF'         MARK ALL THOSE UNITS PROCESSED               
         BE    WPPL135                                                          
         CLI   SUITFLAG-SUITUNIT(RF),SUITFPRO                                   
         BE    WPPL134                                                          
*                                                                               
         CLC   SUITUNIT,0(RF)                                                   
         BNE   *+8                                                              
         MVI   SUITFLAG-SUITUNIT(RF),SUITFPRO                                   
*                                                                               
WPPL134  AHI   RF,SUITABLN                                                      
         B     WPPL132                                                          
*                                                                               
WPPL135  AHI   R3,SUITABLN                                                      
         B     WPPL110                                                          
         DROP  R3,RF                                                            
*                                                                               
* FINISH UP ANY LEFTOVER DOLLARS                                                
*                                                                               
* GO THROUGH TACW'S TO SEE IF ANY UNITS WERE NOT TAXED YET                      
*                                                                               
         USING TACWD,R4                                                         
WPPL140  L     R4,ASRTCHK          A(CHECK RECORD)                              
         MVI   ELCODE,TACWELQ      GET WITHHOLDING EL.                          
         BAS   RE,GETEL                                                         
         BE    WPPL145                                                          
         B     WPPLX                                                            
*                                                                               
WPPL142  MVI   ELCODE,TACWELQ      GET NEXT WITHHOLDING EL.                     
         BAS   RE,NEXTEL                                                        
         BNE   WPPL180                                                          
*                                                                               
WPPL145  CLC   TACWUNIT,=C'FD '    ALREADY PROCESSED FEDERAL, SKIP IT           
         BE    WPPL142                                                          
*                                                                               
         USING SUITABD,RF                                                       
         L     RF,ASUITAB                                                       
WPPL147  CLI   0(RF),X'FF'         DID WE ALREADY TAKE TAXES?                   
         BE    WPPL149                                                          
         CLC   SUITUNIT,TACWUNIT                                                
         BE    WPPL142                                                          
         AHI   RF,SUITABLN                                                      
         B     WPPL147                                                          
         DROP  RF                                                               
*                                                                               
WPPL149  CLI   TACWUNIT+2,C' '                                                  
         BNE   WPPL150                                                          
         TM    TACWSTAT,TACWSRES   RESIDENCE?                                   
         BZ    *+10                                                             
         MVC   SRTSOR,TACWUNIT     STATE OF RESIDENCE                           
         TM    TACWSTAT,TACWSTAX   TAXABLE?                                     
         BZ    *+10                                                             
         MVC   SRTTXST,TACWUNIT    SET TAXABLE STATE                            
         B     WPPL160                                                          
*                                                                               
WPPL150  TM    TACWSTAT,TACWSRES   RESIDENCE?                                   
         BZ    *+10                                                             
         MVC   SRTCOR,TACWUNIT     CITY OF RESIDENCE                            
         TM    TACWSTAT,TACWSTAX   TAXABLE?                                     
         BZ    *+10                                                             
         MVC   SRTTXCY,TACWUNIT    SET TAXABLE CITY                             
*                                                                               
WPPL160  MVC   TGTHREE,TACWUNIT    SET UNIT                                     
         BAS   RE,WITHHOLD         REMOVE TAXES                                 
         B     WPPL142                                                          
         DROP  R4                                                               
*                                                                               
         USING SUITABD,RF                                                       
WPPL180  L     RF,ASUITAB                                                       
WPPL185  CLI   0(RF),X'FF'         CLEAR OUT PROCESSED FLAG                     
         BE    WPPL250                                                          
         MVI   SUITFLAG,0                                                       
         AHI   RF,SUITABLN                                                      
         B     WPPL185                                                          
         DROP  RF                                                               
*                                                                               
WPPL250  BAS   RE,CHKQC            CHECK IF ADDITIONAL 9% QUEBEC TAX            
*                                                                               
WPPLX    B     XIT                                                              
*                                                                               
ACURELEM DS    F                                                                
*                                                                               
         EJECT                                                                  
* THIS ROUTINE USES THE WITHHOLDING ELEMENTS FOUND IN THE W4 RECORD             
* (WHICH SHOULD RESIDE IN AW4IO) TO ADD CHECK WITHHOLDING ELEMENTS              
* TO THE CHECK RECORD.                                                          
*                                                                               
W4WITH   NTR1                                                                   
         L     R4,AW4IO            R4 = A(FIRST W4 WITHHOLDING ELEMENT)         
         MVI   ELCODE,TAWHELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   WWX                 DONE IF NO ELEMENTS                          
         USING TAWHD,R4                                                         
*                                                                               
         LA    R5,ELEM             BUILD SKELETON CHECK WITHHOLD ELEM           
         USING TACWD,R5                                                         
         XC    ELEM,ELEM                                                        
         MVI   TACWEL,TACWELQ                                                   
*                                                                               
WW10     OC    TAWHEMP,TAWHEMP     IF NO EMPLOYER DEFINED                       
         BNZ   *+10                                                             
         MVC   TAWHEMP,TGTPEMP     SET TO DEFAULT EMPLOYER                      
*                                                                               
         CLC   TAWHEMP,TGTPEMP     ONLY USE TALENT PARTNERS RULES               
         BNE   WW90                ** NOT SUPPORTING MULT. RULES YET **         
*                                                                               
         MVC   TACWUNIT,TAWHUNIT   MOVE TAX UNIT TO CW ELEMENT                  
         MVC   TACWMST,TAWHSTAT         MARITAL STATUS                          
*                                                                               
*                                  MOVE EXEMPTIONS TO CW ELEMENT                
         EDIT  (1,TAWHEXS),(2,TACWEXS),ALIGN=RIGHT,FILL=0                       
*                                                                               
         OI    TACWSTAT,TACWSRES   SET RESIDENT BIT IN STATUS                   
*                                                                               
         MVC   TACWTAX,TAWHFLAT    SAVE FLAT RATE IN TAX IF ANY                 
*                                                                               
         MVI   TACWLEN,TACWLN2Q                                                 
         CLI   RECNUM,PK                                                        
         BNE   WW20                                                             
         CLC   TACWUNIT,=C'FD '                                                 
         BE    *+10                                                             
         CLC   TACWUNIT,=C'CN '                                                 
         BE    *+12                                                             
         MVI   TACWLEN,TACWLN3Q    STATE                                        
         CLI   TACWUNIT+2,C' '                                                  
         BE    *+8                                                              
         MVI   TACWLEN,TACWLN4Q    CITY                                         
*                                                                               
WW20     MVC   AIO,ASRTCHK         ADD CW ELEMENT TO CHECK RECORD               
         GOTO1 ADDELEM                                                          
         MVC   AIO,AIO1                                                         
*                                                                               
WW90     BAS   RE,NEXTEL           GET NEXT WH ELEMENT                          
         BE    WW10                REPEAT UNTIL NO MORE                         
*                                                                               
WWX      B     XIT                                                              
         DROP  R4,R5                                                            
         EJECT                                                                  
* THIS ROUTINE TAKES THE TAX UNIT FROM THE CAST ELEMENT AND USES IT TO          
* EITHER UPDATE OR ADD A NEW CHECK WITHHOLDING ELEMENT TO THE CHECK             
* RECORD.                                                                       
*                                                                               
CASTWITH NTR1                                                                   
         MVC   AIO,ASRTCHK         SET I/O AREA = CHECK RECORD                  
*                                                                               
         MVI   ELCODE,TACWELQ      GET SOR WITHHOLDING EL.                      
         GOTO1 GETL,DMCB,(3,SRTSOR)                                             
         L     R4,TGELEM                                                        
         BE    CW10                                                             
         GOTO1 GETL,DMCB,=C'FD '   IF NOT FOUND GET FEDERAL                     
         L     R4,TGELEM                                                        
         BE    CW10                                                             
         L     R4,ASRTCHK                                                       
         BAS   RE,GETEL            ELSE TRY ANY                                 
         BNE   CWX                                                              
*                                                                               
         USING TACWD,R4            USE IT TO BUILD NEW ELEMENTS                 
CW10     MVC   ELEM(TACWLN2Q),TACWEL                                            
         LA    R4,ELEM                                                          
         MVI   TACWSTAT,TACWSWRK   PRESET WORK INDICATOR                        
*                                                                               
         GOTO1 GETL,DMCB,(3,SRTSOW)  IF ELEMENT FOR SOW ALREADY EXISTS          
         BNE   CW30                                                             
         L     R4,TGELEM           UPDATE ELEM WITH WORK INDICATOR              
         OI    TACWSTAT,TACWSWRK                                                
         B     CW40                                                             
*                                                                               
CW30     MVC   TACWUNIT,SRTSOW     ELSE ADD NEW ELEMENT FOR WORK UNIT           
         CLC   TACWUNIT,=C'CN '    IF SOW IS CANADA                             
         BNE   *+10                                                             
         XC    TACWTAX,TACWTAX     DON'T ALLOW FLAT TAX RATE                    
         GOTO1 ADDELEM                                                          
*                                                                               
CW40     OC    SRTCOW,SRTCOW       IF WE HAVE CITY OF WORK                      
         BZ    CW100                                                            
         GOTO1 GETL,DMCB,(3,SRTCOW)  AND ELEMENT FOR IT EXISTS                  
         BNE   CW50                                                             
         L     R4,TGELEM                                                        
         OI    TACWSTAT,TACWSWRK   UPDATE ELEM WITH WORK INDICATOR              
         B     CW100                                                            
*                                                                               
CW50     LA    R4,ELEM             ELSE ADD NEW ELEMENT FOR CITY UNIT           
         MVC   TACWUNIT,SRTCOW                                                  
         XC    TACWTAX,TACWTAX     NEVER CARRY FLAT TAX FROM W4 FOR COW         
         GOTO1 ADDELEM                                                          
*                                                                               
CW100    CLC   SRTTXST,=C'CN '     IF TAXABLE STATE IS NOT CANADA               
         BE    CWX                                                              
         CLC   SRTTXST,=C'OT '     OR OTHER                                     
         BE    CWX                                                              
*                                                                               
         GOTO1 GETL,DMCB,=C'FD '   THEN IF FEDERAL ELEMENT EXISTS               
         BNE   CW110                                                            
         L     R4,TGELEM                                                        
         OI    TACWSTAT,TACWSWRK+TACWSTAX  SET WORKED/TAXABLE                   
         B     CWX                                                              
*                                                                               
CW110    LA    R4,ELEM             ELSE ADD NEW ELEMENT FOR FEDERAL             
         MVC   TACWUNIT,=C'FD '                                                 
         OI    TACWSTAT,TACWSWRK+TACWSTAX  SET WORKED/TAXABLE                   
         GOTO1 ADDELEM                                                          
*                                                                               
CWX      MVC   AIO,AIO1                                                         
         B     XIT                                                              
         DROP  R4                                                               
         EJECT                                                                  
* THIS ROUTINE CONTROLS WITHHOLDING FOR THE CHECK BY CALLING WITHHOLD           
* FOR EACH TAX UNIT WHICH MAY REQUIRE WITHHOLDING.  WITHHOLD WILL GET           
* THE CORRESPONDING CHECK WITHHOLDING ELEMENT AND CALL PROCWITH TO              
* REMOVE THE TAXES.                                                             
*                                                                               
WITHLOOP NTR1                                                                   
         USING PYTDTABD,R3                                                      
         LA    R3,BLOCK            BUILD ENTRY FOR THIS EMPLOYER/SSN            
         XC    0(PYTDLEN,R3),0(R3)                                              
         MVC   PYTDEMP,SRTEMP                                                   
         TM    SRTINVS2,TAINSFRC   IF FORCING DATE TO END OF LAST YEAR          
         BZ    *+8                                                              
         NI    PYTDEMP,X'DF'       TURN OFF X'40' BIT IN EMPLOYER               
         MVC   PYTDSSN,SRTSSN                                                   
*                                                                               
         L     R2,APYTDTAB         R2=A(BINSRCH PARAMETERS/TABLE)               
         USING BIND,R2                                                          
         MVC   DMCB+8(16),BININ    SET PARAMTERS TO BINSRCH                     
*                                                                               
         GOTO1 BINSRCH,DMCB,(R3),BINTABLE                                       
*                                                                               
         CLI   0(R1),1             MUST BE FOUND                                
         BE    WL05                                                             
*                                                                               
         L     R3,DMCB                                                          
         MVC   TGYTDSUI,PYTDAMTS+(TYCSUI-TYCYTD)  PREV YTD SUI EARNINGS         
         DROP  R2,R3                                                            
*                                                                               
WL05     MVC   TGTHREE,=C'FD '     SET FEDERAL TAX UNIT                         
         BAS   RE,WITHHOLD         REMOVE TAXES FOR FEDERAL ELEMENT             
*                                                                               
         OC    SRTSOW,SRTSOW       IF STATE OF WORK DEFINED                     
         BZ    WL10                                                             
         MVC   TGTHREE,SRTSOW      SET STATE OF WORK                            
         BAS   RE,WITHHOLD         REMOVE TAXES FOR SOW ELEMENT                 
*                                                                               
         OC    SRTCOW,SRTCOW       IF CITY OF WORK DEFINED                      
         BZ    WL10                                                             
         MVC   TGTHREE,SRTCOW      SET CITY OF WORK                             
         BAS   RE,WITHHOLD         REMOVE TAXES FOR COW ELEMENT                 
*                                                                               
WL10     OC    SRTSOR,SRTSOR       IF STATE OF RESIDENCE DEFINED                
         BZ    WL30                                                             
         CLC   SRTSOR,SRTSOW       AND IT'S DIFFERENT THAN SOW                  
         BE    WL20                                                             
         MVC   TGTHREE,SRTSOR      SET STATE OF RESIDENCE                       
         BAS   RE,WITHHOLD         REMOVE TAXES FOR SOR ELEMENT                 
*                                                                               
WL20     OC    SRTCOR,SRTCOR       IF CITY OF RESIDENCE DEFINED                 
         BZ    WL30                                                             
         CLC   SRTCOR,SRTCOW       AND IT'S DIFFERENT THAN COW                  
         BE    WL30                                                             
         MVC   TGTHREE,SRTCOR      SET CITY OF RESIDENCE                        
         BAS   RE,WITHHOLD         REMOVE TAXES FOR COR ELEMENT                 
*                                                                               
WL30     TM    SRTBITS,SRTBCAN     IF CANADIAN TAX IS TAKEN                     
         BZ    WL40                                                             
         CLC   SRTSOW,=C'CN '      THEN WITHHOLD IF HAVEN'T DONE SO YET         
         BE    WL40                                                             
         CLC   SRTSOR,=C'CN '                                                   
         BE    WL40                                                             
         MVC   TGTHREE,=C'CN '                                                  
         BAS   RE,WITHHOLD         REMOVE TAXES FOR CANADA                      
*                                                                               
WL40     BAS   RE,CHKQC            CHECK IF ADDITIONAL 9% QUEBEC TAX            
*                                                                               
WLX      B     XIT                                                              
         EJECT                                                                  
* THIS ROUTINE GETS THE WITHHOLDING ELEMENT FOR THE TAX UNIT PASSED             
* IN TGTHREE AND THEN CALLS PROCWITH TO REMOVE THE TAXES.                       
* TATUS ARE BUILT IN AIO3                                                       
*                                                                               
WITHHOLD NTR1                                                                   
         CLI   RECNUM,PK           PAYROLL PLUS?                                
         BNE   WITHH30                                                          
         MVI   SRTFREQ,C'W'        WEEKLY                                       
         TM    SRTBITS2,SRTTAXSM   SEMI-MONTHLY?                                
         BZ    *+8                                                              
         MVI   SRTFREQ,C'S'                                                     
*                                                                               
         CLC   TGTHREE,=C'PIT'                                                  
         BE    WITHH20                                                          
         CLC   TGTHREE,=C'CIN'                                                  
         BE    WITHH20                                                          
         CLC   TGTHREE,=C'CLV'                                                  
         BE    WITHH20                                                          
*                                                                               
WITHH05  TM    TUFLAG,TUPHLRES     PHILADELPHIA RESIDENT?                       
         BZ    WITHH10                                                          
         CLI   TGTHREE+2,C' '      CITY?                                        
         BE    WITHH20                                                          
         CLC   TGTHREE,=C'PHL'                                                  
         BNE   XIT                                                              
         B     WITHH20                                                          
*                                                                               
WITHH10  TM    TUFLAG,TUNYCRES     NYC RESIDENT?                                
         BZ    WITHH20                                                          
         CLI   TGTHREE+2,C' '      CITY?                                        
         BE    WITHH20                                                          
         CLC   TGTHREE,=C'PHL'                                                  
         BE    WITHH20                                                          
         CLC   TGTHREE,=C'NYC'                                                  
         BNE   XIT                                                              
*                                                                               
WITHH20  MVC   AIO,ASRTCHK         NEW TATU ELEMS                               
         MVI   ELCODE,TATUELQ                                                   
         GOTO1 GETL,DMCB,(3,TGTHREE)                                            
         BE    *+14                                                             
         MVC   AIO,ASRTCHK                                                      
         B     XIT                                                              
*                                                                               
         USING TATUD,R4                                                         
         L     R4,TGELEM                                                        
         MVC   TGTWAGE,TATUWAAD    WAGES                                        
         MVC   TGTNWAGE,TATUTNAD   TAXABLE NON WAGES                            
*                                                                               
         CLC   TATUUNIT,=C'FD '                                                 
         BNE   *+10                                                             
         MVC   SRTTXRE,TGTNWAGE    SET TAXABLE REIMBURSEMENTS                   
         DROP  R4                                                               
*                                                                               
* IF FEDERAL AND WORKED IN PR, THEN:                                            
*                                                                               
* FEDERAL TAXES CALCULATED AT TOTAL TAXABLE AMOUNT MINUS PR TAXABLE             
* FICA CALCULATED AT TOTAL TAXABLE AMOUNT                                       
*                                                                               
         CLC   TGTHREE,=C'FD '     IF PROCESSING FEDERAL TAXES                  
         BNE   WITHH30             AND SOME WORK WAS DONE IN PR                 
*                                                                               
         MVC   AIO,ASRTCHK         NEW TATU ELEMS                               
         MVI   ELCODE,TATUELQ                                                   
         GOTO1 GETL,DMCB,(3,=C'PR ')                                            
         BNE   WITHH30                                                          
         MVC   AIO,ASRTCHK                                                      
*                                                                               
         ICM   RF,15,TGTWAGE       ADD WAGES AND TAXABLE NON-WAGES              
         ICM   RE,15,TGTNWAGE      TO GET TOTAL TAXABLE AMOUNT                  
         AR    RF,RE                                                            
         STCM  RF,15,TASEEARN                                                   
*                                                                               
         SAM31                                                                  
         USING ESUTABD,R5                                                       
         GOTO1 GETESU,DMCB,(3,=C'FD ')                                          
         L     R5,0(R1)                                                         
         MVC   TASEYTDE,ESUEARN    MOVE YTD EARNINGS TO SEGUE BLOCK             
         MVC   TASEYTDF,ESUFICA         YTD FICA                                
         SAM24                                                                  
         DROP  R5                                                               
*                                                                               
         USING TACWD,R4                                                         
         MVI   ELCODE,TACWELQ                                                   
         GOTO1 GETL,DMCB,(3,=C'FD ')                                            
         BNE   WITHH30                                                          
         L     R4,TGELEM                                                        
         MVC   TASEUNIT,TACWUNIT   SET FEDERAL EXEMPTIONS                       
         MVC   TASESTAT,TACWMST                                                 
         MVC   TASEEXS,TACWEXS                                                  
         MVI   TASERES,C'N'                                                     
         TM    TACWSTAT,TACWSRES                                                
         BZ    *+8                                                              
         MVI   TASERES,C'Y'                                                     
         DROP  R4                                                               
*                                                                               
         MVC   TASEFREQ,SRTFREQ    CALL TASEGUE TO CALC FICA ON FULL            
         XC    TASETAX(76),TASETAX                 TAXABLE AMOUNT               
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'S',(R9)),TASELQ,=C'PR FICA INPUT'                
*                                                                               
         GOTO1 TASEGUE,DMCB,(R9)                                                
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'S',(R9)),TASELQ,=C'PR FICA OUTPUT'               
*                                                                               
         MVC   AIO,ASRTCHK         NEW TATU ELEMS                               
         MVI   ELCODE,TATUELQ                                                   
         GOTO1 GETL,DMCB,(3,=C'PR ')                                            
         BNE   WITHH30                                                          
         MVC   AIO,ASRTCHK                                                      
*                                                                               
         USING TATUD,R4                                                         
         L     R4,TGELEM                                                        
         ICM   RE,15,TGTWAGE       THEN SUBTRACT PUERTO RICO PORTION            
         ICM   RF,15,TATUWAAD      OF THE WAGES AND TAXABLE NON-WAGES           
         SR    RE,RF                                                            
         STCM  RE,15,TGTWAGE                                                    
         ICM   RE,15,TGTNWAGE                                                   
         ICM   RF,15,TATUTNAD                                                   
         SR    RE,RF                                                            
         STCM  RE,15,TGTNWAGE                                                   
         DROP  R4                                                               
*                                                                               
         USING TACWD,R4                                                         
         MVI   ELCODE,TACWELQ                                                   
         GOTO1 GETL,DMCB,(3,=C'FD ')                                            
         BNE   WITHH30                                                          
         L     R4,TGELEM                                                        
         MVC   TACWFICA,TASEFICA   AND SAVE AWAY FICA                           
*                                                                               
WITHH30  MVC   AIO,ASRTCHK                                                      
         MVI   ELCODE,TACWELQ                                                   
         GOTO1 GETL,DMCB,(3,TGTHREE)                                            
         MVC   AIO,AIO1                                                         
         BNE   ERRECFD                                                          
         L     R4,TGELEM                                                        
         USING TACWD,R4                                                         
*                                                                               
WITHH40  BAS   RE,PROCWITH         REMOVE TAXES FOR WITHHOLDING EL.             
         B     XIT                                                              
         EJECT                                                                  
*                                                                               
* THIS ROUTINE CALLS TASEGUE AND REMOVES THE TAXES FOR THE UNIT FOR             
* THE WITHHOLDING ELEMENT POINTED TO BY R4.                                     
*                                                                               
PROCWITH NTR1                                                                   
         USING TACWD,R4                                                         
*                                  GET A(ESUTAB ENTRY) FOR THIS UNIT            
         GOTO1 GETESU,DMCB,TACWUNIT                                             
*        L     R5,0(R1)                                                         
         SAM31                                                                  
         L     RF,0(R1)                                                         
         LA    R5,BLOCK                                                         
         XC    BLOCK,BLOCK                                                      
         MVC   BLOCK(ESULEN),0(RF)                                              
         SAM24                                                                  
         USING ESUTABD,R5                                                       
*                                                                               
         CLC   TACWUNIT,=C'CN '    IF UNIT IS CANADA                            
         BNE   PW20                                                             
         TM    SRTBITS,SRTBCAN     AND CANADIAN TAX IS TAKEN                    
         BZ    PW10                                                             
*                                                                               
         BAS   RE,CALCCTAX         TAKE CANADIAN NON-RES TAX                    
         BAS   RE,SUBTAX           SUBTRACT TAXES FROM REMAINING BAL            
         B     PWX                                                              
*                                                                               
*W4      CLC   =C'ACT',SRTUN       OR ACTRA PAYMENT                             
*        BNE   PWX                                                              
PW10     CLI   SRTTYPE,TAW4TYCO    IF PERFORMER IS CORPORATIN                   
         BE    *+14                                                             
         CLC   SRTGST#,SPACES      WITH GST # ON W4                             
         BNH   PWX                                                              
*                                                                               
         XC    TGFULL,TGFULL                                                    
         MVC   AIO,ASRTCHK                                                      
         MVI   ELCODE,TATUELQ                                                   
         GOTO1 GETL,DMCB,(3,=C'FD ')                                            
         BNE   PW15                                                             
*                                                                               
         L     RF,TGELEM                                                        
         USING TATUD,RF                                                         
         MVC   TGFULL,TATUSTRE     TAXABLE NON WAGES                            
         DROP  RF                                                               
*                                                                               
PW15     CLI   SRTTYPE,TAW4TYCO    IF PERFORMER IS CORPORATIN                   
         BNE   PW18                                                             
         BAS   RE,CALCCTAX         TAKE CANADIAN NON-RES TAX                    
         CLC   SRTGST#,SPACES      WITH GST # ON W4                             
         BH    PW18                                                             
         BAS   RE,SUBTAX                                                        
         B     PWX                                                              
*                                                                               
PW18     MVI   ELCODE,TACWELQ      RESET ELCODE                                 
         L     RF,SRTGRS                                                        
         A     RF,TGFULL                                                        
         A     RF,SRTINR                                                        
         A     RF,SRTPNH                                                        
         BAS   RE,CALCGST          TAKE US$ GST                                 
         B     PWX                                                              
*                                                                               
PW20     MVC   TASEUNIT,TACWUNIT   ELSE SAVE TAX UNIT IN TASEGUE BLOCK          
         MVC   TASESTAT,TACWMST              MARITAL STATUS                     
         MVC   TASEEXS,TACWEXS               EXEMPTIONS                         
*                                                                               
         MVI   TASERES,C'N'        SAVE RESIDENCE INDICATOR (Y/N) IN            
         TM    TACWSTAT,TACWSRES       TASEGUE BLOCK                            
         BZ    PW30                                                             
         MVI   TASERES,C'Y'                                                     
         CLC   TASEUNIT,=C'PR '    PUERTO RICO RESIDENTS: SPECIAL RULE          
         BNE   PW30                                                             
         CLC   TASEEXS,=C'99'      99 EXEMPTIONS                                
         BNE   PW30                                                             
         MVI   TASESTAT,C'S'       SINGLE 1                                     
         MVC   TASEEXS,=C'01'                                                   
*                                                                               
PW30     MVC   TASEYTDE,ESUEARN    MOVE YTD EARNINGS TO SEGUE BLOCK             
         MVC   TASEYTDF,ESUFICA         YTD FICA (OR SDI)                       
         MVC   TASEYTDU,ESUSUI          YTD UNEMPLOYEMNT (OR SUI)               
         MVC   TASEYTDL,ESUSFLI         YTD FAMILY LEAVE (OR FLI)               
*                                                                               
         CLI   RECNUM,PK           PAYROLL PLUS?                                
         BNE   PW35                                                             
         ICM   RE,15,ESUEARN       ACCUMULATE EARNINGS AND                      
         ICM   RF,15,ESUTXRE       TAXABLE REIMBURSEMENTS                       
         AR    RE,RF                                                            
         STCM  RE,15,TASEYTDE      MOVE YTD EARNINGS TO SEGUE BLOCK             
         BAS   RE,PROCWPP          PROCESS PAYROLL PLUS TAXES                   
         B     PWX                                                              
*                                                                               
***********************************************************************         
PW35     CLC   TACWUNIT,=C'NY '    IF NEW YORK STATE                            
         BNE   PW40                                                             
         CLC   SRTTXST,=C'NY '     AND TAXABLE STATE IS NY                      
         BNE   PW60                                                             
         TM    TGUSSTA2,LIVEWORK   AND CHECK IS FOR LIVE WORK                   
         BZ    PW60                                                             
         CLI   TGUSEQU,UGRT        AND NOT FOR GRT                              
         BE    PW60                                                             
         CLI   TGUSEQU,UPPF        OR PPF                                       
         BE    PW60                                                             
         CLI   SRTTYPE,TAW4TYES    AND PERFORMER IS NOT PAID AS AN              
         BE    PW60                ESTATE                                       
*                                                                               
         MVI   TASEFREQ,C'W'       SET FREQUENCY TO WEEKLY                      
*                                                                               
         XC    TASETAX(76),TASETAX CLEAR TASEGUE RETURN AMOUNTS                 
*                                                                               
         GOTO1 TASEGUE,DMCB,(R9)   CALL TASEGUE AND PASS SEGUE BLOCK            
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'S',(R9)),TASELQ,=C'SEGUE SDI OUTPUT'             
*                                                                               
         MVC   TACWSDI,TASESDI     SAVE AWAY WEEKLY SDI                         
         CLC   TACWSDI,=F'60'      SDI IS .60 MAX                               
         BNH   PW60                                                             
         MVC   TACWSDI,=F'60'                                                   
         B     PW60                                                             
*                                                                               
***********************************************************************         
PW40     BRAS  RE,GETHAWII         GET HAWAII TAXES                             
         BNE   PW50                                                             
*                                  GET A(ESUTAB ENTRY) FOR THIS UNIT            
         GOTO1 GETESU,DMCB,TACWUNIT                                             
*        L     R5,0(R1)                                                         
         SAM31                                                                  
         L     RF,0(R1)                                                         
         LA    R5,BLOCK                                                         
         XC    BLOCK,BLOCK                                                      
         MVC   BLOCK(ESULEN),0(RF)                                              
         SAM24                                                                  
         USING ESUTABD,R5                                                       
         B     PW60                HAVE TO PASS HAWAII EARNING QTD              
*                                                                               
***********************************************************************         
PW50     CLC   TACWUNIT,=C'CA '    OR CALIFORNIA                                
         BNE   PW60                                                             
         CLC   SRTTXST,=C'CA '     AND TAXABLE STATE IS CALIFORNIA              
         BNE   PW60                                                             
*                                                                               
         MVI   TASEFREQ,C'W'       SET FREQUENCY TO WEEKLY                      
*                                                                               
         XC    TASETAX(76),TASETAX CLEAR TASEGUE RETURN AMOUNTS                 
*                                                                               
         GOTO1 TASEGUE,DMCB,(R9)   CALL TASEGUE AND PASS SEGUE BLOCK            
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'S',(R9)),TASELQ,=C'SEGUE SDI OUTPUT'             
*                                                                               
         MVC   TACWSDI,TASESDI     SAVE AWAY WEEKLY SDI                         
*                                                                               
***********************************************************************         
*                                                                               
PW60     CLC   TACWUNIT,=C'FD '    IF FEDERAL                                   
         BNE   PW80                                                             
*                                                                               
         MVI   TASEFREQ,C'W'       SET FREQUENCY TO WEEKLY                      
         CLC   SRTGRS,=F'100000'   IF TAXABLE EARNINGS <= $1000                 
         BNH   PW70                                                             
         CLI   TGUSEQU,USOP        OR IF THIS IS SOAP PAYMENT                   
         BE    PW70                                                             
                                                                                
         MVI   TASEFREQ,C'M'       SET FREQUENCY TO MONTHLY                     
         CLC   SRTGRS,=F'350000'   IF TAXABLE EARNINGS <= $3500                 
         BNH   PW70                                                             
                                                                                
         MVI   TASEFREQ,C'Q'       ELSE SET FREQUENCY TO QUATERLY               
*                                                                               
PW70     XC    TASETAX(76),TASETAX CLEAR TASEGUE RETURN AMOUNTS                 
*                                                                               
         GOTO1 TASEGUE,DMCB,(R9)   CALL TASEGUE AND PASS SEGUE BLOCK            
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'S',(R9)),TASELQ,=C'SEGUE FICA OUTPUT'            
*&&DO                                                                           
         TM    TGSYSTAT,TASYSFLT   NEW FLAT TAX RULES?                          
         BZ    PW75                                                             
         CLI   SRTTYPE,TAW4TYES    IF ESTATE, NO FICA                           
         BNE   *+10                                                             
         XC    TASEFICA,TASEFICA                                                
*&&                                                                             
PW75     MVC   TACWFICA,TASEFICA   SAVE AWAY FICA                               
*                                                                               
*              GET TAXES FOR YTD AT CURRENT W4 STATUS                           
*                                                                               
PW80     GOTO1 GETESU,DMCB,TACWUNIT                                             
*        L     R5,0(R1)                                                         
         SAM31                                                                  
         L     RF,0(R1)                                                         
         LA    R5,BLOCK                                                         
         XC    BLOCK,BLOCK                                                      
         MVC   BLOCK(ESULEN),0(RF)                                              
         SAM24                                                                  
         USING ESUTABD,R5                                                       
*                                                                               
         MVC   TASEEARN,ESUEARN    GET TAXES FOR YTD EARNINGS                   
         XC    TASEYTDU,TASEYTDU   CLEAR OUT YTD VALUES                         
         XC    TASEYTDE,TASEYTDE                                                
         XC    TASEYTDD,TASEYTDD                                                
         XC    TASEYTDL,TASEYTDL                                                
         MVI   TASEFREQ,C'A'       SET FREQUENCY TO ANNUAL                      
*                                                                               
         XC    TASETAX(76),TASETAX CLEAR TASEGUE RETURN AMOUNTS                 
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'S',(R9)),TASELQ,=C'SEGUE INPUT'                  
*                                                                               
         GOTO1 TASEGUE,DMCB,(R9)   CALL TASEGUE AND PASS SEGUE BLOCK            
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'S',(R9)),TASELQ,=C'SEGUE OUTPUT'                 
*                                                                               
         MVC   TASEFREQ,SRTFREQ    RESET TAX FREQUENCY                          
         MVC   TASEEARN,SRTGRS     AND GROSS                                    
*                                                                               
         MVC   TGYTDTAX,TASETAX    SAVE YTD TAXES                               
         MVC   TGYTDSUI,TASESUI    SAVE YTD SUI                                 
         MVC   TGYTDFLI,TASESFLI   SAVE YTD FLI                                 
         MVC   TGYTDSDI,TASESDI    SAVE YTD SDI                                 
*                                                                               
*              CALL ALLTAX WITH FOR CURRENT CHECK                               
*                                                                               
         ICM   RE,15,TASEEARN      SET TO CALC TAXES ON THIS CHECK'S            
         ICM   RF,15,ESUEARN       EARNINGS                                     
         AR    RE,RF               PLUS PREVIOUS YTD EARNINGS                   
         STCM  RE,15,TASEEARN                                                   
         XC    TASEYTDU,TASEYTDU   CLEAR OUT YTD VALUES                         
         XC    TASEYTDE,TASEYTDE                                                
         XC    TASEYTDD,TASEYTDD                                                
         XC    TASEYTDL,TASEYTDL                                                
         MVI   TASEFREQ,C'A'       SET FREQUENCY TO ANNUAL                      
*                                                                               
         XC    TASETAX(76),TASETAX CLEAR TASEGUE RETURN AMOUNTS                 
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'S',(R9)),TASELQ,=C'SEGUE INPUT'                  
*                                                                               
         GOTO1 TASEGUE,DMCB,(R9)   CALL TASEGUE AND PASS SEGUE BLOCK            
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'S',(R9)),TASELQ,=C'SEGUE OUTPUT'                 
*                                                                               
         MVC   TASEFREQ,SRTFREQ    RESET TAX FREQUENCY                          
         MVC   TASEEARN,SRTGRS     AND GROSS                                    
*                                                                               
*              GET CURRENT CHECKS TAXES BY SUBTRACTING OFF YTD TAXES            
*                                                                               
         ICM   RE,15,TASETAX                                                    
         ICM   RF,15,TGYTDTAX      NEW YTD TAXES                                
         XC    TASETAX,TASETAX                                                  
         CR    RE,RF                                                            
         BNH   PW90                                                             
         SR    RE,RF               SUBTRACT PREVIOUS TAXES WITHHELD             
         STCM  RE,15,TASETAX       FROM CALCULATED TAXES WITHHELD               
*                                                                               
PW90     CLC   TACWUNIT,=C'FD '    IF UNIT IS FEDERAL                           
         BE    PW120               FICA IS ALREADY CALCULATED                   
*                                                                               
         CLI   TACWUNIT+2,C' '     IF UNIT IS A STATE                           
         BH    PW120                                                            
         ICM   RE,15,TASESUI                                                    
         ICM   RF,15,TGYTDSUI      NEW YTD SUI                                  
         XC    TASESUI,TASESUI                                                  
         CR    RE,RF                                                            
         BNH   PW100                                                            
         SR    RE,RF               SUBTRACT PREV UNEMP INSUR WITHHELD           
         STCM  RE,15,TASESUI       FROM CALCULATED UNEMP INSUR WITHHELD         
*                                                                               
PW100    ICM   RE,15,TASESFLI                                                   
         ICM   RF,15,TGYTDFLI      NEW YTD FLI                                  
         XC    TASESFLI,TASESFLI                                                
         CR    RE,RF                                                            
         BNH   PW110                                                            
         SR    RE,RF               SUBTRACT PREV FLI WITHHELD                   
         STCM  RE,15,TASESFLI      FROM CALCULATED FLI WITHHELD                 
*                                                                               
PW110    CLC   TACWUNIT,=C'NY '    IF NOT NEW YORK STATE                        
         BE    PW120                                                            
         CLC   TACWUNIT,=C'HI '    AND NOT HAWAII                               
         BE    PW120                                                            
         CLC   TACWUNIT,=C'CA '    AND NOT CALIFORNIA                           
         BE    PW120                                                            
         ICM   RE,15,TASESDI                                                    
         ICM   RF,15,TGYTDSDI      NEW YTD SDI                                  
         XC    TASESDI,TASESDI                                                  
         CR    RE,RF                                                            
         BNH   PW120                                                            
         SR    RE,RF               SUBTRACT PREV SDI WITHHELD                   
         STCM  RE,15,TASESDI       FROM CALCULATED SDI WITHHELD                 
*                                                                               
PW120    BAS   RE,MORETAX          CALCULATE REMAINING TAXES                    
*                                                                               
PWX      B     XIT                                                              
         DROP  R5                                                               
         EJECT                                                                  
*                                                                               
* THIS ROUTINE CALLS TASEGUE AND REMOVES THE TAXES FOR THE UNIT FOR             
* THE WITHHOLDING ELEMENT POINTED TO BY R4 FOR PAYROLL PLUS.                    
*                                                                               
         USING ESUTABD,R5                                                       
PROCWPP  NTR1                                                                   
         ICM   RF,15,TGTWAGE       WAGES                                        
         ICM   RE,15,TGTNWAGE      TAXABLE NON WAGES                            
         AR    RF,RE                                                            
         STCM  RF,15,TASEEARN      IF NOT FEDERAL, THEN SET FROM TATU           
*                                                                               
         TM    TUFLAG,TUPHLRES+TUNYCRES                                         
         BZ    PWPP10                                                           
         CLC   TACWUNIT,=C'NYC'                                                 
         BNE   *+8                                                              
         MVI   TASERES,C'Y'                                                     
*                                                                               
PWPP10   BRAS  RE,GETHAWII         GET HAWAII TAXES                             
         BNE   PWPP20                                                           
*                                  GET A(ESUTAB ENTRY) FOR THIS UNIT            
         GOTO1 GETESU,DMCB,TACWUNIT                                             
*        L     R5,0(R1)                                                         
         SAM31                                                                  
         L     RF,0(R1)                                                         
         LA    R5,BLOCK                                                         
         XC    BLOCK,BLOCK                                                      
         MVC   BLOCK(ESULEN),0(RF)                                              
         SAM24                                                                  
         USING ESUTABD,R5                                                       
*                                                                               
PWPP20   MVC   TASEFREQ,SRTFREQ                                                 
*                                                                               
         XC    TASETAX(76),TASETAX CLEAR TASEGUE RETURN AMOUNTS                 
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'S',(R9)),TASELQ,=C'SEGUE INPUT'                  
*                                                                               
         GOTO1 TASEGUE,DMCB,(R9)   CALL TASEGUE AND PASS SEGUE BLOCK            
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'S',(R9)),TASELQ,=C'SEGUE OUTPUT'                 
*&&DO                                                                           
         TM    TGSYSTAT,TASYSFLT   NEW FLAT TAX RULES?                          
         BZ    PWPP25                                                           
         CLI   SRTTYPE,TAW4TYES    IF ESTATE, NO FICA                           
         BNE   *+10                                                             
         XC    TASEFICA,TASEFICA                                                
*&&                                                                             
PWPP25   CLC   TACWUNIT,=C'FD '                                                 
         JNE   PWPP30                                                           
         OC    TACWFICA,TACWFICA                                                
         BNZ   PWPP50                                                           
         MVC   TACWFICA,TASEFICA   SAVE AWAY FICA                               
         J     PWPP50                                                           
*                                                                               
PWPP30   CLC   TACWUNIT,=C'HI '                                                 
         BE    PWPP40                                                           
         OC    TACWSDI,TACWSDI     ALREADY CALCULATED SDI?                      
         BNZ   *+10                                                             
         MVC   TACWSDI,TASESDI     SAVE AWAY SDI                                
*                                                                               
PWPP40   CLC   TACWUNIT,=C'NY '                                                 
         BNE   PWPP50                                                           
         CLI   SRTTYPE,TAW4TYES    IF ESTATE, NO SDI                            
         BNE   *+14                                                             
         XC    TACWSDI,TACWSDI                                                  
         B     PWPP50                                                           
*                                                                               
         MVC   TGFULL,=F'60'       SDI MAX FOR WEEKLY                           
         TM    SRTBITS2,SRTTAXSM   SEMI-MONTHLY?                                
         BZ    *+10                                                             
         MVC   TGFULL,=F'130'      SDI MAX FOR SEMI-MONTHLY                     
         CLC   TACWSDI,TGFULL      IF SDI > MAX, THEN SET TO MAX                
         BNH   PWPP50                                                           
         MVC   TACWSDI,TGFULL                                                   
*                                                                               
PWPP50   BAS   RE,MORETAX          CALCULATE REMAINING TAXES                    
*                                                                               
PWPPX    B     XIT                                                              
         DROP  R5                                                               
         EJECT                                                                  
*                                                                               
*              ROUTINE TO CACULATE MORE TAXES                                   
*                                                                               
MORETAX  NTR1                                                                   
         BRAS  RE,FLATTAX          CHNG TASETAX TO FLAT TAX IF GREATER          
*                                                                               
         CLC   =C'PA ',TACWUNIT    IF PENNSYLVANIA                              
         BE    MT10                                                             
         CLC   =C'PIT',TACWUNIT    OR PITTSBURGH                                
         BE    MT10                                                             
         CLC   =C'CIN',TACWUNIT    OR CINCINNATI                                
         BE    MT10                                                             
         CLC   =C'CLV',TACWUNIT    OR CLEVELAND                                 
         BE    MT10                                                             
         CLC   =C'PHL',TACWUNIT    OR PHILADELPHA                               
         BE    MT10                   ALWAYS TAKE TAX OUT                       
         CLC   TASEEXS,=C'99'      IF 99 EXEMPTIONS ARE DEFINED                 
         BNE   *+10                                                             
         XC    TASETAX,TASETAX     CLEAR TAX AMOUNT                             
*                                                                               
MT10     CLC   TACWUNIT,=C'FD '    IF UNIT IS FEDERAL                           
         BNE   MT20                                                             
         CLI   RECNUM,PK           PAYROLL PLUS?                                
         BE    MT20                                                             
         CLC   SRTTXST,=C'PR '     AND IF TAXABLE STATE IS PUERTO RICO          
         BNE   MT20                                                             
         XC    TASETAX,TASETAX     CLEAR TAX AMOUNT                             
*                                                                               
MT20     CLI   SRTTYPE,TAW4TYES    IF PERFORMER PAID AS AN ESTATE               
         BNE   MT30                                                             
         XC    TASETAX,TASETAX     THEN CLEAR TAX                               
*                                                                               
         CLC   TACWUNIT,=C'FD '    IF UNIT IS NOT FEDERAL                       
         BE    MT30                                                             
         XC    TASESDI,TASESDI     THEN CLEAR SDI                               
         XC    TASESUI,TASESUI                SUI                               
         XC    TASESFLI,TASESFLI              SUI                               
*                                                                               
MT30     BRAS  RE,NORMTAX          NORMALIZE SEGUE TAX AMOUNTS                  
*                                                                               
         MVC   TACWTAX,TASETAX     SAVE TAX IN CW ELEMENT                       
*                                                                               
         CLC   TACWUNIT,=C'FD '    IF UNIT IS FEDERAL                           
         BNE   MT40                                                             
         MVC   TACWFICA,TASEFICA   SAVE FICA IN CW ELEMENT                      
         MVC   SRTFTAX,TACWTAX     SAVE FEDERAL TAX IN SORT REC                 
         MVC   SRTFICA,TACWFICA         FICA                                    
         B     MT110                                                            
*                                                                               
MT40     CLI   TACWUNIT+2,C' '     ELSE IF UNIT IS A STATE                      
         BH    MT80                                                             
*                                                                               
         CLC   TACWUNIT,SRTTXST    THEN IF THIS IS TAXABLE STATE                
         BNE   MT70                                                             
         OI    TACWSTAT,TACWSTAX   SET TAXABLE STATUS BIT IN CW ELEMENT         
         MVC   TACWSUI,TASESUI               SUI                                
         MVC   TACWSFLI,TASESFLI             FLI                                
*&&DO                                                                           
         CLC   TACWUNIT,=C'NY '    IF NOT NEW YORK                              
         BE    MT50                                                             
         CLC   TACWUNIT,=C'HI '    AND NOT HAWAII                               
         BE    MT50                                                             
         CLC   TACWUNIT,=C'CA '    AND NOT CALIFORNIA                           
         BE    MT50                                                             
*&&                                                                             
         MVC   TACWSDI,TASESDI     SET SDI IN CW ELEMENT                        
*                                                                               
         CLI   RECNUM,PK           LOOK UP SUI IN EVTIME ORDER                  
         BNE   MT50                                                             
*                                                                               
         TM    TGSYSTA2,TASYSROI   RETAIN ORDER OF INPUT?                       
         BZ    MT50                                                             
*                                                                               
         L     RF,ASUITAB                                                       
MT45     CLI   0(RF),X'FF'         IF IT'S NOT ON EVTIME                        
         BE    MT50                KEEP THE SUI                                 
         USING SUITABD,RF                                                       
*                                                                               
         CLC   TACWUNIT,SUITUNIT   IF IT IS, THEN DELETE SUI                    
         BE    *+12                                                             
         AHI   RF,SUITABLN                                                      
         B     MT45                                                             
         XC    TACWSUI,TACWSUI     AND LOOK IT UP IN WITHSUI ROUTINE            
         DROP  RF                                                               
*                                                                               
MT50     OC    SRTSUC,SRTSUC       IF NOT EMPLOYER IN THIS STATE                
         BNZ   MT60                                                             
         XC    TACWTAX,TACWTAX     THEN CLEAR TAX                               
         XC    TACWSUI,TACWSUI                SUI                               
         XC    TACWSDI,TACWSDI                SDI                               
         XC    TACWSFLI,TACWSFLI              FLI                               
*                                                                               
MT60     MVC   SRTSDI,TACWSDI      SAVE SDI IN SORT REC                         
         MVC   SRTSUI,TACWSUI           SUI                                     
         MVC   SRTSFLI,TACWSFLI         FLI                                     
*                                                                               
MT70     CLC   TACWUNIT,SRTTXST    UNLESS THIS IS TAXABLE STATE                 
         BE    *+10                                                             
         XC    TACWTAX,TACWTAX     CLEAR TAX AMOUNT                             
*                                                                               
         TM    TACWSTAT,TACWSWRK   IF STATE OF WORK                             
         BZ    *+14                                                             
         MVC   SRTWTAX,TACWTAX     SAVE WORK STATE TAX IN SORT REC              
         B     MT110                                                            
*                                                                               
         MVC   SRTRTAX,TACWTAX     ELSE SAVE RES STATE TAX IN SORT REC          
         B     MT110                                                            
*                                                                               
MT80     CLI   RECNUM,PK                                                        
         BE    MT85                                                             
         CLC   SRTCOR,=C'NYC'                                                   
         BNE   MT82                                                             
         CLC   TACWUNIT,=C'PHL'                                                 
         BNE   MT82                                                             
         OC    TACWWHLD,TACWWHLD   ANY TAXES WITHHELD?                          
         BZ    *+8                                                              
         OI    TACWSTAT,TACWSTAX                                                
         B     MT100                                                            
*                                                                               
MT82     CLC   SRTCOR,=C'PHL'                                                   
         BNE   MT90                                                             
         CLC   TACWUNIT,=C'NYC'                                                 
         BNE   MT85                                                             
         OC    TACWWHLD,TACWWHLD   ANY TAXES WITHHELD?                          
         BZ    *+8                                                              
         OI    TACWSTAT,TACWSTAX                                                
         B     MT100                                                            
*                                                                               
MT85     TM    TUFLAG,TUPHLRES+TUNYCRES   PHL/NYC RESIDENT?                     
         BZ    MT90                                                             
         CLC   TACWUNIT,=C'NYC'                                                 
         BE    MT100                                                            
         CLC   TACWUNIT,=C'PHL'                                                 
         BE    MT100                                                            
*                                                                               
MT90     CLC   TACWUNIT,SRTTXCY    IF THIS IS TAXABLE CITY                      
         BNE   MT95                                                             
         OI    TACWSTAT,TACWSTAX   SET TAXABLE STATUS BIT IN ELEMENT            
*                                                                               
         CLC   TACWUNIT,=C'PHL'    IF THIS IS NOT PHILADELPHIA                  
         BE    MT100                                                            
         CLC   TACWUNIT,=C'DET'    OR DETROIT                                   
         BE    MT100                                                            
         CLC   TACWUNIT,=C'CIN'    OR CINCINNATI                                
         BE    MT100                                                            
         CLC   TACWUNIT,=C'CLV'    OR CLEVELAND                                 
         BE    MT100                                                            
         CLC   TACWUNIT,=C'NYC'    OR NEW YORK CITY                             
         BNE   MT95                THEN CLEAR LOCAL TAX                         
*                                                                               
         CLC   CHKDATE1,=X'990701' FOR NYC WORK AS OF 7/1/99                    
         BL    MT100                                                            
**NO-OP**CLC   SRTSOR,=C'NY '                                                   
**NO-OP**BNE   MT100                                                            
         CLC   SRTCOR,=C'NYC'      IF LIVING OUTSIDE OF NYC                     
         BE    MT100                                                            
         NI    TACWSTAT,X'FF'-TACWSTAX DON'T ACCUM EARNINGS IN YTD &            
*                                      ALSO CLEAR LOCAL TAX                     
MT95     XC    TACWTAX,TACWTAX     CLEAR LOCAL TAX                              
*                                                                               
MT100    L     RF,TACWTAX          ADD LOCAL TAX TO LOCAL TAX IN                
         A     RF,SRTLTAX              SORT RECORD                              
         ST    RF,SRTLTAX                                                       
*                                                                               
MT110    BAS   RE,SUBTAX           SUBTRACT TAXES FROM REMAINING BAL            
MTX      B     XIT                                                              
         EJECT                                                                  
*                                                                               
*              ROUTINE TAKES CANADIAN TAX                                       
*                                                                               
* ALWAYS TAKE 23/15 AND 9% SPLIT (JULY 2014) FOR EVERYONE                       
* EXCEPT CANADIAN CORPS                                                         
*                                                                               
*                                                                               
         SPACE 1                                                                
CALCCTAX NTR1                                                                   
         XC    QCTAX,QCTAX                                                      
                                                                                
         TM    TGSYSTAT,TASYSFLT   NEW FLAT TAX RULES?                          
         BZ    CALCCT20                                                         
         CLI   SRTTYPE,TAW4TYCO    CORPORATION?                                 
         BNE   CALCCT20                                                         
*                                                                               
         MVC   AIO,AW4IO                                                        
         MVI   ELCODE,TAA2ELQ                                                   
         GOTO1 GETL,DMCB,0                                                      
         BNE   CALCCT20            DONE IF NO NEW TYPE ADDRESS ELEM             
         L     RF,TGELEM           R4 = A(FIRST W4 WITHHOLDING ELEMENT)         
         USING TAA2D,RF                                                         
         CLC   TAA2CTRY,=C'CA'     CANADIAN CORP?                               
         JNE   CALCCT20                                                         
*                                                                               
         MVC   AIO,ASRTCHK                                                      
         MVI   ELCODE,TACWELQ                                                   
         GOTO1 GETL,DMCB,(3,=C'CN ')                                            
         JNE   CALCCTX                                                          
         J     CALCCTX             NEEDED TO GET GST                            
*                                                                               
         L     RF,TGELEM                                                        
         MVI   0(RF),X'FF'         SINCE THERE ARE NO TAXES, NO TACW            
         MVI   ELCODE,X'FF'                                                     
         GOTO1 REMELEM                                                          
         J     CALCCTX             NO TAXES TO CALCULATE                        
         DROP  RF                                                               
*                                                                               
CALCCT20 MVC   AIO,ASRTCHK                                                      
         MVC   FULL,=F'23000'      DEFAULT TO 23%                               
*                                                                               
         CLI   RECNUM,PC                                                        
         JNE   CALCCT25                                                         
         TM    TGSYSTAT,TASYSFLT   NEW FLAT TAX RULES?                          
         BZ    *+10                                                             
         MVC   FULL,=F'15000'      PP = 15%                                     
*                                                                               
CALCCT25 CLI   RECNUM,PK           PAYROLL PLUS?                                
         BNE   CALCCT30                                                         
*                                                                               
         MVI   ELCODE,TACAELQ                                                   
         GOTO1 HELLO,DMCB,(C'G',=C'TALFIL'),(ELCODE,ASRTCHK)                    
         CLI   12(R1),6                                                         
         BE    *+12                                                             
         CLI   12(R1),0                                                         
         BNE   CALCCT30                                                         
*                                                                               
         USING TACAD,RF                                                         
         ICM   RF,15,12(R1)                                                     
         TM    TACASTA3,TACASACT   ACTOR?                                       
         BO    CALCCT30                                                         
         DROP  RF                                                               
*                                                                               
         MVC   FULL,=F'15000'                                                   
*                                                                               
CALCCT30 L     RF,SRTGRS           THEN CANADIAN TAX = 23% OR 15%               
         CLI   RECNUM,PK                                                        
         BNE   CALCCT35                                                         
         CLI   SRTTYPE,TAW4TYCO    CORPORATION?                                 
         BNE   CALCCT35                                                         
*                                                                               
         MVC   AIO,ASRTCHK                                                      
         MVI   ELCODE,TATUELQ                                                   
         GOTO1 GETL,DMCB,(3,=C'CN ')    WORKING IN CANADA?                      
         BE    *+12                                                             
         L     RF,SRTGRS                                                        
         B     CALCCT35                                                         
*                                                                               
         USING TATUD,R2                                                         
         L     R2,TGELEM                                                        
         ICM   RF,15,TATUWAAD                                                   
         ICM   RE,15,TATUTNAD                                                   
         AR    RF,RE                                                            
         DROP  R2                                                               
*                                                                               
CALCCT35 SR    RE,RE                                                            
         M     RE,FULL                                                          
         D     RE,=F'50000'        PERCENTAGE                                   
         LTR   RF,RF                                                            
         BM    *+8                                                              
         AH    RF,=H'1'            & ROUND IT                                   
         SRA   RF,1                                                             
*                                                                               
         L     RE,SRTNET           IF CANADIAN TAX > REM BALANCE                
         S     RE,SRTMDED                                                       
         S     RE,SRTDUES                                                       
         CR    RF,RE                                                            
         BNH   *+6                                                              
         LR    RF,RE               THEN CANADIAN TAX = REM BALANCE              
*                                                                               
         ST    RF,SRTCTAX          SAVE IN SORT RECORD                          
         ST    RF,TACWTAX          ALSO SAVE IN WITHHLD ELEMENT                 
*                                                                               
         CLI   RECNUM,PK                                                        
         JE    CALCCT40                                                         
         TM    TGSYSTAT,TASYSFLT   NEW FLAT TAX RULES?                          
         BZ    CALCCTX                                                          
         CLI   RECNUM,CK                                                        
         JE    *+12                                                             
         CLI   RECNUM,PC                                                        
         JNE   CALCCTX                                                          
*                                                                               
         USING TACAD,R4                                                         
         L     R4,ASRTCHK                                                       
         MVI   ELCODE,TACAELQ                                                   
         BRAS  RE,GETEL                                                         
         JNE   CALCCTX                                                          
         CLC   TACAUNIT,=C'QC '                                                 
         JE    CALCCT50                                                         
         J     CALCCTX                                                          
*                                                                               
CALCCT40 MVC   AIO,ASRTCHK                                                      
         MVI   ELCODE,TATUELQ                                                   
         GOTO1 GETL,DMCB,(3,=C'QC ')    WORKING IN QUEBEC?                      
         BNE   CALCCTX                                                          
*                                                                               
CALCCT50 MVC   FULL,=F'9000'       ADDTIONAL 9%                                 
*                                                                               
         L     RF,SRTGRS           THEN QUEBEC TAX = 9.000%                     
         CLI   RECNUM,PK                                                        
         BNE   CALCCT55                                                         
         CLI   SRTTYPE,TAW4TYCO    CORPORATION?                                 
         BNE   CALCCT55                                                         
*                                                                               
         MVC   AIO,ASRTCHK                                                      
         MVI   ELCODE,TATUELQ                                                   
         GOTO1 GETL,DMCB,(3,=C'CN ')    WORKING IN CANADA?                      
         BE    *+12                                                             
         L     RF,SRTGRS                                                        
         B     CALCCT55                                                         
*                                                                               
         USING TATUD,R2                                                         
         L     R2,TGELEM                                                        
         ICM   RF,15,TATUWAAD                                                   
         ICM   RE,15,TATUTNAD                                                   
         AR    RF,RE                                                            
         DROP  R2                                                               
*                                                                               
CALCCT55 SR    RE,RE                                                            
         M     RE,FULL                                                          
         D     RE,=F'50000'        PERCENTAGE                                   
         LTR   RF,RF                                                            
         BM    *+8                                                              
         AH    RF,=H'1'            & ROUND IT                                   
         SRA   RF,1                                                             
*                                                                               
         L     RE,SRTNET           IF QUEBEC TAX > REM BALANCE                  
         S     RE,SRTMDED                                                       
         S     RE,SRTDUES                                                       
         CR    RF,RE                                                            
         BNH   *+6                                                              
         LR    RF,RE               THEN QUEBEC TAX = REM BALANCE                
*                                                                               
         ST    RF,QCTAX            SET QUEBEC TAX                               
*                                                                               
         L     RE,SRTCTAX                                                       
         AR    RF,RE                                                            
         ST    RF,SRTCTAX          UPDATE CANADIAN TAX WITHHELD                 
*                                                                               
CALCCTX  MVC   AIO,ASRTCHK                                                      
         B     XIT                                                              
         SPACE 2                                                                
*                                                                               
*              ROUTINE ADDS A QC TACW IF NEEDED                                 
*                                                                               
CHKQC    NTR1                                                                   
         CLI   SRTTYPE,TAW4TYCA    IF PERFORMER IS CANADIAN                     
         JE    CHKQCX                                                           
         BRAS  RE,CHKSSN           SKIP IF SOME CHECKS                          
         JNE   CHKQCX              NOTE: MAY HAVE TO CHECK SSN'S                
         OC    QCTAX,QCTAX         ANY QUEBEC TAX REMOVED?                      
         JZ    CHKQCX                                                           
*                                                                               
         USING TACWD,R4                                                         
         MVI   ELCODE,TACWELQ                                                   
         MVC   AIO,ASRTCHK                                                      
         GOTO1 GETL,DMCB,(3,=C'CN ')                                            
         BNE   CHKQCX                                                           
*                                                                               
         L     R4,TGELEM                                                        
         XC    ELEM,ELEM                                                        
         ZIC   R1,TACWLEN                                                       
         SHI   R1,1                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   ELEM(0),0(R4)                                                    
         DROP  R4                                                               
*                                                                               
         LA    R5,ELEM                                                          
         USING TACWD,R5                                                         
         MVC   TACWUNIT,=C'QC '                                                 
         MVC   TACWTAX,QCTAX                                                    
         GOTO1 ADDELEM                                                          
         DROP  R5                                                               
*                                                                               
         USING TACWD,R4                                                         
         MVC   AIO,ASRTCHK                                                      
         MVI   ELCODE,TACWELQ                                                   
         GOTO1 GETL,DMCB,(3,=C'QC ')                                            
         BNE   CHKQCX                                                           
         L     R4,TGELEM                                                        
*                                                                               
         CLI   SRTTYPE,TAW4TYCO    IF PERFORMER IS CORPORATIN                   
         BE    *+12                                                             
         BAS   RE,SUBTAX                                                        
         B     XIT                                                              
*                                                                               
         L     RF,TACWTAX          RF = TOTAL TAXES FOR THIS ELEMENT            
         A     RF,TACWFICA                                                      
         A     RF,TACWSUI                                                       
*                                                                               
         L     RE,SRTNET           SUBTRACT TAXES FROM REMAINING BAL            
         SR    RE,RF                                                            
         ST    RE,SRTNET                                                        
*                                                                               
         L     RE,SRTTDED          ADD TAXES TO TOTAL DEDUCTIONS                
         AR    RE,RF                                                            
         ST    RE,SRTTDED                                                       
*                                                                               
CHKQCX   B     XIT                                                              
*                                                                               
QCTAX    DS    F                   QUEBEC TAXES REMOVED                         
*                                                                               
*              ROUTINE TAKES GST & SETS GST PAID IF GST # ON W4                 
*                                  NTRY RF=AMOUNT SUBJECT TO GST                
         SPACE 1                                                                
         USING TACWD,R4                                                         
CALCGST  NTR1                                                                   
         LR    R1,RF               SAVE AMOUNT FOR PST CALCULATION              
         TM    SRTBITS2,SRTGSTO    USE OLD GST RATE?                            
         BNO   CALCGST3                                                         
         MVC   WORK,=AL4(GSTOLDRT) 7% GST                                       
         B     *+10                                                             
CALCGST3 MVC   WORK,TGGSTRAT       GST FROM RATE RECORD                         
         M     RE,WORK             CALCULATE GST                                
         D     RE,=F'5000'                                                      
         LTR   RF,RF                                                            
         BM    *+8                                                              
         AH    RF,=H'1'                                                         
         SRA   RF,1                                                             
         LCR   RF,RF               COMP AMOUNT TO ADD BACK TO CHECK             
*                                                                               
         CLI   TGUSEQU,UCPN        NO GST FOR CPN PAYMENTS                      
         BE    CALCGST9                                                         
         CLI   TGUSEQU,UPEN        OR PEN PAYMENTS                              
         BE    CALCGST9                                                         
         CLC   SRTGST#,SPACES      IF PERFORMER HAS GST NUMBER                  
         BNH   CALCGST9                                                         
         ST    RF,SRTGST           SAVE GST DUE IN SORT RECORD                  
         ST    RF,SRTGSTPD         SAVE IN SORT RECORD AS GST PAID              
         ST    RF,TACWGST          SAVE IN WITHHOLDING ELEMENT                  
*                                                                               
         TM    TGPRVST,TGPSTHST                                                 
         BZ    CALCGST5                                                         
* DON'T TAKE PST ON GST - QUEBEC 4/13 (SCHT)                                    
*&&DO                                                                           
         TM    TGPRVST,TGPSTQUE    QUEBEC QST IS DIFFERENT                      
         BZ    CALCGST4                                                         
         LR    RE,R1                                                            
         SR    RE,RF               TAXABLE AMOUNT + GST (NEG FROM LCR)          
         LR    RF,RE                                                            
         B     *+6                                                              
*&&                                                                             
CALCGST4 LR    RF,R1               RESTORE AMOUNT TO BE TAXED                   
         SR    RE,RE                                                            
         MVC   WORK,TGPSTRAT       CALCULATE PST                                
         M     RE,WORK                                                          
         D     RE,=F'5000'                                                      
         LTR   RF,RF                                                            
         BM    *+8                                                              
         AH    RF,=H'1'                                                         
         SRA   RF,1                                                             
         ST    RF,SRTPST                                                        
*                                                                               
CALCGST5 BAS   RE,SUBTAX           ADJUST TOTALS ACCORDINGLY                    
*                                                                               
         LA    R4,ELEM             BUILD FREE-FORM NUMBER ELEMENT               
         USING TANUD,R4                                                         
         XC    ELEM,ELEM                                                        
         MVI   TANUEL,TANUELQ                                                   
         MVI   TANULEN,TANULNQ+L'SRTGST#                                        
         MVI   TANUTYPE,TANUTGST            SET TYPE = GST ID NUMBER            
         MVC   TANUMBER(L'SRTGST#),SRTGST#  SAVE GST NUMBER IN ELEMENT          
*                                                                               
         MVC   AIO,ASRTCHK         ADD ELEMENT TO CHECK RECORD                  
         GOTO1 ADDELEM                                                          
         MVC   AIO,AIO1                                                         
*                                                                               
CALCGST9 LA    R4,ELEM             BUILD CHECK EXTRA DETAILS ELEMENT            
         USING TACXD,R4                                                         
         XC    ELEM,ELEM                                                        
         MVI   TACXEL,TACXELQ                                                   
         MVI   TACXLEN,TACXLNQ2                                                 
         L     R1,SRTGST           UNCOMPLEMENT GST DUE                         
         LCR   R1,R1                                                            
         ST    R1,TACXGST          SAVE GST IN ELEMENT                          
         MVC   TACXPST,SRTPST      SAVE PST IN ELEMENT                          
*                                                                               
*        MVC   AIO,ASRTCHK         ADD ELEMENT TO CHECK RECORD                  
*        GOTO1 ADDELEM                                                          
         MVC   AIO,AIO1                                                         
         B     XIT                                                              
         EJECT                                                                  
* THIS ROUTINE SUBTRACTS THE TAX AMOUNTS FOUND IN THE CHECK WITHHOLDING         
* ELEMENT FROM THE REMAINING CHECK BALANCE AND ADDS THEM TO THE TOTAL           
* DEDUCTIONS.                                                                   
*                                                                               
SUBTAX   NTR1                                                                   
         USING TACWD,R4                                                         
         CLI   TACWEL,TACWELQ      STILL HAVE A TACW ELEMENT?                   
         BNE   STX                                                              
*                                                                               
         L     RF,TACWTAX          RF = TOTAL TAXES FOR THIS ELEMENT            
         A     RF,TACWFICA                                                      
         A     RF,TACWSUI                                                       
         S     RF,SRTPST                                                        
         A     RF,TACWSFLI                                                      
*                                                                               
         L     RE,SRTNET           SUBTRACT TAXES FROM REMAINING BAL            
         SR    RE,RF                                                            
         ST    RE,SRTNET                                                        
*                                                                               
         CLC   TACWUNIT,=C'CN '    IF UNIT IS CANADA                            
         BNE   STX50                                                            
         S     RF,TACWSUI          GST PAID IS NOT A DEDUCT. (TACWGST)          
         A     RF,SRTPST           PST IS NOT A DEDUCT. (TACXPST)               
*                                                                               
STX50    L     RE,SRTTDED          ADD TAXES TO TOTAL DEDUCTIONS                
         AR    RE,RF                                                            
         ST    RE,SRTTDED                                                       
*                                                                               
STX      B     XIT                                                              
         SPACE 2                                                                
* THE FOLLOWING ARE PLACES THAT THE PROGRAM COMES TO IF AN ERROR OCCURS         
* THAT MAKES IT IMPOSSIBLE TO CONTINUE PROCESSING THE INVOICE.  THE             
* TYPE OF THE ERROR IS SAVED IN INVTERR AND THEN THE PROCESSING OF              
* THE INVOICE IS ABORTED BY LOADING RD WITH INVRD (WHICH WAS SET BY             
* PROCINV) AND DOING AND XIT.  CONTROL WILL BE RETURNED TO CHKPASS1             
* WHICH WILL CALL UPDINV TO WRITE BACK THE INVOICE RECORD WITH THE              
* ERROR STATUS INCLUDED.                                                        
*                                                                               
ERRECFD  MVI   INVTERR,TAINECFD    NO FEDERAL WITHHLD ELEM IN W4 REC            
         B     ERRABORT                                                         
*                                                                               
ERRABORT BAS   RE,ADDERI           ADD INVOICE ERROR TO ERITAB                  
         MVC   AIO,AIO1            RESTORE AIO JUST IN CASE                     
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'I',SRTREC),SRTCHECK-SRTREC,=C'ERROR'             
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'I',ASRTCHK),0,=C'SRTCHECK'                       
*                                                                               
         L     RD,INVRD            RETURN TO CALLER OF PROCINV                  
         B     XIT                                                              
*                                                                               
* THIS ROUTINE ADDS AN ENTRY TO THE ERROR INVOICE TABLE AND PUTS THE            
* INVOICE NUMBER AND INVOICE ERROR NUMBER IN THE ENTRY.                         
*                                                                               
ADDERI   L     R5,NEXTERI          R5 = A(NEXT INVTAB ENTRY TO ADD)             
         USING ERITABD,R5                                                       
*                                                                               
         MVC   ERIAGY,SRTAGY       SAVE AGENCY NUMBER IN ENTRY                  
         MVC   ERIINV,SRTINV       SAVE INVOICE NUMBER IN ENTRY                 
         MVC   ERITERR,INVTERR     SAVE INVOICE ERROR NUMBER IN ENTRY           
*                                                                               
         LA    R5,ERILEN(R5)       SAVE ADDRESS OF NEXT ENTRY                   
         ST    R5,NEXTERI                                                       
         BR    RE                                                               
         EJECT                                                                  
* MISCELLANEOUS CODE                                                            
*                                                                               
         GETEL R4,DATADISP,ELCODE                                               
*                                                                               
YES      SR    RC,RC                                                            
NO       LTR   RC,RC                                                            
XIT      XIT1                                                                   
         EJECT                                                                  
* CONSTANTS, ETC.                                                               
*                                                                               
HEXFFS   DC    6X'FF'                                                           
*                                                                               
*                                                                               
TUFLAG   DS    XL1                 TATU FLAGS                                   
TUPHLRES EQU   X'80'               PHL RESIDENT                                 
TUNYCRES EQU   X'40'               NYC RESIDENT                                 
*                                                                               
TGTWAGE  DS    XL4                 TAXABLE WAGES                                
TGTNWAGE DS    XL4                 TAXABLE NON WAGES                            
TGNTWAGE DS    XL4                 NON TAXABLE WAGES/EARNINGS                   
TGTSTRE  DS    XL4                 TAXABLE NON WAGES                            
TGYTDTAX DS    XL4                 YTD TAXES                                    
TGYTDSUI DS    XL4                 YTD SUI                                      
TGYTDSDI DS    XL4                 YTD SDI                                      
TGYTDFLI DS    XL4                 YTD FLI                                      
*                                                                               
CHKFLAGS DS    XL1                 FLAGS                                        
CHKFROI  EQU   X'80'               LEFTOVER $ (RETAIN ORDER OF INPUT)           
         LTORG                                                                  
                                                                                
* THIS ROUTINE DETERMINES THE TAXABLE STATE.  USUALLY IT IS THE STATE           
* OF WORK.  IF THE SOW IS NOT DEFINED ON THE EMPLOYER RECORD AND IF THE         
* STATE OF RESIDENCE IS DEFINED ON THE EMPLOYER RECORD THEN THE SOR IS          
* THE TAXABLE STATE FOR PURPOSES OF DISABILITY, UNEMPLOYMENT, ETC.              
* AN ATTEMPT IS MADE FOR THE SOR TO BE THE TAXABLE STATE IF THE SOW IS          
* DEFINED AS THE RECIPROCAL STATE ON THE PERFORMER'S W4 RECORD. IF BOTH         
* THE SOW & SOR ARE NOT DEFINED ON THE EMPLOYER RECORD THEN THE ROUTINE         
* ATTEMPTS TO ASSIGN THE DEFAULT TAXABLE STATE FROM THE EMPLOYER                
* RECORD.  THE STATE U.C.# FOR THE TAXABLE STATE IS SET ACCORDINGLY.            
*                                                                               
SETTAXBL NTR1  BASE=*,LABEL=*                                                   
         CLC   RECIPST,SRTSOW      IF RECIPROCAL STATE = STATE OF WORK          
         BE    SETTX20             SOR SHOULD BE TAXABLE STATE                  
*                                                                               
SETTX10  MVC   SRTTXST,SRTSOW      SET TAXABLE STATE IS STATE OF WORK           
         MVC   SRTTXCY,SRTCOW                  CITY IS CITY OF WORK             
*                                                                               
         GOTO1 GETSUC,DMCB,SRTSOW  LOOK UP STATE OF WORK ON EMP RECORD          
*                                                                               
         OC    SRTSUC,SRTSUC       IF STATE OF RESIDENCE IS FOUND               
         BZ    SETTX15                                                          
         CLC   SRTCOR,=C'PHL'      AND CITY OF RESIDENCE IS PHL                 
         BE    SETTX12                                                          
         CLC   SRTCOR,=C'NYC'                                                   
         BNE   SETTXX                                                           
SETTX12  MVC   SRTTXCY,SRTCOR      SET TAXABLE CITY AS PHL/NYC                  
         B     SETTXX                                                           
*                                                                               
SETTX15  CLC   RECIPST,SRTSOW      IF RECIPROCAL STATE = STATE OF WORK          
         BE    SETTX40             TRIED SOR ALREADY - MUST DEFAULT             
*                                                                               
SETTX20  GOTO1 GETSUC,DMCB,SRTSOR  LOOK UP STATE OF RES. ON EMP RECORD          
*                                                                               
         MVC   SRTTXST,SRTSOR      SET TAXABLE STATE IS STATE OF RES.           
         MVC   SRTTXCY,SRTCOR                  CITY IS CITY OF RES.             
*                                                                               
         OC    SRTSUC,SRTSUC       IF FOUND THEN DONE                           
         BNZ   SETTXX                                                           
*                                                                               
         CLC   RECIPST,SRTSOW      IF RECIPROCAL STATE = STATE OF WORK          
         BE    SETTX10             GO BACK AND TRY STATE OF WORK                
*                                                                               
SETTX40  XC    SRTTXST,SRTTXST     CLEAR TAXABLE STATE                          
         XC    SRTTXCY,SRTTXCY               AND CITY                           
*                                                                               
         OC    DEFSTATE,DEFSTATE   IF NO DEFAULT TAXABLE STATE DEFINED          
         BZ    SETTXX              THEN DONE                                    
*                                                                               
         MVC   SRTTXST(2),DEFSTATE ELSE SET DEFAULT TAXABLE STATE               
         MVI   SRTTXST+2,C' '                                                   
*                                                                               
         XC    ELEMENT,ELEMENT     BUILD CHECK WITHHOLDING EL FOR IT            
         LA    R4,ELEMENT                                                       
         USING TACWD,R4                                                         
         MVI   TACWEL,TACWELQ                                                   
         OI    TACWSTAT,TACWSTAX+TACWSDEF   SET TAXABLE/DEFAULT STATE           
         MVC   TACWUNIT,SRTTXST                                                 
*                                                                               
         MVI   TACWLEN,TACWLN2Q                                                 
         CLI   RECNUM,PK                                                        
         BNE   SETTX50                                                          
         CLC   TACWUNIT,=C'FD '                                                 
         BE    *+10                                                             
         CLC   TACWUNIT,=C'CN '                                                 
         BE    *+12                                                             
         MVI   TACWLEN,TACWLN3Q    STATE                                        
         CLI   TACWUNIT+2,C' '                                                  
         BE    *+8                                                              
         MVI   TACWLEN,TACWLN4Q    CITY                                         
*                                                                               
SETTX50  MVC   AIO,ASRTCHK                                                      
         GOTO1 ADDELEM             ADD IT TO CHECK RECORD                       
         MVC   AIO,AIO1                                                         
SETTXX   J     XIT                                                              
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
* ACCUMULATE TACW TAXES (RETAIN ORDER OF INPUT)                                 
*                                                                               
UPDTACW  NTR1  BASE=*,LABEL=*                                                   
N        USING TACWD,ELEM                                                       
         MVC   AIO,ASRTCHK                                                      
         MVI   ELCODE,TACWELQ                                                   
         GOTO1 GETL,DMCB,(3,N.TACWUNIT)                                         
         JNE   UPDTACWX                                                         
*                                                                               
         L     R4,TGELEM                                                        
         USING TACWD,R4                                                         
*                                                                               
         ICM   RF,15,TACWTAX                                                    
         ICM   RE,15,N.TACWTAX                                                  
         AR    RF,RE                                                            
         STCM  RF,15,TACWTAX                                                    
         ICM   RF,15,TACWFICA                                                   
         ICM   RE,15,N.TACWFICA                                                 
         AR    RF,RE                                                            
         STCM  RF,15,TACWFICA                                                   
         ICM   RF,15,TACWMED                                                    
         ICM   RE,15,N.TACWMED                                                  
         AR    RF,RE                                                            
         STCM  RF,15,TACWMED                                                    
*                                                                               
         CLI   TACWUNIT+2,C' '     CITY?                                        
         JE    UTACW10                                                          
         CLC   SRTCOR,TACWUNIT     RESIDENT CITY?                               
         JNE   UTACW10                                                          
         TM    CHKFLAGS,CHKFROI    PROCESSING LEFTOVER DOLLARS?                 
         BO    UTACW10                                                          
         ICM   RF,15,TACWRTX                                                    
         ICM   RE,15,N.TACWTAX                                                  
         AR    RF,RE                                                            
         STCM  RF,15,TACWRTX                                                    
*                                                                               
UTACW10  CLI   TACWLEN,TACWLN2Q                                                 
         JL    UPDTACWX                                                         
*                                                                               
         ICM   RF,15,TACWSFLI                                                   
         ICM   RE,15,N.TACWSFLI                                                 
         AR    RF,RE                                                            
         STCM  RF,15,TACWSFLI                                                   
*                                                                               
         CLI   TACWLEN,TACWLN3Q                                                 
         JL    UPDTACWX                                                         
*                                                                               
         ICM   RF,15,TACWPFTX                                                   
         ICM   RE,15,N.TACWPFTX                                                 
         AR    RF,RE                                                            
         STCM  RF,15,TACWPFTX                                                   
         ICM   RF,15,TACWPFFI                                                   
         ICM   RE,15,N.TACWPFFI                                                 
         AR    RF,RE                                                            
         STCM  RF,15,TACWPFFI                                                   
*                                                                               
         CLI   TACWLEN,TACWLN4Q                                                 
         JL    UPDTACWX                                                         
*                                                                               
         ICM   RF,15,TACWPSTX                                                   
         ICM   RE,15,N.TACWPSTX                                                 
         AR    RF,RE                                                            
         STCM  RF,15,TACWPSTX                                                   
         ICM   RF,15,TACWPSDI                                                   
         ICM   RE,15,N.TACWPSDI                                                 
         AR    RF,RE                                                            
         STCM  RF,15,TACWPSDI                                                   
         ICM   RF,15,TACWPSUI                                                   
         ICM   RE,15,N.TACWPSUI                                                 
         AR    RF,RE                                                            
         STCM  RF,15,TACWPSUI                                                   
         ICM   RF,15,TACWPFLI                                                   
         ICM   RE,15,N.TACWPFLI                                                 
         AR    RF,RE                                                            
         STCM  RF,15,TACWPFLI                                                   
*                                                                               
UPDTACWX J     XIT                                                              
         DROP  R4,N                                                             
         LTORG                                                                  
*                                                                               
*        CHECK IF SSN EXCLUDED FROM HANDLING OR GST CALC                        
*        THESE SSNS ARE I&R OR ACTRAWORKS CHECKS                                
*                                  XIT - CC EQU OKAY TO TAKE TAXES              
         SPACE 1                                                                
CHKSSN   NTR1  BASE=*,LABEL=*                                                   
         TM    TGSYSTAT,TASYSFLT   NEW FLAT TAX RULES?                          
         BZ    CHKSSN5                                                          
         CLC   SRTSSN,=C'000117358'                                             
         JE    NO                                                               
         CLC   SRTSSN,=C'000112843'                                             
         JE    NO                                                               
*                                                                               
CHKSSN5  LA    R4,CANXTAB          R4=A(SSN TABLE)                              
*                                                                               
CHKSSN10 CLI   0(R4),X'FF'         END OF TABLE                                 
         JE    YES                                                              
         CLC   SRTSSN,0(R4)        IF I&R OR ACTRAWRKS ACCT - DONE              
         JE    NO                                                               
         LA    R4,L'CANXTAB(R4)    BUMP TABLE                                   
         B     CHKSSN10                                                         
*                                                                               
CANXTAB  DS    0CL9                                                             
         DC    CL9'000000055'                                                   
         DC    CL9'000003755'                                                   
         DC    CL9'000003855'                                                   
         DC    CL9'000003876'                                                   
         DC    CL9'000007106'                                                   
         DC    CL9'000008213'                                                   
         DC    CL9'000000066'                                                   
*        DC    CL9'000117358'                                                   
*        DC    CL9'000002533'                                                   
         DC    X'FF'                                                            
         LTORG                                                                  
         EJECT                                                                  
* THIS ROUTINE DETERMINES IF WE SHOULD DO UNIT WITHHOLDING FOR                  
* NONRESIDENT CORPS WORKING IN UNIT PASSED IN PARM1                             
*   SETS CC EQUAL IF YES, ELSE                                                  
*        CC NOT EQUAL.                                                          
*                                                                               
TSTNRES  NTR1  BASE=*,LABEL=*                                                   
         L     R3,0(R1)            ADDRESS OF UNIT                              
                                                                                
         CLI   RECNUM,PK                                                        
         JE    TSTNRES5                                                         
                                                                                
         CLC   SRTUNIT,0(R3)       TEST WORKING IN CALIFORNIA                   
         JE    TSTNRES5                                                         
         CLC   =C'CA ',0(R3)       CALIFORNIA HAS TO TEST FOR LAX               
         JNE   NO                                                               
         CLC   SRTUNIT,=C'LAX'                                                  
         JNE   NO                                                               
TSTNRES5 CLC   RECIPST,0(R3)       RECIPROCAL STATE MUST NOT BE UNIT            
         JE    NO                                                               
         L     R4,AW4IO            R4 = A(W4 DETAILS ELEMENT)                   
         MVI   ELCODE,TAW4ELQ                                                   
         BRAS  RE,GETEL                                                         
         JNE   NO                                                               
         USING TAW4D,R4                                                         
         TM    TAW4STA2,TAW4SFGN   OK IF FOREIGN ADDRESS                        
         JO    YES                                                              
         L     R4,AW4IO            R4 = A(FIRST W4 WITHHOLDING ELEMENT)         
         MVI   ELCODE,TAA2ELQ                                                   
         BRAS  RE,GETEL                                                         
         JNE   NO                  DONE IF NO NEW TYPE ADDRESS ELEM             
         USING TAA2D,R4                                                         
         CLC   TAA2ST,0(R3)        WITHHOLD IF STATE IS NOT UNIT                
         JNE   YES                                                              
         CLC   RECIPST,=C'OT '     ELSE ONLY IF RECIPROCAL STATE IS OT          
         JE    YES                                                              
         J     NO                                                               
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
* GET TAXES FOR CALIFORNIA OR PUERTO RICO                                       
*                                                                               
         USING TATUD,R4                                                         
TAXCAPR  NTR1  BASE=*,LABEL=*                                                   
         L     R4,ASRTCHK                                                       
         MVI   ELCODE,TATUELQ                                                   
         BRAS  RE,GETEL                                                         
         B     *+8                                                              
TAXCP10  BRAS  RE,NEXTEL                                                        
         BNE   TAXCAPRX                                                         
*                                                                               
         MVC   TGFULL,=F'7'        CALCULATE ONLY CALIFORNIA                    
         CLC   TATUUNIT,=C'CA '    OR PUERTO RICO TAXES                         
         BE    TAXCP20                                                          
         CLC   TATUUNIT,=C'PR '                                                 
         BNE   TAXCP10                                                          
         MVC   TGFULL,=F'29'                                                    
*                                                                               
TAXCP20  ICM   RF,15,TATUWAAD                                                   
         ICM   RE,15,TATUTNAD                                                   
         AR    RF,RE                                                            
*                                                                               
         CLC   TATUUNIT,=C'PR '    REMOVE PR TAXABLE WAGES                      
         BNE   TAXCP30                                                          
*        ICM   R1,15,FORGRS                                                     
*        SR    R1,RF                                                            
*        STCM  R1,15,FORGRS                                                     
*        ICM   R1,15,FORNET                                                     
*        SR    R1,RF                                                            
*        STCM  R1,15,FORNET                                                     
         LARL  R1,FORGRS                                                        
         ICM   RE,15,0(R1)                                                      
         SR    RE,RF                                                            
         STCM  RE,15,0(R1)                                                      
         LARL  R1,FORNET                                                        
         ICM   RE,15,0(R1)                                                      
         SR    RE,RF                                                            
         STCM  RE,15,0(R1)                                                      
*                                                                               
TAXCP30  XR    RE,RE                                                            
         M     RE,TGFULL                                                        
*                                                                               
         MVC   SRTUNIT,TATUUNIT                                                 
*        MVI   NRESFLG,NRESFFOR                                                 
         MVI   BYTE,NRESFFOR                                                    
*                                                                               
         GOTOR ADDNRES,DMCB,(RF),(RE),BYTE                                      
         MVI   ELCODE,TATUELQ                                                   
         B     TAXCP10                                                          
*                                                                               
TAXCAPRX J     XIT                                                              
*                                                                               
* THIS ROUTINE INSURES THAT THE TAX AMOUNTS RETURNED FROM TASEGUE DO            
* NOT EXCEED THE REMAINING BALANCE ON THE CHECK.                                
*                                                                               
         USING TACWD,R4                                                         
NORMTAX  NTR1  BASE=*,LABEL=*                                                   
         L     RF,SRTNET           RF = REMAINING BALANCE ON CHECK              
         S     RF,SRTMDED                                                       
         S     RF,SRTDUES                                                       
*                                                                               
         CLC   TACWUNIT,=C'FD '    FICA CALCULATED BEFORE                       
         BE    *+10                                                             
         CLC   TACWUNIT,=C'NY '    IF NEW YORK STATE                            
         BE    *+10                                                             
         CLC   TACWUNIT,=C'HI '    OR HAWAII                                    
         BE    *+10                                                             
         CLC   TACWUNIT,=C'CA '    OR CALIFORNIA, SDI CALCULATED BEFORE         
         BNE   *+10                                                             
NORMTAX3 MVC   TASEFICA,TACWFICA                                                
                                                                                
NORMTAX4 ICM   RE,15,TASEFICA      RE = FICA AMOUNT TAKEN                       
         CR    RE,RF               IF FICA AMOUNT EXCEEDS REM. BALANCE          
         BNH   *+10                                                             
         LR    RE,RF               THEN MAKE FICA AMOUNT = REM. BALANCE         
         STCM  RE,15,TASEFICA                                                   
*                                                                               
         SR    RF,RE               REDUCE REMAINING BALANCE                     
*                                                                               
         ICM   RE,15,TASETAX       RE = TAX AMOUNT TAKEN                        
         CR    RE,RF               IF TAX AMOUNT EXCEEDS REM. BALANCE           
         BNH   NORMTAX5                                                         
         LR    RE,RF               THEN MAKE TAX AMOUNT = REM. BALANCE          
         TM    TGTASTAT,TALUSRND   IF STATE INCOME TAX ROUNDING REQ'D           
         BZ    NORMTAX5                                                         
         CVD   RF,DUB              THEN ROUND DOWN REM. BAL TO DOLLARS          
         ZAP   WORK(16),DUB                                                     
         SRP   WORK(16),64-2,0     BY DIVIDING BY 100                           
         SRP   WORK(16),2,0        THEN MULT. BY 100                            
         ZAP   DUB,WORK+8(8)                                                    
         CVB   RE,DUB                                                           
NORMTAX5 STCM  RE,15,TASETAX                                                    
*                                                                               
         SR    RF,RE               REDUCE REMAINING BALANCE                     
*                                                                               
         ICM   RE,15,TASESUI       RE = SUI AMOUNT TAKEN                        
         CR    RE,RF               IF SUI AMOUNT EXCEEDS REM. BALANCE           
         BNH   *+10                                                             
         LR    RE,RF               THEN MAKE SUI AMOUNT = REM. BALANCE          
         STCM  RE,15,TASESUI                                                    
*                                                                               
         SR    RF,RE               REDUCE REMAINING BALANCE                     
*                                                                               
         ICM   RE,15,TASESFLI      RE = FLI AMOUNT TAKEN                        
         CR    RE,RF               IF FLI AMOUNT EXCEEDS REM. BALANCE           
         BNH   *+10                                                             
         LR    RE,RF               THEN MAKE FLI AMOUNT = REM. BALANCE          
         STCM  RE,15,TASESFLI                                                   
*                                                                               
         J     XIT                                                              
         LTORG                                                                  
         EJECT                                                                  
*=====================================================================          
* THIS ROUTINE SETS SOME FLAGS FOR PROCESSING                                   
*=====================================================================          
SETFLAGS NTR1  BASE=*,LABEL=*                                                   
         MVI   TUFLAG,0                                                         
*                                                                               
         USING TAWHD,R4                                                         
         L     R4,AW4IO            IF RESIDES IN PHILADELPHIA THEN              
         MVI   ELCODE,TAWHELQ      ALWAYS TAKE PHILADELPHIA TAXES               
         BRAS  RE,GETEL                                                         
         B     *+8                                                              
SETFLG10 BRAS  RE,NEXTEL                                                        
         BNE   SETFLGX                                                          
         CLC   TAWHUNIT,=C'PHL'    PHL RESIDENT?                                
         BNE   *+8                                                              
         OI    TUFLAG,TUPHLRES     PHL RESIDENT                                 
         CLC   TAWHUNIT,=C'NYC'    NYC RESIDENT?                                
         BNE   *+8                                                              
         OI    TUFLAG,TUNYCRES     NYC RESIDENT                                 
         B     SETFLG10                                                         
*                                                                               
SETFLGX  J     XIT                                                              
         LTORG                                                                  
*=====================================================================          
* THIS ROUTINE ADDS THE TACW ELEMENT FOR NON-RESIDENT TAX                       
*=====================================================================          
ADDNRES  NTR1  BASE=*,LABEL=*                                                   
         L     RF,8(R1)            NRESFLG                                      
         MVC   TGBYTE,0(RF)                                                     
*                                                                               
         L     RF,0(R1)            BASE                                         
         L     RE,4(R1)            RATE                                         
                                                                                
         D     RE,=F'50'                                                        
         LTR   RF,RF                                                            
         BM    *+8                                                              
         AH    RF,=H'1'                                                         
         SRA   RF,1                                                             
*                                                                               
         L     RE,SRTNET           IF TAX > AVAILABLE BALANCE                   
         S     RE,SRTMDED                                                       
         S     RE,SRTDUES                                                       
         CR    RF,RE                                                            
         BNH   *+6                                                              
         LR    RF,RE               THEN TAX = AVAILABLE BALANCE                 
*                                                                               
         LTR   RF,RF               DON'T GO ANY FURTHER IF NOTHING              
         BZ    ADDNRESX            WITHHELD                                     
*                                                                               
         ST    RF,SRTWTAX          SAVE IN SORT RECORD FOR WORK STATE           
*                                                                               
         L     RE,SRTNET           SUBTRACT TAX FROM REMAINING BALANCE          
         SR    RE,RF                                                            
         ST    RE,SRTNET                                                        
*                                                                               
         L     RE,SRTTDED          ADD TAX TO TOTAL DEDUCTIONS                  
         AR    RE,RF                                                            
         ST    RE,SRTTDED                                                       
*                                                                               
         LA    R4,ELEM             BUILD CHECK WITHHOLDING ELEMENT              
         USING TACWD,R4                                                         
         XC    ELEM,ELEM                                                        
         MVI   TACWEL,TACWELQ                                                   
         MVC   TACWUNIT,SRTUNIT                                                 
         CLC   SRTUNIT,=C'LAX'                                                  
         BNE   *+10                                                             
         MVC   TACWUNIT,=C'CA '                                                 
*                                                                               
         MVI   TACWLEN,TACWLN2Q                                                 
         CLI   RECNUM,PK                                                        
         BNE   ADDNR10                                                          
         CLC   TACWUNIT,=C'FD '                                                 
         BE    *+10                                                             
         CLC   TACWUNIT,=C'CN '                                                 
         BE    *+12                                                             
         MVI   TACWLEN,TACWLN3Q    STATE                                        
         CLI   TACWUNIT+2,C' '                                                  
         BE    *+8                                                              
         MVI   TACWLEN,TACWLN4Q    CITY                                         
*                                                                               
ADDNR10  OI    TACWSTAT,TACWSTAX+TACWSWRK  SET TAXABLE+WORK STATUS BITS         
         OC    TACWSTAT,TGBYTE     NRESFLG                                      
         MVC   TACWTAX,SRTWTAX             SAVE TAX IN ELEMENT                  
*                                                                               
         MVC   AIO,ASRTCHK         ADD ELEMENT TO CHECK RECORD                  
         GOTO1 ADDELEM                                                          
         MVC   AIO,AIO1                                                         
*                                                                               
ADDNRESX J     XIT                                                              
         DROP  R4                                                               
         LTORG                                                                  
* THIS ROUTINE WILL CHANGE TASETAX TO BE A FLAT TAX IF TACWTAX HAS A            
* PERCENTAGE IN IT AND THE RESULTING FLAT TAX IS GREATER THAN TASETAX.          
*                                                                               
FLATTAX  NTR1  BASE=*,LABEL=*                                                   
         USING TACWD,R4                                                         
*                                                                               
         OC    TACWTAX,TACWTAX     IF NO FLAT PERCENTAGE THEN RETURN            
         BZ    FLATX                                                            
*                                                                               
         CLC   TACWUNIT,=C'FD '    IF THIS IS NOT FEDERAL                       
         BE    FLAT10                                                           
         OC    TASETAX,TASETAX     AND IF NO WITHHOLDING FROM ALLTAX            
         BNZ   FLAT10                                                           
*                                                                               
         GOTO1 TAXVAL,DMCB,(3,TACWUNIT)  LOOK UP UNIT CODE                      
*                                                                               
         TM    TGTASTAT,TALUNTAX   IF NO INCOME TAX FOR THIS UNIT               
         BO    FLATX               THEN IGNORE FLAT TAX RATE                    
*                                                                               
FLAT10   ICM   RF,15,TACWTAX       FLAT TAX = FLAT PERCENTAGE (2 DEC.)          
         CLI   RECNUM,PK           PAYROLL PLUS?                                
         BNE   FLAT20                                                           
         MVC   TGFULL,TASEEARN                                                  
         M     RE,TGFULL               * TAXABLE EARNINGS                       
         B     FLAT25                                                           
*                                                                               
FLAT20   M     RE,SRTGRS               * TAXABLE EARNINGS                       
FLAT25   D     RE,=F'10000'                                                     
*                                                                               
         TM    TGTASTAT,TALUSRND   IF STATE INCOME TAX ROUNDING REQ'D           
         BZ    FLAT30                                                           
         CVD   RF,DUB              ROUND TO NEAREST DOLLAR                      
         ZAP   WORK(16),DUB                                                     
         SRP   WORK(16),64-2,5     BY DIVIDING BY 100 & ROUNDING                
         SRP   WORK(16),2,0        THEN MULT. BY 100                            
         ZAP   DUB,WORK+8(8)                                                    
         CVB   RF,DUB                                                           
*                                                                               
FLAT30   CLM   RF,15,TASETAX       IF FLAT TAX > TAEGUE TAX                     
         BNH   *+8                                                              
         STCM  RF,15,TASETAX       THEN SET TASEGUE TAX TO FLAT TAX             
*                                                                               
FLATX    B     XIT                                                              
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*              ROUTINE TO GET HAWAII TAXES                                      
*                                                                               
GETHAWII NTR1  BASE=*,LABEL=*                                                   
         USING TACWD,R4                                                         
*                                                                               
         CLC   TACWUNIT,=C'HI '    IF HAWAII                                    
         JNE   NO                                                               
         CLC   SRTTXST,=C'HI '     AND TAXABLE STATE IS HAWAII                  
         JNE   NO                                                               
*                                  GET A(QTDTAB ENTRY) FOR THIS UNIT            
         GOTO1 GETQTD,DMCB,TACWUNIT                                             
         L     R5,0(R1)                                                         
         USING QTDTABD,R5                                                       
         MVI   TASEFREQ,C'Q'       SET FREQUENCY TO QUARTERLY                   
         ICM   R3,15,TASEEARN      SAVE ORIGINAL TASEEARN                       
                                                                                
         LR    RE,R3               SET TO CALC TAXES ON THIS CHECK'S            
         ICM   RF,15,QTDEARN       QTD EARNINGS                                 
         AR    RE,RF               PLUS PREVIOUS YTD EARNINGS                   
         STCM  RE,15,TASEEARN                                                   
*                                                                               
         XC    TASETAX(76),TASETAX CLEAR TASEGUE RETURN AMOUNTS                 
*                                                                               
         CLC   =C'P+',TASEEMP                                                   
         BNE   *+10                                                             
         MVC   TASEEMP,=C'TP '     FORCE EMPLOYER TP                            
*                                                                               
         GOTO1 TASEGUE,DMCB,(R9)   CALL TASEGUE AND PASS SEGUE BLOCK            
*                                                                               
         MVC   TASEEMP,SRTEMP      RESTORE EMPLOYER                             
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'S',(R9)),TASELQ,=C'SEGUE SDI OUTPUT'             
*                                                                               
         MVC   TACWSDI,TASESDI     SAVE AWAY WEEKLY SDI                         
*                                                                               
         ICM   RF,15,TASESDI       CALC QTD SDI                                 
         ICM   RE,15,QTDSDI        - PREV QTD SDI                               
         SR    RF,RE                                                            
         STCM  RF,15,TACWSDI       CURRENT SDI                                  
*                                                                               
         STCM  R3,15,TASEEARN      RESTORE ORIGINAL TASEEARN                    
         J     YES                                                              
         LTORG                                                                  
         EJECT                                                                  
* THIS ROUTINE ADDS A CANADIAN WITHHOLDING ELEMENT TO THE CHECK RECORD          
* IF THE TAPDSCAN BIT (TAKE CANADIAN TAXES) IS SET IN THE PAYMENT               
* DETAILS ELEMENT OR IF THIS IS CANADIAN DOLLAR CHECK.                          
*                                                                               
CANWITH  NTR1  BASE=*,LABEL=*                                                   
*        CLI   RECNUM,CN           IF CANADIAN DOLLAR CHECKS                    
*        BE    CANW05                                                           
         TM    SRTBITS,SRTBCAN     IF CANADIAN TAX IS TAKEN                     
         JO    CANW05               (SET BY PAY)                                
         CLI   SRTTYPE,TAW4TYCO    IF PERFORMER IS CORPORATION                  
         BNE   CANW02                                                           
         CLC   =C'OT ',SRTSOW      OUTSIDE OF US & CANADA?                      
         BE    CANW02                                                           
         MVC   TGCTRY,=C'CA'       WORK IN CN? YES - THEN TAKE TAXES            
         GOTO1 TAXVAL,DMCB,(X'FF',SRTSOW) UNIT                                  
         BE    CANW05                                                           
*                                                                               
*&&DO                                                                           
         CLC   =C'ACT',SRTUN       OR US$ ON ACTRA PAYMENT (GST)                
         JE    CANW02                                                           
         CLC   =C'UDA',SRTUN       OR US$ ON UDA PAYMENT                        
         JE    CANW02                                                           
         CLC   =C'NON',SRTUN       OR US$ ON NON UNION AND                      
         JNE   NO                                                               
         CLC   =C'CAN',SRTLOCL     CANADA LOCAL PAYMENT                         
         JNE   NO                                                               
*&&                                                                             
CANW02   CLC   SRTGST#,SPACES                                                   
         JNH   CANW04                                                           
         CLI   TGUSEQU,UCPN        NO GST FOR CPN PAYMENTS                      
         JE    CANW04                                                           
         CLI   TGUSEQU,UPEN        OR PEN PAYMENTS                              
         JNE   CANW05                                                           
*                                                                               
CANW04   OC    SRTPST,SRTPST       ANY AOS PST?                                 
         JZ    NO                  NO, LEAVE ELSE SET DUMMY CW ELEM             
*                                                                               
CANW05   MVC   AIO,ASRTCHK         SET I/O AREA = CHECK RECORD                  
*                                                                               
CANW10   MVI   ELCODE,TACWELQ      THEN IF CANADIAN CW ELEM NOT FOUND           
         GOTO1 GETL,DMCB,=C'CN '                                                
         JE    CANWX                                                            
*                                                                               
         LA    R4,ELEM             THEN BUILD NEW CANADIAN CW ELEMENT           
         USING TACWD,R4                                                         
         XC    ELEM,ELEM                                                        
         MVI   TACWEL,TACWELQ                                                   
         MVI   TACWLEN,TACWLN2Q                                                 
         MVC   TACWUNIT,=C'CN '                                                 
*                                                                               
         GOTO1 ADDELEM             ADD ELEMENT TO CHECK RECORD                  
         J     CANW10              GO FIND IT                                   
*                                                                               
CANWX    MVC   AIO,AIO1                                                         
         J     YES                                                              
         DROP  R4                                                               
         EJECT                                                                  
         LTORG                                                                  
*                                                                               
* ADDS STATE TATU ELEMENT IF ONLY PAID AT CITY LEVEL                            
* ADDS PHL/NYC TATU ELEMENT IF RESIDENT AND NOT PAID IN PHL/NYC                 
* ADDS NON PHL/NYC CITY TAXES TO PHL/NYC IF RESIDENT                            
*                                                                               
TUWITH   NTR1  BASE=*,LABEL=*                                                   
         L     RF,0(R1)                                                         
         ST    RF,AIO                                                           
*                                                                               
         MVI   TUFLAG,0                                                         
*                                                                               
TU10     L     R4,AIO                                                           
         MVI   ELCODE,TATUELQ                                                   
         BRAS  RE,GETEL                                                         
         B     *+8                 DONE IF NO ELEMENTS                          
TU20     BRAS  RE,NEXTEL                                                        
         BNE   TU40                REPEAT UNTIL NO MORE                         
         USING TATUD,R4                                                         
         CLI   TATUUNIT+2,C' '                                                  
         BE    TU20                                                             
*                                                                               
         TM    TATUSTAT,TATUSPRO   ALREADY PROCESSED THIS?                      
         BO    TU20                                                             
         OI    TATUSTAT,TATUSPRO                                                
*                                                                               
         GOTO1 TAXVAL,DMCB,(3,TATUUNIT)                                         
         MVC   TGTHREE,TGTASTCY                                                 
         MVC   TGTWAGE,TATUWAGE    WAGES                                        
         MVC   TGTNWAGE,TATUTNWA   TAXABLE NON WAGES                            
         MVC   TGTSTRE,TATUSTRE   TAXABLE NON WAGES                             
         MVC   TGNTWAGE,TATUNNWA   NON TAXABLE NON WAGES                        
*                                                                               
         MVI   ELCODE,TATUELQ                                                   
         GOTO1 GETL,DMCB,(3,TGTHREE)                                            
         BNE   TU30                                                             
         DROP  R4                                                               
*                                                                               
         L     R5,TGELEM                                                        
         USING TATUD,R5                                                         
*                                                                               
         ICM   RF,15,TATUWAGE      ACCUM WAGES                                  
         ICM   RE,15,TGTWAGE                                                    
         AR    RE,RF                                                            
         STCM  RE,15,TATUWAGE                                                   
*                                                                               
         ICM   RF,15,TATUTNWA      ACCUM TAXABLE NON WAGES                      
         ICM   RE,15,TGTNWAGE                                                   
         AR    RE,RF                                                            
         STCM  RE,15,TATUTNWA                                                   
*                                                                               
         ICM   RF,15,TATUNNWA      ACCUM NON TAXABLE NON WAGES                  
         ICM   RE,15,TGNTWAGE                                                   
         AR    RE,RF                                                            
         STCM  RE,15,TATUNNWA                                                   
*                                                                               
         ICM   RF,15,TATUSTRE      ACCUM TAXABLE NON WAGES                      
         ICM   RE,15,TGTSTRE                                                    
         AR    RE,RF                                                            
         STCM  RE,15,TATUSTRE                                                   
         B     TU20                                                             
*                                                                               
TU30     LA    R5,ELEM             BUILD STATE TU ELEMENT                       
         XC    ELEM,ELEM                                                        
         MVC   ELEM(TATULNQ),0(R4) COPY CITY ELEMENT                            
         MVC   TATUUNIT,TGTHREE    MOVE IN STATE                                
         GOTO1 ADDELEM                                                          
         B     TU10                                                             
         DROP  R5                                                               
*                                                                               
TU40     L     R4,AIO              CLEAR OUT PROCESSED MARKER                   
         MVI   ELCODE,TATUELQ                                                   
         BRAS  RE,GETEL                                                         
         B     *+8                                                              
TU50     BRAS  RE,NEXTEL                                                        
         BNE   TU60                                                             
         USING TATUD,R4                                                         
         NI    TATUSTAT,X'FF'-TATUSPRO                                          
         B     TU50                                                             
         DROP  R4                                                               
*                                                                               
TU60     L     R4,AW4IO            IF RESIDES IN PHILADELPHIA THEN              
         MVI   ELCODE,TAWHELQ      ALWAYS TAKE PHILADELPHIA TAXES               
         BRAS  RE,GETEL                                                         
         BNE   TU80                                                             
         B     *+12                                                             
TU65     BRAS  RE,NEXTEL                                                        
         BNE   TU67                                                             
         USING TAWHD,R4                                                         
         CLC   TAWHUNIT,=C'PHL'    PHL RESIDENT?                                
         BNE   *+8                                                              
         OI    TUFLAG,TUPHLRES     PHL RESIDENT                                 
         CLC   TAWHUNIT,=C'NYC'    NYC RESIDENT?                                
         BNE   *+8                                                              
         OI    TUFLAG,TUNYCRES     NYC RESIDENT                                 
         B     TU65                                                             
*                                                                               
TU67     TM    TUFLAG,TUPHLRES+TUNYCRES                                         
         JZ    TU80                                                             
*                                                                               
         MVI   ELCODE,TATUELQ                                                   
         GOTO1 GETL,DMCB,(3,=C'FD ')                                            
*                                                                               
         L     R5,TGELEM           GET TAXABLE AMOUNTS FROM FEDERAL             
         USING TATUD,R5                                                         
         MVC   TGTWAGE,TATUWAGE    WAGES                                        
         MVC   TGTNWAGE,TATUTNWA   TAXABLE NON WAGES                            
         MVC   TGTSTRE,TATUSTRE    TAXABLE NON WAGES                            
*                                                                               
         MVI   ELCODE,TATUELQ                                                   
         MVC   MYTHREE,=C'PHL'                                                  
         TM    TUFLAG,TUNYCRES                                                  
         BZ    *+10                                                             
         MVC   MYTHREE,=C'NYC'                                                  
         GOTO1 GETL,DMCB,(3,MYTHREE)                                            
         BE    TU70                                                             
*                                                                               
         LA    R5,ELEM             NO - CREATE NEW PHLNYC TATU                  
         XC    ELEM,ELEM                                                        
         MVI   TATUEL,TATUELQ                                                   
         MVI   TATULEN,TATULNQ                                                  
         MVC   TATUUNIT,MYTHREE                                                 
         MVC   TATUWAGE,TGTWAGE    SET PHL/NYC WAGES TO FEDERAL                 
         MVC   TATUTNWA,TGTNWAGE                                                
         MVC   TATUSTRE,TGTSTRE                                                 
         OI    TATUSTAT,TATUSRES   RESIDENCE ONLY, NOT WORKING                  
         GOTO1 ADDELEM                                                          
         B     TU80                                                             
*                                                                               
TU70     L     R5,TGELEM                                                        
         MVC   TATUWAGE,TGTWAGE    SET PHL/NYC WAGES TO FEDERAL                 
         MVC   TATUTNWA,TGTNWAGE                                                
         MVC   TATUSTRE,TGTSTRE                                                 
         DROP  R5                                                               
*                                                                               
TU80     MVI   ELCODE,TATUELQ      WORKED IN CANADA?                            
         GOTO1 GETL,DMCB,(3,=C'CN ')                                            
         BNE   TUX                                                              
*                                                                               
         L     R5,TGELEM           GET TAXABLE AMOUNTS                          
         USING TATUD,R5                                                         
         MVC   TGTWAGE,TATUWAGE    WAGES                                        
         MVC   TGTNWAGE,TATUTNWA   TAXABLE NON WAGES                            
         MVC   TGTSTRE,TATUSTRE    TAXABLE NON WAGES                            
*                                                                               
         L     R4,AW4IO            IF WORKING IN CANADA                         
         MVI   ELCODE,TAWHELQ      MUST PAY US STATE AND CITY TAXES             
         BRAS  RE,GETEL                                                         
         B     *+8                                                              
TU85     BRAS  RE,NEXTEL                                                        
         BNE   TUX                                                              
         USING TAWHD,R4                                                         
*                                                                               
         CLC   TAWHUNIT,=C'FD '                                                 
         BE    TU85                                                             
         CLC   TAWHUNIT,=C'CN '                                                 
         BE    TU85                                                             
*                                                                               
         MVI   ELCODE,TATUELQ                                                   
         GOTO1 GETL,DMCB,(3,TAWHUNIT)                                           
         BE    TU90                                                             
*                                                                               
         LA    R5,ELEM             NO - CREATE NEW TATU                         
         XC    ELEM,ELEM                                                        
         MVI   TATUEL,TATUELQ                                                   
         MVI   TATULEN,TATULNQ                                                  
         MVC   TATUUNIT,TAWHUNIT                                                
         OI    TATUSTAT,TATUSRES   RESIDENCE ONLY, NOT WORKING                  
         MVC   TATUWAGE,TGTWAGE    SET WAGES TO CANADIAN                        
         MVC   TATUTNWA,TGTNWAGE                                                
         MVC   TATUSTRE,TGTSTRE                                                 
         GOTO1 ADDELEM                                                          
         MVI   ELCODE,TAWHELQ                                                   
         B     TU85                                                             
*                                                                               
TU90     L     R5,TGELEM                                                        
         MVC   TATUWAGE,TGTWAGE    SET PHL/NYC WAGES TO FEDERAL                 
         MVC   TATUTNWA,TGTNWAGE                                                
         MVC   TATUSTRE,TGTSTRE                                                 
         B     TU85                                                             
         DROP  R4,R5                                                            
*                                                                               
TUX      J     XIT                                                              
*                                                                               
MYTHREE  DS    XL3                                                              
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
* THIS ROUTINE PRORATES TAX WITHHOLDINGS AT THE UNIT LEVEL                      
*                                                                               
PROTACW  NTR1  BASE=*,LABEL=*                                                   
         MVC   AIO,ASRTCHK                                                      
*                                                                               
         MVI   ELCODE,TACWELQ                                                   
         GOTO1 GETL,DMCB,(3,=C'FD ')                                            
         BNE   PROTACWX                                                         
*                                                                               
         L     R4,TGELEM                                                        
         USING TACWD,R4                                                         
         MVC   SVFTAX,TACWTAX      SAVE FEDERAL TAX                             
         MVC   SVFICA,TACWFICA     SAVE FICA                                    
*                                                                               
*        MVC   AIO,AIO3                                                         
         MVC   AIO,ASRTCHK                                                      
         MVI   ELCODE,TATUELQ                                                   
         GOTO1 GETL,DMCB,(3,=C'FD ')                                            
         BNE   PROTACWX                                                         
*                                                                               
         ZAP   SVFEARN,=P'0'                                                    
*                                                                               
         L     R4,TGELEM                                                        
         USING TATUD,R4                                                         
*        ICM   RF,15,TATUWAGE                                                   
*        ICM   RE,15,TATUSTRE                                                   
         ICM   RF,15,TATUWAAD                                                   
         ICM   RE,15,TATUTNAD                                                   
         AR    RF,RE                                                            
         CVD   RF,SVFEARN          FEDERAL EARNINGS                             
*                                                                               
* BUILD TABLE FOR ALL STATES/CITIES AND THEIR % OF THE FEDERAL/STATE            
*                                                                               
         LA    RE,PROTAB                                                        
         LA    RF,500                                                           
         XCEF                                                                   
*                                                                               
         LA    R5,PROTAB                                                        
         USING PROTABD,R5                                                       
         MVI   0(R5),X'FF'         DENOTE END OF TABLE                          
*                                                                               
*        L     R4,AIO3                                                          
         L     R4,ASRTCHK                                                       
         MVI   ELCODE,TATUELQ                                                   
         BAS   RE,GETEL                                                         
         B     *+8                                                              
PRCW10   BAS   RE,NEXTEL                                                        
         BNE   PRCW30                                                           
                                                                                
*        CLC   TATUUNIT,=C'FD '                                                 
*        BE    PRCW10                                                           
*                                                                               
         LA    RF,SKPUNTS          UNITS TO SKIP (FED AND CANADIAN)             
PRCW13   CLI   0(RF),X'FF'                                                      
         BE    PRCW15                                                           
         CLC   0(2,RF),TATUUNIT                                                 
         BE    PRCW10                                                           
         AHI   RF,3                                                             
         B     PRCW13                                                           
*                                                                               
PRCW15   CLI   TATUUNIT+2,C' '     CITY?                                        
         BE    PRCW20                                                           
*                                                                               
         GOTO1 TAXVAL,DMCB,(3,TATUUNIT)    GET STATE EARNINGS                   
         MVC   TGTHREE,TGTASTCY                                                 
         GOTO1 GETL,DMCB,(3,TGTHREE)                                            
         BNE   PRCW20                                                           
         DROP  R4                                                               
*                                                                               
         L     R2,TGELEM                                                        
         USING TATUD,R2                                                         
*        ICM   RF,15,TATUWAGE                                                   
*        ICM   RE,15,TATUSTRE                                                   
         ICM   RF,15,TATUWAAD                                                   
         ICM   RE,15,TATUTNAD                                                   
         AR    RF,RE                                                            
         CVD   RF,SVSEARN          STATE EARNINGS                               
         DROP  R2                                                               
*                                                                               
         USING TATUD,R4                                                         
         MVC   PROTUNIT,TATUUNIT   CALCULATE PRORATED CITY% FOR STATE           
         MVC   PROTSPCT,=F'10000'  DEFAULT TO 100%                              
*        ICM   RF,15,TATUWAGE                                                   
*        ICM   RE,15,TATUSTRE                                                   
         ICM   RF,15,TATUWAAD                                                   
         ICM   RE,15,TATUTNAD                                                   
         AR    RF,RE                                                            
         ZAP   TMPPL16,=P'0'                                                    
         CVD   RF,TMPPL16+8                                                     
*                                                                               
         CP    SVSEARN,=P'0'                                                    
         BE    PRCW20                                                           
*                                                                               
         MP    TMPPL16(16),=PL5'1000000'                                        
         DP    TMPPL16(16),SVSEARN                                              
         ZAP   DUB,TMPPL16(8)                                                   
         CVB   R1,DUB                                                           
         STCM  R1,15,PROTSPCT      STATE %                                      
*                                                                               
PRCW20   MVC   PROTUNIT,TATUUNIT   CALCULATE PRORATED STATE% FOR FED.           
         MVC   PROTFPCT,=F'10000'  DEFAULT TO 100%                              
*        ICM   RF,15,TATUWAGE                                                   
*        ICM   RE,15,TATUSTRE                                                   
         ICM   RF,15,TATUWAAD                                                   
         ICM   RE,15,TATUTNAD                                                   
         AR    RF,RE                                                            
         ZAP   TMPPL16,=P'0'                                                    
         CVD   RF,TMPPL16+8                                                     
*                                                                               
         CP    SVFEARN,=P'0'                                                    
         BE    PRCW22                                                           
*                                                                               
         MP    TMPPL16(16),=PL5'1000000'                                        
         DP    TMPPL16(16),SVFEARN                                              
         ZAP   DUB,TMPPL16(8)                                                   
         CVB   R1,DUB                                                           
         STCM  R1,15,PROTFPCT      FEDERAL %                                    
*                                                                               
PRCW22   AHI   R5,PROTABLN                                                      
         MVI   0(R5),X'FF'                                                      
         B     PRCW10                                                           
*                                                                               
* AT THIS POINT, PROTAB IS BUILT.  NOW PRORATE FEDERAL WITHHOLDINGS             
* FOR EACH STATE AND PRORATE STATE WITHHOLDINGS FOR EACH CITY                   
*                                                                               
PRCW30   LA    R5,PROTAB                                                        
PRCW35   CLI   0(R5),X'FF'                                                      
         BE    PRCW60                                                           
*                                                                               
         MVC   AIO,ASRTCHK                                                      
         MVI   ELCODE,TACWELQ                                                   
         GOTO1 GETL,DMCB,(3,PROTUNIT)                                           
         BNE   PRCW50                                                           
*                                                                               
         L     R4,TGELEM                                                        
         USING TACWD,R4                                                         
*                                                                               
         GOTO1 PRORATE,DMCB,PROTFPCT,SVFTAX       FEDERAL TAX                   
         MVC   TACWPFTX,TGFULL                                                  
         GOTO1 PRORATE,DMCB,PROTFPCT,SVFICA       FEDERAL FICA                  
         MVC   TACWPFFI,TGFULL                                                  
*                                                                               
         CLI   PROTUNIT+2,C' '     CITY?                                        
         BE    PRCW50                                                           
*                                                                               
         GOTO1 TAXVAL,DMCB,(3,PROTUNIT)                                         
         MVC   TGTHREE,TGTASTCY                                                 
*                                                                               
         MVI   ELCODE,TACWELQ      GET STATE WITHOLDINGS                        
         GOTO1 GETL,DMCB,(3,TGTHREE)                                            
         BNE   PRCW50                                                           
*                                                                               
         L     R4,TGELEM                                                        
         XC    SVSTAXES(SVSTAXLN),SVSTAXES                                      
*                                                                               
         GOTO1 PRORATE,DMCB,PROTSPCT,TACWTAX      STATE TAX                     
         MVC   SVSTAX,TGFULL                                                    
         GOTO1 PRORATE,DMCB,PROTSPCT,TACWSDI      STATE SDI                     
         MVC   SVSSDI,TGFULL                                                    
         GOTO1 PRORATE,DMCB,PROTSPCT,TACWSUI      STATE SUI                     
         MVC   SVSSUI,TGFULL                                                    
         GOTO1 PRORATE,DMCB,PROTSPCT,TACWSFLI     STATE FLI                     
         MVC   SVSFLI,TGFULL                                                    
*                                                                               
         MVI   ELCODE,TACWELQ                                                   
         GOTO1 GETL,DMCB,(3,PROTUNIT)                                           
         BNE   PRCW50                                                           
*                                                                               
         L     R4,TGELEM                                                        
         MVC   TACWPSTX,SVSTAX     % OF STATE TAXES                             
         MVC   TACWPSDI,SVSSDI     % OF STATE SDI                               
         MVC   TACWPSUI,SVSSUI     % OF STATE SUI                               
         MVC   TACWPFLI,SVSFLI     % OF STATE FLI                               
*                                                                               
PRCW50   AHI   R5,PROTABLN                                                      
         B     PRCW35                                                           
*                                                                               
* AT THIS POINT, ALL TACW'S ARE BUILT WITH PRORATED WITHHOLDINGS                
* BUT WE NEED TO TAKE ROUNDING INTO ACCOUNT.  AS A RULE, THE LAST               
* STATE WILL HAVE THE REMAINDING FEDERAL WITHHOLDINGS.                          
*                                                                               
PRCW60   XC    TGFULL,TGFULL                                                    
         XC    SVSTAXES(SVSTAXLN),SVSTAXES                                      
*                                                                               
         L     R4,ASRTCHK                                                       
         MVI   ELCODE,TACWELQ                                                   
         BAS   RE,GETEL                                                         
         B     *+8                                                              
PRCW65   BAS   RE,NEXTEL                                                        
         BNE   PRCW70                                                           
*                                                                               
         USING TACWD,R4                                                         
*        CLC   TACWUNIT,=C'FD '                                                 
*        BE    PRCW65                                                           
         LA    RF,SKPUNTS          UNITS TO SKIP (FED AND CANADIAN)             
PRCW68   CLI   0(RF),X'FF'                                                      
         BE    PRCW69                                                           
         CLC   0(2,RF),TACWUNIT                                                 
         BE    PRCW65                                                           
         AHI   RF,3                                                             
         B     PRCW68                                                           
                                                                                
PRCW69   CLI   TACWUNIT+2,C' '                                                  
         BNE   PRCW65                                                           
*                                                                               
         ST    R4,TGFULL           A(LAST STATE WITHHOLDING ELEM)               
*                                                                               
         L     RF,SVSTAX           ACCUMULATE PRORATED STATE TAXES              
         ICM   RE,15,TACWPFTX                                                   
         AR    RF,RE                                                            
         ST    RF,SVSTAX                                                        
*                                                                               
         L     RF,SVSFICA          ACCUMULATE PRORATED STATE FICA               
         ICM   RE,15,TACWPFFI                                                   
         AR    RF,RE                                                            
         ST    RF,SVSFICA                                                       
         B     PRCW65                                                           
*                                                                               
PRCW70   L     R4,TGFULL                                                        
*                                                                               
         L     RF,SVFTAX           FEDERAL TAXES                                
         L     RE,SVSTAX           ACCUMULATED PRORATED STATE TAXES             
         SR    RF,RE                                                            
         ST    RF,SVSTAX           LEFTOVER/REMAINING TAXES                     
*                                                                               
         ICM   RE,15,TACWPFTX                                                   
         AR    RE,RF               UPDATE TAXES IN LAST STATE                   
         STCM  RE,15,TACWPFTX                                                   
*                                                                               
         L     RF,SVFICA           FEDERAL FICA                                 
         L     RE,SVSFICA          ACCUMULATED PRORATED STATE FICA              
         SR    RF,RE                                                            
         ST    RF,SVSFICA          LEFTOVER/REMAINING FICA                      
*                                                                               
         ICM   RE,15,TACWPFFI                                                   
         AR    RE,RF               UPDATE TAXES IN LAST STATE                   
         STCM  RE,15,TACWPFFI                                                   
*                                                                               
         MVC   TGTHREE,TACWUNIT    SAVE STATE                                   
*                                                                               
* AT THIS POINT, THE LAST STATE'S PRORATED WITHHOLDINGS HAVE BEEN               
* UPDATED WITH THE LEFTOVER/REMAINDER. NOW GET THE LAST CITY FOR THAT           
* STATE AND DO THE SAME THING.                                                  
*                                                                               
         XC    TGFULL,TGFULL                                                    
         L     R4,ASRTCHK                                                       
         MVI   ELCODE,TACWELQ                                                   
         BAS   RE,GETEL                                                         
         B     *+8                                                              
PRCW80   BAS   RE,NEXTEL                                                        
         BNE   PRCW90                                                           
*                                                                               
         USING TACWD,R4                                                         
*        CLC   TACWUNIT,=C'FD '                                                 
*        BE    PRCW80                                                           
         LA    RF,SKPUNTS          UNITS TO SKIP (FED AND CANADIAN)             
PRCW83   CLI   0(RF),X'FF'                                                      
         BE    PRCW85                                                           
         CLC   0(2,RF),TACWUNIT                                                 
         BE    PRCW80                                                           
         AHI   RF,3                                                             
         B     PRCW83                                                           
                                                                                
PRCW85   CLI   TACWUNIT+2,C' '                                                  
         BE    PRCW80                                                           
*                                                                               
         GOTO1 TAXVAL,DMCB,(3,TACWUNIT)                                         
         CLC   TGTASTCY,TGTHREE    CITY FOR LAST STATE?                         
         BNE   PRCW80                                                           
         ST    R4,TGFULL           A(LAST CITY FOR LAST STATE)                  
         B     PRCW80                                                           
*                                                                               
PRCW90   OC    TGFULL,TGFULL       FOUND A CITY?                                
         BZ    PROTACWX                                                         
*                                                                               
         L     R4,TGFULL                                                        
         L     RF,SVSTAX           LEFTOVER/REMAINING TAXES                     
         ICM   RE,15,TACWPSTX                                                   
         AR    RE,RF               UPDATE TAXES IN LAST CITY FOR STATE          
         STCM  RE,15,TACWPSTX                                                   
*                                                                               
         L     RF,SVSFICA          LEFTOVER/REMAINING FICA                      
         ICM   RE,15,TACWPFFI                                                   
         AR    RE,RF               UPDATE TAXES IN LAST CITY FOR STATE          
         STCM  RE,15,TACWPFFI                                                   
*                                                                               
PROTACWX J     YES                                                              
         DROP  R4,R5                                                            
SKPUNTS  DC    C'FD '                                                           
         DC    C'CN '                                                           
         DC    C'AB '                                                           
         DC    C'BC '                                                           
         DC    C'MB '                                                           
         DC    C'NB '                                                           
         DC    C'NL '                                                           
         DC    C'NS '                                                           
         DC    C'NT '                                                           
         DC    C'NU '                                                           
         DC    C'ON '                                                           
         DC    C'PE '                                                           
         DC    C'QC '                                                           
         DC    C'SK '                                                           
         DC    C'YT '                                                           
         DC    X'FF'                                                            
*                                                                               
* THIS ROUTINE TAKES A VALUE AND A PERCENTAGE AND CALCULTES THE NEW             
* PROTATED AMOUNT                                                               
*                                                                               
PRORATE  NTR1                                                                   
         XC    TGFULL,TGFULL                                                    
         ZAP   TMPPL16,=P'0'                                                    
*                                                                               
         L     RF,4(R1)            UNIT %                                       
         ICM   RE,15,0(RF)                                                      
         CVD   RE,DUB                                                           
*                                                                               
         L     RF,0(R1)            TAX AMOUNT                                   
         OC    0(4,RF),0(RF)                                                    
         BZ    YES                                                              
         ICM   RE,15,0(RF)                                                      
         CVD   RE,TMPPL16+8                                                     
*                                                                               
         MP    TMPPL16(16),DUB                                                  
         SRP   TMPPL16(16),64-6,5   ROUND TO HUNDREDTHS                         
         ZAP   DUB,TMPPL16+8(8)                                                 
         CVB   R1,DUB                                                           
         ST    R1,TGFULL                                                        
         J     YES                                                              
*                                                                               
SVFTAXES DS    0X                                                               
SVFTAX   DS    F                   FEDERAL TAXES                                
SVFICA   DS    F                   FEDERAL FICA                                 
SVFEARN  DS    D                   FEDERAL EARNINGS                             
SVFTAXLN EQU   *-SVFTAXES                                                       
*                                                                               
SVSEARN  DS    D                   STATE EARNINGS                               
*                                                                               
SVSTAXES DS    0X                                                               
SVSTAX   DS    F                   STATE TAXES                                  
SVSFICA  DS    F                   STATE FICA                                   
SVSSDI   DS    F                   STATE SDI                                    
SVSSUI   DS    F                   STATE SUI                                    
SVSFLI   DS    F                   STATE FLI                                    
SVSTAXLN EQU   *-SVSTAXES                                                       
*                                                                               
TMPPL16  DS    PL16                                                             
*                                                                               
PROTAB   DS    XL500                                                            
         LTORG                                                                  
*                                                                               
* PAYROLL PLUS CANADIAN TAXES                                                   
*                                                                               
PKCANTAX NTR1  BASE=*,LABEL=*                                                   
         MVI   MYBYTE,0                                                         
         XC    SVCTAX(SVCTAXL),SVCTAX                                           
*                                                                               
         MVC   AIO,AIO2                                                         
         GOTO1 RECVAL,DMCB,TLSYCDQ,(X'20',0)                                    
*                                                                               
* IF NO EVTIME2 SET UP AND NO TD1, THEN CALL ALL TAX FOR CAN TAXES              
*                                                                               
         L     R4,ASRTCHK                                                       
         MVI   ELCODE,TAATELQ                                                   
         BRAS  RE,GETEL                                                         
         JE    PKCT05              EVTIME2 SET UP - USE THAT                    
         L     R4,AW4IO                                                         
         MVI   ELCODE,TAD1ELQ                                                   
         BRAS  RE,GETEL                                                         
         JNE   PKCT07                                                           
*                                                                               
         BAS   RE,PKALLCAN         USE ALLTAX FOR CANADIAN TAXES                
         J     PKCT20                                                           
*                                                                               
PKCT05   MVC   AIO,ASRTCHK                                                      
         MVI   ELCODE,TAATELQ      TEST IF PROVINCE CHANGED SINCE PAY           
         MVC   TGTHREE,SRTW4CP                                                  
         MVI   TGTHREE+2,C' '                                                   
         GOTO1 GETL,DMCB,(3,TGTHREE)                                            
         JE    *+8                                                              
         MVI   MYBYTE,1            YES, PROVINCE CHANGED                        
*                                                                               
         USING TAATD,R4                                                         
PKCT07   L     R4,ASRTCHK                                                       
         MVI   ELCODE,TAATELQ                                                   
         BRAS  RE,GETEL                                                         
         J     *+8                                                              
PKCT10   BRAS  RE,NEXTEL                                                        
         JNE   PKCT20                                                           
*                                                                               
         CLC   TAATUNIT,=C'CN '                                                 
         JNE   *+14                                                             
         MVC   SVCFD,TAATTAX                                                    
         J     PKCT10                                                           
*                                                                               
         CLI   MYBYTE,0            PROVINCE CHANGED?                            
         JE    PKCT15                                                           
         MVC   TAATUNIT,SRTW4CP    UPDATE CHECK WITH CURRENT PROVINCE           
         MVI   TAATUNIT+2,C' '                                                  
*                                                                               
         CLC   TAATUNIT,=C'QC '                                                 
         JE    PKCT15                                                           
         CLC   SRTW4CP,=C'QC '     YES, WAS IT FORMERLY QUEBEC?                 
         JNE   PKCT15                                                           
         XC    TAATPIP,TAATPIP     YES, REMOVE PARENTIAL INSURANCE PLAN         
*                                                                               
PKCT15   MVC   SVCPR,TAATTAX                                                    
         MVC   SVCPP,TAATPP                                                     
         MVC   SVCEI,TAATEI                                                     
         MVC   SVCQPIP,TAATPIP                                                  
         J     PKCT10                                                           
*                                                                               
PKCT20   MVI   ELCODE,TATUELQ                                                   
         GOTO1 GETL,DMCB,(3,=C'CN ')   WORKED IN CANADA?                        
         JE    PKCT30                                                           
*                                                                               
         TM    TGSYSTAT,TASYSFLT   NEW FLAT TAX RULES?                          
         BZ    PKCT25                                                           
         TM    SRTBITS2,SRTFONTX   DON'T TAX FOREIGNER FED TAX?                 
         BO    PKCT30              YES, SKIP IT                                 
         MVI   ELCODE,TATUELQ                                                   
         GOTO1 GETL,DMCB,(3,=C'PR ')   WORKED IN PUERTO RICO?                   
         JNE   PKCT25                                                           
*                                                                               
         USING TATUD,RF                                                         
         L     RF,TGELEM           NO FED TAX ON ON PR WAGES                    
         ICM   RE,15,TATUWAAD      WAGES                                        
         ICM   R0,15,TATUTNAD      TAXABLE REIMBURSEMENTS                       
         AR    RE,R0                                                            
         L     RF,SVCNET                                                        
         SR    RF,RE                                                            
         B     PKCT27                                                           
         DROP  RF                                                               
*                                                                               
PKCT25   L     RF,SVCNET                                                        
PKCT27   MVC   FULL,=F'30000'      THEN US FEDERAL TAX FIRST (30%)              
         SR    RE,RE                                                            
         M     RE,FULL                                                          
         D     RE,=F'50000'        PERCENTAGE                                   
         LTR   RF,RF                                                            
         JM    *+8                                                              
         AH    RF,=H'1'            & ROUND IT                                   
         SRA   RF,1                                                             
         ST    RF,SVCFDTAX         SAVE FEDERAL TAX TAKEN                       
         L     RE,SRTNET                                                        
         SR    RE,RF                                                            
         ST    RE,SRTNET                                                        
         L     RE,SRTTDED                                                       
         AR    RE,RF                                                            
         ST    RE,SRTTDED                                                       
*                                                                               
PKCT30   L     RF,SRTNET                                                        
         OC    SVCTAX(SVCTAXL),SVCTAX   NO CANADIAN TAXES TO REMOVE             
         JZ    PKCT65                                                           
*                                                                               
         L     RF,SRTNET           SUBTRACT FROM NET                            
         OC    SRTNET,SRTNET       ANY MORE NET TO TAKE FROM?                   
         JNZ   PKCT35                                                           
         XC    SVCPP,SVCPP                                                      
         XC    SVCEI,SVCEI                                                      
         XC    SVCQPIP,SVCQPIP                                                  
         XC    SVCFD,SVCFD                                                      
         XC    SVCPR,SVCPR                                                      
         J     PKCT60                                                           
*                                                                               
PKCT35   ICM   RE,15,SVCPP         -PENSION PLAN                                
         SR    RF,RE                                                            
*                                                                               
         LTR   RF,RF                                                            
         BNM   PKCT40                                                           
         AR    RE,RF               OVERCHARGE                                   
         STCM  RE,15,SVCPP                                                      
         SR    RF,RF                                                            
         XC    SVCEI,SVCEI         NO OTHER TAXES TAKEN                         
         XC    SVCQPIP,SVCQPIP                                                  
         XC    SVCFD,SVCFD                                                      
         XC    SVCPR,SVCPR                                                      
         J     PKCT60                                                           
*                                                                               
PKCT40   ICM   RE,15,SVCEI         -EMPLOYER'S INSURANCE                        
         SR    RF,RE                                                            
*                                                                               
         LTR   RF,RF                                                            
         BNM   PKCT45                                                           
         AR    RE,RF               OVERCHARGE                                   
         STCM  RE,15,SVCEI                                                      
         SR    RF,RF                                                            
         XC    SVCQPIP,SVCQPIP     NO OTHER TAXES TAKEN                         
         XC    SVCFD,SVCFD                                                      
         XC    SVCPR,SVCPR                                                      
         J     PKCT60                                                           
*                                                                               
PKCT45   ICM   RE,15,SVCQPIP       -QUEBEC PARENTAL INSURANCE                   
         SR    RF,RE                                                            
*                                                                               
         LTR   RF,RF                                                            
         BNM   PKCT50                                                           
         AR    RE,RF               OVERCHARGE                                   
         STCM  RE,15,SVCQPIP                                                    
         SR    RF,RF                                                            
         XC    SVCFD,SVCFD         NO OTHER TAXES TAKEN                         
         XC    SVCPR,SVCPR                                                      
         J     PKCT60                                                           
*                                                                               
PKCT50   ICM   RE,15,SVCFD         -FEDERAL                                     
         SR    RF,RE                                                            
*                                                                               
         LTR   RF,RF                                                            
         BNM   PKCT55                                                           
         AR    RE,RF               OVERCHARGE                                   
         STCM  RE,15,SVCFD                                                      
         SR    RF,RF                                                            
         XC    SVCPR,SVCPR         NO OTHER TAXES TAKEN                         
         J     PKCT60                                                           
*                                                                               
PKCT55   ICM   RE,15,SVCPR         -PROVINCE                                    
         SR    RF,RE                                                            
*                                                                               
         LTR   RF,RF                                                            
         BNM   PKCT65                                                           
         AR    RE,RF               OVERCHARGE                                   
         STCM  RE,15,SVCPR                                                      
         SR    RF,RF                                                            
*                                                                               
PKCT60   OI    SRTBITS2,SRTPKCAN   NOT ALL CANADIAN TAXES TAKEN                 
PKCT65   ST    RF,SRTNET                                                        
*                                                                               
         L     RF,SVCFD                                                         
         A     RF,SVCPR                                                         
         A     RF,SVCPP                                                         
         A     RF,SVCEI                                                         
         A     RF,SVCQPIP                                                       
         ST    RF,SRTCTAX                                                       
*                                                                               
         TM    SVCFLAG,SVCALLTX    USED ALLTAX FOR CANADIAN TAXES?              
         JO    PKCT80                                                           
*                                                                               
         USING TAATD,R4                                                         
         L     R4,ASRTCHK          UPDATE TAAT ELEMS WITH TAXES                 
         MVI   ELCODE,TAATELQ      ACTUALLY REMOVED                             
         BRAS  RE,GETEL                                                         
         J     *+8                                                              
PKCT70   BRAS  RE,NEXTEL                                                        
         JNE   PKCT80                                                           
*                                                                               
         CLC   TAATUNIT,=C'CN '                                                 
         JNE   *+14                                                             
         MVC   TAATTAX,SVCFD                                                    
         J     PKCT70                                                           
*                                                                               
         MVC   TAATTAX,SVCPR                                                    
         MVC   TAATPP,SVCPP                                                     
         MVC   TAATEI,SVCEI                                                     
         MVC   TAATPIP,SVCQPIP                                                  
         J     PKCT70                                                           
*                                                                               
PKCT80   OC    SVCFDTAX,SVCFDTAX   ANY FEDERAL TAX TAKEN?                       
         JZ    PKCT90                                                           
*                                                                               
         USING TACWD,R5            YES, ADD FEDERAL TAXES WITHHELD              
         LA    R5,ELEM                                                          
         XC    ELEM,ELEM                                                        
         MVI   TACWEL,TACWELQ                                                   
         MVI   TACWLEN,TACWLN2Q                                                 
         MVC   TACWUNIT,=C'FD '                                                 
         MVC   TACWTAX,SVCFDTAX    SET FEDERAL TAXES                            
         OI    TACWSTAT,TACWSWRK   WORKING INDICATOR                            
*                                                                               
         MVC   AIO,ASRTCHK         ADD CW ELEMENT TO CHECK RECORD               
         GOTO1 ADDELEM                                                          
*                                                                               
         MVC   SRTFTAX,SVCFDTAX                                                 
*                                                                               
         LA    R4,ELEM             BUILD OTHER WITHHOLDING ELEMENT              
         USING TAODD,R4                                                         
         XC    ELEM,ELEM                                                        
         MVI   TAODEL,TAODELQ                                                   
         MVI   TAODLEN,TAODLNQ                                                  
         MVI   TAODTYPE,TAODTYPF                                                
         MVC   TAODAMT,SRTFTAX     SAVE TAX IN ELEMENT                          
*                                                                               
         MVC   AIO,ASRTCHK         ADD ELEMENT TO CHECK RECORD                  
         GOTO1 ADDELEM                                                          
*                                                                               
         TM    SVCFLAG,SVCALLTX    USED ALLTAX FOR CANADIAN TAXES?              
         JO    PKCT100                                                          
*                                                                               
         USING TAATD,R4                                                         
PKCT90   L     R4,ASRTCHK                                                       
         MVI   ELCODE,TAATELQ                                                   
         BRAS  RE,GETEL                                                         
         J     *+8                                                              
PKCT95   BRAS  RE,NEXTEL                                                        
         JNE   PKCT100                                                          
         CLI   TAATLEN,TAATLN2Q    OLD STYLE?                                   
         JL    PKCT95                                                           
*                                                                               
         MVC   TAATCCVT,CCVTRATE   CANADIAN CONVERSION RATE                     
         GOTO1 CONVCAD,DMCB,TAATTAX,TAATCTAX                                    
         GOTO1 CONVCAD,DMCB,TAATPP,TAATCPP                                      
         GOTO1 CONVCAD,DMCB,TAATEI,TAATCEI                                      
         GOTO1 CONVCAD,DMCB,TAATPIP,TAATCPIP                                    
         J     PKCT95                                                           
*                                                                               
PKCT100  TM    TGSYSTAT,TASYSFLT   NEW FLAT TAX RULES?                          
         BZ    *+8                                                              
         BRAS  RE,TAXCAPR          TAX CA OR PR                                 
*                                                                               
PKCANTX  J     XIT                                                              
         DROP  R4,R5                                                            
*                                                                               
* CONVERT USD TO CAD                                                            
*                                                                               
CONVCAD  NTR1                                                                   
         L     R2,0(R1)                                                         
         L     R3,4(R1)                                                         
*                                                                               
         ZAP   DUB,CCVTRATE                                                     
         CVB   R1,DUB                                                           
         ST    R1,FULL                                                          
*                                                                               
         ICM   R1,15,0(R2)                                                      
         XR    R0,R0                                                            
         M     R0,=F'10000'                                                     
         D     R0,FULL                                                          
*                                                                               
         L     RE,FULL                                                          
         AHI   RE,1                                                             
         SRA   RE,1                                                             
         CR    R0,RE                                                            
         BL    *+8                                                              
         AHI   R1,1                                                             
         STCM  R1,15,0(R3)                                                      
         J     XIT                                                              
*                                                                               
* CONVERT TO US DOLLARS                                                         
*        ON ENTRY, PARAM 1 = FIELD TO BE CONVERTED                              
*                  PARAM 2 = CONVERTED LOCATION                                 
*                                                                               
CONVUSD  NTR1                                                                   
         L     R2,0(R1)                                                         
         L     R3,4(R1)                                                         
*                                                                               
         ZAP   DUB,CCVTRATE                                                     
         CVB   R1,DUB                                                           
         ST    R1,FULL                                                          
*                                                                               
         ICM   R1,15,0(R2)                                                      
         TM    0(R2),X'80'         NEGATIVE? (VOID/CREDIT/CANCEL)               
         BZ    *+6                                                              
         LPR   R1,R1                                                            
*                                                                               
         XR    R0,R0                                                            
         M     R0,FULL                                                          
         D     R0,=F'5000'                                                      
*                                                                               
         LTR   R1,R1                                                            
         BM    *+8                                                              
         AH    R1,=H'1'                                                         
         SRA   R1,1                                                             
*                                                                               
         TM    0(R2),X'80'         NEGATIVE? (VOID/CREDIT/CANCEL)               
         BZ    *+6                                                              
         LNR   R1,R1                                                            
*                                                                               
         STCM  R1,15,0(R3)                                                      
         J     XIT                                                              
         LTORG                                                                  
*                                                                               
* CALL ALLTAX TO GET CANADIAN TAXES                                             
*                                                                               
PKALLCAN NTR1                                                                   
         OI    SVCFLAG,SVCALLTX    USE ALLTAX FOR CANADIAN TAXES                
         XC    0(TASELQ,R9),0(R9)                                               
*                                                                               
         MVC   TASECKDT,CHKDATEA   SAVE CHECK DATE IN TASEGUED                  
         TM    SRTINVS2,TAINSFRC   IF FORCING LAST DAY OF LAST YEAR             
         JZ    *+10                                                             
         MVC   TASECKDT,CHKDTEAL   SET DIFFERENT DATE                           
         XC    TASESTAT,TASESTAT                                                
         XC    TASEEXS,TASEEXS                                                  
         MVC   TASEEMP,=C'TP '          EMPLOYER                                
         MVI   TASERES,C'Y'                                                     
         MVI   TASEFREQ,C'S'       DEFAULT TO SEMI-MONTHLY                      
         TM    SRTBITS2,SRTTAXWK   WEEKLY?                                      
         JZ    *+8                                                              
         MVI   TASEFREQ,C'W'                                                    
*                                                                               
         L     R4,AW4IO                                                         
         MVI   ELCODE,TAD1ELQ                                                   
         BRAS  RE,GETEL                                                         
         ST    R4,ATD1EL           SET TGELEM TO FIRST TAD1 ELEMENT             
*                                                                               
         MVC   AIO,ASRTCHK                                                      
         USING TATUD,R4                                                         
         L     R4,ASRTCHK                                                       
         MVI   ELCODE,TATUELQ                                                   
         BRAS  RE,GETEL                                                         
         J     *+8                                                              
PKAC10   BRAS  RE,NEXTEL                                                        
         JNE   PKAC10                                                           
         CLC   TATUUNIT,=C'FD '                                                 
         JE    PKAC10                                                           
         CLC   TATUUNIT,=C'CN '                                                 
         JE    PKAC10                                                           
*                                  GET A(ESUTAB ENTRY) FOR THIS UNIT            
         GOTO1 GETESU,DMCB,TATUUNIT                                             
*        L     R5,0(R1)                                                         
         SAM31                                                                  
         L     RF,0(R1)                                                         
         LA    R5,BLOCK                                                         
         XC    BLOCK,BLOCK                                                      
         MVC   BLOCK(ESULEN),0(RF)                                              
         SAM24                                                                  
         USING ESUTABD,R5                                                       
*                                                                               
         MVC   TASEUNIT,=C'CN '                                                 
         MVC   TASEUNT2,SRTW4CP    CURRENT RESIDENT PROVINCE                    
         MVI   TASEUNT2+2,C' '                                                  
*                                                                               
         ICM   RE,15,ESUCEARN      ACCUMULATE EARNINGS AND                      
         ICM   RF,15,ESUCTXRE      TAXABLE REIMBURSEMENTS                       
         AR    RE,RF                                                            
         STCM  RE,15,TASEYTDE      MOVE YTD EARNINGS TO SEGUE BLOCK             
*                                                                               
*        ICM   RF,15,TATUWAGE      WAGES                                        
*        ICM   RE,15,TATUSTRE      TAXABLE NON WAGES                            
         ICM   RF,15,TATUWAAD      WAGES                                        
         ICM   RE,15,TATUTNAD      TAXABLE NON WAGES                            
         AR    RF,RE                                                            
         ST    RF,TGFULL                                                        
         GOTO1 CONVCAD,DMCB,TGFULL,TASEEARN    TAXABLE EARNINGS                 
*                                                                               
         MVC   TASEYPWG,TASEYTDE   YTD EARNINGS PP                              
         MVC   TASEYUWG,TASEYTDE   YTD EARNINGS EI                              
         MVC   TASEYQPW,TASEYTDE   YTD EARNINGS QPIP                            
         MVC   TASEYPTX,ESUCPP     YTD PP TAXES                                 
         MVC   TASEYUTX,ESUCEI     YTD EI TAXES                                 
         MVC   TASEYQPT,ESUCPIP    YTD PIP TAXES                                
*                                                                               
         USING TAD1D,R5                                                         
         L     R5,ATD1EL                                                        
PKAC30   CLI   0(R5),TAD1ELQ                                                    
         JNE   PKAC50                                                           
         TM    TAD1STAT,TAD1SCAN   CANADIAN FEDERAL?                            
         JZ    PKAC35                                                           
         MVC   SVTD1STF,TAD1STAT   SAVE FEDERAL STATUS                          
         TM    TAD1STAT,TAD1SEXM   EXEMPT?                                      
         JO    PKAC40                                                           
         MVC   SVCLAIMF,TAD1NCL1   NET CLAIM                                    
         MVC   SVPZONEF,TAD1PZON   PRESCRIBED ZONE                              
*                                                                               
         TM    TAD1STAT,TAD1SCC1   DEFAULT TO CLAIM CODE 1?                     
         JZ    PKAC40                                                           
         MVC   AIO,AIO2                                                         
         MVI   ELCODE,TAC1ELQ                                                   
         GOTO1 GETL,DMCB,(3,=C'CN ')                                            
         JNE   PKAC40                                                           
         L     RF,TGELEM                                                        
         USING TAC1D,RF                                                         
         MVC   SVCLAIMF,TAC1DCC1   SET DEFAULT CLAIM CODE                       
         MVC   TAD1NCL1,TAC1DCC1   UPDATE TD1 ELEM ON CHECK                     
         J     PKAC40                                                           
*                                                                               
PKAC35   MVC   SVTD1STP,TAD1STAT   SAVE PROVINCIAL STATUS                       
         TM    TAD1STAT,TAD1SPRO   PROVINCIAL?                                  
         JZ    PKAC40                                                           
         TM    TAD1STAT,TAD1SEXM   EXEMPT?                                      
         JO    PKAC40                                                           
         MVC   SVCLAIMP,TAD1NCL1   NET CLAIM                                    
         MVC   SVPZONEP,TAD1PZON   PRESCRIBED ZONE                              
*                                                                               
         TM    TAD1STAT,TAD1SCC1   DEFAULT TO CLAIM CODE 1?                     
         JZ    PKAC40                                                           
         MVC   AIO,AIO2                                                         
         MVI   ELCODE,TAC1ELQ                                                   
         GOTO1 GETL,DMCB,(3,TATUUNIT)                                           
         JNE   PKAC40                                                           
         L     RF,TGELEM                                                        
         USING TAC1D,RF                                                         
         MVC   SVCLAIMP,TAC1DCC1   SET DEFAULT CLAIM CODE                       
         MVC   TAD1NCL1,TAC1DCC1   UPDATE TD1 ELEM ON CHECK                     
         DROP  RF                                                               
*                                                                               
PKAC40   XC    ELEM,ELEM           SAVE TD1 INFO AT TIME OF CHECK               
         MVC   ELEM(TAD1LNQ),0(R5)                                              
         MVI   ELCODE,TAD1ELQ                                                   
         MVC   AIO,ASRTCHK                                                      
         GOTO1 ADDELEM                                                          
*                                                                               
         ZIC   RF,1(R5)                                                         
         AR    R5,RF                                                            
         J     PKAC30                                                           
         DROP  R5                                                               
*                                                                               
PKAC50   XC    TASETAX(76),TASETAX CLEAR TASEGUE RETURN AMOUNTS                 
*                                                                               
* GET CANADIAN FEDERAL TAX                                                      
*                                                                               
         MVC   TASENCL1,SVCLAIMF   NET CLAIM FEDERAL                            
         MVC   TASEPZON,SVPZONEF   PRESCRIBED ZONE FEDERAL                      
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'S',(R9)),TASELQ,=C'SEGUE INPUT'                  
*                                                                               
         GOTO1 TASEGUE,DMCB,(R9)   CALL TASEGUE AND PASS SEGUE BLOCK            
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'S',(R9)),TASELQ,=C'SEGUE OUTPUT'                 
*                                                                               
         TM    SVTD1STF,TAD1SEXM   EXEMPT FROM FEDERAL TAX?                     
         JO    *+10                                                             
         MVC   SVCCFD,TASETAX      SAVE CANADIAN FEDERAL TAX                    
*                                                                               
* GET PROVINCIAL TAXES                                                          
*                                                                               
         MVC   TASEUNIT,TASEUNT2                                                
         MVC   TASENCL1,SVCLAIMP   NET CLAIM PROVINCIAL                         
         MVC   TASEPZON,SVPZONEP   PRESCRIBED ZONE PROVINCIAL                   
*                                                                               
         XC    TASETAX(76),TASETAX CLEAR TASEGUE RETURN AMOUNTS                 
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'S',(R9)),TASELQ,=C'SEGUE INPUT'                  
*                                                                               
         GOTO1 TASEGUE,DMCB,(R9)   CALL TASEGUE AND PASS SEGUE BLOCK            
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'S',(R9)),TASELQ,=C'SEGUE OUTPUT'                 
*                                                                               
         TM    SVTD1STP,TAD1SEXM   EXEMPT FROM PROVINCIAL TAX?                  
         JO    *+10                                                             
         MVC   SVCCPR,TASETAX                                                   
*                                                                               
         TM    SVTD1STP,TAD1SECP   EXEMPT FROM CPP?                             
         JO    *+10                                                             
         MVC   SVCCPP,TASECPN                                                   
*                                                                               
         MVC   SVCCEI,TASEUIC                                                   
         MVC   SVCCQPIP,TASEQPIP                                                
*                                                                               
         XC    ELEM,ELEM           ADD DUMMY TAAT ELEMENTS                      
         LA    R5,ELEM                                                          
         USING TAATD,R5                                                         
         MVI   TAATEL,TAATELQ                                                   
         MVI   TAATLEN,TAATLN2Q                                                 
         MVC   TAATUNIT,=C'CN '                                                 
         MVC   TAATCTAX,SVCCFD                                                  
         MVC   TAATCPP,SVCCPP                                                   
         MVC   TAATCEI,SVCCEI                                                   
         MVC   TAATCPIP,SVCCQPIP                                                
         GOTO1 CONVUSD,DMCB,TAATCTAX,TAATTAX                                    
         GOTO1 CONVUSD,DMCB,TAATCPP,TAATPP                                      
         GOTO1 CONVUSD,DMCB,TAATCEI,TAATEI                                      
         GOTO1 CONVUSD,DMCB,TAATCPIP,TAATPIP                                    
         MVC   SVCFD,TAATTAX                                                    
         MVC   SVCPP,TAATPP                                                     
         MVC   SVCEI,TAATEI                                                     
         MVC   SVCQPIP,TAATPIP                                                  
*                                                                               
         GOTO1 ADDELEM                                                          
*                                                                               
         XC    ELEM,ELEM                                                        
         LA    R5,ELEM                                                          
         MVI   TAATEL,TAATELQ                                                   
         MVI   TAATLEN,TAATLN2Q                                                 
         MVC   TAATUNIT,TATUUNIT                                                
         MVC   TAATCTAX,SVCCPR                                                  
         MVC   TAATCPP,SVCCPP                                                   
         MVC   TAATCEI,SVCCEI                                                   
         MVC   TAATCPIP,SVCCQPIP                                                
         GOTO1 CONVUSD,DMCB,TAATCTAX,TAATTAX                                    
         MVC   TAATPP,SVCPP                                                     
         MVC   TAATEI,SVCEI                                                     
         MVC   TAATPIP,SVCQPIP                                                  
         MVC   SVCPR,TAATTAX                                                    
*                                                                               
         CLC   TAATUNIT,SRTW4CP    PROVINCE CHANGED?                            
         JE    PKAC60                                                           
         MVC   TAATUNIT,SRTW4CP    UPDATE CHECK WITH CURRENT PROVINCE           
         MVI   TAATUNIT+2,C' '                                                  
         CLC   TAATUNIT,=C'QC '                                                 
         JE    PKAC60                                                           
         CLC   SRTW4CP,=C'QC '     YES, WAS IT FORMERLY QUEBEC?                 
         JNE   PKAC60                                                           
         XC    SVCQPIP,SVCQPIP     YES, REMOVE PARENTIAL INSURANCE PLAN         
*                                                                               
PKAC60   GOTO1 ADDELEM                                                          
*                                                                               
PKACX    J     XIT                                                              
         DROP  R4,R5                                                            
*                                                                               
MYBYTE   DS    X                                                                
ATD1EL   DS    F                   ADDRESS OF FIRST TD1 ELEMENT                 
*                                                                               
SVCTAX   DS    0X                                                               
SVCFLAG  DS    XL1                 FLAGS                                        
SVCALLTX EQU   X'80'               USED ALLTAX TO CALCULATE TAXES               
*                                                                               
SVCFD    DS    F                   FEDERAL TAX                                  
SVCPP    DS    F                   PENSION PLAN                                 
SVCEI    DS    F                   EMPLOYER'S INSURANCE                         
SVCQPIP  DS    F                   PARENTIAL INSURANCE                          
SVCPR    DS    F                   PROVINCE                                     
SVCCFD   DS    F                   FEDERAL TAX CAD                              
SVCCPP   DS    F                   PENSION PLAN CAD                             
SVCCEI   DS    F                   EMPLOYER'S INSURANCE CAD                     
SVCCQPIP DS    F                   PARENTIAL INSURANCE CAD                      
SVCCPR   DS    F                   PROVINCE CAD                                 
SVCFDTAX DS    F                   FEDERAL TAX TAKEN                            
SVCLAIMF DS    F                   CLAIM 1                                      
SVCLAIMP DS    F                   CLAIM 2                                      
SVPZONEF DS    F                   PRESCRIBED ZONE 1                            
SVPZONEP DS    F                   PRESCRIBED ZONE 2                            
SVTD1STP DS    CL1                 PROVINCIAL STATUS                            
SVTD1STF DS    CL1                 FEDERAL STATUS                               
SVCTAXL  EQU   *-SVCTAX                                                         
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
* THIS ROUTINE HANDLES OTHER DEDUCTIONS (MPTRF AND PERMANENT                    
* CHARITIES).  IT LOCATES THESE ON THE W4 RECORD, AND ADDS TAOD                 
* ELEMENTS TO THE CHECK RECORD, IF NECESSARY.                                   
*                                                                               
ODWITH   NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         CLC   =C'D2',AGYALPH      IF NOT AGENCY DPS2, THEN DON'T               
         JNE   ODX                  WITHHOLD                                    
*                                                                               
         L     R4,AW4IO            R4 = A(FIRST OTHER WITHHOLDING ELEM)         
         MVI   ELCODE,TAOWELQ                                                   
         BRAS  RE,GETEL                                                         
         JNE   ODX                 DONE IF NO ELEMENTS                          
         USING TAOWD,R4                                                         
*                                                                               
OD10     L     RF,SRTGRS           COMPUTE DEDUCTION                            
         M     RE,TAOWFLAT                                                      
         D     RE,=F'5000'                                                      
         LTR   RF,RF                                                            
         BM    *+8                                                              
         AH    RF,=H'1'                                                         
         SRA   RF,1                                                             
*                                                                               
         L     RE,SRTNET           IF DEDUCTION > AVAILABLE BALANCE             
         S     RE,SRTMDED                                                       
         S     RE,SRTDUES                                                       
         CR    RF,RE                                                            
         JNH   *+6                                                              
         LR    RF,RE               THEN DEDUCTION = AVAILABLE BALANCE           
*                                                                               
         LTR   RF,RF               DON'T GO ANY FURTHER IF NOTHING              
         JZ    OD40                WITHHELD                                     
*                                                                               
         CLI   TAOWTYPE,TAOWTCHA   PUT DEDUCTION INTO SORT RECORD               
         JNE   OD20                                                             
         LA    R0,TAODTYPP                                                      
         ST    RF,SRTCHAR          IT'S PERMANENT CHARITIES                     
         J     OD30                                                             
*                                                                               
OD20     CLI   TAOWTYPE,TAOWTMPF   PUT DEDUCTION INTO SORT RECORD               
         JNE   OD40                                                             
         LA    R0,TAODTYPM         IT'S MPTRF                                   
         ST    RF,SRTMPT                                                        
*                                                                               
OD30     L     RE,SRTNET           SUBTRACT DEDUCTION FROM BALANCE              
         SR    RE,RF                                                            
         ST    RE,SRTNET                                                        
*                                                                               
         L     RE,SRTTDED          ADD TAX TO TOTAL DEDUCTIONS                  
         AR    RE,RF                                                            
         ST    RE,SRTTDED                                                       
*                                                                               
         LA    R5,ELEM             BUILD OTHER WITHHOLDING ELEMENT              
         USING TAODD,R5                                                         
         XC    ELEM,ELEM                                                        
         MVI   TAODEL,TAODELQ                                                   
         MVI   TAODLEN,TAODLNQ                                                  
         STC   R0,TAODTYPE                                                      
         ST    RF,TAODAMT          SAVE DEDUCTION IN ELEMENT                    
         DROP  R5                                                               
*                                                                               
         MVC   AIO,ASRTCHK         ADD ELEMENT TO CHECK RECORD                  
         GOTO1 ADDELEM                                                          
*                                                                               
OD40     BRAS  RE,NEXTEL           LOOK FOR MORE DEDUCTIONS                     
         JE    OD10                                                             
*                                                                               
         MVC   AIO,AIO1                                                         
*                                                                               
ODX      J     XIT                                                              
         DROP  R4                                                               
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
* GET SUI WITHHOLDINGS IN EVTIME ORDER                                          
*                                                                               
WITHSUI  NTR1  BASE=*,LABEL=*                                                   
         TM    TGSYSTA2,TASYSROI   RETAIN ORDER OF INPUT?                       
         JZ    WITHSUIX                                                         
*                                                                               
         BRAS  RE,GETTSUI          GET TAXABLE SUI                              
*                                                                               
         L     R3,ASUITAB                                                       
         USING SUITABD,R3                                                       
*                                                                               
WSUI10   CLI   0(R3),X'FF'                                                      
         JE    WITHSUIX                                                         
         TM    SUITSTAT,SUITWAGE+SUITTAX                                        
         JZ    WSUI30                                                           
         OC    SUITAMNT,SUITAMNT                                                
         JZ    WSUI30                                                           
*                                                                               
         CLC   SUITUNIT,=C'CN '                                                 
         JE    WSUI30                                                           
*                                                                               
         MVC   AIO,ASRTCHK                                                      
         MVI   ELCODE,TACWELQ                                                   
         GOTO1 GETL,DMCB,(3,SUITUNIT)                                           
         JNE   WSUI30                                                           
*                                                                               
         L     R4,TGELEM                                                        
         USING TACWD,R4                                                         
*                                                                               
         MVC   TASEUNIT,TACWUNIT   ELSE SAVE TAX UNIT IN TASEGUE BLOCK          
         MVC   TASESTAT,TACWMST              MARITAL STATUS                     
         MVC   TASEEXS,TACWEXS               EXEMPTIONS                         
                                                                                
         MVI   TASERES,C'N'        SAVE RESIDENCE INDICATOR (Y/N) IN            
         TM    TACWSTAT,TACWSRES       TASEGUE BLOCK                            
         BZ    WSUI20                                                           
         MVI   TASERES,C'Y'                                                     
         CLC   TASEUNIT,=C'PR '    PUERTO RICO RESIDENTS: SPECIAL RULE          
         BNE   WSUI20                                                           
         CLC   TASEEXS,=C'99'      99 EXEMPTIONS                                
         BNE   WSUI20                                                           
         MVI   TASESTAT,C'S'       SINGLE 1                                     
         MVC   TASEEXS,=C'01'                                                   
*                                                                               
WSUI20   ICM   RF,15,SUITYTDE                                                   
         ICM   RE,15,SUITAMNT                                                   
         SR    RF,RE                                                            
         STCM  RF,15,TASEYTDE      MOVE YTD EARNINGS TO SEGUE BLOCK             
*                                                                               
         MVC   TASEEARN,SUITAMNT   CURRENT TAXABLE EARNINGS                     
         MVC   TASEFREQ,SRTFREQ    FREQUENCY                                    
                                                                                
         XC    TASETAX(76),TASETAX CLEAR TASEGUE RETURN AMOUNTS                 
                                                                                
         GOTO1 DOTRACE,DMCB,(C'S',(R9)),TASELQ,=C'SEGUE INPUT'                  
                                                                                
         GOTO1 TASEGUE,DMCB,(R9)   CALL TASEGUE AND PASS SEGUE BLOCK            
                                                                                
         GOTO1 DOTRACE,DMCB,(C'S',(R9)),TASELQ,=C'SEGUE OUTPUT'                 
*                                                                               
         ICM   RF,15,TACWSUI       ACCUMULATE SUI TAXES                         
         ICM   RE,15,TASESUI                                                    
         AR    RF,RE                                                            
         STCM  RF,15,TACWSUI                                                    
*                                                                               
         L     RF,SRTNET           UPDATE NET                                   
         ICM   RE,15,TASESUI                                                    
         SR    RF,RE                                                            
         ST    RF,SRTNET                                                        
*                                                                               
         L     RE,SRTTDED          ADD TAXES TO TOTAL DEDUCTIONS                
         AR    RE,RF                                                            
         ST    RE,SRTTDED                                                       
*                                                                               
WSUI30   AHI   R3,SUITABLN                                                      
         J     WSUI10                                                           
*                                                                               
WITHSUIX J     XIT                                                              
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
GETTSUI  NTR1  BASE=*,LABEL=*                                                   
         L     R4,ASRTCHK                                                       
         USING TACDD,R4                                                         
         MVI   ELCODE,TACDELQ                                                   
         BRAS  RE,GETEL                                                         
         JNE   GETTSUIX                                                         
         ICM   RF,15,TACDEARN      TEST IF NEGATIVE CHECK                       
         LTR   RF,RF                                                            
         BM    GETTSUIX                                                         
*                                                                               
         USING PYTDTABD,R3                                                      
         LA    R3,BLOCK            BUILD ENTRY FOR THIS EMPLOYER/SSN            
         XC    0(PYTDLEN,R3),0(R3)                                              
         MVC   PYTDEMP,SRTEMP                                                   
         TM    SRTINVS2,TAINSFRC   IF FORCING DATE TO END OF LAST YEAR          
         BZ    *+8                                                              
         NI    PYTDEMP,X'DF'       TURN OFF X'40' BIT IN EMPLOYER               
         MVC   PYTDSSN,SRTSSN                                                   
*                                                                               
         L     R2,APYTDTAB         R2=A(BINSRCH PARAMETERS/TABLE)               
         USING BIND,R2                                                          
         MVC   DMCB+8(16),BININ    SET PARAMTERS TO BINSRCH                     
*                                                                               
         GOTO1 BINSRCH,DMCB,(R3),BINTABLE                                       
*                                                                               
         CLI   0(R1),1             MUST BE FOUND                                
         BE    GETTSUIX                                                         
*                                                                               
         L     R3,DMCB                                                          
         MVC   SUIYTD,PYTDAMTS+(TYCSUI-TYCYTD)                                  
         DROP  R2,R3                                                            
*                                                                               
         LA    R2,BLOCK            R4=A(TALIM INTERFACE BLOCK)                  
         USING TMDD,R2                                                          
         XCEF  (R2),'TMLNQ'        CLEAR INTERFACE TO TALIM                     
         MVC   TMEMP,SRTEMP        EMPLOYER                                     
         MVI   TMCURR,C'U'         IF CURRENCY IS US THEN TMCURR = U            
         MVC   TMSSN,SRTSSN        S/S NUMBER                                   
         MVC   TMW4TYPE,SRTW4TY    W4 TYPE                                      
         MVI   TMSTAT,TMSCHK       REQUESTING PAYROLL DATA                      
         GOTO1 DATCON,DMCB,(9,CHKDATEA),(0,TMEFDTE0)                            
         TM    SRTINVS2,TAINSFRC   IF FORCING DATE TO END OF LAST YEAR          
         BZ    GTSUI10                                                          
         GOTO1 DATCON,DMCB,(9,CHKDTEAL),(0,TMEFDTE0)                            
GTSUI10  ST    RC,TMRC             A(GEND)                                      
*                                                                               
         USING SUITABD,R3                                                       
         L     R3,ASUITAB                                                       
GTSUI20  CLI   0(R3),X'FF'                                                      
         JE    GETTSUIX                                                         
         TM    SUITSTAT,SUITWAGE+SUITTAX  ONLY USE TAXABLE AMOUNTS              
         JZ    GTSUI50                                                          
         MVC   TGTHREE,SUITUNIT                                                 
*                                                                               
         CLI   SUITUNIT+2,C' '     STATE?                                       
         BE    GTSUI30                                                          
         GOTO1 TAXVAL,DMCB,(3,SUITUNIT)                                         
         MVC   TGTHREE,TGTASTCY                                                 
*                                                                               
GTSUI30  MVC   TMUNIT,TGTHREE                                                   
         MVC   TMTXEARN,SUITYTDE   YTD EARNINGS                                 
*                                                                               
         GOTO1 =V(TALIM),DMCB,TMD    OFF TO TALIM                               
*                                                                               
* LOGIC FOR CALCULATING TAXABLE SUI:                                            
* MAXIMUM BASE SUI LIMIT - YTD TAXABLE SUI = X                                  
* IF X < 0, THEN TACYTSUI=0                                                     
*    ELSE TOTAL TAXABLE WAGES - X = Y                                           
* IF Y < 0, THEN TACYTSUI = TOTAL TAXABLE WAGES                                 
*    ELSE TACYTSUI = X                                                          
*                                                                               
         XC    SUITTSUI,SUITTSUI                                                
*                                                                               
         L     RF,TMBSUI           MAX BASE SUI LIMIT                           
         L     RE,SUIYTD           - YTD TAXABLE SUI                            
         SR    RF,RE               IF < 0 THEN NO TAXABLE SUI FOR THIS          
         BNP   GTSUI50             UNIT                                         
         ST    RF,TGFULL                                                        
*                                                                               
         MVC   AIO,ASRTCHK                                                      
         TM    SRTADJS,TAPDADIS    ISSUE?                                       
         BZ    *+12                                                             
         L     RF,SRTEARN                                                       
         B     GTSUI40                                                          
*                                                                               
         ICM   RF,15,SUITAMNT      TAXABLE WAGES                                
GTSUI40  STCM  RF,15,SUITTSUI      DEFAULT = TOTAL TAXABLE WAGES                
         L     RE,TGFULL                                                        
         SR    RF,RE                                                            
         BNP   *+10                                                             
         MVC   SUITTSUI,TGFULL                                                  
*                                                                               
         L     RF,SUIYTD           UPDATE YTD TAXABLE SUI FOR                   
         ICM   RE,15,SUITTSUI      CURRENT CHECK                                
         AR    RF,RE                                                            
         ST    RF,SUIYTD                                                        
*                                                                               
GTSUI50  AHI   R3,SUITABLN                                                      
         J     GTSUI20                                                          
         DROP  R3                                                               
*                                                                               
GETTSUIX J     XIT                                                              
*                                                                               
SUIYTD   DS    F                   YTD TAXABLE SUI                              
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
SVCNET   DS    F                   NET FOR P+ CANADIANS                         
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
       ++INCLUDE TASEGUED                                                       
         EJECT                                                                  
       ++INCLUDE TACKREPD                                                       
         EJECT                                                                  
       ++INCLUDE DDDLCB                                                         
         EJECT                                                                  
TMDD     DSECT                                                                  
       ++INCLUDE TALIMD                                                         
*DDSPOOLD                                                                       
*DDSPLWORKD                                                                     
*TAGENFILE                                                                      
*TASYSDSECT                                                                     
*TASYSEQUS                                                                      
*TAREPWORKD                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDSPOOLD                                                       
       ++INCLUDE DDSPLWORKD                                                     
       ++INCLUDE TAGENFILE                                                      
       ++INCLUDE TASYSDSECT                                                     
       ++INCLUDE TASYSEQUS                                                      
       ++INCLUDE TAREPWORKD                                                     
*                                                                               
PROTABD  DSECT                                                                  
PROTUNIT DS    XL3                                                              
PROTFPCT DS    XL4                                                              
PROTSPCT DS    XL4                                                              
PROTABLN EQU   *-PROTUNIT                                                       
*                                                                               
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'118TACKTAX   07/27/15'                                      
         END                                                                    
