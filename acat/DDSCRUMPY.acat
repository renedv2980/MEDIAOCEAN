*          DATA SET DDSCRUMPY  AT LEVEL 089 AS OF 04/18/01                      
*CATALP SCRUMPY                                                                 
         TITLE 'SCRUMPY - EXECUTE SCRIPT WORKER FILES'                          
         PRINT NOGEN                                                            
*&&      SET   NOP=N                                                            
         EJECT                                                                  
SCRUMPY  CSECT                                                                  
         NMOD1 WORKX-WORKD,**SCRY**,RA,RR=R3,CLEAR=YES                          
         USING WORKD,RC                                                         
         ST    R1,APARM                                                         
         ST    R3,RELO                                                          
         ST    RD,SAVERD                                                        
*                                                                               
         L     R9,0(R1)            P1=A(SYSFACS)                                
         USING COMFACSD,R9                                                      
         MVC   ABUFF1,4(R1)        P2=A(BUFFER1)                                
         MVC   ABUFF2,8(R1)        P2=A(BUFFER2)                                
         L     R1,12(R1)                                                        
         MVC   WRKKEY1,0(R1)       P4=WORKER KEY                                
*                                                                               
         L     R1,=A(IOAREA1-WORKD)                                             
         AR    R1,RC                                                            
         ST    R1,AIO1                                                          
         L     R1,=A(IOAREA2-WORKD)                                             
         AR    R1,RC                                                            
         ST    R1,AIO2                                                          
*                                                                               
         LA    R1,GET              SCRIPT EXIT ROUTINES                         
         ST    R1,AGETROUT                                                      
         LA    R1,PUT                                                           
         ST    R1,APUTROUT                                                      
*                                                                               
         GOTO1 ,WFCB,,WRKFIL,WRKKEY1,AIO1,ABUFF1                                
         GOTO1 ,WFC2,,WRKFIL,WRKKEY2,AIO2,ABUFF2                                
*                                                                               
         BAS   RE,MAIN                                                          
*                                                                               
         B     XMOD1                                                            
*                                                                               
XITEQ    CR    RB,RB               EXIT EQU                                     
         B     XIT1                                                             
XITNE    LTR   RB,RB               EXIT NEQ                                     
XIT1     XIT1                                                                   
*                                                                               
XMOD1    L     RD,SAVERD           BASE RD                                      
         L     R1,APARM            RESTORE R1                                   
         MVC   8(1,R1),RETURN      SET RETURN CODE                              
         XMOD1                                                                  
         EJECT                                                                  
************************************************************                    
*        MAIN PROGRAM                                      *                    
************************************************************                    
         SPACE 1                                                                
MAIN     NTR1                                                                   
         BAS   RE,GFILED                                                        
*                                                                               
MAIN010  BAS   RE,READREC          READ FIRST RECORD                            
         BAS   RE,SETRUN           SET STATUS TO RUNNING                        
         L     R2,AIO1                                                          
*                                                                               
         MVC   SCRHDR,0(R2)        SAVE HEADER REC                              
*                                                                               
         BAS   RE,VALHDR                                                        
*                                                                               
         CLI   SCRTYPE,C'N'        TEST FOR NEW FILE                            
         BNE   MAIN030                                                          
         MVI   FILE,2                                                           
         MVC   WRKKEY2+0(2),WRKKEY1      COPY USERID                            
         MVC   WRKKEY2+2(6),SCRFILE      NAME FROM HEADER                       
         BAS   RE,NEWFILE                                                       
         MVC   0(L'SCRHDR,R2),SCRHDR     SAVE HEADER REC                        
         BAS   RE,WRITEREC                                                      
*                                                                               
MAIN030  MVC   WORK,WRKKEY1                                                     
         BAS   RE,READREC          GET FIRST RECORD                             
         MVC   WRKKEY1,WORK                                                     
*                                                                               
         CLI   SCRTYPE,C'A'        TEST APPEND                                  
         BNE   *+16                                                             
         BAS   RE,COPYBUF                                                       
         BAS   RE,APPFILE                                                       
         B     MAIN080                                                          
*                                                                               
         CLI   SCRTYPE,C'O'        TEST OVERWRITE                               
         BNE   *+16                                                             
         BAS   RE,COPYBUF                                                       
         BAS   RE,REPFILE                                                       
         B     MAIN080                                                          
*                                                                               
         CLI   SCRTYPE,C'I'        TEST INSERT                                  
         BE    MAIN050                                                          
         CLI   SCRTYPE,C'S'        TEST SPECIAL INSERT                          
         BNE   ERR1                INVALID TYPE                                 
         MVI   SCRTYPE,C'I'        SET TYPE I                                   
         MVI   FLAG1,C'S'          WITH SPECIAL FLAG                            
*                                                                               
MAIN050  OC    BINILEN,BINILEN     DO WE HAVE OLD HEADER                        
         BNZ   MAIN080                                                          
*                                                                               
         L     R1,AIO1             CALCULATE INPUT LEN FROM 1ST RECORD          
         L     R1,0(R1)                                                         
         SRL   R1,16                                                            
         LH    R0,BINHLEN          SUB HLEN                                     
         SR    R1,R0                                                            
         LH    R0,BINOLEN          SUB OLEN                                     
         SR    R1,R0                                                            
         STH   R1,BINILEN          THE REST IS INPUT LEN                        
*                                                                               
MAIN080  BAS   RE,SCRIPT           CALL SCRIPT                                  
*                                                                               
         L     RF,APARM            TEST FOR SYSTEM DOWN                         
         CLC   16(2,RF),=H'1000'                                                
         BL    MAIN085                                                          
         CLC   16(2,RF),=H'1255'                                                
         BH    MAIN085                                                          
*                                                                               
         BAS   RE,SETACTV          SET BACK TO ACTIVE                           
         B     MAIN999                                                          
*                                                                               
MAIN085  CLI   FLAG1,C'S'          NO STATUS CHANGE ON S TYPE                   
         BE    MAIN999                                                          
         BAS   RE,SETPROC          SET STATUS TO PROCESSED                      
*                                                                               
MAIN999  B     XMOD1                                                            
         EJECT                                                                  
************************************************************                    
*        OPEN NEW WORKER FILE WRKKEY=KEY - REFNO RETURNED  *                    
*        OPEN WORKER FILE WRKKEY=REFNO   - WRKKEY RETURNED *                    
************************************************************                    
         SPACE 1                                                                
APPFILE  MVI   BYTE,C'A'           APPEND                                       
         B     OPENFILE                                                         
REPFILE  MVI   BYTE,C'R'           REPLACE                                      
         B     OPENFILE                                                         
NEWFILE  MVI   BYTE,C'N'           NEW                                          
*                                                                               
OPENFILE NTR1                                                                   
         L     R2,AIO1             BUILD HEADER IN IO1                          
         CLI   FILE,2                                                           
         BNE   *+8                                                              
         L     R2,AIO2             OR IO2 IF FILE=2                             
*                                                                               
         USING WLHDRD,R2                                                        
         XC    WLHEAD,WLHEAD       CLEAR TO ZEROS                               
         XC    WLINDEX,WLINDEX                                                  
         XC    WLBATTR,WLBATTR                                                  
         XC    WLDATA,WLDATA                                                    
*                                                                               
         MVC   WLSOFLAB,=C'*SOFSOF*'                                            
         MVC   WLINDEX,WRKKEY1     FILE=1 USE WRKKEY1                           
         CLI   FILE,2                                                           
         BNE   *+10                                                             
         MVC   WLINDEX,WRKKEY2     FILE=2 USE WRKKEY2                           
*                                                                               
         CLI   BYTE,C'N'           NEW FILE                                     
         BE    OPEN010                                                          
         OI    WLFLAG,WLFLREFN+WLFLRCOP                                         
         CLI   BYTE,C'R'           REPLACE FILE                                 
         BE    OPEN010                                                          
         OI    WLFLAG,WLFLMOD      ELSE APPEND FILE                             
*                                                                               
OPEN010  EQU   *                                                                
         LA    R1,WFCB             USE WFCB                                     
         CLI   FILE,2                                                           
         BNE   *+8                                                              
         LA    R1,WFC2             FILE=2 USE WFC2                              
         GOTO1 CDATAMGR,(R1),DMPRINT                                            
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                ERROR ON OPEN                                
         EDIT  (B2,WLREPRNO),(5,SCRREFN),FILL=0                                 
*                                                                               
         CLI   FILE,1                                                           
         BNE   *+10                                                             
         MVC   WRKKEY1+UKFILENO-UKRECD(2),WLREPRNO                              
         CLI   FILE,2                                                           
         BNE   *+10                                                             
         MVC   WRKKEY2+UKFILENO-UKRECD(2),WLREPRNO                              
         B     XIT1                                                             
         DROP  R2                                                               
         EJECT                                                                  
************************************************************                    
*        FIND FILE BY NAME OR NUMBER                       *                    
************************************************************                    
         SPACE 1                                                                
         USING UKRECD,WRKKEY1                                                   
GFILEN   MVI   BYTE,C'N'           NAME SEARCH                                  
         B     GETFILE                                                          
GFILED   MVI   BYTE,C'D'           DIRECT LOCATE                                
*                                                                               
GETFILE  NTR1                                                                   
         MVC   WRKFIL,WRKF1                                                     
         GOTO1 CDATAMGR,WFCB,GFILE                                              
         MVC   WRKFIL,UKUSRINF     GET THE WORKER FILE FOR THIS ID              
*                                                                               
         CLI   BYTE,C'D'           TEST DIRECT LOCATE                           
         BNE   *+8                                                              
         OI    UKFLAG,X'80'                                                     
         GOTO1 CDATAMGR,WFCB,INDEX                                              
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                ERROR ON INDEX READ                          
         B     XIT1                                                             
         EJECT                                                                  
************************************************************                    
*        READ HEADER RECORD                                *                    
************************************************************                    
         SPACE 1                                                                
READHDR  NTR1                                                                   
         L     R1,AIO1                                                          
         XC    0(12,R1),0(R1)                                                   
         MVC   4(4,R1),=C'REC '                                                 
         GOTO1 CDATAMGR,WFCB,RANDOM                                             
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                RANDOM ERROR                                 
         B     XIT1                                                             
         SPACE 2                                                                
************************************************************                    
*        READ NEXT RECORD                                  *                    
************************************************************                    
         SPACE 1                                                                
READREC  NTR1                                                                   
         GOTO1 CDATAMGR,WFCB,READ                                               
         CLI   8(R1),0                                                          
         BE    XITEQ                                                            
         CLI   8(R1),X'90'         TEST EOF                                     
         BE    XITNE                                                            
         DC    H'0'                READ ERROR                                   
         SPACE 2                                                                
************************************************************                    
*        SET STATUS TO RUNNING                             *                    
************************************************************                    
         SPACE 1                                                                
SETRUN   NTR1                                                                   
         GOTO1 CDATAMGR,WFCB,RUNNING                                            
         CLI   8(R1),0                                                          
         BE    XITEQ                                                            
         DC    H'0'                ERROR                                        
         SPACE 2                                                                
************************************************************                    
*        SET STATUS TO ACTIVE                              *                    
************************************************************                    
         SPACE 1                                                                
SETACTV  NTR1                                                                   
         GOTO1 CDATAMGR,WFCB,ACTV                                               
         CLI   8(R1),0                                                          
         BE    XITEQ                                                            
         DC    H'0'                ERROR                                        
         SPACE 2                                                                
************************************************************                    
*        SET STATUS TO PROCESSED                           *                    
************************************************************                    
         SPACE 1                                                                
SETPROC  NTR1                                                                   
         GOTO1 CDATAMGR,WFCB,PROC                                               
         CLI   8(R1),0                                                          
         BE    XITEQ                                                            
         DC    H'0'                ERROR                                        
         SPACE 2                                                                
************************************************************                    
*        WRITE THIS RECORD                                 *                    
************************************************************                    
         SPACE 1                                                                
WRITEREC NTR1                                                                   
         GOTO1 CDATAMGR,WFCB,WRITE                                              
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                WRITE ERROR                                  
         B     XIT1                                                             
         SPACE 2                                                                
************************************************************                    
*        PUT A RECORD                                      *                    
************************************************************                    
         SPACE 1                                                                
PUTREC   NTR1                                                                   
         LA    R1,WFCB                                                          
         CLI   FILE,2                                                           
         BNE   *+8                                                              
         LA    R1,WFC2                                                          
         GOTO1 CDATAMGR,(R1),DMPRINT                                            
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                ERROR ON PUT                                 
         B     XIT1                                                             
         EJECT                                                                  
************************************************************                    
*        PUT EOF RECORD TO CLOSE                           *                    
************************************************************                    
         SPACE 1                                                                
PUTEOF   NTR1                                                                   
         L     R1,AIO1                                                          
         CLI   FILE,2                                                           
         BNE   *+8                                                              
         L     R1,AIO2                                                          
         MVC   0(8,R1),=C'*EOFEOF*'                                             
         LA    R1,WFCB                                                          
         CLI   FILE,2                                                           
         BNE   *+8                                                              
         LA    R1,WFC2                                                          
         GOTO1 CDATAMGR,(R1),DMPRINT                                            
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                ERROR ON CLOSE                               
         B     XIT1                                                             
         SPACE 2                                                                
************************************************************                    
*        COPY BUFF1 TO BUFF2                               *                    
************************************************************                    
         SPACE 1                                                                
COPYBUF  NTR1                                                                   
         L     RE,AIO1                                                          
         L     R0,AIO2                                                          
         LH    R1,=H'4096'                                                      
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
         B     XIT1                                                             
         EJECT                                                                  
************************************************************                    
*        BUILD SCRIPT PARAMETER LIST                       *                    
************************************************************                    
         SPACE 1                                                                
SCRIPT   NTR1                                                                   
         LA    R2,SCRIPTPL                                                      
         USING SCPLD,R2                                                         
*                                                                               
         LA    R1,SCRNAME          SCRIPT NAME                                  
         ST    R1,SCPLAS                                                        
         OI    SCPLINDS,SCPLCTF+SCPLUTB+SCPLBALL                                
*                                                                               
         L     R1,AIO1                                                          
         LA    R1,8(R1)                                                         
         CLI   SCRTYPE,C'I'        INSERT                                       
         BNE   *+12                                                             
         AH    R1,BINHLEN                                                       
         AH    R1,BINOLEN                                                       
         ST    R1,SCPLAID          INPUT BUFFER                                 
*                                                                               
         MVC   SCPLLIX,BINILEN     INPUT LENGTH                                 
         MVC   SCPLAIR,AGETROUT                                                 
         LA    R1,PARMS                                                         
         ST    R1,SCPLAIP                                                       
*                                                                               
         CLI   SCRTYPE,C'I'        INSERT                                       
         BNE   *+24                                                             
         L     R1,AIO1             OUTPUT BUFFER                                
         LA    R1,8(R1)                                                         
         AH    R1,BINHLEN                                                       
         ST    R1,SCPLAOD                                                       
         B     SCRIP040                                                         
*                                                                               
         CLI   SCRTYPE,C'N'        NEW                                          
         BNE   *+14                                                             
         MVC   SCPLAOD,AIO2        OUTPUT BUFFER                                
         B     SCRIP040                                                         
*                                                                               
         L     R1,AIO1                                                          
         SR    RF,RF                                                            
         ICM   RF,3,BINOLEN                                                     
         LA    RF,8(RF)                                                         
         STCM  RF,3,0(R1)                                                       
         LA    R1,4(R1)                                                         
         MVC   0(4,R1),=A(ITSCDATA)                                             
         LA    R1,4(R1)                                                         
         ST    R1,SCPLAOD          OUTPUT BUFFER                                
         L     R1,AIO2                                                          
         LA    R1,8(R1)                                                         
         ST    R1,SCPLAID          INPUT IN BUFF2                               
*                                                                               
SCRIP040 MVC   SCPLLOX,BINOLEN     OUTPUT LENGTH                                
         MVC   SCPLAOR,APUTROUT                                                 
         LA    R1,PARMS                                                         
         ST    R1,SCPLAOP                                                       
*                                                                               
         L     RF,SCPLAOD          CLEAR BUFFER                                 
         SR    R1,R1                                                            
         ICM   R1,3,BINOLEN                                                     
         SH    R1,=H'2'                                                         
         MVI   0(RF),C' '                                                       
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   1(0,RF),0(RF)                                                    
*                                                                               
         LA    R1,ERRBUFF          SET UP ERROR BUFFER                          
         ST    R1,SCPLAEB                                                       
         LH    R1,=Y(MAXERR*60)                                                 
         STH   R1,SCPLLEB                                                       
*                                                                               
         LA    R1,PARMS            SET RC+R9 IN PARMS                           
         ST    RC,4(R1)                                                         
         ST    R9,8(R1)                                                         
*                                                                               
         GOTO1 CSCRIPT,SCRIPTPL    CALL SCRIPT                                  
         L     RF,APARM                                                         
         MVC   16(4,RF),4(R1)      RETURN SCRIPT CODE IN P5                     
*                                                                               
         CLI   SCRERR,C'Y'                                                      
         BNE   SCRIP090                                                         
*                                                                               
         CLI   SCRTYPE,C'I'        INSERT MODE                                  
         BNE   *+8                                                              
         BAS   RE,APPFILE          OPEN FILE TO APPEND ERRORS                   
*                                                                               
         LA    R0,MAXERR           SPOOL OUT ERRORS                             
         LA    R2,ERRBUFF                                                       
SCRIP080 CLI   0(R2),0                                                          
         BE    SCRIP089                                                         
         L     R1,AIO1                                                          
         MVC   0(4,R1),=X'00400000'                                             
         MVC   4(4,R1),=A(ITSCRERR)                                             
         MVC   8(60,R1),0(R2)                                                   
         BAS   RE,PUTREC                                                        
SCRIP089 LA    R2,60(R2)                                                        
         BCT   R0,SCRIP080                                                      
         BAS   RE,PUTEOF           CLOSE OUT FILE                               
         B     XIT1                                                             
*                                                                               
SCRIP090 CLI   SCRTYPE,C'I'        TEST FOR INSERT                              
         BE    XIT1                                                             
*                                                                               
         BAS   RE,PUTEOF           CLOSE OUT FILE                               
*                                                                               
         B     XIT1                                                             
         EJECT                                                                  
*************************************************************                   
*        VALIDATE NUMBER ROUTINE                            *                   
*************************************************************                   
         SPACE 1                                                                
       ++INCLUDE DDVALNUM                                                       
         EJECT                                                                  
*************************************************************                   
*        VALIDATE SCRIPT HEADER                             *                   
*************************************************************                   
         SPACE 1                                                                
VALHDR   NTR1                                                                   
         CLC   SCRMAD,=A(ITSCRHDR)                                              
         BE    VALH010                                                          
         CLC   SCRMAD,=A(ITSCRIPT)                                              
         BE    VALH050                                                          
         B     ERR1                                                             
*                                                                               
VALH010  CLC   SCRIPID,=C'SCRIPT'  CHECK FOR SCRIPT HEADER                      
         BNE   ERR1                                                             
         LA    R3,5                                                             
         LA    R4,SCRILEN          VALIDATE INPUT LEN                           
         BAS   RE,VALNUM                                                        
         CLI   DUB,X'FF'                                                        
         BE    ERR1                                                             
         CVB   R1,DUB                                                           
         STH   R1,BINILEN                                                       
         LA    R3,5                                                             
         LA    R4,SCROLEN          VALIATE OUTPUT LEN                           
         BAS   RE,VALNUM                                                        
         CLI   DUB,X'FF'                                                        
         BE    ERR1                                                             
         CVB   R1,DUB                                                           
         STH   R1,BINOLEN                                                       
         LA    R3,5                                                             
         LA    R4,SCRHLEN          VALIDATE HEADER LEN                          
         BAS   RE,VALNUM                                                        
         CLI   DUB,X'FF'                                                        
         BE    ERR1                                                             
         CVB   R1,DUB                                                           
         STH   R1,BINHLEN                                                       
         B     XIT1                                                             
*                                                                               
VALH050  MVI   SCRTYPE,C'I'                                                     
         LA    R3,2                                                             
         LA    R4,SCRHL            VALIDATE HEADER LEN                          
         BAS   RE,VALNUM                                                        
         CLI   DUB,X'FF'                                                        
         BE    ERR1                                                             
         CVB   R1,DUB                                                           
         STH   R1,BINHLEN                                                       
         LA    R3,2                                                             
         LA    R4,SCROL            VALIATE OUTPUT LEN                           
         BAS   RE,VALNUM                                                        
         CLI   DUB,X'FF'                                                        
         BE    ERR1                                                             
         CVB   R1,DUB                                                           
         STH   R1,BINOLEN                                                       
         MVC   DUB,SCRNAME1                                                     
         OC    DUB,SPACES                                                       
         MVC   SCRNAME,DUB                                                      
         B     XIT1                                                             
         EJECT                                                                  
*************************************************************                   
*        ERROR EXITS                                        *                   
*************************************************************                   
         SPACE 1                                                                
ERR1     MVI   RETURN,X'10'        INVALID SCRIPT HEADER                        
         B     XMOD1                                                            
ERR2     MVC   RETURN,8(R1)        WRKF ERROR                                   
         B     XMOD1                                                            
         EJECT                                                                  
*************************************************************                   
*        CONSTANTS & LTORG                                  *                   
*************************************************************                   
         SPACE 1                                                                
         LTORG                                                                  
*                                                                               
SPACES   DC    CL60' '                                                          
DMREAD   DC    CL7'DMREAD'                                                      
CTFILE   DC    CL7'CTFILE'                                                      
TEMPSTR  DC    CL7'TEMPSTR'                                                     
READ     DC    CL8'READ'                                                        
WRITE    DC    CL8'WRITE'                                                       
ADD      DC    CL8'ADD'                                                         
RANDOM   DC    CL8'RANDOM'                                                      
DMPRINT  DC    CL8'DMPRINT'                                                     
WRKF1    DC    CL8'WRKF1'                                                       
WRKFILE  DC    CL8'WRKFILE'                                                     
GFILE    DC    CL8'GFILE'                                                       
INDEX    DC    CL8'INDEX'                                                       
RUNNING  DC    CL8'RUNNING'                                                     
ACTV     DC    CL8'ACTI'                                                        
PROC     DC    CL8'PROC'                                                        
*                                                                               
         DROP  RA,RB                                                            
         EJECT                                                                  
*************************************************************                   
*        GET NEXT SCRIPT INPUT BUFFER                       *                   
*************************************************************                   
         DS    0H                                                               
         USING *,RB                                                             
GET      NTR1  BASE=(RF)                                                        
         L     RC,4(R1)            GET BACK MY REGISTERS                        
         L     R9,8(R1)                                                         
         LR    R3,R1                                                            
         MVI   0(R3),0                                                          
*                                                                               
GET010   EQU   *                                                                
*                                  CLEAR INPUT AREA TO SPACES                   
         L     RE,AIO1                                                          
         ICM   RF,15,=AL4(4096)                                                 
         LA    R0,*                                                             
         SR    R1,R1                                                            
         ICM   R1,8,=C' '                                                       
         MVCL  RE,R0                                                            
*                                                                               
GET020   GOTO1 CDATAMGR,WFCB,=C'READ'                                           
         CLI   8(R1),0                                                          
         BNE   GET030                                                           
         L     RE,AIO1             IGNORE NON DATA RECORDS                      
         CLC   4(4,RE),=A(ITSCDATA)                                             
         BE    GETXIT                                                           
         B     GET020                                                           
*                                                                               
GET030   CLI   8(R1),X'90'         TEST EOF                                     
         BE    *+6                                                              
         DC    H'0'                READ ERROR                                   
*                                                                               
         MVI   0(R3),X'80'         SET EOF                                      
GETXIT   XIT1                                                                   
         LTORG                                                                  
         DROP  RB                                                               
         EJECT                                                                  
*************************************************************                   
*        PUT NEXT SCRIPT OUTPUT BUFFER                      *                   
*************************************************************                   
         DS    0H                                                               
         USING *,RB                                                             
PUT      NTR1  BASE=(RF)                                                        
         L     RC,4(R1)            GET BACK MY REGISTERS                        
         L     R9,8(R1)                                                         
         LR    R3,R1                                                            
*                                                                               
         CLI   SCRTYPE,C'I'        TEST FOR INSERT                              
         BNE   PUT010                                                           
*                                                                               
         GOTO1 CDATAMGR,WFCB,=C'WRITE' WRITE THIS RECORD                        
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                WRITE ERROR                                  
         B     PUTXIT                                                           
*                                                                               
PUT010   LA    R1,WFCB             PUT A RECORD                                 
         CLI   FILE,2                                                           
         BNE   *+8                                                              
         LA    R1,WFC2                                                          
         GOTO1 CDATAMGR,(R1),=C'DMPRINT'                                        
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                ERROR ON PUT                                 
*                                                                               
         L     RF,12(R1)           CLEAR BUFFER                                 
         SR    R1,R1                                                            
         ICM   R1,3,0(RF)                                                       
         LA    RF,8(RF)                                                         
         SH    R1,=H'8'                                                         
         MVI   0(RF),C' '                                                       
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   1(0,RF),0(RF)                                                    
*                                                                               
PUTXIT   XIT1                                                                   
         LTORG                                                                  
         DROP  RB                                                               
         EJECT                                                                  
*************************************************************                   
*        WORKING STORAGE DSECT                              *                   
*************************************************************                   
         SPACE 1                                                                
WORKD    DSECT                                                                  
RELO     DS    F                                                                
SAVERD   DS    F                                                                
APARM    DS    F                                                                
*                                                                               
DUB      DS    D                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    X                                                                
RETURN   DS    X                                                                
FLAG1    DS    X                                                                
DMCB     DS    6F                                                               
WFCB     DS    6F                                                               
WFC2     DS    6F                                                               
PARMS    DS    6F                                                               
WORK     DS    XL64                                                             
*                                                                               
ABUFF1   DS    A                   WRKF BUFFERS                                 
ABUFF2   DS    A                                                                
AIO1     DS    A                   IO AREAS                                     
AIO2     DS    A                                                                
AGETROUT DS    A                                                                
APUTROUT DS    A                                                                
*                                                                               
SCRIPTPL DS    12F                 FASCRIPT PARAMETER LIST                      
*                                                                               
WRKFIL   DS    CL8                                                              
WRKKEY1  DS    CL34                                                             
WRKKEY2  DS    CL34                                                             
*                                                                               
FILE     DS    X                   SET TO 1 OR 2 FOR WRKF ACTIONS               
*                                                                               
SCRHDR   DS    0XL50               WORK FILE HEADER                             
SCRLEN   DS    XL4                 RECORD LEN                                   
SCRMAD   DS    XL4                 MAD OBJECT                                   
SCRIPID  DS    CL6                 SCRIPT                                       
SCRNAME  DS    CL8                 SCRIPT NAME                                  
SCRTYPE  DS    CL1                 OUT TYPE A/I/O/N                             
SCRFILE  DS    CL6                 OUT FILE NAME                                
SCRREFN  DS    CL5                 OUT FILE NUMBER (RETURNED)                   
SCRHLEN  DS    CL5                 HEADER LEN                                   
SCRILEN  DS    CL5                 INPUT LEN                                    
SCROLEN  DS    CL5                 OUTPUT LEN                                   
SCRERR   DS    CL1                 ERROR FLAG Y/N                               
*                                                                               
         ORG   SCRIPID                                                          
SCRHL    DS    CL2                 SCRIPT HEADER                                
SCROL    DS    CL2                                                              
SCRNAME1 DS    CL8                                                              
         ORG                                                                    
*                                                                               
BINHLEN  DS    H                   HEADER LEN BINARY                            
BINILEN  DS    H                   INPUT LEN BINARY                             
BINOLEN  DS    H                   OUTPUT LEN BINARY                            
*                                                                               
*&&US                                                                           
MAXERR   EQU   15                                                               
*&&                                                                             
*&&UK                                                                           
MAXERR   EQU   90                                                               
*&&                                                                             
ERRBUFF  DS    (MAXERR)CL60             WAS 10CL60                              
IOAREA1  DS    4096C                                                            
IOAREA2  DS    4096C                                                            
*                                                                               
WORKX    EQU   *                                                                
         EJECT                                                                  
*************************************************************                   
*        OTHER DSECTS                                       *                   
*************************************************************                   
         SPACE 1                                                                
* DDCOMFACS                                                                     
* FASCRIPTD                                                                     
* FADSECTS                                                                      
* DMWRKFK                                                                       
* DMWRKFD                                                                       
* DMWRKFL                                                                       
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
       ++INCLUDE FASCRIPTD                                                      
       ++INCLUDE FADSECTS                                                       
       ++INCLUDE DMWRKFK                                                        
       ++INCLUDE DMWRKFD                                                        
       ++INCLUDE DMWRKFL                                                        
       ++INCLUDE CTMADEQUS                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'089DDSCRUMPY 04/18/01'                                      
         END                                                                    
