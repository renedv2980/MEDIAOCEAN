*          DATA SET DDBINSRCH  AT LEVEL 002 AS OF 04/24/02                      
*CATALP BINSRCH2                                                                
         SPACE 1                                                   *            
*==================================================================*            
*                                                                  *            
*   PARAMETER 1  BYTE 0    X'01'=INSERT IF NOT FOUND               *            
*                BYTE 1-3  A(RECORD TO BE ADDED OR FOUND)          *            
*                                                                  *            
*   PARAMETER 2            A(TABLE)                                *            
*                                                                  *            
*   PARAMETER 3            NUMBER OF RECORDS IN TABLE SO FAR.      *            
*                          THIS WILL BE UPDATED BY BINSRCH.        *            
*                                                                  *            
*   PARAMETER 4            L'RECORD                                *            
*                                                                  *            
*   PARAMETER 5  BYTE 0    DISPLACEMENT OF KEY INTO RECORD         *            
*                BYTE 1-3  L'KEY (MAX=255)                         *            
*                                                                  *            
*   PARAMETER 6            MAXIMUM NUMBER OF RECORDS IN TABLE      *            
*                                                                  *            
*   AFTER BINSRCH                                                  *            
*   -------------                                                  *            
*                                                                  *            
*   PARAMETER 1  BYTE 0    X'01' = RECORD NOT FOUND                *            
*                BYTE 1-3  A(RECORD FOUND) OR                      *            
*                          A(WHERE RECORD HAS BEEN INSERTED) OR    *            
*                          A(0) WHEN TABLE IS FULL                 *            
*==================================================================*            
         TITLE 'BINARY SEARCH AND SEQUENTIAL ADD ROUTINE'                       
BINSRCH  CSECT                                                                  
         NMOD1 WRKX-WRKD,BINSRCH                                                
         USING WRKD,RC                                                          
         LA    RE,*+6              SET 24-BIT MODE                              
         BSM   0,RE                                                             
         LR    R9,R1                                                            
         USING BSPARA,R9                                                        
         LM    R2,R5,BSPSTRT       SET REGISTERS FROM PARAM LIST                
         LA    R5,0(R5)                                                         
         SR    R6,R6                                                            
         IC    R6,BSPKEYD                                                       
         L     R7,BSPEND                                                        
         L     R8,BSPAREC                                                       
         LA    R2,0(R2)                                                         
         BCTR  R5,0                                                             
         MVI   SW,0                                                             
         MVC   SVACT,BSPADD                                                     
         MVC   SVADDR,BSPSTRT      SAVE TABLE ADDRESS                           
         MVI   SVADDR,0            ZERO HIGH ORDER BYTES                        
         LTR   R3,R3               ANY DATA IN TABLE                            
         BNZ   BS1                  YES - SEARCH FOR EQUAL                      
         CLI   BSPADD,1            ADD RECORD                                   
         BE    AFRST                YES - ADD TO TABLE                          
         XC    BSPAREC,BSPAREC                                                  
         MVI   BSPNF,1                                                          
         B     BSEXIT                                                           
AFRST    LA    R3,1(R3)            BUMP RECORD COUNTER                          
         ST    R3,BSPNOR                                                        
         BCTR  R4,0                SET INSERT LENGTH                            
         ST    R2,BSPAREC                                                       
         MVI   BSPNF,1                                                          
AFRST1   CH    R4,=H'256'                                                       
         BL    AFRST2                                                           
         MVC   0(255,R2),0(R8)                                                  
         LA    R2,255(R2)                                                       
         LA    R8,255(R8)                                                       
         SH    R4,=H'255'                                                       
         B     AFRST1                                                           
AFRST2   LTR   R4,R4                                                            
         BZ    BSEXIT                                                           
         EX    R4,*+8                                                           
         B     BSEXIT                                                           
         MVC   0(0,R2),0(R8)                                                    
*                                                                               
BS1      LR    R1,R3               CALCULATE END OF TABLE                       
         BCTR  R1,0                                                             
         MH    R1,BSPLENR+2                                                     
         AR    R1,R2                                                            
         LA    RF,0(R6,R8)         CALCULATE KEY DISPLACEMENTS                  
         LA    RA,0(R6,R2)                                                      
         LR    R0,R4               INCREMENT VALUE                              
         CH    R3,=H'10'           LT. 10 ENTRIES - LINEAR SEARCH               
         BH    BSS1                                                             
         AR    R1,R6               BUMP BY KEY DISPLACEMENT                     
BS2      EX    R5,*+8                                                           
         B     *+10                                                             
         CLC   0(0,RA),0(RF)                                                    
         BNE   BS3                                                              
         SR    R1,R6               MINUS KEY DISPLACEMENT                       
         LR    R2,RA                                                            
         SR    R2,R6                                                            
         B     SETOUTA                                                          
BS3      BH    *+8                                                              
         BXLE  RA,R0,BS2                                                        
         SR    R1,R6               MINUS KEY DISPLACEMENT                       
         LR    R2,RA                                                            
         SR    R2,R6                                                            
         B     BS4                                                              
*                                                                               
BSS1     AR    R1,R4               BINARY SEARCH ROUTINE                        
         BCTR  R1,R0                                                            
         LR    R0,R4                                                            
         SR    R2,R0                                                            
         SR    R1,R2                                                            
*********BXLE  0,0,*               IDF CAN'T SINGLE-STEP THROUGH THIS!          
         AR    R0,R0                                                            
         CR    R0,R1                                                            
         BNH   *-4                                                              
         AR    R1,R2                                                            
         BASR  RE,0                                                             
         SRL   R0,1                                                             
         CR    R0,R4                                                            
         BL    BSS3                                                             
         BXH   R2,R0,BSS2                                                       
         LA    RA,0(R6,R2)                                                      
         EX    R5,*+8                                                           
         B     *+10                                                             
         CLC   0(0,RF),0(RA)                                                    
         BE    SETOUTA                                                          
         BHR   RE                                                               
BSS2     SR    R2,R0                                                            
         BR    RE                                                               
BSS3     LA    R1,1(R1)                                                         
         AR    R2,R4                                                            
*                                                                               
BS4      CLI   BSPADD,1            ADD                                          
         BE    *+12                 YES - ADD IN SEQUENCE                       
         MVI   BSPNF,1              NO - SET RECORD NOT FOUND                   
         B     BSEXIT                                                           
         MVI   BSPNF,1                                                          
         L     R3,BSPNOR                                                        
         B     BS5                                                              
BS4A     XC    BSPAREC,BSPAREC     NO - RETURN END OF TABLE                     
         B     BSEXIT                                                           
*                                                                               
BS5      MVI   BSPADD,1            SLIDE TABLE FOR INSERT                       
         CH    R3,=H'10'                                                        
         BH    *+6                                                              
         AR    R1,R4                                                            
         LR    RF,R1                                                            
         CR    R3,R7               INPUT NO ENTRY LT. MAX NO ENTRY              
         BL    *+8                  YES - ADD ENTRY                             
         B     BS4A                 NO - RETURN END OF TABLE                    
         CR    R2,RF                                                            
         BE    AFRST                                                            
SLD1     LR    RA,RF                                                            
         SR    RA,R2               LENGTH OF MOVE                               
         LR    RE,RF                                                            
         AR    RF,R4                                                            
SLD1A    CH    RA,=H'255'                                                       
         BL    SLD2                                                             
         SH    RE,=H'255'                                                       
         SH    RF,=H'255'                                                       
         MVC   HOLD,0(RE)                                                       
         MVC   0(255,RF),HOLD                                                   
         SH    RA,=H'255'                                                       
         B     SLD1A                                                            
SLD2     SR    RE,RA                                                            
         SR    RF,RA                                                            
         LTR   RA,RA                                                            
         BZ    AFRST                                                            
         BCTR  RA,0                                                             
         EX    RA,*+8                                                           
         B     *+10                                                             
         MVC   HOLD(1),0(RE)                                                    
         EX    RA,*+8                                                           
         B     AFRST                                                            
         MVC   0(1,RF),HOLD                                                     
         B     AFRST                                                            
SETOUTA  ST    R2,BSPAREC          SET OUTPUT RECORD ADDRESS                    
         MVI   BSPNF,0             TURN OFF NOT FOUND                           
         CLI   SVACT,X'80'         DELETE RECORD FROM BINSRCH TABLE             
         BNE   BSEXIT2                                                          
         L     R5,BSPNOR           SET END OF TABLE ADDRESS                     
         BCTR  R5,0                DECREMENT FOR DELETED SLOT                   
         MH    R5,BSPLENR+2                                                     
         A     R5,SVADDR                                                        
         SR    R5,R2               SET CURRENT RECORD                           
DEL1     CH    R5,=H'255'                                                       
         BL    DEL2                                                             
         LR    RF,R2                                                            
         A     RF,BSPLENR                                                       
         MVC   0(255,R2),0(RF)                                                  
         SH    R5,=H'255'                                                       
         AH    R2,=H'255'                                                       
         B     DEL1                                                             
DEL2     LTR   R5,R5               END                                          
         BZ    DEL3                 YES - DECREMENT TABLE COUNT                 
         LR    RF,R2                                                            
         A     RF,BSPLENR                                                       
         BCTR  R5,0                                                             
         EX    R5,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),0(RF)                                                    
DEL3     L     R5,BSPNOR                                                        
         BCTR  R5,0                                                             
         ST    R5,BSPNOR                                                        
*                                                                               
BSEXIT   CLI   SVACT,2                                                          
         BNE   BSEXIT2                                                          
         L     R7,BSPNOR           GET NUMBER OF RECORDS IN TABLE               
         MH    R7,BSPLENR+2        X RECLEN                                     
         A     R7,BSPSTRT                                                       
         BCTR  R7,0                                                             
         CLR   R2,R7               TEST ADDRESS IN TABLE                        
         BL    *+8                 YES                                          
         B     BSEXIT2             NO - MAINTAIN REC NOT FOUND                  
         ST    R2,BSPAREC                                                       
         MVI   BSPNF,0                                                          
BSEXIT2  XIT1                                                                   
         ORG   *-2                                                              
         BSM   0,RE                RESTORE CALLERS ADDRESSING MODE              
         EJECT                                                                  
         LTORG                                                                  
         SPACE 2                                                                
WRKD     DSECT                                                                  
DUB      DS    D                                                                
SW       DS    CL1                                                              
HALF     DS    H                                                                
SVACT    DS    C                                                                
SVADDR   DS    F                                                                
HOLD     DS    CL255                                                            
WRKX     EQU   *                                                                
         SPACE 2                                                                
       ++INCLUDE DDBSPARA                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'002DDBINSRCH 04/24/02'                                      
         END                                                                    
