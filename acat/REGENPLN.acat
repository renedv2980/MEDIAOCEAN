*          DATA SET REGENPLN   AT LEVEL 008 AS OF 08/27/90                      
*CATALP REGENPLN                                                                
         TITLE 'REGENPLN - REPPAK PLAN BUILD MODULE'                            
*              P1 = A(BUYREC)      BUYCOS IS UPDATED                            
*              P2 = A(PLANREC)     BYTE 0=0 ON RETURN IF NO CHANGE              
*              P3 = A(ADDRESS BLOCK)                                            
*                        WORD 1 = V(GTBROAD) NOT USED                           
*                             2 = A(GETDAY)                                     
*                             3 = A(ADDAY)                                      
*                             4 = A(DATCON)                                     
*                             5 = V(RECUP)                                      
* THIS MODULE ADDS WEEK DATE ELEMENTS TO PLAN REC BASED ON BUY DATES            
REGENPLN CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 25,REGENPLN                                                      
         USING WORKD,RC                                                         
         ST    R1,SAVER1                                                        
         USING BUYREC,RA                                                        
         L     RA,0(R1)            BUYREC                                       
         SR    R4,R4                                                            
         L     R5,4(R1)            PLNREC                                       
         CLC   RBUYKMLN,RBUYKLIN   MAKE-GOOD?                                   
         BNE   XIT                                                              
         L     R6,8(R1)                                                         
         MVC   VGETDAY(20),4(R6)                                                
*                                                                               
         LA    R6,RBUYCOS-RBUYREC(R5)                                           
         MVC   WORK(2),=X'0305'    PLAN DATE ELEM CODE + LEN                    
         CLC   RBUYCOS,0(R6)                                                    
         BE    *+14                                                             
         MVC   0(4,R6),RBUYCOS     NEW PLAN COST                                
         MVI   4(R1),1             CHANGE IND                                   
         LA    R3,RBUYELEM                                                      
* SEARCH BUYREC                                                                 
P50      IC    R4,1(R3)            NEXT BUYREC ELEM                             
         AR    R3,R4                                                            
         CLI   0(R3),0             LAST?                                        
         BNE   P100                                                             
XIT      XMOD1 1                                                                
P100     CLI   0(R3),3             DATE ELEM IN BUYREC?                         
         BNE   P50                                                              
* BUY DATE ELEM - ADD TO PLAN WEEK ELEMENTS                                     
         L     R1,SAVER1                                                        
         L     R1,4(R1)            A(PLNREC)                                    
         LA    R5,34(R1)           FIRST ELEM IN PLNREC                         
* GET BUY MONDAY WEEK DATE                                                      
         GOTO1 VDATCON,DMCB,(3,2(R3)),WORK2                                     
         GOTO1 VGETDAY,(R1),WORK2,WORK2+6                                       
*                                                                               
         SR    R6,R6                                                            
         IC    R6,DMCB             DAY OF WEEK                                  
         BCTR  R6,R0                                                            
         LNR   R6,R6                                                            
         GOTO1 VADDAY,(R1),WORK2,WORK2+6,(R6)                                   
         GOTO1 VDATCON,(R1),(3,5(R3)),WORK2+24    BUY ELEM END DATE             
* BACK UP END DATE IN CASE OUT OF WEEK ROTATOR                                  
         GOTO1 VADDAY,(R1),WORK2+24,WORK2+18,(R6)                               
P150     GOTO1 VDATCON,(R1),WORK2+6,(3,WORK+2)                                  
* SEE IF MONDAY DATE IN PLAN YET                                                
P200     IC    R4,1(R5)            PLAN ELEM LEN                                
         AR    R5,R4               NEXT ELEM IN PLN                             
         CLI   0(R5),0             LAST?                                        
         BE    P250                                                             
* PLAN DATE ELEM                                                                
         CLC   WORK(5),0(R5)       MONDAY DATE V PLAN MONDAY DATE               
         BH    P200                                                             
         BE    P300                                                             
* ADD PLAN ELEMENT                                                              
P250     L     R6,SAVER1                                                        
         MVI   4(R6),1             ACTIVITY IND                                 
         L     R6,4(R6)            A(PLNREC)                                    
         GOTO1 VRECUP,(R1),(2,(R6)),WORK,(R5)                                   
P300     MVC   WORK2+12(6),WORK2+6 CURRENT MONDAY RUNNING DATE                  
* GET NEXT WEEK OF BUY                                                          
         LA    R2,7                                                             
         TM    8(R3),X'40'         ALTERNATE WEEKS                              
         BZ    *+8                                                              
         LA    R2,14                                                            
         GOTO1 VADDAY,(R1),WORK2+12,WORK2+6,(R2)                                
* SEE IF RUNNING DATE EXCEEDS BUY ELEM END DATE                                 
         CLC   WORK2+6(6),WORK2+18                                              
         BH    P50                                                              
         B     P150                                                             
BUYREC   DSECT                                                                  
       ++INCLUDE REGENBUY                                                       
WORKD    DSECT                                                                  
VGETDAY  DS    A                                                                
VADDAY   DS    A                                                                
VDATCON  DS    A                                                                
VRECUP   DS    A                                                                
DMCB     DS    6F                                                               
WORK     DS    CL48                                                             
SAVER1   DS    F                                                                
WORK2    DS    CL48                                                             
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'008REGENPLN  08/27/90'                                      
         END                                                                    
