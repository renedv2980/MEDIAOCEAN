*          DATA SET ACWINTER   AT LEVEL 019 AS OF 08/21/97                      
*CATALP WINTER                                                                  
WINTER   TITLE 'ACCOUNTING SYSTEM WORKER INTERFACE'                             
         SPACE                                                                  
WINTER   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WINTERX-WINTERD,**WINTER,R9                                      
         USING WINTERD,RC          RC=A(PROGRAM W/S)                            
         L     RA,=V(ACWORKC)                                                   
         USING ACWORKD,RA          RA=A(GENERAL W/S)                            
         LA    R8,SAVEAREA                                                      
         USING SAVED,R8            R8=A(SAVED VALUES)                           
         ST    R1,AWINTBLK                                                      
         MVC   WINTBLK(WINTBLKL),0(R1)                                          
*                                                                               
         MVC   SVHDHOOK,HEADHOOK   SAVE HEADHOOK VALUE                          
         MVC   SVCLRHED,CLEARHED   SAVE CLEARHED VALUE                          
         GOTO1 DATAMGR,WDMCB,DMKEY,ACCDIR,SAVEKEY                               
*                                                                               
         CLI   WIACTION,WIAINIT                                                 
         BE    INIT                                                             
         CLI   WIACTION,WIAPUT                                                  
         BE    PUT                                                              
         CLI   WIACTION,WIALIST                                                 
         BE    LIST                                                             
         CLI   WIACTION,WIACLOSE                                                
         BE    CLOS                                                             
         DC    H'0'                                                             
         SPACE 1                                                                
EXIT     OC    SAVEKEY(L'ACCKEY),SAVEKEY                                        
         BZ    EXIT2                                                            
         GOTO1 DATAMGR,WDMCB,DMKEY,ACCDIR,WORK                                  
         CLC   SAVEKEY(L'ACCKEY),WORK                                           
         BE    EXIT2                                                            
         GOTO1 DATAMGR,WDMCB,(X'08',DMREAD),ACCDIR,SAVEKEY,SAVEKEY              
         BE    EXIT2                                                            
         TM    8(R1),X'02'         TEST ERROR IS RECORD DELETED                 
         BNZ   EXIT2                                                            
         DC    H'0'                ANY OTHER ERROR MEANS DISASTER               
*                                                                               
EXIT2    DS    0H                  RESTORE WINTBLK                              
         L     R1,AWINTBLK                                                      
         MVC   0(WINTBLKL,R1),WINTBLK                                           
         MVC   HEADHOOK,SVHDHOOK   RESTORE HEADHOOK VALUE                       
         MVC   CLEARHED,SVCLRHED   RESTORE CLEARHED VALUE                       
*                                                                               
XIT      XIT1  ,                   RETURN TO CALLER                             
         EJECT                                                                  
INIT     DS    0H                                                               
*                                                                               
         LA    R0,MISSTAB          MISSING ACCOUNTS TABLE                       
         LA    R1,MISSTLEN                                                      
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE               CLEARED TO ZEROES                            
*                                                                               
         LA    R0,IOANUM           SAVE A(IO AREAS)                             
         LA    RF,IOAREAS                                                       
         LA    RE,WIAIO                                                         
INIT100  OC    0(4,RE),0(RE)       DID USER SUPPLY A(IOAREA)?                   
         BNZ   *+8                 YES - SKIP WINTER A(IOAREA)                  
         ST    RF,0(RE)                                                         
         LA    RF,IOALEN(RF)                                                    
         LA    RE,4(RE)                                                         
         BCT   R0,INIT100                                                       
*                                                                               
         L     RF,=A(WKBUFF)                                                    
         ST    RF,AWKBUFF                                                       
*                                                                               
INIT200  LA    R2,LDGNUM           NO. OF ENTRIES IN LDGTAB                     
         LA    R3,WILDGNUM         NO. OF ENTRIES IN WILDGS                     
         CR    R2,R3               MUST BE EQUAL                                
         BE    *+6                                                              
         DC    H'0'                                                             
         LA    R2,LDGTAB           R2=A(LEDGER TABLE)                           
         USING LDGTABD,R2                                                       
         LA    R3,WILDGS           R3=A(WINTBLK LEDGER CODES)                   
*                                                                               
INIT210  CLI   0(R3),C'A'          USER SUPPLIED LEDGER CODE?                   
         BL    *+10                NOTHING SENSIBLE                             
         MVC   LDGCODE,0(R3)       YES - OVERWRITE DEFAULT                      
         CLI   LDGLENA,1           DOES WINTER WANT L'LEVELS?                   
         BE    INIT212                                                          
         CLI   LDGOFFP,1           AND/OR OFFPOS?                               
         BNE   INIT220             NO FURTHER INFORMATION REQUIRED              
*                                                                               
INIT212  MVC   ACCKEY,SPACES       BUILD KEY FOR LEDGER RECORD READ             
         MVC   ACCKEY(1),RCCOMPFL  COMPANY                                      
         MVC   ACCKEY+1(2),LDGCODE UNIT/LEDGER                                  
         MVC   AIO,WIAIO           GENERAL IO AREA                              
         BAS   RE,ACCIO            READ FOR LEDGER                              
         BE    *+6                                                              
         DC    H'0'                                                             
         BAS   RE,GETACCEL         SET A(ELEMENTS)                              
*                                                                               
         CLI   LDGLENA,1           DOES WINTER WANT L'LEVELS?                   
         BNE   INIT214                                                          
         ICM   R1,15,AACCLEDG                                                   
         BNZ   *+6                                                              
         DC    H'0'                                                             
         USING ACHEIRD,R1                                                       
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         IC    RF,ACHRLEVA                                                      
         STC   RF,LDGLENA          L'LEVEL A (OVERWRITES INDICATOR)             
         ICM   RE,1,ACHRLEVB       LEVEL B IN USE?                              
         BZ    INIT214                                                          
         SR    RE,RF               ACHRLEVB-ACHRLEVA                            
         STC   RE,LDGLENB          =L'LEVEL B                                   
         ICM   RE,1,ACHRLEVC       LEVEL C IN USE?                              
         BZ    INIT214                                                          
         IC    RF,ACHRLEVB                                                      
         SR    RE,RF               ACHRLEVC-ACHRLEVB                            
         STC   RE,LDGLENC          =L'LEVEL C                                   
         ICM   RE,1,ACHRLEVD       LEVEL D IN USE?                              
         BZ    INIT214                                                          
         IC    RF,ACHRLEVC                                                      
         SR    RE,RF               ACHRLEVD-ACHRLEVC                            
         STC   RE,LDGLEND          =L'LEVEL D                                   
         B     INIT214                                                          
*                                                                               
INIT214  CLI   LDGOFFP,1           DOES WINTER WANT OFFPOS?                     
         BNE   INIT220                                                          
         MVI   LDGOFFP,0           CLEAR INDICATOR                              
         ICM   R1,15,AACCLEDG                                                   
         BZ    INIT220                                                          
         USING ACLEDGD,R1                                                       
         CLI   ACLTOFF,12          POSTITION IN KEY?                            
         BH    INIT220                                                          
         MVC   LDGOFFP,ACLTOFF     OFFPOS (OVERWRITES INDICATOR)                
         B     INIT220                                                          
*                                                                               
INIT220  LA    R2,LDGNTRY(R2)      NEXT LEDGER IN LDGTAB                        
         CLI   0(R2),X'FF'         E-O-T?                                       
         BE    INIT300                                                          
         LA    R3,WILDGLEN(R3)     NEXT LEDGER IN WILDGS                        
         B     INIT210                                                          
*                                                                               
         DROP  R1,R2                                                            
*                                                                               
INIT300  DS    0H                  CLEAR...                                     
         MVI   PRUNITFA,C' '       PRODUCTION UNIT FOR ANALYSIS                 
         ZAP   TOTRECS,=P'0'       TOTAL RECORDS ADDED                          
         ZAP   TOTCASH,=P'0'       TOTAL CASH                                   
         ZAP   XTRAMISS,=P'0'      EXTRA MISSING ACCOUNTS COUNT                 
*                                                                               
         OC    WIABIN,WIABIN       SET UP BINSRCH PARAMETER LIST                
         BZ    INIT400             USER DID NOT PROVIDE AREA                    
         MVC   BINP2,WIABIN        SAVE A(BINSRCH BUFFER)                       
         SR    R1,R1                                                            
         ICM   R1,1,WIBINSIZ       BINSRCH BUFFER SIZE (IN K)                   
         BNZ   *+6                                                              
         DC    H'0'                USER IS COMPLETELY NUTTY                     
         MH    R1,=H'1024'                                                      
         SR    R0,R0                                                            
         LA    RE,BINRECL                                                       
         ST    RE,BINP4            SAVE L'BINREC                                
         DR    R0,RE                                                            
         ST    R1,BINP6            SAVE MAX ENTRIES (L'BINREC/BYTES)            
         LA    RE,BINKEYL                                                       
         ST    RE,BINP5            SAVE L'BINREC KEY                            
         LA    RE,BINKEY                                                        
         ST    RE,BINP1            SAVE A(BINREC KEY)                           
*                                                                               
INIT400  DS    0H                                                               
         L     RF,ADMASTC                                                       
         USING MASTD,RF                                                         
         PACK  WDUB(2),MCDATE(3)   MCDATE FORMAT IS CL8 (DD/MM/YY)              
         MVC   TODAYP,WDUB         TODAY'S DAY FROM MCDATE                      
         MVC   THISMOS(1),MCDATE+7 MOS YEAR STRAIGHT FROM MCDATE                
         PACK  WDUB,MCDATE+3(2)                                                 
         CVB   R1,WDUB                                                          
         LA    R1,MOSTAB(R1)       CONVERT INTO CHARACTER                       
         MVC   THISMOS+1(1),0(R1)  MOS MONTH FROM CONVERSION TABLE              
         MVC   ORIGIDNO,MCORIGID   ORIGIN ID NUMBER                             
         XC    ORIGUSER,ORIGUSER                                                
         OC    ORIGUSER,MCUSERID   DID USER CLEAR MCUSERID?                     
         BNZ   *+8                                                              
         BAS   RE,GETOID           YES - SET ORIGIN ID NAME                     
         MVC   CTRY,MCCTRY         COUNTRY                                      
         MVC   LANG,MCLANG         LANGUAGE                                     
         DROP  RF                                                               
*                                                                               
INITX    B     EXIT                                                             
         EJECT                                                                  
PUT      DS    0H                                                               
         L     R1,WIAREC                                                        
         LA    R1,4(R1)                                                         
         CLI   0(R1),PSSBELQ       SUB-FILE TRAILER?                            
         BE    EXIT                IGNORE IT                                    
*                                                                               
         TM    WIACTIND,WIAIOPEN   TEST FILE IS OPEN                            
         BNZ   PUT20                                                            
         TM    WIPRGIND,WIPIKEYS   TEST CALLER PROVIDED KEY                     
         BNZ   PUT10                                                            
         MVC   WIWIDNO,ORIGIDNO    ORIGIN ID NUMBER                             
         MVI   WIWSYS,C'A'         ACCOUNTING SYSTEM                            
         MVC   WIWPRG,WIPRGNUM     PROGRAM CODE                                 
         TM    WIPRGIND,WIPILINK   LEDGER IN KEY?                               
         BZ    PUT04                                                            
         MVC   WIWMEDIA,QLEDGER    TAKE LEDGER                                  
         B     PUT06               AND DON'T TEST FOR MINK                      
PUT04    TM    WIPRGIND,WIPIMINK   MEDIA IN KEY?                                
         BZ    PUT06                                                            
         MVC   WIWMEDIA,QMEDIA     TAKE MEDIA                                   
PUT06    MVC   WIWDAYP,TODAYP                                                   
*                                                                               
PUT10    TM    WIPRGIND,WIPIDUPS   ALLOW DUPLICATES?                            
         BZ    *+8                                                              
         OI    WIWFLAG,ALLOWDUP    SET WIWFLAG INDICATOR                        
         MVI   WIWCLASS,C'P'       'POSTING'                                    
         MVC   WKCMND,OPEN                                                      
         BAS   RE,WORKIO                                                        
         OI    WIACTIND,WIAIOPEN   SET FILE OPENED                              
*                                                                               
PUT20    CLI   0(R1),PSHDELQ                                                    
         BNE   PUT100                                                           
         BAS   RE,GETPSTEL         GET A(ELEMENTS)                              
         TM    WIACTIND,WIAIPRAW   'RAW' PUT?                                   
         BO    PUT32               SKIP OFFICE MOVE AND PRODUCTION TEST         
         USING PSHEADD,R1                                                       
         CLI   WIOFFICE,C' '                                                    
         BNH   PUT30                                                            
         SR    RF,RF                                                            
         IC    RF,VATOFFP                                                       
         CLC   PSHDACC+1(2),VAT                                                 
         BE    PUT24                                                            
         CLC   PSHDACC+1(2),INC                                                 
         BNE   PUT30                                                            
         IC    RF,INCOFFP                                                       
PUT24    LA    RE,PSHDACC+2(RF)    POINT TO OFFPOS                              
         LA    RF,PSHDACC+L'PSHDACC-1                                           
         SR    RF,RE               TEST IF BEYOND END OF ACCOUNT                
         BM    PUT30               FORGET IT                                    
         MVC   0(1,RE),WIOFFICE                                                 
*                                                                               
PUT30    CLC   PSHDACC+1(2),PROD   PRODUCTION?                                  
         BE    PUT40                                                            
*                                  NON-SJ OR 'RAW' PUT JUST DO GETACC           
PUT32    MVC   AIO,WIAIO           DEFAULT IO AREA                              
         MVC   ACCKEY,SPACES                                                    
         MVC   ACCKEY(L'PSHDACC),PSHDACC                                        
         BAS   RE,GETACC           SPECIAL ALL-INCLUSIVE ACCIO ROUTINE          
         BNE   PUT50               ACCOUNT NOT FOUND                            
         B     PUT60                                                            
PUT40    MVC   AIO,WIAJOBIO        JOB IO AREA FOR PRODUCTION                   
         MVC   ACCKEY,SPACES                                                    
         MVC   ACCKEY(L'PSHDACC),PSHDACC                                        
         BAS   RE,ACCIO            DIRECT ACCOUNT READ                          
         BNE   PUT50                                                            
         BAS   RE,GETACCEL                                                      
         BAS   RE,SETACCNM                                                      
         B     PUT60                                                            
PUT50    BAS   RE,MISSACC          DEAL WITH MISSING ACCOUNTS                   
*                                                                               
PUT60    CLC   PSHDSBAC(3),SPACES  BILLING SOURCE CONTRA?                       
         BE    PUT80                                                            
         CLI   PSHDSBAC,C'*'       FUNNY 29/2P CONTRA?                          
         BE    PUT80                                                            
         MVC   AIO,WIAIO           DEFAULT IO AREA                              
         MVC   ACCKEY,SPACES                                                    
         MVC   ACCKEY(L'PSHDSBAC),PSHDSBAC                                      
         BAS   RE,GETACC           ALL-INCLUSIVE ACCIO ROUTINE                  
         BNE   PUT80                                                            
         MVC   PSHDSBNM,ACCNAME                                                 
*                                                                               
PUT80    ICM   R1,15,APSTTRNS      A(TRANS ELEMENT) PRESENT?                    
         BZ    PUT100                                                           
         USING TRANSD,R1                                                        
         CLC   TRNSBTCH(2),SPACES  BATCH MOS PRESENT?                           
         BNE   *+10                                                             
         MVC   TRNSBTCH(2),THISMOS                                              
         CLI   TRNSANAL,C' '       ANALYSIS CODE PRESENT?                       
         BNE   *+10                                                             
         MVC   TRNSANAL(1),PRUNITFA                                             
         TM    TRNSSTAT,X'80'      DEBIT?                                       
         BZ    PUT100                                                           
         AP    TOTCASH,TRNSAMNT                                                 
PUT100   MVC   WKCMND,ADD                                                       
         BAS   RE,WORKIO                                                        
         AP    TOTRECS,=P'1'                                                    
PUTX     B     EXIT                                                             
         DROP  R1                                                               
         EJECT                                                                  
LIST     DS    0H                                                               
         OC    WIWKEY,WIWKEY                                                    
         BZ    LISTX               NO POSTING FILE - STRAIGHT OUT               
         CP    TOTRECS,=P'0'                                                    
         BE    LIST02                                                           
         BAS   RE,BLDEOF                                                        
*                                                                               
LIST02   MVI   FORCECLR,C'Y'       CLEAR PRINT BLOCK TO SPACES                  
         GOTO1 ACREPORT                                                         
*                                                                               
         L     RE,ADBXAREA         SAVE CALLER'S BOX VALUES                     
         LA    RF,BOXL                                                          
         LA    R0,SAVEBOX                                                       
         LR    R1,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         MVC   ASVSPEC,ACSPECS     SAVE ACSPECS                                 
         LA    R0,WINTSPEC         WINTER HEADING ACDEFS                        
         ST    R0,ACSPECS                                                       
*                                                                               
         GOTO1 ADDICTAT,WDMCB,C'LU  ',INLIST,OUTLIST                            
         ORG   *-2                                                              
         TM    RCFLAG1,RCFREPLC    TEST PRINTING MIXED CASE                     
         BZ    *+8                                                              
         MVI   WDMCB+1,C'L'        MIXED CASE CALL TO DICTATE                   
         BASR  RE,RF                                                            
*                                                                               
         LA    RF,HOOK                                                          
         ST    RF,HEADHOOK                                                      
         MVI   CLEARHED,C'N'       DON'T CLEAR MY HEADS, ACREPORT               
         L     RF,ADBXAREA                                                      
         USING BOXD,RF                                                          
         TM    WIPRGIND,WIPIBXRQ   DOES USER WANT BOXES NOW?                    
         BO    LIST04              YES - CARRY  ON                              
         CLI   BOXYORN,C'Y'        DOES USER HAVE BOXES ON?                     
         BNE   LIST06              NO                                           
         OI    WIPRGIND,WIPIBXRQ   FORCE BIT ON IN WINTBLK                      
LIST04   MVI   BOXYORN,C'Y'                                                     
         MVC   BOXROWS,SPACES                                                   
         MVC   BOXCOLS,SPACES                                                   
         MVI   BOXROWS+8,C'T'      TOP                                          
         MVI   BOXROWS+10,C'M'     MIDDLE                                       
         MVI   BOXROWS+99,C'B'     BOTTOM                                       
LIST06   LA    RF,HEAD7+13                                                      
         MVI   0(RF),C'?'                                                       
         CLC   ORIGUSER,SPACES                                                  
         BNH   LIST08                                                           
         MVC   0(L'ORIGUSER,RF),ORIGUSER                                        
         LA    RF,L'ORIGUSER-1(RF)                                              
         CLI   0(RF),C' '                                                       
         BH    *+8                                                              
         BCT   RF,*-8                                                           
LIST08   MVI   1(RF),C','                                                       
         MVC   2(4,RF),WIWSYS      SYS/PRG/MEDIA                                
         CLI   5(RF),C' '          WAS MEDIA PRESENT IN KEY?                    
         BH    *+8                                                              
         MVI   5(RF),C'*'          NO - ASTERISK IN PLACE                       
         UNPK  6(3,RF),WIWDAYP(2)  8(RF) WILL BE UNPRINTABLE RUBBISH            
         MVC   8(1,RF),WIWCLASS    OVERWRITE RUBBISH WITH CLASS                 
         MVI   9(RF),C','                                                       
         CURED WIWSEQN,(4,10(RF)),0,ALIGN=LEFT                                  
*                                                                               
         LA    R1,PRGTAB                                                        
         USING PRGTABD,R1                                                       
LIST10   CLI   PRGNUM,X'FF'        TEST END OF TABLE                            
         BE    LIST12                                                           
         CLC   PRGNUM,WIPRGNUM     TEST MATCH ON REPORT NUMBER                  
         BE    *+12                                                             
         LA    R1,PRGTBLQ(R1)                                                   
         B     LIST10                                                           
*                                                                               
         MVC   HEAD5+40(L'AC@CRTDB),AC@CRTDB                                    
         SR    RF,RF                                                            
         ICM   RF,3,PRGNDSP        R1=DISPLACEMENT TO NAME                      
         LA    RF,OUTLIST(RF)                                                   
         SR    RE,RE                                                            
         IC    RE,PRGNLN                                                        
         BCTR  RE,0                                                             
         LA    R1,HEAD5+40+L'AC@CRTDB-1                                         
         CLI   0(R1),C' '                                                       
         BNE   *+8                                                              
         BCT   R1,*-8                                                           
         EX    RE,*+4                                                           
         MVC   2(0,R1),0(RF)       PUT NAME IN HEAD LINE                        
         DROP  R1                                                               
*                                                                               
LIST12   OC    MISSTAB(MISSNTRY),MISSTAB                                        
         BZ    LIST32                                                           
         MVI   RCSUBPRG,0          PRINT LIST OF MISSING ACCOUNTS               
         TM    WIPRGIND,WIPIBXRQ   BOXES?                                       
         BZ    LIST14              NO - SKIP                                    
         MVI   RCSUBPRG,1          ALIGN HEADS FOR BOXES                        
         L     RF,ADBXAREA                                                      
         LA    RE,BOXCOLS+1                                                     
         TM    RCFLAG1,RCFSHIFT                                                 
         BZ    *+8                                                              
         LA    RE,BOXCOLS+2                                                     
         MVI   00(RE),C'L'         LEFT                                         
         MVI   20(RE),C'C'         CENTRE 1                                     
         MVI   40(RE),C'C'         CENTRE 2                                     
         MVI   60(RE),C'C'         CENTRE 3                                     
         MVI   80(RE),C'R'         RIGHT                                        
LIST14   MVC   PAGE,=H'1'                                                       
         MVC   LINE,MAXLINES                                                    
         LA    R2,MISSTAB                                                       
LIST16   LA    R0,4                UP TO FOUR PRINT LINES TOGETHER              
         LA    R4,P+1                                                           
         TM    WIPRGIND,WIPIBXRQ   BOXES?                                       
         BZ    LIST18                                                           
         LA    R4,P+2                                                           
LIST18   LA    R3,4                FOUR ACCOUNTS PER LINE                       
         LR    R5,R4                                                            
LIST20   MVC   0(MISSNTRY,R5),0(R2)                                             
         LA    R2,MISSNTRY(R2)                                                  
         CLI   0(R2),X'00'                                                      
         BE    LIST22                                                           
         CLI   0(R2),X'FF'                                                      
         BE    LIST24                                                           
         LA    R5,MISSNTRY+6(R5)                                                
         BCT   R3,LIST20                                                        
         LA    R4,L'P(R4)                                                       
         BCT   R0,LIST18                                                        
LIST22   GOTO1 ACREPORT                                                         
         CLI   0(R2),X'00'                                                      
         BE    LIST30                                                           
         B     LIST16                                                           
LIST24   LA    R4,L'P(R4)                                                       
         CP    XTRAMISS,=P'0'                                                   
         BE    LIST28              JUST PRINT, IF NO EXTRA MISSING              
         BCT   R0,LIST26           IF ROOM IN PRINT BLOCK, BUILD ON             
         GOTO1 ACREPORT            ELSE, PRINT WHAT'S THERE                     
         LA    R4,P+1              AND START AGAIN                              
LIST26   CURED XTRAMISS,(6,0(R4)),0,ALIGN=LEFT                                  
         AR    R4,R0               R0=SIGNIFICANT LENGTH                        
         LA    R4,1(R4)                                                         
         MVC   0(L'AC@OTMIS,R4),AC@OTMIS                                        
*                                                                               
LIST28   GOTO1 ACREPORT                                                         
*                                                                               
LIST30   L     RF,ADBXAREA         CLOSE MISSING ACCOUNTS BOX                   
         MVI   BOXREQ,C'C'                                                      
         GOTO1 ACREPORT                                                         
         L     RF,ADBXAREA         SET RE-INITIALISE BOX                        
         MVI   BOXINIT,0                                                        
*                                                                               
LIST32   MVC   HEAD3,SPACES        PRINT POSTING FILE                           
         MVC   HEAD4,SPACES                                                     
         MVC   HEAD10,SPACES                                                    
         MVI   RCSUBPRG,2          SET SUB-PROGRAM                              
         TM    WIACTIND,WIAIDRAF                                                
         BZ    *+8                                                              
         MVI   RCSUBPRG,3          AMEND SUBPRG FOR DRAFT                       
         MVC   PAGE,=H'1'                                                       
         MVC   LINE,MAXLINES                                                    
         ZAP   TOTDEBS,=P'0'                                                    
         ZAP   TOTCRDS,=P'0'                                                    
         TM    WIPRGIND,WIPIBXRQ   BOXES?                                       
         BZ    LIST40                                                           
         IC    RF,RCSUBPRG         INCREMENT SUBPRG FOR BOX HEADS               
         LA    RF,2(RF)                                                         
         STC   RF,RCSUBPRG         SUBPRG = 4 OR 5                              
         L     RF,ADBXAREA                                                      
         MVC   BOXCOLS,SPACES                REBUILD COLUMNS                    
         LA    RE,BOXCOLS+1                                                     
         TM    RCFLAG1,RCFSHIFT                                                 
         BZ    *+8                                                              
         LA    RE,BOXCOLS+2                                                     
         MVI   0(RE),C'L'            HARD LEFT                                  
         MVI   LBXC1-LINED(RE),C'C'  CENTRE 1                                   
         MVI   LBXC2-LINED(RE),C'C'  CENTRE 2                                   
         MVI   LBXC3-LINED(RE),C'C'  CENTRE 3                                   
         MVI   LBXC4-LINED(RE),C'C'  CENTRE 4                                   
         MVI   LBXC5-LINED(RE),C'C'  CENTRE 5                                   
         MVI   LBXC6-LINED(RE),C'C'  CENTRE 6                                   
         MVI   LBXC7-LINED(RE),C'C'  CENTRE 7                                   
         MVI   LBXR-LINED(RE),C'R'   RIGHT                                      
         DROP  RF                                                               
LIST40   MVC   WKCMND,READ         READ A WORKER RECORD                         
         BAS   RE,WORKIO                                                        
         BNE   LIST80              END-OF-FILE                                  
         BAS   RE,GETPSTEL         GET A(ELEMENTS)                              
         ICM   R2,15,APSTHEAD                                                   
         BZ    LIST60                                                           
         USING PSHEADD,R2          FORMAT A PRINT LINE                          
         TM    WIACTIND,WIAINCST   NO COSTING?                                  
         BZ    *+14                                                             
         CLC   PSHDACC+1(2),COST   SKIP COSTING POSTING                         
         BE    LIST40                                                           
         LA    R3,P                                                             
         TM    WIPRGIND,WIPIBXRQ   BOXES?                                       
         BZ    *+8                                                              
         LA    R3,P+1              SHUNT FOR BOXES                              
         USING LINED,R3                                                         
         MVC   LINACC,PSHDACC+1                                                 
         MVC   LINSBAC,PSHDSBAC+1                                               
         MVC   LINKANAL,PSHDANAL                                                
         ICM   R2,15,APSTTRNS                                                   
         BZ    LIST40                                                           
         USING TRANSD,R2                                                        
         CLC   TRNSANAL,SPACES                                                  
         BE    LIST42                                                           
         MVC   LINTANAL,TRNSANAL                                                
         CLC   LINKANAL,SPACES                                                  
         BE    LIST42                                                           
         MVI   LINSLASH,C'/'       IF TWO CODES, SEPARATE THEM                  
LIST42   GOTO1 DATCON,WDMCB,(1,TRNSDATE),(8,LINDATE)                            
         TM    TRNSSTAT,X'80'      DEBIT OR CREDIT?                             
         BO    LIST44                                                           
         AP    TOTCRDS,TRNSAMNT                                                 
         CURED TRNSAMNT,(L'LINCRED,LINCRED),2,MINUS=YES                         
         B     LIST46                                                           
LIST44   AP    TOTDEBS,TRNSAMNT                                                 
         CURED TRNSAMNT,(L'LINDEB,LINDEB),2,MINUS=YES                           
LIST46   MVC   LINREF,TRNSREF                                                   
         SR    R0,R0               PRINT ONE/TWO LINES OF NARRATIVE             
         IC    R0,TRNSLEN                                                       
         SH    R0,=Y(TRNSNARR-TRANSD)                                           
         BZ    LIST50                                                           
         CLC   TRNSANAL,=C'99'     SJ BILLS HAVE PACKED FIELDS IN NARR.         
         BNE   *+8                                                              
         LA    R0,15               JUST FIRST 15 BYTES, PLEASE                  
         GOTO1 CHOPPER,WDMCB,((R0),TRNSNARR),(L'LINNARR,LINNARR),      X        
               (C'P',2)                                                         
LIST50   GOTO1 ACREPORT                                                         
         B     LIST40                                                           
*                                  PRINT FILE TOTALS                            
LIST60   ICM   R2,15,APSTSUBF                                                   
         BZ    LIST40                                                           
         USING PSSUBFD,R2                                                       
         LA    R3,PSECOND                                                       
         TM    WIPRGIND,WIPIBXRQ   BOXES?                                       
         BZ    *+8                                                              
         LA    R3,PSECOND+1                                                     
         MVC   LINSBAC(L'AC@TOTLS),AC@TOTLS                                     
         CURED TOTDEBS,(L'LINDEB,LINDEB),2,MINUS=YES                            
         CURED TOTCRDS,(L'LINCRED,LINCRED),2,MINUS=YES                          
         CURED PSSBRECS,(8,LINNARR),0,ALIGN=LEFT                                
         LA    R3,LINNARR+1                                                     
         AR    R3,R0               R0=SIGNIFICANT LENGTH                        
         CP    PSSBRECS,=P'1'      ONE ITEM?                                    
         BE    *+14                                                             
         MVC   2(L'AC@ITEMS,R3),AC@ITEMS                                        
         B     *+10                                                             
         MVC   2(L'AC@ITEM,R3),AC@ITEM                                          
         GOTO1 ACREPORT                                                         
         B     LIST40                                                           
LIST80   ZAP   TOTCASH,=P'0'                                                    
         ZAP   TOTRECS,=P'0'                                                    
*                                                                               
         TM    WIPRGIND,WIPIBXRQ   BOXES?                                       
         BZ    LIST90                                                           
         L     RE,ADBXAREA                                                      
         MVI   BOXREQ-BOXD(RE),C'C'  CLOSE THE BOX                              
         GOTO1 ACREPORT                                                         
         DROP  R2,R3                                                            
*                                                                               
LIST90   MVC   ACSPECS,ASVSPEC     RESTORE CALLER'S SPECS                       
*                                                                               
         L     R0,ADBXAREA         RESTORE CALLER'S BOX VALUES                  
         LA    R1,BOXL                                                          
         LA    RE,SAVEBOX                                                       
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
*                                                                               
         TM    WIACTIND,WIAIDRAF   IF DRAFT RUN PURGE THE POSTING FILE          
         BZ    *+14                                                             
         MVC   WKCMND,PURGE                                                     
         BAS   RE,WORKIO                                                        
         XC    WIWKEY,WIWKEY       CLEAR WORKER KEY                             
*                                                                               
LISTX    B     EXIT                                                             
         EJECT                                                                  
HOOK     TM    WIPRGIND,WIPIBXRQ   TEST PRINTING BOXES                          
         BNZR  RE                                                               
         MVI   HEAD11+3,X'00'      NO - FORCE BLANK LINE                        
         BR    RE                                                               
         EJECT                                                                  
CLOS     DS    0H                                                               
         CP    TOTRECS,=P'0'                                                    
         BE    CLOSX                                                            
         BAS   RE,BLDEOF           BUILD TRAILER/ADD TRAILER/CLOSE FILE         
         ZAP   TOTRECS,=P'0'                                                    
         ZAP   TOTCASH,=P'0'                                                    
         TM    WIACTIND,WIAIDRAF   IF DRAFT RUN PURGE THE POSTING FILE          
         BZ    *+14                                                             
         MVC   WKCMND,PURGE                                                     
         BAS   RE,WORKIO                                                        
         XC    WIWKEY,WIWKEY       CLEAR WORKER KEY                             
*                                                                               
CLOSX    B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* ROUTINE READS CONTROL FILE FOR ORIGIN ID WHEN MCUSERID IS CLEARED   *         
* BY USER                                                             *         
***********************************************************************         
*                                                                               
GETOID   NTR1  ,                                                                
         SR    R0,R0                                                            
         ICM   R0,3,ORIGIDNO                                                    
         CVD   R0,WDUB                                                          
         OI    WDUB+7,X'0F'                                                     
         UNPK  ORIGUSER(5),WDUB    SET ORIGIN NUMBER                            
         L     R2,WIAIO            SEEK ORIGIN NAME                             
         USING CTIKEY,R2                                                        
         XC    CTIKEY,CTIKEY                                                    
         MVI   CTIKTYP,CTIKTYPQ                                                 
         MVC   CTIKNUM,ORIGIDNO                                                 
         GOTO1 DATAMGR,WDMCB,(0,DMREAD),CTFILE,CTIKEY,CTIKEY                    
         CLI   8(R1),0                                                          
         BNE   GETOIDX                                                          
         LA    R1,CTIDATA                                                       
         SR    R0,R0                                                            
         USING CTDSCD,R1                                                        
GETOID2  CLI   CTDSCEL,0           TEST END OF RECORD                           
         BE    GETOIDX                                                          
         CLI   CTDSCEL,CTDSCELQ    TEST ID CODE ELEMENT                         
         BE    *+14                                                             
         IC    R0,CTDSCLEN                                                      
         AR    R1,R0                                                            
         B     GETOID2                                                          
         MVC   ORIGUSER,CTDSC      SET ORIGIN CODE                              
GETOIDX  B     XIT                                                              
         DROP  R1,R2                                                            
         EJECT                                                                  
***********************************************************************         
* ROUTINE READS RECORDS FROM ACCFILE OR FROM BINSRCH BUFFER AND ADDS  *         
* ACCOUNTS TO BINSRCH BUFFER.                                         *         
* EXIT - CC EQU IF FOUND, CC NEQ IF NOT FOUND                         *         
***********************************************************************         
*                                                                               
GETACC   NTR1  ,                                                                
         TM    WIACTIND,WIAIPRAW   'RAW' PUT JUST SEEK RECORD                   
         BO    GETAC02                                                          
         CLC   ACCKEY+1(2),COST    SPECIAL TREATMENT FOR COSTING                
         BE    GETAC10                                                          
         CLC   ACCKEY+1(2),RECV    RECEIVABLES                                  
         BE    GETAC10                                                          
         CLC   ACCKEY+1(2),PROD    PRODUCTION                                   
         BE    GETAC10                                                          
GETAC02  BAS   RE,BINGET           SEEK RECORD IN BINSRCH TABLE                 
         BE    GETACYES                                                         
         BAS   RE,ACCIO            NOT FOUND - READ ACCOUNT FILE                
         BNE   GETACNO                                                          
         BAS   RE,GETACCEL         IF FOUND EXTRACT VALUES                      
         BAS   RE,SETACCNM                                                      
         BAS   RE,BINPUT           AND ADD TO BINSRCH TABLE                     
         B     GETACYES                                                         
*                                  HANDLE SPECIAL LEDGERS                       
GETAC10  MVC   SACCKEY,ACCKEY      SAVE CALLER'S ACCKEY                         
         MVC   ACCKEY+1(L'ACCKEY-1),SPACES                                      
         MVC   ACCKEY+1(2),PROD                                                 
         LA    R0,2                LOOP COUNTER                                 
         LA    R1,SACCKEY+3        BUILD PRODUCTION CLI/PRO KEYS                
         LA    RE,ACCKEY+3                                                      
         SR    RF,RF                                                            
         IC    RF,PRODLENA                                                      
GETAC20  BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,RE),0(R1)                                                    
         BCT   R0,*+14                                                          
         MVC   PROKEY,ACCKEY       SECOND TIME - SAVE PROKEY                    
         B     GETAC30                                                          
         MVC   CLIKEY,ACCKEY       FIRST TIME - SAVE CLIKEY                     
         LA    R1,1(R1,RF)         BEYOND CLIENT IN SACCKEY                     
         LA    RE,1(RE,RF)         BEYOND CLIENT IN ACCKEY                      
         IC    RF,PRODLENB                                                      
         B     GETAC20                                                          
*                                                                               
GETAC30  CLC   CLIKEY,LCLIKEY                                                   
         BE    GETAC40             CHECK FOR CHANGE OF CLIENT                   
         MVC   ACCKEY,SPACES                                                    
         MVC   ACCKEY(L'CLIKEY),CLIKEY                                          
         XC    LSTKEYS(LSTKEYLN),LSTKEYS                                        
         XC    CLIPROF,CLIPROF                                                  
         XC    PROPROF,PROPROF                                                  
         XC    COSKEY,COSKEY                                                    
         XC    RCVKEY,RCVKEY                                                    
         MVI   PRUNITFA,C' '                                                    
         MVC   AIO,WIACLIIO        RESET IO AREA FOR CLIENT                     
         BAS   RE,ACCIO                                                         
         BNE   GETACNO             EXIT IF CAN'T READ CLIENT                    
         MVC   LCLIKEY,CLIKEY                                                   
         BAS   RE,GETACCEL                                                      
         ICM   RE,15,AACCPROF                                                   
         BNE   *+6                                                              
         DC    H'0'                NO CLIENT PROFILE ELEMENT                    
         USING ACPROFD,RE                                                       
         MVC   CLIPROF,0(RE)       SAVE CLIENT PROFILE                          
         MVC   PRUNITFA,ACPRUNIT                                                
         DROP  RE                                                               
*                                  HANDLE PRODUCT                               
GETAC40  CLC   PROKEY,LPROKEY                                                   
         BE    GETAC60             CHECK FOR CHANGE OF CLIENT/PRODUCT           
         MVC   ACCKEY,SPACES                                                    
         MVC   ACCKEY(L'PROKEY),PROKEY                                          
         XC    LPROKEY,LPROKEY                                                  
         XC    PROPROF,PROPROF                                                  
         MVC   AIO,WIAPROIO        RESET IO AREA FOR PRODUCT                    
         BAS   RE,ACCIO                                                         
         BE    GETAC50                                                          
         CLC   SACCKEY+1(2),PROD   EXIT IF READING FOR PRODN. PRODUCT           
         BNE   GETAC60                                                          
         MVC   ACCKEY,PROKEY       PRODUCT INTO CALLER'S KEY                    
         B     GETACNO                                                          
GETAC50  MVC   LPROKEY,PROKEY                                                   
         BAS   RE,GETACCEL                                                      
         BAS   RE,SETACCNM                                                      
         ICM   RE,15,AACCPROF                                                   
         BZ    GETAC60                                                          
         MVC   PROPROF,0(RE)       SAVE PRODUCT PROFILE                         
*                                  DEDUCE RECEIVABLE/COSTING KEYS               
GETAC60  LA    RE,CLIPROF          DEFAULT TO CLIENT PROFILES                   
         USING ACPROFD,RE                                                       
         MVC   RCVKEY,ACPRRECV                                                  
         MVC   COSKEY,ACPRCOST                                                  
         MVC   PRUNITFA,ACPRUNIT                                                
         LA    RE,PROPROF          MERGE IN ANY PRODUCT PROFILES                
         OC    ACPRRECV,ACPRRECV                                                
         BZ    *+10                                                             
         MVC   RCVKEY,ACPRRECV                                                  
         OC    ACPRCOST,ACPRCOST                                                
         BZ    *+10                                                             
         MVC   COSKEY,ACPRCOST                                                  
         CLI   ACPRUNIT,0                                                       
         BE    *+10                                                             
         MVC   PRUNITFA,ACPRUNIT                                                
         DROP  RE                                                               
*                                  SET KEY CALL VALUE                           
         MVC   AIO,WIAPROIO                                                     
         LA    R1,PROKEY                                                        
         CLC   SACCKEY+1(2),PROD                                                
         BE    GETAC70                                                          
         MVC   AIO,WIAIO                                                        
         LA    R1,RCVKEY                                                        
         CLC   SACCKEY+1(2),RECV                                                
         BE    GETAC70                                                          
         LA    R1,COSKEY                                                        
         CLC   SACCKEY+1(2),COST                                                
         BE    GETAC70                                                          
         DC    H'0'                                                             
GETAC70  MVC   ACCKEY,SPACES                                                    
         MVC   ACCKEY(L'ACKEYACC),0(R1)                                         
         OC    ACCKEY(L'ACKEYACC),ACCKEY                                        
         BZ    GETACNO                                                          
         BAS   RE,BINGET           CHECK IF RECORD IN BINSRCH BUFFER            
         BE    GETACYES                                                         
         BAS   RE,ACCIO            NOT FOUND - READ ACCOUNT FILE                
         BNE   GETACNO                                                          
         BAS   RE,GETACCEL         IF FOUND EXTRACT VALUES                      
         BAS   RE,SETACCNM                                                      
         BAS   RE,BINPUT           AND ADD TO BINSRCH TABLE                     
         B     GETACYES                                                         
*                                                                               
GETACNO  LTR   RB,RB               CC NEQ FOR RECORD NOT FOUND                  
         B     GETACCX                                                          
GETACYES CR    RB,RB               CC EQU FOR RECORD FOUND                      
GETACCX  B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
* ROUTINE ADDS ACCOUNT CODE TO MISSING ACCOUNT TABLE UNLESS NAME      *         
* ELEMENT IS CARRIED ON POSTING.  WHEN TABLE IS FULL, A COUNT OF      *         
* EXTRA MISSING ACCOUNTS IS MAINTAINED                                *         
***********************************************************************         
*                                                                               
MISSACC  NTR1  ,                                                                
         ICM   R4,15,APSTNAME      NAME ELEMENT PASSED?                         
         BNZ   MISSACCX            EXIT - UPDATE WILL ADD ACCOUNT               
         ICM   R4,15,APSTHEAD                                                   
         USING PSHEADD,R4                                                       
         LA    R1,MISSTAB                                                       
MISSAC2  CLI   0(R1),X'FF'         END OF TABLE                                 
         BE    MISSAC6             GET OUT                                      
         CLI   0(R1),X'00'         CHECK ACCOUNT ALREADY IN MISSTAB             
         BE    MISSAC4             NO - ADD IT, IF POSSIBLE                     
         CLC   0(14,R1),PSHDACC+1                                               
         BE    MISSACCX                                                         
         LA    R1,14(R1)                                                        
         B     MISSAC2                                                          
MISSAC4  MVC   0(14,R1),PSHDACC+1                                               
         B     MISSACCX                                                         
*                                                                               
MISSAC6  AP    XTRAMISS,=P'1'                                                   
MISSACCX B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
* WORKER FILE IO ROUTINE                                              *         
*                                                                     *         
* EXIT - CC NEQ FOR END OF FILE                                       *         
***********************************************************************         
*                                                                               
WORKIO   NTR1  ,                                                                
         GOTO1 DATAMGR,WDMCB,WKCMND,WKFILE,WIWKEY,WIAREC,AWKBUFF                
         CLI   8(R1),0                                                          
         BE    WORKIOX                                                          
         CLC   WKCMND,READ         E-O-F ALLOWED FOR READ                       
         BE    *+6                                                              
         DC    H'0'                                                             
         TM    8(R1),X'81'                                                      
         BZ    *-6                                                              
         CLI   8(R1),0             SET CC=NEQ FOR EOF                           
WORKIOX  B     XIT                                                              
         SPACE 2                                                                
***********************************************************************         
* ACCOUNT FILE IO ROUTINE                                             *         
*                                                                     *         
* NTRY - AIO CONTAINS A(IO AREA FOR READ)                             *         
*        ACCKEY CONTAINS 42-BYTE, SPACE-FILLED KEY FOR READ           *         
* EXIT - CC NEQ ON ANY ERROR                                          *         
***********************************************************************         
*                                                                               
ACCIO    L     RF,AIO              CHECK IF WE HAVE IT ALREADY                  
         CLC   ACCKEY,0(RF)                                                     
         BER   RE                                                               
         NTR1  ,                                                                
         GOTO1 DATAMGR,WDMCB,(0,DMREAD),ACCOUNT,ACCKEY,AIO                      
         CLI   8(R1),0             SET CC=NEQ ON ANY ERROR CONDITION            
         B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
* ROUTINE GETS A(ELEMENTS) ON A POSTING RECORD                        *         
***********************************************************************         
*                                                                               
GETPSTEL NTR1  ,                                                                
         XC    PSTELS(PSTELEN),PSTELS                                           
         L     R1,WIAREC                                                        
         LA    R1,4(R1)                                                         
         SR    R0,R0                                                            
*                                                                               
GETPSEL2 CLI   0(R1),0                                                          
         BE    GETPSELX                                                         
         LA    RF,APSTHEAD                                                      
         CLI   0(R1),PSHDELQ                                                    
         BNE   GETPSEL4                                                         
         CLI   1(R1),PSHDLENQ      CHECK LENGTH TOO                             
         BE    GETPSEL6                                                         
         B     GETPSEL8                                                         
GETPSEL4 LA    RF,APSTSUBF                                                      
         CLI   0(R1),PSSBELQ                                                    
         BE    GETPSEL6                                                         
         LA    RF,APSTNAME                                                      
         CLI   0(R1),X'20'                                                      
         BE    GETPSEL6                                                         
         LA    RF,APSTTRNS                                                      
         CLI   0(R1),X'44'                                                      
         BNE   GETPSEL8                                                         
*                                                                               
GETPSEL6 ST    R1,0(RF)                                                         
GETPSEL8 IC    R0,1(R1)                                                         
         AR    R1,R0                                                            
         B     GETPSEL2                                                         
GETPSELX B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
* ROUTINE GETS A(ELEMENTS) ON AN ACCOUNT FILE RECORD                  *         
***********************************************************************         
*                                                                               
GETACCEL NTR1  ,                                                                
         XC    ACCELS(ACCELEN),ACCELS                                           
         L     R1,AIO                                                           
         LA    R1,ACRECORD-ACKEYD(R1)                                           
         SR    R0,R0                                                            
*                                                                               
GETACEL2 CLI   0(R1),0                                                          
         BE    GETACELX                                                         
         LA    RF,AACCCOMP                                                      
         CLI   0(R1),X'10'                                                      
         BE    GETACEL6                                                         
         LA    RF,AACCLEDG                                                      
         CLI   0(R1),X'14'                                                      
         BE    GETACEL6                                                         
         LA    RF,AACCHEIR                                                      
         CLI   0(R1),X'16'                                                      
         BE    GETACEL6                                                         
         LA    RF,AACCNAME                                                      
         CLI   0(R1),X'20'                                                      
         BE    GETACEL6                                                         
         LA    RF,AACCPROF                                                      
         CLI   0(R1),X'24'                                                      
         BNE   GETACEL8                                                         
*                                                                               
GETACEL6 ST    R1,0(RF)                                                         
GETACEL8 IC    R0,1(R1)                                                         
         AR    R1,R0                                                            
         B     GETACEL2                                                         
GETACELX B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
* BINSRCH BUFFER INTERFACE ROUTINES                                   *         
***********************************************************************         
*                                                                               
BINPUT   NTR1  ,                   ADD AN ACCOUNT TO BINSRCH BUFFER             
         OC    BINP2,BINP2                                                      
         BZ    BINPUTX                                                          
         MVC   BINKEY,ACCKEY+1                                                  
         MVC   BINNAME,ACCNAME                                                  
BINPUT2  GOTO1 BINSRCH,BINPARMS,(1,BINREC)                                      
         OC    BINP1,BINP1                                                      
         BNZ   BINPUTX                                                          
         XC    BINP3,BINP3                                                      
         B     BINPUT2                                                          
BINPUTX  B     XIT                                                              
         SPACE 2                                                                
BINGET   NTR1  ,                   SEEK AN ACCOUNT IN BINSRCH BUFFER            
         MVI   BINP1,1                                                          
         OC    BINP3,BINP3                                                      
         BZ    BINGETX                                                          
         OC    BINP2,BINP2                                                      
         BZ    BINGETX                                                          
         MVC   BINKEY,ACCKEY+1                                                  
         GOTO1 BINSRCH,BINPARMS,BINREC                                          
         CLI   BINP1,0                                                          
         BNE   BINGETX                                                          
         L     RE,0(R1)                                                         
         MVC   BINREC,0(RE)                                                     
         MVC   ACCNAME,BINNAME                                                  
BINGETX  CLI   BINP1,0                                                          
         B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
* EXTRACT AND SPACE FILL ACCOUNT NAME INTO ACCNAME                    *         
***********************************************************************         
*                                                                               
SETACCNM MVC   ACCNAME,SPACES                                                   
         ICM   RF,15,AACCNAME                                                   
         BZR   RE                                                               
         SR    R1,R1                                                            
         IC    R1,1(RF)                                                         
         SH    R1,=H'3'                                                         
         EX    R1,*+6                                                           
         BR    RE                                                               
         MVC   ACCNAME(0),2(RF)                                                 
         SPACE 2                                                                
***********************************************************************         
* ROUTINE BUILDS TRAILER, ADDS TO WORKER AND CLOSES WORKER FILE       *         
***********************************************************************         
*                                                                               
BLDEOF   NTR1  ,                                                                
         L     R1,WIAIO                                                         
         ST    R1,WIAREC                                                        
         LA    RE,4(R1)                                                         
         USING PSSUBFD,RE          BUILD POSTING TRAILER ELEMENT                
         MVI   PSSBEL,PSSBELQ                                                   
         MVI   PSSBLEN,PSSBLENQ                                                 
         MVC   PSSBDESC,WIPRGNAM                                                
         ZAP   PSSBRECS,TOTRECS                                                 
         ZAP   PSSBCASH,TOTCASH                                                 
         SR    RF,RF                                                            
         IC    RF,1(RE)                                                         
         AR    RE,RF                                                            
         MVI   0(RE),0                                                          
         LA    RE,1(RE)                                                         
         SR    RE,R1                                                            
         SLL   RE,16               SET RECORD LENGTH                            
         ST    RE,0(R1)                                                         
         MVC   WKCMND,ADD                                                       
         BAS   RE,WORKIO                                                        
         MVC   WKCMND,CLOSE                                                     
         BAS   RE,WORKIO                                                        
         NI    WIACTIND,X'FF'-WIAIOPEN                                          
         B     XIT                                                              
*                                                                               
         DROP  RE                                                               
         EJECT                                                                  
*                                  LITERALS, CONSTANTS, EQUATES                 
         LTORG                                                                  
         SPACE 2                                                                
MOSTAB   DS    C'1234567689ABC'    MOS COMVERSION TABLE                         
*                                                                               
PRGTAB   DS    0C                                                               
         DC    C'DB',AL2(AC@PGADB-OUTLIST),AL1(L'AC@PGADB)                      
         DC    C'DC',AL2(AC@PGADC-OUTLIST),AL1(L'AC@PGADC)                      
         DC    C'DD',AL2(AC@PGADD-OUTLIST),AL1(L'AC@PGADD)                      
         DC    C'DT',AL2(AC@PGADT-OUTLIST),AL1(L'AC@PGADT)                      
         DC    C'DU',AL2(AC@PGADU-OUTLIST),AL1(L'AC@PGADU)                      
         DC    C'DV',AL2(AC@PGADV-OUTLIST),AL1(L'AC@PGADV)                      
         DC    C'D5',AL2(AC@PGAD5-OUTLIST),AL1(L'AC@PGAD5)                      
         DC    C'HP',AL2(AC@PGAHP-OUTLIST),AL1(L'AC@PGAHP)                      
*&&UK*&& DC    C'TU',AL2(AC@PGATU-OUTLIST),AL1(L'AC@PGATU)                      
*&&UK*&& DC    C'TV',AL2(AC@PGATV-OUTLIST),AL1(L'AC@PGATV)                      
         DC    C'T1',AL2(AC@PGAT1-OUTLIST),AL1(L'AC@PGAT1)                      
         DC    C'21',AL2(AC@PGA21-OUTLIST),AL1(L'AC@PGA21)                      
*                                                                               
         DC    C'22',AL2(AC@PGA22-OUTLIST),AL1(L'AC@PGA22)                      
         DC    C'55',AL2(AC@PGA55-OUTLIST),AL1(L'AC@PGA55)                      
         DC    C'D0',AL2(AC@PGAD0-OUTLIST),AL1(L'AC@PGAD0)                      
*&&UK*&& DC    C'EB',AL2(AC@PGAEB-OUTLIST),AL1(L'AC@PGAEB)                      
*&&UK*&& DC    C'HD',AL2(AC@PGAHD-OUTLIST),AL1(L'AC@PGAHD)                      
         DC    C'5P',AL2(AC@PGA5P-OUTLIST),AL1(L'AC@PGA5P)                      
PRGTABX  DC    X'FF'                                                            
*                                                                               
ADD      DC    C'ADD    '          WORKER FILE CONSTANTS                        
CLOSE    DC    C'CLOSE  '                                                       
OPEN     DC    C'OPEN   '                                                       
PURGE    DC    C'PURGE  '                                                       
READ     DC    C'READ   '                                                       
WKFILE   DC    C'WKFILE '                                                       
CTFILE   DC    C'CTFILE '                                                       
ACCOUNT  DC    C'ACCOUNT'                                                       
ACCDIR   DC    C'ACCDIR '                                                       
*                                                                               
INLIST   DS    0C                                                               
         DCDDL AC#OTMIS,14,L                                                    
         DCDDL AC#TOTLS,10,L                                                    
         DCDDL AC#ITEMS,8,L                                                     
         DCDDL AC#ITEM,8,L                                                      
         DCDDL AC#CRTDB,10,L                                                    
         DCDDL AC#PGA55,25,L                                                    
         DCDDL AC#PGA5P,25,L                                                    
         DCDDL AC#PGADC,25,L                                                    
         DCDDL AC#PGAD0,25,L                                                    
         DCDDL AC#PGADB,25,L                                                    
         DCDDL AC#PGADD,25,L                                                    
         DCDDL AC#PGADT,25,L                                                    
         DCDDL AC#PGADU,25,L                                                    
         DCDDL AC#PGADV,25,L                                                    
         DCDDL AC#PGAD5,25,L                                                    
         DCDDL AC#PGAHP,25,L                                                    
*&&UK*&& DCDDL AC#PGATU,25,L                                                    
*&&UK*&& DCDDL AC#PGATV,25,L                                                    
         DCDDL AC#PGAT1,25,L                                                    
         DCDDL AC#PGA21,25,L                                                    
         DCDDL AC#PGA22,25,L                                                    
*&&UK*&& DCDDL AC#PGAEB,25,L                                                    
*&&UK*&& DCDDL AC#PGAHD,25,L                                                    
INLISTX  DC    X'00'                                                            
*                                                                               
ALLOWDUP EQU   X'01'               ALLOW DUPLICATE KEYS ON WORKER FILE          
*                                                                               
PSHDLENQ EQU   X'46'                                                            
PSSBLENQ EQU   X'1D'                                                            
         EJECT                                                                  
LDGTAB   DS    0C                  LEDGER TABLE                                 
*                                                                               
PROD     DC    C'SJ'               PRODUCTION LEDGER                            
PRODLENA DC    AL1(1)              PRODUCTION L'LEVEL A                         
PRODLENB DC    AL1(0)                                 B                         
PRODLENC DC    AL1(0)                                 C                         
         DC    AL1(0)                                                           
         DC    AL1(0)                                                           
LDGNTRY  EQU   *-LDGTAB            L'LEDGER TABLE ENTRY                         
*                                                                               
RECV     DC    C'SR'               RECEIVABLE LEDGER                            
         DC    AL1(0)                                                           
         DC    AL1(0)                                                           
         DC    AL1(0)                                                           
         DC    AL1(0)                                                           
         DC    AL1(0)                                                           
*                                                                               
COST     DC    C'1C'               COSTING LEDGER                               
         DC    AL1(0)                                                           
         DC    AL1(0)                                                           
         DC    AL1(0)                                                           
         DC    AL1(0)                                                           
         DC    AL1(0)                                                           
*                                                                               
VAT      DC    C'SG'               VAT LEDGER                                   
         DC    AL1(0)                                                           
         DC    AL1(0)                                                           
         DC    AL1(0)                                                           
         DC    AL1(0)                                                           
VATOFFP  DC    AL1(1)              OFFPOS IN VAT LEDGER                         
*                                                                               
INC      DC    C'SI'               INCOME LEDGER                                
         DC    AL1(0)                                                           
         DC    AL1(0)                                                           
         DC    AL1(0)                                                           
         DC    AL1(0)                                                           
INCOFFP  DC    AL1(1)              OFFPOS IN INCOME LEDGER                      
LDGNUM   EQU   (*-LDGTAB)/LDGNTRY                                               
         DC    X'FF'               E-O-T                                        
         SPACE 2                                                                
MISSTAB  DS    100CL(L'ACKEYACC-1) MISSING ACCOUNTS TABLE                       
MISSTLEN EQU   *-MISSTAB                                                        
         DC    X'FF'               E-O-T                                        
MISSNTRY EQU   L'MISSTAB                                                        
         SPACE 2                                                                
         DS    0D                  ALIGNED SAVED AREA                           
SAVEAREA DC    (SAVEDLEN)X'00'                                                  
         EJECT                                                                  
WINTSPEC DS    0X                  WINTER HEADING ACDEFS                        
         ACDEF SPROG,0,1,2,3,4,5                                                
         ACDEF H3,2,RUN                                                         
         ACDEF H3,87,PAGE                                                       
         ACDEF H6,2,AC#CPY,11,L                                                 
         ACDEF H6,14,COMPNAME                                                   
         ACDEF H7,2,AC#WKKEY,12,L                                               
         ACDEF SPROG,0                                                          
         ACDEF H10,2,AC#ACC,15,L                                                
         ACDEF H10,22,AC#ACC,15,L                                               
         ACDEF H10,42,AC#ACC,15,L                                               
         ACDEF H10,62,AC#ACC,15,L                                               
         ACDEF SPROG,1                                                          
         ACDEF H10,3,AC#ACC,15,L                                                
         ACDEF H10,23,AC#ACC,15,L                                               
         ACDEF H10,43,AC#ACC,15,L                                               
         ACDEF H10,63,AC#ACC,15,L                                               
         ACDEF SPROG,2,3                                                        
         ACDEF H10,2,AC#ACC,15,L                                                
         ACDEF H10,18,AC#CTR,15,L                                               
         ACDEF H10,34,AC#CODES,5,L                                              
         ACDEF H10,40,AC#DATE,8,L                                               
         ACDEF H10,49,AC#DRS,12,R                                               
         ACDEF H10,66,AC#CRS,12,R                                               
         ACDEF H10,81,AC#REF,8,L                                                
         ACDEF H10,90,AC#NRTV,20,L                                              
         ACDEF SPROG,4,5                                                        
         ACDEF H10,3,AC#ACC,15,L                                                
         ACDEF H10,19,AC#CTR,15,L                                               
         ACDEF H10,35,AC#CODES,5,L                                              
         ACDEF H10,41,AC#DATE,8,L                                               
         ACDEF H10,50,AC#DRS,12,R                                               
         ACDEF H10,67,AC#CRS,12,R                                               
         ACDEF H10,82,AC#REF,8,L                                                
         ACDEF H10,91,AC#NRTV,20,L                                              
         ACDEF SPROG,0,1                                                        
         ACDEF H3,41,AC#LSTMA,24,L                                              
         ACDEF H4,41,AC#LSTMA,24,LU                                             
         ACDEF SPROG,2,4                                                        
         ACDEF H3,41,AC#APOS,24,L                                               
         ACDEF H4,41,AC#APOS,24,LU                                              
         ACDEF SPROG,3,5                                                        
         ACDEF H3,41,AC#LSDAP,36,L                                              
         ACDEF H4,41,AC#LSDAP,36,LU                                             
         DC    X'00'               SPEC END MARKER                              
         SPACE 2                                                                
*                                                                               
IOAREAS  DC    2000X'00'           DEFAULT IO AREA                              
IOALEN   EQU   *-IOAREAS                                                        
         DC    2000X'00'           CLIENT RECORD IO AREA                        
         DC    2000X'00'           PRODUCT RECORD 1O AREA                       
         DC    2000X'00'           JOB IO AREA                                  
IOANUM   EQU   (*-IOAREAS)/(IOALEN)                                             
*                                                                               
WKBUFF   CSECT                                                                  
         DC    4096X'00'           4K WORKER BUFFER                             
         SPACE 2                                                                
         EJECT                                                                  
WINTERD  DSECT                                                                  
       ++INCLUDE ACWINTBLK                                                      
         DS    0D                                                               
WDUB     DS    D                                                                
*                                                                               
WDMCB    DS    0CL24               WINTER GENERAL PARAMETER LIST                
         DS    6F                  6 PARAMETERS                                 
*                                                                               
*DTF     DS    A                   A(ACCDIR/ACCOUNT DTF)                        
AWINTBLK DS    A                   A(WINTBLK)                                   
AIO      DS    A                   A(IO AREA FOR DATAMGR)                       
ASVSPEC  DS    F                   A(SAVED SPECS)                               
*                                                                               
ACCELS   DS    0F                  A(ACCOUNT RECORD ELEMENTS)                   
AACCCOMP DS    A                                                                
AACCLEDG DS    A                                                                
AACCHEIR DS    A                                                                
AACCNAME DS    A                                                                
AACCPROF DS    A                                                                
ACCELEN  EQU   *-ACCELS                                                         
*                                                                               
PSTELS   DS    0F                  A(POSTING RECORD ELEMENT)                    
APSTHEAD DS    A                                                                
APSTSUBF DS    A                                                                
APSTNAME DS    A                                                                
APSTTRNS DS    A                                                                
PSTELEN  EQU   *-PSTELS                                                         
*                                                                               
*NDS     DS    XL1                 INDICATORS                                   
*NDSEMUD EQU   X'80'               EMULATED ACCOUNT FILE                        
WKCMND   DS    CL7                 WORKER COMMAND                               
*                                                                               
BINREC   DS    0CL50               BINSRCH RECORD                               
BINKEY   DS    CL14                                                             
BINNAME  DS    CL36                                                             
BINRECL  EQU   *-BINREC                                                         
BINKEYL  EQU   L'BINKEY                                                         
*                                                                               
SAVEKEY  DS    XL64                SAVED KEY FROM ACCFIL DTF                    
ACCKEY   DS    CL42                ACCOUNT KEY FOR DATAMGR                      
SACCKEY  DS    CL42                SAVED ACCOUNT KEY FOR DATAMGR                
*                                                                               
SVHDHOOK DS    CL(L'HEADHOOK)      SAVED HEADHOOK VALUE                         
SVCLRHED DS    CL(L'CLEARHED)      SAVED CLEARHED VALUE                         
*                                                                               
OUTLIST  DS    0C                  DICTIONARY DS DECLARATIVES                   
         DSDDL PRINT=YES                                                        
*                                                                               
SAVEBOX  DS    (BOXL)X                                                          
*                                                                               
WINTERX  EQU   *                                                                
         EJECT                                                                  
SAVED    DSECT                     DSECT COVERS SAVED VALUES                    
*                                                                               
AWKBUFF  DS    A                   A(WORKER BUFFER)                             
*                                                                               
BINPARMS DS    0F                  BINSRCH PARAMETER LIST                       
BINP1    DS    F                                                                
BINP2    DS    F                                                                
BINP3    DS    F                                                                
BINP4    DS    F                                                                
BINP5    DS    F                                                                
BINP6    DS    F                                                                
BINPLEN  EQU   *-BINPARMS                                                       
*                                                                               
TOTCASH  DS    PL8                                                              
TOTDEBS  DS    PL8                                                              
TOTCRDS  DS    PL8                                                              
TOTRECS  DS    PL6                                                              
XTRAMISS DS    PL3                                                              
*                                                                               
ACCNAME  DS    CL36                CURRENT ACCOUNT NAME                         
CLIPROF  DS    CL44                CLIENT PROFILE                               
PROPROF  DS    CL44                PRODUCT PROFILE                              
PRUNITFA DS    C                   PRODUCTION UNIT FOR ANALYSIS                 
TODAYP   DS    X                   TODAY'S DAY PWOS                             
THISMOS  DS    CL2                 TODAY'S MOS (FOR BATCH REF)                  
*                                                                               
ORIGIDNO DS    CL(L'MCORIGID)      ORIGIN ID NUMBER                             
ORIGUSER DS    CL(L'MCUSERID)      ORIGIN USER ID                               
CTRY     DS    CL(L'MCCTRY)        COUNTRY                                      
LANG     DS    CL(L'MCLANG)        LANGUAGE                                     
*                                                                               
CLIKEY   DS    CL(L'ACKEYACC)      CLIENT KEY                                   
PROKEY   DS    CL(L'ACKEYACC)      PRODUCT KEY                                  
RCVKEY   DS    CL(L'ACKEYACC)      RECEIVABLE KEY                               
COSKEY   DS    CL(L'ACKEYACC)      COSTING KEY                                  
*                                                                               
LSTKEYS  DS    0C                                                               
LCLIKEY  DS    CL(L'ACKEYACC)      LAST CLIENT KEY                              
LPROKEY  DS    CL(L'ACKEYACC)      LAST PRODUCT KEY                             
LSTKEYLN EQU   *-LSTKEYS                                                        
*                                                                               
SAVEDLEN EQU   *-SAVED                                                          
         EJECT                                                                  
LDGTABD  DSECT                     DSECT COVERS LDGTAB ENTRIES                  
LDGCODE  DS    CL2                                                              
LDGLENA  DS    X                   L'LEVEL A, PRE-USED AS INDICATOR             
LDGLENB  DS    X                   L'LEVEL B                                    
LDGLENC  DS    X                   L'LEVEL C                                    
LDGLEND  DS    X                   L'LEVEL D                                    
LDGOFFP  DS    X                   OFFPOS, PRE-USED AS INDICATOR                
*                                                                               
PRGTABD  DSECT                     DSECT COVERS PRGTAB                          
PRGNUM   DS    CL2                 REPORT NUMBER                                
PRGNDSP  DS    AL2                 DISPLACEMENT TO REPORT NAME                  
PRGNLN   DS    AL1                 LENGTH OF REPORT NAME                        
PRGTBLQ  EQU   *-PRGTABD                                                        
*                                                                               
LINED    DSECT                     DSECT COVERS PRINT LINE (P)                  
         DS    C                                                                
LINACC   DS    CL(L'PSHDACC-1)     ACCOUNT                                      
         DS    C                                                                
LBXC1    DS    C                   (BOX)                                        
LINSBAC  DS    CL(L'PSHDSBAC-1)    CONTRA ACCOUNT                               
         DS    C                                                                
LBXC2    DS    C                   (BOX)                                        
LINKANAL DS    CL(L'PSHDANAL)      KEY ANALYSIS CODE                            
LINSLASH DS    C                                                                
LINTANAL DS    CL(L'TRNSANAL)      TRANSACTION ANALYSIS CODE                    
LBXC3    DS    C                   (BOX)                                        
LINDATE  DS    CL8                 TRANSACTION DATE                             
LBXC4    DS    C                   (BOX)                                        
LINDEB   DS    CL13                DEBIT AMOUNT                                 
         DS    C                                                                
LBXC5    DS    C                   (BOX)                                        
         DS    C                                                                
         DS    C                                                                
LINCRED  DS    CL13                CREDIT AMOUNT                                
         DS    C                                                                
LBXC6    DS    C                   (BOX)                                        
LINREF   DS    CL(L'TRNSREF)       REFERENCE                                    
         DS    CL2                                                              
LBXC7    DS    C                   (BOX)                                        
LINNARR  DS    CL21                NARRATIVE                                    
LBXR     DS    C                   (BOX RIGHT)                                  
         EJECT                                                                  
* ACGENBOTH                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENBOTH                                                      
         PRINT ON                                                               
* ACGENPOST                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENPOST                                                      
         PRINT ON                                                               
* ACREPWORKD                                                                    
         PRINT OFF                                                              
       ++INCLUDE ACREPWORKD                                                     
         PRINT ON                                                               
* CTGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE CTGENFILE                                                      
         PRINT ON                                                               
* DMDTFIS                                                                       
*********PRINT OFF                                                              
*********INCLUDE DMDTFIS                                                        
*********PRINT ON                                                               
* DDMASTD                                                                       
         PRINT OFF                                                              
       ++INCLUDE DDMASTD                                                        
         PRINT ON                                                               
* DDEBLOCK                                                                      
         PRINT OFF                                                              
       ++INCLUDE DDEBLOCK                                                       
         PRINT ON                                                               
* ACDDEQUS                                                                      
         PRINT OFF                                                              
       ++INCLUDE ACDDEQUS                                                       
         PRINT ON                                                               
* DDLANGEQUS                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDLANGEQUS                                                     
         PRINT ON                                                               
* DDBIGBOX                                                                      
         PRINT OFF                                                              
       ++INCLUDE DDBIGBOX                                                       
BOXL     EQU   *-BOXD                                                           
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'019ACWINTER  08/21/97'                                      
         END                                                                    
