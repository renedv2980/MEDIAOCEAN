*          DATA SET CPREPDM    AT LEVEL 005 AS OF 05/01/02                      
*CATALP CPDTAMGR                                                                
         TITLE 'MODULE TO AID DATAMGR CALLS'                                    
CPDMGR   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 21,**CPDM**                                                      
         USING CPDMD,RC                                                         
         L     RA,=V(CPWORKC)                                                   
         USING CPWORKD,RA                                                       
         LA    R9,2048(RA)                                                      
         LA    R9,2048(R9)                                                      
         USING CPWORKD+4096,R9                                                  
         MVI   DMCB+8,0            RESET ERROR IND                              
         SRL   RF,24                                                            
         ST    RF,SAVENUM                                                       
         BCTR  RF,0                                                             
         SLL   RF,2                                                             
         LA    R2,INFOLIST(RF)                                                  
         SR    R3,R3                                                            
         IC    R3,0(R2)                                                         
         SLL   R3,2                                                             
         B     BRANCH(R3)                                                       
         SPACE 2                                                                
BRANCH   B     DIRREAD                                                          
         B     DIRHIGH                                                          
         B     DIRSEQ                                                           
         B     DIRWRITE                                                         
         EJECT                                                                  
*              DIRECTORY READ/WRITE                                             
         SPACE 3                                                                
DIRREAD  MVC   COMMAND,DMREAD                                                   
         B     DIRLINK                                                          
         SPACE 2                                                                
DIRHIGH  MVC   COMMAND,DMRDHI                                                   
         MVC   KEYSAVE,KEY                                                      
         B     DIRLINK                                                          
         SPACE 2                                                                
DIRSEQ   MVC   COMMAND,DMRSEQ                                                   
         B     DIRLINK                                                          
         SPACE 2                                                                
DIRWRITE MVC   COMMAND,DMWRT                                                    
         CLI   RCWRITE,C'N'                                                     
         BE    DMEND                                                            
         SPACE 2                                                                
DIRLINK  MVC   TRACEKEY,KEY                                                     
         L     R4,=V(CPDTABUF)                                                  
         GOTO1 DATAMGR,DMCB,(DMINBTS,COMMAND),CPFILE,KEY,(R4),         X        
               (0,DMWORK)                                                       
         MVC   KEY,0(R4)                                                        
         B     DMCHECK                                                          
         EJECT                                                                  
*              CHECK FOR ERRORS                                                 
         SPACE 3                                                                
DMCHECK  MVC   BYTE,DMOUTBTS                                                    
         NC    BYTE,DMCB+8                                                      
         BZ    ANYTRACE                                                         
         MVC   DMERRMSG+5(2),RCPROG                                             
         MVC   DMERRMSG+7(2),RCAGENCY                                           
         OI    RCRQTOT+2,X'0F'                                                  
         UNPK  DMERRMSG+34(4),RCRQTOT                                           
         GOTO1 LOGIO,MYPARA,1,(40,DMERRMSG)                                     
*                                                                               
         MVC   P,SPACES                                                         
         MVC   P(40),DMERRMSG                                                   
         MVI   SKIPSPEC,C'Y'                                                    
         GOTO1 REPORT                                                           
         BAS   RE,DMTRACE                                                       
         MVI   MODE,DISKERR                                                     
         B     DMEND                                                            
         SPACE 2                                                                
ANYTRACE CLI   RCTRACE,C'Y'        TRACE OPTION                                 
         BNE   *+8                                                              
         BAS   RE,DMTRACE                                                       
         SPACE 2                                                                
DMEXT    DS    0H                                                               
         SPACE 2                                                                
DMEND    XMOD1 1                                                                
*                                                                               
DMERRMSG DC    CL40'* CP099XX * DATAMGR ERROR REQUEST 9999'                     
         EJECT                                                                  
*              TRACE FACILITY                                                   
         SPACE 3                                                                
DMTRACE  NTR1                                                                   
         MVC   P,SPACES                                                         
         MVC   P(8),COMMAND                                                     
         MVC   P+10(8),FILE                                                     
         LM    R2,R3,DMCB+8                                                     
         MVC   P+20(20),TRACEKEY                                                
         MVI   SKIPSPEC,C'Y'                                                    
         GOTO1 HEXOUT,MYPARA,TRACEKEY,P+45,20,=C'N'                             
         GOTO1 REPORT                                                           
         MVC   P+20(20),KEY                                                     
         GOTO1 HEXOUT,MYPARA,KEY                                                
         SPACE 2                                                                
DMTRACE4 GOTO1 HEXOUT,MYPARA,DMCB,P+100,1,=C'N'                                 
         GOTO1 (RF),(R1),DMCB+8,P+104                                           
         MVI   SKIPSPEC,C'Y'                                                    
         GOTO1 REPORT                                                           
         XIT1                                                                   
         EJECT                                                                  
*              INFORMATION LIST                                                 
         SPACE 3                                                                
INFOLIST DS    0F                                                               
         DC    AL1(0),AL3(0)       DIRECTORY I/O                                
         DC    AL1(1),AL3(0)                                                    
         DC    AL1(2),AL3(0)                                                    
         DC    AL1(3),AL3(0)                                                    
         DC    AL1(4),AL3(0)                                                    
         SPACE 1                                                                
         SPACE 3                                                                
*              DSECT FOR MODULE                                                 
         SPACE 3                                                                
CPDMD    DSECT                                                                  
MYPARA   DS    6F                                                               
MYWORK   DS    CL30                                                             
TRACEWRK DS    CL30                                                             
TRACEKEY DS    CL32                                                             
SAVENUM  DS    F                                                                
         PRINT OFF                                                              
       ++INCLUDE CPREPMODES                                                     
         EJECT                                                                  
       ++INCLUDE CPREPWORKD                                                     
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'005CPREPDM   05/01/02'                                      
         END                                                                    
