*          DATA SET DRIVHEADS  AT LEVEL 009 AS OF 05/01/02                      
*CATALP DRIVHEAD                                                                
         TITLE 'DRIVHEAD - DRIVER HEADLINE CONTROL'                             
DRIVHEAD CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,DRHEAD**,R9                                                    
         USING GLOBALD,RA                                                       
         L     RC,AOCOND                                                        
         USING DOUTD,RC                                                         
         EJECT                                                                  
*              HEAD HOOK ROUTINES                                               
         SPACE 3                                                                
         L     R5,GLAIO                                                         
         ZIC   R2,0(R5)            FROM RECORD NUMBER                           
         LTR   R2,R2               PROTECT FOR BUDGET (NO RECORDS)              
         BNZ   *+8                                                              
         LA    R2,1                                                             
         BCTR  R2,0                                                             
         SLL   R2,2                                                             
         LA    R2,GLAINTD(R2)                                                   
         L     R2,0(R2)                                                         
         USING GLINTD,R2                                                        
         L     R3,GLAFOUT          SO WE CAN GET TO THE OUTS                    
         SPACE 1                                                                
HOOK2    L     RE,AMYH1            PRECLEAR MY HEADLINES                        
         LA    R0,14                                                            
         SPACE 1                                                                
HOOK4    MVC   0(198,RE),SPACES                                                 
         LA    RE,198(RE)                                                       
         BCT   R0,HOOK4                                                         
         SPACE 1                                                                
         L     R6,ABOX             INITIALIZE BOXES                             
         USING BOXD,R6                                                          
         MVI   BOXINIT,0                                                        
         MVI   BOXWT,1                                                          
         MVI   BOXYORN,C'Y'                                                     
         MVI   BOXOFF,0                                                         
         CLI   GLBOXOPT,C'N'       OPTION NOT TO HAVE BOXES                     
         BNE   *+8                                                              
         MVI   BOXOFF,C'Y'                                                      
         MVI   BOXBLANK,C'N'                                                    
         MVC   BOXROWS,SPACES                                                   
         ZIC   R1,GLFHEADL         FIRST HEADLINE                               
         SR    RE,RE                                                            
         ICM   RE,1,GLFHLOVR       TEST FOR FIRST HEADLINE OVERRIDE             
         BZ    *+6                                                              
         LR    R1,RE                                                            
         SH    R1,=H'2'                                                         
         BM    HBOX2                                                            
         LA    R1,BOXROWS(R1)                                                   
         MVI   0(R1),C'T'                                                       
         SPACE 1                                                                
HBOX2    MVI   BOXROWS+58,C'B'                                                  
         MVC   BOXCOLS,SPACES      START OFF COLUMNS                            
         MVC   BOXCOLSR,SPACES                                                  
         LA    R6,BOXCOLS          USE R6 FOR BOXCOLS POINTER                   
         DROP  R6                                                               
         AH    R6,GLPDISP                                                       
         MVI   0(R6),C'L'          FIRST WILL BE AN L                           
         XC    AFCBOX,AFCBOX                                                    
         XC    CHUNKADD,CHUNKADD   CLEAR SWITCHES                               
         MVC   LASTHEAD,SPACES                                                  
         B     HOUT12                                                           
         EJECT                                                                  
*              OUT RELATED BOX ROUTINES                                         
         SPACE 3                                                                
HOUT12   CLI   0(R3),X'12'                                                      
         BNE   HOUT30                                                           
         TM    GLINDS2,GLEXTBOX    OPTION FOR ANOTHER BOX                       
         BNO   *+12                                                             
         MVI   0(R6),C'C'                                                       
         LA    R6,1(R6)                                                         
         ST    R6,AFCBOX           SAVE A(FIRST COLUMN BOX)                     
         B     HOOKEND                                                          
         SPACE 1                                                                
HOUT30   CLI   0(R3),X'30'                                                      
         BNE   HEAD                                                             
         ST    R3,LAST30                                                        
         USING DROD,R3                                                          
         MVC   LASTLTYP,DROLTYP    SAVE LINE TYPE FOR HEAD ROUTES               
         MVC   LASTNHED,DROHDFOL   SAVE NUMBER OF HEADS THAT FOLLOW             
         CLI   DROLTYP,C'P'                                                     
         BNE   HOUT33B             P OUTS DICTATE THE BOXES                     
         MVC   SAVOLEN,DROLEN      SAVE ODDMENTS FOR HEAD AND CHUNK             
         ST    R6,ALASTBXC                                                      
         CLI   0(R6),C'L'          (FIRST COLUMN)                               
         BE    HOUT32                                                           
         TM    DROFORM,X'02'       TEST FOLD SET ON THIS                        
         BZ    *+8                                                              
         MVI   0(R6),C'R'          YES-SET RIGHT HAND SIDE OF BOX               
         CLI   0(R6),C'R'          TEST RIGHT HAND SIDE SET YET                 
         BE    HOUT33              YES                                          
         TM    DROFORM,X'20'       OPTION TO SUPPRESS BOX TO LEFT               
         BO    HOUT32                                                           
         OC    CHUNKADD,CHUNKADD   (SEE IF CHUNK IS IN OPERATION)               
         BNZ   HOUT32                                                           
         MVI   0(R6),C'C'                                                       
         TM    GLNORBOX,X'80'      OPTION NOT TO HAVE ROW BOXES                 
         BNO   HOUT32                                                           
         OC    AFCBOX,AFCBOX                                                    
         BNZ   HOUT32                                                           
         MVI   0(R6),C' '                                                       
         SPACE 1                                                                
HOUT32   CLI   DROLINE,1           IF WE ARE ON LINE 1                          
         BH    HOUT33                                                           
         ZIC   R1,DROLEN           ADVANCE TO NEXT BOXCOLS                      
         AR    R6,R1                                                            
         ZIC   R1,GLGAP                                                         
         AR    R6,R1                                                            
         SPACE 1                                                                
HOUT33   C     R3,CHUNKADD         HAVE WE REACHED END OF CHUNK?                
         BNE   HOUT33B                                                          
         XC    CHUNKADD,CHUNKADD   YES SO CLEAR IT                              
         SPACE 1                                                                
HOUT33B  MVI   OUTYN,C'N'          CHECK FOR CONDITIONALS                       
         GOTO1 =V(DRIVTEST),DMCB,(RA),DROIFS                                    
         BNE   HOOKEND                                                          
         MVI   OUTYN,C'Y'                                                       
         CLI   DROLTYP,C'H'        ONLY INTERESTED IN HEADS                     
         BNE   HOOKEND                                                          
         EJECT                                                                  
*              DEAL WITH OUT RELATED TEXTS                                      
         SPACE 3                                                                
HOUT34   ST    R3,GLADTENT                                                      
         L     R5,DROAPOS          R5=A(OUTPUT)                                 
         CLI   DROELEN,140         DO WE NEED TO HANDLE A LITERAL               
         BE    HOUT36                                                           
         ZIC   R1,DROELEN          YES                                          
         SH    R1,=H'140'                                                       
         STC   R1,LITLEN           PASS ITS LENGTH                              
         LA    R1,DROLIT                                                        
         ST    R1,ALITIN           ITS ADDRESS                                  
         ST    R5,ALITOUT          AND WHERE IT GOES                            
         BAS   RE,DOLIT                                                         
         L     R5,ALITOUT          R5=A(NEXT SPACE)                             
         LA    R5,1(R5)            LEAVE A BLANK                                
         SPACE 1                                                                
HOUT36   ST    R5,GLAOFLD                                                       
         XC    GLAIFLD,GLAIFLD                                                  
         L     R1,DROIADD                                                       
         LTR   R1,R1                                                            
         BZ    HOUT38                                                           
         USING DRIND,R1                                                         
         LH    R1,DRINDISP                                                      
         DROP  R1                                                               
         A     R1,AACTREC                                                       
         ST    R1,GLAIFLD                                                       
         SPACE 1                                                                
HOUT38   OC    DRORADD,DRORADD     IS THERE A USER ROUTINE FOR THIS             
         BZ    HOUT40                                                           
         MVI   GLHOOK,GLROUT       YES SO SET UP FOR A HOOK                     
         MVC   GLLABEL,DROROUT                                                  
         MVC   GLAROUT,DRORADD                                                  
         MVC   GLARGS,DROARGS                                                   
         BAS   RE,GOHOOK                                                        
         B     HOOKEND                                                          
         SPACE 1                                                                
HOUT40   GOTO1 =V(DRIVFORM)                                                     
         B     HOOKEND                                                          
         EJECT                                                                  
*              DEAL WITH HEAD ELEMENTS                                          
         SPACE 3                                                                
HEAD     CLI   0(R3),X'40'                                                      
         BNE   CHUNK                                                            
         USING DRHDD,R3                                                         
         CLI   OUTYN,C'N'          IGNORE IF PREVIOUS OUT FAILED                
         BE    HOOKEND                                                          
         CLI   LASTLTYP,C'P'       ONLY INTERESTED IN P TYPE                    
         BNE   HOOKEND                                                          
         GOTO1 =V(DRIVTEST),DMCB,(RA),DRHDIFS                                   
         BNE   HOOKEND                                                          
         TM    DRHDSTAT,X'80'      IGNORE DELETED HEADS                         
         BO    HOOKEND                                                          
         SPACE 1                                                                
         ST    R3,GLADTENT                                                      
         L     R5,DRHDAPOS         R5=A(OUTPUT)                                 
         SR    R1,R1                                                            
         ICM   R1,1,GLFHLOVR       TEST FIRST HEAD LINE OVERRIDE                
         BZ    HEAD1                                                            
         ZIC   RE,GLFHEADL         YES - ADJUST R5 TO NEW A(OUTPUT)             
         SR    R1,RE                                                            
         BNP   HEAD1                                                            
         M     R0,=F'198'                                                       
         AR    R5,R1                                                            
*                                                                               
HEAD1    ST    R5,AHEAD            SAVE A(OUTPUT)                               
         CLI   DRHDELEN,56         DO WE NEED TO HANDLE A LITERAL               
         BE    HEAD2                                                            
         ZIC   R1,DRHDELEN         YES                                          
         SH    R1,=H'56'                                                        
         STC   R1,LITLEN           PASS ITS LENGTH                              
         LA    R1,DRHDLIT                                                       
         ST    R1,ALITIN           ITS ADDRESS                                  
         ST    R5,ALITOUT          AND WHERE IT GOES                            
         BAS   RE,DOLIT                                                         
         L     R5,ALITOUT                                                       
         LA    R5,1(R5)                                                         
         SPACE 1                                                                
HEAD2    ST    R5,GLAOFLD                                                       
         XC    GLAIFLD,GLAIFLD                                                  
         L     R1,LAST30                                                        
         USING DROD,R1                                                          
         L     R1,DROIADD          PICK UP A(IN) IF ANY                         
         LTR   R1,R1                                                            
         BZ    HEAD3                                                            
         USING DRIND,R1                                                         
         LH    R1,DRINDISP                                                      
         DROP  R1                                                               
         A     R1,AACTREC                                                       
         ST    R1,GLAIFLD          RECORD A(INPUT FIELD IN RECORD)              
         SPACE 1                                                                
HEAD3    OC    DRHDRADD,DRHDRADD   IS THERE A USER ROUTINE FOR THIS             
         BZ    HEAD4                                                            
         MVI   GLHOOK,GLROUT       YES SO SET UP FOR A HOOK                     
         MVC   GLLABEL,DRHDROUT                                                 
         MVC   GLAROUT,DRHDRADD                                                 
         MVC   GLARGS,DRHDARGS                                                  
         BAS   RE,GOHOOK                                                        
         ZIC   R1,SAVOLEN          TEST SECOND HEAD LINE FORMATTED              
         BCTR  R1,0                                                             
         L     R5,GLAOFLD                                                       
         EX    R1,CHK2LNS                                                       
         BNH   HEAD4                                                            
         MVI   LASTNHED,2          YES-INDICATE AT LEAST 2 LINES                
         SPACE 1                                                                
*                                  AUTO CHUNK ROUTINES                          
HEAD4    CLI   DRHDLINE,1          ONLY ON THE FIRST HEADING LINE               
         BNE   HEAD8                                                            
         CLI   GLAUTOCH,C'N'       USER CAN TURN OFF AUTOCHUNK                  
         BE    HEAD8                                                            
         L     R5,AHEAD                                                         
         CLI   SAVOLEN,0                                                        
         BE    HEAD5                                                            
         CLI   SAVOLEN,198                                                      
         BH    HEAD5                                                            
         ZIC   R1,SAVOLEN                                                       
         BCTR  R1,0                                                             
         CLC   LASTHEAD,SPACES                                                  
         BE    HEAD6                                                            
         CLI   LASTNHED,1          CHUNK ONLY OK IF AT LEAST 2 LINES            
         BE    HEAD6                                                            
         EX    R1,CHKAUTCH         CHECK IF AUTO CHUNK IS ON                    
         BNE   HEAD6                                                            
         L     RE,ALASTBXC         YES - SO PICK UP ADDRESS LAST 'C'            
         MVI   0(RE),C' '                AND CREAM IT                           
         L     R4,ALASTH1          R4=A(BEGINNING OF CHUNK)                     
         SR    R5,R4                                                            
         LA    R5,1(R1,R5)         R5=LENGTH                                    
         EX    R5,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),SPACES      CLEAR COMBINED SPACE                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),LASTHEAD    MOVE IN JOINT NAME TO LEFT                   
*                                  CENTER THIS INTO COMMUNAL SPACE              
         GOTO1 CENTER,DMCB,(R4),(R5)                                            
         B     HOOKEND                                                          
         SPACE 1                                                                
HEAD5    MVC   LASTHEAD,SPACES     SOMETHING WRONG                              
         XC    CHUNKADD,CHUNKADD   CLEAN UP                                     
         B     HEAD8                                                            
         SPACE 1                                                                
HEAD6    MVC   LASTHEAD,SPACES                                                  
         CLI   LASTNHED,1          CHUNK ONLY OK IF AT LEAST 2 LINES            
         BE    HEAD7                                                            
         EX    R1,SAVEHEAD         SAVE THIS HEADING FOR FUTURE                 
*                                  TEST FOR AUTO CHUNK                          
HEAD7    ST    R5,ALASTH1          ALSO SAVE ITS ADDRESS                        
         SPACE 1                                                                
HEAD8    CLI   DRHDALIN,C'L'       IF LEFT ALIGN, WE'RE DONE                    
         BE    HOOKEND                                                          
         L     R1,AHEAD                                                         
         ZIC   R0,DRHDWDTH                                                      
         LTR   R0,R0                                                            
         BZ    HOOKEND                                                          
         LR    RE,R0                                                            
         BCTR  RE,0                                                             
         SPACE 1                                                                
HOOK10   CLI   0(R1),C' '          LEFT ALIGN FIRST                             
         BNE   HOOK12                                                           
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R1),1(R1)                                                    
         BCT   R0,HOOK10                                                        
         SPACE 1                                                                
HOOK12   L     RF,CENTER           DEFAULT IS TO CENTER                         
         CLI   DRHDALIN,C'R'       OPTION TO RIGHT ALIGN                        
         BNE   *+8                                                              
         L     RF,RIGHT                                                         
         MVC   DMCB(4),AHEAD       PASS PRINT POSTION                           
         XC    DMCB+4(4),DMCB+4                                                 
         MVC   DMCB+7(1),DRHDWDTH  AND WIDTH                                    
         GOTO1 (RF),DMCB                                                        
         B     HOOKEND                                                          
         SPACE 2                                                                
CHKAUTCH CLC   0(0,R5),LASTHEAD    ** EXECUTED                                  
SAVEHEAD MVC   LASTHEAD(0),0(R5)                                                
CHK2LNS  CLC   198(0,R5),SPACES                                                 
         EJECT                                                                  
*              DEAL WITH CHUNK ELEMENTS                                         
         SPACE 3                                                                
CHUNK    CLI   0(R3),X'42'                                                      
         BNE   HOOKEND                                                          
         USING DRCHD,R3                                                         
         CLI   OUTYN,C'N'          IGNORE IF PREVIOUS OUT FAILED                
         BE    HOOKEND                                                          
         GOTO1 =V(DRIVTEST),DMCB,(RA),DRCHIFS                                   
         BNE   HOOKEND                                                          
         SPACE 1                                                                
         ST    R3,GLADTENT                                                      
         L     R5,DRCHAO1          R5=A(OUTPUT)                                 
         SR    R1,R1                                                            
         ICM   R1,1,GLFHLOVR       TEST FIRST HEAD LINE OVERRIDE                
         BZ    CHUNK1                                                           
         ZIC   RE,GLFHEADL         YES - ADJUST R5 TO NEW A(OUTPUT)             
         SR    R1,RE                                                            
         BNP   CHUNK1                                                           
         M     R0,=F'198'                                                       
         AR    R5,R1                                                            
*                                                                               
CHUNK1   ST    R5,ACHUNK           SAVE A(OUTPUT)                               
         CLI   DRCHELEN,72         DO WE NEED TO HANDLE A LITERAL               
         BE    CHUNK2                                                           
         ZIC   R1,DRCHELEN         YES                                          
         SH    R1,=H'72'                                                        
         STC   R1,LITLEN           PASS ITS LENGTH                              
         LA    R1,DRCHLIT                                                       
         ST    R1,ALITIN           ITS ADDRESS                                  
         ST    R5,ALITOUT          AND WHERE IT GOES                            
         BAS   RE,DOLIT                                                         
         L     R5,ALITOUT                                                       
         LA    R5,1(R5)                                                         
         SPACE 1                                                                
CHUNK2   ST    R5,GLAOFLD                                                       
         XC    GLAIFLD,GLAIFLD                                                  
         OC    DRCHRADD,DRCHRADD   IS THERE A USER ROUTINE FOR THIS             
         BZ    CHUNK4                                                           
         MVI   GLHOOK,GLROUT       YES SO SET UP FOR A HOOK                     
         MVC   GLLABEL,DRCHROUT                                                 
         MVC   GLAROUT,DRCHRADD                                                 
         MVC   GLARGS,DRCHARGS                                                  
         BAS   RE,GOHOOK                                                        
         SPACE 1                                                                
CHUNK4   L     R4,ACHUNK           CENTER EXPRESSION                            
         LH    R5,DRCHLEN                                                       
         GOTO1 CENTER,DMCB,(R4),(R5)                                            
         EJECT                                                                  
*              FINALLY MERGE INTO USER'S HEAD LINES                             
         SPACE 3                                                                
HOOKEND  ZIC   R1,1(R3)                                                         
         AR    R3,R1                                                            
         CLI   0(R3),X'00'                                                      
         BE    HOOKEND2                                                         
         CLI   0(R3),X'10'                                                      
         BNE   HOUT12                                                           
         SPACE 1                                                                
HOOKEND2 MVI   0(R6),C'R'          PUT IN RIGHT HAND SIDE OF BOX                
         L     R6,ABOX                                                          
         USING BOXD,R6                                                          
         MVC   SAVEBXCL,BOXCOLS    SAVE CURRENT BOX COLUMNS                     
         L     RF,AMYH1                                                         
         L     R3,GLAH1                                                         
         ZIC   R0,GLNHEAD                                                       
         L     R1,WIDTH                                                         
         SPACE 1                                                                
HOOKEND4 BAS   RE,HMERGE           MERGE IN HEAD LINES                          
         LA    RF,198(RF)                                                       
         A     R3,WIDTH                                                         
         BCT   R0,HOOKEND4                                                      
         SPACE 1                                                                
         MVI   GLHOOK,GLHEAD                                                    
         MVC   GLADTENT,GLAFOUT                                                 
         XC    GLAROUT,GLAROUT                                                  
         MVI   GLAROUT,1                                                        
         BAS   RE,GOHOOK           FINALLY HOOK TO APPLICATION                  
         MVI   GLAROUT,2                                                        
         BAS   RE,GOHOOK           AND SYSTEM CONTROLLER                        
         BAS   RE,SETLAST          SET BOX M FROM LAST HEADLINE                 
         B     XIT                                                              
         SPACE 1                                                                
HMERGE   NTR1                                                                   
         SPACE 1                                                                
HMERGE2  DS    0H                  MERGE IN SIGNIFICANT DATA                    
         CLI   0(RF),X'20'         CHECK  FOR DICTATE ESCAPE SEQUENCES          
         BL    HMERGE3                                                          
         CLI   0(RF),X'2D'                                                      
         BH    HMERGE3                                                          
         LA    RF,4                SET DEFAULT ESCAPE SEQUENCE LENGTH           
         CLI   0(RF),X'20'         CHECK FOR EXCEPTIONS                         
         BNE   *+8                                                              
         LA    RF,3                                                             
         CLI   0(RF),X'21'                                                      
         BNE   *+8                                                              
         LA    RF,3                                                             
         CLI   0(RF),X'26'                                                      
         BNE   *+8                                                              
         LA    RF,1                                                             
         CLI   0(RF),X'28'                                                      
         BNE   *+8                                                              
         LA    RF,3                                                             
         CLI   0(RF),X'29'                                                      
         BNE   *+8                                                              
         LA    RF,3                                                             
         BCTR  RF,0                DECREMENT FOR EXECUTE                        
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),0(RF)       MOVE ESCAPE SEQUENCE TO OUTPUT               
         LA    RF,1(RF,RF)         POINT TO NEXT INPUT  BYTE                    
         LA    R3,1(RF,R3)         POINT TO NEXT OUTPUT BYTE                    
         SR    R1,RF               DECREMENT INPUT COUNTER                      
         B     HMERGE4                                                          
*                                                                               
HMERGE3  CLI   0(RF),X'41'                                                      
         BL    *+10                                                             
         MVC   0(1,R3),0(RF)                                                    
         LA    RF,1(RF)                                                         
         LA    R3,1(R3)                                                         
HMERGE4  BCT   R1,HMERGE2                                                       
         B     XIT                                                              
         EJECT                                                                  
*              SET MIDDLE BOX COMMAND FROM LAST USED HEAD LINE                  
         SPACE 3                                                                
SETLAST  NTR1                                                                   
         L     R2,GLAH1            POSITION R2 TO LAST USER HEAD LINE           
         ZIC   R1,GLNHEAD                                                       
         L     R6,ABOX                                                          
         USING BOXD,R6                                                          
         LA    R3,BOXROWS(R1)      R3=LAST+1 IN BOXROWS                         
         DROP  R6                                                               
         BCTR  R1,0                                                             
         M     R0,WIDTH                                                         
         AR    R2,R1                                                            
         L     R4,WIDTH            R4=WIDTH-1                                   
         BCTR  R4,0                                                             
         SPACE 1                                                                
SETLAST2 EX    R4,SETLEX                                                        
         BNE   SETLAST4                                                         
         SR    R2,R4                                                            
         BCTR  R2,0                                                             
         BCT   R3,SETLAST2                                                      
         DC    H'0'                                                             
         SPACE 1                                                                
SETLAST4 MVI   0(R3),C'M'          PUT AN M INTO THE ROWS                       
         CLI   NUMMIDS,0                                                        
         BE    XIT                                                              
         MVI   0(R3),C'B'          IF MID LINES NEED TO PRINT                   
         ZIC   R1,NUMMIDS          END BOX BEFORE MIDLINES                      
         LA    R3,1(R1,R3)                                                      
         MVI   0(R3),C'T'          AND START A NEW BOX AFTER                    
         B     XIT                                                              
         SPACE 1                                                                
SETLEX   CLC   0(0,R2),SPACES                                                   
         SPACE 1                                                                
         EJECT                                                                  
       ++INCLUDE DRIVOSHR                                                       
*              LTORG                                                            
         SPACE 3                                                                
         LTORG                                                                  
         EJECT                                                                  
       ++INCLUDE DRIVOUTD                                                       
         PRINT OFF                                                              
       ++INCLUDE DRGLOBAL                                                       
       ++INCLUDE DRLOCAL                                                        
       ++INCLUDE DRINTRECD2                                                     
       ++INCLUDE DRIVETABLE                                                     
       ++INCLUDE DDBIGBOX                                                       
       ++INCLUDE DDBUFFALOD                                                     
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'009DRIVHEADS 05/01/02'                                      
         END                                                                    
