*          DATA SET SPBGSTATS  AT LEVEL 054 AS OF 05/01/02                      
*CATALP SPBGSTAT                                                                
         TITLE 'SPGBSTAT - BUY GUIDELINES STATUS RECORD UPDATE'                 
         PRINT NOGEN                                                            
BGSTAT   CSECT                                                                  
         NMOD1 BGWORKL,**BGSTAT                                                 
         L     R7,0(R1)                                                         
         USING BGWORKD,RC                                                       
         L     R3,4(R1)            A(TEXT LINE)                                 
         ST    R3,BGATXT                                                        
         USING GEND,R7                                                          
         L     R8,ASPOOLD                                                       
         USING SPOOLD,R8                                                        
         L     R9,ASYSD                                                         
         USING SYSD,R9                                                          
         L     RA,ATWA                                                          
         USING CONHEADH-64,RA      TWA                                          
         LA    R5,MINBLK                                                        
         USING MINBLKD,R5                                                       
*                                                                               
         MVC   LKRQEFFD,=C'971209'    EFF DATE FOR LOCKIN REQS                  
*                                     (REMOVE WHEN CONVENIENT)                  
         MVC   VRECUP,=V(RECUP)                                                 
         L     RF,ACOMFACS                                                      
         MVC   VMINIO,CMINIO-COMFACSD(RF)                                       
*                                                                               
         CLI   RPMODE,RPRUNLST      AT RUN LAST                                 
         BNE   BG03                                                             
         BAS   RE,REQPROC          CLOSE OUTPUT REQUEST FILE                    
         B     EXIT                                                             
*                                                                               
BG03     DS    0H                                                               
         MVI   BGRCTL,X'FF'        BITS TO CONTROL WHAT ERRS TO TRACK           
*                                  (CURRENTLY ALL SET ON)                       
         L     R3,BGATXT           A(TEXT LINE)                                 
         MVC   BGLINE,0(R3)                                                     
*                                                                               
         CLI   SBBPRD,0            BGR'S ONLY FOR SINGLE PRODUCT                
         BE    EXIT                                                             
         CLI   SBBPRD,X'FF'        NOT POL                                      
         BE    EXIT                                                             
         CLI   SBBEST,0            AND SINGLE ESTIMATE                          
         BE    EXIT                                                             
*                                                                               
         B     BG04                PATCH TO SEE INPUT LINES                     
         GOTO1 VPRINT,BGDMCB,BGLINE-1,=C'BL01'                                  
*                                                                               
BG04     DS    0H                                                               
         CLI   FRSTSW,0                                                         
         BNE   *+8                                                              
         BAS   RE,INIT                                                          
*                                                                               
         CLC   BGLINE(5),=C'** MA' MARKET SUMMARY IS END OF MARKET              
         BNE   BG06                                                             
         CLI   MINOPEN,C'Y'        OPEN IF NECESSARY                            
         BE    *+8                                                              
         BAS   RE,OPNPROC                                                       
         BAS   RE,BSTPROC          DO STATUS ELEM                               
         BAS   RE,CLSPROC          AND CLOSE                                    
         B     BG50                                                             
*                                                                               
BG06     DS    0H                  DETAIL LINE                                  
         CLC   BGLINE(4),=C'BUY='  BUY DESCRIPTOR OK                            
         BE    BG06B                                                            
         CLI   BGLINE,C'A'         ELSE 'RULE TYPE' A+ IS DAYPART               
         BNL   BG50                OF A MARKET SUMMARY LINE - SKIP IT           
BG06B    DS    0H                                                               
         CLI   MINOPEN,C'Y'        OPEN IF NECESSARY                            
         BE    BG06D                                                            
         BAS   RE,OPNPROC                                                       
         XC    ELSEQ,ELSEQ                                                      
*                                                                               
BG06D    DS    0H                                                               
         BAS   RE,LINPROC                                                       
*                                                                               
BG50     DS    0H                                                               
*                                                                               
EXIT     DS    0H                                                               
         XIT                                                                    
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        INITIALIZE MINIO, ETC.                                                 
*                                                                               
*        NOTE- RE-DONE FOR EACH REQ                                             
*                                                                               
***********************************************************************         
*                                                                               
INIT     NTR1                                                                   
         MVI   FRSTSW,1                                                         
*                                                                               
INIT02   DS    0H                                                               
         L     R3,TWAMASTC                                                      
         USING MASTD,R3                                                         
         MVC   BGWRITE,MCWRITE                                                  
         DROP  R3                                                               
*                                                                               
*        INITIALIZE MINIO VALUES                                                
*                                                                               
         LA    R0,MINBLK           CLEAR MINBLOCK                               
         LA    R1,MINBLKL                                                       
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         MVC   MINRECUP,VRECUP       A(RECUP)                                   
         MVC   MINCOMF,ACOMFACS      A(COMFACS)                                 
         MVI   MINOPEN,C'N'          SET NOT OPEN                               
         MVC   MINFIL,=CL8'XSPFIL'   FILE NAME                                  
         MVC   MINDIR,=CL8'XSPDIR'   DIR NAME                                   
         MVI   MINFKLEN,L'BGRKEY     KEY LENGTH                                 
         MVI   MINEKLEN,L'BGRKMINK   ELEMENT KEY LENGTH                         
         MVI   MINEKDSP,L'BGRKMAST   DISPLACEMENT TO ELEMENT KEY                
         MVI   MINNCTL,L'BGRDSTAT    NUMBER OF CONTROL BYTES                    
         MVC   MINFRCLM,MAXRECSZ     MAXIMUM RECORD LENGTH                      
*                                                                               
         L     RF,=A(MBUFF1)         POINT TO START OF MINIO BUFFERS            
         ST    RF,MINBUFF            A(FIRST BUFFER)                            
*                                                                               
         MVI   MINNBUF,2             USE TWO BUFFERS                            
*                                                                               
         L     RF,=A(MRTAB)                                                     
         ST    RF,MINRTAB            A(AREA FOR RECORD TABLE)                   
*                                                                               
         MVC   MINRTABL,=Y(L'MRTAB)  LENGTH OF RECORD TABLE                     
*                                                                               
         L     RF,=A(MELEM)          A(AREA FOR ELEM OR CLUSTER)                
         ST    RF,MINELEM                                                       
*                                                                               
         XC    0(L'MELEM,RF),0(RF)  CLEAR MINELEM AREA                          
*                                                                               
         MVC   MINMAXEL,=Y(L'MELEM)      MAX LENGTH OF ELEM OR CLUSTER          
*                                                                               
         MVI   MINDELSW,C'Y'       PROCESS DELETED RECORDS                      
*                                                                               
         MVC   MINWRITE,BGWRITE    SET WRITE SWITCH (Y,N)                       
*                                                                               
INITX    DS    0H                                                               
         XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        BUY STATUS PROC                                                        
*                                                                               
***********************************************************************         
*                                                                               
BSTPROC  NTR1                                                                   
         XC    MELEM,MELEM                                                      
         MVC   MELEM(BGRSTLNQ),SVGSTEL  USE SAVED STATUS ELM (IF ANY)           
         LA    R4,MELEM                                                         
         USING BGRSTELD,R4                                                      
*                                                                               
         MVI   BGRSTEL,BGRSTELQ                                                 
         MVI   BGRSTLEN,BGRSTLNQ                                                
         MVC   BGRSRDAT,SBQTODAY                                                
         MVC   BGRSTERS,BGDERRS                                                 
*                                                                               
         GOTO1 VMINIO,BGDMCB,('MINADD',MINBLKD)                                 
         CLI   MINERR,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         DROP  R4                                                               
*                                  ACTIVITY ELEMENT                             
         LA    R2,MELEM                                                         
         USING ACTVD,R2                                                         
         L     R3,TWAMASTC                                                      
         USING MASTD,R3                                                         
         CLI   NEWREC,C'Y'         IF NEW RECORD                                
         BE    BGT32               BUILD NEW ELEM                               
         XC    MINEKEY,MINEKEY     ELSE FIND ONE ALREADY THERE                  
         MVI   MINEKEY,X'F1'                                                    
         MVI   MINFILTL,1                                                       
         GOTO1 VMINIO,BGDMCB,('MINHI',MINBLKD)                                  
         CLI   MINERR,0            TREAT ERROR AS NOT FOUND                     
         BE    BGT34                                                            
*                                  NEW ACTIVITY ELEM                            
BGT32    DS    0H                                                               
         XC    MELEM(ACTVLENQ+1),MELEM                                          
         MVI   ACTVEL,X'F1'                                                     
         MVI   ACTVLEN,ACTVLENQ                                                 
         MVC   ACTVADDT,SBBTODAY                                                
         MVC   ACTVADID,MCORIGID                                                
*                                                                               
         GOTO1 VMINIO,BGDMCB,('MINADD',MINBLKD)                                 
         CLI   MINERR,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         B     BGT36                                                            
*                                                                               
BGT34    DS    0H                  UPDATE EXISTING ELEM                         
         MVC   ACTVCHDT,SBBTODAY                                                
         MVC   ACTVCHID,MCORIGID                                                
         GOTO1 VMINIO,BGDMCB,('MINWRT',MINBLKD)                                 
         CLI   MINERR,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         DROP  R2                                                               
         DROP  R3                                                               
*                                                                               
BGT36    DS    0H                                                               
*                                                                               
BGTX     DS    0H                                                               
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        LINE PROC                                                              
*                                                                               
***********************************************************************         
*                                                                               
LINPROC  NTR1                                                                   
         L     R3,BGATXT                                                        
         MVC   BGLINE,0(R3)         R3 POINTS TO LINE                           
*                                                                               
         CLC   BGLINE(4),=C'BUY='  BUY DESCRIPTOR                               
         BNE   LPR04                                                            
         CLC   SVBUYD,BGLINE       SKIP DUPE BUY DESCRIPTORS                    
         BE    LPRX                                                             
         MVC   SVBUYD,BGLINE                                                    
         MVI   BGELCODE,X'24'                                                   
         LA    R3,BGLINE                                                        
         MVI   RULTYPE,0           0=BUY DESCRIPTOR                             
         BAS   RE,GENEL                                                         
         B     LPRX                                                             
*                                                                               
LPR04    DS    0H                                                               
         CLI   BGLINE,41           RULE TYPE LESS THAN 41                       
         BNL   LPR6                                                             
         MVI   BGELCODE,X'24'      ARE BUY RULES, NOT GLOBALS                   
         LA    R3,BGLINE+2         TEXT IS AT +2                                
         MVC   RULTYPE,BGLINE      RULE TYPE IS FIRST BYTE                      
         BAS   RE,SETERR           SET BGDERRS BYTE                             
         BAS   RE,GENEL                                                         
         B     LPRX                                                             
*                                                                               
LPR6     DS    0H                                                               
         MVI   BGELCODE,X'24'      GLOBAL RULES                                 
         LA    R3,BGLINE+2         TEXT IS AT +2                                
         MVC   RULTYPE,BGLINE      RULE TYPE IS FIRST BYTE                      
         BAS   RE,SETERR           SET BGDERRS BYTE                             
         BAS   RE,GENEL                                                         
*                                                                               
LPRX     DS    0H                                                               
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        OPEN PROC                                                              
*                                                                               
***********************************************************************         
*                                                                               
OPNPROC  NTR1                                                                   
         XC    SVGSTEL,SVGSTEL     CLEAR BG STATUS ELEM SAVE                    
         XC    SVMSTEL,SVMSTEL     AND MIS                                      
*                                                                               
         XC    MINMKEY,MINMKEY                                                  
         LA    R2,MINMKEY                                                       
         USING BGRKEYD,R2                                                       
*                                                                               
         MVI   BGRKTYPE,BGRKTYPQ                                                
         MVI   BGRKSUB,BGRKSUBQ                                                 
         MVC   BGRKAM,SBBAGYMD                                                  
         MVC   BGRKCLT,SBBCLT                                                   
         MVC   BGRKPRD,SBBPRD                                                   
         MVC   BGRKEST,SBBEST                                                   
         MVC   BGRKMKT,SBBMKT                                                   
*                                                                               
         MVC   BGRDSTAT+2(2),AGENCY    AGENCY ALPHA                             
*                                                                               
         MVI   NEWREC,C'Y'                                                      
         MVI   FRSTADD,C'Y'                                                     
         MVI   BGDERRS,C'N'                                                     
         MVC   BGDERRS+1(L'BGDERRS-1),BGDERRS                                   
*                                                                               
         GOTO1 VMINIO,BGDMCB,('MINOPN',MINBLKD)                                 
         CLI   MINERR,0                                                         
         BNE   OPN08                                                            
         MVI   NEWREC,C'N'                                                      
*                            MINIO SET EXISTS, CLEAR OUT BG ELEMS               
         XC    MINEKEY,MINEKEY                                                  
         MVI   MINFILTL,0                                                       
*                                                                               
         GOTO1 VMINIO,BGDMCB,('MINHI',MINBLKD)                                  
         B     OPN06B                                                           
*                                                                               
OPN06    DS    0H                                                               
         GOTO1 VMINIO,BGDMCB,('MINSEQ',MINBLKD)                                 
OPN06B   DS    0H                                                               
         CLI   MINERR,0            TREAT ALL ERRS HERE AS EOF                   
         BNE   OPN08                                                            
         CLI   MELEM,BGRTXGEQ      DELETE ERROR TEXT ELEMS                      
         BE    OPN07                                                            
         CLI   MELEM,BGRMSELQ      SAVE MIS STATUS ELEM                         
         BNE   OPN06D                                                           
         MVC   SVMSTEL,MELEM                                                    
         B     OPN06                                                            
*                                                                               
OPN06D   DS    0H                                                               
         CLI   MELEM,BGRSTELQ      DELETE BG STATUS ELEM                        
         BNE   OPN06                                                            
         MVC   SVGSTEL,MELEM       BUT SAVE IT FOR LATER                        
*                                                                               
OPN07    DS    0H                                                               
         GOTO1 VMINIO,BGDMCB,('MINDEL',MINBLKD)                                 
         CLI   MINERR,0                                                         
         BE    OPN06                                                            
         DC    H'0'                                                             
*                                                                               
OPN08    DS    0H                                                               
OPNPX    DS    0H                                                               
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        CLOSE PROC                                                             
*                                                                               
***********************************************************************         
*                                                                               
CLSPROC  NTR1                                                                   
         XC    MINEKEY,MINEKEY                                                  
         MVI   MINEKEY,BGRSTELQ                                                 
         MVI   MINFILTL,1                                                       
         GOTO1 VMINIO,BGDMCB,('MINHI',MINBLKD)                                  
         CLI   MINERR,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    R4,MELEM                                                         
         USING BGRSTELD,R4                                                      
         MVC   BGRSTERS,BGDERRS    SET DISCREPANCY/ERROR BYTES                  
*                                                                               
         BC    0,CLSP01D             ***RE-OP AUTO APPROVAL                     
*                                                                               
         CLI   BGRSAST,C'R'        IF REJECTED, LEAVE AS IS                     
         BE    CLSP01D                                                          
         CLI   BGRSAST,C'A'        ALSO IF ALREADY APPROVED                     
         BE    CLSP01D                                                          
*                                  ELSE AUTO-APPROVE IF NO VIOLATIONS           
         LA    RF,BGRSTERS                                                      
         LA    R0,L'BGRSTERS                                                    
*                                                                               
CLSP01   DS    0H                                                               
         CLI   0(RF),C'Y'          RULE VIOLATED                                
         BE    CLSP01D             NO AUTO-APPROVAL                             
*                                                                               
         LA    RF,1(RF)                                                         
         BCT   R0,CLSP01                                                        
*                                                                               
         CLI   SVMSTEL,0      FOR AUTO APPROVE WE MUST HAVE MIS ELEM            
         BE    CLSP01D                                                          
         CLI   SVMSTEL+BGRMSTAT-BGRMSELD,0   AND HAVE NO ERRORS                 
         BNE   CLSP01D                                                          
*                                                                               
         MVI   BGRSAST,C'A'        SET APPROVED                                 
         MVC   BGRSAPER,SPACES                                                  
         MVC   BGRSAPER(7),=C'AUTO-BG'                                          
         MVC   BGRSADAT,SBQTODAY                                                
         CLC   BGRSADAT,LKRQEFFD    IF BEFORE LOCKIN REQUEST DATE               
         BL    CLSP01D                                                          
         TM    BGRSTAT,BGRSTLKQ    OR IF ALREADY DID LOCKIN REQ                 
         BNZ   CLSP01D             SKIP LOCKIN REQUEST                          
*                                                                               
         OI    BGRSTAT,BGRSTLKQ    SET HAVE DONE LOCKIN REQUEST                 
         BAS   RE,REQPROC          GENERATE REQUEST                             
*                                                                               
CLSP01D  DS    0H                                                               
         GOTO1 VMINIO,BGDMCB,('MINWRT',MINBLKD)                                 
         CLI   MINERR,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         B     CLSP50              PATCH TO SEE TRACE                           
*                                                                               
         XC    MINEKEY,MINEKEY                                                  
         MVI   MINFILTL,0                                                       
*                                                                               
CLSP02   DS    0H                                                               
         GOTO1 VMINIO,BGDMCB,('MINHI',MINBLKD)                                  
         B     CLSP03B                                                          
CLSP03   DS    0H                                                               
         GOTO1 VMINIO,BGDMCB,('MINSEQ',MINBLKD)                                 
CLSP03B  DS    0H                                                               
         CLI   MINERR,0                                                         
         BNE   CLSP50                                                           
*                                                                               
         L     R3,MINELEM                                                       
         CLI   0(R3),X'F1'         SKIP ACTIVITY                                
         BE    CLSP03                                                           
         MVC   BGLINE,SPACES                                                    
         GOTO1 HEXOUT,BGDMCB,0(R3),BGLINE,4,=C'N'                               
         CLI   0(R3),X'10'         STATUS ELEM                                  
         BNE   CLSP04                                                           
         MVC   BGLINE+9(26),4(R3)                                               
         GOTO1 VPRINT,BGDMCB,BGLINE-1,=C'BL01'                                  
         B     CLSP03                                                           
*                                                                               
CLSP04   DS    0H                                                               
         CLI   0(R3),X'20'         20+ ARE ERRORS                               
         BL    CLSP03                                                           
         MVC   BGLINE,SPACES                                                    
         GOTO1 HEXOUT,BGDMCB,0(R3),BGLINE,4,=C'N'                               
         ZIC   RF,1(R3)                                                         
         SH    RF,=Y(BGRTXTXT-BGRTXELD+1)                                       
         BM    CLSP03                                                           
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   BGLINE+9(0),BGRTXTXT-BGRTXELD(R3)                                
         GOTO1 VPRINT,BGDMCB,BGLINE-1,=C'BL01'                                  
         B     CLSP03                                                           
*                                                                               
CLSP50   DS    0H                                                               
         GOTO1 VMINIO,BGDMCB,('MINCLS',MINBLKD)                                 
         CLI   MINERR,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
CLSX     DS    0H                                                               
         B     EXIT                                                             
*                                                                               
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        REQPROC - CODE FOR LOCKIN REQUEST FILE                                 
*                                                                               
***********************************************************************         
*                                                                               
REQPROC  NTR1                                                                   
         L     R2,ATWA                                                          
         L     R2,TWADCONS-T204FFD(,R2)                                         
         L     R2,TSPFUSER-TWADCOND(,R2) GET DCB ADDRESS                        
         LA    R2,SPRULRDL(R2)           START IS USED FOR RULER                
         OC    0(100,R2),0(R2)           TEST FIRST TIME                        
         BNZ   REQP04                                                           
         MVC   0(128,R2),BGLKRQF   MOVE DCB                                     
*                                                                               
         OPEN ((R2),OUTPUT)                                                     
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
*                                                                               
REQP04   DS    0H                                                               
         CLI   RPMODE,RPRUNLST     RUNLAST                                      
         BNE   REQP05                                                           
*                                                                               
         MVC   RQAREA,SPACES                                                    
         MVC   RQAREA(2),=C'/*'                                                 
         PUT   (R2),RQAREA                                                      
         CLOSE ((R2))                                                           
         B     REQPX                                                            
*                                                                               
REQP05   DS    0H                                                               
         MVC   RQAREA,SPACES                                                    
         MVC   RQCODE,=C'ML'                                                    
         MVC   RQAGY,SBAGY                                                      
         MVC   RQMED,SBMED                                                      
         MVC   RQCLT,SBCLT                                                      
         MVC   RQPRD,SBPRD                                                      
         MVC   RQMKT,SBMKT                                                      
         MVC   RQEST,SBEST                                                      
         MVC   RQSTAUTO(2),=C'ES'                                               
         MVC   RQUESTOR(2),=C'Z5'                                               
*                                                                               
         PUT   (R2),RQAREA                                                      
*                                                                               
REQPX    DS    0H                                                               
         XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        GENEL - GENERIC TEXT ELEM ROUTINE                                      
*                                                                               
*                CREATE AN 'ELCODE' ELEM WITH DATA AT AT R3                     
*                                                                               
***********************************************************************         
*                                                                               
GENEL    NTR1                                                                   
         LA    RF,98(R3)        STRIP TRAILING BLANKS                           
         CLI   0(RF),C' '          98 IS TEXT LENGTH (100-2)                    
         BH    *+8                                                              
         BCT   RF,*-8                                                           
         LR    R0,R3                                                            
         SR    RF,R0                                                            
         BNM   GENL10                                                           
         MVI   0(R3),C'*'         BLANK LINE                                    
         SR    RF,RF                                                            
*                                                                               
GENL10   DS    0H                                                               
         LA    R4,MELEM                                                         
         USING BGRTXELD,R4                                                      
         XC    MELEM,MELEM                                                      
         MVC   BGRTXEL,BGELCODE    SET ELEM CODE                                
         LA    R0,BGRTXTXT-BGRTXEL+1(RF)   ELEM LENGTH                          
         STC   R0,BGRTXLEN                                                      
         MVC   BGRTXSEQ,ELSEQ      SEQUENCE                                     
         MVC   BGRTXRUL,RULTYPE    RULE TYPE                                    
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   BGRTXTXT(0),0(R3)                                                
*                                                                               
         CLI   BGWRITE,C'Y'        IF NOT WRITING TO FILE,                      
         BNE   GENL40              SKIP ADDS (TO AVOID MINIO PROBLEMS)          
*                                                                               
         GOTO1 VMINIO,BGDMCB,('MINADD',MINBLKD)                                 
         CLI   MINERR,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         CLI   FRSTADD,C'Y'        ON FIRST ADD                                 
         BNE   GENL40                                                           
         MVI   FRSTADD,C'N'                                                     
*                                                                               
         GOTO1 VMINIO,BGDMCB,('MINCLS',MINBLKD)   CLOSE AND OPEN TO             
         GOTO1 (RF),(R1),('MINOPN',MINBLKD)  PREVENT MINIO BUG WITH             
*                                            SPLITS OF NEW RECS                 
GENL40   DS    0H                                                               
         LH    RF,ELSEQ            BUMP ELEM SEQUENCE NUMBER                    
         LA    RF,1(RF)                                                         
         STH   RF,ELSEQ                                                         
*                                                                               
GENLX    DS    0H                                                               
         XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        SETERR- SET BGDERRS BYTE                                               
*                                                                               
***********************************************************************         
*                                                                               
SETERR   DS    0H                                                               
         LA    RF,ERRTAB                                                        
*                                                                               
SERR2    DS    0H                                                               
         CLI   0(RF),X'FF'         EOL                                          
         BER   RE                                                               
         CLC   RULTYPE,0(RF)                                                    
         BE    SERR4                                                            
         LA    RF,2(RF)                                                         
         B     SERR2                                                            
*                                                                               
SERR4    DS    0H                                                               
         ZIC   R1,1(RF)            DISPLACMENT INTO BGDERRS                     
         LA    RF,BGDERRS(R1)                                                   
         MVI   0(RF),C'Y'                                                       
         BR    RE                                                               
         SPACE 3                                                                
*        ERRTAB                                                                 
ERRTAB   DS    0X                                                               
         DC    AL1(SPRUBPGM,BGRSBPGM-BGRSTERS)                                  
         DC    AL1(SPRUBDST,BGRSBDST-BGRSTERS)                                  
         DC    AL1(SPRUBRTG,BGRSBRTG-BGRSTERS)                                  
         DC    AL1(SPRUBHOL,BGRSBHOL-BGRSTERS)                                  
         DC    AL1(SPRUBDTM,BGRSBDTM-BGRSTERS)                                  
         DC    AL1(SPRUBSPD,BGRSBSPD-BGRSTERS)                                  
         DC    AL1(SPRUGDST,BGRSGDST-BGRSTERS)                                  
         DC    AL1(SPRUGPGM,BGRSGPGM-BGRSTERS)                                  
         DC    AL1(SPRUGSPW,BGRSGSPW-BGRSTERS)                                  
         DC    X'FF'                                                            
*                                                                               
         LTORG                                                                  
         SPACE 2                                                                
*                                                                               
*        NOTE- DCB FOR LOCK-IN AUTO-REQUEST FILE                                
*              COPIED TO CORE-RESIDENT AREAF BEFORE OPEN                        
*                                                                               
BGLKRQF  DCB   DDNAME=BGLKRQF,                                         X        
               DSORG=PS,                                               X        
               RECFM=FB,                                               X        
               LRECL=80,                                               X        
               BLKSIZE=2000,                                           X        
               MACRF=PM                                                         
*                                                                               
FRSTSW   DC    X'00'                                                            
MAXRECSZ DC    H'3950'                                                          
SVBUYD   DC    CL20' '                                                          
         DC    C' '                                                             
BGLINE   DC    CL132' '                                                         
FRSTADD  DS    X                                                                
HADRUNL  DS    X                                                                
ELSEQ    DS    H                                                                
LKRQEFFD DS    CL6                                                              
RULTYPE  DS    X                                                                
VMINIO   DS    A                                                                
VRECUP   DS    A                                                                
NEWREC   DS    CL1                                                              
SVGSTEL  DS    XL(BGRSTLNQ)        SAVE BG STATUS ELEM                          
SVMSTEL  DS    XL(BGRMSLNQ)        SAVE MIS STATUS ELEM                         
BGB      DS    X                   WORK FIELDS                                  
BGH      DS    H                                                                
BGF      DS    F                                                                
BGWORK   DS    XL64                                                             
BGATXT   DS    F                                                                
BGDMCB   DS    6F                                                               
BGMODE   DS    CL1                                                              
BGRCTL   DS    CL1                                                              
BGELCODE DS    XL1                                                              
*                                                                               
BGDERRS  DS    CL20                RULE STATUSES - SEE BGRSTERS                 
BGWRITE  DS    CL1                 Y OR N                                       
*                                                                               
RQAREA   DS    0CL80                                                            
RQCODE   DS    CL2                 1                                            
RQAGY    DS    CL2                 3                                            
RQMED    DS    CL1                 5                                            
RQCLT    DS    CL3                 6                                            
         DS    CL3                                                              
RQPRD    DS    CL3                 12                                           
RQMKT    DS    CL4                                                              
RQSTA    DS    CL5                 19                                           
RQEST    DS    CL3                 24                                           
         DS    CL5                                                              
RQSTAUTO DS    CL3                 32                                           
RQPRD2   DS    CL3                 35                                           
RQSTART  DS    CL6                 38                                           
RQEND    DS    CL6                                                              
RQBOOK   DS    CL6                                                              
         DS    CL13                                                             
RQUESTOR DS    CL12                69                                           
*                                                                               
*                                                                               
         DS    0D                                                               
         DC    CL8'*MINBLK*'                                                    
MINBLK   DS    XL(MINBLKL)                                                      
         DS    0D                                                               
         DC    CL8'*MELEM**'                                                    
MELEM    DS    XL256                                                            
*                                                                               
         DS    0D                                                               
         DC    CL8'*MBUFF**'                                                    
MBUFF1   DS    XL4000                                                           
MBUFF2   DS    XL4000                                                           
         DS    0D                                                               
         DC    CL8'*MRTAB**'                                                    
MRTAB    DS    XL(200*(6+L'BGRKMAST))                                           
         SPACE 2                                                                
*                                                                               
BGWORKD  DSECT                                                                  
*                                                                               
BGWORKL  EQU   *-BGWORKD                                                        
         EJECT                                                                  
       ++INCLUDE SPGENBGR                                                       
         SPACE 2                                                                
       ++INCLUDE DDMINBLK                                                       
         EJECT                                                                  
       ++INCLUDE SPWRIFFD                                                       
         SPACE 2                                                                
       ++INCLUDE DDGENTWA                                                       
         SPACE 2                                                                
TWADCOND DSECT                                                                  
       ++INCLUDE DDTWADCONS                                                     
*                                                                               
*++INCLUDE DDACTIVD/SPWRIWORKD/DDSPOOLD/DDSPLWORKD/DDMASTD                      
*++INCLUDE DDCOMFACSD/SPRULERD                                                  
         PRINT OFF                                                              
       ++INCLUDE DDACTIVD                                                       
       ++INCLUDE SPWRIWORKD                                                     
       ++INCLUDE DDSPOOLD                                                       
       ++INCLUDE DDSPLWORKD                                                     
       ++INCLUDE DDMASTD                                                        
       ++INCLUDE DDCOMFACSD                                                     
       ++INCLUDE SPRULERD                                                       
*                                                                               
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'054SPBGSTATS 05/01/02'                                      
         END                                                                    
