*          DATA SET DDGETRAD   AT LEVEL 004 AS OF 01/31/13                      
*CATALP GETRAD                                                                  
         TITLE 'GET NAME CODES FROM CTFILE FOR RADIO UPLOAD'                    
         PRINT NOGEN                                                            
***********************************************************************         
* ROUTINE STORES RADIO UPLOAD RECORDS IN TABLES IN XA AND RETURNS THEM*         
* ON DEMAND. THERE ARE 4 TYPES OF RECORD STORED, AND HENCE 4 TABLES   *         
* NTRY:  P1 B0   = RECORD TYPE - C'M' - METRO                         *         
*                                C'O' - OWNER                         *         
*                                C'R' - REP                           *         
*                                C'F' - FORMAT                        *         
*                                C'P' - PARENT                        *         
*           B1-3 = A(CODE) (LENGTHS-METRO=3, OWNER=6, REP=4, FORMAT=3,*         
*                                   PARENT=5)                                   
*        P2 B0-3 = A(RETURN AREA FOR NAME)                            *         
* EXIT:  P2 B0   = 0    NAME FOUND AND RETURN AREA POPULATED          *         
*                  255  NAME NOT FOUND                                *         
***********************************************************************         
         SPACE 1                                                                
GETRAD   CSECT                                                                  
         NMOD1 WORKL,*GETRAD*,RA,CLEAR=YES                                      
         USING WORKD,RC                                                         
         ST    R1,SAVER1                                                        
         BRAS  RE,INIT                                                          
*                                                                               
         CLI   0(R1),C'M'                                                       
         BE    GETMET                                                           
         CLI   0(R1),C'O'                                                       
         BE    GETOWN                                                           
         CLI   0(R1),C'R'                                                       
         BE    GETREP                                                           
         CLI   0(R1),C'F'                                                       
         BE    GETFRM                                                           
         CLI   0(R1),C'P'                                                       
         BE    GETPAR                                                           
         DC    H'0'                BETTER LUCK NEXT TIME SONNY                  
         EJECT                                                                  
***********************************************************************         
* INITIALISATION                                                      *         
***********************************************************************         
         SPACE 1                                                                
INIT     NTR1  ,                                                                
         ICM   R0,15,AMETTAB                                                    
         BNZ   *+8                                                              
         BRAS  RE,BLDMET           BUILD METRO TABLE                            
         ICM   R0,15,AOWNTAB                                                    
         BNZ   *+8                                                              
         BRAS  RE,BLDOWN           BUILD OWNER TABLE                            
         ICM   R0,15,AREPTAB                                                    
         BNZ   *+8                                                              
         BRAS  RE,BLDREP           BUILD REPS TABLE                             
         ICM   R0,15,AFRMTAB                                                    
         BNZ   *+8                                                              
         BRAS  RE,BLDFRM           BUILD FORMAT TABLE                           
         ICM   R0,15,APARTAB                                                    
         BNZ   *+8                                                              
         BRAS  RE,BLDPAR           BUILD PARENT TABLE                           
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* GET METRO TABLE ENTRY FROM XA TABLE                                 *         
***********************************************************************         
         SPACE 1                                                                
GETMET   XC    WORK,WORK                                                        
         XR    RF,RF                                                            
         ICM   RF,7,1(R1)          SET UP RECORD KEY                            
         MVC   WORK(L'CT99KSRC+L'CT99KMET),0(RF)                                
         MVC   BINPARMS,METPARMS                                                
         MVI   BINPAR4,X'00'       FORCE DO NOT ADD                             
         GOTO1 ,BINPARMS,WORK                                                   
         BRAS  RE,BINSRCH                                                       
         L     R1,SAVER1                                                        
*                                                                               
         TM    BINPARMS,X'80'      TEST RECORD FOUND                            
         BZ    *+12                                                             
         MVI   4(R1),255           ERROR RETURN                                 
         B     EXITL                                                            
*                                                                               
         MVI   4(R1),0             SET GOOD RETURN                              
         SAM31                                                                  
         L     R2,BINPAR1                                                       
         L     RF,4(R1)                                                         
         MVC   0(L'CMNAME,RF),L'CT99KSRC+L'CT99KMET(R2)                         
         SAM24                                                                  
         B     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
* GET FORMAT TABLE ENTRY FROM XA TABLE                                *         
***********************************************************************         
         SPACE 1                                                                
GETFRM   XC    WORK,WORK                                                        
         XR    RF,RF                                                            
         ICM   RF,7,1(R1)          SET UP RECORD KEY                            
         MVC   WORK(L'CT99KSRC+L'CT99KFRM),0(RF)                                
         MVC   BINPARMS,FRMPARMS                                                
         MVI   BINPAR4,X'00'       FORCE DO NOT ADD                             
         GOTO1 ,BINPARMS,WORK                                                   
         BRAS  RE,BINSRCH                                                       
         L     R1,SAVER1                                                        
*                                                                               
         TM    BINPARMS,X'80'      TEST RECORD FOUND                            
         BZ    *+12                                                             
         MVI   4(R1),255           ERROR RETURN                                 
         B     EXITL                                                            
*                                                                               
         MVI   4(R1),0             SET GOOD RETURN                              
         SAM31                                                                  
         L     R2,BINPAR1                                                       
         L     RF,4(R1)                                                         
         MVC   0(L'CFNAME,RF),L'CT99KSRC+L'CT99KFRM(R2)                         
         SAM24                                                                  
         B     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
* GET OWNER TABLE ENTRY FROM XA TABLE                                 *         
***********************************************************************         
         SPACE 1                                                                
GETOWN   XC    WORK,WORK                                                        
         XR    RF,RF                                                            
         ICM   RF,7,1(R1)          SET UP RECORD KEY                            
         MVC   WORK(L'CT99KSRC+L'CT99KOME+L'CT99KOWN),0(RF)                     
         MVC   BINPARMS,OWNPARMS                                                
         MVI   BINPAR4,X'00'       FORCE DO NOT ADD                             
         GOTO1 ,BINPARMS,WORK                                                   
         BRAS  RE,BINSRCH                                                       
         L     R1,SAVER1                                                        
*                                                                               
         TM    BINPARMS,X'80'      TEST RECORD FOUND                            
         BZ    *+12                                                             
         MVI   4(R1),255           ERROR RETURN                                 
         B     EXITL                                                            
*                                                                               
         MVI   4(R1),0             SET GOOD RETURN                              
         SAM31                                                                  
         L     R2,BINPAR1                                                       
         L     RF,4(R1)                                                         
         MVC   0(L'CONAME,RF),L'CT99KSRC+L'CT99KOME+L'CT99KOWN(R2)              
         B     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
* GET PARENT TABLE ENTRY FROM XA TABLE                                *         
***********************************************************************         
         SPACE 1                                                                
GETPAR   XC    WORK,WORK                                                        
         XR    RF,RF                                                            
         ICM   RF,7,1(R1)          SET UP RECORD KEY                            
         MVC   WORK(L'CT99KSRC+L'CT99KMMD),0(RF)                                
         MVC   BINPARMS,PARPARMS                                                
         MVI   BINPAR4,X'00'       FORCE DO NOT ADD                             
         GOTO1 ,BINPARMS,WORK                                                   
         BRAS  RE,BINSRCH                                                       
         L     R1,SAVER1                                                        
*                                                                               
         TM    BINPARMS,X'80'      TEST RECORD FOUND                            
         BZ    *+12                                                             
         MVI   4(R1),255           ERROR RETURN                                 
         B     EXITL                                                            
*                                                                               
         MVI   4(R1),0             SET GOOD RETURN                              
         SAM31                                                                  
         L     R2,BINPAR1                                                       
         L     RF,4(R1)                                                         
         MVC   0(L'CMMNAME,RF),L'CT99KSRC+L'CT99KMMD(R2)                        
         B     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
* GET REPS TABLE ENTRY FROM XA TABLE                                  *         
***********************************************************************         
         SPACE 1                                                                
GETREP   XC    WORK,WORK                                                        
         XR    RF,RF                                                            
         ICM   RF,7,1(R1)          SET UP RECORD KEY                            
         MVC   WORK(L'CT99KSRC+L'CT99KREP),0(RF)                                
         MVC   BINPARMS,REPPARMS                                                
         MVI   BINPAR4,X'00'       FORCE DO NOT ADD                             
         GOTO1 ,BINPARMS,WORK                                                   
         BRAS  RE,BINSRCH                                                       
         L     R1,SAVER1                                                        
*                                                                               
         TM    BINPARMS,X'80'      TEST RECORD FOUND                            
         BZ    *+12                                                             
         MVI   4(R1),255           ERROR RETURN                                 
         B     EXITL                                                            
*                                                                               
         MVI   4(R1),0             SET GOOD RETURN                              
         SAM31                                                                  
         L     R2,BINPAR1                                                       
         L     RF,4(R1)                                                         
         MVC   0(L'CREAME,RF),L'CT99KSRC+L'CT99KREP(R2)                         
         B     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
* BUILD METRO TABLE IN XA                                             *         
***********************************************************************         
         SPACE 1                                                                
BLDMET   NTR1  ,                                                                
         PROT  OFF                                                              
         MVC   AMETTAB+4(4),=CL4'INIT'                                          
         XC    BINPARMS,BINPARMS                                                
*                                                                               
         LA    R2,IO                                                            
         USING CT99RECD,R2                                                      
         XC    CT99KEY,CT99KEY                                                  
         MVI   CT99KTYP,CT99KTYQ                                                
         MVI   CT99KSUB,CT99KSME                                                
         BRAS  RE,GETCOUNT                                                      
         L     R0,FULL                                                          
         AHI   R0,10                                                            
         ST    R0,BINPAR6          MAX # OF RECORDS IN TABLE                    
*                                                                               
         MHI   R0,(L'CT99KSRC+L'CT99KMET+L'CMNAME+8)                            
         AHI   R0,4096+4095        ADD 4K AND ROUND TO NEXT 4K                  
         SRL   R0,12                                                            
         SLL   R0,12                                                            
         GETMAIN RU,LV=(0),LOC=(ANY,ANY),BNDRY=PAGE,KEY=8,SP=132                
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                 GETMAIN FAILURE - BUMMER                    
*                                                                               
         ST    R1,AMETTAB                                                       
         SAM31                                                                  
         MVC   0(8,R1),=CL8'METRO***'                                           
         MVC   1(24,R1),0(R1)                                                   
         SAM24                                                                  
         AHI   R1,32                                                            
         ST    R1,BINPAR2          SET A(TABLE)                                 
*                                                                               
         LHI   R0,L'CT99KSRC+L'CT99KMET+L'CMNAME                                
         ST    R0,BINPAR4          SET RECORD LENGTH                            
         LHI   R0,L'CT99KSRC+L'CT99KMET                                         
         ST    R0,BINPAR5          SET KEY LENGTH                               
*                                                                               
         LA    R2,IO                                                            
         USING CT99RECD,R2                                                      
         XC    CT99KEY,CT99KEY                                                  
         MVI   CT99KTYP,CT99KTYQ                                                
         MVI   CT99KSUB,CT99KSME                                                
         MVC   HALF,0(R2)                                                       
         GOTO1 VDMGR,DMCB,DMRDHI,CTFILE,IO,IO                                   
         B     BMT04                                                            
*                                                                               
BMT02    GOTO1 VDMGR,DMCB,DMRSEQ,CTFILE,IO,IO                                   
*                                                                               
BMT04    CLC   HALF,0(R2)                                                       
         BNE   BMT06                                                            
*                                                                               
         LA    R3,CT99DATA                                                      
         USING CMNAMD,R3                                                        
         CLI   CMNMEL,CMNMELQ                                                   
         BNE   BMT02                                                            
*                                                                               
         XC    WORK,WORK           BUILD RECORD TO ADD TO TABLE                 
         MVC   WORK(L'CT99KSRC),CT99KSRC                                        
         MVC   WORK+L'CT99KSRC(L'CT99KMET),CT99KMET                             
         MVC   WORK+L'CT99KSRC+L'CT99KMET(L'CMNAME),CMNAME                      
*                                                                               
         MVI   BINPAR4,X'01'       ADD TO TABLE                                 
         GOTO1 ,BINPARMS,WORK                                                   
         BRAS  RE,BINSRCH                                                       
         OC    0(4,R1),0(R1)                                                    
         BNZ   BMT02                                                            
         DC    H'0'                                                             
*                                                                               
BMT06    MVC   METPARMS,BINPARMS                                                
         MVC   AMETTAB+4(4),=CL4'GOOD'                                          
         PROT  ON                                                               
         B     EXIT                                                             
         DROP  R2,R3                                                            
         EJECT                                                                  
***********************************************************************         
* BUILD FORMAT TABLE IN XA                                            *         
***********************************************************************         
         SPACE 1                                                                
BLDFRM   NTR1  ,                                                                
         PROT  OFF                                                              
         MVC   AFRMTAB+4(4),=CL4'INIT'                                          
         XC    BINPARMS,BINPARMS                                                
*                                                                               
         LA    R2,IO                                                            
         USING CT99RECD,R2                                                      
         XC    CT99KEY,CT99KEY                                                  
         MVI   CT99KTYP,CT99KTYQ                                                
         MVI   CT99KSUB,CT99KSFM                                                
         BRAS  RE,GETCOUNT                                                      
         L     R0,FULL                                                          
         AHI   R0,10                                                            
         ST    R0,BINPAR6          MAX # OF RECORDS IN TABLE                    
*                                                                               
         MHI   R0,(L'CT99KSRC+L'CT99KFRM+L'CFNAME+8)                            
         AHI   R0,4096+4095        ADD 4K AND ROUND TO NEXT 4K                  
         SRL   R0,12                                                            
         SLL   R0,12                                                            
         GETMAIN RU,LV=(0),LOC=(ANY,ANY),BNDRY=PAGE,KEY=8,SP=132                
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                 GETMAIN FAILURE - BUMMER                    
*                                                                               
         ST    R1,AFRMTAB                                                       
         SAM31                                                                  
         MVC   0(8,R1),=CL8'FORMAT**'                                           
         MVC   1(24,R1),0(R1)                                                   
         SAM24                                                                  
         AHI   R1,32                                                            
         ST    R1,BINPAR2          SET A(TABLE)                                 
*                                                                               
         LHI   R0,L'CT99KSRC+L'CT99KFRM+L'CFNAME                                
         ST    R0,BINPAR4          SET RECORD LENGTH                            
         LHI   R0,L'CT99KSRC+L'CT99KFRM                                         
         ST    R0,BINPAR5          SET KEY LENGTH                               
*                                                                               
         LA    R2,IO                                                            
         USING CT99RECD,R2                                                      
         XC    CT99KEY,CT99KEY                                                  
         MVI   CT99KTYP,CT99KTYQ                                                
         MVI   CT99KSUB,CT99KSFM                                                
         MVC   HALF,0(R2)                                                       
         GOTO1 VDMGR,DMCB,DMRDHI,CTFILE,IO,IO                                   
         B     BFM04                                                            
*                                                                               
BFM02    GOTO1 VDMGR,DMCB,DMRSEQ,CTFILE,IO,IO                                   
*                                                                               
BFM04    CLC   HALF,0(R2)                                                       
         BNE   BFM06                                                            
*                                                                               
         LA    R3,CT99DATA                                                      
         USING CFNAMD,R3                                                        
         CLI   CFNMEL,CFNMELQ                                                   
         BNE   BFM02                                                            
*                                                                               
         XC    WORK,WORK           BUILD RECORD TO ADD TO TABLE                 
         MVC   WORK(L'CT99KSRC),CT99KSRC                                        
         MVC   WORK+L'CT99KSRC(L'CT99KFRM),CT99KFRM                             
         MVC   WORK+L'CT99KSRC+L'CT99KFRM(L'CFNAME),CFNAME                      
*                                                                               
         MVI   BINPAR4,X'01'       ADD TO TABLE                                 
         GOTO1 ,BINPARMS,WORK                                                   
         BRAS  RE,BINSRCH                                                       
         OC    0(4,R1),0(R1)                                                    
         BNZ   BFM02                                                            
         DC    H'0'                                                             
*                                                                               
BFM06    MVC   FRMPARMS,BINPARMS                                                
         MVC   AFRMTAB+4(4),=CL4'GOOD'                                          
         PROT  ON                                                               
         B     EXIT                                                             
         DROP  R2,R3                                                            
         EJECT                                                                  
***********************************************************************         
* BUILD OWNER TABLE IN XA                                             *         
***********************************************************************         
         SPACE 1                                                                
BLDOWN   NTR1  ,                                                                
         PROT  OFF                                                              
         MVC   AOWNTAB+4(4),=CL4'INIT'                                          
         XC    BINPARMS,BINPARMS                                                
*                                                                               
         LA    R2,IO                                                            
         USING CT99RECD,R2                                                      
         XC    CT99KEY,CT99KEY                                                  
         MVI   CT99KTYP,CT99KTYQ                                                
         MVI   CT99KSUB,CT99KSOW                                                
         BRAS  RE,GETCOUNT                                                      
         L     R0,FULL                                                          
         AHI   R0,10                                                            
         ST    R0,BINPAR6          MAX # OF RECORDS IN TABLE                    
*                                                                               
         MHI   R0,(L'CT99KSRC+L'CT99KOME+L'CT99KOWN+L'CONAME+8)                 
         AHI   R0,4096+4095        ADD 4K AND ROUND TO NEXT 4K                  
         SRL   R0,12                                                            
         SLL   R0,12                                                            
         GETMAIN RU,LV=(0),LOC=(ANY,ANY),BNDRY=PAGE,KEY=8,SP=132                
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                 GETMAIN FAILURE - BUMMER                    
*                                                                               
         ST    R1,AOWNTAB                                                       
         SAM31                                                                  
         MVC   0(8,R1),=CL8'OWNER***'                                           
         MVC   8(24,R1),0(R1)                                                   
         SAM24                                                                  
         AHI   R1,32                                                            
         ST    R1,BINPAR2          SET A(TABLE)                                 
*                                                                               
         LHI   R0,L'CT99KSRC+L'CT99KOME+L'CT99KOWN+L'CONAME                     
         ST    R0,BINPAR4          SET RECORD LENGTH                            
         LHI   R0,L'CT99KSRC+L'CT99KOME+L'CT99KOWN                              
         ST    R0,BINPAR5          SET KEY LENGTH                               
*                                                                               
         LA    R2,IO                                                            
         USING CT99RECD,R2                                                      
         XC    CT99KEY,CT99KEY                                                  
         MVI   CT99KTYP,CT99KTYQ                                                
         MVI   CT99KSUB,CT99KSOW                                                
         MVC   HALF,0(R2)                                                       
         GOTO1 VDMGR,DMCB,DMRDHI,CTFILE,IO,IO                                   
         B     BOW04                                                            
*                                                                               
BOW02    GOTO1 VDMGR,DMCB,DMRSEQ,CTFILE,IO,IO                                   
*                                                                               
BOW04    CLC   HALF,0(R2)                                                       
         BNE   BOW06                                                            
*                                                                               
         LA    R3,CT99DATA                                                      
         USING CONAMD,R3                                                        
         CLI   CONMEL,CONMELQ                                                   
         BNE   BOW02                                                            
*                                                                               
         XC    WORK,WORK           BUILD RECORD TO ADD TO TABLE                 
         MVC   WORK(L'CT99KSRC),CT99KSRC                                        
         MVC   WORK+L'CT99KSRC(L'CT99KOME+L'CT99KOWN),CT99KOME                  
         MVC   WORK+L'CT99KSRC+L'CT99KOME+L'CT99KOWN(L'CONAME),CONAME           
*                                                                               
         MVI   BINPAR4,X'01'       ADD TO TABLE                                 
         GOTO1 ,BINPARMS,WORK                                                   
         BRAS  RE,BINSRCH                                                       
         OC    0(4,R1),0(R1)                                                    
         BNZ   BOW02                                                            
         DC    H'0'                                                             
*                                                                               
BOW06    MVC   OWNPARMS,BINPARMS                                                
         MVC   AOWNTAB+4(4),=CL4'GOOD'                                          
         PROT  ON                                                               
         B     EXIT                                                             
         DROP  R2,R3                                                            
         EJECT                                                                  
***********************************************************************         
* BUILD PARENT TABLE IN XA                                            *         
***********************************************************************         
         SPACE 1                                                                
BLDPAR   NTR1  ,                                                                
         PROT  OFF                                                              
         MVC   APARTAB+4(4),=CL4'INIT'                                          
         XC    BINPARMS,BINPARMS                                                
*                                                                               
         LA    R2,IO                                                            
         USING CT99RECD,R2                                                      
         XC    CT99KEY,CT99KEY                                                  
         MVI   CT99KTYP,CT99KTYQ                                                
         MVI   CT99KSUB,CT99KSMM   PARENT ARE UNDER MULTIMEDIA SUBTYPE          
         BRAS  RE,GETCOUNT                                                      
         L     R0,FULL                                                          
         AHI   R0,10                                                            
         ST    R0,BINPAR6          MAX # OF RECORDS IN TABLE                    
*                                                                               
         MHI   R0,(L'CT99KSRC+L'CT99KMMD+L'CMMNAME+8)                           
         AHI   R0,4096+4095        ADD 4K AND ROUND TO NEXT 4K                  
         SRL   R0,12                                                            
         SLL   R0,12                                                            
         GETMAIN RU,LV=(0),LOC=(ANY,ANY),BNDRY=PAGE,KEY=8,SP=132                
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                 GETMAIN FAILURE - BUMMER                    
*                                                                               
         ST    R1,APARTAB                                                       
         SAM31                                                                  
         MVC   0(8,R1),=CL8'PARENT**'                                           
         MVC   8(24,R1),0(R1)                                                   
         SAM24                                                                  
         AHI   R1,32                                                            
         ST    R1,BINPAR2          SET A(TABLE)                                 
*                                                                               
         LHI   R0,L'CT99KSRC+L'CT99KMMD+L'CMMNAME                               
         ST    R0,BINPAR4          SET RECORD LENGTH                            
         LHI   R0,L'CT99KSRC+L'CT99KMMD                                         
         ST    R0,BINPAR5          SET KEY LENGTH                               
*                                                                               
         LA    R2,IO                                                            
         USING CT99RECD,R2                                                      
         XC    CT99KEY,CT99KEY                                                  
         MVI   CT99KTYP,CT99KTYQ                                                
         MVI   CT99KSUB,CT99KSMM                                                
         MVC   HALF,0(R2)                                                       
         GOTO1 VDMGR,DMCB,DMRDHI,CTFILE,IO,IO                                   
         B     BPA04                                                            
*                                                                               
BPA02    GOTO1 VDMGR,DMCB,DMRSEQ,CTFILE,IO,IO                                   
*                                                                               
BPA04    CLC   HALF,0(R2)                                                       
         BNE   BPA06                                                            
*                                                                               
         LA    R3,CT99DATA                                                      
         USING CMMNAMD,R3                                                       
         CLI   CMMNMEL,CMMNMELQ                                                 
         BNE   BPA02                                                            
*                                                                               
         XC    WORK,WORK           BUILD RECORD TO ADD TO TABLE                 
         MVC   WORK(L'CT99KSRC),CT99KSRC                                        
         MVC   WORK+L'CT99KSRC(L'CT99KMMD),CT99KMMD                             
         MVC   WORK+L'CT99KSRC+L'CT99KMMD(L'CMMNAME),CMMNAME                    
*                                                                               
         MVI   BINPAR4,X'01'       ADD TO TABLE                                 
         GOTO1 ,BINPARMS,WORK                                                   
         BRAS  RE,BINSRCH                                                       
         OC    0(4,R1),0(R1)                                                    
         BNZ   BPA02                                                            
         DC    H'0'                                                             
*                                                                               
BPA06    MVC   PARPARMS,BINPARMS                                                
         MVC   APARTAB+4(4),=CL4'GOOD'                                          
         PROT  ON                                                               
         B     EXIT                                                             
         DROP  R2,R3                                                            
         EJECT                                                                  
***********************************************************************         
* BUILD REPS TABLE IN XA                                              *         
***********************************************************************         
         SPACE 1                                                                
BLDREP   NTR1  ,                                                                
         PROT  OFF                                                              
         MVC   AREPTAB+4(4),=CL4'INIT'                                          
         XC    BINPARMS,BINPARMS                                                
*                                                                               
         LA    R2,IO                                                            
         USING CT99RECD,R2                                                      
         XC    CT99KEY,CT99KEY                                                  
         MVI   CT99KTYP,CT99KTYQ                                                
         MVI   CT99KSUB,CT99KSRE                                                
         BRAS  RE,GETCOUNT                                                      
         L     R0,FULL                                                          
         AHI   R0,10                                                            
         ST    R0,BINPAR6          MAX # OF RECORDS IN TABLE                    
*                                                                               
         MHI   R0,(L'CT99KSRC+L'CT99KREP+L'CREAME+8)                            
         AHI   R0,4096+4095        ADD 4K AND ROUND TO NEXT 4K                  
         SRL   R0,12                                                            
         SLL   R0,12                                                            
         GETMAIN RU,LV=(0),LOC=(ANY,ANY),BNDRY=PAGE,KEY=8,SP=132                
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                 GETMAIN FAILURE - BUMMER                    
*                                                                               
         ST    R1,AREPTAB                                                       
         SAM31                                                                  
         MVC   0(8,R1),=CL8'REPS****'                                           
         MVC   8(24,R1),0(R1)                                                   
         SAM24                                                                  
         AHI   R1,32                                                            
         ST    R1,BINPAR2          SET A(TABLE)                                 
*                                                                               
         LHI   R0,L'CT99KSRC+L'CT99KREP+L'CREAME                                
         ST    R0,BINPAR4          SET RECORD LENGTH                            
         LHI   R0,L'CT99KSRC+L'CT99KREP                                         
         ST    R0,BINPAR5          SET KEY LENGTH                               
*                                                                               
         LA    R2,IO                                                            
         USING CT99RECD,R2                                                      
         XC    CT99KEY,CT99KEY                                                  
         MVI   CT99KTYP,CT99KTYQ                                                
         MVI   CT99KSUB,CT99KSRE                                                
         MVC   HALF,0(R2)                                                       
         GOTO1 VDMGR,DMCB,DMRDHI,CTFILE,IO,IO                                   
         B     BRE04                                                            
*                                                                               
BRE02    GOTO1 VDMGR,DMCB,DMRSEQ,CTFILE,IO,IO                                   
*                                                                               
BRE04    CLC   HALF,0(R2)                                                       
         BNE   BRE06                                                            
*                                                                               
         LA    R3,CT99DATA                                                      
         USING CRENAMD,R3                                                       
         CLI   CREMEL,CREMELQ                                                   
         BNE   BRE02                                                            
*                                                                               
         XC    WORK,WORK           BUILD RECORD TO ADD TO TABLE                 
         MVC   WORK(L'CT99KSRC),CT99KSRC                                        
         MVC   WORK+L'CT99KSRC(L'CT99KREP),CT99KREP                             
         MVC   WORK+L'CT99KSRC+L'CT99KREP(L'CREAME),CREAME                      
*                                                                               
         MVI   BINPAR4,X'01'       ADD TO TABLE                                 
         GOTO1 ,BINPARMS,WORK                                                   
         BRAS  RE,BINSRCH                                                       
         OC    0(4,R1),0(R1)                                                    
         BNZ   BRE02                                                            
         DC    H'0'                                                             
*                                                                               
BRE06    MVC   REPPARMS,BINPARMS                                                
         MVC   AREPTAB+4(4),=CL4'GOOD'                                          
         PROT  ON                                                               
         B     EXIT                                                             
         DROP  R2,R3                                                            
         EJECT                                                                  
***********************************************************************         
* COUNT RECORDS OF A SPECIFIC TYPE/SUBTYPE                            *         
* NTRY: R2     = A(RECORD KEY)                                        *         
* EXIT: FULL   = COUNT OF RECORDS                                     *         
***********************************************************************         
         SPACE 1                                                                
GETCOUNT NTR1  ,                                                                
         XC    FULL,FULL                                                        
         MVC   HALF,0(R2)                                                       
         GOTO1 VDMGR,DMCB,DMRDHI,CTFILE,IO,IO                                   
         B     GCN04                                                            
*                                                                               
GCN02    GOTO1 VDMGR,DMCB,DMRSEQ,CTFILE,IO,IO                                   
*                                                                               
GCN04    CLC   HALF,0(R2)                                                       
         BNE   EXIT                                                             
         L     R0,FULL                                                          
         AHI   R0,1                                                             
         ST    R0,FULL                                                          
         B     GCN02                                                            
         EJECT                                                                  
***********************************************************************         
* NTRY: P1      A(RECORD TO BE ADDED OR FOUND)                        *         
*       P2      A(TABLE)                                              *         
*       P3      # RECORDS IN TABLE (UPDATED BY BINSRCH)               *         
*       P4 B0   X'00' = FIND A RECORD                                 *         
*               X'01' = INSERT IF NOT FOUND                           *         
*               X'02' = READ HIGH                                     *         
*               X'80' = DELETE                                        *         
*          B2-3 L'RECORD                                              *         
*       P5 B0   DISPLACEMENT OF KEY INTO RECORD                       *         
*          B1-3 L'KEY (MAX=255)                                       *         
*       P6      MAXIMUM # OF RECORDS IN TABLE                         *         
*                                                                     *         
* EXIT: P1 B0   X'80' = RECORD NOT FOUND                              *         
*          B0-3 A(RECORD FOUND) OR                                    *         
*               A(WHERE RECORD HAS BEEN INSERTED) OR                  *         
*               A(0) WHEN TABLE IS FULL                               *         
***********************************************************************         
         SPACE 1                                                                
BINSRCH  NTR1  ,                                                                
         SAM31                                                                  
         LM    R2,R3,BINPAR2       R2-->TABLE   R3-->COUNT                      
         LH    R4,BINPAR4+2        R4 --> RECORD LENGTH                         
         LH    R5,BINPAR5+2        R5 --> KEY LENGTH                            
         BCTR  R5,0                  -1 (FOR EX)                                
         XR    R6,R6                                                            
         ICM   R6,1,BINPAR5        R6 --> KEY DSPL                              
*                                                                               
         L     R7,BINPAR6          R7 --> MAX RECORDS                           
         L     R8,BINPAR1          R8 --> A(RECORD)                             
*                                                                               
         LTR   R3,R3               ANY DATA IN TABLE                            
         BNZ   BS1                  YES - SEARCH FOR EQUAL                      
         TM    BINPAR4,X'01'       INSERT IF NOT FOUND                          
         BO    AFRST                YES - ADD TO TABLE                          
*                                                                               
         XC    BINPAR1,BINPAR1                                                  
         OI    BINPAR1,X'80'       SET RECORD NOT FOUND                         
         B     BSEXIT                                                           
         EJECT                                                                  
***********************************************************************         
* ADD FIRST RECORD                                                    *         
***********************************************************************         
         SPACE 1                                                                
AFRST    LA    R3,1(R3)            BUMP RECORD COUNTER                          
         ST    R3,BINPAR3                                                       
         ST    R2,BINPAR1                                                       
*                                                                               
         LR    R3,R4                                                            
         LR    R9,R4                                                            
         MVCL  R2,R8               MOVE RECORD TO TABLE                         
         OI    BINPAR1,X'80'       SET RECORD NOT FOUND                         
         B     BSEXIT                                                           
         EJECT                                                                  
***********************************************************************         
* BINARY SEARCH ROUTINE INITIALISATION                                *         
***********************************************************************         
         SPACE 1                                                                
BS1      LR    R1,R3               CALCULATE END OF TABLE                       
         BCTR  R1,0                                                             
         MH    R1,BINPAR4+2                                                     
         AR    R1,R2               R1 = A(LAST RECORD)                          
*                                                                               
         LA    RF,0(R6,R8)         CALCULATE KEY DISPLACEMENTS                  
         LA    RA,0(R6,R2)                                                      
         LR    R0,R4               INCREMENT VALUE                              
*                                                                               
         CHI   R3,10               LT. 10 ENTRIES - LINEAR SEARCH               
         BH    BSS1                                                             
*                                                                               
         AR    R1,R6               BUMP BY KEY DISPLACEMENT                     
BS2      EX    R5,BSCMP                                                         
         BNE   BS3                                                              
         SR    R1,R6               MINUS KEY DISPLACEMENT                       
         LR    R2,RA                                                            
         SR    R2,R6               R2 = A(RECORD)                               
         B     SETOUTA                                                          
*                                                                               
BS3      BH    *+8                 PAST RECORD                                  
         BXLE  RA,R0,BS2                                                        
         SR    R1,R6               MINUS KEY DISPLACEMENT                       
         LR    R2,RA                                                            
         SR    R2,R6                                                            
         B     BS4                                                              
*                                                                               
BSCMP    CLC   0(0,RA),0(RF)                                                    
         EJECT                                                                  
***********************************************************************         
* BINARY SEARCH ROUTINE                                               *         
***********************************************************************         
         SPACE 1                                                                
BSS1     AR    R1,R4                                                            
         BCTR  R1,0                R1=A(END-1)                                  
         LR    R0,R4                                                            
         SR    R2,R0                                                            
         SR    R1,R2                                                            
         AR    R0,R0                                                            
         CR    R0,R1                                                            
         BNH   *-4                                                              
*                                                                               
         AR    R1,R2                                                            
         BASR  RE,0                                                             
         SRL   R0,1                                                             
         CR    R0,R4                                                            
         BL    BSS3                                                             
         BXH   R2,R0,BSS2                                                       
*                                                                               
         LA    RA,0(R6,R2)                                                      
         EX    R5,BSSCMP                                                        
         BE    SETOUTA             MATCHED                                      
         BHR   RE                  HIGH                                         
*                                                                               
BSS2     SR    R2,R0               LOW                                          
         BR    RE                                                               
*                                                                               
BSS3     LA    R1,1(R1)                                                         
         AR    R2,R4               R2=A(INSERTION POINT)                        
         B     BS4                                                              
*                                                                               
BSSCMP   CLC   0(0,RF),0(RA)                                                    
         EJECT                                                                  
***********************************************************************         
* RECORD NOT FOUND                                                    *         
***********************************************************************         
         SPACE 1                                                                
BS4      OI    BINPAR1,X'80'       SET RECORD NOT FOUND                         
         TM    BINPAR4,X'01'       ADD IF NOT FOUND?                            
         BZ    BSEXIT              NO                                           
*                                                                               
         MVI   BINPAR4,X'01'       SLIDE TABLE FOR INSERT                       
         L     R3,BINPAR3                                                       
         CHI   R3,10               DID SEQUENTIAL SEARCH?                       
         BH    *+6                 NO                                           
         AR    R1,R4                                                            
         LR    RF,R1                                                            
         CR    R3,R7               INPUT NO ENTRY LT. MAX NO ENTRY              
         BL    *+14                YES - ADD ENTRY                              
         XC    BINPAR1,BINPAR1     NO - RETURN END OF TABLE                     
         B     BSEXIT                                                           
*                                                                               
         CR    R2,RF               ANY ENTRIES IN TABLE?                        
         BE    AFRST               NO                                           
*                                                                               
         LR    RA,RF                                                            
         SR    RA,R2               LENGTH OF MOVE                               
         LR    RE,RF                                                            
         AR    RF,R4                                                            
*                                                                               
SLD1     CHI   RA,255                                                           
         BL    SLD2                                                             
         AHI   RE,-255                                                          
         AHI   RF,-255                                                          
         MVC   HOLD,0(RE)                                                       
         MVC   0(255,RF),HOLD                                                   
         AHI   RA,-255                                                          
         B     SLD1                                                             
*                                                                               
SLD2     SR    RE,RA                                                            
         SR    RF,RA                                                            
         LTR   RA,RA                                                            
         BZ    SLD3                                                             
         BCTR  RA,0                                                             
         EX    RA,SLDMVE                                                        
         EX    RA,SLDMVE1                                                       
SLD3     B     AFRST                                                            
*                                                                               
SLDMVE   MVC   HOLD(1),0(RE)                                                    
*                                                                               
SLDMVE1  MVC   0(1,RF),HOLD                                                     
         EJECT                                                                  
***********************************************************************         
* EXACT MATCH ON RECORD                                               *         
***********************************************************************         
         SPACE 1                                                                
SETOUTA  ST    R2,BINPAR1          SET RECORD ADDRESS                           
         NI    BINPAR1,X'7F'       TURN OFF NOT FOUND                           
*                                                                               
         TM    BINPAR4,X'80'       DELETE RECORD FROM TABLE?                    
         BZ    BSEXIT2             NO                                           
*                                                                               
         L     R3,BINPAR3          SET END OF TABLE ADDRESS                     
         BCTR  R3,0                DECREMENT FOR DELETED SLOT                   
         MH    R3,BINPAR4+2                                                     
         A     R3,BINPAR2          ADD TABLE ADDRESS                            
         SR    R3,R2               SET LENGTH TO MOVE                           
*                                                                               
         LR    R4,R2                                                            
         AH    R4,BINPAR4+2                                                     
         LR    R5,R3                                                            
         SH    R5,BINPAR4+2                                                     
         MVCL  R2,R4               CLOSE UP TABLE                               
*                                                                               
         L     R5,BINPAR3                                                       
         BCTR  R5,0                                                             
         ST    R5,BINPAR3                                                       
         EJECT                                                                  
***********************************************************************         
* EXIT POINT                                                          *         
***********************************************************************         
         SPACE 1                                                                
BSEXIT   TM    BINPAR4,X'02'       TEST READ HIGH                               
         BZ    EXIT                                                             
*                                                                               
         XC    BINPAR1,BINPAR1                                                  
         MH    R7,BINPAR4+2        CALCULATE TABLE END ADDRESS                  
         A     R7,BINPAR2                                                       
         BCTR  R7,0                                                             
         CLR   R2,R7               FZSA                                         
         BL    *+8                                                              
         B     BSEXIT2              YES - MAINTAIN REC NOT FOUND                
         ST    R2,BINPAR1                                                       
         NI    BINPAR1,X'7F'                                                    
*                                                                               
BSEXIT2  B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* USEFUL ROUTINES                                                     *         
***********************************************************************         
         SPACE 1                                                                
EXITOK   CR    RB,RB                                                            
         B     EXIT                                                             
*                                                                               
EXITL    CLI   *,255                                                            
         B     EXIT                                                             
*                                                                               
EXIT     XIT1  ,                                                                
         EJECT                                                                  
***********************************************************************         
* LITERALS AND CONSTANTS                                              *         
***********************************************************************         
         SPACE 1                                                                
         DS    0D                                                               
         DC    CL8'METRO==>'                                                    
AMETTAB  DC    A(0),A(0)                                                        
METPARMS DC    XL32'00'                                                         
         DC    CL8'OWNER==>'                                                    
AOWNTAB  DC    A(0),A(0)                                                        
OWNPARMS DC    XL32'00'                                                         
         DC    CL8'REPS===>'                                                    
AREPTAB  DC    A(0),A(0)                                                        
REPPARMS DC    XL32'00'                                                         
         DC    CL8'FORMAT=>'                                                    
AFRMTAB  DC    A(0),A(0)                                                        
FRMPARMS DC    XL32'00'                                                         
         DC    CL8'PARENT=>'                                                    
APARTAB  DC    A(0),A(0)                                                        
PARPARMS DC    XL32'00'                                                         
*                                                                               
         LTORG                                                                  
*                                                                               
VDMGR    DC    V(DATAMGR)                                                       
*                                                                               
CTFILE   DC    CL8'CTFILE  '                                                    
DMRDHI   DC    CL8'DMRDHI  '                                                    
DMRSEQ   DC    CL8'DMRSEQ  '                                                    
         EJECT                                                                  
***********************************************************************         
* WORKING STORAGE                                                     *         
***********************************************************************         
         SPACE 1                                                                
WORKD    DSECT                                                                  
DUB      DS    D                                                                
FULL     DS    F                                                                
SAVER1   DS    F                                                                
DMCB     DS    6F                                                               
*                                                                               
BINPARMS DS    0XL32                                                            
BINPAR1  DS    F                                                                
BINPAR2  DS    F                                                                
BINPAR3  DS    F                                                                
BINPAR4  DS    F                                                                
BINPAR5  DS    F                                                                
BINPAR6  DS    F                                                                
BINPAR7  DS    F                                                                
BINPAR8  DS    F                                                                
*                                                                               
HALF     DS    H                                                                
WORK     DS    XL80                                                             
HOLD     DS    CL256                                                            
IO       DS    1024X                                                            
WORKL    EQU   *-WORKD                                                          
         EJECT                                                                  
***********************************************************************         
* OTHER INCLUDED DSECTS                                               *         
***********************************************************************         
         SPACE 1                                                                
* CTGENRAD                                                                      
         PRINT OFF                                                              
       ++INCLUDE CTGENRAD                                                       
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'004DDGETRAD  01/31/13'                                      
         END                                                                    
