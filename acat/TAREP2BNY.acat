*          DATA SET TAREP2BNY  AT LEVEL 077 AS OF 02/05/14                      
*CATALP TA2BNY                                                                  
         TITLE 'TA2BNY - NY DISK'                                               
TA2BNY   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,TA2BNY,R7,R6,R5                                                
         L     RC,0(R1)            P1=A(GEND)                                   
         USING GEND,RC                                                          
         L     RA,4(R1)            P2=A(MYD)                                    
         USING MYD,RA                                                           
         L     R9,ASUBSYSD                                                      
         USING SUBSYSD,R9                                                       
         L     R8,ASPOOLD                                                       
         USING SPOOLD,R8                                                        
         CLI   0(R1),1             P1, BYTE 1 1=UC A(REC IN ASORTREC)           
         BE    NYPOST                                                           
         DC    H'0'                                                             
*                                                                               
NYPOST   DS    0H                                                               
         CLI   FIRSTIME,C'Y'                                                    
         BNE   PRTAPE                                                           
         L     R2,=A(FILEOUT)                                                   
         CLI   ACTEQU,ACTDOWN      DOWNLOADING?                                 
         BNE   *+8                 NO                                           
         L     R2,=A(TADOWN)       YES, USE TEMPORARY DATASET                   
         OPEN  ((2),OUTPUT)                                                     
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         MVI   FIRSTIME,C'N'                                                    
         ZAP   N1ERECS,=P'0'                                                    
         ZAP   N1WRECS,=P'0'                                                    
         ZAP   N1WTOT,=P'0'                                                     
         EJECT                                                                  
*                                                                               
*        PROCESS THE TAPE                                                       
*                                                                               
PRTAPE   DS    0H                                                               
         USING UCRD,R3                                                          
         L     R3,ASORTREC                                                      
         USING TNYTPD,R4                                                        
         LA    R4,TAPEIO                                                        
         MVC   0(128,R4),SPACES    CLEAR AREA WITH SPACES                       
         CLI   UCRTYPE,UCRTHEAD    HEADER RECORD                                
         BE    TNYTHEAD                                                         
         CLI   UCRTYPE,UCRTRAIL    TRAILER RECORD                               
         BE    TNYTTRAL                                                         
*                                  THIS IS AN INDIVIDUAL'S RECORD               
*                                                                               
         MVC   0(128,R4),SPACES    CLEAR AREA WITH SPACES                       
         MVI   TNYTIDN,TNYTID1     '1'                                          
         MVI   TNYTIDC,TNYTIDW     EMPLOYEE RECORD - 'W'                        
*                                                                               
         MVC   TNYTSSN,UCRSSN      SS NUMBER                                    
         LA    R1,TNYTSSNM         R1=A(LAST NAME ON TAPE)                      
         LA    R2,UCRLAST          R2=A(LAST NAME TO MOVE)                      
         LA    R0,L'UCRLAST        R0=(LENGTH OF LAST NAME)                     
         BAS   RE,MVCNME           MOVE CHARACTERS IN LAST NAME                 
*                                                                               
         LA    R1,TNYTSSNM+L'TNYTSSNM                                           
         BAS   RE,SHUFFLE          SHUFFLE BACK TO NON BLANK                    
         LA    R2,UCRFRST          R2=A(FIRST NAME TO MOVE)                     
         LA    R0,L'UCRFRST        R0=(LENGTH OF FIRST NAME)                    
         BAS   RE,MVCNME           MOVE CHARACTERS IN FIRST NAME                
*                                                                               
         MVI   TNYTWPIN,TNYTWPIW   WAGE/PENSION INDICATOR                       
         EDIT  (P8,UCRGROSQ),(14,TNYTWAGE),FILL=0     GRS FOR QTR               
         CLI   UCRSIGN,UCRFUDGE    IF NEED TO FUDGE QUARTER GROSS               
         BNE   *+10                                                             
         MVC   TNYTWAGE,=14C'0'    FUDGE IT                                     
         EDIT  (P8,UCRGROSY),(14,TNYTYTDW),FILL=0     GRS FOR YEAR              
         EDIT  (P8,UCRTAXY),(14,TNYTTTAX),FILL=0      TAX FOR YEAR              
*                                                                               
         CLI   UCRSIGN,C'N'        IF NEGATIVE                                  
         BE    *+12                                                             
         CLI   UCRSIGN,UCRERROR    OR IF ERROR CODE                             
         BNE   *+12                                                             
         BAS   RE,PRREP            PRINT OUT W/ERROR MESSAGE                    
         B     TNYTX               AND DON'T PUT TO TAPE                        
*                                                                               
         AP    N1WRECS,=P'1'       INCREMENT N'EMPLOYEES                        
         B     TNYTPUT                                                          
*                                                                               
MVCNME   DS    0H                                                               
MVCNME5  CLI   0(R2),C'A'          IF CHARACTER                                 
         BL    *+14                                                             
         MVC   0(1,R1),0(R2)       MVE CHARACTER TO TAPE                        
         LA    R1,1(R1)            BUMP TO NEXT CHARACTER ON TAPE               
         LA    R2,1(R2)            BUMP TO NEXT CHARACTER TO MOVE               
         BCT   R0,MVCNME5          LOOP                                         
         BR    RE                                                               
         EJECT                                                                  
*                                                                               
TNYTHEAD DS    0H                                                               
         BAS   RE,PROCHED          PROCESS HEADER INFO                          
         MVC   THISEMP,UCREMP                                                   
         ZAP   N1WRECS,=P'0'       CLEAR N'1W FOR THIS EMPLOYER                 
         MVI   TNYTIDN,TNYTID1     '1'                                          
         MVI   TNYTIDC,TNYTIDA     TRANSMITTER RECORD - 'A'                     
         GOTO1 DATCON,DMCB,(0,TGTODAY0),(X'20',WORK)                            
         MVC   TNYTCD(4),WORK+2     TAPE CREATION DATE(NEED MMDDYY)             
         MVC   TNYTCD+4(2),WORK                                                 
*                                                                               
         MVC   TNYTTIN,FEDID                   TRANSMITTER FEDID NUM            
         MVC   TNYTTMNM(L'NAMEFLD),NAMEFLD     TRANSMITTER NAME                 
         MVC   TNYTTMAD(L'EMPADDR),EMPADDR                 ADDRESS              
         MVC   TNYTTMCT(L'EMPCITY),EMPCITY                 CITY                 
         MVC   TNYTTMST(L'EMPSTATE),EMPSTATE               STATE                
         MVC   TNYTTMZP(L'EMPZIP),EMPZIP                   ZIP CODE             
         BAS   RE,PUTTAPE                                                       
*                                                                               
         MVC   0(128,R4),SPACES    CLEAR AREA WITH SPACES                       
         MVI   TNYTIDN,TNYTID1     '1'                                          
         MVI   TNYTIDC,TNYTIDE     EMPLOYER RECORD - C'E'                       
         LA    R2,TNYTEMM          SET QTR ENDING MONTH                         
         BAS   RE,SETMTH                                                        
         MVC   TNYTEYY,UCRRYR      DISPLAYABLE REPORTING YEAR                   
         MVC   TNYTEIN,FEDID       EMPLOYER FEDID NUMBER                        
         MVC   TNYTEMNM(L'NAMEFLD),NAMEFLD     EMPLOYER NAME                    
         MVC   TNYTEMAD(L'EMPADDR),EMPADDR              ADDRESS                 
         MVC   TNYTEMCT(L'EMPCITY),EMPCITY              CITY                    
         MVC   TNYTEMST(L'EMPSTATE),EMPSTATE            STATE                   
         MVC   TNYTEMZP(L'EMPZIP),EMPZIP                ZIP CODE                
         MVI   TNYTERET,TNYTEREO   TYPE OF RETURN (C'O' - ORIGINAL)             
         CLI   AMENDTAP,C'Y'                                                    
         BNE   *+8                                                              
         MVI   TNYTERET,TNYTEREA   TYPE OF RETURN (C'A' - AMENDED)              
         AP    N1ERECS,=P'1'                                                    
         B     TNYTPUT                                                          
         EJECT                                                                  
*                                                                               
TNYTTRAL DS    0H                                                               
         MVC   0(128,R4),SPACES    CLEAR AREA WITH SPACES                       
         MVI   TNYTIDN,TNYTID1     '1'                                          
         MVI   TNYTIDC,TNYTIDT     TOTAL RECORD - C'T'                          
         EDIT  (P3,N1WRECS),(7,TNYTTCNT),FILL=0    N'EMPLOYEES                  
         EDIT  (P8,UCRGROSQ),(14,TNYTTGRS),FILL=0   GRS FOR QTR                 
         EDIT  (P8,UCRGROSY),(14,TNYTTSUB),FILL=0   ANNUAL GRS                  
         EDIT  (P8,UCRTAXY),(14,TNYTTTAX),FILL=0    ANNUAL TAXES                
         AP    N1WTOT,N1WRECS      INCREMENT N'EMPLOYEES (FINAL COUNT)          
         BAS   RE,PUTTAPE                                                       
*                                                                               
         MVC   0(128,R4),SPACES    CLEAR AREA WITH SPACES                       
         MVI   TNYTIDN,TNYTID1     '1'                                          
         MVI   TNYTIDC,TNYTIDF     FINAL RECORD - C'F'                          
         EDIT  (P3,N1ERECS),(10,TNYTFCNT),FILL=0  N'EMPLOYERS                   
         EDIT  (P3,N1WTOT),(10,TNYTFECT),FILL=0   N'EMPLOYEES                   
         BAS   RE,PUTTAPE                                                       
         L     R2,=A(FILEOUT)                                                   
         CLI   ACTEQU,ACTDOWN      DOWNLOADING?                                 
         BNE   *+8                 NO                                           
         L     R2,=A(TADOWN)       YES, USE TEMPORARY DATASET                   
         CLOSE ((2))               CLOSE TAPE                                   
         CLI   ACTEQU,ACTDOWN      ARE WE DOWNLOADLING?                         
         BNE   XIT                 NO                                           
         BAS   RE,PREPD            YES, DO DOWNLOAD REPORT                      
         B     TNYTX                                                            
*                                                                               
TNYTPUT  BAS   RE,PUTTAPE                                                       
*                                                                               
TNYTX    B     XIT                                                              
         EJECT                                                                  
*                                                                               
*        ROUTINE TO PROCESS HEADER RECORD                                       
*                                                                               
PROCHED  NTR1                                                                   
*                                                                               
         L     R3,ASORTREC                                                      
         USING UCRD,R3                                                          
         MVI   HEADNAME,44         LENGTH OF FIELD & HEADER                     
         MVC   TGEMP,UCREMP                                                     
         CLC   RYEAR,=C'2004'                                                   
         BNL   PCH05                                                            
         CLC   TGEMP,=C'TP '                                                    
         BNE   PCH05                                                            
         MVC   TGEMP,=C'TP1'                                                    
*                                                                               
PCH05    GOTO1 RECVAL,DMCB,TLEMCDQ,(X'A8',0),HEADNAME                           
*                                                                               
         CLC   RYEAR,=C'2004'                                                   
         BNL   PCH05B                                                           
         CLC   TGEMP,=C'TP1'                                                    
         BNE   PCH05X                                                           
         MVC   TGEMP,=C'TP '                                                    
         B     PCH05X                                                           
*                                                                               
PCH05B   CLC   TGEMP,=C'TP '                                                    
         BNE   PCH05X                                                           
         MVI   HEADNAME+5,35                                                    
         MVC   HEADNAME+8(35),=C'TALENT PARTNERS COMMERCIAL SERVICES'           
*                                                                               
PCH05X   OC    NAMEFLD,SPACES                                                   
         USING TATID,R4                                                         
         MVI   ELCODE,TATIELQ                                                   
         MVI   WORK,TATITYUN       ELEMENT TYPE                                 
         MVC   WORK+1(3),=C'FD '   GET FED ID NUMBER                            
         GOTO1 GETL,DMCB,(4,WORK)                                               
         BNE   PCH20                                                            
         L     R4,TGELEM                                                        
         LA    R1,FEDID                                                         
         MVC   FEDID,SPACES        PAD WITH SPACES                              
         BAS   RE,SETID            TAKE OUT ALL BLANKS AND DASHES               
*                                                                               
PCH20    L     R4,AIO                                                           
         USING TAADD,R4                                                         
         MVI   ELCODE,TAADELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   PCHX                                                             
         ZIC   R1,TAADLNES         N'ADDRESS LINES                              
         MH    R1,=H'30'                                                        
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   ADDRESS(0),TAADADD                                               
*                                                                               
         BAS   RE,GETADD                                                        
         MVC   EMPADDR,BLOCK      STREET ADDRESS                                
         MVC   EMPCITY,BLOCK+90   CITY                                          
         MVC   EMPSTATE,DUB       STATE                                         
         MVC   EMPZIP,WORK                                                      
*                                                                               
PCHX     B     XIT                                                              
         EJECT                                                                  
*                                                                               
*        GET ADDRESS                                                            
*        INPUT  R4           - A(ADDRESS ELEMENT)                               
*        RETURN BLOCK        - STREET ADDRESS                                   
*               BLOCK+90     - CITY                                             
*               DUB          - STATE                                            
*               WORK         - ZIP CODE                                         
*                                                                               
         USING TAADD,R4                                                         
GETADD   NTR1                                                                   
         MVC   BLOCK(120),SPACES                                                
         MVC   WORK,SPACES                                                      
         MVC   DUB,SPACES                                                       
         LA    R3,TAADADD          A(ADDRESS)                                   
         ZIC   R2,2(R4)            N'LINES                                      
         BCTR  R2,0                                                             
         MH    R2,=H'30'                                                        
*                                                                               
         LA    R1,BLOCK                                                         
         MVC   0(30,R1),0(R3)      STREET ADDRESS                               
         LA    R1,30(R1)                                                        
         ST    R1,TGFULL                                                        
*                                                                               
GA10     LA    R4,30(R3)           NEXT LINE                                    
         GOTO1 =V(ADSCAN),DMCB,((R2),0(R4)),(30,BLOCK+90),DUB,(9,WORK)          
         CLI   0(R1),0             2 ACCEPTABLE RETURN CODES                    
         BE    GA20                                                             
         CLI   0(R1),2                                                          
         BNE   GWX                                                              
*                                                                               
GA20     CLC   BLOCK+90(30),SPACES     DID WE FIND CITY                         
         BH    GA30                                                             
         SH    R2,=H'30'           LESS 1 LINE                                  
         BNP   GA30                                                             
         LA    R3,30(R3)                                                        
         L     R1,TGFULL                                                        
         MVC   0(30,R1),0(R3)      STREET ADDRESS                               
         LA    R1,30(R1)                                                        
         ST    R1,TGFULL                                                        
         B     GA10                                                             
*                                                                               
GA30     GOTO1 SQUASHER,DMCB,BLOCK,90                                           
*                                                                               
GETADDX  B     XIT                                                              
         SPACE 2                                                                
*                                                                               
*        REMOVE BLANKS & DASHES FROM TAX ID                                     
*              BYTE   = R(IGHT)/L(EFT) PAD WITH ZEROES                          
*              TGELEM = TATI ELEMENT                                            
*              R1     = SAVED ID AREA                                           
*                                                                               
SETID    NTR1                                                                   
         LR    R3,R1                                                            
         USING TATID,R4                                                         
         L     R4,TGELEM                                                        
         LA    R1,L'TATIID                                                      
         LA    R2,TATIID                                                        
*                                                                               
SID10    CLI   0(R2),C' '          IGNORE BLANKS                                
         BNH   SID30                                                            
         CLI   0(R2),C'-'          AND DASHES                                   
         BE    SID30                                                            
         MVC   0(1,R3),0(R2)                                                    
         LA    R3,1(R3)                                                         
*                                                                               
SID30    LA    R2,1(R2)            BUMP                                         
         BCT   R1,SID10                                                         
         B     XIT                                                              
         EJECT                                                                  
*                                                                               
*        SET QUARTER ENDING MONTH                                               
*                                                                               
SETMTH   NTR1                                                                   
         MVC   0(2,R2),=C'03'      QUARTER ENDING MONTH                         
         CLI   QQUART,1                                                         
         BE    SMXIT                                                            
         MVC   0(2,R2),=C'06'                                                   
         CLI   QQUART,2                                                         
         BE    SMXIT                                                            
         MVC   0(2,R2),=C'09'                                                   
         CLI   QQUART,3                                                         
         BE    SMXIT                                                            
         MVC   0(2,R2),=C'12'                                                   
*                                                                               
SMXIT    B     XIT                                                              
         EJECT                                                                  
*                                                                               
*        GET W4 RECORD & GET PERFORMER'S ADDRESS                                
*        RETURN BLOCK(120)    - STREET ADDRESS                                  
*               BLOCK+90(30) - CITY                                             
*                                                                               
GETW4    NTR1                                                                   
         GOTO1 RECVAL,DMCB,TLW4CDQ,(X'A4',0)                                    
         BNE   GWX                                                              
         L     R4,AIO              IF NEW ADDRESS ELEMENT FOUND                 
         USING TAA2D,R4                                                         
         MVI   ELCODE,TAA2ELQ      USE IT                                       
         BAS   RE,GETEL                                                         
         BNE   GW10                                                             
         MVC   BLOCK(L'TAA2ADDR),TAA2ADDR        STREET ADDRESS                 
         GOTO1 SQUASHER,DMCB,BLOCK,90                                           
         MVC   BLOCK+90(L'TAA2CITY),TAA2CITY     CITY                           
         MVC   DUB(L'TAA2ST),TAA2ST              STATE                          
         MVC   WORK(L'TAA2ZIP),TAA2ZIP           ZIP CODE                       
         B     GWX                                                              
*                                                                               
GW10     L     R4,AIO                                                           
         USING TAADD,R4                                                         
         MVI   ELCODE,TAADELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   GWX                                                              
         BAS   RE,GETADD           GET ADDRESS                                  
*                                                                               
GWX      B     XIT                                                              
         EJECT                                                                  
*              PRINT OUT REPORT                                                 
         SPACE 2                                                                
PRREP    NTR1                                                                   
         USING TNYTPD,R4                                                        
         LA    R4,TAPEIO                                                        
         USING LINED,R2                                                         
         LA    R2,P                                                             
         L     R3,ASORTREC                                                      
         USING UCRD,R3             PRINT RECORD INFO                            
*                                                                               
         CLI   TNYTIDC,TNYTIDT     IF TOTAL RECORD  -  C'T'                     
         BNE   PRREP5                                                           
         BAS   RE,PRINTIT                                                       
         MVC   LSSNM(15),=C'TOTAL EMPLOYEES'                                    
         EDIT  (P3,N1WTOT),(10,LSSNM+16),ALIGN=LEFT,COMMAS=YES                  
         B     PRREP10                                                          
*                                                                               
PRREP5   CLI   TNYTIDC,TNYTIDW     OR IF DETAIL - C'W'                          
         BNE   PRREPX                                                           
         MVC   LSSN,TNYTSSN                                                     
         MVC   LSSNM,TNYTSSNM                                                   
         CLI   UCRSIGN,C'N'        IF NEGATIVE QUARTER                          
         BNE   *+10                                                             
         MVC   LTMSG,=C'**NEG AMTS - NOT PUT TO TAPE**'                         
         CLI   UCRSIGN,UCRFUDGE    IF FUDGED QUARTER GROSS                      
         BNE   *+10                                                             
         MVC   LTMSG,=C'**SET ZERO QTR - PUT TO TAPE**'                         
         CLI   UCRSIGN,UCRERROR    IF ERROR CODE                                
         BNE   *+10                                                             
         MVC   LTMSG,=CL30'**ERROR - NOT PUT TO TAPE**'                         
         CLI   UCRSIGN,UCRWARN     IF WARNING CODE                              
         BNE   *+10                                                             
         MVC   LTMSG,=CL30'**QTR GROSS, NO YTD GROSS**'                         
*                                                                               
PRREP10  EDIT  (P8,UCRGROSQ),(14,LWAGE),2,MINUS=YES GRS FOR QTR                 
         EDIT  (P8,UCRGROSY),(14,LYTDW),2,MINUS=YES GRS FOR YEAR                
         EDIT  (P8,UCRTAXY),(14,LTTAX),2,MINUS=YES  TAX FOR YEAR                
         BAS   RE,PRINTIT                                                       
*                                                                               
PRREPX   B     XIT                                                              
         DROP  R2,R3,R4                                                         
         EJECT                                                                  
*              ROUTINE TO GET FROM DATASET AND PRINT SECOND REPORT              
*                                                                               
PREPD    NTR1                                                                   
         BAS   RE,NEWPRTQ           SET NEW PRINT QUEUE REPORT                  
         GOTO1 REQTWA,DMCB,(3,ATWA),,VPRINT,(C'B',ABOX)                         
         CLI   DMCB+8,0                                                         
         BE    *+6                                                              
         DC    H'00'                                                            
         MVI   FORCEHED,C'Y'                                                    
         GOTO1 SPOOL,DMCB,(R8)                                                  
*                                                                               
         L     R2,AMASTD            DO NOT PRINT LOGOS                          
         USING MASTD,R2                                                         
         NI    MCPRTIND,X'FF'-MCPRTINL                                          
         DROP  R2                                                               
*                                                                               
         L     R2,=A(TADOWN)                                                    
         OPEN  ((2),INPUT)                                                      
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    R3,DLBLOCK                                                       
         USING DLCBD,R3                                                         
         BAS   RE,INITDWN                                                       
*                                                                               
PREPD2   GET   (R2),TAPEREC        GET FROM TEMP DATASET                        
         LA    R5,TAPEREC                                                       
         LA    R0,3                RECORD IS 128 BYTES LONG (40X3+8)            
*                                                                               
PREPD3   MVI   DLCBACT,DLCBPUT     PUT ITEM TO PRINT LINE                       
         MVI   DLCBTYP,DLCBTXT     DATA TYPE IS TEXT                            
*                                                                               
         MVI   DLCBLEN,40                                                       
         MVC   DLCBFLD(40),0(R5)    PASS DATA                                   
         GOTO1 =V(DLFLD),DLCBD                                                  
*                                                                               
         LA    R5,40(R5)           BUMP TO NEXT TAPE FIELD                      
         BCT   R0,PREPD3                                                        
*                                                                               
         MVI   DLCBACT,DLCBPUT     PUT ITEM TO PRINT LINE                       
         MVI   DLCBTYP,DLCBTXT     DATA TYPE IS TEXT                            
         MVI   DLCBLEN,8                                                        
         MVC   DLCBFLD(8),0(R5)    PASS DATA                                    
         GOTO1 =V(DLFLD),DLCBD                                                  
*                                                                               
         MVI   DLCBACT,DLCBEOL     END OF LINE                                  
         GOTO1 =V(DLFLD),DLCBD                                                  
         B     PREPD2                                                           
*                                                                               
NOMORE   CLOSE ((2))               CLOSE THE DATASET                            
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
         CLI   ACTNUM,ACTDOWN                                                   
         BE    NOMORE2                                                          
         L     R1,ALOGO            NO-OP SO DON'T END AGAIN                     
         MVC   0(2,R1),=X'07FE'                                                 
         B     XIT                                                              
NOMORE2  MVI   DLCBACT,DLCBEOR                                                  
         GOTO1 =V(DLFLD),DLCBD                                                  
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO SET UP SECOND REPORT ON QUEUE                         
*                                                                               
NEWPRTQ  NTR1                                                                   
         XC    SPECS,SPECS         CLEAR SPECS                                  
         XC    HEADHOOK,HEADHOOK   AND HEADLINE HOOK                            
         MVI   FORCEHED,C'Y'                                                    
         GOTO1 SPOOL,DMCB,(R8)                                                  
*                                                                               
         L     R2,ALOGOC                                                        
         USING LOGOD,R2                                                         
         MVI   LOGOTYPE,C'E'                                                    
         GOTO1 ALOGO,DMCB,(R2)     END REPORT LOGOS                             
*                                                                               
         TM    WHEN,X'20'          SOON?                                        
         BO    NPRT10                                                           
         GOTO1 VPRINT,DMCB,=C'CLOSE'                                            
*                                                                               
NPRT10   L     R2,ABOX                                                          
         USING BOXD,R2                                                          
         LA    RE,132                                                           
         ST    RE,BOXWIDTH         SET LENGTH OF PRINT LINE                     
*                                                                               
         L     RF,AMASTD                                                        
         USING MASTD,RF                                                         
         L     R2,AREMOT                                                        
         USING REMOTED,R2                                                       
         TM    WHEN,X'20'                                                       
         BZ    NPRT20                                                           
         XC    MCREMPQK,MCREMPQK                                                
         B     NPRT30                                                           
*                                                                               
NPRT20   MVC   REMOTABF,MCVPQBUF                                                
         MVC   REMOTADM,MCVDMGR                                                 
         MVC   REMOTAOP,MCVPQOPN                                                
         MVC   REMOTDST,MCDESTID                                                
         XC    MCALTREF,MCALTREF                                                
         MVI   REMOTCPY,C'1'                                                    
         MVI   REMOTCLS,C'Q'                                                    
         MVC   REMOTJID,=C'TND'                                                 
NPRT30   MVC   REMOTKEY(11),SPACES                                              
         MVC   REMOTSYS(6),=C'NYDATA'                                           
         MVC   REMOTFRM(4),=C'DATA'                                             
         B     XIT                                                              
         DROP  R2                                                               
         EJECT                                                                  
*              ROUTINE TO SET UP SECOND REPORT ON QUEUE                         
*                                                                               
INITDWN  NTR1                                                                   
         XC    DLCBD(DLCBXLX),DLCBD                                             
         MVI   DLCBACT,DLCBINIT    INITIALIZE FOR DOWNLOAD                      
         LA    R1,SPLATDWN         A(HOOK ROUTINE FOR PRINTING)                 
         ST    R1,DLCBAPR                                                       
         LA    R1,P                A(PRINT LINE)                                
         ST    R1,DLCBAPL                                                       
         MVC   DLCBAED,EDITOR                                                   
         MVC   DLCXMAXL,=Y(L'P)    MAXIMUM LENGTH OF PRINT LINE                 
         MVI   DLCXDELC,C' '       DELIMITER                                    
         MVI   DLCXEOTC,C'"'       TEXT DELIMITER                               
         MVI   DLCXEOTA,C''''      TEXT DELIMITER ALTERNATE                     
         MVI   DLCXEOLC,X'5E'      SEMI-COLON FOR END OF LINE                   
         MVI   DLCXEORC,C':'       END OF REPORT                                
         GOTO1 =V(DLFLD),DLCBD                                                  
         B     XIT                                                              
         EJECT                                                                  
*              USER SUPPLIED PRINT ROUTINE - DOWNLOAD                           
         SPACE 1                                                                
SPLATDWN NTR1                                                                   
         GOTO1 SPOOL,DMCB,(R8)                                                  
         MVI   LINE,1              PREVENT PAGE BREAK                           
         B     XIT                                                              
         SPACE 2                                                                
*                                                                               
         EJECT                                                                  
*                                                                               
*        SHUFFLE BACK TO 1ST NON BLANK                                          
*                ENTRY - R1 = END OF DATA                                       
*                EXIT  - R1 = 1ST NON BLANK + 1                                 
*                                                                               
         SPACE 1                                                                
SHUFFLE  CLI   0(R1),C' '          STOP AFTER 1ST BLANK                         
         BH    SHX                                                              
         BCT   R1,SHUFFLE          BUMP TO NEXT CHAR                            
*                                                                               
SHX      LA    R1,2(R1)            LEAVE 1 BLANK                                
         BR    RE                                                               
         SPACE 2                                                                
*                                                                               
*        PUT RECORD TO TAPE                                                     
*                                                                               
PUTTAPE  NTR1                                                                   
         BAS   RE,TRACEM                                                        
*                                                                               
PUTTAPE2 LA    R0,TAPEIO                                                        
         BAS   RE,PRREP            PRINT OUT THE REPORT                         
         CLI   WRITAPE,C'N'        DON'T WRITE TAPE                             
         BE    XIT                                                              
         L     R1,=A(FILEOUT)                                                   
         CLI   ACTEQU,ACTDOWN      DOWNLOADING?                                 
         BNE   *+8                 NO                                           
         L     R1,=A(TADOWN)       YES, USE TEMPRARY DATASET                    
         PUT   (1),(0)                                                          
         B     XIT                                                              
         EJECT                                                                  
*              ODD ROUTINES                                                     
         SPACE 2                                                                
TRACEM   NTR1                                                                   
         CLI   UTRACE,C'Y'                                                      
         BNE   XIT                                                              
         LA    R2,TAPEIO                                                        
         MVC   P(128),0(R2)                                                     
         MVI   SPACING,2                                                        
         BAS   RE,PRINTIT                                                       
         B     XIT                                                              
         SPACE 2                                                                
PRINTIT  NTR1                                                                   
*        CLI   ACTEQU,ACTDOWN                                                   
*        BE    XIT                                                              
         GOTO1 SPOOL,DMCB,(R8)                                                  
         MVC   P,SPACES                                                         
         MVI   SPACING,1                                                        
         B     XIT                                                              
         SPACE 3                                                                
YES      SR    R1,R1                                                            
         B     *+8                                                              
NO       LA    R1,1                                                             
         LTR   R1,R1                                                            
*                                                                               
XIT      XIT1                                                                   
         SPACE 3                                                                
         GETEL R4,DATADISP,ELCODE                                               
         EJECT                                                                  
*        CONSTANTS                                                              
         SPACE 1                                                                
*                                                                               
FIRSTIME DC    C'Y'                                                             
*                                                                               
*               FILEOUT IS NOW A TALDISK DATASET.                               
FILEOUT  DCB   DDNAME=FILEOUT,DSORG=PS,RECFM=FB,MACRF=PM,              X        
               LRECL=128,BLKSIZE=1280                                           
*                                                                               
TADOWN   DCB   DDNAME=TADOWN,DSORG=PS,RECFM=FB,MACRF=(GM,PM),          X        
               LRECL=128,BLKSIZE=1280,EODAD=NOMORE                              
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
N1ERECS  DS    PL3                 N'1E RECORDS ON TAPE                         
N1WRECS  DS    PL3                 N'1W RECORDS FOR THIS EMPLOYER               
N1WTOT   DS    PL3                 N'1W RECORDS ON TAPE                         
*                                                                               
FEDID    DS    CL12                SAVED ID NUMBER                              
*                                                                               
EMPADDR  DS    CL30                EMPLOYER STREET ADDRESS                      
EMPCITY  DS    CL25                         CITY                                
EMPSTATE DS    CL2                          STATE                               
EMPZIP   DS    CL9                          ZIP                                 
*                                                                               
HEADNAME DS    CL8                 FAKE HEADER FOR NAME                         
NAMEFLD  DS    CL36                NAME                                         
ADDRESS  DS    0CL90               ADDRESS                                      
ADDR1    DS    CL30                ADDRESS                                      
ADDR2    DS    CL30                                                             
ADDR3    DS    CL30                                                             
*                                                                               
DLBLOCK  DS    CL(DLCBXLX)                                                      
TAPEREC  DS    CL127                                                            
         EJECT                                                                  
       ++INCLUDE DDDLCB                                                         
       ++INCLUDE TAREP2BMY                                                      
       ++INCLUDE TAREP2BUCR                                                     
         EJECT                                                                  
       ++INCLUDE TANYTAPED                                                      
         EJECT                                                                  
LINED    DSECT                                                                  
         DS    CL1                                                              
LSSN     DS    CL9                                                              
         DS    CL1                                                              
LSSNM    DS    CL30                                                             
         DS    CL1                                                              
LWAGE    DS    CL14                                                             
         DS    CL1                                                              
LYTDW    DS    CL14                                                             
         DS    CL1                                                              
LTTAX    DS    CL14                                                             
         DS    CL1                                                              
LTMSG    DS    CL30                                                             
         EJECT                                                                  
*              OTHER DSECTS ARE HIDDEN IN HERE                                  
         SPACE 3                                                                
*TAREPWORKD                                                                     
*DDSPOOLD                                                                       
*DDSPLWORKD                                                                     
*DDREMOTED                                                                      
*DDMASTD                                                                        
*DDLOGOD                                                                        
*DDBIGBOX                                                                       
*TAGENFILE                                                                      
*DMPRTQL                                                                        
*TAREPFFD                                                                       
         PRINT OFF                                                              
       ++INCLUDE TAREPWORKD                                                     
         PRINT OFF                                                              
       ++INCLUDE DDSPOOLD                                                       
         PRINT OFF                                                              
       ++INCLUDE DDSPLWORKD                                                     
       ++INCLUDE DDREMOTED                                                      
       ++INCLUDE DDMASTD                                                        
       ++INCLUDE DDLOGOD                                                        
       ++INCLUDE DDBIGBOX                                                       
       ++INCLUDE TAGENFILE                                                      
       ++INCLUDE DMPRTQL                                                        
       ++INCLUDE TAREPFFD                                                       
TALIMD   DSECT                                                                  
       ++INCLUDE TALIMD                                                         
         PRINT ON                                                               
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'077TAREP2BNY 02/05/14'                                      
         END                                                                    
