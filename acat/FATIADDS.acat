*          DATA SET FATIADDS   AT LEVEL 012 AS OF 05/01/02                      
*CATALP FATIADDS                                                                
         TITLE 'TIADDS - ADDS TWX TERMINAL INPUT TRANSLATOR'                    
TIADDS   CSECT                                                                  
         DS    1200C                                                            
         ORG   *-1200                                                           
         PRINT NOGEN                                                            
         NMOD1 10,**TIADDS                                                      
         USING WORKD,RC                                                         
         LM    R2,R4,0(R1)         A(TIA)/A(TWA)/A(UTL)                         
         XC    0(8,R1),0(R1)                                                    
*                                                                               
         LR    RA,R3                                                            
         USING UTLD,R4                                                          
         ST    R4,SVUTLADR                                                      
         MVI   SRFLAG,0                                                         
         OC    TSVCREQ,TSVCREQ                                                  
         BZ    *+8                                                              
         MVI   SRFLAG,C'Y'                                                      
         DROP  R4                                                               
         EJECT                                                                  
         LA    R2,64(R2)                                                        
         LA    R3,64(R3)                                                        
*                                                                               
NEXTINP  DC    0H'0'                                                            
         CLC   0(2,R2),ESCY        TEST MOD FIELD START                         
         BNE   EXIT                                                             
         CLI   SRFLAG,C'Y'         TEST SVC REQ                                 
         BE    NEXTTWA             YES - MATCH SEQUENTIALLY                     
* WORK OUT ROW/COL - SAVE IN DUB                                                
         ZIC   RE,2(R2)            GET ROW                                      
         IC    RE,ADDRTAB(RE)      GET ROW NUMBER                               
         BCTR  RE,0                                                             
         MH    RE,=H'80'                                                        
         ZIC   RF,3(R2)            GET COL                                      
         IC    RF,ADDRTAB(RF)      GET COL NUMBER                               
         BCTR  RF,0                                                             
         AR    RE,RF                                                            
         STH   RE,DUB                                                           
*                                                                               
NEXTTWA  DC    0H'0'                                                            
         ZIC   R0,0(R3)                                                         
         AR    R3,R0                                                            
         CLI   0(R3),0                                                          
         BE    EXIT                                                             
         TM    1(R3),X'20'         SKIP PROTECTED FIELDS                        
         BO    NEXTTWA                                                          
*                                                                               
         CLI   0(R2),ESC           IS THERE MORE INPUT                          
         BNE   NEXTTW1                                                          
*                                                                               
         CLI   SRFLAG,C'Y'                                                      
         BE    SCAN                                                             
*                                                                               
         CLC   DUB(2),2(R3)        RIGHT FIELD                                  
         BE    SCAN                                                             
         EJECT                                                                  
* IF DATA IN INPUT FIELD, MAKE SURE LEN PROPERLY SET                            
*                                                                               
NEXTTW1  DC    0H'0'                                                            
         TM    4(R3),X'40'         TEST PREVIOUSLY INPUT                        
         BO    NEXTTWA                                                          
*                                                                               
         MVI   5(R3),0             RESET INPUT LENGTH                           
         ZIC   R7,0(R3)                                                         
         SH    R7,=H'9'                                                         
         TM    1(R3),X'02'         TEST EXTENDED FIELD HEADER                   
         BZ    *+8                                                              
         SH    R7,=H'8'                                                         
         EX    R7,*+8                                                           
         B     *+10                                                             
         OC    8(0,R3),8(R3) *EXECUTED*                                         
         BZ    NEXTTWA                                                          
         CLI   8(R3),C' '                                                       
         BNE   NEXTTW2                                                          
         BCTR  R7,0                                                             
         LTR   R7,R7                                                            
         BM    NEXTTWA                                                          
         EX    R7,*+8                                                           
         B     *+10                                                             
         CLC   9(0,R3),8(R3) *EXECUTED*                                         
         BE    NEXTTWA                                                          
*                                                                               
NEXTTW2  DC    0H'0'                                                            
         ZIC   R7,0(R3)                                                         
         SH    R7,=H'8'            GET MAX LEN                                  
         TM    1(R3),X'02'         TEST EXTENDED FIELD HEADER                   
         BZ    *+8                                                              
         SH    R7,=H'8'                                                         
         LA    R6,8(R3,R7)                                                      
         BCTR  R6,0                POINT TO LAST CHAR                           
*                                                                               
NEXTTW4  CLI   0(R6),0                                                          
         BE    *+12                                                             
         CLI   0(R6),C' '                                                       
         BNE   NEXTTW6                                                          
         BCTR  R6,0                                                             
         BCT   R7,NEXTTW4                                                       
*                                                                               
NEXTTW6  STC   R7,5(R3)            SET INPUT LENGTH                             
         OI    4(R3),X'40'         SET PREVIOUSLY INPUT                         
         LTR   R7,R7               TEST NO INPUT                                
         BZ    NEXTTWA                                                          
*                                                                               
         LR    R0,R2               SAVE INPUT POINTER                           
         LA    R2,8(R3)            POINT TO TWA DATA                            
         OI    4(R3),X'0E'         PRESET VALIDITY BITS                         
*                                                                               
         BAS   RE,SETVAL           SET VALIDITY BITS                            
         LA    R2,1(R2)                                                         
         BCT   R7,*-8                                                           
         LR    R2,R0               RESTORE                                      
*                                                                               
         B     NEXTTWA                                                          
         EJECT                                                                  
SCAN     OI    6(R3),X'80'         SET XMT BIT TO UNMODIFY                      
*                                                                               
         LA    R2,4(R2)            POINT TO DATA                                
         ZIC   R7,0(R3)            GET MAX FIELD LEN                            
         SH    R7,=H'8'                                                         
         TM    1(R3),X'02'         TEST EXTENDED FIELD HEADER                   
         BZ    *+8                                                              
         SH    R7,=H'8'                                                         
*                                                                               
         SR    R5,R5               RESET INPUT LEN                              
         CLC   =C'//',0(R2)        TEST ERASE E-O-S                             
         BNE   *+12                                                             
         MVI   SRFLAG,X'FF'        SET FLAG                                     
         B     SCANX                                                            
         CLI   0(R2),C'/'                                                       
         BNE   *+12                                                             
         CLI   1(R2),C' '          TEST ERASE E-O-F                             
         BNH   SCANX                                                            
*                                                                               
         TM    1(R3),X'80'         TEST DROP LEADING BLANKS                     
         BNZ   SCAN4                                                            
SCAN2    CLI   0(R2),C' '                                                       
         BNE   SCAN4                                                            
         LA    R2,1(R2)                                                         
         BCT   R7,SCAN2                                                         
         SR    R5,R5               RESET LEN                                    
         B     SCANX                                                            
*                                                                               
SCAN4    LR    R4,R2               SAVE INPUT START                             
         SR    R5,R5               RESET FIELD LEN                              
         SR    R6,R6               RESET COUNTER FOR EMBEDDED BLANKS            
         OI    4(R3),X'0E'         PRESET VALID BITS                            
         NI    4(R3),X'EF'                                                      
*                                                                               
SCAN6    CLI   0(R2),ESC                                                        
         BE    SCANX                                                            
         CLI   0(R2),CR                                                         
         BE    SCANX                                                            
         TM    1(R3),X'80'         TEST DROP TRAILING BLANKS                    
         BNZ   SCAN10                                                           
         CLI   0(R2),C' '                                                       
         BNE   SCAN10                                                           
         LA    R6,1(R6)            BUMP BLANK COUNTER                           
         LA    R2,1(R2)                                                         
         BCT   R7,SCAN6                                                         
         B     SCANX                                                            
*                                                                               
SCAN10   AR    R5,R6               ADD EMBEDDED BLANKS                          
         SR    R6,R6               AND RESET COUNTER                            
*                                                                               
         BAS   RE,SETVAL           SET VALIDITY BITS                            
*                                                                               
SCAN14   LA    R2,1(R2)                                                         
         LA    R5,1(R5)                                                         
         BCT   R7,SCAN6                                                         
         EJECT                                                                  
SCANX    LR    RE,R3               GET TWA ADDR                                 
         SR    RE,RA               COMPUTE DSPL FROM TOP                        
         STH   RE,2(R1)                                                         
         OC    0(2,R1),0(R1)       TEST FIRST TIME                              
         BNZ   *+8                                                              
         STH   RE,0(R1)                                                         
* BUMP NUMBER OF FIELDS                                                         
         LH    RE,4(R1)                                                         
         LA    RE,1(RE)                                                         
         STH   RE,4(R1)                                                         
*                                                                               
         OI    4(R3),X'80'         SET INPUT THIS TIME                          
         NI    4(R3),X'9F'         RESET PREV INPUT AND VALIDATED               
         ZIC   RE,0(R3)                                                         
         SH    RE,=H'9'                                                         
         TM    1(R3),X'02'         TEST EXTENDED FIELD HEADER                   
         BZ    *+8                                                              
         SH    RE,=H'8'                                                         
         EX    RE,*+8                                                           
         B     *+10                                                             
         XC    8(0,R3),8(R3) *EXECUTED*                                         
*                                                                               
         STC   R5,5(R3)            SET LENGTH                                   
         LTR   R5,R5                                                            
         BZ    SCANX2                                                           
         BCTR  R5,0                                                             
         EX    R5,*+8                                                           
         B     *+10                                                             
         MVC   8(0,R3),0(R4) *EXECUTED*                                         
*                                                                               
SCANX2   CLI   0(R2),ESC           TEST MORE INPUT                              
         BE    NEXTINP             YES                                          
*                                                                               
         CLI   0(R2),CR            TEST CARRIAGE RETN (END OF XMT)              
         BE    NEXTTWA             YES-SET INPUT LENS FOR RMNG FLDS             
*                                                                               
         CLI   SRFLAG,X'FF'        TEST ERASE TO E-O-S RECEIVED                 
         BE    SCANX10             YES                                          
*                                                                               
         LA    R2,1(R2)            ELSE CONTINUE TO END OF FIELD                
         B     SCANX2                                                           
         EJECT                                                                  
* ERASE TO E-O-S RECEIVED. CLEAR ALL REMAINING INPUT FIELDS                     
SCANX10  DC    0H'0'                                                            
         ZIC   R0,0(R3)                                                         
         AR    R3,R0                                                            
         CLI   0(R3),0                                                          
         BE    EXIT                                                             
         TM    1(R3),X'20'         TEST PROT                                    
         BO    SCANX10             YES - SKIP                                   
         ZIC   RE,0(R3)                                                         
         SH    RE,=H'9'                                                         
         TM    1(R3),X'02'         TEST EXTENDED FIELD HEADER                   
         BZ    *+8                                                              
         SH    RE,=H'8'                                                         
         EX    RE,*+8              TEST FIELD ALREADY CLEAR                     
         B     *+10                                                             
         OC    8(0,R3),8(R3) *EXECUTED*                                         
         BZ    SCANX10                                                          
         CLI   8(R3),C' '                                                       
         BNE   SCANX12                                                          
         BCTR  RE,0                                                             
         LTR   RE,RE                                                            
         BM    SCANX10                                                          
         EX    RE,*+8              TEST FIELD = SPACES                          
         B     *+10                                                             
         CLC   9(0,R3),8(R3) *EXECUTED*                                         
         BE    SCANX10                                                          
SCANX12  OI    6(R3),X'80'         SET ON XMT BIT                               
         B     SCANX               GO RESET OTHER INDS                          
         EJECT                                                                  
SETVAL   DC    0H'0'                                                            
         CLI   0(R2),C'A'                                                       
         BL    *+12                                                             
         CLI   0(R2),C'Z'                                                       
         BNH   *+8                                                              
         NI    4(R3),X'FB'         NOT ALPHA                                    
*                                                                               
         CLI   0(R2),C'0'                                                       
         BL    *+12                                                             
         CLI   0(R2),C'9'                                                       
         BNH   SETVAL10                                                         
         NI    4(R3),X'F7'         NOT NUM                                      
*                                                                               
         TM    1(R3),X'10'         TEST NUM SPECIFIED                           
         BZ    *+8                                                              
         OI    4(R3),X'10'                                                      
*                                                                               
SETVAL10 CLI   0(R2),C'A'                                                       
         BL    SETVAL12                                                         
         CLI   0(R2),C'F'                                                       
         BNH   SETVAL14                                                         
         CLI   0(R2),C'0'                                                       
         BL    SETVAL12                                                         
         CLI   0(R2),C'9'                                                       
         BNH   SETVAL14                                                         
*                                                                               
SETVAL12 NI    4(R3),X'FD'         NOT HEX                                      
*                                                                               
SETVAL14 BR    RE                                                               
*                                                                               
EXIT     XMOD1 1                                                                
         LTORG                                                                  
         EJECT                                                                  
ADDRTAB  DC    0H'0'                                                            
*                 0 1 2 3 4 5 6 7 8 9 A B C D E F                               
         DC    X'00000000000000000000000000000000' 0                            
         DC    X'00000000000000000000000000000000' 1                            
         DC    X'00000000000000000000000000000000' 2                            
         DC    X'00000000000000000000000000000000' 3                            
         DC    X'01000000000000000000000F1D090C00' 4                            
         DC    X'0700000000000000000002050B0A1C3F' 5                            
         DC    X'0E100000000000000000000D06401F20' 6                            
         DC    X'000000000000000000411B0421081E03' 7                            
         DC    X'0042434445464748494A000000000000' 8                            
         DC    X'004B4C4D4E4F50000000000000000000' 9                            
         DC    X'000000000000000000000000003C0000' A                            
         DC    X'000000000000000000000000003E0000' B                            
         DC    X'0022232425262728292A000000000000' C                            
         DC    X'002B2C2D2E2F30313233000000000000' D                            
         DC    X'3D003435363738393A3B000000000000' E                            
         DC    X'1112131415161718191A000000000000' F                            
*                                                                               
ESCY     DC    AL1(ESC)                                                         
         DC    C'Y'                                                             
         SPACE 2                                                                
WORKD    DSECT                                                                  
*                                                                               
DUB      DS    D                                                                
SVUTLADR DS    A                                                                
SRFLAG   DS    C                                                                
         EJECT                                                                  
       ++INCLUDE FAUTL                                                          
         EJECT                                                                  
       ++INCLUDE FASCREQUS                                                      
         EJECT                                                                  
       ++INCLUDE DDFLDHDR                                                       
 END                                                                            
