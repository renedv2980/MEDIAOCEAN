*          DATA SET SPRUNCON   AT LEVEL 090 AS OF 06/24/19                      
*CATALP SPRUNCON                                                                
         ENTRY EMLPUT                                                           
         TITLE 'MODULE TO CONTROL SPONSOR RUN'                                  
SPRUNCON CSECT                                                                  
         PRINT NOGEN                                                            
         NBASE 0,SPRUNCON,=V(SPSAVE)                                            
         LA    R7,2048(RB)                                                      
         LA    R7,2048(R7)                                                      
         USING SPRUNCON+4096,R7                                                 
         L     RA,=V(SPWORKC)                                                   
         USING SPWORKD,RA                                                       
         LA    R9,2048(RA)                                                      
         LA    R9,2048(R9)                                                      
         USING SPWORKD+4096,R9                                                  
         L     R8,LOGOC                                                         
         USING LOGOD,R8                                                         
         L     R6,=V(MASTC)                                                     
         USING MASTD,R6                                                         
*                                                                               
         OC    PRINT,PRINT         TEST VPRINT UNRESOLVED                       
         BNZ   *+12                                                             
         L     R0,=V(PRINTS)       POINT TO NEW ENTRY POINT                     
         ST    R0,PRINT                                                         
*                                                                               
         MVC   MCAPHAS1,SPECS                                                   
         ST    RA,MCAWORK                                                       
*                                                                               
         MVI   MCS2OSYS,2          FORCE SPOT SYSTEM SENUM INFO INTO            
*                                   SECONDARY SENUM AREA (SO IT'S               
*                                   AVAILABLE TO NETPAK APPLICATIONS)           
*                                                                               
         LA    R2,DUB                                                           
         EXTRACT (R2),FIELDS=TIOT                                               
         MVC   RCJOB,0(R2)                                                      
*                                                                               
         GOTO1 =V(RUNSTART)                                                     
         MVC   RCUPSI,MCUPSI                                                    
*                                                                               
         L     RE,=V(NONTNMS)                                                   
         ST    RE,VNONTNMS                                                      
*                                                                               
         L     RE,=V(POL50EL)                                                   
         ST    RE,VPOL50EL                                                      
*                                                                               
         MVI   BYTE,QGETRATE       LOAD GETRATE                                 
         BAS   RE,SPLOAD                                                        
         MVC   GETRATE,4(R1)                                                    
*                                                                               
         MVI   BYTE,QRCPACK        LOAD RCPACK                                  
         BAS   RE,SPLOAD                                                        
         MVC   VRCPACK,4(R1)                                                    
*                                                                               
         MVI   BYTE,QSPSLNTB       LOAD SLNTAB                                  
         BAS   RE,SPLOAD                                                        
         MVC   VSLNTAB,4(R1)                                                    
*                                                                               
         L     R2,ADCONLST                                                      
         USING SPADCONS,R2                                                      
*                                                                               
         MVI   BYTE,QTSAROFF       LOAD TSAROFF                                 
         BAS   RE,SPLOAD                                                        
         MVC   TSAROFF,4(R1)                                                    
*                                                                               
         MVI   BYTE,QOFFICER       LOAD OFFICER                                 
         BAS   RE,SPLOAD                                                        
         MVC   VOFFICER,4(R1)                                                   
*                                                                               
         MVI   BYTE,QSPDEMUP       LOAD SPDEMUP                                 
         BAS   RE,SPLOAD                                                        
         MVC   VSPDEMUP,4(R1)                                                   
         DROP  R2                                                               
*                                                                               
         MVI   BYTE,QBLDMGA        LOAD SPBLDMGA                                
         BAS   RE,SPLOAD                                                        
         MVC   VBLDMGA,4(R1)                                                    
*                                                                               
         MVI   BYTE,QBLDMGN        LOAD SPBLDMGN <== 2-BYTE VRSN                
         BAS   RE,SPLOAD                                                        
         MVC   VBLDMGN,4(R1)                                                    
*                                                                               
         MVI   BYTE,QGETBUY        LOAD SPGETBUY                                
         BAS   RE,SPLOAD                                                        
         MVC   VGETBUY,4(R1)                                                    
*                                                                               
         MVI   BYTE,QSTAPACK       LOAD STAPACK                                 
         BAS   RE,SPLOAD                                                        
         MVC   VSTAPACK,4(R1)                                                   
*                                                                               
         MVI   BYTE,QGETDEM1       LOAD SPGETDEME (T00A20)                      
         BAS   RE,SPLOAD                                                        
         MVC   GETDEMO,4(R1)                                                    
*                                                                               
         MVI   BYTE,QDEMOCON                                                    
         BAS   RE,SPLOAD                                                        
         MVC   DEMOCON,4(R1)                                                    
*                                                                               
         MVI   BYTE,QSPDEMEXT                                                   
         BAS   RE,SPLOAD                                                        
         MVC   VDEMEXT,4(R1)                                                    
*                                                                               
         L     R2,ACOMFACS                                                      
         USING COMFACSD,R2                                                      
*                                                                               
         MVC   CMASTC,VMASTC                                                    
         MVC   CDATAMGR,DATAMGR                                                 
         MVC   CDATCON,DATCON                                                   
         MVC   CGETDAY,GETDAY                                                   
         MVC   CADDAY,ADDAY                                                     
         MVC   CDYNALOC,DYNALLOC                                                
*                                                                               
         MVI   BYTE,QDEMTABS                                                    
         BAS   RE,SPLOAD                                                        
         MVC   CDEMTABS,4(R1)                                                   
*                                                                               
         MVI   BYTE,QDEMAND        DEGET                                        
         BAS   RE,SPLOAD                                                        
         MVC   CDEMAND,4(R1)                                                    
         MVC   DEMAND,4(R1)                                                     
*                                                                               
         MVI   BYTE,QMINIO                                                      
         BAS   RE,SPLOAD                                                        
         MVC   CMINIO,4(R1)                                                     
*                                                                               
         MVI   BYTE,QPARSNIP                                                    
         BAS   RE,SPLOAD                                                        
         MVC   CPARSNIP,4(R1)                                                   
*                                                                               
         MVI   BYTE,QDEMOUT                                                     
         BAS   RE,SPLOAD                                                        
         MVC   CDEMOUT,4(R1)                                                    
*                                                                               
         MVI   BYTE,QDEMEL                                                      
         BAS   RE,SPLOAD                                                        
         MVC   CDEMEL,4(R1)                                                     
*                                                                               
         MVI   BYTE,QDEMADDR                                                    
         BAS   RE,SPLOAD                                                        
         MVC   CDEMADDR,4(R1)                                                   
*                                                                               
         MVI   BYTE,QDEMOMTH                                                    
         BAS   RE,SPLOAD                                                        
         MVC   CDEMOMTH,4(R1)                                                   
*                                                                               
         MVI   BYTE,QDEMAINT                                                    
         BAS   RE,SPLOAD                                                        
         MVC   CDEMAINT,4(R1)                                                   
*                                                                               
         MVI   BYTE,QDDISP         DEMDISP                                      
         BAS   RE,SPLOAD                                                        
         MVC   CT00AD0,4(R1)                                                    
*                                                                               
         MVI   BYTE,QDEMTABS                                                    
         BAS   RE,SPLOAD                                                        
         MVC   CDEMTABS,4(R1)                                                   
*                                                                               
         MVI   BYTE,QDEMTBOF                                                    
         BAS   RE,SPLOAD                                                        
         MVC   CDEMTBOF,4(R1)                                                   
         DROP  R2                                                               
*                                                                               
         CLI   MCNETPAK,C'Y'       FOR NETPAK APPLICATIONS ONLY...              
         BNE   CHKCOMSC                                                         
         MVI   BYTE,QNETIO                                                      
         BAS   RE,SPLOAD           ...LOAD NETIO                                
         MVC   NETIO,4(R1)                                                      
*                                                                               
CHKCOMSC MVC   COMSCORE,MCCSMODE   SAVE COMSCORE PASS LOCALLY                   
*                                                                               
         CLI   COMSCORE,0                                                       
**NOP**  JE    CHKCOMSCX  <==== FOR NOW, JUST ALWAYS LOAD COMINTER              
         MVI   BYTE,QCOMINTR                                                    
         BAS   RE,SPLOAD                                                        
         MVC   VCOMINTER,4(R1)                                                  
******************************************************                          
         B     READCARD                                                         
*                                                                               
SPLOAD   LR    R0,RE                                                            
         MVC   MCDUB,SPACES                                                     
         MVC   MCDUB(4),=C'T00A'                                                
         GOTO1 HEXOUT,DMCB,BYTE,MCDUB+4,1,0                                     
         GOTO1 MCVLOADM,DMCB,0                                                  
         LR    RE,R0                                                            
         BR    RE                                                               
         EJECT                                                                  
**********************************************************************          
*    INITIAL CONTROL CARDS *                                                    
**********************************************************************          
READCARD DS    0H                                                               
         MVC   SPECS,MCAPHAS1      SET LOAD ADDRESS                             
         MVC   APPLIC,MCAPHAS2                                                  
         MVC   RCTRACE,MCTRACE     TRANSFER MASTER VALUES                       
         MVC   RCDUMP,MCDUMP                                                    
         MVC   RCWRITE,MCWRITE                                                  
         MVC   RCDATE,MCDATE                                                    
         GOTO1 DATCON,DMCB,(4,RCDATE),(0,TODAY)                                 
         GOTO1 (RF),(R1),,(3,TODAYB)                                            
         GOTO1 (RF),(R1),,(2,TODAYP)                                            
         MVC   RCSPECNO,MCNUMBER                                                
         MVC   RCPROG,MCPROG                                                    
         MVC   RCAGYCRD,MCUSER                                                  
         MVC   RCAGENCY,MCUSER                                                  
         MVC   RCLANG,MCLANG                                                    
         MVC   RCCTRY,MCCTRY                                                    
         MVC   AGYNM,MCORIGIN                                                   
         MVC   AGYADR,MCORIGAD                                                  
         MVC   RCORIGID,MCORIGID                                                
         MVC   RECOVERY,MCRECOVR                                                
         MVC   OVERLOAD,MCLOAD                                                  
         L     R3,MCAPHAS3         FOR MVS THIS ADDRESS WILL BE SET             
         ST    R3,SUBCON            PROPERLY AFTER LOAD IS DONE                 
         MVI   CROSSFIL,C'N'       NOT A CROSS FILE ID                          
         TM    MCFLAGS2,MCCROSSF   CROSS FILE ID?                               
         BZ    *+8                 NO                                           
         MVI   CROSSFIL,C'Y'       YES - THIS IS A CROSS FILE ID                
*                                                                               
         MVI   RCFLONLY,YES        GO TO EXECUTE I/O AND RUN SPECS              
         GOTO1 REPORT                                                           
         MVI   RCFLONLY,NO                                                      
*                                  NOW LOAD IN SPECIAL FILE HANDLER             
         MVC   DUB,FCPHASE                                                      
         CLI   DUB+6,C' '                                                       
         BNE   *+10                                                             
         MVC   DUB+6(1),MCTEST3                                                 
         GOTO1 LOADER,DMCB,DUB,(R3)                                             
         BAS   RE,LOADCHEK                                                      
         MVC   SUBCON,4(R1)        SET SUBCON ADDRESS AFTER LOAD                
         EJECT                                                                  
*&&DO                                                                           
         A     R3,DMCB                                                          
         LA    R3,8(R3)                                                         
         SRL   R3,3                                                             
         SLL   R3,3                                                             
         ST    R3,ASORT                                                         
*&&                                                                             
********************************************************************            
* THE FOLLOWING CODE ALWAYS ATTEMPTS TO LOAD AN 04 PHASE. PREVIOUS *            
* TEST FOR V(MEDTABLE) PRESENT WAS INEFFECTIVE. WHEN ERROR OCCURS  *            
* IT IS IGNORED. NEED A NEW SPEC FOR MEDIA SUMMARY MODULES TO      *            
* EXPLICITLY STATE THAT AN 04 PHASE IS PRESENT.                    *            
********************************************************************            
         MVC   DUB,=C'SPNN04  '    LOAD IN A SPNN04 PHASE                       
         MVC   DUB+2(2),RCPROG                                                  
         MVC   DUB+6(1),MCTEST4                                                 
         CLC   OVERLOAD,SPACES                                                  
         BE    *+10                                                             
         MVC   DUB(4),OVERLOAD                                                  
         L     R3,MEDTABLE                                                      
         GOTO1 LOADER,DMCB,DUB,(R3)                                             
         MVC   MEDTABLE,4(R1)         SET LOAD ADDRESS                          
         B     OPEN2                                                            
*                                                                               
LOADCHEK OC    DMCB+4(4),DMCB+4                                                 
         BCR   7,RE                                                             
         MVC   WORK(16),=C'CANT FIND PHASE '                                    
         MVC   WORK+16(8),DUB                                                   
         GOTO1 LOGIO,DMCB,1,(24,WORK)                                           
         GOTO1 (RF),(R1),,=C'SPONSOR IS GIVING UP'                              
         ABEND 999                                                              
         EJECT                                                                  
**********************                                                          
* OPEN FILE ROUTINES *                                                          
**********************                                                          
         USING SSBD,RF                                                          
OPEN2    L     RF,=V(SSB)          SEE IN SSB IN SPREPFILEC                     
         OI    SSOSTAT2,SSOSNRCV   NO RECOVERY                                  
*                                                                               
         CLI   RCWRITE,NO          WRITE = NO ?                                 
         BE    OPEN3               FORCE NO RECOVERY TOO                        
*                                  (DON'T MOVE THIS BECAUSE OF REP)             
         CLI   RECOVERY,NO         DEFAULT IS TO WRITE TO RECOVERY              
         BE    *+8                                                              
         NI    SSOSTAT2,255-SSOSNRCV   TURN RECOVERY BACK ON                    
         DROP  RF                                                               
*                                                                               
         CLC   =C'NNN',FCUPDIR     UPDATE FOR SPOT/XSPOT/STATION?               
         BE    OPEN3               NO, SO FILE LIST OKAY AS IS                  
         CLI   RECOVERY,NO         SEE IF WE NEED SPOT RECOVERY                 
         BE    *+8                 NO                                           
         MVI   FRECV,C'U'          YES                                          
         CLI   FCUPFILE,YES        SPOT/XSPOT FILE?                             
         BNE   *+12                NO                                           
         MVI   FSPTFILE,C'U'                                                    
         MVI   FXSPFILE,C'U'                                                    
*                                                                               
         CLI   FCUPDIR,YES         SPOT/XSPOT DIR?                              
         BNE   *+12                NO                                           
         MVI   FSPTDIR,C'U'                                                     
         MVI   FXSPDIR,C'U'        SET TO OPEN FOR UPDATE                       
*                                                                               
         CLI   FCUPSTAT,YES        STATION?                                     
         BNE   *+8                 NO                                           
         MVI   FSTAFILE,C'U'                                                    
*                                                                               
         CLI   MCRECOVR,C'W'       FACWK RECOVERY FOR UPDATE SOON               
         BNE   OPEN3               NO                                           
         GOTO1 =V(DATCON),DMCB,(4,MCDATE),(1,FULL)                              
*                                                                               
         USING SSBD,RF                                                          
         L     RF,=V(SSB)                                                       
         OI    SSOSTAT2,SSOSRWRK    SET FACWK RECOVERY BIT IN SSB               
         L     R0,SSOFWBUF                                                      
         L     RF,SSOFWNDX                                                      
*                                                                               
         USING UKRECD,RF            SET FACWRK KEY VALUES                       
         MVC   UKUSRID,MCORIGID     USERID                                      
         MVC   UKSYSPRG(1),MCSYSTEM SYSTEM                                      
         MVC   UKSYSPRG+1(2),MCPROG PROGRAM                                     
         CLI   MCFACPAK,C' '                                                    
         BNH   *+10                                                             
         MVC   UKSUBPRG,MCFACPAK    FACPAK SYSTEM ID                            
         MVC   UKDAY,FULL+2         DAY NUMBER                                  
         MVI   UKCLASS,C'R'         CLASS=RECOVERY                              
         MVI   UKFLAG,X'01'         FLAG DUPLICATES ALLOWED                     
         MVC   UKFILNO,=X'FFFF'     LET WRKR KNOW UKFLAG IS VALID               
         DROP  RF                                                               
*                                                                               
OPEN3    L     R2,ADAGY                                                         
         MVC   WORK(2),RCAGYCRD                                                 
         BAS   RE,GETAGINF         GO AND SET FILE NO. IN UTL                   
*                                                                               
         GOTO1 DATAMGR,DMCB,=C'DMREAD',=C'SYSFLES',0                            
         ICM   RE,15,DMCB+12       A(FILE LIST FOR THIS SPOT SYSTEM)            
         BNZ   *+6                                                              
         DC    H'0'                                                             
         SR    R1,R1                                                            
         ICM   R1,3,2(RE)          NUMBER OF FILES IN SYSTEM                    
         LA    RE,4(RE)            BUMP TO FIRST FILE IN LIST                   
*                                                                               
OPEN3A   CLI   3(RE),X'33'         STRFDR?                                      
         BE    OPEN3B              YES -- SEE IF IT'S NOP                       
         LA    RE,8(RE)            NO, TRY NEXT FILE                            
         BCT   R1,OPEN3A                                                        
         B     OPEN3C              NO TRAFFIC FILES -- REMOVE FROM LIST         
*                                                                               
OPEN3B   TM    0(RE),X'80'         ARE TRAFFIC FILES NOP                        
         BZ    OPEN3E              NO                                           
*                                                                               
OPEN3C   MVI   FSTRFFL,C'Z'        NOP SPOT TRAFFIC FILE                        
         MVI   FSTRFDR,C'Z'        NOP SPOT TRAFFIC DIR                         
         MVI   FCUPTRF,NO          AND FORGET ABOUT UPDATING TRAFFIC            
*                                                                               
OPEN3E   DS    0H                                                               
         L     R2,ADBUY            PROVIDE I/O AREA FOR OPEN                    
         GOTO1 DATAMGR,DMCB,=C'OPEN',=C'SPOT',SPTFLIST,(R2)                     
         CLI   FCOPNDEM,YES        OPTION TO OPEN DEM FILES                     
         BNE   OPEN4                                                            
         GOTO1 (RF),(R1),,,DEMFLIST                                             
         EJECT                                                                  
*=========================================================*                     
* TEST TO OPEN UPDATIVE TRAFFIC FILES                     *                     
* IF YES, NEED TO GET SE NUMBER FROM SYSLIST/SYSLIST REC  *                     
*=========================================================*                     
OPEN4    CLI   FCUPTRF,YES         TRAFFIC UPDATIVE                             
         BNE   OPEN10                                                           
*                                                                               
         XC    KEY,KEY                                                          
         MVI   KEY,C'W'                                                         
         MVI   KEY+17,C'S'                                                      
         MVI   KEY+24,X'02'        READ SPOT SYSTEM REC FOR NAME                
         GOTO1 DATAMGR,DMCB,=C'DMRDHI',=C'CTFILE',KEY,ADBUY                     
         L     R6,ADBUY                                                         
         CLC   KEY(25),0(R6)                                                    
         BE    *+6                                                              
         DC    H'0'                                                             
         MVI   ELCODE,X'A4'                                                     
         BAS   RE,GETEL                                                         
         B     *+8                                                              
*                                                                               
OPEN4A   BAS   RE,NEXTEL                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
         CLC   11(1,R6),SPOTSE     TEST RIGHT SE                                
         BNE   OPEN4A                                                           
***********************************************************************         
* NOW WE KNOW THE SPOT SYSTEM NUMBER FROM SPOTX                                 
***********************************************************************         
         MVC   DUB(3),=C'STR'      BUILD THE TRAFFIC SYSTEM NAME STRX           
         MVC   DUB+3(2),7(R6)      MOVE SYSTEM NUMBER                           
***********************************************************************         
* NOW LOOK FOR A TRAFFIC ELEMENT BY THIS NAME                                   
***********************************************************************         
         XC    KEY,KEY                                                          
         MVI   KEY,C'W'                                                         
         MVI   KEY+17,C'S'                                                      
         MVI   KEY+24,X'0D'        READ STR SYSTEM RECORD                       
         GOTO1 DATAMGR,DMCB,=C'DMRDHI',=C'CTFILE',KEY,ADBUY                     
         L     R6,ADBUY                                                         
         CLC   KEY(25),0(R6)                                                    
         BE    *+6                                                              
         DC    H'0'                                                             
         MVI   ELCODE,X'A4'                                                     
         BAS   RE,GETEL                                                         
         B     *+8                                                              
*                                                                               
OPEN4B   BAS   RE,NEXTEL                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
         CLC   3(5,R6),DUB         MATCH NAME                                   
         BNE   OPEN4B                                                           
         MVC   RCUTLTRF,11(R6)     SAVE TRAFFIC SYSTEM NUMBER                   
*                                                                               
         USING UTLD,RE                                                          
         L     RE,=V(UTL)                                                       
         MVC   TSYS,RCUTLTRF       SET SE NUMBER FOR OPEN                       
         CLI   RCWRITE,NO                                                       
         BE    OPEN4F                                                           
         MVI   STRFFILU,C'U'       MAKE UPDATIVE                                
         MVI   STRFDIRU,C'U'                                                    
         CLI   RECOVERY,NO                                                      
         BE    OPEN4F                                                           
         MVI   STRFRCV,C'U'                                                     
*                                                                               
OPEN4F   L     R2,ADBUY                                                         
         GOTO1 DATAMGR,DMCB,=C'OPEN',=C'STRF',STRFLIST,(R2)                     
*                                                                               
         L     RE,=V(UTL)                                                       
         MVC   TSYS,SPOTSE         RESTORE SPOT SE NUMBER                       
         B     OPEN10                                                           
         DROP  RE                                                               
         EJECT                                                                  
         GETEL R6,28,ELCODE                                                     
ELCODE   DS    X                                                                
*                                                                               
STRFLIST DS    0D                                                               
STRFFILU DC    CL8'NTRFFILE'                                                    
STRFDIRU DC    CL8'NTRFDIR '                                                    
STRFRCV  DC    CL8'XTRFRCV '                                                    
         DC    CL8'X       '                                                    
*                                                                               
SPTFLIST DS    0C                                                               
FSPTFILE DC    CL8'NSPTFILE'                                                    
FXSPFILE DC    CL8'NXSPFILE'                                                    
FSPTDIR  DC    CL8'NSPTDIR '                                                    
FXSPDIR  DC    CL8'NXSPDIR '                                                    
FSTAFILE DC    CL8'NSTAFILE'                                                    
         DC    CL8'NCTFILE '                                                    
FSTRFFL  DC    CL8'NSTRFFL '                                                    
FSTRFDR  DC    CL8'NSTRFDR '                                                    
FRECV    DC    CL8'XRECV   '                                                    
         DC    CL8'X       '              SPACE TO ADD ON RECOVERY              
*                                                                               
DEMFLIST DS    0C                                                               
         DC    CL8'NDEMDIRN'                                                    
         DC    CL8'NL=DEMFN'                                                    
         DC    CL8'NDEMDIRA'                                                    
         DC    CL8'NL=DEMFA'                                                    
         DC    CL8'NDEMDIRR'                                                    
         DC    CL8'NL=DEMFR'                                                    
         DC    CL8'NNTIDIR '                                                    
         DC    CL8'NL=NTIFL'                                                    
         DC    CL8'NPAVDIR '                                                    
         DC    CL8'NL=PAVFL'                                                    
         DC    C'X '                                                            
*                                                                               
OPEN10   DS    0H                                                               
         SR    R0,R0                                                            
*                                                                               
         CLC   =C'**',RCAGYCRD     IF AGY = **, FUGGEDABOUDIT                   
         BE    OPEN12                                                           
*                                                                               
         XC    KEY,KEY             GET AGENCY RECORD INTO CORE                  
         MVI   KEY,X'06'                                                        
         MVC   KEY+1(2),RCAGYCRD                                                
         GOTO1 READ                                                             
         GOTO1 GETAGY                                                           
*                                                                               
OPEN12   CLC   LOGONAME,SPACES                                                  
         BNE   *+10                                                             
         MVC   LOGONAME,AGYNM                                                   
         CLC   LOGOADD,SPACES                                                   
         BNE   *+10                                                             
         MVC   LOGOADD,AGYADR                                                   
         CLI   RCLINEUP,C'Y'                                                    
         BNE   *+10                                                             
         ZAP   LOGOLINE,=P'66'                                                  
         EJECT                                                                  
***********************************************************************         
* CONTROL DIVIDERS AND REQUESTS                                                 
***********************************************************************         
         CLI   RCREQSUM,C'Y'       IS A DIVIDER WANTED                          
         BNE   GOREQS                                                           
         GOTO1 LOGO,DMCB,LOGOC                                                  
*                                                                               
GOREQS   BAS   RE,ANYLINUP                                                      
         GOTO1 FILCON                                                           
         SR    R2,R2               SET FOR NORMAL TERMINATION                   
*                                                                               
FINALISE CLI   RCREQSUM,C'Y'                                                    
         BNE   CLOSE1                                                           
         MVI   LOGOTYPE,C'E'                                                    
         ZAP   LOGOREQS,RCRQVAL                                                 
         GOTO1 LOGO,DMCB,LOGOC     PRINT END LOGOS                              
         DROP  R7                                                               
         EJECT                                                                  
***************************                                                     
* CLOSE FILES AND END JOB *                                                     
***************************                                                     
CLOSE1   DS    0H                                                               
         L     R6,=V(MASTC)                                                     
         USING MASTD,R6                                                         
         USING SSBD,RF                                                          
         L     RF,=V(SSB)                                                       
         TM    SSOSTAT2,SSOSRWRK   TEST FACWK RECOVERY                          
         BZ    CLOSE2                                                           
*                                                                               
         ICM   R7,15,SSOFWNDX      CLOSE FACWK FILE                             
         USING UKRECD,R7                                                        
         BNZ   *+6                                                              
         DC    H'0'                                                             
         LA    R0,=C'CLOSE'                                                     
         CP    MCNDUMPS,=P'0'                                                   
         BE    *+8                                                              
         LA    R0,=C'CLO/PUR'                                                   
         GOTO1 =V(DATAMGR),DMCB,(X'00',(R0)),=C'FACWRK',(R7),MCIO,     +        
               SSOFWBUF                                                         
         DROP  RF                                                               
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                ERROR ON FACWRK FILE                         
*                                                                               
CLOSE2   DS    0H                                                               
         CLIY  EMLFLAG,YES                                                      
         BNE   CLOSE3              NO                                           
         L     RF,=A(EMLCLSE)      CLOSE IT                                     
         BASR  RE,RF                                                            
*                                                                               
CLOSE3   ESTAE 0                                                                
         CP    MCNDUMPS,=P'0'      ANY DUMPS THEN TRY RECOVERY                  
         BE    CLOSE4                                                           
         WTO   'INVOKING SYSTEM RECOVERY'                                       
         GOTO1 MCVDMGR,DMCB,=C'RECOVR'                                          
*                                                                               
         USING UTLD,R4                                                          
CLOSE4   L     R4,=V(UTL)                                                       
         L     R3,ADAGY                                                         
         MVC   TSYS,SPOTSE         RESTORE SPOT SYSTEM                          
         GOTO1 DATAMGR,DMCB,=C'DMCLSE',=C'SPOT',,(R3)                           
         CLI   RCUTLTRF,0                                                       
         BE    CLOSE5                                                           
*                                                                               
         MVC   TSYS,RCUTLTRF       SET SE NUMBER FOR OPEN                       
         GOTO1 DATAMGR,DMCB,=C'DMCLSE',=C'STRF',,(R3)                           
*                                                                               
CLOSE5   MVI   TSYS,X'0A'          CLOSE CONTROL SYSTEM                         
         GOTOR DATAMGR,DMCB,=C'DMCLSE',=C'CONTROL',,(R3)                        
         MVC   TSYS,SPOTSE         RESTORE UTL                                  
         DROP  R4                                                               
*                                                                               
         GOTO1 ,DMCB,=C'CLOSE'     CLOSE PRINT QUEUE                            
         CP    MCNDUMPS,=P'0'                                                   
         BE    GOPRT                                                            
         GOTO1 ,DMCB,=C'CLO/ERR'                                                
         OC    MCREMPQK,MCREMPQK   IS IT A SOON JOB?                            
         BZ    *+8                 NO                                           
         LA    R2,1                                                             
*                                                                               
GOPRT    GOTO1 =V(PRINT)                                                        
         CLI   MCREPTS,2                                                        
         BNE   GOEND                                                            
         GOTO1 =V(PRINTS2)                                                      
*                                                                               
GOEND    DS    0H                                                               
         GOTOR MCVNQDQ,DMCB,(C'D',=CL8'IFI') DEQUEUE                            
         CP    MCNDUMPS,=P'0'                                                   
         BE    NORMEND                                                          
         ABEND 664                                                              
*                                                                               
NORMEND  DS    0H                                                               
         L     R2,MCAEXTRA                                                      
         USING MCEXTRA,R2                                                       
         CLI   MCXTRSMF,C'Y'                                                    
         BNE   NORMENDX                                                         
         DROP  R2                                                               
*                                                                               
         L     RF,=V(XTRSMF)                                                    
         BASR  RE,RF                                                            
*                                                                               
NORMENDX DS    0H                                                               
         XBASE                                                                  
SVSE     DS    X                                                                
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO EXTRACT FILE NUMBER & AGENCY CODE                                  
* FROM 2-BYTE AGENCY CODE                                                       
***********************************************************************         
GETAGINF NTR1                                                                   
         L     R3,ADBUY             PROVIDE AN I/O AREA                         
         USING CND,R3                                                           
         XC    CNLEN,CNLEN                                                      
         MVC   CNAGY,WORK                                                       
         CLC   RCORIGID,=X'002B'     IF TCH1 (OR NO ORIGIN GIVEN)               
         BE    *+10                                                             
         MVC   CNID,RCORIGID                                                    
         GOTO1 =V(CONFID),DMCB,CND,(1,FULL)                                     
         OC    FULL,FULL                                                        
         BNZ   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         USING UTLD,R5                                                          
         L     R5,=V(UTL)                                                       
         MVC   TSYS,CNSSE                                                       
         ZIC   R0,CNSCD                                                         
         SRL   R0,4                                                             
         STC   R0,BAGY                                                          
***************************                                                     
* SPECIAL CODE FOR NETPAK *                                                     
***************************                                                     
         CLI   MCNETPAK,C'Y'                                                    
         BNE   GETAG4                                                           
         MVC   TSYS,CNNSE                                                       
         ZIC   R0,CNNCD                                                         
         SRL   R0,4                                                             
         STC   R0,BAGY                                                          
*                                                                               
GETAG4   MVC   SPOTSE,TSYS         SET FROM UTL ENTRY                           
         MVC   PRNTSE,CNPSE                                                     
         MVC   ACCTSE,CNASE                                                     
         MVC   ACCTCD,CNACD                                                     
         DROP  R5                                                               
*                                                                               
         MVC   SYSSE#,SPOTSE                                                    
         GOTO1 DATAMGR,DMCB,(0,=C'DDNAME'),SYSSE,0                              
         TM    8(R1),X'10'                                                      
         JO    *+2                 SYSTEM NOT FOUND                             
         L     RE,8(,R1)           A(DDNADATA)                                  
         USING DDNAMED,RE                                                       
         MVC   FILENUM,DDNASEID    GET ONE CHARACTER ID (SHOULD BE TWO)         
         MVC   FILENUM2,DDNASEID   GET TWO CHARACTER ID                         
         XIT1                                                                   
         DROP  RE                                                               
*                ,                                                              
DDNAME   DC    CL8'DDNAME'                                                      
SYSSE    DC    C'SE=',X'00'                                                     
SYSSE#   DS    X                                                                
         EJECT ,                                                                
***************************                                                     
* LINE-UP PATTERN CONTROL *                                                     
***************************                                                     
ANYLINUP NTR1                                                                   
         CLI   RCLINEUP,C'Y'                                                    
         BNE   LINEND                                                           
*                                                                               
         L     RF,=V(REMOTEC)                                                   
         USING REMOTED,RF                                                       
         OC    REMOTKEY,REMOTKEY   NO LINEUP IF REMOTE                          
         BNZ   LINEND                                                           
         DROP  RF                                                               
         L     R6,=V(MASTC)                                                     
         USING MASTD,R6                                                         
         OC    MCREMPQK,MCREMPQK   NO LINEUP IF SOON                            
         BNZ   LINEND                                                           
         DROP  R6                                                               
         MVC   P,SPACES                                                         
         GOTO1 PRINT,DMCB,P,=C'BC01'                                            
         GOTO1 PRINT,DMCB,P,=C'BL09'                                            
         SPACE 2                                                                
USLINUP  DS    0H                                                               
         LA    R4,3                DO IT THREE TIMES                            
USLINUP2 LA    R2,3                                                             
         GOTO1 PRINT,DMCB,P,=C'BL04'                                            
         SPACE 2                                                                
USLINUP4 DS    0H                  85 X'S ON HEADS 14,15,16                     
         MVI   P,C'X'                                                           
         MVC   P+1(84),P                                                        
         GOTO1 PRINT,DMCB,P,=C'BL01'                                            
         BCT   R2,USLINUP4                                                      
         MVC   P,SPACES                                                         
         GOTO1 (RF),(R1),,=C'BC01'                                              
         GOTO1 (RF),(R1),,=C'BL09'                                              
         BCT   R4,USLINUP2                                                      
         MVC   P,SPACES                                                         
         SPACE 2                                                                
LINEND   XIT1                                                                   
         SPACE 2                                                                
OVERLOAD DC    CL4' '                                                           
RECOVERY DC    AL1(YES)                                                         
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* PUT A PRINT LINE TO AN EXTRA OUTPUT FILE FOR LATER PROCESSING       *         
* NTRY:  P1    = A(PRINT LINE)                                        *         
*        P2    = A(PRINT CONTROL CHARACTERS)                          *         
***********************************************************************         
EMLPUT   NTR1  BASE=*,LABEL=*                                                   
         LR    R2,R1                                                            
*                                                                               
         CLIY  EMLFLAG,YES                                                      
         BE    *+8                                                              
         BRAS  RE,EMLOPEN          OPEN IT                                      
*                                                                               
         L     R4,=A(EMLLINE)                                                   
         L     RE,4(R2)                                                         
         MVC   0(4,R4),0(RE)       PRINTER CONTROL CHARACTERS                   
         L     RE,0(R2)                                                         
         MVC   4(132,R4),0(RE)     PRINT LINE                                   
*                                                                               
         L     R3,=A(EMLFILE)                                                   
         PUT   (R3),0(R4)          PUT RECORD TO FILE                           
         XIT1  ,                                                                
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* OPEN EMLFILE FOR OUTPUT                                             *         
***********************************************************************         
EMLOPEN  NTR1  BASE=*,LABEL=*                                                   
         L     R3,=A(EMLFILE)                                                   
         OPEN  ((R3),OUTPUT)                                                    
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         MVIY  EMLFLAG,YES         SET FILE CLOSED                              
         XIT1  ,                                                                
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* CLOSE EMLFILE                                                       *         
***********************************************************************         
EMLCLSE  NTR1  BASE=*,LABEL=*                                                   
         L     R3,=A(EMLFILE)                                                   
         CLOSE (R3)                                                             
*                                                                               
         MVIY  EMLFLAG,NO          SET FILE CLOSED                              
         XIT1  ,                                                                
*                                                                               
         LTORG                                                                  
*                                                                               
EMLFLAG  DC    AL1(NO)                                                          
*                                                                               
EMLLINE  DC    136C' '                                                          
EMLFILE  DCB   DSORG=PS,MACRF=PM,DDNAME=EMLFILE,RECFM=FB,LRECL=(136)            
         EJECT                                                                  
***********************************************************************         
* OTHER INCLUDED DSECTS                                               *         
***********************************************************************         
YES      EQU   C'Y'                                                             
NO       EQU   C'N'                                                             
       ++INCLUDE DDREMOTED                                                      
         EJECT                                                                  
       ++INCLUDE DDCNTRL                                                        
         PRINT OFF                                                              
         EJECT                                                                  
       ++INCLUDE SPREPWORKD                                                     
       ++INCLUDE DDLOGOD                                                        
       ++INCLUDE DDREPMASTD                                                     
       ++INCLUDE DDCOREQUS                                                      
       ++INCLUDE DDCOMFACS                                                      
       ++INCLUDE FASSB                                                          
         ORG   SSBD                                                             
       ++INCLUDE FASSBOFF                                                       
         ORG                                                                    
       ++INCLUDE FAUTL                                                          
       ++INCLUDE DMWRKRK                                                        
DDNAMED DSECT                                                                   
       ++INCLUDE DMDDNAMED                                                      
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'090SPRUNCON  06/24/19'                                      
         END                                                                    
