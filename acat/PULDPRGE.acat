*          DATA SET PULDPRGE   AT LEVEL 002 AS OF 02/12/80                      
*CATALP PULDPRGE                                                                
         TITLE 'LDPURGE - PUB - LOAD PURGE TEST'                                
*        PARAMS VIA R1                                                          
*        XL1   X'00'  RETURN  X'00'=NOPURGE  OR  X'FF'=PURGE                    
*        AL3   A(RECORD)                                                        
*                                                                               
*        UPSI  00000001 PURGE X'80' DELETES                                     
*        UPSI  00000010 PURGE X'C3' CLOSEOUTS                                   
*        UPSI  00000100 PURGE TEST AGY RECORDS                                  
*        UPSI  00001000 PURGE EMPTY RECORDS                                     
         PRINT NOGEN                                                            
LDPURGE  CSECT                                                                  
         NMOD1 0,PULDPRGE                                                       
*                                                                               
CNTL     EQU   27                  DISPLACEMENT OF CONTROL BYTES                
*                                  FROM START OF REC                            
AGY      EQU   7                   DISPLACEMENT OF AGENCY ID                    
         SPACE 2                                                                
* GET UPSI ON FIRST TIME                                                        
PU       BC    0,PU0                                                            
         OI    PU+1,X'F0'          SET BRANCH                                   
         LR    R0,R1               SAVE R1                                      
         L     R1,=V(UPSIVAL)                                                   
         MVC   UPSI,0(R1)                                                       
         LR    R1,R0               RESTORE R1                                   
         SPACE 2                                                                
PU0      TM    UPSI,X'0F'          ANY PURGES                                   
         BC    8,XIT               NO                                           
         L     R3,0(R1)            POINT TO RECORD                              
*                                                                               
         TM    UPSI,X'08'          TEST FOR EMPTY RECORD                        
         BZ    PU1                                                              
         CLC   25(2,R3),MINLEN                                                  
         BH    PU1                                                              
         MVI   0(R1),255                                                        
         B     XIT                                                              
*                                                                               
PU1      TM    CNTL(R3),X'C0'      ANY DELETE INDS ON                           
         BC    7,ACAN              YES. A CANDIDATE                             
* NO DELETE INDICATORS. TEST FOR TEST AGENCY RECORD PURGE OPTION                
PU2      TM    UPSI,X'04'                                                       
         BC    1,TAG               YES. GO TEST FOR TEST AG                     
         SPACE 2                                                                
XIT      XMOD1 1                                                                
         EJECT                                                                  
* THIS IS A CANDIDATE FOR A PURGE                                               
ACAN     TM    UPSI,X'03'          DELETE OR CLOSEOUT PURGE                     
         BC    8,PU2               NO, GO TEST FOR TEST AG                      
         BC    1,BOTH              DELETE BOTH X'80' AND X'C0'                  
         SPACE 2                                                                
* DELETE ONE OR THE OTHER                                                       
         TM    UPSI,X'01'                                                       
         BC    1,O80               ONLY X'80'                                   
         TM    UPSI,X'02'                                                       
         BC    1,OC0               ONLY X'C0'                                   
         B     PU2                                                              
         SPACE                                                                  
* DELETE ONLY X'80'                                                             
O80      TM    CNTL(R3),X'C0'                                                   
         BC    1,PU2               BOTH ON, NO DELETE                           
         TM    CNTL(R3),X'80'                                                   
         BC    8,PU2                                                            
O801     MVI   0(R1),255                                                        
         B     XIT                                                              
         SPACE 2                                                                
* DELETE ONLY X'C0'                                                             
OC0      TM    CNTL(R3),X'C0'                                                   
         BC    1,O801                                                           
         B     PU2                                                              
         SPACE 2                                                                
* DELTEE BOTH                                                                   
BOTH     TM    CNTL(R3),X'C0'                                                   
         BC    1,O801                                                           
         TM    CNTL(R3),X'80'                                                   
         BC    1,O801                                                           
         B     PU2                                                              
         SPACE 2                                                                
* TEST AGENCY PURGE                                                             
TAG      CLC   AGY(2,R3),TSTAG                                                  
         BC    8,O801                                                           
         B     XIT                                                              
         EJECT                                                                  
MINLEN   DC    H'34'                                                            
UPSI     DC    X'00'                                                            
TSTAG    DC    C'T1'                                                            
         LTORG                                                                  
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'002PULDPRGE  02/12/80'                                      
         END                                                                    
