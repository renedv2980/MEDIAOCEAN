*          DATA SET DEDEMTIME  AT LEVEL 014 AS OF 08/24/83                      
*CATALP DEMTIME                                                                 
         TITLE 'CONVERT ALPHA OR MILITARY TIME TO QTR HR SEGEMENT'              
HRTOQH   CSECT                                                                  
*        PARAMETERS 1.   BYTE  0   0=MILITARY TIME 1=ALPHA TIME                 
*                        BYTES 1-3 A(INPUT TIME)                                
*                                                                               
*                   2.   BYTES 0-3 A(QTR HR NUMBER)                             
*                                                                               
         NMOD1 0,HRTOQH                                                         
         L     R2,0(R1)            SET INPUT ADDRESS                            
         L     R3,4(R1)            SET OUTPUT ADDRESS                           
         LA    R2,0(R2)            CLEAR HIGH ORDER BYTE                        
         MVC   HALF,0(R2)                                                       
         CLI   0(R1),1             ALPHA TIME GIVEN                             
         BNE   CNVMIL               NO - CONVERT MILITARY TIME                  
         GOTO1 =V(TIMVAL),DUB,(X'05',(R2)),HALF                                 
         CLI   0(R1),X'FF'                                                      
         BNE   *+6                                                              
         DC    H'0'                                                             
CNVMIL   LH    R4,HALF                                                          
         CH    R4,=H'600'          TIME GT MIDNIGHT                             
         BNL   *+8                                                              
         AH    R4,=H'2400'          YES - ADD 1 DAY                             
         SH    R4,=H'600'          SET FOR 6A START                             
         LTR   R4,R4                                                            
         BNZ   *+12                                                             
         MVI   0(R3),0                                                          
         B     HREXIT                                                           
         SRDA  R4,32                                                            
         D     R4,=F'100'                                                       
         MH    R5,=H'4'                                                         
         STC   R5,0(R3)                                                         
         SRDA  R4,32                                                            
         D     R4,=F'15'                                                        
         XC    HALF,HALF                                                        
         MVC   HALF+1(1),0(R3)                                                  
         AH    R5,HALF                                                          
         STC   R5,0(R3)            STORE IN OUTPUT                              
HREXIT   XMOD1 1                                                                
HALF     DS    H                                                                
         DS    H                                                                
DUB      DS    D                                                                
         LTORG                                                                  
         TITLE 'CONVERT QTR HR TO ALPHA OR MILITARY TIME'                       
QHTOHR   CSECT                                                                  
*        PARAMETERS 1.   BYTES 0-3 A(INPUT QTR HR)                              
*                                                                               
*                   2.   BYTE  0   0=MILITARY TIME 1=ALPHA TIME                 
*                              1-3 A(OUTPUT AREA)                               
*                                                                               
         NMOD1 0,QHTOHR                                                         
         L     R2,0(R1)            SET INPUT ADDRESS                            
         L     R3,4(R1)            SET OUTPUT ADDRESS                           
         ST    R1,STORER1                                                       
         LA    R3,0(R3)            CLEAR HIGH ORDER BYTE                        
         SR    R4,R4                                                            
         SR    R5,R5                                                            
         IC    R5,0(R2)                                                         
         D     R4,=F'4'                                                         
         MH    R5,=H'100'                                                       
         MH    R4,=H'15'           CALCULATE MILITARY TIME                      
         AR    R4,R5                                                            
         AH    R4,=H'600'                                                       
         CH    R4,=H'2400'         AFTER MIDNIGHT                               
         BNH   *+8                                                              
         SH    R4,=H'2400'          YES - START AT 0                            
         STH   R4,HALF2            SAVE MILITARY TIME IN OUTPUT AREA            
         MVC   0(2,R3),HALF2                                                    
         CLI   4(R1),1             ALPHA TIME REQUESTED                         
         BNE   QHEXIT               NO - EXIT                                   
         GOTO1 =V(UNTIME),DUB2,HALF2,(R3)  CONVERT TO ALPHA                     
QHEXIT   XMOD1 1                                                                
DUB2     DS    D                                                                
HALF2    DS    H                                                                
         DC    H'0'                                                             
STORER1  DS    F                                                                
         LTORG                                                                  
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'014DEDEMTIME 08/24/83'                                      
         END                                                                    
