*          DATA SET DDEXPRESS  AT LEVEL 003 AS OF 05/01/02                      
*CATALP EXPRESS                                                                 
         TITLE 'VALIDATE ENCODED INPUT EXPRESSION AND INPUT FIELD'              
* PARAM1 - A(2 BYTE DATA TYPE)                                                  
*                                                                               
*      2 - A(20 BYTE EXPRESSION)                                                
*                                                                               
*      3 - A(OPTIONAL 4 BYTE INPUT FIELD)                                       
*                                                                               
*      4 - V(SCANNER) USED IF V(SCANNER) NOT INCLUDED IN LNKEDT                 
*                                                                               
* RETURN IN P1 BYTE 0 - ZERO IF OK AND P2 BYTE 0=FIELD VALUE                    
*                  OR - PARAMETER IN ERROR (1,2 OR 3) WITH P5 POINTING          
*                       TO 60 BYTE ERROR MESSAGE                                
         PRINT NOGEN                                                            
EXPRESS  CSECT                                                                  
         NMOD1 140,**EXPS**                                                     
         USING EXWORK,RC           RC=A(W/S)                                    
         LR    R2,R1               R2=A(PARMS)                                  
         LM    R3,R5,0(R2)                                                      
         RELOC RELO                                                             
         L     R1,=V(SCANNER)      GET A(SCANNER)                               
         LTR   R1,R1                                                            
         BNZ   *+12                                                             
         L     R1,12(R2)                                                        
         B     EXP2                                                             
         A     R1,RELO                                                          
*                                                                               
EXP2     ST    R1,VSCANNER                                                      
         LA    RA,TYPELST          SEARCH TYPELST FOR DATA TYPE                 
         MVI   0(R2),1             SET FIELD IN PARM                            
         MVI   ERROR,1             INVALID INPUT FIELD                          
         MVI   FNDX,0                                                           
*                                                                               
EXP4     CLI   0(RA),X'FF'         END OF LIST ?                                
         BE    EXPERR                                                           
         CLC   0(1,R3),0(RA)                                                    
         BE    *+12                                                             
         LA    RA,L'TYPELST(RA)                                                 
         B     EXP4                                                             
         USING EXDATAT,RA          RA=A(DATA DEFINITION ENTRY)                  
         XC    FULL,FULL                                                        
         MVC   FULL+1(3),EXAVALS                                                
         L     RE,FULL                                                          
         A     RE,RELO                                                          
         MVC   EXVALUES,0(RE)      SET VALID INPUT VALUES                       
         MVI   WORK,C' '                                                        
         MVC   WORK+1(L'WORK-1),WORK                                            
         MVC   WORK(20),0(R4)      MOVE EXPRESSION                              
         MVI   0(R2),2                                                          
*                                                                               
         GOTO1 VSCANNER,DMCB,(C'C',WORK),(20,EXTABLE),C',=,-'                   
         CLI   DMCB+4,0                                                         
         BE    EXPERR                                                           
         MVC   EXLINES,DMCB+4                                                   
         MVI   FNDX,1                                                           
         LA    R6,EXTABLE                                                       
*                                                                               
EXP6     CLC   FNDX,EXLINES                                                     
         BH    EXP14                                                            
         MVC   WORK(1),0(R6)       SET LOW VALUE INDICS FOR VALIDATION          
         MVC   WORK+1(1),2(R6)                                                  
         MVC   WORK+2(10),12(R6)                                                
         BAS   RE,EXVALS                                                        
         BNE   EXPERR                                                           
         MVC   EXLOW,WORK+12       LOW RANGE VALUE                              
         CLI   1(R6),0                                                          
         BNE   *+14                                                             
         MVC   EXHI,EXLOW          SET HI TO LOW IF N/I                         
         B     EXP8                                                             
         MVC   WORK(1),1(R6)       SET HI VALUE INDICS FOR VALIDATION           
         MVC   WORK+1(1),3(R6)                                                  
         MVC   WORK+2(10),22(R6)                                                
         BAS   RE,EXVALS                                                        
         BNE   EXPERR                                                           
         MVC   EXHI,WORK+12        HI RANGE VALUE                               
*                                                                               
EXP8     MVI   ERROR,2             HI VALUE LE LOW VALUE                        
         CLC   EXHI,EXLOW                                                       
         BL    EXPERR                                                           
         SR    R8,R8                                                            
         SR    R9,R9                                                            
         IC    R8,EXLOW                                                         
         IC    R9,EXHI                                                          
         LA    R8,EXVALUES(R8)                                                  
         LA    R9,EXVALUES(R9)                                                  
         MVI   ERROR,3             RANGES OVERLAP                               
*                                                                               
EXP10    CR    R8,R9                                                            
         BH    EXP12                                                            
         CLI   0(R8),0                                                          
         BE    *+16                                                             
         TM    0(R8),X'80'                                                      
         BO    EXPERR                                                           
         OI    0(R8),X'80'         SET X'80' IF VALID INPUT                     
         LA    R8,1(R8)                                                         
         B     EXP10                                                            
*                                                                               
EXP12    SR    R1,R1               BUMP FIELD INDEX #                           
         IC    R1,FNDX                                                          
         LA    R1,1(R1)                                                         
         STC   R1,FNDX                                                          
         LA    R6,32(R6)           AND LIST POINTER                             
         B     EXP6                                                             
         EJECT                                                                  
* VALID INPUT VALUES HAVE NOW BEEN ESTABLISHED NOW SEE IF ANY INPUT             
* IS TO BE VERIFIED.                                                            
*                                                                               
EXP14    LTR   R5,R5               R5=A(FIELD) OR ZERO IF N/R                   
         BZ    EXOK                                                             
         MVI   0(R2),3                                                          
         MVI   ERROR,1                                                          
         MVI   WORK,C' '                                                        
         MVC   WORK+1(L'WORK-1),WORK                                            
         MVC   WORK(4),0(R5)                                                    
         MVI   FNDX,0                                                           
         GOTO1 VSCANNER,DMCB,(C'C',WORK),(1,EXTABLE),0                          
         CLI   DMCB+4,0                                                         
         BE    EXPERR                                                           
         LA    R6,EXTABLE                                                       
*                                                                               
         CLI   1(R6),0             SECOND HALF INPUT                            
         BNE   EXPERR                                                           
         MVC   WORK(1),0(R6)                                                    
         MVC   WORK+1(1),2(R6)                                                  
         MVC   WORK+2(10),12(R6)                                                
         BAS   RE,EXVALS           VALIDATE INPUT                               
         BNE   EXPERR                                                           
         SR    R8,R8                                                            
         IC    R8,WORK+12                                                       
         LA    R8,EXVALUES(R8)                                                  
         MVI   ERROR,4             INPUT VALUE NOT DEFINED IN AV LIST           
         TM    0(R8),X'80'                                                      
         BZ    EXPERR                                                           
         MVC   4(1,R2),WORK+12     RETURN DECODED INPUT VALUE                   
*                                                                               
EXOK     MVI   0(R2),0                                                          
         B     EXIT                                                             
         EJECT                                                                  
* BUILD *ERROR* - ERROR CONDITION FIELD# NN INTO WORK                           
*                                                                               
EXPERR   SR    R1,R1                                                            
         IC    R1,ERROR                                                         
         BCTR  R1,0                                                             
         MH    R1,=H'40'                                                        
         LA    R1,ERRLST(R1)                                                    
         MVI   WORK,C' '                                                        
         MVC   WORK+1(L'WORK-1),WORK                                            
         MVC   WORK(09),=C'*ERROR* -'                                           
         MVC   WORK+10(40),0(R1)                                                
         CLI   FNDX,0                                                           
         BE    EXPERR2                                                          
         LA    R1,WORK+50                                                       
         CLI   0(R1),C' '                                                       
         BNE   *+10                                                             
         BCTR  R1,0                                                             
         B     *-10                                                             
         LA    R1,2(R1)                                                         
         MVC   0(6,R1),=C'FIELD#'                                               
         EDIT  (B1,FNDX),(2,7(R1)),ALIGN=LEFT,WRK=EXTABLE                       
*                                                                               
EXPERR2  LA    R1,WORK                                                          
         ST    R1,16(R2)           RETURN A(ERROR) IN P5                        
*                                                                               
EXIT     XMOD1 1                                                                
         EJECT                                                                  
* CHECK INPUT FIELD - RETURN WITH CC=NEQ IF FIELD IN ERROR WITH                 
* ERROR=ERROR NUMBER                                                            
*                                                                               
EXVALS   NTR1                                                                   
         MVI   ERROR,5             INVALID NUMERIC                              
         TM    EXMASK,X'80'                                                     
         BO    EXVAL0                                                           
         MVI   ERROR,7             INVALID HEXADECIMAL                          
         TM    EXMASK,X'20'                                                     
         BO    EXVAL0                                                           
         MVI   ERROR,6             INVALID CHARACTER                            
*                                                                               
EXVAL0   DS    0H                                                               
         SR    R1,R1                                                            
         IC    R1,EXMASK                                                        
         LTR   R1,R1               ANYTHING GOES                                
         BZ    *+20                                                             
         EX    R1,*+8                                                           
         B     *+8                                                              
         TM    WORK+1,0                                                         
         BZ    EXXIT                                                            
         MVI   ERROR,8             L'INPUT LE MINIMUM                           
         CLC   WORK(1),EXMINI                                                   
         BL    EXXIT                                                            
         MVI   ERROR,9             L'INPUT GR MAXIMUM                           
         CLC   WORK(1),EXMAXI                                                   
         BH    EXXIT                                                            
*                                                                               
         IC    R1,EXINDX                                                        
         SLL   R1,2                                                             
         B     *(R1)                                                            
*                                                                               
         B     EXVAL2                                                           
         B     EXVAL4                                                           
         B     EXVAL6                                                           
*                                                                               
EXVAL2   MVC   WORK+12(1),WORK+2   CHARACTER                                    
         B     EXVALX                                                           
*                                                                               
EXVAL4   L     R7,RELO             HEXADECIMAL                                  
         GOTO1 VHEXIN,DMCB,WORK+2,WORK+12,2,RR=R7                               
         B     EXVALX                                                           
*                                                                               
EXVAL6   SR    R7,R7               NUMERIC                                      
         IC    R7,WORK                                                          
         BCTR  R7,0                                                             
         EX    R7,*+8                                                           
         B     *+10                                                             
         PACK  DUB,WORK+2(0)                                                    
         CVB   R7,DUB                                                           
         MVI   ERROR,10            FIELD VALUE EXCEEDS MAXIMUM                  
         CH    R7,=H'255'                                                       
         BH    EXXIT                                                            
         STC   R7,WORK+12                                                       
         B     EXVALX                                                           
*                                                                               
EXVALX   SR    R7,R7                                                            
         IC    R7,WORK+12                                                       
         LA    R7,EXVALUES(R7)                                                  
         MVI   ERROR,4                                                          
         CLI   0(R7),0                                                          
         BE    EXXIT                                                            
         MVI   ERROR,0                                                          
*                                                                               
EXXIT    CLI   ERROR,0                                                          
         XIT1                                                                   
         EJECT                                                                  
*              LITERALS ETC.                                                    
*                                                                               
         LTORG                                                                  
*                                                                               
*              LIST OF VALID DATA TYPES (COVERED BY DSECT EXDATAT)              
*                                                                               
TYPELST  DS    0CL9                                                             
         DC    C'C ',X'00',AL1(1,1,1),AL3(ALLVAL)                               
         DC    C'X ',X'20',AL1(2,2,2),AL3(ALLVAL)                               
         DC    C'N ',X'80',AL1(1,3,3),AL3(ALLVAL)                               
         DC    X'FF'                                                            
*                                                                               
ALLVAL   DC    256X'01'                                                         
*                                                                               
VHEXIN   DC    V(HEXIN)                                                         
*                                                                               
ERRLST   DS    0CL40                                                            
         DC    CL40'INVALID INPUT FIELD'                                        
         DC    CL40'HIGH VALUE LESS THAN LOW VALUE'                             
         DC    CL40'EXPRESSION OVERLAPS PREVIOUS'                               
         DC    CL40'INPUT IS NOT AN ACCEPTABLE VALUE'                           
         DC    CL40'INPUT IS NOT VALID NUMERIC'                                 
         DC    CL40'INPUT IS NOT A VALID CHARACTER'                             
         DC    CL40'INPUT IS NOT VALID HEXADECIMAL'                             
         DC    CL40'INPUT LENGTH LESS THAN MINIMUM'                             
         DC    CL40'INPUT LENGTH GREATER THAN MAXIMUM'                          
         DC    CL40'NUMERIC VALUE EXCEEDS MAXIMUM'                              
         EJECT                                                                  
*              DSECT TO COVER WORKING STORAGE                                   
*                                                                               
EXWORK   DSECT                                                                  
DUB      DS    D                                                                
FULL     DS    F                                                                
DMCB     DS    6F                                                               
WORK     DS    CL80                                                             
*                                                                               
RELO     DS    A                                                                
VSCANNER DS    A                                                                
*                                                                               
ERROR    DS    C                                                                
FNDX     DS    C                                                                
*                                                                               
EXLINES  DS    C                                                                
EXLOW    DS    C                                                                
EXHI     DS    C                                                                
EXVALUES DS    CL256                                                            
EXTABLE  DS    20CL32                                                           
*                                                                               
*              DSECT TO COVER TYPELST ENTRY                                     
*                                                                               
EXDATAT  DSECT                                                                  
EXDATA1  DS    C                                                                
EXDATA2  DS    C                                                                
EXMASK   DS    C                                                                
EXMINI   DS    C                                                                
EXMAXI   DS    C                                                                
EXINDX   DS    C                                                                
EXAVALS  DS    AL3                                                              
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003DDEXPRESS 05/01/02'                                      
         END                                                                    
