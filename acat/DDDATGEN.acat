*          DATA SET DDDATGEN   AT LEVEL 186 AS OF 05/01/02                      
*CATALP T00A19A                                                                 
         TITLE 'DATGEN- NETWORK DATE HANDLING UTILITY ROUTINES'                 
         EJECT                                                                  
         MACRO                                                                  
&NAME    CLEAN &I,&L,&C                                                         
         LCLA  &L1                                                              
         LCLC  &L2                                                              
&L1      SETA  &L-1                                                             
&NAME    MVI   &I,&C                                                            
         AIF   (T'&I EQ 'U').SS1                                                
         MVC   &I+1(&L1),&I                                                     
         AGO   .SS                                                              
.SS1     ANOP                                                                   
         AIF   ('&I'(K'&I-2,1) NE 'R').SS                                       
&L2      SETC  '1+'.'&I'(1,K'&I-3).'&L1,'.'&I'(K'&I-2,3)                        
         MVC   &L2,&I                                                           
.SS      MEND                                                                   
*                                                                               
         EJECT                                                                  
         PRINT NOGEN                                                            
NDDATES  CSECT                                                                  
         NMOD1 WORKX-WORKD,**NETUL**,RA,RR=R2                                   
         USING WORKD,RC                                                         
         L     R7,0(R1)                                                         
         USING NDDATB,R7                                                        
         ST    R2,RELO                                                          
*                                                                               
******** ROUTINE NUMBERS MUST CORRESPOND TO THOSE IN NENDEQUS *****             
*                                                                               
         LR    RF,R7                                                            
         SRL   RF,24               GET ROUTINE NUMBER                           
         BAS   RE,VALID                                                         
         SLL   RF,2                MULTIPLY BY 4                                
         B     VBRANCH(RF)                                                      
*                                                                               
VBRANCH  B     VERROR              0                                            
         B     NDPERLST            1                                            
         B     NDMNAME             2                                            
         B     NDADDAY             3                                            
         B     VERROR              4                                            
         B     VERROR              5                                            
         B     VERROR              6                                            
         B     VERROR              7                                            
         B     NDWKST              8                                            
         B     NDWKEN              9                                            
         B     NDMONST             10                                           
         B     NDMONEN             11                                           
         B     NDQTRST             12                                           
         B     NDQTREN             13                                           
         B     NDYRST              14                                           
         B     NDYREN              15                                           
         B     NDBETWN             16                                           
         B     NDDOW               17                                           
*                                                                               
VCOUNT   EQU   (*-VBRANCH)/4                                                    
VERROR   DS    0H                                                               
*                                                                               
*                                                                               
         EJECT                                                                  
VALID    NTR1                                                                   
         CH    RF,=H'3'            NOT FOR NEADDAY                              
         BE    VT0                                                              
         CH    RF,=H'1'                                                         
         BNE   VT4                                                              
         LA    R3,NDLPR            VALIDATE THE LENGTH OF THE PERIOD            
         LA    R4,TYPL1                                                         
         LA    R6,5                                                             
         BAS   RE,VAL                                                           
*                                                                               
VT4      LA    R3,NDTPR            VALIDATE THE TYPE OF THE PERIOD              
         LA    R4,TYPL2                                                         
         LA    R6,7                                                             
         BAS   RE,VAL                                                           
*                                                                               
VT0      LA    R8,NDBYR            VALIDATE THE BEGINING OF THE YEAR            
         BAS   RE,VALBY                                                         
         CLI   ERB,1                                                            
         BNE   VT1                                                              
         MVC   NDBYR(2),=2X'01'                                                 
*                                                                               
VT1      CLI   NDSWDOW,0           VALIDATE THE BEGINING OF THE WEEK            
         BE    VT2                                                              
         CLI   NDSWDOW,7                                                        
         BH    VT2                                                              
         B     *+8                                                              
VT2      MVI   NDSWDOW,1                                                        
*                                                                               
         CH    RF,=H'1'            ONLY FOR GETLIST                             
         BE    *+12                                                             
         CH    RF,=H'14'                                                        
         BNE   VT3                                                              
         CLC   NDEDAT,NDBDAT      VALIDATE THE BEGINING VS THE END DATE         
         BNL   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LH    R8,NDBDAT           VALIDATE THE BEGINING DATE                   
         BAS   RE,VALD                                                          
         MVC   STRTDAT,CURDATE                                                  
*                                                                               
VT3      LH    R8,NDEDAT           VALIDATE THE END DATE                        
         BAS   RE,VALD                                                          
         MVC   ENDDAT,CURDATE                                                   
*                                                                               
         L     R8,NDAMTBL          SET THE MONTH TABLE                          
         CLI   NDTPR,C'C'                                                       
         BE    BLTC                                                             
         CLI   NDTPR,C'B'                                                       
         BE    BLTB                                                             
         CLI   NDTPR,C'S'                                                       
         BE    BLTX                                                             
         CLC   NDTPR,=C'1S'                                                     
         BNE   *+14                                                             
         MVC   0(12,R8),=4AL1(34,27,27)                                         
         B     BLTX                                                             
         CLC   NDTPR,=C'2S'                                                     
         BNE   *+14                                                             
         MVC   0(12,R8),=4AL1(27,34,27)                                         
         B     BLTX                                                             
         CLC   NDTPR,=C'3S'                                                     
         BNE   *+14                                                             
         MVC   0(12,R8),=4AL1(27,27,34)                                         
         B     BLTX                                                             
         CLEAN 0(R8),13,X'1B'                                                   
         B     BLTX                                                             
BLTC     MVI   0(R8),X'1E'                                                      
         BAS   RE,UTSETYR                                                       
         ABIN  S,(1,YRTBL+5),1,OT=(1,1(R8))                                     
         MVC   2(10,R8),=X'1E1D1E1D1E1E1D1E1D1E'                                
         B     BLTX                                                             
BLTB     CLEAN 0(R8),12,X'BB'                                                   
BLTX     XIT1                                                                   
         EJECT                                                                  
VAL      NTR1                                                                   
VAL1     ZIC   R5,0(R3)                                                         
         EX    R5,COMP                                                          
         BE    VALX                                                             
         LA    R3,1(R5,R3)                                                      
         BCT   R6,VAL1                                                          
         DC    H'0'                                                             
COMP     CLC   0(0,R3),0(R4)                                                    
VALX     XIT1                                                                   
*                                                                               
VALD     NTR1                                                                   
         BAS   RE,UTUNCOM                                                       
         CLI   CURYEAR,99                                                       
         BH    VALDX                                                            
         BAS   RE,UTINIT                                                        
         MVC   TYPL3(12),=X'1F1D1F1E1F1E1F1F1E1F1E1F'                           
         MVC   TYPL3+1(1),YRTBL+5                                               
         LA    R8,CURMONTH                                                      
         BAS   RE,VALBY                                                         
         CLI   ERB,1                                                            
         BNE   *+6                                                              
VALDX    DC    H'0'                                                             
         XIT1                                                                   
*                                                                               
VALBY    NTR1                                                                   
         XC    ERB,ERB                                                          
         CLC   0(2,R8),=2X'00'                                                  
         BE    VALBYX                                                           
         CLI   0(R8),0                                                          
         BE    VALBYX                                                           
         CLI   0(R8),12                                                         
         BH    VALBYX                                                           
         ABIN  S,(1,0(R8)),1,OT=(R3)                                            
         ABIN  A,%TYPL3,(3),OT=(3)                                              
         CLC   1(1,R8),0(R3)                                                    
         BH    VALBYX                                                           
         B     *+8                                                              
VALBYX   MVI   ERB,1                                                            
         XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
* NDPERLST- ROUTINE WHICH WILL RETURN A LIST OF DATES IN COMPRESSED   *         
*           FORM.EACH ENTRY IN THE LIST HAS 4 BYTES,REPRESENTING THE  *         
*           BEGINING AND THE END OF EACH PERIOD.                      *         
*           USER CALLABLE                                             *         
*             R1-> BLOCK OF PARAMETERS(NDDATB)                        *         
*                                                                     *         
*         NECESSARY PARAMETERS:                                       *         
*             NDLPR,NDTPR,NDBDAT,NDEDAT,NDALIST,NDNEL                 *         
*                                                                     *         
*         OPTIONAL PARAMETERS:                                        *         
*             NDBYR,NDSWDOW,NDAMTBL                                   *         
***********************************************************************         
NDPERLST DS    0H                                                               
*                                                                               
         CLI   NDLPR,C'D'                                                       
         BE    DAYLIST                                                          
         CLI   NDLPR,C'W'      CHAR DESCRIBES LIST TYPE                         
         BE    WKLIST                                                           
         CLI   NDLPR,C'M'                                                       
         BE    MONLIST                                                          
         CLI   NDLPR,C'Q'                                                       
         BE    QRTLIST                                                          
         CLI   NDLPR,C'Y'                                                       
         BE    YRLIST                                                           
*                                                                               
DAYLIST  LA    R2,DAYHK1                                                        
         B     DMYLIST                                                          
*                                                                               
DAYHK1   NTR1                                                                   
         XIT1                                                                   
*                                                                               
WKLIST   LA    R2,WKLSTHK1                                                      
         ST    R2,FIL1HOOK         ROUTINE TO GET FIRST OF WEEK                 
         LA    R2,WKLSTHK2         ROUTINE TO GET PREV WEEK                     
         ST    R2,FIL2HOOK                                                      
         BAS   RE,FILLIST          FILL THE LIST                                
         B     GLXIT                                                            
WKLSTHK1 NTR1                                                                   
         ZIC   R8,NDSWDOW                                                       
         BAS   RE,UTPRVDOW         GET LAST DOW = START DOW                     
         XIT1                                                                   
WKLSTHK2 NTR1                      HAVE END-OF-WK, GET STRT-OF-WK               
         L     R8,=F'-6'           SO SUBTRACT SIX DAYS                         
         BAS   RE,UTADDAY                                                       
         XIT1                                                                   
*                                                                               
MONLIST  LA    R2,UTFRSTMN         ROUTINE TO GET FIRST OF MONTH                
         B     DMYLIST                                                          
*                                                                               
QRTLIST  LA    R2,UTFRSTQR                                                      
         B     DMYLIST                                                          
*                                                                               
YRLIST   LA    R2,UTFRSTYR                                                      
*                                                                               
DMYLIST  ST    R2,FIL1HOOK         USE IT AS A HOOK                             
         ST    R2,FIL2HOOK         SERVES DOUBLE DUTY                           
         BAS   RE,FILLIST                                                       
         B     GLXIT                                                            
*                                                                               
GLXIT    XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
*                                                                     *         
* FILLIST -  MODULE TO FILL IN A PERIOD LIST.                         *         
*   INPUTS: STRTDAT, ENDDAT -  START AND END DATES                    *         
*           MAXNTRYS - MAXIMUM ENTRIES TO PUT IN LIST. MINIMUM 3.     *         
*           OUTLIST  - ADDRESS OF LIST TO BUILD                       *         
*           FIL1HOOK - THE ROUTINE TO GET BEGINNING OF LAST PERIOD    *         
*           FIL2HOOK - THE ROUTINE TO GET BEGINNING OF ALL OTHER PERS *         
*                COMDAT - COMPRESSED CURRENT DATE FILLED BY FILADDR   *         
*                                                                     *         
*    OUTPUTS: OUTLIST - FILLED IN. FIRST SET IS MINDAT TO STRTDAT-1   *         
*                            LAST IS ENDDAT+1 TO MAX DATE.            *         
*             MAXNTRYS - NUMBER OF ENTRIES IN LIST                    *         
*                                                                     *         
*   LOCALS : R3 - CURRENT ADDRESS IN LIST                             *         
*            R4 - CURRENT NUMBER OF ENTRIES LEFT IN LIST              *         
*            R5 - A(OUTLIST)                                          *         
*            R6 - INPUT MAX NUMBER ENTRIES                            *         
***********************************************************************         
FILLIST  NTR1                                                                   
*                                                                               
         L     R5,NDALIST                                                       
         L     R6,NDNEL                                                         
         LR    R3,R6                                                            
         BCTR  R3,0                                                             
         SLL   R3,2                  EACH ENTRY IS 4 BYTES                      
         LA    R3,0(R3,R5)         POINT R3 TO LAST ENTRY IN LIST               
         LTR   R4,R6                                                            
         BZ    XITFILL             IF NO ENTRIES REQUESTED                      
         BCTR  R4,0                R4 IS NUMBER OF ENTRIES LEFT IN LIST         
*                                                                               
*                                                                               
STRTFIL  MVC   CURDATE,ENDDAT                                                   
         BAS   RE,COMPRESS                                                      
         MVC   2(2,R3),COMDAT                                                   
         BAS   RE,COMPRESS                                                      
         L     RF,FIL1HOOK                                                      
         BASR  RE,RF               GET THE BEGINNING OF THIS PERIOD             
         BAS   RE,COMPRESS         COMPRESS THIS DATE INTO COMDAT               
FILLOOP  CLC   CURDATE,STRTDAT     IF NEW DATE BEFORE (OR =) START              
         BH    FL2                                                              
         MVC   CURDATE,STRTDAT                                                  
         BAS   RE,COMPRESS                                                      
         MVC   0(2,R3),COMDAT      ..START DATE IS BEGINNING OF PERIOD          
         B     FILLED              ..LIST DONE                                  
*                                                                               
FL2      MVC   0(2,R3),COMDAT      NEW DATE IS BEGINNING OF PERIOD              
         LTR   R4,R4               CHECK FOR END OF LIST                        
         BNH   LISTFULL                                                         
         LH    R8,=H'-1'                                                        
         BAS   RE,UTADDAY          GET DAY BEFORE                               
         BAS   RE,COMPRESS         COMPRESS THIS DATE                           
         SH    R3,=H'4'            PREVIOUS ENTRY IN LIST                       
         MVC   2(2,R3),COMDAT      STORE NEXT ENDDATE                           
         BCTR  R4,0                                                             
*                                                                               
*                                                                               
SOMELEFT L     RF,FIL2HOOK                                                      
         BASR  RE,RF               GET BEGINNING OF THIS PERIOD                 
         BAS   RE,COMPRESS         COMPRESS THIS NEW DATE (SET COMDAT)          
         B     FILLOOP                                                          
*                                                                               
LISTFULL B     XITFILL                                                          
*                                                                               
FILLED   DS    0H                  NOW ALIGN LIST SO STARTS AT OUTLIST          
         LR    R2,R6                                                            
         SR    R2,R4               NUMBER OF ENTRIES USED                       
         ST    R2,NDNEL            RETURN NUMBER USED                           
         SLL   R2,2                NUMBER OF BYTES USED                         
         MOVE  ((R5),(R2)),(R3)                                                 
*                                                                               
         LA    R5,0(R5,R2)         END OF CURRENT LIST                          
         SLL   R6,2                NUM OF BYTES IN USER SUPPLIED LIST           
         SR    R6,R2               NUMBER OF BYTES TO CLEAR                     
         XCEF  (R5),(R6)           CLEAR OUT REST OF LIST                       
*                                                                               
XITFILL  XIT1                                                                   
         EJECT                                                                  
*********************************************************************           
* NDMNAME  - RETURNS THE ALPANUMERIC MONTH FOR THE COMPRESSED DATE. *           
*            USER CALLABLE                                          *           
*                R1-> BLOCK OF PARAMETERS(NDDATB)                   *           
*    NECESSARY PARAMETERS:                                          *           
*       NDTPR                                                       *           
*       NDEDAT : INPUT DATE                                         *           
*       NDALIST: (OUTPUT)  BYTE 0: LENGTH OF ALPHA REQUIRED (9 MAX) *           
*                          BYTE 1-3: A(OUTPUT FIELD FOR ALPHA)      *           
*                                                                   *           
*    OPTIONAL PARAMETERS:                                           *           
*       NDBYR,NDSWDOW,NDAMTBL                                       *           
*********************************************************************           
NDMNAME  DS    0H                                                               
         LH    R8,NDEDAT           COMPRESSED DATE                              
         ZIC   R3,NDALIST          NUMBER OF BYTES TO MOVE                      
         BCTR  R3,0                ONE LESS MORE EXMVC                          
         L     R4,NDALIST          OUTPUT AREA ADDRESS                          
         BAS   RE,UNCOMPRS         GET IN NORMAL FORM                           
         BAS   RE,UTYR             MONTH NUMBER                                 
         L     R4,WAM                                                           
         ZIC   R2,0(R4)                                                         
         MH    R2,=H'9'            OFFSET INTO LOOKUP TABLE                     
         LA    R2,MONLKUP(R2)                                                   
         EXMVC R3,0(R4),0(R2)                                                   
         XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
* NDADDAY - USER CALLABLE ADDAY ROUTINE.                              *         
*           R1->BLOCK OF PARAMETERS(NDDATB)                           *         
*    NECESSARY PARAMETERS:                                            *         
*          NDEDAT  : INPUT DATE                                       *         
*          NDALIST : A(RETURNED DATE)                                 *         
*          NDNEL   : A(FULLWORD TO ADD IN)                            *         
***********************************************************************         
NDADDAY  DS    0H                                                               
         LH    R8,NDEDAT           COMPRESSED DATE                              
         L     R3,NDALIST          ADDRESS OF RETURN DATE                       
         L     R4,NDNEL            ADRESS OF FULLWORD TO ADD                    
         BAS   RE,UNCOMPRS         GET IN NORMAL FORM                           
         L     R8,0(R4)            FULLWORD TO ADD                              
         BAS   RE,UTADDAY          ADD IT                                       
         BAS   RE,COMPRESS         RECOMPRESS IT                                
         MVC   0(2,R3),COMDAT      MOVE IT TO OUTPUT AREA                       
         XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
* NDWKST  - USER CALLABLE ROUTINE FOR THE FIRST DAY OF THE WEEK       *         
***********************************************************************         
* NDWKEN  - USER CALLABLE ROUTINE FOR THE LAST DAY OF THE WEEK        *         
***********************************************************************         
* NDMONST - USER CALLABLE ROUTINE FOR THE FIRST DAY OF THE MONTH      *         
***********************************************************************         
* NDMONEN - USER CALLABLE ROUTINE FOR THE LAST DAY OF THE MONTH       *         
***********************************************************************         
* NDQTRST - USER CALLABLE ROUTINE FOR THE FIRST DAY OF THE QUARTER    *         
***********************************************************************         
* NDQTREN - USER CALLABLE ROUTINE FOR THE LAST DAY OF THE QUARTER     *         
***********************************************************************         
* NDYRST  - USER CALLABLE ROUTINE FOR THE FIRST DAY OF THE YEAR       *         
***********************************************************************         
* NDYREN  - USER CALLABLE ROUTINE FOR THE LAST DAY OF THE YEAR        *         
***********************************************************************         
*          R1-> BLOCK OF PARAMETERS(NDDATB)                           *         
*    NECESSARY PARAMETERS:                                            *         
*          NDTPR                                                      *         
*          NDEDAT  :  INPUT DATE                                      *         
*          NDALIST :  A(RETURNED DATE)                                *         
*                                                                     *         
*    OPTIONAL PARAMETERS:                                             *         
*          NDBYR,NDSWDOW,NDAMTBL                                      *         
***********************************************************************         
NDWKST   DS    0H                                                               
         LA    R4,UTFRSTWK                                                      
         BAS   R5,NEMD                                                          
         XIT1                                                                   
*                                                                               
NDWKEN   DS    0H                                                               
         LA    R4,UTLSTWK                                                       
         BAS   R5,NEMD                                                          
         XIT1                                                                   
*                                                                               
NDMONST  DS    0H                                                               
         LA    R4,UTFRSTMN                                                      
         BAS   R5,NEMD                                                          
         XIT1                                                                   
*                                                                               
NDMONEN  DS    0H                                                               
         LA    R4,UTLSTMN                                                       
         BAS   R5,NEMD                                                          
         XIT1                                                                   
*                                                                               
NDQTRST  DS    0H                                                               
         LA    R4,UTFRSTQR                                                      
         BAS   R5,NEMD                                                          
         XIT1                                                                   
*                                                                               
NDQTREN  DS    0H                                                               
         LA    R4,UTLSTQR                                                       
         BAS   R5,NEMD                                                          
         XIT1                                                                   
*                                                                               
NDYRST   DS    0H                                                               
         LA    R4,UTFRSTYR                                                      
         BAS   R5,NEMD                                                          
         XIT1                                                                   
*                                                                               
NDYREN   DS    0H                                                               
         LA    R4,UTLSTYR                                                       
         BAS   R5,NEMD                                                          
         XIT1                                                                   
*                                                                               
NEMD     DS    0H                                                               
         L     R3,NDALIST          ADDRESS OF RETURN DATE                       
         BAS   RE,0(R4)                                                         
         BAS   RE,COMPRESS         RECOMPRESS IT                                
         MVC   0(2,R3),COMDAT      MOVE IT TO OUTPUT AREA                       
         BR    R5                                                               
         EJECT                                                                  
***********************************************************************         
*  NDDOW - USER CALLABLE ROUTINE FOR THE DAY OF THE WEEK              *         
***********************************************************************         
*          R1-> BLOCK OF PARAMETERS(NDATB)                            *         
*    NECESSARY PARAMETERS : NDEDAT : INPUT COMPRESSED DATE            *         
*                           NDALIST: A(ONE BYTE FIELD CONTAINING DOW) *         
***********************************************************************         
NDDOW    DS    0H                                                               
         L     R3,NDALIST                                                       
         MVC   0(1,R3),CURDOW                                                   
         XIT1                                                                   
***********************************************************************         
*  NDBETWN - USER CALLABLE ROUTINE FOR THE ELAPSED # OF DAYS          *         
***********************************************************************         
*          R1-> BLOCK OF PARAMETERS(NDDATB)                           *         
*    NECESSARY PARAMETERS : NDBDAT : LOWER INPUT COMPRESSED DATE      *         
*                           NDEDAT : HIGHER INPUT COMPRESSED DATE     *         
*                           NDALIST: A(10 BYTE FIELD)                 *         
*                             THE FORMAT OF THE FIELD IS:             *         
*                                1ST HALFWORD:# OF DAYS(INCLUSIVE)    *         
*                                2ND HALFWORD:REMAINDER OF DAYS/7     *         
*                                3RD HALFWORD:QUOTIENT OF DAYS/7      *         
*                                4TH HALFWORD:# OF MONTHS(INCLUSIVE)  *         
*                                5TH HALFWORD:# OF YEARS(INCLUSIVE)   *         
***********************************************************************         
NDBETWN  DS    0H                                                               
         XC    DMCB,DMCB                                                        
         GOTO1 UTSUB,STRTDAT,ENDDAT,NDALIST                                     
         XIT1                                                                   
         EJECT                                                                  
**********************************************************************          
*  UNCOMPRS - UTILITY TO CONVERT A COMPRESSED DATE TO CURDATE FORMAT.*          
*   ARG: R8 - THE COMPRESSED DATE TO CONVERT (2 BYTES)               *          
**********************************************************************          
UNCOMPRS NTR1                                                                   
         BAS   RE,UTUNCOM                                                       
         BAS   RE,UTINIT           SET MONADDR, YRADDR, CURDOW                  
         XIT1                                                                   
**********************************************************************          
UTUNCOM  NTR1                                                                   
         STH   R8,COMDAT                                                        
         ZIC   R2,COMDAT           GET YEAR                                     
         SRL   R2,1                                                             
         STC   R2,CURYEAR                                                       
         ZIC   R2,COMDAT+1         GET DAY                                      
         N     R2,=XL4'0000001F'                                                
         STC   R2,CURDAY                                                        
         LH    R2,COMDAT                                                        
         N     R2,=XL4'000001FF'                                                
         SRL   R2,5                                                             
         STC   R2,CURMONTH                                                      
         XIT1                                                                   
**********************************************************************          
*  UTINIT - UTILITY TO SET CURDOW, YRADDR, MONADDR FROM CURDATE.     *          
**********************************************************************          
UTINIT   NTR1                                                                   
         SR    R8,R8                                                            
         BAS   RE,UTSETYR          SET YRADDR                                   
         ZIC   R2,CURMONTH                                                      
         BCTR  R2,0                SUBTRACT ONE FROM MONTH                      
         SLL   R2,2                MULT BY 4 FOR OFFSET OF THIS MONTH           
         ABIN  A,(R2),%YRTBL,OT=MONADDR                                         
*                                                                               
         BAS   RE,UTDOW                                                         
         XIT1                                                                   
***********************************************************************         
* COMPRESS - PUTS THE CURRENT LOW LEVEL COMMON DATE IN COMPRESSED FORM*         
*    OUTPUT - COMDAT - COMPRESSED DATE                                *         
***********************************************************************         
COMPRESS NTR1                                                                   
         IC    R2,CURDAY                                                        
         SRDL  R2,5                                                             
         IC    R2,CURMONTH                                                      
         SRDL  R2,4                                                             
         IC    R2,CURYEAR                                                       
         SRDL  R2,23               R7 NOW HAS COMPRRESSED DATE                  
         STH   R3,COMDAT                                                        
         XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
*     LOW LEVEL UTILITY TO DEAL WITH YEARS.                           *         
***********************************************************************         
*    UTILITY TO  INCREMENT,DECREMENT THE YEAR. SETS CURYEAR.          *         
***********************************************************************         
UTADYR   NTR1                                                                   
         ABIN  A,(R8),CURYEAR                                                   
         ABIN  A,(R8),1900                                                      
         ABIN  D,(R8),100,RD=N                                                  
         STC   R0,CURYEAR                                                       
         ABIN  M,(R8),100                                                       
         BAS   RE,UTSETYR                                                       
         XIT1                                                                   
***********************************************************************         
*     UTILITY TO RESET THE CALENDAR YEAR TABLE.                       *         
* EACH ENTRY IN THE TABLE IS 4 BYTES:                                 *         
*    (MONTH,MONTH LENGTH,1ST OF THE MOTH DOW,END OF THE MONTH DOW)    *         
***********************************************************************         
UTSETYR  NTR1                                                                   
         XC    DMCB,DMCB                                                        
         XC    YEAR(56),YEAR                                                    
         ABIN  A,YEAR,CURYEAR                                                   
         LA    R3,YRTBL                                                         
         GOTO1 TYB,DMCB,YEAR,(R3),(R8)                                          
         XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
*     LOW LEVEL UTILITY TO ADD MONTHS.                                *         
***********************************************************************         
*    SETS CURMONTH, MONADDR(POSSIBLY CURYEAR,YRADDR)                  *         
***********************************************************************         
UTADMON  NTR1                                                                   
         ABIN  A,(R8),CURMONTH                                                  
         ABIN  D,(R8),12,RD=N                                                   
         LTR   R8,R8                                                            
         BH    UTAM3                                                            
         BL    UTAM2                                                            
         LTR   R0,R0                                                            
         BH    UTAM1                                                            
         B     UTAM2                                                            
UTAM3    LTR   R0,R0                                                            
         BNZ   UTAM1                                                            
UTAM2    SH    R8,=H'1'                                                         
         AH    R0,=H'12'                                                        
UTAM1    STC   R0,CURMONTH                                                      
         LTR   R8,R8                                                            
         BZ    *+8                                                              
         BAS   RE,UTADYR                                                        
         ABIN  M,CURMONTH,4,OT=(2)                                              
         SH    R2,=H'4'                                                         
         ABIN  A,%YRTBL,(2),OT=MONADDR                                          
         XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
* UTADDAY - TO ADD DAYS.                                              *         
*       ARGS:  R8 - NUMBER OF DAYS TO ADD                             *         
*                                                                     *         
* SETS  CURDAY, CURDOW                                                *         
* POSSIBLY SETS CURMON, MONADDR.   (IF SPANS MONTHS)                  *         
* POSSIBLY SETS CURYR, YRADDR.     (IF SPANS YEARS)                   *         
*   R2 - SUM OF CURDAY + NUMBER TO ADD LEFT TO PROCESS                *         
*   R3 - MONADDR                                                      *         
*   R4 - NUMBER OF DAYS IN MONTH                                      *         
***********************************************************************         
UTADDAY  NTR1                                                                   
         ZIC   R2,CURDAY                                                        
         AR    R2,R8               ADD NUMBER OF DAYS                           
         BH    ADCKHI                                                           
*                                  FOR NEGATIVE OR ZERO SUM                     
ADLOOP1  LH    R8,=H'-1'                                                        
         BAS   RE,UTADMON          PREVIOUS MONTH (UPDATE MONADDR)              
         L     R3,MONADDR                                                       
         ZIC   R4,MNLEN(R3)        NUMBER OF DAYS IN PREVIOUS MONTH             
         AR    R2,R4                                                            
         BH    ADXIT               ANOTHER MONTH BEFORE?                        
         B     ADLOOP1                                                          
*                                                                               
ADCKHI   DS    0H                  SUM GREATER THAN ZERO                        
ADLOOP2  L     R3,MONADDR          MONTH ADDRESS                                
         ZIC   R4,MNLEN(R3)        NUMBER OF DAYS IN MONTH                      
         CR    R2,R4                                                            
         BNH   ADXIT               CK IF IN NEXT MONTH                          
         LH    R8,=H'1'                                                         
         BAS   RE,UTADMON          INCREMENT MONTH (MONADDR UPDATED)            
         SR    R2,R4               DAYS PAST BEGINNING OF NEXT MONTH            
         B     ADLOOP2                                                          
*                                                                               
ADXIT    STC   R2,CURDAY           R2 HAS NEW DAY                               
         BAS   RE,UTDOW                                                         
         XIT1                                                                   
*                                                                               
         EJECT                                                                  
***********************************************************************         
*  LOW LEVEL UTILITIES TO DEAL WITH DOW                               *         
***********************************************************************         
*  UTDOW - ROUTINE TO OBTAIN THE DOW OF THE CURRENT DATE              *         
***********************************************************************         
UTDOW    NTR1                                                                   
         L     R3,MONADDR                                                       
         ABIN  A,(1,2(R3)),CURDAY,OT=(R4)                                       
         SH    R4,=H'2'                                                         
         ABIN  D,(4),7,RD=N,KEEP=Y                                              
         AH    R0,=H'1'                                                         
         STC   R0,CURDOW                                                        
         XIT1                                                                   
***********************************************************************         
*  UTPRVDOW - ROUTINE TO GET THE PREVIOUS DAY = DOW                   *         
*  ARGS: R8 - THE DOW TO MATCH                                        *         
***********************************************************************         
UTPRVDOW NTR1                                                                   
         ZIC   R2,CURDOW           THE CURRENT DAY OF WEEK                      
         CR    R8,R2                                                            
         BNH   GP2                                                              
         AH    R2,=H'7'            IF PREVIOUS > CURRENT                        
GP2      SR    R8,R2               NUMBER OF DAYS BEFORE IS IN R8               
         BAS   RE,UTADDAY          SUBTRACT THIS MANY DAYS                      
         XIT1                                                                   
*********************************************************************           
*  UTNXTDOW - ROUTINE TO GET THE NEXT  DAY = DOW                    *           
*  ARGS: R8 - THE DOW TO MATCH                                      *           
*********************************************************************           
UTNXTDOW NTR1                                                                   
         ZIC   R2,CURDOW           THE CURRENT DAY OF WEEK                      
         CR    R8,R2                                                            
         BNL   GN2                                                              
         AH    R8,=H'7'            IF PREVIOUS > CURRENT                        
GN2      SR    R8,R2               NUMBER OF DAYS AFTER IS IN R1                
         BAS   RE,UTADDAY          ADD THIS MANY DAYS                           
         XIT1                                                                   
         EJECT                                                                  
**********************************************************************          
* LOW LEVEL UTILITIES TO DEAL WITH WEEKS                             *          
**********************************************************************          
* UTFRSTWK - GET THE FIRST DAY OF THE CURRENT WEEK                   *          
**********************************************************************          
UTFRSTWK NTR1                                                                   
         LH    R2,=H'-7'                                                        
         BAS   RE,TWK                                                           
         XIT1                                                                   
**********************************************************************          
* UTLSTWK - GET THE LAST DAY OF THE CURRENT WEEK                     *          
**********************************************************************          
UTLSTWK  NTR1                                                                   
         LH    R2,=H'-1'                                                        
         BAS   RE,TWK                                                           
         XIT1                                                                   
**********************************************************************          
TWK      NTR1                                                                   
         BAS   RE,UTYR                                                          
         L     R4,WAM                                                           
         MVC   CURDATE(4),2(R4)                                                 
         BAS   RE,UTINIT                                                        
         LA    R3,7                                                             
         LA    R8,7                                                             
TWK1     BAS   RE,UTADDAY                                                       
         CLC   ENDDAT(3),CURDATE                                                
         BL    TWK3                                                             
         BCT   R3,TWK1                                                          
TWK3     LR    R8,R2                                                            
         BAS   RE,UTADDAY                                                       
TWK4     XIT1                                                                   
**********************************************************************          
* LOW LEVEL UTILITIES TO DEAL WITH MONTHS                            *          
**********************************************************************          
* UTFRSTMN - GET THE FIRST DAY OF THE CURRENT MONTH.                 *          
**********************************************************************          
UTFRSTMN NTR1                                                                   
         BAS   RE,UTYR                                                          
         L     R4,WAM                                                           
         MVC   CURDATE(4),2(R4)                                                 
         XIT1                                                                   
*                                                                               
**********************************************************************          
* UTLSTMN - GET THE LAST DAY OF THE CURRENT MONTH.                  *           
**********************************************************************          
UTLSTMN  NTR1                                                                   
         BAS   RE,UTYR                                                          
         L     R4,WAM                                                           
         MVC   CURDATE(4),6(R4)                                                 
         XIT1                                                                   
         EJECT                                                                  
**********************************************************************          
* LOW LEVEL UTILITIES TO DEAL WITH QUARTERS                          *          
**********************************************************************          
* UTFRSTQR - GET THE FIRST DAY OF THE CURRENT QUARTER                *          
**********************************************************************          
UTFRSTQR NTR1                                                                   
         MVC   TAM1(14),=AL1(0,1,0,0,4,0,0,7,0,0,10,0,0,0)                      
         LH    R5,=H'-10'                                                       
         BAS   RF,TQR                                                           
         MVC   CURDATE(4),2(R4)                                                 
         XIT1                                                                   
**********************************************************************          
* UTLSTQR - GET THE LAST DAY OF THE CURRENT QUARTER                  *          
**********************************************************************          
UTLSTQR  NTR1                                                                   
         MVC   TAM1(14),=AL1(0,0,0,3,0,0,6,0,0,9,0,0,12,13)                     
         LH    R5,=H'10'                                                        
         BAS   RF,TQR                                                           
         CH    R2,=H'12'                                                        
         BNE   QRX                                                              
         CLC   NDTPR(2),=C'4S'                                                  
         BNE   QRX                                                              
         LA    R4,10(R4)                                                        
QRX      MVC   CURDATE(4),6(R4)                                                 
         XIT1                                                                   
**********************************************************************          
TQR      BAS   RE,UTYR                                                          
         L     R4,WAM                                                           
         SR    R1,R1                                                            
         SR    R2,R2                                                            
         TRT   0(1,R4),TAM1                                                     
         BNZ   *+10                                                             
         AR    R4,R5                                                            
         B     *-12                                                             
         BR    RF                                                               
**********************************************************************          
* LOW LEVEL UTILITIES TO DEAL WITH YEARS                             *          
**********************************************************************          
* UTFRSTYR - GET THE FIRST DAY OF THE CURRENT YEAR.                  *          
**********************************************************************          
UTFRSTYR NTR1                                                                   
         BAS   RE,UTYR                                                          
         MVC   CURDATE(4),YRTBLS+2                                              
         XIT1                                                                   
**********************************************************************          
* UTLSTYR  - GET THE LAST DAY OF THE CURRENT YEAR.                   *          
**********************************************************************          
UTLSTYR  NTR1                                                                   
         BAS   RE,UTYR                                                          
         CLC   NDTPR,=C'4S'                                                     
         BNE   *+14                                                             
         MVC   CURDATE(4),YRTBLS+126                                            
         B     *+10                                                             
         MVC   CURDATE(4),YRTBLS+116                                            
         XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
*UTSUB IS A ROUTINE WHICH GIVES THE # OF DAYS ELAPSED BETWEEN 2 DATES *         
*      PARAMETER1 :1ST DATE;FORM : 3 BYTES(YEAR,MONTH,DAY)            *         
*      PARAMETER2 :2ND DATE;FORM : 3 BYTES(YEAR,MONTH,DAY)            *         
*      PARAMETER3 :WORD CONTAINING THE ADDRESS OF A 10 BYTES FIELD    *         
*                             THE FORMAT OF THE FIELD IS:             *         
*                                1ST HALFWORD:# OF DAYS(INCLUSIVE)    *         
*                                2ND HALFWORD:REMAINDER OF DAYS/7     *         
*                                3RD HALFWORD:QUOTIENT OF DAYS/7      *         
*                                4TH HALFWORD:# OF MONTHS(INCLUSIVE)  *         
*                                5TH HALFWORD:# OF YEARS(INCLUSIVE)   *         
***********************************************************************         
UTSUB    NTR1                                                                   
         MVC   SAVM+12(12),CURDATE                                              
         LM    R3,R5,0(R1)                                                      
         L     R5,0(R5)                                                         
         XC    0(8,R5),0(R5)                                                    
         MVC   8(2,R5),=H'1'                                                    
         CLC   0(3,R3),0(R4)                                                    
         BL    SUBEX                                                            
         MVC   CURDATE(3),0(R4)                                                 
         BAS   RE,UTINIT                                                        
         CLC   0(2,R3),0(R4)                                                    
         BNE   SUBD1                                                            
         ABIN  S,(1,2(R3)),(1,2(R4)),OT=(2,0(R5))                               
         ABIN  A,(2,6(R5)),1                                                    
         B     SUBEX                                                            
SUBD1    L     R6,MONADDR                                                       
         ABIN  S,(1,1(R6)),(1,2(R4)),OT=(2,0(R5))                               
         ABIN  A,(2,6(R5)),1                                                    
SUBD4    CLC   0(2,R3),0(R4)                                                    
         BE    SUBD2                                                            
         CLI   1(R4),12                                                         
         BNE   SUBD3                                                            
         MVC   1(2,R4),=X'0101'                                                 
         ABIN  A,(1,0(R4)),1                                                    
         MVC   CURDATE(3),0(R4)                                                 
         BAS   RE,UTINIT                                                        
         ABIN  A,(2,8(R5)),1                                                    
         B     SUBD5                                                            
SUBD3    ABIN  A,(1,1(R4)),1                                                    
         MVI   2(R4),1                                                          
         ABIN  A,MONADDR,4                                                      
SUBD5    CLC   0(2,R3),0(R4)                                                    
         BE    SUBD2                                                            
         L     R6,MONADDR                                                       
         ABIN  A,(2,0(R5)),(1,1(R6))                                            
         ABIN  A,(2,6(R5)),1                                                    
         B     SUBD4                                                            
SUBD2    ABIN  A,(2,0(R5)),(1,2(R3))                                            
         ABIN  A,(2,6(R5)),1                                                    
SUBEX    MVC   CURDATE(12),SAVM+12                                              
         ABIN  A,(2,0(R5)),1                                                    
         ABIN  D,(2,0(R5)),7,RD=N,OT=(2,4(R5))                                  
         STH   R0,2(R5)                                                         
         XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
*  UTYR IS A ROUTINE WHICH BUILDS A(10 BYTES/ENTRY) GENERALIZED YEAR  *         
*TABLE,FOR THE SPECIFIC CURRENT DATE.IT ALSO LOADS IN WAM AN ADDRESS  *         
* OF THE MONTH ENTRY WHERE THE CURRENT DATE BELONGS.                  *         
***********************************************************************         
UTYR     NTR1                                                                   
         MVC   WYR,CURYEAR                                                      
         CLC   NDTPR,=C'4S'                                                     
         BE    *+12                                                             
         LA    R5,YRTBLS+110                                                    
         B     *+8                                                              
         LA    R5,YRTBLS+120                                                    
UTY1     GOTO1 TYBS,DMCB,NDBYR,NDSWDOW,WYR,NDAMTBL,NDTPR,YRTBLS                 
         CLC   CURDATE(4),YRTBLS+2                                              
         BNL   UTY2                                                             
         ABIN  S,WYR,1                                                          
         B     UTY1                                                             
UTY2     CLC   CURDATE(4),6(R5)                                                 
         BNH   UTYX                                                             
         ABIN  A,WYR,1                                                          
         B     UTY1                                                             
UTYX     LA    R3,YRTBLS                                                        
         LA    R4,10                                                            
UTYX1    CLC   CURDATE(4),2(R3)                                                 
         BH    *+8                                                              
         B     UTYX2                                                            
         CLC   CURDATE(4),6(R3)                                                 
         BNH   *+8                                                              
         BXLE  R3,R4,UTYX1                                                      
UTYX2    ST    R3,WAM                                                           
         XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
**************************** TYB **************************************         
***********************************************************************         
*TYB IS A ROUTINE WHICH BUILDS A CALENDAR YEAR TABLE AS AN OUTPUT     *         
*                                                                     *         
*        PARAMETER1:THE INPUT YEAR(ADDRESS OF WORD CONTAINIG A 2      *         
*                   DIGIT NUMBER)                                     *         
*        PARAMETER2:THE ADDRESS OF THE 48 BYTES OUTPUT TABLE          *         
*                                                                     *         
*        PARAMETER3:THE BASE YEAR(ADDRESS OF A WORD CONTAINIG A 4     *         
*                       DIGIT NUMBER:1900 OR 2000 OR 2100,ETC)        *         
*                   IN CASE THE ADDRESS=0,THE DEFAULT VALUE FOR THE   *         
*                   BASE YEAR IS 1900.                                *         
***********************************************************************         
TYB      NTR1                                                                   
         LM    R3,R5,0(R1)                                                      
         N     R3,=X'0FFFFFFF'                                                  
         ABIN  D,0(R3),4,KEEP=Y,RD=N                                            
         C     R0,=F'0'                                                         
         BNE   *+8                                                              
         O     R3,=X'80000000'                                                  
         LTR   R5,R5                                                            
         BZ    TYB1                                                             
         ABIN  A,0(R3),(R5),KEEP=Y                                              
         B     TYB2                                                             
TYB1     ABIN  A,1900,0(R3)                                                     
TYB2     CLC   0(4,R3),=F'50'                                                   
         BH    *+8                                                              
         AH    R1,=H'100'                                                       
         LR    R6,R1                                                            
*  ALGORITHM                                                                    
         ABIN  S,(R6),1900                                                      
         ABIN  D,(R6),4,RD=N,OT=(R2)                                            
         CL    R3,=X'80000000'                                                  
         BL    *+6                                                              
         BCTR  R2,0                                                             
         ABIN  A,(R6),(R2)                                                      
         ABIN  D,(R6),7,RD=N,KEEP=Y                                             
         LR    R8,R0                                                            
         LA    R8,1(R8)                                                         
*  YEAR TABLE                                                                   
         SR    RC,RC                                                            
         LR    R9,R4                                                            
         LA    R6,12                                                            
         LA    R2,=X'1F1C1F1E1F1E1F1F1E1F1E1F'                                  
TBB      ABIN  A,(12),1                                                         
         STC   RC,0(R9)                                                         
         MVC   1(1,R9),0(R2)                                                    
         CH    RC,=H'2'                                                         
         BNE   TBB1                                                             
         CL    R3,=X'80000000'                                                  
         BL    TBB1                                                             
         ABIN  A,(1,1(R9)),1                                                    
TBB1     STC   R8,2(R9)                                                         
         ABIN  A,(8),(1,1(R9))                                                  
         ABIN  S,(8),2                                                          
         ABIN  D,(8),7,RD=N,KEEP=Y                                              
         LR    R8,R0                                                            
         ABIN  A,(8),1,OT=(1,3(R9))                                             
         CLI   3(R9),7                                                          
         BL    *+12                                                             
         LA    R8,1                                                             
         B     TBB2                                                             
         ABIN  A,(1,3(R9)),1,OT=(8)                                             
TBB2     ABIN  A,(9),4                                                          
         ABIN  A,(2),1                                                          
         BCT   R6,TBB                                                           
         XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
*************************** TYBS **************************************         
***********************************************************************         
*   TYBS IS A ROUTINE WHICH BUILDS GENERALIZED YEAR TABLE             *         
*        PARAM1 : BEGINING OF THE YEAR(2 BYTES-MONTH&DAY)             *         
*        PARAM2 : STARTING DATE OF THE WEEK(DOW:1-7)                  *         
*        PARAM3 : STARTING DATE OF THE NECESSARY TABLE                *         
*        PARAM4 : MONTH TABLE(12 BYTES CONSISTING OF RESPECTIVE       *         
*                             LEGTHS)                                 *         
*        YRTBLS : GENERALIZED YEAR TABLE (120 BYTES WITH THE FORMAT   *         
*                 AL1(MONTH),AL1(MONLEN),AL4(BEGMON),AL4(ENDMON)      *         
*                 BEGMON,ENDMON = (YEAR,MON,DAY,DOW)                  *         
***********************************************************************         
TYBS     NTR1                                                                   
         MVC   SAVDATE,CURDATE                                                  
         CLEAN DAT1,12,X'00'                                                    
         LM    R3,R6,0(R1)                                                      
         LM    R8,R9,16(R1)                                                     
         XC    0(130,R9),0(R9)                                                  
         L     R6,0(R6)                                                         
         MVC   CURYEAR(1),0(R5)                                                 
         MVC   CURMONTH(2),0(R3)                                                
         BAS   RE,UTINIT                                                        
         LR    R3,R8                                                            
*                                                                               
         MVC   DAT1(4),CURDATE                                                  
         CLI   0(R3),C'C'                                                       
         BE    TS6                                                              
         ZIC   R8,0(R4)                                                         
         BAS   RE,UTPRVDOW                                                      
         CLI   0(R3),C'B'                                                       
         BE    TS6                                                              
*                                                                               
         MVC   DAT2(3),CURDATE                                                  
         MVC   SAVM(12),CURDATE                                                 
         MVC   CURDATE(3),DAT1                                                  
         ABIN  A,CURYEAR,1                                                      
         BAS   RE,UTINIT                                                        
         ZIC   R8,0(R4)                                                         
         BAS   RE,UTPRVDOW                                                      
         LH    R8,=H'-1'                                                        
         BAS   RE,UTADDAY                                                       
         MVC   DAT1(3),CURDATE                                                  
         XC    DMCB,DMCB                                                        
         LA    R2,TAM                                                           
         ST    R2,COUNT                                                         
         GOTO1 UTSUB,DMCB,DAT1,DAT2,COUNT                                       
         CLC   4(2,R2),=H'53'                                                   
         BNE   TS7                                                              
         CLC   0(2,R3),=C'4S'                                                   
         BNE   TS8                                                              
         ABIN  A,(1,12(R6)),7                                                   
         B     TS7                                                              
TS8      ABIN  A,(1,11(R6)),7                                                   
TS7      MVC   CURDATE(12),SAVM                                                 
         BAS   RE,UTINIT                                                        
*                                                                               
TS6      LH    R8,=H'-1'                                                        
         BAS   RE,UTADDAY                                                       
         LA    R2,12                                                            
         CLC   0(2,R3),=C'4S'                                                   
         BNE   *+8                                                              
         LA    R2,1(R2)                                                         
         LA    R9,YRTBLS                                                        
*                                                                               
TS1      LA    R8,1                                                             
         BAS   RE,UTADDAY                                                       
         CLC   0(2,R3),=C'4S'                                                   
         BNE   TS9                                                              
         ABIN  S,14,(2),OT=(1,0(R9))                                            
         B     TS10                                                             
TS9      ABIN  S,13,(2),OT=(1,0(R9))                                            
TS10     MVC   2(4,R9),CURDATE                                                  
         CLI   0(R3),C'B'                                                       
         BE    TS2                                                              
         ZIC   R8,0(R6)                                                         
         BAS   RE,UTADDAY                                                       
         ABIN  A,(1,0(R6)),1,OT=(1,1(R9))                                       
TS4      MVC   6(4,R9),CURDATE                                                  
         B     TSX                                                              
TS2      CLI   CURDAY,1                                                         
         BNE   TS3                                                              
         LA    R8,1                                                             
TS5      BAS   RE,UTADMON                                                       
         MVI   CURDAY,1                                                         
         BAS   RE,UTDOW                                                         
         ZIC   R8,0(R4)                                                         
         BAS   RE,UTPRVDOW                                                      
         LH    R8,=H'-1'                                                        
         BAS   RE,UTADDAY                                                       
         B     TS4                                                              
TS3      LA    R8,2                                                             
         B     TS5                                                              
TSX      CLI   0(R3),C'B'                                                       
         BNE   TSX1                                                             
         XC    DMCB,DMCB                                                        
         MVC   DAT1(3),6(R9)                                                    
         MVC   DAT2(3),2(R9)                                                    
         GOTO1 UTSUB,DMCB,DAT1,DAT2,COUNT                                       
         MVC   1(1,R9),COUNT+3                                                  
TSX1     LA    R6,1(R6)                                                         
         LA    R9,10(R9)                                                        
         BCT   R2,TS1                                                           
*                                                                               
         MVC   CURDATE,SAVDATE                                                  
         XIT1                                                                   
         EJECT                                                                  
***************************************************                             
********* CONSTANTS **************                                              
*                                                                               
MON      EQU   X'01'                                                            
TUE      EQU   X'02'                                                            
WED      EQU   X'03'                                                            
THU      EQU   X'04'                                                            
FRI      EQU   X'05'                                                            
SAT      EQU   X'06'                                                            
SUN      EQU   X'07'                                                            
MAXDAY   EQU   X'08'                                                            
*                                                                               
JAN      EQU   X'01'                                                            
FEB      EQU   X'02'                                                            
MAR      EQU   X'03'                                                            
APR      EQU   X'04'                                                            
MAY      EQU   X'05'                                                            
JUN      EQU   X'06'                                                            
JUL      EQU   X'07'                                                            
AUG      EQU   X'08'                                                            
SEP      EQU   X'09'                                                            
OCT      EQU   X'0A'                                                            
NOV      EQU   X'0B'                                                            
DEC      EQU   X'0C'                                                            
MAXMON   EQU   X'0D'                                                            
*                                                                               
LENJAN   EQU   31                                                               
LENFEB   EQU   28                                                               
LENFEBLP EQU   29                                                               
LENMAR   EQU   31                                                               
LENAPR   EQU   30                                                               
LENMAY   EQU   31                                                               
LENJUN   EQU   30                                                               
LENJUL   EQU   31                                                               
LENAUG   EQU   31                                                               
LENSEP   EQU   30                                                               
LENOCT   EQU   31                                                               
LENNOV   EQU   30                                                               
LENDEC   EQU   31                                                               
*                                                                               
******************* CUMULATIVE DAYS                                             
BEGJAN   EQU   1                                                                
ENDJAN   EQU   LENJAN                                                           
BEGFEB   EQU   LENJAN+1                                                         
ENDFEB   EQU   ENDJAN+LENFEB                                                    
BEGMAR   EQU   ENDFEB+1                                                         
ENDMAR   EQU   ENDFEB+LENMAR                                                    
BEGAPR   EQU   ENDMAR+1                                                         
ENDAPR   EQU   ENDMAR+LENAPR                                                    
BEGMAY   EQU   ENDAPR+1                                                         
ENDMAY   EQU   ENDAPR+LENMAY                                                    
BEGJUN   EQU   ENDMAY+1                                                         
ENDJUN   EQU   ENDMAY+LENJUN                                                    
BEGJUL   EQU   ENDJUN+1                                                         
ENDJUL   EQU   ENDJUN+LENJUL                                                    
BEGAUG   EQU   ENDJUL+1                                                         
ENDAUG   EQU   ENDJUL+LENAUG                                                    
BEGSEP   EQU   ENDAUG+1                                                         
ENDSEP   EQU   ENDAUG+LENSEP                                                    
BEGOCT   EQU   ENDSEP+1                                                         
ENDOCT   EQU   ENDSEP+LENOCT                                                    
BEGNOV   EQU   ENDOCT+1                                                         
ENDNOV   EQU   ENDOCT+LENNOV                                                    
BEGDEC   EQU   ENDNOV+1                                                         
ENDDEC   EQU   ENDNOV+LENDEC                                                    
*                                                                               
         EJECT                                                                  
**************** MONTH TABLE ************************8                          
*                                                                               
MNNTRYLN EQU   4                   LENGTH OF MONTH TABLE ENTRY                  
MNNUM    EQU   0                   OFFSET TO MONTH NUMBER                       
MNLEN    EQU   1                   OFFSET TO LENGTH OF MONTH                    
MNBEGDOW EQU   2                   OFFSET TO MONTH BEGINNING DAY OF WK          
MNENDDOW EQU   3                   OFFSET TO MONTH END DAY OF WEEK              
**************** LOOKUP TABLES *******************8                             
*                                                                               
MINDAT   DC    XL2'0000'           COMPRESSED MINIMUM DATE                      
MAXDAT   DC    XL2'FFFF'           COMPRESSED MAXIMUM DATE                      
*                                                                               
DAYLKUP  DC    C'000MONTUEWEDTHUFRISATSUNMAX'                                   
*                                                                               
LENMONLK EQU   9                   LENGTH OF MONTH LOOKUP ENTRIES               
MONLKUP  DC    C'MINIMUM  '                                                     
         DC    C'JANUARY  '                                                     
         DC    C'FEBRUARY '                                                     
         DC    C'MARCH    '                                                     
         DC    C'APRIL    '                                                     
         DC    C'MAY      '                                                     
         DC    C'JUNE     '                                                     
         DC    C'JULY     '                                                     
         DC    C'AUGUST   '                                                     
         DC    C'SEPTEMBER'                                                     
         DC    C'OCTOBER  '                                                     
         DC    C'NOVEMBER '                                                     
         DC    C'DECEMBER '                                                     
         DC    C'MAXIMUM  '                                                     
*                                                                               
TYPL1    DC    X'00',C'D'                                                       
         DC    X'00',C'W'                                                       
         DC    X'00',C'M'                                                       
         DC    X'00',C'Q'                                                       
         DC    X'00',C'Y'                                                       
*                                                                               
TYPL2    DC    X'00',C'C'                                                       
         DC    X'00',C'B'                                                       
         DC    X'00',C'S'                                                       
         DC    X'01',C'1S'                                                      
         DC    X'01',C'2S'                                                      
         DC    X'01',C'3S'                                                      
         DC    X'01',C'4S'                                                      
*                                                                               
         EJECT                                                                  
***************LTORG *******************                                        
         LTORG                                                                  
*                                                                               
         EJECT                                                                  
**  WORKING STORAGE                                                             
WORKD    DSECT                                                                  
*** COMMON FOR LOW LEVEL DATE UTILITIES                                         
         DS    0F                                                               
RELO     DS    F                                                                
CURDATE  DS    0CL12                                                            
CURYEAR  DS    CL1                                                              
CURMONTH DS    CL1                                                              
CURDAY   DS    CL1                                                              
CURDOW   DS    CL1                                                              
MONADDR  DS    A                                                                
YRADDR   DS    A                                                                
COMDAT   DS    CL2                 COMPRESSED DATE (NOT ALWAYS FILLED)          
*                                                                               
*** SAVE OF COMMON FOR LOW LEVEL DATE UTILITIES                                 
         DS    0F                                                               
SAVDATE  DS    0CL12                                                            
SAVYEAR1 DS    CL1                                                              
SAVMON1  DS    CL1                                                              
SAVDAY   DS    CL1                                                              
SAVDOW   DS    CL1                                                              
SAVMNADR DS    A                                                                
SAVYRADR DS    A                                                                
*                                                                               
SAVCMDAT DS    CL2                                                              
*                                                                               
**** COMMON FOR USER CALLABLE DATE LIST UTILITIES                               
         DS    0F                                                               
STRTDAT  DS    0CL12               START DATE                                   
STRTYR   DS    CL1                                                              
STRTMON  DS    CL1                                                              
STRTDAY  DS    CL1                                                              
STRTDW   DS    CL1                                                              
STRTMADR DS    A                                                                
STRTYADR DS    A                                                                
*                                                                               
         DS    0F                                                               
ENDDAT   DS    CL12                END DATE                                     
MAXNTRYS DS    F                   THE MAXIMUM NUMBER OF LIST ENTRIES           
OUTLIST  DS    A                   ADDRESS OF LIST TO BUILD                     
STRTDOW  DS    CL1                 DOW FOR START OF PERIOD                      
*                                                                               
*  ARGUMENT LIST FOR ROUTINE FILLIST                                            
FIL1HOOK DS    A                   ADDR OF MODULE TO GET STRT OF PERIOD         
FIL2HOOK DS    A                   ADDR OF MODULE TO GET PREV PERIOD            
*                                                                               
DAT1     DS    CL6                                                              
DAT2     DS    CL6                                                              
         DS    0H                                                               
DMCB     DS    CL32                                                             
WORK     DS    CL32                                                             
DUB      DS    D                                                                
YEAR     DS    F                                                                
BASY     DS    F                                                                
YRTBL    DS    CL48                                                             
YRTBLS   DS    CL130                                                            
ERB      DS    CL1                                                              
WYR      DS    CL1                                                              
ALTMON   DS    CL1                                                              
WAM      DS    F                                                                
COUNT    DS    F                                                                
SAVM     DS    6F                                                               
TYPL3    DS    CL12                                                             
TAM      DS    CL10                                                             
TAM1     DS    CL14                                                             
*                                                                               
WORKX    EQU   *                                                                
*                                                                               
         EJECT                                                                  
         DSECT                                                                  
       ++INCLUDE DDDATGEND                                                      
*                                                                               
         EJECT                                                                  
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'186DDDATGEN  05/01/02'                                      
         END                                                                    
