*          DATA SET DDUSSIO    AT LEVEL 004 AS OF 10/08/18                      
*CATALP USSIO                                                                   
         TITLE 'ISSUE I/O TO UNIX SYSTEM SERVICES HFS FILE'                     
*                                                                               
* PID  LVl DATE    COMMENTS                                                     
* ---------------------------------                                             
* GSUM 003 04FEB16 PCM03181 SUPPORT EDIHUB PARM FOR /u/EDIHUB/ PATH             
* YNGX 002 25MAR15 OT82993L DON'T LOAD BPX1OPN/WRT/CLO EVERY CALL               
* YNGX 001 10AUG14 PCA01185 NEW VERSION USED BY SCRIBE                          
*                                                                               
***********************************************************************         
* THIS PROVIDES AN INTERFACE TO ISSUE AN I/O TO USS HFS FILE          *         
*                                                                     *         
* FILEPATH IS BASE ON THE DSPACE IN USE UNLESS OVERRIDE IN P2/B0      *         
* DEFAULT    =   /u/prod/ADV/                                         *         
* DSPACE=T   =   /u/prod/TST/                                         *         
* DSPACE=Q   =   /u/prod/FQA/                                         *         
* DSPACE=C   =   /u/prod/CSC/                                         *         
* P2B0=C'E'  =   /u/EDIHUB/       (regardless of DSPACE)              *         
*                                                                     *         
* THIS PROGRAM SHOULD BE CALLED IN THE FOLLOWING SEQUENCE:            *         
* 1) CREATE/OPEN                                                      *         
* 2) PUT   (CAN BE DONE MULTIPLE TIMES)                               *         
* 3) CLOSE                                                            *         
*                                                                     *         
***********************************************************************         
         EJECT                                                                  
***********************************************************************         
* PARAMETER LISTS                                                     *         
*                                                                     *         
* OPEN: ALLOCATES & CLEARS A BUFFER, BUILDS HEADER IN IT              *         
*                                                                     *         
* NTRY:  P1                A(C'CREATE') OR A(C'OPEN')                 *         
*        P2 B0             C'E' for EDIHUB path                       *         
*        P2 B1-B3          A(USS FILE NAME)                           *         
*        P3                LENGTH OF USS FILE NAME                    *         
*        P4                0                                          *         
*        P5                0                                          *         
*                                                                     *         
* EXIT:  P1      NO CHANGE                                            *         
*        P2      NO CHANGE                                            *         
*        P3      NO CHANGE                                            *         
*        P4 B0   X'80' - ERROR OCCURRED - SEE P5                      *         
*        P5      A(ERROR STRING => XL4(ERROR #),CL24(TEXT))           *         
*                                                                     *         
* PUT :  PUT MESSAGE INTO THE BUFFER                                  *         
*                                                                     *         
* NTRY:  P1                A(C'PUT')                                  *         
*        P2                A(MESSAGE)                                 *         
*        P3                L'MESSAGE                                  *         
*        P4                0                                          *         
*        P5                0                                          *         
*                                                                     *         
* EXIT:  P1      NO CHANGE                                            *         
*        P2      NO CHANGE                                            *         
*        P3      NO CHANGE                                            *         
*        P4 B0   X'80' - ERROR OCCURRED - SEE P6                      *         
*        P5      A(ERROR STRING => XL4(ERROR #),CL24(TEXT))           *         
*                                                                     *         
* CLOSE - CLOSE FILE                                                  *         
*                                                                     *         
* NTRY:  P1                A(C'CLOSE')                                *         
*        P2                0                                          *         
*        P3                0                                          *         
*        P4                0                                          *         
*        P5                0                                          *         
*                                                                     *         
* EXIT:  P1      NO CHANGE                                            *         
*        P2      NO CHANGE                                            *         
*        P3      NO CHANGE                                            *         
*        P4 B0   X'80' - ERROR OCCURRED - SEE P5                      *         
*        P5      A(ERROR STRING => XL4(ERROR #),CL24(TEXT))           *         
*                                                                     *         
* EXTRA INFORMATION IS IN FORM XL4(ERROR NUMBER),CL24(DESCRIPTION)    *         
***********************************************************************         
         PRINT NOGEN                                                            
USSIO    CSECT                                                                  
         NMOD1 WORKL,USSIO,CLEAR=YES                                            
         USING WORKD,RC            WORK AREA FROM D-CHAIN                       
         LAY   R8,COMMON           LOCAL STORAGE AREA                           
         LR    R9,R8                                                            
         AHI   R9,4096                                                          
         USING COMMON,R8,R9                                                     
*                                                                               
         ST    R1,APARMS           SAVE A(PARMS)                                
         MVC   PARMS,0(R1)         SAVE INPUT PARMS                             
         XC    RETCODE,RETCODE                                                  
         XC    RSNCODE,RSNCODE                                                  
*                                                                               
         L     RE,P1               COMMAND                                      
         CLC   =C'CREATE',0(RE)    CREATE A DATA FILE                           
         BE    CREATE                                                           
         CLC   =C'OPEN',0(RE)      OPEN A SFTP FILE                             
         BE    OPEN                                                             
         CLC   =C'PUT',0(RE)       PUT A LINE INTO A FILE                       
         BE    PUT                                                              
         CLC   =C'CLOSE',0(RE)     CLOSE FILE                                   
         BE    CLOSE                                                            
         MVI   ERROR,ERUKCMDQ      UNKNOWN COMMAND                              
         B     EXTERR                                                           
                                                                                
***********************************************************************         
*  ROUTINE TO CREATE A UNIX SYSTEM SERVICES HFS FILE                  *         
* NTRY: P2     = A(USS CONTROL BLOCK)                                 *         
*       P3     = L'USS CONTROL BLOCK                                  *         
***********************************************************************         
CREATE   DS    0H                                                               
         BRAS  RE,SETOPTS          SAVE OPTIONS FROM USS CONTROL BLK            
         BNE   EXTERR                                                           
*                                                                               
         SAM31                                                                  
         XC    S_MODE,S_MODE                                                    
         MVI   S_MODE2,S_IRUSR       USER READ/WRITE, GROUP READ,               
         MVI   S_MODE3,S_IWUSR+S_IRGRP+S_IROTH           OTHER READ             
         XC    O_FLAGS(OPNF#LENGTH),O_FLAGS                                     
         MVI   O_FLAGS4,O_CREAT+O_RDWR  CREATE FOR READ & WRITE                 
         OC    ABPX1OPN,ABPX1OPN     TEST BPX1OPN LOADED                        
         BNZ   CREATE2               YES - OK                                   
         LOAD  EP=BPX1OPN                                                       
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
         ST    R0,ABPX1OPN                                                      
*                                                                               
CREATE2  L     RF,ABPX1OPN                                                      
         CALL  (15),                 OPEN A FILE  BPX1OPN              X        
               (BUFLEN,              INPUT: PATHNAME LENGTH            X        
               PATHNAME,             INPUT: PATHNAME                   X        
               O_FLAGS,              INPUT: ACCESS            BPXYOPNF X        
               S_MODE,               INPUT: MODE    BPXYMODE, BPXYFTYP X        
               RETVAL,               RETURN VALUE:-1 OR FILE DESCRIPTO X        
               RETCODE,              RETURN CODE                       X        
               RSNCODE),             REASON CODE                       X        
               VL,MF=(E,PLIST)                                                  
         ICM   RF,B'1111',RETVAL    TEST RETVAL           BL    PSEUD           
         BL    CRTNG                                                            
         ST    RF,FILEDESC          SAVE FILE DESCRIPTOR                        
         SAM24                                                                  
         B     EXITOK                                                           
                                                                                
***********************************************************************         
*  ROUTINE TO OPEN A FILE ON UNIX SYSTEM SERVICES FOR WRITING         *         
* NTRY: P2     = A(USS CONTROL BLOCK)                                 *         
*       P3     = L'USS CONTROL BLOCK                                  *         
***********************************************************************         
OPEN     DS    0H                                                               
         BRAS  RE,SETOPTS          SAVE OPTIONS FROM USS CONTROL BLK            
         BNE   EXTERR                                                           
*                                                                               
         SAM31                                                                  
         XC    S_MODE,S_MODE                                                    
         MVI   S_MODE2,S_IRUSR           ALL READ AND WRITE ON SFTP             
         MVI   S_MODE3,S_IWUSR+S_IRGRP+S_IWGRP+S_IROTH+S_IWOTH                  
         XC    O_FLAGS(OPNF#LENGTH),O_FLAGS                                     
         MVI   O_FLAGS4,O_RDWR+O_APPEND  OPEN FOR READ & WRITE                  
         OC    ABPX1OPN,ABPX1OPN     TEST BPX1OPN LOADED                        
         BNZ   OPEN2                 YES - OK                                   
         LOAD  EP=BPX1OPN                                                       
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
         ST    R0,ABPX1OPN                                                      
*                                                                               
OPEN2    L     RF,ABPX1OPN                                                      
         CALL  (15),                 OPEN A FILE  BPX1OPN              X        
               (BUFLEN,              INPUT: PATHNAME LENGTH            X        
               PATHNAME,             INPUT: PATHNAME                   X        
               O_FLAGS,              INPUT: ACCESS            BPXYOPNF X        
               S_MODE,               INPUT: MODE    BPXYMODE, BPXYFTYP X        
               RETVAL,               RETURN VALUE:-1 OR FILE DESCRIPTO X        
               RETCODE,              RETURN CODE                       X        
               RSNCODE),             REASON CODE                       X        
               VL,MF=(E,PLIST)                                                  
         ICM   RF,B'1111',RETVAL    TEST RETVAL           BL    PSEUD           
         BL    OPENNG                                                           
         ST    RF,FILEDESC          SAVE FILE DESCRIPTOR                        
         SAM24                                                                  
         B     EXITOK                                                           
                                                                                
***********************************************************************         
*  ROUTINE TO OPEN A UNIX SYSTEM SERVICES HFS FILE                    *         
* NTRY: P2 = A(MESSAGE)                                               *         
*       P3 = L'MESSAGE                                                *         
***********************************************************************         
PUT      DS    0H                                                               
         SAM31                                                                  
         MVHI  ALET,0                Move 0 into ALET (new)                     
         MVC   BUFADDR,P2                                                       
         MVC   BUFLEN,P3                                                        
*                                                                               
         OC    ABPX1WRT,ABPX1WRT     TEST BPX1WRT LOADED                        
         BNZ   PUT2                  YES - OK                                   
         LOAD  EP=BPX1WRT                                                       
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
         ST    R0,ABPX1WRT                                                      
*                                                                               
PUT2     L     RF,ABPX1WRT                                                      
         CALL  (15),                 Write to a file                   x        
               (FILEDESC,            Input: File descriptor            x        
               BUFADDR,              Input: ->Buffer                   x        
               ALET,                 Input: Buffer ALET                x        
               BUFLEN,               Input: Number of bytes to writ    x        
               RETVAL,               Return value: -1 or bytes writ    x        
               RETCODE,              Return code                       x        
               RSNCODE),             Reason code                       x        
               VL,MF=(E,PLIST)                                                  
         ICM   RF,B'1111',RETVAL    TEST RETVAL           BL    PSEUD           
         BL    WRTNG                                                            
         SAM24                                                                  
         B     EXITOK                                                           
                                                                                
***********************************************************************         
*  ROUTINE TO CLOSE A UNIX SYSTEM SERVICES HFS FILE                             
***********************************************************************         
CLOSE    DS    0H                                                               
         SAM31                                                                  
         OC    ABPX1CLO,ABPX1CLO     TEST BPX1CLO LOADED                        
         BNZ   CLOSE2                YES - OK                                   
         LOAD  EP=BPX1CLO                                                       
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
         ST    R0,ABPX1CLO                                                      
*                                                                               
CLOSE2   L     RF,ABPX1CLO                                                      
         CALL  (15),                 Close a file                      x        
               (FILEDESC,            Input: File descriptor            x        
               RETVAL,               Return value: 0 or -1             x        
               RETCODE,              Return code                       x        
               RSNCODE),             Reason code                       x        
               VL,MF=(E,PLIST)                                                  
         ICM   RF,B'1111',RETVAL    TEST RETVAL           BL    PSEUD           
         BL    CLOSENG                                                          
         SAM24                                                                  
         B     EXITOK              AND YOU ARE DONE                             
         EJECT                                                                  
***********************************************************************         
* SETOPTS - SAVE OPTIONS FROM USS CONTROL BLK                         *         
* NTRY: P2     = A(USS FILE NAME)                                     *         
*       P3     = L'USS FILE NAME                                      *         
***********************************************************************         
SETOPTS  NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         L     R1,P3               SAVED LENGHT OF USS FILE NAME                
         CHI   R1,L'USSFNM                                                      
         BNH   *+12                                                             
         MVI   ERROR,ERUFNLQ                                                    
         B     EXITL               LEN OF USS FILE NAME TOO LONG                
         CHI   R1,0                                                             
         BH    *+12                                                             
         MVI   ERROR,ERMUFNQ                                                    
         B     EXITL               MISSING USS FILE NAME                        
         STC   R1,USSFNML                                                       
         BCTR  R1,0                                                             
         XR    RF,RF                                                            
         ICM   RF,7,P2+1                                                        
         BNZ   *+12                                                             
         MVI   ERROR,ERMUFNQ                                                    
         B     EXITL               MISSING USS FILE NAME                        
         EXMVC R1,USSFNM,0(RF)     SAVED FILE NAME                              
*                                                                               
         CLI   USSFNM,C' '         CHECK THE VALUES                             
         BH    *+12                                                             
         MVI   ERROR,ERMUFNQ                                                    
         B     EXITL               MISSING USS FILE NAME                        
*                                                                               
         L     RF,VSSB                                                          
         USING SSBD,RF                                                          
         MVI   PATHNAME,C' '       Clear PATHNAME to spaces                     
         MVC   PATHNAME+1(L'PATHNAME-1),PATHNAME                                
         LHI   RE,L'FILPATHE                                                    
         MVC   PATHNAME(L'FILPATHE),FILPATHE USE EDIHUB                         
         CLI   P2,C'E'             P2B0=C'E'?                                   
         BE    SOPTS08                                                          
         LHI   RE,L'FILPATHL                                                    
         MVC   PATHNAME(L'FILPATHT),FILPATHT USE TST                            
         CLI   SSODSPAC,C'T'       TST?                                         
         BE    SOPTS08                                                          
         MVC   PATHNAME(L'FILPATHQ),FILPATHQ USE FQA                            
         CLI   SSODSPAC,C'Q'       FQA?                                         
         BE    SOPTS08                                                          
         MVC   PATHNAME(L'FILPATHC),FILPATHC USE CSC                            
         CLI   SSODSPAC,C'C'       CSC?                                         
         BE    SOPTS08                                                          
         MVC   PATHNAME(L'FILPATHL),FILPATHL USE LIVE                           
         DROP  RF                                                               
*                                                                               
SOPTS08  LLC   R1,USSFNML          R1=LENGTH OF USER PART OF FILENAME           
         BCTR  R1,0                -1                                           
         LA    RF,PATHNAME(RE)     RE=LENGTH OF FIRST PART                      
         MVC   0(0,RF),USSFNM      RF=WHERE TO APPEND USER PART                 
         EX    R1,*-6                                                           
         LA    R1,1(RE,R1)                                                      
         ST    R1,BUFLEN           SAVE LENGTH OF FULL PATH NAME                
         B     EXITOK                                                           
                                                                                
***********************************************************************         
* RETERR - RETURN ERROR INFORMATION IN USER PARAMETER LIST            *         
* NTRY: ERROR   = ERROR CODE                                          *         
*                                                                     *         
* EXIT: P4 B0   = X'80' - SIGNIFIES ERROR OCCURRED                    *         
*       P5      = A(EXTRA ERROR INFORMATION)                          *         
*                                                                     *         
* EXTRA INFORMATION IS IN FORM XL4(ERROR NUMBER),CL24(DESCRIPTION)    *         
***********************************************************************         
RETERR   NTR1  BASE=*,LABEL=*                                                   
         L     R2,APARMS                                                        
         MVI   12(R2),X'80'        SET X'80' BIT IN RETURN P4                   
*                                                                               
         LAY   RF,ERRTAB                                                        
         ST    RF,16(R2)           DEFAULT ERROR RETURN IN P5                   
*                                                                               
         LAY   RF,ERRTAB                                                        
         XR    R0,R0                                                            
         IC    R0,ERROR            R0 = ERROR CODE                              
*                                                                               
RERR02   CLI   0(RF),X'FF'         END OF TABLE?                                
         BE    EXITOK              YES - NOT FOUND                              
         CLM   R0,15,0(RF)         MATCH ON ERROR CODE?                         
         BE    *+12                YES                                          
         AHI   RF,28               BUMP TO NEXT TABLE ENTRY                     
         B     RERR02                                                           
*                                                                               
         ST    RF,16(R2)           PUT A(ERROR STRING) IN P5                    
         B     EXITOK                                                           
         EJECT                                                                  
CRTNG    DS    0H                                                               
         ICM   R7,B'1111',RETCODE    RETURN CODE                                
         ICM   R8,B'1111',RSNCODE    REASON CODE                                
         ABEND 1,DUMP                                                           
OPENNG   DS    0H                                                               
         ICM   R7,B'1111',RETCODE    RETURN CODE                                
         ICM   R8,B'1111',RSNCODE    REASON CODE                                
         ABEND 2,DUMP                                                           
WRTNG    DS    0H                                                               
         ICM   R7,B'1111',RETCODE    RETURN CODE                                
         ICM   R8,B'1111',RSNCODE    REASON CODE                                
         ABEND 3,DUMP                                                           
CLOSENG  DS    0H                                                               
         ICM   R7,B'1111',RETCODE    RETURN CODE                                
         ICM   R8,B'1111',RSNCODE    REASON CODE                                
         ABEND 4,DUMP                                                           
         DROP  RB                                                               
                                                                                
***********************************************************************         
* COMMON STORAGE                                                      *         
***********************************************************************         
COMMON   DS    0D                                                               
         DC    CL8'*COMMON*'                                                    
*                                                                               
EXTERR   BRAS  RE,RETERR           SET ERROR PARMS                              
         J     EXIT                                                             
*                                                                               
EXITOK   CR    RB,RB               RETURN CC EQUAL                              
         J     EXIT                                                             
*                                                                               
EXITL    CLI   *,255               RETURN CC NOT EQUAL                          
         J     EXIT                                                             
*                                                                               
EXIT     XIT1  ,                                                                
                                                                                
***********************************************************************         
* LITERALS AND CONSTANTS                                              *         
***********************************************************************         
         DS    0D                                                               
         DC    C'**RTC***'                                                      
ERRRTC   DC    X'00000000'                                                      
         DC    C'**RSN***'                                                      
ERRRSN   DC    X'00000000'                                                      
*                                                                               
FILPATHL DC    C'/u/prod/ADV/'                                                  
FILPATHT DC    C'/u/prod/TST/'                                                  
FILPATHQ DC    C'/u/prod/FQA/'                                                  
FILPATHC DC    C'/u/prod/CSC/'                                                  
FILPATHE DC    C'/u/EDIHUB/'                                                    
*                                                                               
VSSB     DC    V(SSB)                                                           
ABPX1OPN DC    A(0)                                                             
ABPX1WRT DC    A(0)                                                             
ABPX1CLO DC    A(0)                                                             
*                                                                               
USSFNML  DC    X'00'                                                            
USSFNM   DC    CL140' '                                                         
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
* COMMAND EQUATES                                                               
*PX1OPN  EQU   156                                                              
*PX1RED  EQU   176                                                              
*PX1CLO  EQU   72                                                               
*PX1WRT  EQU   220                                                              
*                                                                               
*ERROR TABLE (ERROR #1-3)                                                       
ERUKCMDQ EQU   001                 UNKNOWN COMMAND                              
ERUFNLQ  EQU   002                 USS CONTROL BLOCK LENGTH TOO LONG            
ERMUFNQ  EQU   003                 MISSING USS FILE NAME                        
*                                                                               
ERRTAB   DC    A(ERUKCMDQ),CL24'UNKNOWN COMMAND'                                
         DC    A(ERUFNLQ),CL24'USS FILE NAME TOO LONG'                          
         DC    A(ERMUFNQ),CL24'MISSING USS FILE NAME'                           
         DC    X'FF'                                                            
                                                                                
         EJECT                                                                  
*********************************************************************           
* EQUATES AND DSECTS                                                *           
*********************************************************************           
SSBD     DSECT                                                                  
       ++INCLUDE FASSBOFF                                                       
*                                                                               
WORKD    DSECT                                                                  
APARMS   DS    A                   A(INPUT PARMS)                               
*                                                                               
PARMS    DS    0XL24               COPY OF INPUT PARMS                          
P1       DS    F                                                                
P2       DS    F                                                                
P3       DS    F                                                                
P4       DS    F                                                                
P5       DS    F                                                                
P6       DS    F                                                                
*                                                                               
PLIST    DS    13A              CALL PARMLIST WORK AREA                         
ALET     DS    F                                                                
RETCODE  DS    F                RETURN CODE (ERRNO)                             
RSNCODE  DS    F                REASON CODE                                     
RETVAL   DS    F                RETURN VALUE (0, -1 OR OTHER)                   
*                                                                               
BUFLEN   DS    F                                                                
BUFADDR  DS    A                                                                
PATHNAME DS    CL255                                                            
*                                                                               
ERROR    DS    X                ERROR #                                         
*                                                                               
FILEDESC DS    F                FILE DESCRIPTOR                                 
* FLAGS                                                                         
         BPXYOPNF DSECT=NO                                                      
         BPXYMODE DSECT=NO                                                      
WORKL    EQU   *-WORKD                                                          
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'004DDUSSIO   10/08/18'                                      
         END                                                                    
