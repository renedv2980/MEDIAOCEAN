*          DATA SET DECNVRDTPT AT LEVEL 019 AS OF 06/11/07                      
*CATALP DERDTPT                                                                 
         TITLE 'LOOK-UP TPT FILE FOR ADI/TSA PUT DATA'                          
DERDTPT  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,**RDTPT                                              
         USING WORKD,RC            RC=A(W/S)                                    
         L     RA,0(R1)                                                         
         USING DEMCOND,RA          RA=A(DEMCON GLOBAL W/S)                      
         L     R7,ASREC                                                         
         USING INTERD,R7           R7=A(SORT RECORD)                            
         LA    R6,MYDBLOCK                                                      
         USING DBLOCKD,R6          R6=A(DBLOCK)                                 
*                                                                               
         CLI   4(R1),C'S'          SEE IF SETVAL CALL                           
         BE    SETVAL               YES IT IS                                   
         SPACE 1                                                                
         CLI   FTSWITCH,0          TEST IF FIRST TIME                           
         BNE   RD8                                                              
         MVI   FTSWITCH,1                                                       
         XC    LASTMRKT,LASTMRKT   CLEAR LAST MARKET                            
         XC    DBLOCK,DBLOCK                                                    
         MVC   DBFILE,=C'TP '                                                   
         MVC   DBSELSRC,OUTSRC                                                  
         MVC   DBSELMED,MEDIA                                                   
         MVC   DBSELAGY,=C'11'                                                  
         MVC   DBSELBK,INTBOOK                                                  
         MVC   DBBTYPE,INTBTYP                                                  
         CLI   BOOKTYPE,0                                                       
         BE    *+10                                                             
         MVC   DBBTYPE,BOOKTYPE                                                 
         XC    WORK,WORK                                                        
         MVC   WORK(3),=C'PAV'                                                  
         MVC   WORK+7(2),EFFS                                                   
         L     RF,ACOMFACS         GET A(DISPLACEMENT TABLES)                   
         ST    RF,DBCOMFCS                                                      
         L     RF,CDEMEL-COMFACSD(RF)                                           
         GOTO1 (RF),MYDMCB,(C'D',WORK),DBLOCKD                                  
         CLI   DBERROR,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   AIDISP,12(R1)                                                    
         MVC   AODISP,16(R1)                                                    
*                                  BUILD A LIST OF DEMOS TO LOOK-UP             
         L     R3,AODISP                                                        
         USING DSPHDRD,R3                                                       
         SR    R4,R4                                                            
         ICM   R4,3,DSPLDE                                                      
         ICM   R5,7,DSPAET                                                      
         LA    R5,0(R3,R5)                                                      
         BCTR  R5,0                                                             
         LA    R3,DSPHDRLN(R3)                                                  
         USING DSPDTAD,R3                                                       
         ST    R3,AODISPS          SAVE A(FIRST DEMO IN LIST)                   
         LA    RE,DEMOLIST                                                      
         LA    RF,DEMODISP                                                      
*                                                                               
RD2      CLI   DSPMOD,C'M'         ARB FIELDS                                   
         BE    RD4                                                              
         CLI   DSPMOD,C'Q'                                                      
         BE    RD4                                                              
         CLI   DSPMOD,C'U'                                                      
         BE    RD4                                                              
         CLI   DSPMOD,C'L'         NSI FIELDS                                   
         BE    RD4                                                              
         CLI   DSPMOD,C'N'                                                      
         BE    RD4                                                              
         CLI   DSPMOD,C'W'                                                      
         BE    RD4                                                              
         B     RD6                                                              
*                                                                               
RD4      MVC   0(1,RE),DSPPREC                                                  
         MVC   1(2,RE),DSPMOD                                                   
         CLI   1(RE),C'M'          CONVERT M TO R                               
         BNE   *+8                                                              
         MVI   1(RE),C'R'                                                       
         CLI   1(RE),C'Q'          CONVERT Q TO T                               
         BNE   *+8                                                              
         MVI   1(RE),C'T'                                                       
         CLI   1(RE),C'N'          CONVERT N TO F                               
         BNE   *+8                                                              
         MVI   1(RE),C'F'                                                       
         CLI   1(RE),C'W'          CONVERT W TO J                               
         BNE   *+8                                                              
         MVI   1(RE),C'J'                                                       
         LR    R1,R3                                                            
         S     R1,AODISPS                                                       
         SR    R0,R0                                                            
         DR    R0,R4                                                            
         SLL   R1,2                                                             
         STCM  R1,3,0(RF)                                                       
         LA    RE,3(RE)                                                         
         LA    RF,2(RF)                                                         
*                                                                               
RD6      BXLE  R3,R4,RD2                                                        
         MVI   0(RE),X'FF'                                                      
         MVI   0(RF),X'FF'                                                      
*                                  TEST FOR CHANGE OF MARKET                    
RD8      CLC   INTMRKT,LASTMRKT                                                 
         BE    RD18                                                             
         L     RE,VPUTBUFF         CLEAR PUT BUFFER ON MARKET CHANGE            
         L     RF,PUTBUFFL                                                      
         XCEF                                                                   
         B     RD10                                                             
*                                                                               
EXIT     XMOD1 1                                                                
         EJECT                                                                  
* BUILD A BUFFER CONTAINING ADI/TSA PUT DATA BY DAY & QTR HOUR.                 
*                                                                               
RD10     MVC   LASTMRKT,INTMRKT    BUILD DBLOCK                                 
         MVC   DBSELRMK,INTMRKT                                                 
         MVC   DBBTYPE,INTBTYP                                                  
         CLI   BOOKTYPE,0                                                       
         BE    *+10                                                             
         MVC   DBBTYPE,BOOKTYPE                                                 
         MVI   DBFUNCT,DBGETTOT                                                 
         MVI   DBSELDAY,X'7F'                                                   
         MVC   DBSELTIM,=AL2(0600,2858) WAS 2850 WHICH DROPPED LAST QHR         
         LA    R1,IOAREA                                                        
         ST    R1,DBAREC                                                        
         L     RF,ACOMFACS                                                      
         ST    RF,DBCOMFCS                                                      
         L     RF,CDEMAND-COMFACSD(RF)                                          
         GOTO1 (RF),MYDMCB,DBLOCK,RD12                                          
         CLI   DBERROR,X'80'                                                    
         BE    *+6                                                              
         DC    H'0'                                                             
         MVI   DBFUNCT,DBGETTOT                                                 
         MVI   DBSELDAY,X'7F'                                                   
         MVC   DBSELTIM,=AL2(0500,0600)                                         
         LA    R1,IOAREA                                                        
         ST    R1,DBAREC                                                        
         L     RF,ACOMFACS                                                      
         ST    RF,DBCOMFCS                                                      
         L     RF,CDEMAND-COMFACSD(RF)                                          
         GOTO1 (RF),MYDMCB,DBLOCK,RD12                                          
         CLI   DBERROR,X'80'                                                    
         BE    RD18                                                             
         DC    H'0'                                                             
*                                  GET DEMO VALUES & POST TO BUFFER             
RD12     NTR1                                                                   
         L     R5,DBAQUART                                                      
         USING QHELEM,R5                                                        
         TM    QHWKS,X'10'                                                      
         BNZ   RD16                                                             
         XC    POSTLINE(200),POSTLINE                                           
         XC    POSTLINE+200(200),POSTLINE+200                                   
         XC    POSTLINE+400(200),POSTLINE+400                                   
         L     RF,ACOMFACS                                                      
         L     RF,CDEMOUT-COMFACSD(RF)                                          
         GOTO1 (RF),MYDMCB,(C'P',DEMOLIST),DBLOCK,POSTLINE                      
         CLI   DBERROR,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
*                                  CALULATE DAYS/QTR HOURS TO POST TO           
         ZIC   R0,QHEQH                                                         
         ZIC   R1,QHSQH                                                         
         AH    R0,=H'1'            R1=START QTR HOUR (BASE ZERO)                
         SR    R0,R1               R0=NUMBER OF QTR HOURS                       
         MVC   MYDUB(1),QHDAY                                                   
         NI    MYDUB,X'FF'-X'80'                                                
         ZIC   R8,MYDUB                                                         
         SRDL  R8,4                                                             
         SRL   R9,28                                                            
         LTR   R9,R9               TEST IF END DAY GIVEN                        
         BNZ   *+6                                                              
         LR    R9,R8               NO - EQUATE TO START DAY                     
         BCTR  R8,0                R8=START DAY (BASE ZERO)                     
         SR    R9,R8               R9=NUMBER OF DAYS                            
         STM   R8,R9,POSTDAYS                                                   
*                                                                               
RD14     BAS   RE,PUT              POST TO DAY WITHIN QTR HOUR                  
         LA    R8,1(R8)            BUMP DAY NUMBER                              
         BCT   R9,RD14             DO FOR NUMBER OF DAYS                        
         LM    R8,R9,POSTDAYS      RESET START DAY/NUMBER OF DAYS               
         LA    R1,1(R1)            BUMP QTR HOUR NUMBER                         
         BCT   R0,RD14             DO FOR NUMBER OF QTR HOURS                   
*                                                                               
RD16     B     EXIT                                                             
         EJECT                                                                  
* LOOK-UP ADI/TSA VALUES IN PUT BUFFER AND MOVE TO PAV WORK RECORD.             
*                                                                               
RD18     MVC   MYDUB(1),INTDAYWK   LOOK-UP DAY IN TABLE                         
         NI    MYDUB,X'F0'         TURN OFF BOTTOM NIBBLE                       
         LA    RE,PWDWTAB                                                       
*                                                                               
RD20     CLI   0(RE),X'FF'                                                      
         BE    RD22                                                             
         CLC   0(1,RE),MYDUB                                                    
         BE    RD22                                                             
         LA    RE,L'PWDWTAB(RE)                                                 
         B     RD20                                                             
*                                                                               
RD22     ZIC   R8,1(RE)                                                         
         BCTR  R8,0                R8=START DAY (BASE ZERO)                     
         ZIC   R9,2(RE)            R9=NUMBER OF DAYS                            
         ZIC   R1,INTSQH                                                        
         CH    R1,=H'97'           TEST FOR START OF 6AM OR LATER               
         BL    *+8                                                              
         LA    R1,96                                                            
         ZIC   R0,INTEQH                                                        
*                                                                               
         SR    R0,R1                                                            
         BP    *+8                                                              
         LA    R0,1                                                             
         XC    POSTLINE(200),POSTLINE                                           
         XC    POSTLINE+200(200),POSTLINE+200                                   
         XC    POSTLINE+400(200),POSTLINE+400                                   
         STM   R8,R9,POSTDAYS                                                   
*                                                                               
RD24     DS    0C                                                               
*                                                                               
* CODE TO ALLOW CROSSING OVER 6AM                                               
         CHI   R1,96               6A STARTS AT 0 QHR                           
         BL    *+8                                                              
         SHI   R1,96                                                            
*                                                                               
         BAS   RE,GET              ADD BUFFER VALUES TO POST LINE               
         LA    R8,1(R8)            BUMP DAY NUMBER                              
         BCT   R9,RD24             DO FOR NUMBER OF DAYS                        
*                                                                               
         LM    R8,R9,POSTDAYS      RESET START DAY/NUMBER OF DAYS               
         LA    R1,1(R1)            BUMP QTR HOUR NUMBER                         
         BCT   R0,RD24             DO FOR NUMBER OF QTR HOURS                   
*                                                                               
         ZIC   R9,INTEQH                                                        
****     CH    R9,=H'97'           TEST FOR END OF 2AM OR LATER                 
****     BL    *+8                                                              
****     LA    R9,96                                                            
*                                                                               
         ZIC   R1,INTSQH                                                        
         CH    R1,=H'97'           TEST FOR START OF 2AM OR LATER               
         BL    *+8                                                              
         LA    R1,96                                                            
         SR    R9,R1                                                            
         BP    *+8                                                              
         LA    R9,1                                                             
         M     R8,POSTDAYS+4       R9=AVERAGING FACTOR (DAYS*QTR HOURS)         
         LA    RE,POSTLINE                                                      
         LA    RF,POSTLEN                                                       
*                                  AVERAGE DATA IN POSTLINE                     
RD26     L     R0,0(RE)                                                         
         SR    R1,R1                                                            
         SRDA  R0,31                                                            
         DR    R0,R9                                                            
         LTR   R1,R1                                                            
         BM    *+8                                                              
         AH    R1,=H'1'                                                         
         SRA   R1,1                                                             
         ST    R1,0(RE)                                                         
         LA    RE,4(RE)                                                         
         BCT   RF,RD26                                                          
*                                  MOVE VALUES TO WORK RECORD                   
         LA    RE,DEMODISP         RE=A(FIELD DISP LIST)                        
         LA    R1,POSTLINE         R1=A(OUTPUT VALUES)                          
*                                                                               
RD28     SR    RF,RF                                                            
         ICM   RF,3,0(RE)                                                       
         BM    RD30                                                             
         LA    RF,INTERD(RF)       RF=A(OUTPUT WORK AREA VALUE)                 
         A     RF,ACCDISP                                                       
         MVC   0(4,RF),0(R1)       MOVE VALUE TO WORK RECORD                    
         LA    RE,2(RE)            BUMP TO NEXT FIELD                           
         LA    R1,4(R1)                                                         
         B     RD28                                                             
*                                                                               
RD30     B     EXIT                                                             
         EJECT                                                                  
* POST A LINE OF VALUES TO ADI/TSA PUT BUFFER.                                  
*                                                                               
PUT      MVI   GETPUTSW,C'P'                                                    
         B     GETPUT                                                           
         SPACE 1                                                                
* ADD A LINE OF VALUES FROM ADI/TSA PUT BUFFER INTO POSTLINE.                   
*                                                                               
GET      MVI   GETPUTSW,C'G'                                                    
         B     GETPUT                                                           
         SPACE 1                                                                
* ADD A LINE OF VALUES FROM POSTLINE INTO BUFFER OR VICE-VERSA.                 
*                                                                               
* ON ENTRY R1=QTR HOUR NUMBER (0-95)                                            
*          R8=DAY NUMBER (0-6)                                                  
*          GETPUTSW = C'P' TO ADD FROM LINE INTO BUFFER                         
*                   = C'G' TO ADD FROM BUFFER INTO LINE                         
*          POSTLINE CONTAINS INPUT/OUTPUT VALUES                                
*                                                                               
GETPUT   NTR1                                                                   
         L     R0,QHRBUFFL                                                      
         MR    R0,R0               R1=DISP TO QTR HOUR BUFFER                   
         L     R9,DAYBUFFL                                                      
         MR    R8,R8               R9=DISP TO DAY                               
         AR    R1,R9                                                            
         A     R1,VPUTBUFF         R1=A(DAY/QTR HOUR LINE)                      
*        CLI   GETPUTSW,C'G'                                                    
*        BNE   *+10                                                             
*        CLC   INTPNAME(7),=C'EVERYWO'                                          
*        BNE   *+6                                                              
*        DC    H'0'                                                             
         LA    RE,POSTLINE         RE=A(INPUT/OUTPUT VALUES)                    
         LA    RF,POSTLEN          RF=NUMBER OF VALUES IN LINE                  
*                                                                               
GETPUT2  L     R0,0(R1)                                                         
         A     R0,0(RE)                                                         
         CLI   GETPUTSW,C'P'                                                    
         BNE   *+12                                                             
         ST    R0,0(R1)            PUT VALUE TO BUFFER                          
         B     *+8                                                              
         ST    R0,0(RE)            GET VALUE INTO LINE                          
         LA    RE,4(RE)                                                         
         LA    R1,4(R1)                                                         
         BCT   RF,GETPUT2                                                       
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
*======================== SET CONTROL VALUES =========================*         
SETVAL   L     RF,4(R1)                                                         
         CLC   0(4,RF),=C'DISP'                                                 
         BE    *+6                 ONLY VALID VALUE SO FAR                      
         DC    H'0'                                                             
         MVC   ACCDISP,8(R1)                                                    
         B     EXIT                                                             
***********************************************************************         
         EJECT                                                                  
* LITERALS ETC.                                                                 
*                                                                               
         LTORG                                                                  
         SPACE 1                                                                
FTSWITCH DC    X'00'               0=FIRST TIME                                 
LASTMRKT DS    XL2                 LAST MARKET NUMBER                           
ACCDISP  DS    F                   DISPLACEMENT TO INTACCS                      
AIDISP   DS    A                                                                
AODISP   DS    A                                                                
AODISPS  DS    A                                                                
DEMOLIST DS    150XL3,X                                                         
         DS    250C    JUST IN CASE SOMEONE BUILDS A LONGER LIST                
         DS    0H                                                               
DEMODISP DS    150XL2                                                           
         DS    250C                SEE ABOVE                                    
         DS    0F                                                               
MYDBLOCK DS    CL256                                                            
         DS    CL4                 DBLOCK IS REALLY 260 BYTES                   
         DS    200C                SPARE                                        
POSTLEN  EQU   150                                                              
NUMDAYS  EQU   7                                                                
NUMQTHR  EQU   96                                                               
DAYBUFFL DC    A(POSTLEN*4)                                                     
QHRBUFFL DC    A(POSTLEN*NUMDAYS*4)                                             
PUTBUFFL DC    A(POSTLEN*NUMDAYS*NUMQTHR*4)                                     
EFFS     DC    X'FFFF'                                                          
         SPACE 1                                                                
* TABLE OF DAYS FOR ADI/TSA PUT DATA LOOK-UP.                                   
*                                                                               
* BYTE 0 = PWDW VALUE (TOP NIBBLE ONLY)                                         
*      1 = START DAY FOR LOOK-UP (1=MON, 7=SUN)                                 
*      2 = NUMBER OF DAYS TO LOOK-UP & AVERAGE                                  
*                                                                               
PWDWTAB  DS    0CL3                                                             
         DC    X'00',AL1(1,5)      M-F                                          
         DC    X'10',AL1(1,1)      MON                                          
         DC    X'20',AL1(2,1)      TUE                                          
         DC    X'30',AL1(3,1)      WED                                          
         DC    X'40',AL1(4,1)      THU                                          
         DC    X'50',AL1(5,1)      FRI                                          
         DC    X'60',AL1(6,1)      SAT                                          
         DC    X'70',AL1(7,1)      SUN                                          
         DC    X'80',AL1(1,7)      M-S                                          
         DC    X'D0',AL1(1,7)      TYPICAL (USE M-SU)                           
         DC    X'E0',AL1(6,2)      S-S                                          
         DC    X'FF',AL1(1,5)      VARIOUS (USE M-F)                            
         EJECT                                                                  
* DSECT TO COVER TEMPORARY WORKING STORAGE                                      
*                                                                               
WORKD    DSECT                                                                  
MYDUB    DS    D                                                                
MYDMCB   DS    6F                                                               
POSTDAYS DS    2F                                                               
POSTLINE DS    150F                                                             
GETPUTSW DS    C                   G=GET, P=PUT                                 
IOAREA   DS    1500C                                                            
WORKX    EQU   *                                                                
         SPACE 1                                                                
* DEDEMFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE DEDEMFILE                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* DEDEMCNVD                                                                     
         PRINT OFF                                                              
       ++INCLUDE DEDEMCNVD                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* DEDBLOCK                                                                      
         PRINT OFF                                                              
DBLOCKD  DSECT                                                                  
       ++INCLUDE DEDBLOCK                                                       
         PRINT ON                                                               
         SPACE 1                                                                
* DEDEMTABD                                                                     
         PRINT OFF                                                              
       ++INCLUDE DEDEMTABD                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* DEINTD                                                                        
*        PRINT OFF                                                              
       ++INCLUDE DEINTD                                                         
       ++INCLUDE DEINTPAVD                                                      
*        PRINT ON                                                               
         SPACE 1                                                                
* DDCOMFACS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'019DECNVRDTPT06/11/07'                                      
         END                                                                    
