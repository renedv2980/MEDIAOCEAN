*          DATA SET DDWRKIO    AT LEVEL 024 AS OF 06/17/14                      
*CATALP WRKIO                                                                   
WRKIO    TITLE '- INTERFACE TO DMWRKF/DMWRKZ'                                   
WRKIO    CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKL,**WKIO**,RA,CLEAR=YES                                      
                                                                                
         USING WORKD,RC                                                         
         LR    R2,R1                                                            
         USING WRKIOD,R2           R2=A(CONTROL BLOCK)                          
         MVI   WRKIERRS,0          SET NO ERROR                                 
         L     R9,WRKIACOM                                                      
         USING COMFACSD,R9         R9=A(COMFACS)                                
                                                                                
         MVC   SAVEAREC,WRKIAREC   SAVE WRKIAREC VALUE                          
         GOTOR ,WRKIPARM,,WRKIFILE,WRKINDX,WRKIAREC,WRKIABUF                    
                                                                                
         SR    RF,RF                                                            
         ICM   RF,1,WRKIACTN                                                    
         BNZ   *+6                                                              
         DC    H'0'                INVALID ACTION CODE                          
         CHI   RF,MAXACTS                                                       
         BNH   *+6                                                              
         DC    H'0'                INVALID ACTION CODE                          
         BCTR  RF,0                                                             
         SLL   RF,2                                                             
                                                                                
         LA    RE,WKATAB           POINT TO WRKF BRANCH TABLE                   
         CLI   WRKIFTYP,WRKIFTWF                                                
         BE    WRKIO02                                                          
         LA    RE,WKFTAB           POINT TO EASI BRANCH TABLE (16 BIT)          
         CLI   WRKIFTYP,WRKIFTEZ                                                
         BE    WRKIO02                                                          
**AW     LA    RE,WKZTAB           POINT TO EASI BRANCH TABLE (20 BIT)          
*        CLI   WRKIFTYP,WRKIFTEZ   (WILL BE USED WHEN ALL EASI WORKER           
*        BE    WRKIO02              FILES ARE ON WRKZ)                          
         MVI   WRKIERRS,WRKIEIFT   SET INVALID FILE TYPE & EXIT                 
         B     EXIT                                                             
                                                                                
WRKIO02  AR    RE,RF               ADD INDEX TO BRANCH TABLE ADDRESS            
         BR    RE                  GO TO ACTION BRANCH                          
         EJECT                                                                  
***********************************************************************         
* BRANCH INDEX 16 BIT DISK ADDRESS WORKER FILES (WRKFXX)              *         
***********************************************************************         
                                                                                
WKATAB   DS    0XL4                ** WRKF BRANCH TABLE **                      
         B     WKNEW               OPEN A NEW FILE                              
         B     WKOPN               OPEN EXISTING FILE                           
         B     WKORA               OPEN EXISTING FILE FOR REPLACE               
         B     WKORA               OPEN EXISTING REPORT FOR APPEND              
         B     WKADD               ADD RECORD TO FILE                           
         B     WKPUT               PUT RECORD TO FILE                           
         B     WKGET               GET FIRST/NEXT RECORD FOR FILE               
         B     WKCLO               CLOSE THE FILE                               
         B     WKCLP               CLOSE AND PURGE THE FILE                     
         B     WKCLE               CLOSE AND SET 'IN ERROR' ATTRIBUTE           
         B     WKNDX               GET FIRST/NEXT INDEX RECORD                  
         DC    (L'WKATAB)X'00'     N/D                                          
         DC    (L'WKATAB)X'00'     N/D                                          
         DC    (L'WKATAB)X'00'     N/D                                          
         DC    (L'WKATAB)X'00'     N/D                                          
         DC    (L'WKATAB)X'00'     N/D                                          
         DC    (L'WKATAB)X'00'     N/D                                          
         DC    (L'WKATAB)X'00'     N/D                                          
         DC    (L'WKATAB)X'00'     N/D                                          
         EJECT                                                                  
***********************************************************************         
* ADD A NEW FILE                                                      *         
***********************************************************************         
                                                                                
         USING WKUKRECD,WRKINDX                                                 
         USING WKWLHDRD,HEADER                                                  
WKNEW    GOTOR CLRBUF              CLEAR THE BUFFER                             
         GOTOR GETFIL              ESTABLISH THE FILE                           
         XC    WKUKFILENO(WKUKLAST-WKUKFILENO),WKUKFILENO                       
         XC    WKWLHEAD,WKWLHEAD   CLEAR TO ZEROS                               
         XC    WKWLINDEX,WKWLINDEX                                              
         XC    WKWLBATTR,WKWLBATTR                                              
         XC    WKWLDATA,WKWLDATA                                                
         MVC   WKWLSOFLAB(L'SOFLAB),SOFLAB                                      
         MVC   WKWLINDEX(L'WKUKKEY),WRKWKEY                                     
         MVC   WKWLRETNL,WRKLRETH                                               
         GOTOR CDATAMGR,WRKIPARM,DMPRINT,,,WKWLHDRD                             
         BE    *+12                                                             
         MVI   WRKIERRS,WRKIEFOE   SET FILE OPEN ERROR                          
         B     EXIT                                                             
         MVC   WKUKFILENO,WKWLREPRNO                                            
         MVI   WRKISTAT,WRKISOPN+WRKISUPD                                       
         MVI   WRKIACTN,WRKIAADD   NEXT ACTION IS ADD                           
         B     EXIT                                                             
                                                                                
***********************************************************************         
* OPEN AN EXISTING FILE                                               *         
***********************************************************************         
                                                                                
WKOPN    GOTOR CLRBUF              CLEAR THE BUFFER                             
         GOTOR GETFIL              ESTABLISH THE FILE                           
         OC    WRKWSEQN,WRKWSEQN                                                
         BZ    *+8                                                              
         OI    WKUKFLAG,WKUKFLDAT                                               
         XC    WKWLHEAD,WKWLHEAD   CLEAR TO ZEROS                               
         XC    WKWLINDEX,WKWLINDEX                                              
         XC    WKWLBATTR,WKWLBATTR                                              
         XC    WKWLDATA,WKWLDATA                                                
         MVC   WKWLSOFLAB(L'SOFLAB),SOFLAB                                      
         MVC   WKWLINDEX(L'WRKWKEY),WKUKINDEX                                   
         OI    WKWLFLAG,WKWLFLREFN                                              
         GOTOR CDATAMGR,WRKIPARM,DMINDEX,,,WKWLHDRD                             
         BE    *+12                                                             
         MVI   WRKIERRS,WRKIEFNF                                                
         B     EXIT                                                             
         MVI   WRKISTAT,WRKISOPN                                                
         MVI   WRKIACTN,WRKIAGET   NEXT ACTION IS GET                           
         B     EXIT                                                             
                                                                                
***********************************************************************         
* RE-OPEN A FILE FOR REPLACE OR APPEND                                *         
***********************************************************************         
                                                                                
WKORA    OC    WRKWSEQN,WRKWSEQN   TEST FILE NUMBER SET                         
         BNZ   *+12                                                             
         MVI   WRKIERRS,WRKIEKEY                                                
         B     EXIT                                                             
         GOTOR CLRBUF              CLEAR THE BUFFER                             
         GOTOR GETFIL              ESTABLISH THE FILE                           
         XC    WKWLHEAD,WKWLHEAD   CLEAR TO ZEROS                               
         XC    WKWLINDEX,WKWLINDEX                                              
         XC    WKWLBATTR,WKWLBATTR                                              
         XC    WKWLDATA,WKWLDATA                                                
         MVC   WKWLSOFLAB(L'SOFLAB),SOFLAB                                      
         MVC   WKWLINDEX(L'WRKWKEY),WKUKINDEX                                   
         OI    WKWLFLAG,WKWLFLREFN+WKWLFLRCOP                                   
         CLI   WRKIACTN,WRKIAREP                                                
         BE    *+8                                                              
         OI    WKWLFLAG,WKWLFLMOD  OPEN FOR APPEND                              
         MVC   WKWLRETNL,WRKLRETH  SET LIVE RETENTION HOURS                     
         GOTOR CDATAMGR,WRKIPARM,DMPRINT,,,WKWLHDRD                             
         BE    *+12                                                             
         MVI   WRKIERRS,WRKIEFNF                                                
         B     EXIT                                                             
         MVC   WKUKINDEX,WKWLINDEX                                              
         MVI   WRKISTAT,WRKISOPN+WRKISUPD                                       
         B     EXIT                                                             
                                                                                
***********************************************************************         
* ADD RECORD TO WORKER FILE                                           *         
***********************************************************************         
                                                                                
WKADD    TM    WRKISTAT,WRKISOPN+WRKISUPD                                       
         BO    *+12                                                             
         MVI   WRKIERRS,WRKIEIAS                                                
         B     EXIT                                                             
         GOTOR CDATAMGR,WRKIPARM,DMPRINT                                        
         BE    EXIT                                                             
         MVI   WRKIERRS,WRKIECAR                                                
         B     EXIT                                                             
                                                                                
***********************************************************************         
* PUT LAST READ RECORD BACK TO FILE                                   *         
***********************************************************************         
                                                                                
WKPUT    TM    WRKISTAT,WRKISOPN                                                
         BO    *+12                                                             
         MVI   WRKIERRS,WRKIEIAS                                                
         B     EXIT                                                             
         GOTOR CDATAMGR,WRKIPARM,DMWRITE                                        
         BE    EXIT                                                             
         MVI   WRKIERRS,WRKIECWR                                                
         B     EXIT                                                             
                                                                                
***********************************************************************         
* GET FIRST/NEXT RECORD OF FILE                                       *         
***********************************************************************         
                                                                                
WKGET    TM    WRKISTAT,WRKISOPN                                                
         BNZ   *+12                                                             
         MVI   WRKIERRS,WRKIEIAS                                                
         B     EXIT                                                             
         GOTOR CDATAMGR,WRKIPARM,DMREAD                                         
         BE    EXIT                                                             
         TM    WRKIDMER,WRKIDEOF   TEST END OF FILE                             
         BZ    *+12                                                             
         MVI   WRKIERRS,WRKIEEOF   YES - SET END OF FILE                        
         B     EXIT                                                             
         DC    H'0'                                                             
                                                                                
***********************************************************************         
* (REGULAR) CLOSE OF FILE                                             *         
***********************************************************************         
                                                                                
WKCLO    TM    WRKISTAT,WRKISOPN                                                
         BNZ   *+12                                                             
         MVI   WRKIERRS,WRKIEIAS                                                
         B     EXIT                                                             
         MVI   WRKISTAT,0                                                       
         MVC   WKWLSOFLAB(L'EOFLAB),EOFLAB                                      
         GOTOR CDATAMGR,WRKIPARM,DMPRINT,,,WKWLHDRD                             
         BE    EXIT                                                             
         DC    H'0'                                                             
                                                                                
***********************************************************************         
* CLOSE AND PURGE THE FILE                                            *         
***********************************************************************         
                                                                                
WKCLP    TM    WRKISTAT,WRKISOPN                                                
         BNZ   *+12                                                             
         MVI   WRKIERRS,WRKIEIAS                                                
         B     EXIT                                                             
         MVI   WRKISTAT,0                                                       
         GOTOR CDATAMGR,WRKIPARM,DMCLOPUR                                       
         BE    EXIT                                                             
         DC    H'0'                                                             
                                                                                
***********************************************************************         
* CLOSE FILE AND SET 'IN ERROR' ATTRIBUTE                             *         
***********************************************************************         
                                                                                
WKCLE    TM    WRKISTAT,WRKISOPN                                                
         BNZ   *+12                                                             
         MVI   WRKIERRS,WRKIEIAS                                                
         B     EXIT                                                             
         MVI   WRKISTAT,0                                                       
         MVC   WKWLSOFLAB(L'EOELAB),EOELAB                                      
         GOTOR CDATAMGR,WRKIPARM,DMPRINT,,,WKWLHDRD                             
         BE    EXIT                                                             
         DC    H'0'                                                             
                                                                                
***********************************************************************         
* GET FIRST/NEXT INDEX RECORD AND APPLY FILTERS                       *         
***********************************************************************         
                                                                                
WKNDX    MVI   WRKISTAT,0          SET FILE NOT OPEN                            
         MVC   WKUKUSRID,WRKFUSID  SET USER-ID FOR GETFIL                       
         GOTOR GETFIL              ESTABLISH THE FILE                           
         BE    WKNDX02                                                          
         XC    WRKWKEY,WRKWKEY     CLEAR KEY IF FIRST TIME                      
                                                                                
WKNDX02  GOTOR CDATAMGR,WRKIPARM,DMINDEX                                        
         BE    WKNDX04                                                          
         MVI   WRKIERRS,WRKIEEOF                                                
         XC    WRKWKEY,WRKWKEY                                                  
         B     EXIT                                                             
                                                                                
WKNDX04  CLC   WRKWUSID,WRKFUSID   APPLY USER-ID FILTER                         
         BNE   WKNDX02                                                          
                                                                                
         OC    WRKFSYS,WRKFSYS     TEST/APPLY SYSTEM FILTER                     
         BZ    *+14                                                             
         CLC   WRKWSYS,WRKFSYS                                                  
         BNE   WKNDX02                                                          
                                                                                
         OC    WRKFPRG,WRKFPRG     TEST/APPLY PROGRAM FILTER                    
         BZ    *+14                                                             
         CLC   WRKWPRG,WRKFPRG                                                  
         BNE   WKNDX02                                                          
                                                                                
         OC    WRKFSPG,WRKFSPG     TEST/APPLY SUB-PROGRAM FILTER                
         BZ    *+14                                                             
         CLC   WRKWSPG,WRKFSPG                                                  
         BNE   WKNDX02                                                          
                                                                                
         OC    WRKFDAY,WRKFDAY     TEST/APPLY DAY FILTER                        
         BZ    *+14                                                             
         CLC   WRKWDAY,WRKFDAY                                                  
         BNE   WKNDX02                                                          
                                                                                
         OC    WRKFCLS,WRKFCLS     TEST/APPLY CLASS FILTER                      
         BZ    *+14                                                             
         CLC   WRKWCLS,WRKFCLS                                                  
         BNE   WKNDX02                                                          
                                                                                
         CLI   WKUKSTAT,0          TEST PURGED ENTRY                            
         BE    WKNDX02                                                          
                                                                                
         CLI   WRKFYSTA,0          TEST/APPLY INCLUDE STATUS FILTER             
         BE    WKNDX06                                                          
         MVC   WORK(L'WRKFYSTA),WKUKSTAT                                        
         NC    WORK(L'WRKFYSTA),WRKFYSTA                                        
         CLC   WRKFYSTA,WORK       ALL INCLUDE STATUSES MUST MATCH              
         BNE   WKNDX02                                                          
                                                                                
WKNDX06  CLI   WRKFNSTA,0          TEST/APPLY EXCLUDE STATUS FILTER             
         BE    WKNDX08                                                          
         MVC   WORK(L'WRKFNSTA),WKUKSTAT                                        
         NC    WORK(L'WRKFNSTA),WRKFNSTA                                        
         CLI   WORK,0              ALL EXCLUDE STATUSES MUST MATCH              
         BNE   WKNDX02                                                          
                                                                                
WKNDX08  B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* BRANCH INDEX TO 16 BIT DISK ADDRESS WORKER FILES (WRKFXX) FOR EASI  *         
* (THIS MUST BE KEPT IN SYNC WITH WKZTAB AND THE WKZTAB ROUTINES)               
***********************************************************************         
                                                                                
WKFTAB   DS    0XL4                                                             
         B     EFNEW               OPEN A NEW FILE                              
         B     EFOPN               OPEN AN EXISTING FILE                        
         B     EFORA               OPEN EXISTING FILE FOR REPLACE               
         B     EFORA               OPEN EXISTING FILE FOR APPEND                
         B     EFADD               ADD RECORD TO FILE                           
         B     EFPUT               PUT RECORD TO FILE                           
         B     EFGET               GET FIRST/NEXT RECORD FROM FILE              
         B     EFCLO               CLOSE THE FILE                               
         DC    (L'WKFTAB)X'00'     CLOSE AND PURGE THE FILE                     
         DC    (L'WKFTAB)X'00'     CLOSE AND SET 'IN ERROR' ATTRIBUTE           
         B     EFNDX               GET FIRST/NEXT INDEX RECORD                  
         B     EFDEL               DELETE FILE                                  
         B     EFKEE               KEEP FILE                                    
         B     EFUKE               UNKEEP FILE                                  
         B     EFRET               RETAIN FILE                                  
         B     EFRAN               RANDOM READ                                  
         B     EFGLS               GET FILE LIST                                
         B     EFPUR               PURGE FILE                                   
         B     EFDSC               SET FILE DESCRIPTION                         
         EJECT                                                                  
                                                                                
***********************************************************************         
* ADD A NEW EZ WORKER FILE                                            *         
***********************************************************************         
         USING WKUKRECD,WRKINDX                                                 
         USING WKWLHDRD,HEADER                                                  
                                                                                
EFNEW    GOTOR CLRBUF              CLEAR THE BUFFER                             
         GOTOR GETFIL              ESTABLISH THE FILE                           
                                                                                
         GOTOR EZFORZ              WRKF OR WRKZ                                 
         BNE   EZNEWF                                                           
                                                                                
         MVC   WKUKFILENO(WKUKLAST-WKUKFILENO),NULLS                            
         MVC   WKWLHEAD,NULLS      CLEAR TO ZEROES                              
         MVC   WKWLINDEX,NULLS                                                  
         MVC   WKWLBATTR,NULLS                                                  
         MVC   WKWLDATA,NULLS                                                   
         MVC   WKWLSOFLAB(L'SOFLAB),SOFLAB                                      
         MVC   WKWLINDEX(L'WKUKKEY),WRKEZKEY                                    
         MVC   WKWLRETNL,MAXRET    SET PERMANENT RETENTION                      
         MVC   WKWLSTAT,WRKEZSTA   SET FILE STATUS                              
         MVC   WKWLDESC,WRKEZDSC   SET FILE DESCRIPTION                         
         MVC   WKWLUDATA,WRKEZUDT  SET USER DATA                                
         MVC   WKWLDATEL,WRKEZBDT  SET BATCH DATE                               
         MVC   WKWLTIMEL,WRKEZBTM  SET BATCH TIME                               
         TM    WRKINDS,WRKICOPQ    USER WANTS TO COPY FILE VALUES               
         BZ    *+8                                                              
         OI    WKWLFLAG,WKWLFLRCOP                                              
         GOTOR CDATAMGR,WRKIPARM,DMPRINT,,,WKWLHDRD                             
         BE    EFNEW02                                                          
         MVI   WRKIERRS,WRKIEFOE   SET FILE OPEN ERROR                          
         B     EXIT                                                             
                                                                                
EFNEW02  MVC   WRKEZSQN(2),NULLS                                                
         MVC   WRKEZSQN+2(2),WKWLREPRNO                                         
         MVC   WRKEZBDT,WKUKAGELD                                               
         MVC   WRKEZBTM,WKUKAGELT                                               
         MVI   WRKISTAT,WRKISOPN+WRKISUPD                                       
         MVI   WRKIACTN,WRKIAADD   NEXT ACTION IS ADD                           
         B     EXIT                                                             
                                                                                
***********************************************************************         
* OPEN AN EXISTING EZ WORKER FILE                                     *         
***********************************************************************         
                                                                                
EFOPN    GOTOR CLRBUF              CLEAR THE BUFFER                             
         GOTOR GETFIL              ESTABLISH THE FILE                           
                                                                                
         GOTOR EZFORZ              WRKF OR WRKZ                                 
         BNE   EZOPNF                                                           
                                                                                
         CLC   WRKEZSQN,NULLS                                                   
         BE    *+8                                                              
         OI    WKUKFLAG,WKUKFLDAT                                               
         MVC   WKWLHEAD,NULLS                                                   
         MVC   WKWLINDEX,NULLS                                                  
         MVC   WKWLBATTR,NULLS                                                  
         MVC   WKWLDATA,NULLS                                                   
         MVC   WKWLSOFLAB(L'SOFLAB),SOFLAB                                      
         MVC   WKWLINDEX(WRKKEYLQ),WRKEZKEY                                     
         MVC   WKWLINDEX+WRKKEYLQ(2),WRKEZSQN+2                                 
         MVC   WKUKINDEX,WRKEZKEY                                               
         MVC   WKUKKEY,WRKEZKEY                                                 
         MVC   WKUKFILENO,WRKEZSQN+2                                            
         OI    WKWLFLAG,WKWLFLREFN                                              
         GOTOR CDATAMGR,WRKIPARM,DMINDEX,,,WKWLHDRD                             
         BE    EFOPN02                                                          
         MVI   WRKIERRS,WRKIEFNF                                                
         B     EXIT                                                             
                                                                                
EFOPN02  MVC   WRKEZBDT,WKUKAGELD  SET RETURN VALUES                            
         MVC   WRKEZUDT,WKUKUDATA                                               
         MVI   WRKISTAT,WRKISOPN                                                
         MVI   WRKIACTN,WRKIAGET   SET NEXT ACTION AS 'GET'                     
         B     EXIT                                                             
                                                                                
***********************************************************************         
* OPEN AN EXISTING EZ WORKER FILE FOR APPEND                          *         
***********************************************************************         
                                                                                
EFORA    CLC   WRKEZSQN,NULLS      TEST FILE NUMBER SET                         
         BNE   EFORA02                                                          
         MVI   WRKIERRS,WRKIEKEY                                                
         B     EXIT                                                             
                                                                                
EFORA02  GOTOR CLRBUF              CLEAR THE BUFFER                             
         GOTOR GETFIL              ESTABLISH THE FILE                           
                                                                                
         GOTOR EZFORZ              WRKF OR WRKZ                                 
         BNE   EZORAF                                                           
                                                                                
         MVC   WKWLHEAD,NULLS                                                   
         MVC   WKWLINDEX,NULLS                                                  
         MVC   WKWLBATTR,NULLS                                                  
         MVC   WKWLDATA,NULLS                                                   
         MVC   WKWLSOFLAB(L'SOFLAB),SOFLAB                                      
         MVC   WKWLINDEX(WRKKEYLQ),WRKEZKEY                                     
         MVC   WKWLINDEX+WRKKEYLQ(2),WRKEZSQN+2                                 
         OI    WKWLFLAG,WKWLFLREFN+WKWLFLRCOP                                   
         CLI   WRKIACTN,WRKIAREP                                                
         BE    *+8                                                              
         OI    WKWLFLAG,WKWLFLMOD  SET TO OPEN FOR APPEND                       
         MVC   WKWLRETNL,MAXRET    SET LIVE RETENTION HOURS                     
         GOTOR CDATAMGR,WRKIPARM,DMPRINT,,,WKWLHDRD                             
         BE    EFORA04                                                          
         MVI   WRKIERRS,WRKIEFNF                                                
         B     EXIT                                                             
                                                                                
EFORA04  MVC   WKUKINDEX,WKWLINDEX                                              
         MVI   WRKISTAT,WRKISOPN+WRKISUPD                                       
         MVC   WRKEZSQN+2(L'WKWLREPRNO),WKWLREPRNO                              
         MVC   WRKEZBDT,WKUKAGELD                                               
         MVC   WRKEZUDT,WKUKUDATA                                               
         MVC   WRKEZSTA,WKUKSTAT                                                
         B     EXIT                                                             
                                                                                
***********************************************************************         
* ADD A NEW RECORD TO EZ WORKER FILE                                  *         
***********************************************************************         
                                                                                
EFADD    TM    WRKISTAT,WRKISOPN+WRKISUPD                                       
         BO    *+12                                                             
         MVI   WRKIERRS,WRKIEIAS                                                
         B     EXIT                                                             
                                                                                
         GOTOR EZFORZ              WRKF OR WRKZ                                 
         BNE   EZADD                                                            
                                                                                
         GOTOR CDATAMGR,WRKIPARM,DMPRINT                                        
         BE    EXIT                                                             
         MVI   WRKIERRS,WRKIECAR                                                
         B     EXIT                                                             
                                                                                
***********************************************************************         
* WRITE BACK EZ WORKER FILE RECORD                                    *         
***********************************************************************         
                                                                                
EFPUT    TM    WRKISTAT,WRKISOPN                                                
         BO    *+12                                                             
         MVI   WRKIERRS,WRKIEIAS                                                
         B     EXIT                                                             
                                                                                
         GOTOR EZFORZ              WRKF OR WRKZ                                 
         BNE   EZPUT                                                            
                                                                                
         GOTOR CDATAMGR,WRKIPARM,DMWRITE                                        
         BE    EXIT                                                             
         MVI   WRKIERRS,WRKIECWR                                                
         B     EXIT                                                             
                                                                                
***********************************************************************         
* GET NEXT RECORD FROM AN EZ WORKER FILE                              *         
***********************************************************************         
                                                                                
EFGET    TM    WRKISTAT,WRKISOPN                                                
         BNZ   *+12                                                             
         MVI   WRKIERRS,WRKIEIAS                                                
         B     EXIT                                                             
                                                                                
         GOTOR EZFORZ              WRKF OR WRKZ                                 
         BNE   EZGET                                                            
                                                                                
         GOTOR CDATAMGR,WRKIPARM,DMREAD                                         
         BE    EFGET02                                                          
         TM    WRKIDMER,WRKIDEOF   TEST END OF FILE                             
         BZ    *+12                                                             
         MVI   WRKIERRS,WRKIEEOF   YET - SET END OF FILE                        
         B     EXIT                                                             
         MVI   WRKIERRS,WRKIEIOE                                                
         B     EXIT                                                             
                                                                                
EFGET02  L     R1,WRKIABUF                                                      
         MVC   WRKEZDSC,WKWLDESC-WKWLINDEX(R1)                                  
         B     EXIT                                                             
                                                                                
***********************************************************************         
* CLOSE AN EZ WORKER FILE                                             *         
***********************************************************************         
                                                                                
EFCLO    TM    WRKISTAT,WRKISOPN                                                
         BNZ   EFCLO02                                                          
         MVI   WRKIERRS,WRKIEIAS                                                
         B     EXIT                                                             
                                                                                
EFCLO02  GOTOR EZFORZ              WRKF OR WRKZ                                 
         BNE   EZCLO                                                            
                                                                                
         MVI   WRKISTAT,0                                                       
         MVC   WKWLSOFLAB(L'EOFLAB),EOFLAB                                      
         GOTOR CDATAMGR,WRKIPARM,DMPRINT,,,WKWLHDRD                             
         BE    EXIT                                                             
         MVI   WRKIERRS,WRKIEIOE                                                
         B     EXIT                                                             
                                                                                
***********************************************************************         
* GET FIRST/NEXT INDEX RECORD FOR AN EZ WORKER FILE                   *         
***********************************************************************         
                                                                                
EFNDX    MVI   WRKISTAT,0          SET FILE NOT OPEN                            
         GOTOR GETFIL              ESTABLISH THE FILE                           
                                                                                
         GOTOR EZFORZ              WRKF OR WRKZ                                 
         BNE   EZNDXF                                                           
                                                                                
         TM    WRKINDS,WRKINOXQ                                                 
         BZ    *+8                                                              
         OI    WKUKFLAG,WKUKFLNOX                                               
         TM    WRKINDS,WRKISEQQ    USER WANTS SPECIFIC SEQ?                     
         BZ    EFNDX01                                                          
         OI    WKUKFLAG,WKUKFLDAT  SET FLAG                                     
         MVC   WKUKFILENO,WRKEZSQN+2  SET BATCH SEQ                             
         B     EFNDX02             DO *NOT* CLEAR KEY                           
*                                                                               
EFNDX01  CLC   WRKEZSCL,NULLS      TEST FIRST INDEX CALL                        
         BNE   EFNDX02                                                          
         XC    WRKWKEY,WRKWKEY     YES - CLEAR ACTUAL KEY                       
                                                                                
EFNDX02  GOTOR CDATAMGR,WRKIPARM,DMINDEX                                        
         BE    EFNDX04                                                          
         NI    WRKINDS,X'FF'-WRKISEQQ                                           
         MVI   WRKIERRS,WRKIEEOF                                                
         MVC   WRKEZKEY,NULLS                                                   
         B     EXIT                                                             
                                                                                
EFNDX04  NI    WRKINDS,X'FF'-WRKISEQQ                                           
         CLC   WRKEZUID,NULLS      TEST USER-ID FILTER PRESENT                  
         BE    EFNDX08                                                          
         CLC   WRKWUSID,WRKEZUID   MATCH FILTER                                 
         BNE   EFNDX02             NO - GET NEXT INDEX ENTRY                    
                                                                                
EFNDX08  MVC   WRKEZKEY(WRKKEYLQ),WRKWKEY                                       
         MVC   WRKEZSQN(2),NULLS                                                
         MVC   WRKEZSQN+2(2),WRKWSEQN                                           
         MVC   WRKEZBDT,WKUKAGELD                                               
         MVC   WRKEZBTM,WKUKAGELT                                               
         MVC   WRKEZUDT,WKUKUDATA                                               
         MVC   WRKEZSTA,WKUKSTAT                                                
         MVI   WRKISTAT,WRKISOPN+WRKISUPD                                       
         B     EXIT                                                             
                                                                                
***********************************************************************         
* DELETE AN EZ WORKER FILE                                            *         
***********************************************************************         
                                                                                
EFDEL    TM    WRKISTAT,WRKISOPN                                                
         BNZ   *+12                                                             
         MVI   WRKIERRS,WRKIEIAS                                                
         B     EXIT                                                             
                                                                                
         GOTOR EZFORZ              WRKF OR WRKZ                                 
         BNE   EZDEL                                                            
                                                                                
         GOTOR CDATAMGR,WRKIPARM,DMDELETE                                       
         MVI   WRKISTAT,0                                                       
         B     EXIT                                                             
                                                                                
***********************************************************************         
* KEEP AN EZ WORKER FILE                                              *         
***********************************************************************         
                                                                                
EFKEE    GOTOR EZFORZ              WRKF OR WRKZ                                 
         BNE   EZKEE                                                            
                                                                                
         GOTOR CDATAMGR,WRKIPARM,DMKEEP                                         
         B     EXIT                                                             
                                                                                
***********************************************************************         
* UNKEEP AN EZ WORKER FILE                                            *         
***********************************************************************         
                                                                                
EFUKE    GOTOR EZFORZ              WRKF OR WRKZ                                 
         BNE   EZUKE                                                            
                                                                                
         GOTOR CDATAMGR,WRKIPARM,DMUNKEEP                                       
         B     EXIT                                                             
                                                                                
***********************************************************************         
* SET RETENTION FOR AN EZ WORKER FILE                                 *         
***********************************************************************         
                                                                                
EFRET    GOTOR EZFORZ              WRKF OR WRKZ                                 
         BNE   EZRET                                                            
                                                                                
         OI    WKUKFLAG,WKUKFLHRS                                               
         MVC   WKUKINFO,WRKEZINF                                                
         GOTOR CDATAMGR,WRKIPARM,DMRET                                          
         B     EXIT                                                             
                                                                                
***********************************************************************         
* GET FILE LIST                                                       *         
***********************************************************************         
                                                                                
EFGLS    MVC   WRKIFILE,WRKF1      SET WRKF FILE (VERSION 1)                    
         GOTOR CDATAMGR,WRKIPARM,DMGLIST                                        
         MVC   WRKUSINF,WKUKUSRINF                                              
         B     EXIT                                                             
                                                                                
***********************************************************************         
* READ A RECORD RANDOMLY - WRKRECNO IS RECORD NUMBER                  *         
***********************************************************************         
                                                                                
EFRAN    GOTOR EZFORZ              WRKF OR WRKZ                                 
         BNE   EZRAN                                                            
                                                                                
         L     R1,WRKIAREC                                                      
         XC    0(2,R1),0(R1)                                                    
         MVC   2(2,R1),WRKEZREC                                                 
         MVC   4(4,R1),REC#                                                     
         GOTOR CDATAMGR,WRKIPARM,DMRANDOM                                       
         B     EXIT                                                             
                                                                                
***********************************************************************         
* PURGE FILE                                                          *         
***********************************************************************         
                                                                                
EFPUR    GOTOR EZFORZ              WRKF OR WRKZ                                 
         BNE   EZPUR                                                            
                                                                                
         GOTOR CDATAMGR,WRKIPARM,DMPURGE                                        
         MVI   WRKISTAT,0                                                       
         B     EXIT                                                             
                                                                                
***********************************************************************         
* SET FILE DESCRIPTION                                                *         
***********************************************************************         
                                                                                
EFDSC    GOTOR EZFORZ              WRKF OR WRKZ                                 
         BNE   EZDSC                                                            
                                                                                
         MVC   WKWLDESC,WRKEZDSC   SET FILE DESCRIPTION                         
         GOTOR CDATAMGR,WRKIPARM,DMDESC,,,WKWLHDRD                              
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* BRANCH INDEX TO 20 BIT DISK ADDRESS WORKER FILES (WRKZXX) FOR EASI  *         
***********************************************************************         
                                                                                
WKZTAB   DS    0XL4                                                             
         B     EZNEW               OPEN A NEW FILE                              
         B     EZOPN               OPEN AN EXISTING FILE                        
         B     EZORA               OPEN EXISTING FILE FOR REPLACE               
         B     EZORA               OPEN EXISTING FILE FOR APPEND                
         B     EZADD               ADD RECORD TO FILE                           
         B     EZPUT               PUT RECORD TO FILE                           
         B     EZGET               GET FIRST/NEXT RECORD FROM FILE              
         B     EZCLO               CLOSE THE FILE                               
         DC    (L'WKZTAB)X'00'     CLOSE AND PURGE THE FILE                     
         DC    (L'WKZTAB)X'00'     CLOSE AND SET 'IN ERROR' ATTRIBUTE           
         B     EZNDX               GET FIRST/NEXT INDEX RECORD                  
         B     EZDEL               DELETE FILE                                  
         B     EZKEE               KEEP FILE                                    
         B     EZUKE               UNKEEP FILE                                  
         B     EZRET               RETAIN FILE                                  
         B     EZRAN               RANDOM READ                                  
         B     EZGLS               GET FILE LIST                                
         B     EZPUR               PURGE FILE                                   
         B     EZDSC               SET FILE DESCRIPTION                         
MAXACTS  EQU   (*-WKZTAB)/L'WKZTAB                                              
         EJECT                                                                  
                                                                                
***********************************************************************         
* ADD A NEW EZ WORKER FILE                                            *         
***********************************************************************         
         USING EZUKRECD,WRKINDX                                                 
         USING EZWLHDRD,HEADER                                                  
                                                                                
EZNEW    GOTOR CLRBUF              CLEAR THE BUFFER                             
         GOTOR GETFIL              ESTABLISH THE FILE                           
                                                                                
EZNEWF   MVC   EZUKFILENO(EZUKLAST-EZUKFILENO),NULLS                            
         MVC   EZWLHEAD,NULLS      CLEAR TO ZEROES                              
         MVC   EZWLINDEX,NULLS                                                  
         MVC   EZWLBATTR,NULLS                                                  
         MVC   EZWLDATA,NULLS                                                   
         MVC   EZWLSOFLAB(L'SOFLAB),SOFLAB                                      
         MVC   EZWLINDEX(L'EZUKKEY),WRKEZKEY                                    
         MVC   EZWLRETNL,MAXRET    SET PERMANENT RETENTION                      
         MVC   EZWLSTAT,WRKEZSTA   SET FILE STATUS                              
         MVC   EZWLDESC,WRKEZDSC   SET FILE DESCRIPTION                         
         MVC   EZWLUDATA,WRKEZUDT  SET USER DATA                                
         MVC   EZWLUDATA2,WRKEZMOS SET USER DATA 2                              
         MVC   EZWLDATEL,WRKEZBDT  SET BATCH DATE                               
         MVC   EZWLTIMEL,WRKEZBTM  SET BATCH TIME                               
         TM    WRKINDS,WRKICOPQ    USER WANTS TO COPY FILE VALUES               
         BZ    *+8                                                              
         OI    EZWLFLAG,EZWLFLRCOP                                              
         GOTOR CDATAMGR,WRKIPARM,DMPRINT,,,EZWLHDRD                             
         BE    EZNEW02                                                          
         MVI   WRKIERRS,WRKIEFOE   SET FILE OPEN ERROR                          
         B     EXIT                                                             
                                                                                
EZNEW02  MVC   WRKEZSQN,EZWLREPRNO                                              
         MVC   WRKEZBDT,EZUKAGELD                                               
         MVC   WRKEZBTM,EZUKAGELT                                               
         MVI   WRKISTAT,WRKISOPN+WRKISUPD                                       
         MVI   WRKIACTN,WRKIAADD   NEXT ACTION IS ADD                           
         B     EXIT                                                             
                                                                                
***********************************************************************         
* OPEN AN EXISTING EZ WORKER FILE                                     *         
***********************************************************************         
                                                                                
EZOPN    GOTOR CLRBUF              CLEAR THE BUFFER                             
         GOTOR GETFIL              ESTABLISH THE FILE                           
                                                                                
EZOPNF   CLC   WRKEZSQN,NULLS                                                   
         BE    *+8                                                              
         OI    EZUKFLAG,EZUKFLDAT                                               
         MVC   EZWLHEAD,NULLS                                                   
         MVC   EZWLINDEX,NULLS                                                  
         MVC   EZWLBATTR,NULLS                                                  
         MVC   EZWLDATA,NULLS                                                   
         MVC   EZWLSOFLAB(L'SOFLAB),SOFLAB                                      
         MVC   EZWLINDEX(WRKKEYLQ),WRKEZKEY                                     
         MVC   EZWLINDEX+WRKKEYLQ(4),WRKEZSQN                                   
         MVC   EZUKINDEX,WRKEZKEY                                               
         MVC   EZUKKEY,WRKEZKEY                                                 
         MVC   EZUKFILENO,WRKEZSQN                                              
         OI    EZWLFLAG,EZWLFLREFN                                              
         GOTOR CDATAMGR,WRKIPARM,DMINDEX,,,EZWLHDRD                             
         BE    EZOPN02                                                          
         MVI   WRKIERRS,WRKIEFNF                                                
         B     EXIT                                                             
                                                                                
EZOPN02  MVC   WRKEZBDT,EZUKAGELD  SET RETURN VALUES                            
         MVC   WRKEZUDT,EZUKUDATA                                               
         MVI   WRKISTAT,WRKISOPN                                                
         MVI   WRKIACTN,WRKIAGET   SET NEXT ACTION AS 'GET'                     
         B     EXIT                                                             
                                                                                
***********************************************************************         
* OPEN AN EXISTING EZ WORKER FILE FOR APPEND                          *         
***********************************************************************         
                                                                                
EZORA    CLC   WRKEZSQN,NULLS      TEST FILE NUMBER SET                         
         BNE   EZORA02                                                          
         MVI   WRKIERRS,WRKIEKEY                                                
         B     EXIT                                                             
                                                                                
EZORA02  GOTOR CLRBUF              CLEAR THE BUFFER                             
         GOTOR GETFIL              ESTABLISH THE FILE                           
EZORAF   MVC   EZWLHEAD,NULLS                                                   
         MVC   EZWLINDEX,NULLS                                                  
         MVC   EZWLBATTR,NULLS                                                  
         MVC   EZWLDATA,NULLS                                                   
         MVC   EZWLSOFLAB(L'SOFLAB),SOFLAB                                      
         MVC   EZWLINDEX(WRKKEYLQ),WRKEZKEY                                     
         MVC   EZWLINDEX+WRKKEYLQ(4),WRKEZSQN                                   
         OI    EZWLFLAG,EZWLFLREFN+EZWLFLRCOP                                   
         CLI   WRKIACTN,WRKIAREP                                                
         BE    *+8                                                              
         OI    EZWLFLAG,EZWLFLMOD  SET TO OPEN FOR APPEND                       
         MVC   EZWLRETNL,MAXRET    SET LIVE RETENTION HOURS                     
         GOTOR CDATAMGR,WRKIPARM,DMPRINT,,,EZWLHDRD                             
         BE    EZORA04                                                          
         MVI   WRKIERRS,WRKIEFNF                                                
         B     EXIT                                                             
                                                                                
EZORA04  MVC   EZUKINDEX,EZWLINDEX                                              
         MVI   WRKISTAT,WRKISOPN+WRKISUPD                                       
         MVC   WRKEZSQN(L'EZWLREPRNO),EZWLREPRNO                                
         MVC   WRKEZBDT,EZUKAGELD                                               
         MVC   WRKEZUDT,EZUKUDATA                                               
         MVC   WRKEZSTA,EZUKSTAT                                                
         B     EXIT                                                             
                                                                                
***********************************************************************         
* ADD A NEW RECORD TO EZ WORKER FILE                                  *         
***********************************************************************         
                                                                                
EZADD    TM    WRKISTAT,WRKISOPN+WRKISUPD                                       
         BO    *+12                                                             
         MVI   WRKIERRS,WRKIEIAS                                                
         B     EXIT                                                             
         GOTOR CDATAMGR,WRKIPARM,DMPRINT                                        
         BE    EXIT                                                             
         MVI   WRKIERRS,WRKIECAR                                                
         B     EXIT                                                             
                                                                                
***********************************************************************         
* WRITE BACK EZ WORKER FILE RECORD                                    *         
***********************************************************************         
                                                                                
EZPUT    TM    WRKISTAT,WRKISOPN                                                
         BO    *+12                                                             
         MVI   WRKIERRS,WRKIEIAS                                                
         B     EXIT                                                             
         GOTOR CDATAMGR,WRKIPARM,DMWRITE                                        
         BE    EXIT                                                             
         MVI   WRKIERRS,WRKIECWR                                                
         B     EXIT                                                             
                                                                                
***********************************************************************         
* GET NEXT RECORD FROM AN EZ WORKER FILE                              *         
***********************************************************************         
                                                                                
EZGET    TM    WRKISTAT,WRKISOPN                                                
         BNZ   *+12                                                             
         MVI   WRKIERRS,WRKIEIAS                                                
         B     EXIT                                                             
         GOTOR CDATAMGR,WRKIPARM,DMREAD                                         
         BE    EZGET02                                                          
         TM    WRKIDMER,WRKIDEOF   TEST END OF FILE                             
         BZ    *+12                                                             
         MVI   WRKIERRS,WRKIEEOF   YET - SET END OF FILE                        
         B     EXIT                                                             
         MVI   WRKIERRS,WRKIEIOE                                                
         B     EXIT                                                             
                                                                                
EZGET02  L     R1,WRKIABUF                                                      
         MVC   WRKEZDSC,EZWLDESC-EZWLINDEX(R1)                                  
         B     EXIT                                                             
                                                                                
***********************************************************************         
* CLOSE AN EZ WORKER FILE                                             *         
***********************************************************************         
                                                                                
EZCLO    TM    WRKISTAT,WRKISOPN                                                
         BNZ   EZCLO02                                                          
         MVI   WRKIERRS,WRKIEIAS                                                
         B     EXIT                                                             
                                                                                
EZCLO02  MVI   WRKISTAT,0                                                       
         MVC   EZWLSOFLAB(L'EOFLAB),EOFLAB                                      
         GOTOR CDATAMGR,WRKIPARM,DMPRINT,,,EZWLHDRD                             
         BE    EXIT                                                             
         MVI   WRKIERRS,WRKIEIOE                                                
         B     EXIT                                                             
                                                                                
***********************************************************************         
* GET FIRST/NEXT INDEX RECORD FOR AN EZ WORKER FILE                   *         
***********************************************************************         
                                                                                
EZNDX    MVI   WRKISTAT,0          SET FILE NOT OPEN                            
         GOTOR GETFIL              ESTABLISH THE FILE                           
EZNDXF   TM    WRKINDS,WRKINOXQ                                                 
         BZ    *+8                                                              
         OI    EZUKFLAG,EZUKFLNOX                                               
         TM    WRKINDS,WRKISEQQ    USER WANTS SPECIFIC SEQ?                     
         BZ    EZNDX01                                                          
         OI    EZUKFLAG,EZUKFLDAT  SET FLAG                                     
         MVC   EZUKFILENO,WRKEZSQN SET BATCH SEQ                                
         B     EZNDX02             DO *NOT* CLEAR KEY                           
*                                                                               
EZNDX01  CLC   WRKEZSCL,NULLS      TEST FIRST INDEX CALL                        
         BNE   EZNDX02                                                          
         XC    WRKWKEY,WRKWKEY     YES - CLEAR ACTUAL KEY                       
                                                                                
EZNDX02  GOTOR CDATAMGR,WRKIPARM,DMINDEX                                        
         BE    EZNDX04                                                          
         NI    WRKINDS,X'FF'-WRKISEQQ                                           
         MVI   WRKIERRS,WRKIEEOF                                                
         MVC   WRKEZKEY,NULLS                                                   
         B     EXIT                                                             
                                                                                
EZNDX04  NI    WRKINDS,X'FF'-WRKISEQQ                                           
         CLC   WRKEZUID,NULLS      TEST USER-ID FILTER PRESENT                  
         BE    EZNDX08                                                          
         CLC   WRKWUSID,WRKEZUID   MATCH FILTER                                 
         BNE   EZNDX02             NO - GET NEXT INDEX ENTRY                    
                                                                                
EZNDX08  MVC   WRKEZKEY(WRKKEYLQ),WRKWKEY                                       
         MVC   WRKEZSQN,WRKWSEQN                                                
         MVC   WRKEZBDT,EZUKAGELD                                               
         MVC   WRKEZBTM,EZUKAGELT                                               
         MVC   WRKEZUDT,EZUKUDATA                                               
         MVC   WRKEZMOS,EZUKUDATA2                                              
         MVC   WRKEZSTA,EZUKSTAT                                                
         MVI   WRKISTAT,WRKISOPN+WRKISUPD                                       
         B     EXIT                                                             
                                                                                
***********************************************************************         
* DELETE AN EZ WORKER FILE                                            *         
***********************************************************************         
                                                                                
EZDEL    TM    WRKISTAT,WRKISOPN                                                
         BNZ   *+12                                                             
         MVI   WRKIERRS,WRKIEIAS                                                
         B     EXIT                                                             
         GOTOR CDATAMGR,WRKIPARM,DMDELETE                                       
         MVI   WRKISTAT,0                                                       
         B     EXIT                                                             
                                                                                
***********************************************************************         
* KEEP AN EZ WORKER FILE                                              *         
***********************************************************************         
                                                                                
EZKEE    GOTOR CDATAMGR,WRKIPARM,DMKEEP                                         
         B     EXIT                                                             
                                                                                
***********************************************************************         
* UNKEEP AN EZ WORKER FILE                                            *         
***********************************************************************         
                                                                                
EZUKE    GOTOR CDATAMGR,WRKIPARM,DMUNKEEP                                       
         B     EXIT                                                             
                                                                                
***********************************************************************         
* SET RETENTION FOR AN EZ WORKER FILE                                 *         
***********************************************************************         
                                                                                
EZRET    OI    EZUKFLAG,EZUKFLHRS                                               
         MVC   EZUKINFO,WRKEZINF                                                
         GOTOR CDATAMGR,WRKIPARM,DMRET                                          
         B     EXIT                                                             
                                                                                
***********************************************************************         
* GET FILE LIST                                                       *         
***********************************************************************         
                                                                                
EZGLS    MVC   WRKIFILE,WRKZ1      SET WRKZ FILE                                
         GOTOR CDATAMGR,WRKIPARM,DMGLIST                                        
         MVC   WRKUSINF,EZUKUSRINF                                              
         B     EXIT                                                             
                                                                                
***********************************************************************         
* READ A RECORD RANDOMLY - WRKRECNO IS RECORD NUMBER                  *         
***********************************************************************         
                                                                                
EZRAN    L     R1,WRKIAREC                                                      
         XC    0(2,R1),0(R1)                                                    
         MVC   2(2,R1),WRKEZREC                                                 
         MVC   4(4,R1),REC#                                                     
         GOTOR CDATAMGR,WRKIPARM,DMRANDOM                                       
         B     EXIT                                                             
                                                                                
***********************************************************************         
* PURGE FILE                                                          *         
***********************************************************************         
                                                                                
EZPUR    GOTOR CDATAMGR,WRKIPARM,DMPURGE                                        
         MVI   WRKISTAT,0                                                       
         B     EXIT                                                             
                                                                                
***********************************************************************         
* SET FILE DESCRIPTION                                                *         
***********************************************************************         
                                                                                
EZDSC    MVC   EZWLDESC,WRKEZDSC   SET FILE DESCRIPTION                         
         GOTOR CDATAMGR,WRKIPARM,DMDESC,,,EZWLHDRD                              
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* GET WRKF/WRKZ FILE FOR A USER-ID                                    *         
***********************************************************************         
                                                                                
GETFIL   TM    WRKINDS,WRKIWFNQ    TEST CALLER SET WRKIFILE VALUE               
         BZ    *+8                 YES - DON'T DO ANYTHING                      
         CR    RE,RE                                                            
         BR    RE                                                               
                                                                                
         LA    RF,WRKF1            POINT TO FILE NAME FOR WRKF                  
         LA    R0,WRKINDX+(WKUKUSRINF-WKUKRECD)                                 
         CLI   WRKIFTYP,WRKIFTWF   TEST WRKF WORKER FILE                        
         BE    GETFIL02                                                         
                                                                                
         MVC   WKUKUSRID,WRKEZUID  MUST SET THIS FOR GFILE CALL                 
         LA    RF,WRKF1            POINT TO FILE NAME FOR EZ                    
         LA    R0,WRKINDX+(WKUKUSRINF-WKUKRECD)                                 
         CLI   WRKIFTYP,WRKIFTEZ   TEST WRKZ WORKER FILE                        
         BE    GETFIL02                                                         
         DC    H'0'                                                             
                                                                                
GETFIL02 OC    WRKIFILE,WRKIFILE   TEST FILE NAME SET                           
         BZ    GETFIL04                                                         
         CLC   WRKIFTYP,WRKIFTGF   TEST SAME FILE TYPE AS PREVIOUS              
         BNE   GETFIL04                                                         
         CLC   WRKIUIGF,WKUKUSRID  TEST SAME USER-ID AS PREVIOUS                
         BER   RE                                                               
                                                                                
GETFIL04 MVC   WRKIFTGF,WRKIFTYP   SET FILE TYPE                                
         MVC   WRKIUIGF,WKUKUSRID  SET SAME USER-ID AS PREVIOUS                 
         MVC   WRKIFILE,0(RF)      SET WORKER FILE NAME                         
         ST    R0,FULL             SAVE POINTER TO ACTUAL FILE NAME             
         OC    WRKIUIGF,WRKIUIGF   TEST USER-ID PROVIDED                        
         BNZ   GETFIL06                                                         
         MVI   WRKIERRS,WRKIEKEY   SET INVALID KEY & EXIT                       
         B     EXIT                                                             
                                                                                
GETFIL06 LR    R0,RE               SAVE RETURN ADDRESS FOR EXIT                 
         GOTOR CDATAMGR,WRKIPARM,DMGFILE                                        
         L     RE,FULL             POINT TO ACTUAL FILE NAME                    
         MVC   WRKIFILE,0(RE)      SET FILE NAME                                
         LTR   RE,R0               RESTORE RETURN ADDRESS                       
         BR    RE                                                               
                                                                                
***********************************************************************         
* ROUTINE TO DECIDE IF WRKF OR WRKZ USED FOR EASI WORKER FILES                  
***********************************************************************         
EZFORZ   ST    RE,SAVERE                                                        
*                                                                               
         CLI   WRKIFTYP,WRKIFTEZ   TEST EASI WORKER FILE                        
         JNE   EZWRKF              NO, IT'S WRKF                                
         CLI   WRKIFILE+4,C' '     PROTECTION CODE                              
         JNH   *+2                 DEATH MAKE SURE WE KNOW WHAT FILE            
         CLI   DSPACE,C' '         IS THE DATA SPACE SET                        
         JH    EZFORZ4             YES, NO NEED TO CALL DATAMGR FOR IT          
*                                                                               
         GOTOR CDATAMGR,PARMS,DMSSBAD                                           
         JNE   *+2                                                              
         ICM   R1,15,4(R1)         A(SSB) FROM PARAMETER 2                      
         JZ    *+2                                                              
         CLC   0(2,R1),NULLS       CHECK IF OFF-LINE                            
         JE    EZFORZ1             YES                                          
*                                                                               
         ST    R1,SAVER1                                                        
         GOTOR CPROTOFF            PROTECTION OFF SO WE CAN SAVE DSPACE         
         L     R1,SAVER1                                                        
         USING SSBD,R1                                                          
         MVC   DSPACE,SSBDSPAC     DATA SPACE                                   
         DROP  R1                                                               
         GOTOR CPROTON             PROTECTION BACK ON                           
         J     EZFORZ2                                                          
*                                                                               
         USING SSBOFFD,R1                                                       
EZFORZ1  CLI   SSOXTND,X'FF'       EXTENDED SSB                                 
         JNE   *+2                 MUST BE EXTENDED                             
         MVC   DSPACE,SSODSPAC     DATA SPACE                                   
         DROP  R1                                                               
*                                                                               
EZFORZ2  CLI   DSPACE,C' '         MUST HAVE DSPACE                             
         JNH   *+2                                                              
*                                                                               
EZFORZ4  LA    RF,WKZLIST          LIST OF FILES THAT USE WRKZ                  
EZFORZ5  CLI   0(RF),X'FF'         END OF LIST                                  
         JE    EZWRKF              USES WRKF FOR EASI                           
         CLC   DSPACE,0(RF)        MATCH ON DATA SPACE VALUE                    
         JNE   EZFORZ6                                                          
         CLI   1(RF),C'*'          WILDCARD MEANS ALL WORKER FILES              
         JE    EZWRKZ              USE WRKZ FOR EASI                            
         CLC   WRKIFILE+4(1),1(RF) MATCH ON WORKER FILE NUMBER                  
         JE    EZWRKZ                                                           
EZFORZ6  LA    RF,L'WKZLIST(,RF)   NO MATCH                                     
         J     EZFORZ5             TRY NEXT                                     
*                                                                               
EZWRKF   MVI   WRKIFILE+3,C'F'     USE WRKF FOR EASI                            
         L     RE,SAVERE                                                        
         CR    RE,RE               EXIT EQUAL                                   
         BR    RE                                                               
EZWRKZ   MVI   WRKIFILE+3,C'Z'     USE WRKZ FOR EASI                            
         L     RE,SAVERE                                                        
         LTR   RE,RE               EXIT NOT EQUAL                               
         BR    RE                                                               
*                                                                               
***********************************************************************         
* ROUTINE TO CLEAR THE WORKEY BUFFER AND INITIALIZE WRKISTAT          *         
***********************************************************************         
                                                                                
CLRBUF   STM   RE,R1,12(RD)                                                     
         L     R0,WRKIABUF                                                      
         LHI   R1,BUFFL                                                         
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
         MVI   WRKISTAT,0                                                       
CLRBUFX  LM    RE,R1,12(RD)                                                     
         BR    RE                                                               
                                                                                
***********************************************************************         
* EXIT BACK TO CALLER WITH CONDITION CODE SET (EQUAL=GOOD)            *         
***********************************************************************         
                                                                                
EXIT     CLI   WRKIERRS,0          SET CONDITION CODE FOR CALLER                
         MVC   WRKIAREC,SAVEAREC   RESTORE SAVED WRKIAREC VALUE                 
         XIT1  ,                                                                
         EJECT                                                                  
***********************************************************************         
* CONSTANTS ETC.                                                      *         
***********************************************************************         
                                                                                
BUFFL    EQU   14*1024             BUFFER LENGTH                                
         LTORG                                                                  
                                                                                
REC#     DC    C'REC#'                                                          
EOELAB   DC    C'*EOFERR*'                                                      
EOFLAB   DC    C'*EOFEOF*'                                                      
SOFLAB   DC    C'*SOFSOF*'                                                      
WRKF1    DC    C'WRKF1  '                                                       
WRKZ1    DC    C'WRKZ1  '                                                       
DMGFILE  DC    C'GFILE  '                                                       
DMGLIST  DC    C'GLIST  '                                                       
DMPRINT  DC    C'DMPRINT'                                                       
DMINDEX  DC    C'INDEX  '                                                       
DMREAD   DC    C'READ   '                                                       
DMWRITE  DC    C'WRITE  '                                                       
DMCLOSE  DC    C'CLOSE  '                                                       
DMCLOPUR DC    C'CLO/PUR'                                                       
DMCLOERR DC    C'CLO/ERR'                                                       
DMCOMM   DC    C'COMMENT'                                                       
DMDELETE DC    C'DELETE '                                                       
DMPURGE  DC    C'PURGE  '                                                       
DMKEEP   DC    C'KEEP   '                                                       
DMUNKEEP DC    C'UNKEEP '                                                       
DMRET    DC    C'RETAIN '                                                       
DMRANDOM DC    C'RANDOM '                                                       
DMDESC   DC    C'DESC   '                                                       
DMSSBAD  DC    C'SSBAD  '                                                       
                                                                                
MAXRET   DC    X'FFFF'                                                          
NULLS    DC    256X'00'                                                         
                                                                                
DSPACE   DC    C' '                ONCE SET, WILL NOT CHANGE                    
                                                                                
*----------------------------------------------------------------------         
* LIST OF WRKF FILES TO USE WRKZ FILE FOR EASI (DSPACE,FILE)                    
*----------------------------------------------------------------------         
WKZLIST  DS    0CL2                                                             
         DC    C'A1',C'R1'         ADV:REP:WRKF1 TO WRKZ1                       
         DC    C'A2',C'R2'         ADV:REP:WRKF2 TO WRKZ2                       
         DC    C'A3',C'R3'         ADV:REP:WRKF3 TO WRKZ3                       
         DC    C'A4',C'R4'         ADV:REP:WRKF4 TO WRKZ4                       
         DC    C'A5',C'R5'         ADV:REP:WRKF5 TO WRKZ5                       
         DC    C'A6',C'R6'         ADV:REP:WRKF6 TO WRKZ6                       
         DC    C'A7',C'R7'         ADV:REP:WRKF7 TO WRKZ7                       
         DC    C'A8',C'R8'         ADV:REP:WRKF8 TO WRKZ8                       
         DC    C'A9',C'R9'         ADV:REP:WRKF9 TO WRKZ9                       
         DC    C'AA',C'RA'         ADV:REP:WRKFA TO WRKZA                       
         DC    C'AB',C'RB'         ADV:REP:WRKFB TO WRKZB                       
         DC    C'AC',C'RC'         ADV:REP:WRKFC TO WRKZC                       
         DC    C'AD',C'RD'         ADV:REP:WRKFD TO WRKZD                       
         DC    C'AE',C'RE'         ADV:REP:WRKFE TO WRKZE                       
         DC    C'AF',C'RF'         ADV:REP:WRKFF TO WRKZF                       
         DC    C'Q3'               FQA:WRKF3 TO WRKZ3                           
         DC    C'C*'               CSC:ALL WRKF TO WRKZ                         
         DC    C'T*'               TST:ALL WRKF TO WRKZ                         
         DC    X'FF'               END OF TABLE                                 
                                                                                
***********************************************************************         
* LOCAL WORKING STORAGE                                                         
***********************************************************************         
WORKD    DSECT                                                                  
SAVER1   DS    F                                                                
SAVERE   DS    F                                                                
PARMS    DS    8F                                                               
FULL     DS    F                   FULLWORD WORK AREA                           
SAVEAREC DS    AL4                 SAVED WRKIAREC VALUE                         
HEADER   DS    XL256               HEADER RECORD FOR OPEN                       
WORK     DS    XL64                                                             
WORKL    EQU   *-WORKD                                                          
                                                                                
* DMWRKFK                                                                       
*PREFIX=WK                                                                      
       ++INCLUDE DMWRKFK                                                        
UKLAST   EQU   *                                                                
*PREFIX=                                                                        
                                                                                
* DMWRKFL                                                                       
*PREFIX=WK                                                                      
       ++INCLUDE DMWRKFL                                                        
*PREFIX=                                                                        
                                                                                
* DMWRKZK                                                                       
*PREFIX=EZ                                                                      
       ++INCLUDE DMWRKZK                                                        
UKLAST   EQU   *                                                                
*PREFIX=                                                                        
                                                                                
* DMWRKZL                                                                       
*PREFIX=EZ                                                                      
       ++INCLUDE DMWRKZL                                                        
*PREFIX=                                                                        
                                                                                
* WRKIOD                                                                        
WRKIOD   DSECT                                                                  
       ++INCLUDE DDWRKIOD                                                       
                                                                                
* DDCOMFACS                                                                     
       ++INCLUDE DDCOMFACS                                                      
                                                                                
* FAFACTS                                                                       
       ++INCLUDE FAFACTS                                                        
                                                                                
* FASSBOFF                                                                      
SSBOFFD  DSECT                                                                  
       ++INCLUDE FASSBOFF                                                       
                                                                                
* FASSB                                                                         
       ++INCLUDE FASSB                                                          
                                                                                
* DDMASTC                                                                       
       ++INCLUDE DDMASTC                                                        
                                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'024DDWRKIO   06/17/14'                                      
         END                                                                    
