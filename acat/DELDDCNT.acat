*          DATA SET DELDDCNT   AT LEVEL 033 AS OF 05/06/20                      
*PROCESS USING(WARN(15))                                                        
*CATALP DELDDCNT                                                                
         TITLE 'LDCOUNT - DEMOS - DIRECTORY REC TYPE COUNT/PRINT'               
*        PARAMS VIA R1                                                          
*        XL1   COUNT/PRINT  X'01'=COUNT  X'FF'=PRINT                            
*        AL3   A(RECORD)                                                        
*        AL4   A(ISNAME)                                                        
*        AL4   A(ISDTF#)           INTERNAL I/S FILE NUMBER                     
*        AL4   A(INDICATOR BITS)   XL1: X'04' = PRINT BY BOOK                   
*                                       X'10' = VSAM FILE (NOT DANDX)           
*                                                                               
         WXTRN XSORT               NOT NEEDED FOR VSAM                          
         WXTRN LDDEFN              NOT NEEDED FOR VSAM                          
         WXTRN DEMTABS             NOT NEEDED FOR DANDX                         
*                                                                               
         PRINT NOGEN                                                            
LDCOUNT  CSECT                                                                  
         NMOD1 0,DELDDCNT,R9                                                    
*                                                                               
         LR    R2,R1               R2=A(PARAMETER LIST)                         
*                                                                               
         L     RE,12(R2)           A(COUNT INDICATOR BITS)                      
         MVC   RPTINDS,0(RE)       SAVE INDICATOR BITS                          
*                                                                               
         BC    0,LD50              INITIALIZATION                               
         MVI   *-3,X'F0'           *** SELF-MODIFYING ***                       
*                                                                               
         IF (TM,RPTINDS,VSAM,Z)    IF THIS IS A DANDX FILE:                     
           L     RF,=V(LDDEFN)       V(LDDEFN) AVAILABLE VIA LMDELD             
           USING LDDEFND,RF                                                     
           MVC   VDEMTABS,LDEMTABS                                              
           MVC   VHEXOUT,LHEXOUT                                                
           DROP  RF                                                             
         ELSE ,                    FOR VSAM FILE:                               
           ICM   RE,15,=V(DEMTABS)   ENTRY POINT OF V(DEMTABS)                  
           JZ    *+2                 WE MUST HAVE THIS!                         
           L     RE,0(,RE)           V(DEMTABS)                                 
           ST    RE,VDEMTABS         SAVE V(DEMTABS)                            
           MVC   VHEXOUT,=V(HEXOUT)                                             
         ENDIF ,                                                                
*                                                                               
         L     R0,=A(ACCUMSMX)     MAXIMUM NUMBER OF ACCUMULATORS               
         MHI   R0,ACCUMSLQ         R0 = ACCUMULATOR TABLE SIZE                  
         STORAGE OBTAIN,LENGTH=(R0),BNDRY=PAGE                                  
         LTR   RF,RF                                                            
         JNZ   *+2                 STORAGE OBTAIN FAILURE ?!?                   
         ST    R1,AACCUMS          RETURNED A(ACCUMULATOR TABLE)                
*                                                                               
         GOTO1 VDEMTABS,DMCB,SPBOOKTB   CALL DEMTABS, AND...                    
         MVC   ABKTYPTB,0(R1)      SAVE A(BOOKTYPE TABLE)                       
         L     RF,4(R1)                                                         
         STH   RF,BKTYPLN          SAVE LENGTH OF TABLE ENTRY                   
*                                                                               
         TM    RPTINDS,VSAM        ARE WE PROCESSING A VSAM FILE?               
         BO    LD50                YES: THERE IS NO DANDX SPLIT TABLE           
*                                                                               
         LA    RF,ISFILTAB                                                      
LD20     CLI   0(RF),X'FF'         EOT?                                         
         JE    *+2                 ALL DIRECTORIES SHOULD BE SPLIT NOW!         
         L     RE,8(R2)            RE = A(INTERNAL DTF#)                        
         CLC   0(1,RF),0(RE)       MATCH ON ISFILE NUMBER?                      
         BE    *+12                YES                                          
         LA    RF,L'ISFILTAB(RF)                                                
         B     LD20                                                             
         MVC   ASPLITTB,4(RF)      A(SPLIT FILE TABLE)                          
*                                                                               
         EJECT                                                                  
LD50     DS    0H                                                               
         CLI   0(R2),X'FF'         TEST PRINT                                   
         BE    PRNTTOTS                                                         
*                                                                               
         SR    R3,R3                                                            
         ICM   R3,7,1(R2)          R3 = A(DIRECTORY RECORD)                     
         MVI   SAVEYEAR,0                                                       
*                                                                               
         XC    ACCREC,ACCREC                                                    
REC      USING ACCUMSD,ACCREC      BUILD BINSRCH RECORD IN "ACCREC"             
*                                                                               
         ZAP   REC.ACCREC#,=P'0'   INITIALIZE PACKED ACCUMULATORS               
         ZAP   REC.ACCREC#D,=P'0'                                               
*                                                                               
         CLC   =X'FFFFFF',0(R3)    TRAILER RECORD?                              
         BNE   LD110                                                            
         MVI   REC.ACCUMSD,X'FF'   FILL KEY WITH X'FF'S                         
         MVC   REC.ACCUMSD+1(ACCKEYLQ-1),REC.ACCUMSD                            
         B     LD600                                                            
*                                                                               
LD110    DS    0H                                                               
         MVC   REC.ACCFMS,0(R3)    SET FILE/MEDIA/SOURCE                        
*                                                                               
         IF (CLI,0(R3),EQ,MRCODEQU),AND,  MPA FILE?                             
            (CLI,1(R3),EQ,MRMEDEQU)                                             
           USING MRKEY,R3                                                       
           MVC   REC.ACCBOOK,MRMBOOK                                            
           DROP  R3                                                             
*                                                                               
         ELSEIF (CLI,0(R3),EQ,DFUECODQ)  FUSION UNIV. ESTIMATE PASSIVE?         
           USING DFUEKEY,R3                                                     
           MVC   REC.ACCBOOK,DFUEBOOK                                           
           XC    REC.ACCBOOK,=X'FFFF'                                           
           DROP  R3                                                             
*                                                                               
         ELSEIF (CLI,0(R3),EQ,DRCODEQU),OR,  DEMO RECORD? ("R")                 
                (CLI,0(R3),EQ,C'D')          RADIO DAYPART RECORD?              
           USING DRKEY,R3                                                       
           MVC   REC.ACCBOOK,DRBOOK                                             
           XC    REC.ACCBOOK,=X'FFFF'                                           
           IF (CLC,=C'UA',NE,DRMEDIA)                                           
             MVC   REC.ACCBTYP,DRBTYP                                           
           ENDIF ,                                                              
           DROP  R3                                                             
*                                                                               
         ELSEIF (CLI,0(R3),EQ,SBCODEQU)   STATION PASSIVE? ("S")                
           USING SBKEY,R3                                                       
           MVC   REC.ACCBOOK,SBBOOK                                             
           IF (CLC,=C'UA',NE,SBMEDIA)                                           
             MVC   REC.ACCBTYP,SBBTYP                                           
           ENDIF ,                                                              
           DROP  R3                                                             
*                                                                               
         ELSEIF (CLI,0(R3),EQ,DSECDEQU)   STATION EQUIVALENCE? ("E")            
           USING DSEKEY,R3                                                      
           SELECT CLI,DSEIND,EQ                                                 
             WHEN (DSEIBKLW)                                                    
               MVC   REC.ACCBOOK,DSEEFFBK                                       
               XC    REC.ACCBOOK,=X'FFFF'                                       
             WHEN (DSEIBKHI)                                                    
               MVC   REC.ACCBOOK,DSEBKEFF                                       
               XC    REC.ACCBOOK,=X'FFFF'                                       
             WHEN (DSEISTID)                                                    
               MVC   REC.ACCBOOK,DSESBK                                         
               XC    REC.ACCBOOK,=X'FFFF'                                       
           ENDSEL ,                                                             
           DROP  R3                                                             
*                                                                               
         ELSEIF (CLI,0(R3),EQ,C'U')      WE'RE NOT SURE WHAT THESE ARE          
           MVC   REC.ACCBOOK,5(R3)                                              
           XC    REC.ACCBOOK,=X'FFFF'                                           
*                                                                               
         ELSEIF (CLI,0(R3),EQ,BSCODEQU)  STATION/MARKET LIST PSV? ("M")         
           USING BSKEY,R3                                                       
           IF (CLI,BSIND,EQ,BSINDEQU)                                           
             MVC   REC.ACCBOOK,BSBOOK                                           
             XC    REC.ACCBOOK,=X'FFFF'                                         
             IF (CLC,=C'UA',NE,BSMEDIA)                                         
               MVC   REC.ACCBTYP,BSBTYP                                         
             ENDIF ,                                                            
           ELSE ,                                                               
             USING MLKEY,R3                                                     
             MVC   REC.ACCBOOK,MLBOOK                                           
             XC    REC.ACCBOOK,=X'FFFF'                                         
             IF (CLC,=C'UA',NE,MLMEDIA)                                         
               MVC   REC.ACCBTYP,MLBTYP                                         
             ENDIF ,                                                            
           ENDIF ,                                                              
           DROP  R3                                                             
*                                                                               
         ELSEIF (CLI,0(R3),EQ,CYCODEQU)  COUNTY COVERAGE? ("C")                 
           USING CYKEY,R3                                                       
           MVC   REC.ACCBOOK,CYBOOK                                             
           XC    REC.ACCBOOK,=X'FFFF'                                           
           DROP  R3                                                             
*                                                                               
         ELSEIF (CLI,0(R3),EQ,DRITBEQU)  INTAB RECORD? ("I")                    
           IF (CLI,2(R3),NE,C'N')        PROGRAM INDEX POINTER? ("ITN")         
             USING DRKEY,R3              NATIVE INTAB RECORD ("ITA")            
             MVC   REC.ACCBOOK,DRBOOK                                           
             DROP  R3                                                           
           ELSE ,                                                               
             USING PIKEY,R3                                                     
             MVC   REC.ACCBOOK,PIBOOK                                           
             XC    REC.ACCBOOK,=X'FFFF'                                         
             DROP  R3                                                           
           ENDIF ,                                                              
*                                                                               
         ELSEIF (CLI,0(R3),EQ,DSYCDEQU)  FUSION SUBSCRIBER BASE? ("B")          
           USING DSYKEY,R3                                                      
           MVC   REC.ACCBOOK,DSYBKEFF                                           
           XC    REC.ACCBOOK,=X'FFFF'                                           
           DROP  R3                                                             
*                                                                               
         ELSE ,                    UNKNOWN                                      
           MVI   UNKFLAG,C'Y'        WE HAVE AN UNKNOWN DIRECTORY TYPE!         
           B     LD620               WE CAN'T ACCUMULATE BY BOOK                
*                                   (BECAUSE WE DON'T KNOW WHERE IT IS)         
*                                                                               
         ENDIF ,                                                                
*                                                                               
         TM    RPTINDS,BOOKS       PRINT BOOK BREAKOUT?                         
         BO    LD510               YES                                          
         XC    REC.ACCBOOK,REC.ACCBOOK                                          
         MVI   REC.ACCBTYP,0                                                    
         B     LD620               COLLAPSE TOTALS TOGETHER BY F/M/S            
*                                                                               
LD510    DS    0H                                                               
         MVC   SAVEYEAR,REC.ACCBOOK SAVE THIS BOOK'S YEAR                       
         XC    REC.ACCBOOK,=X'FFFF' FORCE REVERSE CHRONOLOGICAL SORT            
*                                                                               
         IF (CLI,REC.ACCMED,EQ,C'W'),ORIF,  WEEKLIES HAVE WEEK NUMBER           
            (CLI,REC.ACCMED,EQ,C'O'),ORIF,  AS DO OVERNIGHTS                    
            (CLI,REC.ACCMED,EQ,C'C'),AND,   AND CANADIAN DEMOS...               
            (CLI,REC.ACCSRC,EQ,C'N'),OR,     OR CSI...                          
            (CLI,REC.ACCBTYP,EQ,C'W')        ...AND BOOKTYPE "W"                
           MVI   REC.ACCINTVL,ACCINTVL_WEEKLY  IT'S A WEEKLY BOOK               
         ELSE ,                                                                 
           MVI   REC.ACCINTVL,ACCINTVL_MONTHLY  ASSUME MONTHLY BOOK             
         ENDIF ,                                                                
*                                                                               
         DROP  REC                                                              
*                                                                               
LD600    DS    0H                                                               
         TM    18(R3),STATUS_DEL   IS RECORD MARKED FOR DELETION?               
         BZ    *+8                 NO                                           
         MVI   SAVEYEAR,0          DON'T CONSIDER THIS FOR YEARLY RANGE         
*                                                                               
LD620    DS    0H                                                               
         L     R4,AACCUMS          A(ACCUMULATOR TABLE)                         
         GOTO1 =V(BINSRCH),DMCB,(X'01',ACCREC),(R4),ACCUMSN,ACCUMSLQ,  +        
               ACCKEYLQ,A(ACCUMSMX)                                             
         MVC   ACCUMSN,DMCB+8      SAVE NUMBER OF RECS IN TABLE                 
         SR    R4,R4                                                            
         ICM   R4,7,1(R1)                                                       
         BNZ   *+6                 ACCUMS TABLE IS FULL                         
         DC    H'0'                                                             
*                                                                               
         USING ACCUMSD,R4                                                       
         LA    RE,ACCREC#          ASSUME REC ISN'T MARKED FOR DELETION         
         LA    RF,DIRTOT#                                                       
         TM    18(R3),STATUS_DEL   IS IT?                                       
         BZ    *+12                                                             
         LA    RE,ACCREC#D         YES                                          
         LA    RF,DIRTOT#D                                                      
*                                                                               
         AP    0(L'ACCREC#,RE),=P'1'  BUMP APPROPRIATE ACCUMULATORS             
         AP    0(L'DIRTOT#,RF),=P'1'                                            
         DROP  R4                                                               
*                                                                               
         TM    RPTINDS,VSAM        ARE WE PROCESSING A VSAM FILE?               
         BO    LDX900              YES: THERE IS NO DANDX SPLIT TABLE           
*                                                                               
         ICM   R6,15,ASPLITTB      FILE LIST FOR THIS DIRECTORY                 
         JZ    *+2                 ALL DIRECTORIES SHOULD BE SPLIT NOW!         
         USING SPLTFILD,R6                                                      
         LLC   R1,18(R3)           STATUS BYTE                                  
         NILF  GR1,X'003F'         FILE NUMBER IS IN LOW-ORDER 6 BITS           
         MHI   R1,SPLTFLNQ         DISPLACEMENT TO FILE TABLE ENTRY             
         AR    R6,R1               A(FILE TABLE ENTRY)                          
*                                                                               
         AP    SPLTREC#_KEEP,=P'1' INCREMENT TOTAL FOR THIS FILE                
         TM    18(R3),STATUS_DEL   IS KEY MARKED FOR DELETION?                  
         BZ    *+10                                                             
         AP    SPLTREC#_DEL,=P'1'  YES: INCREMENT TOTAL DELETES COUNTER         
*                                                                               
*                                  REMEMBER LOW/HIGH BOOKS FOR FILE             
         CLI   SAVEYEAR,0                                                       
         BE    LDX900                                                           
         CLC   SPLTLWYR,SAVEYEAR                                                
         BL    *+10                                                             
         MVC   SPLTLWYR,SAVEYEAR                                                
         CLC   SPLTHIYR,SAVEYEAR                                                
         BH    *+10                                                             
         MVC   SPLTHIYR,SAVEYEAR                                                
*                                                                               
         DROP  R6                                                               
*                                                                               
LDX900   DS    0H                                                               
         B     XMOD1                                                            
         EJECT                                                                  
PRNTTOTS DS    0H                                                               
         L     RA,=V(CPRINT)                                                    
         USING DPRINT,RA                                                        
*                                                                               
         TM    RPTINDS,VSAM        ARE WE PROCESSING A VSAM FILE?               
         BO    PT60                YES: THERE IS NO DANDX SPLIT TABLE           
*                                                                               
         ICM   R4,15,ASPLITTB      FILE LIST FOR THIS DIRECTORY                 
         JZ    *+2                 ALL DIRECTORIES SHOULD BE SPLIT NOW!         
*                                                                               
* BEFORE PRINTING THE TABLE, SORT IT SUCH THAT THE TEST FILE IS ALWAYS          
* LAST IN THE LIST. NOTE: THE TABLE MAY NO LONGER BE INDEXED AFTER THE          
* SORT!                                                                         
*                                                                               
         SR    R0,R0               INIT ENTRY COUNTER                           
         LR    R1,R4               A(TABLE)                                     
         DO UNTIL=(CLI,0(R1),EQ,X'FF')                                          
           AHI   R0,1                INCREMENT NUMBER OF TABLE ENTRIES          
           LA    R1,SPLTFLNQ(,R1)    BUMP TO NEXT TABLE ENTRY                   
         ENDDO ,                   STOP AT EOT                                  
*                                                                               
         GOTO1 =V(XSORT),DMCB,(R4),(R0),SPLTFLNQ,L'SPLTSORT,           +        
               SPLTSORT-SPLTFILD                                                
*                                                                               
         MVC   TITLE,=CL60'DEMFILE RECORD COUNTS BY SPLIT FILENAME'             
         MVC   MID1,SPACES                                                      
         MVC   MID2,SPACES                                                      
         MVC   MID3,SPACES                                                      
         MVC   MID4,SPACES                                                      
         MVC   SUB1(L'MYSUB1F),MYSUB1F                                          
         MVC   SUB2(L'MYSUB2F),MYSUB2F                                          
         ZAP   LINE,=P'99'                                                      
*                                                                               
         USING SPLTFILD,R4                                                      
PT30     CLI   0(R4),X'FF'         EOT?                                         
         BE    PT50                YES                                          
*                                                                               
         CLC   SPLTFLNM,=C'???????'  IS THIS ENTRY JUST A FILLER?               
         BE    PT45                  YES: SKIP IT                               
*                                                                               
         MVC   PRTFILNM,SPLTFLNM   FILE NAME                                    
*                                                                               
         EDIT  SPLTREC#_KEEP,PRT#REC_KEEP,COMMAS=YES,ZERO=NOBLANK               
         EDIT  SPLTREC#_DEL,PRT#REC_DEL,COMMAS=YES,ZERO=NOBLANK                 
*                                                                               
         CLI   SPLTLWYR,X'FF'      WERE YEAR FIELDS UPDATED?                    
         BE    PT40                NO                                           
         MVC   FULL(1),SPLTLWYR    LOWEST YEAR ON FILE                          
         MVC   FULL+1(2),=X'0101'  ANY MONTH/DAY TO FAKE OUT DATCON             
         GOTO1 =V(DATCON),DMCB,(3,FULL),(20,DUB)                                
         MVC   PRT1STYR,DUB                                                     
         MVC   FULL(1),SPLTHIYR    LAST YEAR ON FILE                            
         MVC   FULL+1(2),=X'0101'  ANY DAY TO FAKE OUT DATCON                   
         GOTO1 =V(DATCON),DMCB,(3,FULL),(20,DUB)                                
         MVC   PRTLSTYR,DUB                                                     
*                                                                               
PT40     DS    0H                                                               
         IF (CLI,SPLTFIL#,EQ,TESTFILE)                                          
           MVC  PRTTEST,=C'**TESTFILE**'                                        
         ENDIF                                                                  
*                                                                               
         GOTO1 =V(PRINTER)                                                      
*                                                                               
PT45     DS    0H                                                               
         LA    R4,SPLTFLNQ(R4)     BUMP TO NEXT SPLIT FILE TABLE ENTRY          
         B     PT30                                                             
         DROP  R4                                                               
*                                                                               
PT50     DS    0H                                                               
         MVC   P(L'MYSUB2F),MYSUB2F                                             
         GOTO1 =V(PRINTER)                                                      
         MVC   PRTFILNM,=C'*TOTAL*'    PRINT TOTAL                              
         EDIT  DIRTOT#,PRT#REC_KEEP,COMMAS=YES,ZERO=NOBLANK                     
         EDIT  DIRTOT#D,PRT#REC_DEL,COMMAS=YES,ZERO=NOBLANK                     
         GOTO1 =V(PRINTER)                                                      
*                                                                               
PT60     DS    0H                                                               
         ICM   R4,15,ACCUMSN                                                    
         BZ    XMOD1                                                            
*                                                                               
         IF (TM,RPTINDS,VSAM,Z)    IF THIS IS A DANDX FILE:                     
           MVC   TITLE,=CL60'DEMFILE RECORD COUNTS BY BOOK'                     
         ENDIF ,                                                                
         MVC   MID1,SPACES                                                      
         MVC   MID2,SPACES                                                      
         MVC   MID3,SPACES                                                      
         MVC   MID4,SPACES                                                      
         MVC   SUB1(L'MYSUB1),MYSUB1                                            
         MVC   SUB2(L'MYSUB2),MYSUB2                                            
         ZAP   LINE,=P'99'                                                      
*                                                                               
         L     R5,AACCUMS          A(ACCUMULATOR TABLE)                         
         USING ACCUMSD,R5                                                       
*                                                                               
PT70     DS    0H                                                               
         LA    R1,MEDSRC           MEDIA/SOURCE TABLE FOR DEMDIRN/A/R           
         USING MEDSRCD,R1                                                       
PT80     CLI   MEDMEDCD,C'X'       UNKNOWN?                                     
         BE    PT90                YES                                          
         CLC   MEDMEDCD,ACCMED     MATCH ON MEDIA?                              
         BNE   *+14                NO                                           
         CLC   MEDSRCCD,ACCSRC     MATCH ON SOURCE?                             
         BE    PT90                YES                                          
         LA    R1,MEDSRCLQ(R1)                                                  
         B     PT80                                                             
*                                                                               
PT90     DS    0H                                                               
         MVC   PRTFILCD,ACCFIL                                                  
         MVC   PRTMEDCD,ACCMED                                                  
         MVC   PRTSRCCD,ACCSRC                                                  
         MVC   PRTMEDIA,MEDMEDIA                                                
         MVC   PRTSRC,MEDSOURC                                                  
         DROP  R1                                                               
*                                                                               
         XC    ACCBOOK,=X'FFFF'    RESTORE ORIGINAL INTERNAL BOOK VALUE         
*                                                                               
         CLC   ACCFMS,=X'FFFFFF'   IS THIS A TRAILER RECORD?                    
         BE    PT180               YES                                          
*                                                                               
         CLC   ACCBOOK,=X'FFFF'    ANY BOOK VALUE PRESENT?                      
         BE    PT150               NO                                           
*                                                                               
         LLC   R1,ACCBOOK+1                                                     
         CLI   ACCINTVL,ACCINTVL_MONTHLY   MONTHLY BOOK?                        
         BE    PT130               YES                                          
         CVD   R1,DUB              WEEK NUMBER                                  
         OI    DUB+7,X'0F'                                                      
         UNPK  PRTBOOK+1(2),DUB                                                 
         CLC   PRTBOOK+1(2),=C'00'                                              
         BNE   *+10                                                             
         MVC   PRTBOOK+1(2),=C'**'                                              
         B     PT140                                                            
*                                                                               
PT130    MHI   R1,3                MONTHLY BOOK                                 
         LA    R1,MONTHS-3(R1)                                                  
         MVC   PRTBOOK(3),0(R1)                                                 
*                                                                               
PT140    MVI   PRTBOOK+3,C'/'                                                   
         LLC   R1,ACCBOOK                                                       
         CVD   R1,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  PRTBOOK+4(2),DUB                                                 
*                                                                               
PT150    DS    0H                                                               
         CLI   ACCBTYP,0           NOW CHECK FOR A BOOK TYPE                    
         BNE   *+18                                                             
         MVI   ACCBTYP,C' '        STANDARD BOOKTYPE                            
         MVC   PRTBKTYP,SPACES                                                  
         B     PT180                                                            
*                                                                               
         CLI   ACCBTYP,X'E0'       SPILL?                                       
         BNE   *+14                                                             
         MVC   PRTSPILL,=C'SPILL'                                               
         B     PT180                                                            
*                                                                               
         L     RF,ABKTYPTB         BOOKTYPE TRANSLATION TABLE                   
         USING SPBKTYPD,RF                                                      
PT160    CLI   0(RF),X'FF'         UNKNOWN?                                     
         BE    PT170               YES                                          
         CLC   SPBKTYPN,ACCBTYP    MATCH ON INTERNAL BOOKTYPE?                  
         BE    *+12                                                             
         AH    RF,BKTYPLN          LENGTH OF TABLE ENTRY                        
         B     PT160                                                            
*                                                                               
         MVC   PRTBKTYP,SPBKTYPA   2-CHAR ALPHA BOOKTYPE                        
         B     PT180                                                            
         DROP  RF                                                               
*                                                                               
PT170    DS    0H                                                               
         MVC   PRTBKTYP(5),=C'X''  '''  PRINT UNKNOWNS IN HEX                   
         GOTO1 VHEXOUT,DMCB,ACCBTYP,PRTBKTYP+2,1,=C'TOG'                        
         CLC   =F'2',DMCB+16                                                    
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
PT180    DS    0H                                                               
         EDIT  ACCREC#,PRT#RECS,COMMAS=YES,ZERO=NOBLANK                         
         EDIT  ACCREC#D,PRT#RECD,COMMAS=YES                                     
*                                                                               
         GOTO1 =V(PRINTER)                                                      
*                                                                               
         AHI   R5,ACCUMSLQ         BUMP TO NEXT ACCUMULATOR ROW                 
         BCT   R4,PT70                                                          
         DROP  R5                                                               
*                                                                               
         MVC   P(L'MYSUB2),MYSUB2                                               
         GOTO1 =V(PRINTER)                                                      
*                                                                               
         MVC   P(7),=C'*TOTALS'    PRINT TOTALS                                 
         LA    R5,P+(PRT#RECS-DPRINT)  TOTAL NOT DELETED                        
         EDIT  DIRTOT#,(L'PRT#RECS,(R5)),COMMAS=YES,ZERO=NOBLANK                
         LA    R5,P+(PRT#RECD-DPRINT)  TOTAL MARKED DELETED                     
         EDIT  DIRTOT#D,(L'PRT#RECS,(R5)),COMMAS=YES,ZERO=NOBLANK               
         GOTO1 =V(PRINTER)                                                      
*                                                                               
         AP    DIRTOT#,DIRTOT#D    COMPUTE GRAND TOTAL                          
         MVC   P(17),=C'** GRAND TOTAL **'                                      
         LA    R5,P+(PRT#RECS-DPRINT)  TOTAL                                    
         EDIT  DIRTOT#,(L'PRT#RECS,(R5)),COMMAS=YES,ZERO=NOBLANK                
         GOTO1 =V(PRINTER)                                                      
*                                                                               
         IF (CLI,UNKFLAG,EQ,C'Y')  IF ANY UNKNOWN RECORDS SEEN:                 
           LA    R4,WARNMS1L         A(L'MESSAGE)                               
           WTO   TEXT=(R4)           SEND AUTONOTE                              
         ENDIF ,                                                                
*                                                                               
         MVC   SUB1,SPACES                                                      
         MVC   SUB2,SPACES                                                      
*                                                                               
XMOD1    DS    0H                                                               
         XMOD1                                                                  
         EJECT                                                                  
         LTORG                                                                  
         SPACE 2                                                                
DUB      DS    D                                                                
FULL     DS    F                                                                
DMCB     DS    6F                                                               
WORK     DS    XL24                                                             
BYTE     DS    X                                                                
SAVEYEAR DS    X                                                                
RPTINDS  DS    X                                                                
UNKFLAG  DC    C'N'                ASSUME NO UNKNOWNS                           
VDEMTABS DC    A(0)                A(DEMTABS)                                   
VHEXOUT  DC    A(0)                A(HEXOUT)                                    
*                                                                               
* THESE BINSRCH COUNTERS ARE DELIBERATELY A-TYPES                               
ACCUMSN  DC    A(0)                ACCUMULATOR TABLE COUNTER                    
*                                                                               
ACCREC   DS    XL(ACCUMSLQ)                                                     
ABKTYPTB DS    A                   A(BOOKTYPE TRANSLATION TABLE)                
BKTYPLN  DS    H                   LENGTH OF BOOKTYPE TABLE ENTRY               
         SPACE 2                                                                
MYSUB1F  DC    C'NEWDEMO=         RECORDS           DELETES       STRT +        
                 LAST'                                                          
MYSUB2F  DC    C'--------     --------------    --------------    ---- +        
                 ----'                                                          
*                                                                               
MYSUB1   DC    C' BOOK  FMS   MEDIA     SOURCE  BKTYPE   # MAJOR KEYS  +        
                MARKED DELETED'                                                 
MYSUB2   DC    C'------ ---   -----     ------  ------  -------------- +        
                --------------'                                                 
         SPACE 2                                                                
WARNMS1L DC    AL2(WARNMS1Q)                                                    
WARNMSG1 DC    C'AUTONOTE*US-MFDEMOSPROGRAMMERS:** WARNING ** UNKNOWN R+        
               ECORD TYPE(S) SEEN IN DELDDCNT'                                  
WARNMS1Q EQU   *-WARNMSG1                                                       
         SPACE 2                                                                
MEDSRC   DS    0C                                                               
         DC    C'PA',C'USA/MPA ',C'??MPA???'                                    
         DC    C'PN',C'USA/MPA ',C'??MPA???'                                    
         DC    C'TA',C'INTAB   ',C'NIELSEN '                                    
         DC    C'TN',C'USA/TP  ',C'NIELSEN '                                    
         DC    C'TC',C'NCM     ',C'NIELSEN '                                    
*********DC    C'TS',C'USA/TP  ',C'SQAD    '  THESE ARE STORED AS "TN"!         
         DC    C'WN',C'USA/TP  ',C'NSI/WKLY'                                    
         DC    C'ON',C'USA/TP  ',C'NSI/ONIT'                                    
         DC    C'RA',C'US/RADIO',C'ARBITRON'                                    
         DC    C'RT',C'US/RADIO',C'TRITON  '                                    
         DC    C'RR',C'US/RADIO',C'RADAR   '                                    
         DC    C'RS',C'US/RADIO',C'SQAD    '                                    
         DC    C'RM',C'CAN/RAD ',C'BBM     '                                    
         DC    C'CA',C'CAN/TP  ',C'BBM     '                                    
         DC    C'CN',C'CAN/TP  ',C'CSI     '                                    
         DC    C'DN',C'USA/DPT ',C'NIELSEN '                                    
         DC    C'TF',C'USA/TP  ',C'FUSION  '                                    
         DC    C'UA',C'COUNTY  ',C'ARBITRON'                                    
         DC    C'UN',C'COUNTY  ',C'NIELSEN '                                    
         DC    X'FFFF',C'*HDR/TLR',C'        '                                  
         DC    C'XX',C'????????',C'????????'                                    
*                                                                               
         DS    0D                                                               
ISFILTAB DS    0XL8                                                             
         DC    AL1(DEMDIRN#),AL3(0),A(DEMDIRN)                                  
         DC    AL1(DEMDIRA#),AL3(0),A(DEMDIRA)                                  
         DC    AL1(DEMDIRR#),AL3(0),A(DEMDIRR)                                  
         DC    X'FF'                                                            
*                                                                               
* NOTE: THESE TABLES ARE INDEXED BY INTERNAL FILE NUMBER (1-BASED).             
*       A FILE NUMBER OF ZERO INDICATES THAT THE DIRECTORY RECORD IS            
*       NOT ASSOCIATED WITH A FILE (I.E., IT IS EITHER A HEADER,                
*       TRAILER, OR PASSIVE).                                                   
*                                                                               
*       EACH TABLE MUST HAVE ENOUGH FILLER ENTRIES TO PERMIT INDEXING           
*       UP TO LOGICAL FILE #31 (THE TEST DEMOFILE).                             
*                                                                               
*       IMPORTANT: THE TABLE UNDERGOES AN IN-PLACE SORT AT LABEL                
*       PRNTTOTS. AFTER THAT, THE TABLE MAY NO LONGER BE INDEXED!!!             
*                                                                               
DEMDIRN  DS    0D                                                               
 DC AL1(00),CL7'N/A    ',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(01),CL7'DEMFILO',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(02),CL7'DEMFILN',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(03),CL7'DEMFILP',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(04),CL7'DEMFILQ',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(05),CL7'DEMFILU',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(06),CL7'DEMFILV',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(07),CL7'DEMFILW',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(08),CL7'DEMFILY',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(09),CL7'DEMFILI',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(10),CL7'DEMFILJ',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(11),CL7'DEMFILK',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(12),CL7'DEMFILL',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(13),CL7'DEMFILM',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(14),CL7'DEMFILS',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(15),CL7'DEMFILZ',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(16),CL7'DEMFILB',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(17),CL7'DEMFILC',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(18),CL7'DEMFILD',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(19),CL7'DEMFILE',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(20),CL7'DEMFILF',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(21),CL7'DEMFILG',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(22),CL7'DEMFILH',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(23),CL7'DEMFIL3',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(24),CL7'DEMFIL5',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(25),CL7'DEMFIL7',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(26),CL7'DEMFIL9',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(27),CL7'DEMFIL1',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(28),CL7'DEMFIL6',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(29),CL7'DEMFIL8',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(30),CL7'DEMFIL$',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(TESTFILE),CL7'DEMFLTN',2PL6'0',X'FF',X'00',X'FF'                        
 DC AL1(32),CL7'DEMFLNA',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(33),CL7'DEMFLNB',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(34),CL7'DEMFLNC',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(35),CL7'DEMFLND',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(36),CL7'DEMFLNE',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(37),CL7'DEMFLNF',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(38),CL7'DEMFLNG',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(39),CL7'DEMFLNH',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(40),CL7'DEMFLNI',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(41),CL7'DEMFLNJ',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(42),CL7'DEMFLNK',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(43),CL7'DEMFLNL',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(44),CL7'DEMFLNM',2PL6'0',X'FF',X'00',X'00'                              
 DC X'FF'                          EOT                                          
*                                                                               
DEMDIRA  DS    0D                                                               
 DC AL1(00),CL7'N/A    ',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(01),CL7'DEMFILA',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(02),CL7'DEMFIL2',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(03),CL7'DEMFIL4',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(04),CL7'DEMFILT',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(05),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(06),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(07),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(08),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(09),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(10),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(11),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(12),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(13),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(14),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(15),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(16),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(17),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(18),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(19),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(20),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(21),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(22),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(23),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(24),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(25),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(26),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(27),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(28),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(29),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(30),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(TESTFILE),CL7'DEMFLTA',2PL6'0',X'FF',X'00',X'FF'                        
 DC X'FF'                          EOT                                          
*                                                                               
DEMDIRR  DS    0D                                                               
 DC AL1(00),CL7'N/A    ',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(01),CL7'DEMFILR',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(02),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(03),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(04),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(05),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(06),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(07),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(08),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(09),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(10),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(11),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(12),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(13),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(14),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(15),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(16),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(17),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(18),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(19),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(20),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(21),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(22),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(23),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(24),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(25),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(26),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(27),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(28),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(29),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(30),CL7'???????',2PL6'0',X'FF',X'00',X'00'                              
 DC AL1(TESTFILE),CL7'DEMFLTR',2PL6'0',X'FF',X'00',X'FF'                        
 DC X'FF'                          EOT                                          
*                                                                               
TESTFILE EQU   31                  ALL TEST FILES ARE LOGICAL FILE #31          
ASPLITTB DC    A(0)                A(SPLIT FILE TABLE)                          
AACCUMS  DC    A(0)                A(ACCUMULATOR TABLE)                         
ACCUMSMX EQU   50000               MAXIMUM NUMBER OF ACCUMULATORS               
*                                                                               
DIRTOT#  DC    PL6'0'              TOTAL NO. OF "KNOWN" DIRECTORY RECS          
DIRTOT#D DC    PL6'0'              TOTAL NO. MARKED FOR DELETION                
*                                                                               
DEMDIRA# EQU   X'2F'                                                            
DEMDIRN# EQU   X'2D'                                                            
DEMDIRR# EQU   X'30'                                                            
*                                                                               
STATUS_DEL EQU X'80'               MARKED FOR DELETION                          
*                                                                               
VSAM     EQU   X'10'               VSAM FILE (NOT DANDX)                        
BOOKS    EQU   X'04'               PRINT BY BOOK                                
*                                                                               
         EJECT                                                                  
ACCUMSD  DSECT                                                                  
ACCINTVL DS    C                   BOOK INTERVAL                                
ACCINTVL_MONTHLY EQU C'M'           MONTHLY BOOK                                
ACCINTVL_WEEKLY  EQU C'W'           WEEKLY BOOK                                 
ACCBOOK  DS    XL2                 ACTUAL BOOK                                  
ACCFMS   DS    0CL3                F/M/S                                        
ACCFIL   DS    C                   FILE CODE                                    
ACCMED   DS    C                   MEDIA CODE                                   
ACCSRC   DS    C                   SOURCE CODE                                  
ACCBTYP  DS    C                   BOOK TYPE                                    
ACCKEYLQ EQU   *-ACCUMSD                                                        
ACCREC#  DS    PL6                 # OF DIRECTORY RECORDS                       
ACCREC#D DS    PL6                 # OF DIR. RECS MARKED FOR DELETION           
ACCUMSLQ EQU   *-ACCUMSD                                                        
         SPACE 2                                                                
MEDSRCD  DSECT                                                                  
MEDMEDCD DS    C                   MEDIA CODE                                   
MEDSRCCD DS    C                   SOURCE CODE                                  
MEDMEDIA DS    CL8                 MEDIA NAME                                   
MEDSOURC DS    CL8                 SOURCE NAME                                  
MEDSRCLQ EQU   *-MEDSRCD                                                        
         SPACE 2                                                                
SPLTFILD DSECT                                                                  
SPLTFIL# DS    AL1                 SPLIT FILE: LOGICAL FILENUM                  
SPLTFLNM DS    CL7                 FILENAME                                     
SPLTREC#_KEEP DS PL6               RECORD COUNT (KEEPS)                         
SPLTREC#_DEL  DS PL6               RECORD COUNT (DELETES)                       
SPLTLWYR DS    X                   EARLIEST BOOK ON FILE (BINARY YEAR)          
SPLTHIYR DS    X                   LATEST BOOK ON FILE (BINARY YEAR)            
SPLTSORT DS    X                   FOR SORT BEFORE PRINTING                     
SPLTFLNQ EQU   *-SPLTFILD                                                       
         EJECT                                                                  
* DDDPRINT                                                                      
* DEDEMFILE                                                                     
* DEMPAFILE                                                                     
* DDMONYREQU                                                                    
* DEMLDDEFN                                                                     
* DEDEMTABD                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDDPRINT                                                       
         EJECT                                                                  
       ++INCLUDE DEDEMFILE                                                      
         EJECT                                                                  
       ++INCLUDE DEMPAFILE                                                      
         EJECT                                                                  
       ++INCLUDE DDMONYREQU                                                     
         EJECT                                                                  
       ++INCLUDE DEMLDDEFN                                                      
         EJECT                                                                  
       ++INCLUDE DEDEMTABD                                                      
         EJECT                                                                  
         PRINT ON                                                               
DPRINT   DSECT                                                                  
         ORG   P                                                                
PRTFILNM DS    CL7                                                              
         DS    CL6                                                              
PRT#REC_KEEP DS CL14                                                            
         DS    CL4                                                              
PRT#REC_DEL  DS CL14                                                            
         DS    CL4                                                              
PRT1STYR DS    CL4                                                              
         DS    CL3                                                              
PRTLSTYR DS    CL4                                                              
         DS    CL2                                                              
PRTTEST  DS    C'**TESTFILE**'                                                  
         ORG                                                                    
         SPACE 3                                                                
         ORG   P                                                                
PRTBOOK  DS    CL6                                                              
         DS    C                                                                
PRTFILCD DS    C                                                                
PRTMEDCD DS    C                                                                
PRTSRCCD DS    C                                                                
         DS    CL3                                                              
PRTMEDIA DS    CL8                                                              
         DS    CL2                                                              
PRTSRC   DS    CL8                                                              
         DS    CL2                                                              
PRTBKTYP DS    CL2                                                              
         DS    CL3                                                              
         ORG   PRTBKTYP                                                         
PRTSPILL DS    CL5                                                              
         DS    C                                                                
PRT#RECS DS    CL14                                                             
         DS    CL2                                                              
PRT#RECD DS    CL14                                                             
         ORG                                                                    
         SPACE 3                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'033DELDDCNT  05/06/20'                                      
         END                                                                    
