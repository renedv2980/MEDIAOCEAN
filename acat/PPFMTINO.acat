*          DATA SET PPFMTINO   AT LEVEL 021 AS OF 08/19/13                      
*CATALP PPFMTINO                                                                
         TITLE 'PPFMTINO - FORMAT PRINT INVOICE NUMBERS'                        
         SPACE 1                                                                
***********************************************************************         
*                                                                               
*   CHANGE LOG                                                                  
*                                                                               
*   BPLA   12/11  ADJUST TO PROPERLY HANDLE B1PROF+4 VALUE 3                    
*                 WHEN WORKING WITH A BILLING HEADER                            
*                                                                               
*   BPLA   2/08   ACCEPT NEW PARAMETER A(BILLREC)                               
*                 AND USE FORMATTING INFO STORED IN IT                          
*                 TO DISPLAY INVOICE #                                          
*                 'B' IN FIRST BYTE OF FIRST PARAMETER                          
*                 INDICATES A(BILLREC) IS IN PARAMETER 5                        
*                                                                               
*   BPLA   3/01   MORE CHANGES TO ACCOMMODATE MORE O&M                          
*                 B1X PROFILE SWITCHES                                          
*                 CONCERNS SPECIAL 'X' VALUE IN B1XPROF+6                       
*                                                                               
*                 O&M BILLING MONTH FORMATS:                                    
*                 UNTIL MAR/99   01-12                                          
*                 APR/99-DEC99   YM FORMAT                                      
*                 JAN/00-DEC/00  MY FORMAT                                      
*                 JAN/01         BASE YEAR FORMAT (1994)                        
*                                                                               
*   BPLA   1/00   MORE 2YK FIXES                                                
*                                                                               
*   BPLA   12/99  NEW B1XPROF+6 OPTION THAT SWITCHES YM TO MY                   
*                 WHEN B1XPROF+12(2) YM MODE START YYMM                         
*                 IS USED                                                       
*                 NOTE THAT IF USED WITH B1 PROFILE +5                          
*                 OPTION 3 FORMAT WILL BE MMY INSTEAD OF YMM                    
*                                                                               
*                 B1XPROF+6  Y = MY FORMAT INSTEAD OF YM                        
*                            X = IF INVOICE DATE IS BEFORE                      
*                                YM FORMAT START DATE (B1XPROF+14(2)            
*                                FORMAT MONTH AS YM                             
*                                UNLESS THE BILL DATE IS MAR/99                 
*                                OR BEFORE IN WHICH CASE JUST                   
*                                DISPLAY MM                                     
***                          X IS A SPECIAL VALUE FOR O&M                       
***                            TO HANDLE THEIR B1X PROFILE SWITCHING            
*                                                                               
*   BPLA   4/98   CHANGES FOR Y2K AND SPECIAL Y/M FORMAT (YMM)                  
*                 NEW VALUE FOR B1PROF+4                                        
*                 MUST BE USED WITH B1XPROF+14(2) YM MODE START YYMM            
*                 3 = MED-YMM  EXAMPLE:  N-8010001                              
*                     8  = LAST DIGIT OF YEAR                                   
*                     01 = JANUARY                                              
*                   0001 = INVOICE NUMBER                                       
*                   THIS AFFECTS BOTH FULL AND SHORT OUTPUT FORMATS             
*                                                                               
*   INPUT                                                                       
*   -----                                                                       
*                                                                               
*   P1 BYTE   0    B MEANS A(BILLREC) IS IN P5                                  
*      BYTES  1-3  A(DATE)      YYMMDD (TODAY OR DATE OF BILL)                  
*                                                                               
*   P2 BYTE   0    2 MEANS INV NO. IS XL2                                       
*      BYTES  1-3  A(INVOICE NUBMER)  CL4 OR XL2                                
*                                                                               
*   P3 BYTE   0    CL1 - MEDIA CODE                                             
*      BYTES  1-3  A(B1 PROFILE)                                                
*                                                                               
*   P4 BYTE   0    NOT USED                                                     
*      BYTES  1-3  A(B1X PROFILE)                                               
*                                                                               
*   P5 BYTE   0    NOT USED                                                     
*      BYTES  1-3  A(BILLREC)  IF B IN FIRST BYTE OF P1                         
*                                                                               
*   OUTPUT   NOTE- PPFMTINO SETS THE ADDRESSES                                  
*   ------                                                                      
*                                                                               
*   P1 BYTE  0    NOT USED                                                      
*      BYTES 1-3  A(FULL INVOICE NUMBER) - ME-MN-NNNN OR MN-ME-NNNN             
*                                                                               
*   P2 BYTE  0    NOT USED                                                      
*      BYTES 1-3  A(SHORT INV NO.) - MN-NNNN                                    
*                                                                               
*   P3 BYTE  0    NOT USED                                                      
*      BYTES 1-3  A(MONTH OR YEAR/MONTH) - 2 CHARACTERS                         
*                                                                               
*   P4 BYTE  0    NOT USED                                                      
*      BYTES 1-3  A(MEDIA) -2 CHARACTERS                                        
*                                                                               
***********************************************************************         
         SPACE 2                                                                
PPFMTINO CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKDL,**FMTI**                                                  
*                                                                               
         USING WORKD,RC                                                         
         ST    R1,SAVR1                                                         
         MVC   PARMS(24),0(R1)                                                  
         L     RF,PARM3                                                         
         MVC   B1PROF,0(RF)        SAVE B1 PROFILE                              
*                                                                               
*                                  FORMAT INVOICE NUMBER                        
FI00     DS    0H                                                               
         MVC   ILONG,=CL10' '      SET OUTPUT IN ILONG                          
*                                                                               
         CLI   PARM1,C'B'          DO I HAVE A(BILLREC) ?                       
         BE    FIBILL                                                           
*                                                                               
FI00A    LA    RF,B1PROF           B1 PROF                                      
         MVC   ILONG(2),2(RF)      MEDIA EXPANSION                              
*                                  *=BLANK, **=USE MEDIA CODE                   
         CLI   ILONG+0,C'*'                                                     
         BNE   *+8                                                              
         MVI   ILONG+0,C' '                                                     
         CLI   ILONG+1,C'*'                                                     
         BNE   *+8                                                              
         MVI   ILONG+1,C' '                                                     
         CLC   ILONG+0(2),=C'  '                                                
         BNE   *+10                                                             
         MVC   ILONG+0(1),PARM3                                                 
         MVC   INOMED,ILONG        SAVE MEDIA EXP                               
         MVI   ILONG+2,C'-'                                                     
*                                                                               
*  NOTE- I COULD GET TO FI00C FROM FIBILL WHEN VALUE 3 IS IN                    
*        BINVSEQ                                                                
*                                                                               
FI00C    L     R2,PARM1            A(DATE)                                      
         MVC   MYDATE,0(R2)        SAVE DATE OF BILL (OR TODAY)                 
*                                                                               
         MVC   ILONG+3(2),MYDATE+2 MONTH                                        
*                                                                               
*                                                                               
*Y2K*                                                                           
         MVI   WORK,C'0'     SET 3 DIGIT YEAR IN WORK (FOR PACKING)             
         MVC   WORK+1(2),MYDATE                                                 
         CLI   MYDATE,X'F9'       CHECK FOR SPECIAL Y2K YEARS (FA-FF)           
         BNH   FI01P              NO - PROCESS NORMALLY                         
         LA    RE,YRTAB                                                         
FI01B    CLC   0(3,RE),=X'FFFFFF'                                               
         BNE   *+6                                                              
         DC    H'0'               BAD YEAR - MUST BE FA-FF                      
*******  CLC   0(1,R2),0(RE)                                                    
         CLC   MYDATE(1),0(RE)                                                  
         BE    FI01D                                                            
         LA    RE,3(RE)                                                         
         B     FI01B                                                            
*                                                                               
FI01D    MVC   WORK(2),1(RE)       EXPANDED YEAR FROM TABLE (FA =10)            
*******  MVC   WORK+2(1),1(R2)     LAST DIGIT OF YEAR                           
         MVC   WORK+2(1),MYDATE+1  LAST DIGIT OF YEAR                           
*                                                                               
FI01P    DS    0H                                                               
         PACK  DUB,WORK(3)         YEAR OF BILL DATE                            
         CVB   R0,DUB                                                           
         STC   R0,SAVBYR   SAVE BINARY YEAR (FOR PROFILE CHECKING)              
         STC   R0,SAVBYRA  ALSO SAVE AS ADJUSTED (FOR INV FORMATTING)           
******   PACK  DUB,2(2,R2)          NOW DO MONTH                                
         PACK  DUB,MYDATE+2(2)      NOW DO MONTH                                
         CVB   R0,DUB                                                           
         STC   R0,SAVBMN   BINARY MONTH (FOR PROFILE CHECKING)                  
*                                                                               
FI01X    DS    0H                                                               
         L     R3,PARM4            A(B1X PROF)                                  
         CLI   5(R3),0             SEE IF INCREMENTING MONTH                    
         BE    FI03                                                             
******** PACK  DUB,2(2,R2)                                                      
         PACK  DUB,MYDATE+2(2)                                                  
         CVB   R0,DUB                                                           
         ZIC   RF,5(R3)                                                         
         AR    R0,RF                                                            
         CH    R0,=H'12'          SEE IF OVER 12                                
         BNH   FI02                                                             
         SH    R0,=H'12'                                                        
*                                                                               
FI02     DS    0H                                                               
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  ILONG+3(2),DUB                                                   
*                                                                               
FI03     DS    0H                                                               
         XC    YMSTYM,YMSTYM       CLEAR START YM OF YM FORMAT START            
         OC    14(2,R3),14(R3)     YM MODE START YEAR/MONTH                     
         BZ    FI05                NO                                           
         MVC   YMSTYM,14(R3)       SAVE YM FORMAT START YM                      
*                                                                               
         CLI   14(R3),80           IF LOWER THAN 80                             
         BH    FI03B               ASSUME CENTURY 2000                          
         ZIC   R0,14(R3)                                                        
         AH    R0,=H'100'          ADJUST BINARY YEAR OF PROFILE                
         STC   R0,YMSTYM                                                        
*                                                                               
FI03B    DS    0H                                                               
         CLC   SAVBYR(2),YMSTYM   CHECK BILL YM VS. PROFILE                     
         BL    FI03X               IF BEFORE MUST STILL                         
*                                                                               
         CLI   6(R3),C'X'         CHECK SPECIAL O&M FORMATTING                  
         BNE   FI03B5                                                           
         CLC   SAVBYR(2),=X'6501'  SEE IF AFTER JAN/01  (2001)                  
         BNL   FI03X               THEN HONOR BASE YEAR LOGIC                   
*                                                                               
FI03B5   DS    0H                                                               
*                                                                               
*******  BH    FI04                                                             
*******  PACK  DUB,2(2,R2)         MONTH OF BILL DATE                           
*******  CVB   R0,DUB                                                           
*******  CLM   R0,1,15(R3)                                                      
*******  BL    FI03X                                                            
*******                                                                         
         CLI   5(R3),0             SEE IF INCREMENTING MONTH                    
         BE    FI04                                                             
*******  PACK  DUB,2(2,R2)                                                      
         PACK  DUB,MYDATE+2(2)                                                  
         CVB   R0,DUB                                                           
         ZIC   RF,5(R3)                                                         
         AR    R0,RF                                                            
         CH    R0,=H'12'          SEE IF OVER 12                                
         BNH   FI03D                                                            
*                                                                               
         SH    R0,=H'12'                                                        
         ZIC   RE,SAVBYRA                                                       
         LA    RE,1(RE)           BUMP YEAR                                     
         STC   RE,SAVBYRA          SAVE ADJUSTED YEAR                           
*                                                                               
******   PACK  DUB,0(2,R2)         BUMP YEAR                                    
******   AP    DUB,=P'1'                                                        
******   OI    DUB+7,X'0F'                                                      
******   UNPK  0(2,R2),DUB        SET ADJUSTED YEAR                             
*                                                                               
FI03D    CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
*******  UNPK  2(2,R2),DUB         SET ADJUSTED MTH IN DATE                     
         UNPK  MYDATE+2(2),DUB     SET ADJUSTED MTH IN DATE                     
         B     FI04                                                             
*                                                                               
*        I GET HERE IF BILLING DATE IS BEFORE YM START-UP DATE                  
*                                                                               
FI03X    DS    0H                                                               
         CLI   6(R3),C'X'      B1X PROFILE+6                                    
         BNE   FI05            X IS A SPECIAL VALUE THAT                        
*                              TELLS ME TO THIS INVOICE MONTH                   
*                              AS YM (Y TELLS ME FORMAT IT AS MY)               
         CLC   0(4,R2),=C'9904'   SEE IF BEFORE APR/99                          
         BL    FI08            YES - DO MM FORMAT ANYWAY                        
*                                                                               
         CLC   SAVBYR(2),=X'6501'   ON OR AFTER JAN/01                          
         BNL   FI05                SHOULD HONOR BASE YEAR                       
*                                                                               
*    I CAN ONLY GET HERE IF 14(2,R3) (B1XPROF+14) IS NON-ZERO                   
*        AND BILL DATE IS AFTER YM START DATE                                   
*        OR 6(R3) (B1X PROFILE+6) IS X AND BILL DATE IS BEFORE                  
*        START DATE.                                                            
*                                                                               
FI04     DS    0H                  ELSE YEAR AS 1-9 AND MONTH AS 1-C            
         ZIC   R0,SAVBYRA    USE ADJUSTED YEAR                                  
         CVD   R0,DUB                                                           
         UNPK  WORK(3),DUB                                                      
         OI    WORK+2,X'F0'                                                     
         MVC   ILONG+3(1),WORK+2   2ND DIGIT OF YEAR                            
*                                                                               
*******  PACK  DUB,2(2,R2)         MONTH                                        
         PACK  DUB,MYDATE+2(2)     MONTH                                        
         CVB   RE,DUB                                                           
         LA    RE,MTHTAB-1(RE)                                                  
         MVC   ILONG+4(1),0(RE)                                                 
*                                                                               
         CLC   SAVBYR(2),YMSTYM  SEE IF BEFORE START DATE                       
         BL    FI08              COULD HAPPEN IF 6(R3) IS "X"                   
*                                                                               
         CLI   6(R3),C'Y'        SEE IF SWITCHING Y AND M                       
         BE    FI04D             TO MY                                          
         CLI   6(R3),C'X'        OR SPECIAL TIMING OF SWITCH                    
         BNE   FI08              TO MY                                          
*                                                                               
FI04D    MVC   ILONG+3(1),0(RE)   MTH                                           
         MVC   ILONG+4(1),WORK+2  2ND DIGIT OF YEAR                             
         B     FI08                                                             
*                                                                               
*             NORMAL MONTH FORMAT                                               
*             BUT MAY BE ADJUSTED BY OTHER PROFILES                             
*                                                                               
FI05     DS    0H                                                               
*                              AS YM (Y TELLS ME FORMAT IT AS MY)               
         SR    RF,RF                                                            
         ICM   RF,1,4(R3)          INVOICE NUMBER BASE YEAR                     
         BZ    FI08                NONE                                         
****                                                                            
**** SPECIAL NOTE - THIS PROFILE BYTE (UNLIKE MOSTY OTHERS)                     
****                WILL BE 100 OR OVER FOR 2000 YEARS                          
****                (SINCE THERE IS NO MONTH ASSOCIATED WITH IT)                
*                                                                               
         ZIC   RE,SAVBYR           YEAR                                         
         SR    RE,RF               UNLESS THIS YEAR HIGHER THAN BASE            
         BNP   FI08                IGNORE                                       
         MH    RE,=H'12'                                                        
*******  PACK  DUB,2(2,R2)         MONTH                                        
         PACK  DUB,MYDATE+2(2)     MONTH                                        
         CVB   R0,DUB                                                           
         AR    R0,RE           MONTH =  MONTH + (DIFF IN YEARS * 12)            
*                                                                               
         CLI   5(R3),0           SEE IF INCREMENTING MONTH                      
         BE    FIO6                                                             
         ZIC   RF,5(R3)                                                         
         AR    R0,RF                                                            
*                                                                               
FIO6     DS    0H                                                               
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  ILONG+3(2),DUB                                                   
*                                                                               
FI08     DS    0H                                                               
         MVC   INOMTH,ILONG+3     SAVE MONTH (OR YM OR MY)                      
*                                                                               
         MVI   ILONG+5,C'-'                                                     
*                                                                               
         CLI   6(R3),C'X'          SPECIAL FORMAT FOR O&M                       
         BNE   FI08A                                                            
         CLC   SAVBYR(2),=X'6304'  APRIL 1999                                   
         BL    FI08BX              IF BEFORE DO 01-12                           
*                                                                               
FI08A    DS    0H                                                               
         CLC   SAVBYR(2),YMSTYM    CHECK VS. PROFILE (YR AND MTH)               
         BL    FI08BX              YM MODE START YEAR                           
         B     FI08B                                                            
********                                                                        
*******  BH    FI08B                                                            
*******  PACK  DUB,2(2,R2)         MONTH OF BILL DATE                           
*******  PACK  DUB,MYDATE+2(2)     MONTH OF BILL DATE                           
*******  CVB   R0,DUB                                                           
*******  CLM   R0,1,15(R3)         VS YM MODE START MTH                         
******   BL    FI08BX                                                           
*                                                                               
FI08B    DS    0H                                                               
         LA    RF,B1PROF           B1 PROFILE                                   
         CLI   4(RF),C'3'          CHCK FOR SPECIAL YM FORMATTING               
         BNE   FI08BX                                                           
         MVC   ILONG+4(2),MYDATE+2 USE FULL MONTH (NO DASH)                     
         CLI   6(R3),C'Y'          SEE IF REVERSING YM TO MY                    
         BNE   FI08BX                                                           
         MVC   ILONG+3(2),MYDATE+2 MONTH FIRST                                  
         MVC   ILONG+5(1),WORK+2   SHOULD STILL BE 2ND DIGIT OF YEAR            
****                                                                            
****    NOTE THAT INOMTH WILL STILL HAVE                                        
***     OCT="A",NOV="B" AND DEC="C"                                             
*                                                                               
FI08BX   DS    0H                                                               
         L     RF,PARM2                                                         
         MVC   ILONG+6(4),0(RF)    INVOICE NUMBER AS CL4                        
         CLI   PARM2,2             BUT IS IT BINARY INSTEAD                     
         BNE   FI08D                                                            
         LH    R0,0(RF)                                                         
         N     R0,=X'00003FFF'     STRIP REVERSAL BITS                          
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  ILONG+6(4),DUB                                                   
*                                                                               
FI08D    DS    0H                                                               
         LA    RF,B1PROF           B1 PROFILE                                   
         CLI   4(RF),C'1'          MODE (ME-MN OR MN=ME)                        
         BE    FI10                                                             
         CLI   4(RF),C'3'          MODE ME-MN WITH SPECIAL YM                   
         BE    FI10                                                             
         MVC   ILONG(2),ILONG+3                                                 
         MVC   ILONG+3(2),INOMED                                                
*                                                                               
FI10     DS    0H                                                               
         MVC   ISHORT(2),INOMTH    SHORT IS MM-NNNN                             
         MVI   ISHORT+2,C'-'                                                    
         MVC   ISHORT+3(4),ILONG+6                                              
         CLI   4(RF),C'3'          MODE ME-MN WITH SPECIAL YM                   
         BNE   *+10                                                             
         MVC   ISHORT(3),ILONG+3   SHORT MAY BE YMMNNNN                         
*                                  IF YM MODE NOT ACTIVE YET                    
*                                  ILONG WILL STILL HAVE DASH                   
*                                                                               
*                                  SQUEEZE BLANKS OUT OF ILONG                  
         LA    RF,9                                                             
         LA    R5,ILONG                                                         
*                                                                               
FI12     DS    0H                                                               
         CLI   0(R5),C' '                                                       
         BH    *+14                                                             
         MVC   0(1,R5),1(R5)                                                    
         MVI   1(R5),C' '                                                       
         LA    R5,1(R5)                                                         
         BCT   RF,FI12                                                          
*                                  SET OUTPUT                                   
         L     R1,SAVR1                                                         
         LA    RF,ILONG                                                         
         ST    RF,0(R1)            A(LONG FORMAT)                               
         LA    RF,ISHORT                                                        
         ST    RF,4(R1)            A(SHORT FORMAT)                              
         LA    RF,INOMTH                                                        
         ST    RF,8(R1)            A(MONTH OR YEAR/MONTH)                       
         LA    RF,INOMED                                                        
         ST    RF,12(R1)           A(MEDIA EXPANSION)                           
*                                                                               
EXIT     DS    0H                                                               
         XIT1                                                                   
*                                                                               
         EJECT                                                                  
*        FORMAT USING DATA IN A(BILLREC)                                        
FIBILL   DS    0H                                                               
         L     R8,PARM5                                                         
         USING BILLRECD,R8                                                      
         CLI   PBILKRCD,X'08'   IS IT REALLY A BILLREC?                         
         BNE   FI00A            NO - USE OLD ROUTINES                           
         CLC   PBILIMO,=C'  '   DO I HAVE A 'YEAR/MONTH' ?                      
         BNH   FI00A            NO - THEN USE OLD ROUTINES                      
         CLC   PBILIMED(3),=C'   '  ARE FORMAT OPTIONS PRESENT?                 
         BNH   FIB10                                                            
         MVC   ILONG(2),PBILIMED                                                
*                                                                               
         LA    RF,B1PROF                                                        
         MVC   4(1,RF),PBILISEQ    ALTER B1 PROFILE                             
         B     FIB20                                                            
*                                                                               
FIB10    DS    0H                                                               
         LA    RF,B1PROF           B1 PROFILE                                   
         MVC   ILONG(2),2(RF)      MEDIA EXPANSION                              
*                                  *=BLANK, **=USE MEDIA CODE                   
         CLI   ILONG+0,C'*'                                                     
         BNE   *+8                                                              
         MVI   ILONG+0,C' '                                                     
         CLI   ILONG+1,C'*'                                                     
         BNE   *+8                                                              
         MVI   ILONG+1,C' '                                                     
         CLC   ILONG+0(2),=C'  '                                                
         BNE   *+10                                                             
         MVC   ILONG+0(1),PARM3                                                 
*                                                                               
FIB20    MVC   INOMED,ILONG        SAVE MEDIA EXP                               
         MVI   ILONG+2,C'-'                                                     
*                                                                               
         CLI   4(RF),C'3'          SEE IF YMM FORMAT                            
         BNE   FIB24                                                            
*                                                                               
         B     FI00C               REST LIKE NON-BILL CODE                      
*                                                                               
*                                                                               
FIB24    MVC   ILONG+3(2),PBILIMO  USE DATA FROM BILLHEADER                     
         MVI   ILONG+5,C'-'                                                     
         MVC   INOMTH,PBILIMO                                                   
*                                  TO MATCH BILL'S                              
         B     FI08BX              REST IS SAME AS OLD                          
*                                                                               
*                                                                               
YRTAB    DC    X'FA',C'10'      YEARS 100-109 (2000-2009)                       
         DC    X'FB',C'11'      YEARS 110-119 (2010-2019)                       
         DC    X'FC',C'12'      YEARS 120-129 (2020-2029)                       
         DC    X'FD',C'13'      YEARS 130-139 (2030-2039)                       
         DC    X'FE',C'14'      YEARS 140-149 (2040-2049)                       
         DC    X'FF',C'15'      YEARS 150-159 (2050-2059)                       
         DC    X'FFFFFF'          END OF TABLE                                  
*                                                                               
MTHTAB   DC    C'123456789ABC'                                                  
         SPACE 2                                                                
         LTORG                                                                  
         SPACE 2                                                                
WORKD    DSECT                                                                  
WORK     DS    CL20                                                             
MYDATE   DS    CL6        PASSED DATE (MAY BE ADJUSTED)                         
YMSTYM   DS    XL2        BIMARY YM FORMAT START YM                             
*                         ADJUSTED FROM BIX PROFILE                             
SAVBYR   DS    X          SAVED BINARY YEAR OF TODAY OR BILL DATE               
SAVBMN   DS    X          SAVED BINARY MONTH OF TODAY OR BBILL DATE             
SAVBYRA  DS    X         ADJUSTED BINARY YEAR FOR FORMATTING                    
*     IF NOT INCREMENTING MONTH (B1X PROF+5) WILL BE SAME AS SAVBYR             
B1PROF   DS    CL16       STORED FOR PARM3                                      
*                                                                               
DUB      DS    D                                                                
SAVR1    DS    F                                                                
PARMS    DS    0F                                                               
PARM1    DS    A                                                                
PARM2    DS    A                                                                
PARM3    DS    A                                                                
PARM4    DS    A                                                                
PARM5    DS    A                                                                
PARM6    DS    A                                                                
*                                                                               
ILONG    DS    CL10                                                             
ISHORT   DS    CL7                                                              
INOMED   DS    CL2                                                              
INOMTH   DS    CL2                                                              
*                                                                               
WORKDL   EQU   *-WORKD                                                          
*                                                                               
BILLRECD DSECT                                                                  
       ++INCLUDE PBILLREC                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'021PPFMTINO  08/19/13'                                      
         END                                                                    
