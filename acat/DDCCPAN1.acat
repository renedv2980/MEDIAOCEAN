*          DATA SET DDCCPAN1   AT LEVEL 044 AS OF 07/17/07                      
*&&      SET   NOP=N                                                            
*CATALP CCPAN1                                                                  
*                                                                               
         TITLE 'CCPAN1 - XREF FROM PAN TAPE'                                    
CCPAN1   CSECT                                                                  
         PRINT NOGEN                                                            
         NBASE WORKX-WORKD,**PANF**,R9,WORK=V(REGSAVE)                          
*                                                                               
         USING WORKD,RC            SET UP WORKING STORAGE USINGS                
         USING PHSCARDD,PHSCARD                                                 
         USING INCCARDD,INCCARD                                                 
*                                                                               
MAIN010  BAS   RE,PANBLD           BUILD A DRECTORY LIST                        
*                                                                               
         OPEN  (TMPFILE,INPUT)     OPEN DIR LIST FILE                           
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         OPEN  (PANFILE,OUTPUT)    OPEN PAN OUT FILE                            
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         OPEN  (REJECTS,OUTPUT)    OPEN REJECT FILE                             
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         GOTO1 VSORTER,DMCB,SRTCARD,RECCARD                                     
*                                                                               
MAIN020  XC    PANINF,PANINF       CLEAR INFO BLOCK                             
         XC    LINES,LINES                                                      
         BAS   RE,PANDIR           READ AN ENTRY                                
         CLI   DMCB,X'FF'          RETURN CODE: END OF PAN LIBRARY              
         BE    MAIN990                                                          
*                                                                               
MAIN030  BAS   RE,PANLIN           GET A LINE OF PAN INPUT                      
         TM    DMCB+8,X'80'                                                     
         BO    MAIN040                                                          
         ICM   R1,15,LINES                                                      
         LA    R1,1(R1)                                                         
         STCM  R1,15,LINES                                                      
         CLC   PANCARD(3),=C'*&&'                                               
         BNE   *+8                                                              
         MVI   FLAG,0              CLEAR IGNORE FLAG                            
         CLI   FLAG,C'I'                                                        
         BE    MAIN030             STILL IGNORING CARDS                         
*                                                                               
*&&UK*&& CLC   PANCARD(8),=C'*&&US*&&'                                          
*&&US*&& CLC   PANCARD(8),=C'*&&UK*&&'                                          
         BE    MAIN030             IGNORE UK/US                                 
*                                                                               
*&&UK*&& CLC   PANCARD(5),=C'*&&US'                                             
*&&US*&& CLC   PANCARD(5),=C'*&&UK'                                             
         BNE   MAIN035                                                          
*                                                                               
         MVI   FLAG,C'I'           SET IGNORE FLAG                              
         B     MAIN030                                                          
*                                                                               
MAIN035  BAS   RE,EXTRACT          EXTRACT USEFUL INFO                          
         B     MAIN030                                                          
*                                                                               
MAIN040  MVI   FLAG,0                                                           
         CLC   PCTYPE,=C'    '     IGNORE IF NO TYPE                            
         BE    MAIN045                                                          
         CLC   PCTYPE,=C'B   '                                                  
         BE    *+24                                                             
         CLC   PCSECT,SPACES       IGNORE BOOKS WITH NO CSECT                   
         BE    MAIN045                                                          
         CLC   PCSECT,=C'DUMMY'    REPORT ON DUMMY CSECTS                       
         BE    MAIN046                                                          
*                                                                               
*&&UK*&& MVC   PCCTRY,=C'UK'       SET COUNTRY                                  
*&&US*&& MVC   PCCTRY,=C'US'                                                    
*                                                                               
         GOTO1 VSORTER,DMCB,PUT,PHSCARD                                         
         B     MAIN050                                                          
*                                                                               
MAIN045  CLC   LINES,=F'1'                                                      
         BH    MAIN050                                                          
         MVC   PLINE,SPACES                                                     
         MVC   PLINE(9),=C'++FORMAT '                                           
         MVC   PLINE+9(10),PCBOOK                                               
         LA    R1,PLINE+9                                                       
         CLI   0(R1),C' '                                                       
         BE    *+12                                                             
         LA    R1,1(R1)                                                         
         B     *-12                                                             
         MVC   0(4,R1),=C',BAL'                                                 
         CLC   PANLANG(4),=C'DATA'                                              
         BNE   *+10                                                             
         MVC   0(5,R1),=C',DATA'                                                
         B     MAIN049                                                          
*                                                                               
MAIN046  MVC   PLINE,SPACES                                                     
         MVC   PLINE(9),=C'NO CSECT '                                           
         MVC   PLINE+9(10),PCBOOK                                               
*                                                                               
MAIN049  PUT   REJECTS,PLINE                                                    
*                                                                               
MAIN050  MVC   PHSCARD,SPACES      CLEAR PHSCARD AND GET NEXT BOOK              
         MVC   PCBOOK,SPACES                                                    
         B     MAIN020                                                          
*                                                                               
MAIN990  BAS   RE,MERGER           READ BACK SORTED BOOKS                       
*                                                                               
MAIN999  CLOSE TMPFILE             CLOSE FILES AND EXIT                         
         CLOSE PANFILE                                                          
         CLOSE REJECTS                                                          
*                                                                               
XBASE    XBASE                                                                  
         EJECT                                                                  
*************************************************************                   
*        BUILD FILE OF DIR ENTRIES                          *                   
*************************************************************                   
         SPACE 1                                                                
PANBLD   NTR1                      FIRST READ ALL DIR ENTRIES                   
         OPEN  (TMPFILE,OUTPUT)                                                 
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         MVC   PANBK,=CL10'A'      START FROM A                                 
*                                                                               
PANBLD3  GOTO1 VPANIC,DMCB,(X'40',READ),(C'T',DIR),PANBK,PANCARD                
*                                                                               
         CLC   PANCARD(2),=C'/*'   END OF DIRECTORY?                            
         BE    PANBLD4             YES                                          
*                                                                               
         PUT   TMPFILE,PANCARD     SAVE DIR TO TMPFILE                          
         B     PANBLD3                                                          
*                                                                               
PANBLD4  CLOSE TMPFILE             CLOSE FILES AND EXIT                         
         GOTO1 VPANIC,DMCB,CLOSE,0,0,0                                          
         XIT1                                                                   
         EJECT                                                                  
*************************************************************                   
*        READ A DIRECTORY ENTRY FROM THE LIST IN THE FILE   *                   
*************************************************************                   
         SPACE 1                                                                
PANDIR   NTR1                                                                   
         GET   TMPFILE,PANCARD     READ IN DIR ENTRY                            
         MVC   PANBK,PANCARD       SET PANBK TO NAME OF BOOK READ               
         MVC   PANLEV,PANCARD+10                                                
         MVC   PANDATE,PANCARD+26                                               
         MVC   PANLANG,PANCARD+18                                               
         GOTO1 VDATCON,DMCB,(4,PANDATE),(0,PANDAT1)                             
         CLI   PANDAT1,X'FA'                                                    
         BNE   *+8                                                              
         MVI   PANDAT1,X'F0'                                                    
         B     XIT1                                                             
*                                                                               
PANDIRX  DS    0H                  NO MORE ITEMS IN DIRECTORY                   
         GOTO1 VPANIC,DMCB,CLOSE,0,0,0                                          
         CLOSE TMPFILE                                                          
         MVI   DMCB,X'FF'          RETURN CODE: END OF PAN LIBRARY              
         XIT1                                                                   
         EJECT                                                                  
*************************************************************                   
*        READ A LINE OF THE PANBOOK                         *                   
*************************************************************                   
         SPACE 1                                                                
PANLIN   NTR1                                                                   
         GOTO1 VPANIC,DMCB,READ,(C'T',PAN),PANBK,PANCARD                        
*                                                                               
         TM    DMCB+8,X'80'        END OF BOOK?                                 
         BO    XIT1                                                             
         CLI   DMCB+8,0            MAKE SURE NO ERRS RDG FILE                   
         BE    *+6                                                              
         DC    H'0'                                                             
XIT1     XIT1                                                                   
         EJECT                                                                  
*************************************************************                   
*        EXTRACT PHASE AND CATALP INFO                      *                   
*************************************************************                   
         SPACE 1                                                                
EXTRACT  NTR1                                                                   
*                                                                               
         CLC   LINES,=F'2'         FIRST OR SECOND LINE ONLY                    
         BH    EXT000                                                           
         CLI   PANCARD,C'S'        LOOK FOR SCREEN                              
         BNE   EXT000                                                           
         MVC   PPHASE(4),PANCARD+4                                              
         MVC   PPHASE+4(2),PANCARD+1                                            
         MVC   PCSECT,PPHASE       USE PHASE AS CSECT                           
*                                                                               
EXT000   CLC   PANBK(2),=C'RM'                                                  
         BE    EXT500                                                           
*                                                                               
         MVC   INCCARD,SPACES                                                   
         CLC   PCTYPE,=C'    '                                                  
         BNE   EXT001                                                           
*                                                                               
         MVC   PCTYPE,=C'A   '                                                  
         MVC   PCBOOK,PANBK                                                     
         MVC   PCLEVEL,PANLEV                                                   
         MVC   PCDATE(6),PANDAT1                                                
*                                                                               
EXT001   CLC   PANCARD(7),=C'*PHASE '                                           
         BNE   EXT010                                                           
         LA    R1,PPHASE                                                        
         LA    RF,PANCARD+7                                                     
EXT005   MVC   0(1,R1),0(RF)                                                    
         LA    R1,1(R1)                                                         
         LA    RF,1(RF)                                                         
         CLI   0(RF),C'A'                                                       
         BNL   EXT005                                                           
*                                                                               
EXT006   LR    RF,R1                                                            
         BCTR  RF,0                BACK UP TO LAST LETTER                       
         CLI   0(RF),C'A'          SPECIAL RULES FOR A                          
         BNE   EXT010                                                           
*                                                                               
         LA    R0,10                                                            
         LA    R1,PCBOOK+9         SYPRGMM00A                                   
EXT007   CLI   0(R1),C'A'                                                       
         BL    EXT008                                                           
         TM    0(R1),X'F0'         IS IT NUMERIC                                
         BNO   EXT010                                                           
         BCTR  R1,0                                                             
         TM    0(R1),X'F0'         AND THIS ONE                                 
         BNO   EXT010                                                           
         MVI   0(RF),C' '          OK PRETEND ITS LIVE PHASE                    
*                                                                               
EXT008   BCTR  R1,0                                                             
         BCT   R0,EXT007                                                        
*                                                                               
EXT010   CLC   PANCARD(7),=C'*CATALP '                                          
         BNE   EXT020                                                           
         LA    R1,PRMBOOK                                                       
         LA    RF,PANCARD+8                                                     
EXT015   MVC   0(1,R1),0(RF)                                                    
         LA    R1,1(R1)                                                         
         LA    RF,1(RF)                                                         
         CLI   0(RF),C'A'                                                       
         BNL   EXT015                                                           
*                                                                               
EXT020   CLC   PANCARD+8(7),=C' CSECT '                                         
         BE    EXT021                                                           
         CLC   PANCARD+9(7),=C' CSECT '                                         
         BE    EXT021                                                           
         CLC   PANCARD+8(7),=C' START '                                         
         BE    EXT021                                                           
         CLC   PANCARD+9(7),=C' START '                                         
         BE    EXT021                                                           
         B     EXT030                                                           
*                                                                               
EXT021   CLC   PCSECT,SPACES       FIRST CSECT ONLY                             
         BNE   EXT030              ACTUALLY LAST CSECT WORKS BETTER             
*                                                                               
         LA    RF,PANCARD                                                       
         MVC   PCSECT,PPHASE       USE PHASE AS CSECT IF NONE                   
*NOP*    MVC   PCSECT,=C'DUMMY'    USE DUMMY AS CSECT IF NONE                   
         CLI   0(RF),C' '                                                       
         BE    EXT030                                                           
         MVC   PCSECT,SPACES                                                    
*                                                                               
         LA    R1,PCSECT                                                        
EXT025   MVC   0(1,R1),0(RF)                                                    
         LA    R1,1(R1)                                                         
         LA    RF,1(RF)                                                         
         CLI   0(RF),C'A'                                                       
         BNL   EXT025                                                           
*                                                                               
EXT030   CLC   PANCARD(9),=C'*INCLUDE '                                         
         BNE   EXT090                                                           
         MVC   ICTYPE,=C'C   '                                                  
*&&UK*&& MVC   ICCTRY,=C'UK'       SET COUNTRY                                  
*&&US*&& MVC   ICCTRY,=C'US'                                                    
         MVC   IINCLUD(10),PANBK                                                
         MVC   IPHASE,PPHASE                                                    
         LA    R1,IRMBOOK                                                       
         LA    RF,PANCARD+9                                                     
EXT035   MVC   0(1,R1),0(RF)                                                    
         LA    R1,1(R1)                                                         
         LA    RF,1(RF)                                                         
         CLI   0(RF),C'A'                                                       
         BNL   EXT035                                                           
*                                                                               
         GOTO1 VSORTER,DMCB,PUT,INCCARD                                         
*                                                                               
EXT090   TM    DMCB+8,X'80'        END OF BOOK?                                 
         BO    XIT1                                                             
         CLI   DMCB+8,0            MAKE SURE NO ERRS RDG FILE                   
         BE    *+6                                                              
         DC    H'0'                                                             
         B     XIT1                                                             
         EJECT                                                                  
*************************************************************                   
*        RM BOOK GO HERE                                    *                   
*************************************************************                   
         SPACE 1                                                                
EXT500   EQU   *                                                                
         CLC   PCTYPE,=C'    '                                                  
         BNE   EXT501                                                           
*                                                                               
         MVC   PCTYPE,=C'B   '                                                  
         MVC   PRMBOOK(8),PANBK+2                                               
*                                                                               
EXT501   CLC   PANCARD+1(3),=C'TXT'                                             
         BNE   EXT502                                                           
         MVC   SAVECARD(56),SAVECARD+56                                         
         MVC   SAVECARD+56(56),PANCARD+16                                       
*                                                                               
EXT502   CLC   PANCARD+1(3),=C'ESD'                                             
         BE    EXT503                                                           
         CLC   PANCARD+1(3),=C'END'                                             
         BNE   EXT504                                                           
         BE    XIT1                                                             
**WHAT?? BE    EXT504                                                           
**WHAT?? BNE   XIT1                                                             
*                                                                               
EXT503   LA    R1,PANCARD+16                                                    
         CLI   8(R1),X'00'                                                      
         BNE   *+14                                                             
         MVC   CSECTSV,0(R1)                                                    
         B     EXT504                                                           
         LA    R1,16(R1)                                                        
         CLI   8(R1),X'00'                                                      
         BNE   *+14                                                             
         MVC   CSECTSV,0(R1)                                                    
         B     EXT504                                                           
         LA    R1,16(R1)                                                        
         CLI   8(R1),X'00'                                                      
         BNE   *+14                                                             
         MVC   CSECTSV,0(R1)                                                    
         B     EXT504                                                           
         B     XIT1                                                             
*                                                                               
EXT504   CLC   PCSECT(8),SPACES    ONLY SAVE THE FIRST CSECT                    
         BNE   *+10                                                             
         MVC   PCSECT(8),CSECTSV                                                
         LA    R1,SAVECARD                                                      
         LA    R0,90                                                            
*                                                                               
         LA    RF,=C'DEVEL=NNN'    FIX UP PANAPT                                
         MVI   0(RF),C'L'                                                       
         LA    RF,=C'DOOK='        FIX UP PANAPT                                
         MVI   0(RF),C'B'                                                       
*                                                                               
EXT510   CLC   0(9,R1),=C'DEVEL=NNN'                                            
         BE    *+14                                                             
         CLC   0(5,R1),=C'LEVEL'                                                
         BE    EXT520                                                           
         CLC   0(5,R1),=C'DOOK='                                                
         BNE   *+10                                                             
         MVC   PCBOOK,5(R1)                                                     
EXT511   LA    R1,1(R1)                                                         
         BCT   R0,EXT510                                                        
         B     XIT1                                                             
*                                                                               
EXT520   MVC   PCLEVEL,6(R1)                                                    
         MVC   DUB,15(R1)                                                       
*                                                                               
EXT522   CLI   DUB+5,C'/'          PROTECT DATCON                               
         BNE   XIT1                                                             
         CLI   DUB+2,C'/'                                                       
         BE    EXT530                                                           
*                                                                               
         BAS   RE,MONC             CONVERT MONTH                                
*                                                                               
EXT530   GOTO1 VDATCON,DMCB,(4,DUB),(0,PCDATE)                                  
         CLI   PCDATE,X'FA'                                                     
         BNE   *+8                                                              
         MVI   PCDATE,X'F0'                                                     
         B     XIT1                                                             
         EJECT                                                                  
*************************************************************                   
*        READ BACK SORTED RECORDS                           *                   
*************************************************************                   
         SPACE 1                                                                
MERGER   NTR1                                                                   
         XC    SAVECARD,SAVECARD                                                
*                                                                               
MERG01   GOTO1 VSORTER,DMCB,GET                                                 
         ICM   R2,15,4(R1)                                                      
         BZ    MERGXX                                                           
*                                                                               
         MVC   PHSCARD,0(R2)                                                    
*                                                                               
         CLC   PCTYPE,=C'B   '     SAVE C CARDS                                 
         BNE   MERG050                                                          
         MVC   SAVECARD,PHSCARD                                                 
         B     MERG990                                                          
*                                                                               
MERG050  CLC   PCTYPE,=C'C   '     MERGE CSECT NAME INTO I CARDS                
         BNE   MERG990                                                          
         CLC   PRMBOOK,SAVECARD+PRMBOOK-PHSCARDD                                
         BNE   MERG990                                                          
         MVC   PCSECT,SAVECARD+PCSECT-PHSCARDD                                  
*                                                                               
MERG990  PUT   PANFILE,PHSCARD                                                  
         B     MERG01                                                           
*                                                                               
MERGXX   B     XIT1                                                             
         EJECT                                                                  
*************************************************************                   
*        READ BACK SORTED RECORDS                           *                   
*************************************************************                   
*&&NOP                                                                          
         SPACE 1                                                                
MERGER   NTR1                                                                   
         XC    SAVECARD,SAVECARD                                                
*                                                                               
MERG01   GOTO1 VSORTER,DMCB,GET                                                 
         ICM   R2,15,4(R1)                                                      
         BZ    MERGXX                                                           
*                                                                               
         MVC   PHSCARD,0(R2)                                                    
*                                                                               
         CLC   PCTYPE,=C'A   '                                                  
         BE    MERG100                                                          
         CLC   PCTYPE,=C'B   '                                                  
         BE    MERG200                                                          
         CLC   PCTYPE,=C'C   '                                                  
         BE    MERG300                                                          
         DC    H'0'                                                             
*                                                                               
MERG100  CLC   PPHASE,SPACES       WRITE ALL B WITH PHASES                      
         BE    MERG110                                                          
         PUT   PANFILE,PHSCARD                                                  
         B     MERG01                                                           
*                                                                               
MERG110  CLC   SAVECARD,SPACES     FIRST TIME                                   
         BE    *+14                                                             
         CLC   PRMBOOK,SAVECARD+PRMBOOK-PHSCARDD                                
         BE    MERG10                                                           
*                                                                               
         MVC   SAVECARD,0(R2)      SAVE AND GET NEXT                            
         B     MERG01                                                           
*                                                                               
MERG10   MVC   DUB,PCDATE          COMPARE DATES                                
         BAS   RE,MONC                                                          
         MVC   DUB1+0(2),DUB+5                                                  
         MVC   DUB1+2(2),FULL+2                                                 
         MVC   DUB1+4(2),DUB                                                    
*                                                                               
         MVC   DUB,SAVECARD+PCDATE-PHSCARDD                                     
         BAS   RE,MONC                                                          
         MVC   DUB2+0(2),DUB+5                                                  
         MVC   DUB2+2(2),FULL+2                                                 
         MVC   DUB2+4(2),DUB                                                    
*                                                                               
         CLC   DUB2(6),DUB1                                                     
         BH    MERG01                                                           
         MVC   SAVECARD,0(R2)      SAVE AND GET NEXT                            
         B     MERG01                                                           
*                                                                               
MERG200  CLC   PRMBOOK,SAVECARD+PRMBOOK-PHSCARDD                                
         BNE   MERG01                                                           
         MVC   SAVECARD+PCLEVEL-PHSCARDD(3),PCLEVEL                             
         MVC   SAVECARD+PCDATE-PHSCARDD(7),PCDATE                               
         B     MERG01                                                           
*                                                                               
MERG300  CLC   PRMBOOK,SAVECARD+PRMBOOK-PHSCARDD                                
         BNE   MERG01                                                           
         MVC   SAVECARD+PINCLUD-PHSCARDD(10),PINCLUD                            
         MVC   SAVECARD+PPHASE-PHSCARDD(8),PPHASE                               
*                                                                               
         PUT   PANFILE,SAVECARD                                                 
         B     MERG01                                                           
*                                                                               
MERGXX   B     XIT1                                                             
*&&                                                                             
         EJECT                                                                  
*************************************************************                   
*        CONVERT MONTH IN DUB TO 1 - 12 IN FULL             *                   
*************************************************************                   
         SPACE 1                                                                
MONC     EQU   *                                                                
         LA    RF,12                                                            
         LA    R1,MONTAB                                                        
MONC010  CLC   0(3,R1),DUB                                                      
         BE    MONC020                                                          
         LA    R1,3(R1)                                                         
         BCT   RF,MONC010                                                       
         DC    H'0'                                                             
MONC020  CVD   RF,DUB1                                                          
         UNPK  DUB(2),DUB1                                                      
         MVI   DUB+2,C'/'                                                       
         CLI   DUB,C'1'                                                         
         BE    *+8                                                              
         MVI   DUB,C'0'                                                         
*&&UK                                                                           
         MVC   DUB(2),DUB+3                                                     
         UNPK  DUB+3(2),DUB1                                                    
*&&                                                                             
         BR    RE                                                               
MONTAB   DC    C'DECNOVOCTSEPAUGJULJUNMAYAPRMARFEBJAN'                          
         EJECT                                                                  
*************************************************************                   
*        ADDCONS                                            *                   
*************************************************************                   
         SPACE 1                                                                
VPANIC   DC    V(PANIC)                                                         
VSORTER  DC    V(SORTER)                                                        
VDATCON  DC    V(DATCON)                                                        
         EJECT                                                                  
*************************************************************                   
*        CONSTANTS TABLES AND DCBS                          *                   
*************************************************************                   
         SPACE 1                                                                
TMPFILE  DCB   DDNAME=TMPFILE,DSORG=PS,RECFM=FB,MACRF=(GM,PM),         X        
               BLKSIZE=3120,LRECL=80,EODAD=PANDIRX                              
PANFILE  DCB   DDNAME=PANFILE,DSORG=PS,RECFM=FB,MACRF=(PM),            X        
               BLKSIZE=3120,LRECL=80                                            
REJECTS  DCB   DDNAME=REJECTS,DSORG=PS,RECFM=FB,MACRF=(PM),            X        
               BLKSIZE=3120,LRECL=80                                            
*                                                                               
SRTCARD  DC    C'SORT FIELDS=(41,8,BI,A,23,3,BI,A) '                            
RECCARD  DC    C'RECORD TYPE=F,LENGTH=132 '                                     
*                                                                               
PUT      DC    CL8'PUT'                                                         
GET      DC    CL8'GET'                                                         
CLOSE    DC    CL8'CLOSE'                                                       
READ     DC    CL8'READ'                                                        
PAN      DC    CL8'PAN'                                                         
DIR      DC    CL8'DIR'                                                         
SPACES   DC    CL80' '                                                          
         EJECT                                                                  
*************************************************************                   
*        WORKING STORAGE                                    *                   
*************************************************************                   
         SPACE 1                                                                
WORKD    DSECT                                                                  
DUB      DS    D                                                                
DUB1     DS    D                                                                
DUB2     DS    D                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    X                                                                
FLAG     DS    X                                                                
LINES    DS    F                                                                
DMCB     DS    6F                                                               
*                                                                               
PANINF   DS    0CL28                                                            
PANBK    DS    CL10                                                             
PANLEV   DS    CL3                                                              
PANDATE  DS    CL8                                                              
PANDAT1  DS    CL7                                                              
CSECTSV  DS    CL8                                                              
PANLANG  DS    CL5                                                              
*                                                                               
PANCARD  DS    CL80                                                             
SAVECARD DS    CL160                                                            
CARD     DS    CL80                                                             
PHSCARD  DS    CL132                                                            
INCCARD  DS    CL132                                                            
PLINE    DS    CL132                                                            
*                                                                               
WORKX    EQU   *                                                                
         EJECT                                                                  
*************************************************************                   
*        OTHER DSECTS                                       *                   
*************************************************************                   
         SPACE 1                                                                
PHSCARDD DSECT                                                                  
         DS    0CL132                                                           
PCSECT   DS    CL8                                                              
         DS    CL1                                                              
PCDATE   DS    CL8                                                              
         DS    CL1                                                              
PCLEVEL  DS    CL3                                                              
         DS    CL1                                                              
PCTYPE   DS    CL4                                                              
PCCTRY   DS    CL2                                                              
         DS    CL1                                                              
PCBOOK   DS    CL10                                                             
         DS    CL1                                                              
PRMBOOK  DS    CL8                                                              
         DS    CL1                                                              
PINCLUD  DS    CL10                                                             
         DS    CL1                                                              
PPHASE   DS    CL8                                                              
         EJECT                                                                  
INCCARDD DSECT                                                                  
         DS    0CL132                                                           
         DS    CL8                                                              
         DS    CL1                                                              
         DS    CL8                                                              
         DS    CL1                                                              
         DS    CL3                                                              
         DS    CL1                                                              
ICTYPE   DS    CL4                                                              
ICCTRY   DS    CL2                                                              
         DS    CL1                                                              
         DS    CL10                                                             
         DS    CL1                                                              
IRMBOOK  DS    CL8                                                              
         DS    CL1                                                              
IINCLUD  DS    CL10                                                             
         DS    CL1                                                              
IPHASE   DS    CL8                                                              
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'044DDCCPAN1  07/17/07'                                      
         END                                                                    
