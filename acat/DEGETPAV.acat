*          DATA SET DEGETPAV   AT LEVEL 036 AS OF 05/06/19                      
*PROCESS USING(WARN(15))           ENABLE ALL USING STATEMENT WARNINGS          
*CATALP DEGETPAV                                                                
         TITLE 'DEGETPAV - PROGRAM AVERAGES READ CONTROLLER'                    
***************************CHANGE LOG*******************************            
*      DATE       LVL    REASON                                                 
*                                                                               
*      OCT10/00    12    VAR CORRECTIONS - IF X'80' IS REQUESTED                
*                                                                               
*      APR02/90     3    TAKE OUT SPECIAL ARB WEEK CODE WHICH DIED              
*                         IN MAY/86 BUT WAS MISSED HERE                         
*                                                                               
********************************************************************            
GETPAV   RSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 PAVWORKL,**GETPAV,RA                                             
         LR    R9,RC                                                            
         USING PAVWORKD,R9                                                      
         L     RC,0(R1)                                                         
         USING DEGETD,RC           RC=A(W/S)                                    
         USING COMFACSD,R8         R8=A(COMFACS)                                
         USING DBLOCKD,R6          R6=A(DBLOCK)                                 
         CLI   0(R1),0             TEST FIRST/NEXT TIME                         
         BNE   GETPAV34                                                         
                                                                                
         L     RE,DBEXTEND         CHECK FOR MULIPLE DAY/TIME                   
NXT01    DS    0H                                                               
         LTR   RE,RE               NO MULTI D/T   - END IT                      
         BZ    DYTMNO                                                           
         USING DBXTLID,RE                                                       
         CLC   DBXTLID,=C'DYTM'    THIS IS THE ONE WE WANT                      
         BE    *+12                                                             
         L     RE,DBXTLNXT                                                      
         B     NXT01                                                            
*                                                                               
         MVC   DBSELDAY(5),DBXTLIST  SET FIRST DAY/TM                           
         MVI   DBXTLIDX,0                                                       
         DROP  RE                                                               
*                                                                               
DYTMNO   DS    0H                                                               
         L     RE,DBEXTEND         CHECK FOR MULIPLE DAY/TIME                   
NXT02    DS    0H                                                               
         LTR   RE,RE               NO MULTI D/T   - END IT                      
         BZ    PURENO                                                           
         USING DBXPUID,RE                                                       
         CLC   DBXPUID,=C'PURE'    THIS IS THE ONE WE WANT                      
         BE    *+12                                                             
         L     RE,DBXPUNXT                                                      
         B     NXT02                                                            
*                                                                               
         MVC   DBSELPUR(2),DBXPULST  SET FIRST PURE NUMBER                      
         MVI   DBXPUIDX,0                                                       
         DROP  RE                                                               
PURENO   DS    0H                                                               
         MVI   DEMOFLAG,0                                                       
         GOTO1 AFIXSTA             DEAL WITH REUSED CALL LETTERS                
         CLI   DBSELDAY,0          TEST IF DAY & TIME SET                       
         BNE   GETPAV1                                                          
         OC    DBSELTIM,DBSELTIM                                                
         BNZ   GETPAV1                                                          
         MVI   DBFUNCT,DBGETPUR    NO - SET FUNCTION TO GET PURE DEMO           
GETPAV1  OC    DBSELBK,DBSELBK     ENSURE SELECTED BOOK SET                     
         BZ    GETPAV30                                                         
GETPAV2  GOTO1 AGETBOOK            GET INITIAL BOOK VALUE                       
         BNE   GETX                EXIT ON ERROR                                
         LA    R1,DBACTBK                                                       
         L     RF,AGETCTRL                                                      
         OC    ACONHDR,ACONHDR     TEST IF A(CONTROL TABLE) SET                 
         BNZ   *+6                                                              
         BASR  RE,RF               NO - GET AGENCY CONTROLS                     
         OC    ACONHDR+1(3),ACONHDR+1                                           
         BZ    GETPAV14                                                         
         MVC   DBACTRMK,DBSELRMK                                                
         OC    DBACTRMK,DBACTRMK                                                
         BNZ   GETPAV3                                                          
         GOTO1 AGETMRKT                                                         
         BNE   GETX                                                             
         XC    DBACTSTA,DBACTSTA   LET CODE BELOW SET IT                        
*                                  GET AGENCY MARKET CONTROLS                   
*                                                                               
GETPAV3  OC    ALET,ALET           SSBTBLET RESOLVED?                           
         BNZ   GETPAV3C            YES                                          
         XC    DUB,DUB             SPECIAL CALL FOR SYSFACS                     
         MVC   DUB(4),=XL4'FEFFFFFF'                                            
         OC    CSWITCH,CSWITCH                                                  
         BNZ   GETPAV3B                                                         
         ICM   RF,15,CMASTC                                                     
         ICM   RF,15,MCSSB-MASTD(RF)       RF=ASSB                              
         MVC   ALET,SSBTBLET-SSBD(RF)                                           
         B     GETPAV3C                                                         
*                                                                               
GETPAV3B L     RF,CSWITCH                                                       
         GOTO1 (RF),DUB                                                         
         L     RF,0(R1)            RF=V(SYSFACS)                                
         L     RF,VSSB-SYSFACD(RF) RF=V(SSB)                                    
         MVC   ALET,SSBTBLET-SSBD(RF)                                           
*                                                                               
GETPAV3C L     R2,ACONHDR          R2=A(CONTROL TABLE)                          
         USING CONHDRD,R2                                                       
         LAM   AR0,ARF,ARZERO                                                   
         LAM   AR2,AR2,ALET                                                     
         LA    R3,CONHDRD+CONHDRLN                                              
         USING CONDTAD,R3                                                       
         CPYA  AR3,AR2                                                          
         SAC   512                                                              
         LA    R4,3                                                             
         SR    R5,R5                                                            
         ICM   R5,7,CONAET                                                      
         CR    R3,R5                                                            
         BNE   GETPAV4                                                          
         SAC   0                                                                
         LAM   AR2,AR3,=2F'0'                                                   
         B     GETPAV14                                                         
         DROP  R2                                                               
*                                  PROCESS VALID MARKET LIST                    
GETPAV4  CLI   CONINDIC,0                                                       
         BNE   GETPAV6                                                          
         OI    DEMOFLAG,X'80'                                                   
         CLC   DBACTRMK,CONMRKT                                                 
         BNE   *+8                                                              
         OI    DEMOFLAG,X'40'                                                   
         B     GETPAV10                                                         
*                                  PROCESS INVALID MARKET LIST                  
GETPAV6  CLI   CONINDIC,X'FF'                                                   
         BNE   GETPAV8                                                          
         OI    DEMOFLAG,X'20'                                                   
         CLC   DBACTRMK,CONMRKT                                                 
         BNE   *+8                                                              
         OI    DEMOFLAG,X'10'                                                   
         B     GETPAV10                                                         
*                                  PROCESS SOURCE OVERRIDE LIST                 
GETPAV8  CLC   DBSELUMK,CONMRKT                                                 
         BNE   GETPAV10                                                         
         OI    DEMOFLAG,X'08'                                                   
         MVC   DUB(1),CONINDIC                                                  
*                                                                               
GETPAV10 BXLE  R3,R4,GETPAV4                                                    
         SAC   0                                                                
         LAM   AR2,AR3,=2F'0'                                                   
         DROP  R3                                                               
*                                  TEST MARKET NUMBERS VALID                    
         TM    DEMOFLAG,X'C0'                                                   
         BM    GETPAV12                                                         
         TM    DEMOFLAG,X'30'                                                   
         BO    GETPAV12                                                         
*                                                                               
         TM    DEMOFLAG,X'08'      TEST SOURCE OVERRIDE PRESENT                 
         BZ    GETPAV14                                                         
         TM    DEMOFLAG,X'01'      YES - HAVE I BEEN HERE BEFORE                
         BNZ   GETPAV12                                                         
         MVI   DEMOFLAG,X'01'      NO - SET NEW SOURCE CODE & RETRY             
         XC    ACONHDR,ACONHDR                                                  
         XC    DBACTUAL,DBACTUAL                                                
         MVC   DBACTSRC,DUB                                                     
         MVC   DBACTMED,DBINTMED                                                
         B     GETPAV2                                                          
*                                  ERROR EXIT                                   
GETPAV12 MVI   DBERROR,INVMRKT                                                  
         B     GETX                                                             
*                                                                               
GETPAV14 DS    0H                                                               
         MVI   PVSELNDY,0          ASSUME "AVG-N" NOT REQUESTED                 
         CLI   DBFUNCT,DBGETPUR    TEST PURE DEMO REQUIRED                      
         BE    GETPAV16                                                         
         OC    DBSELPUR,DBSELPUR   TEST PURE NUMBER PASSED                      
         BNZ   GETPAV16                                                         
         LA    R1,X'80'                                                         
         CLI   DBSELDAY,X'FF'      TEST FOR ALL DAYS REQUIRED                   
         BE    GETPAV15                                                         
         CLI   DBSELDAY,X'7C'      TEST FOR ROTATORS (M-F, M-S)                 
         BE    GETPAV15                                                         
         CLI   DBSELDAY,X'7F'                                                   
         BE    GETPAV15                                                         
         LHI   R1,X'02'                                                         
         TM    DBSELDAY,X'90'      TEST FOR VAR OR AVG-N                        
         BO    GETPAV15                                                         
         SR    R1,R1                                                            
         CLI   DBSELDAY,X'03'      TEST SAT-SUN ROTATOR                         
         BNE   GETPAV15                                                         
         CLI   DBDAYOPT,C'Y'       TEST SPECIAL ROTATOR REQUESTED               
         BNE   GETPAV15                                                         
         TM    FMSIND1,FMS1SASU    TEST SPECIAL ROTATOR SUPPORTED               
         BZ    GETPAV15                                                         
         LA    R1,X'81'            SET FOR SPECIAL DQTAB ENTRY                  
*                                                                               
GETPAV15 DS    0C                  BUILD LIST OF DAYS/QTR HOURS                 
         XC    TPTIM2,TPTIM2                                                    
         CLI   DBSELMED,C'O'       OVERNIGHTS                                   
         BE    *+8                                                              
         CLI   DBSELMED,C'T'       FOR USTV                                     
         JNE   DYTMNOP                                                          
         CLI   DBSELSRC,C'N'       AND NSI                                      
         JNE   DYTMNOP                                                          
* SPECIAL CASE OF 600-545 MEANS ALL DAY                                         
         CLC   DBSELTIM(2),=AL2(600)                                            
         BNE   *+14                                                             
         CLC   DBSELTIM+2(2),=AL2(545)                                          
         BE    DYTMNOP                                                          
ALDAY5AX DS    0C                                                               
*                                                                               
         CLC   DBSELTIM+2(2),=X'0000'                                           
         JE    DYTMNOP                                                          
         CLC   DBSELTIM(2),DBSELTIM+2                                           
         JE    DYTMNOP                                                          
*                                                                               
*   TEST FOR NOW EFFECTIVE NOV_13 FOR TESTING PUPROSES                          
         CLI   DBSELMED,C'T'       FOR USTV                                     
         BNE   CHK5A00B                                                         
***      CLC   DBSELBK,=AL2(NOV_13)                                             
         CLC   DBSELBK,=AL2(DEC_15)                                             
         BL    CHK5AM02                                                         
         B     CHK5A00D                                                         
*                                                                               
CHK5A00B CLI   DBSELMED,C'O'       OVERIGHTS                                    
***      BNE   *+14                                                             
         BNE   CHK5AM02                                                         
***      CLC   DBSELBK,=X'7130'    DEC0113 WEEK FOR OVERNIGHTS                  
         CLC   DBSELBK,=AL1(YR_2015,WEEK_49) NOV30/15 WK FOR OVERNIGHTS         
         BL    CHK5AM02                                                         
*                                                                               
***      CLC   DBSELTIM(2),=AL2(300)                                            
***      BL    DYTMNOP                                                          
* EFFECTIVE 3A START TIME (DEC15)                                               
CHK5A00D CLC   DBSELTIM(2),=AL2(300)    ALLOW DAY SPLIT FROM 3AM                
         BNL   CHK5AM10                 END OF DAY IS 259                       
         MVC   DBSELTIM+2(2),=AL2(259)                                          
         B     DYTMNOP                                                          
CHK5AM02 CLC   DBSELTIM(2),=AL2(500)                                            
         BL    DYTMNOP                                                          
CHK5AM10 CLC   DBSELTIM(2),=AL2(559) START AFTER 6 IS OK                        
         BH    DYTMNOP                                                          
         CLC   DBSELTIM+2(2),=AL2(601) END BEFORE 6 IS OK                       
         BL    DYTMNOP                                                          
         MVC   TPTIM2(4),DBSELTIM  SET UP SPLIT TIMES                           
         MVC   DBSELTIM+2(2),=AL2(559)  END OF TIME1                            
         MVC   TPTIM2(2),=AL2(600)      START OF TIME2                          
*                                                                               
DYTMNOP  DS    0C                                                               
*&&DO                                                                           
* THIS WORKS IN ROVER                                                           
* BECAUSE THEY ACTUALLY SEND THE CORRECT START AND END TIME                     
* DEM32 SENDS 6A-545A REQUEST AND FILTERS IN THE PROGRAM                        
* FOR START TIME <300 THE END TIME CAN ONLY BE 2:59 LATEST                      
         CLC   DBSELTIM(2),=AL2(300)                                            
         BNL   GETPV15B                                                         
         CLC   DBSELTIM+2(2),=AL2(259)                                          
         BNH   *+10                                                             
         MVC   DBSELTIM+2(2),=AL2(259)                                          
*&&                                                                             
GETPV15B CLI   DBSELMED,C'O'       OVERNIGHT                                    
         BNE   *+8                                                              
         LA    R1,0                                                             
         GOTO1 AGETDQ              BUILD LIST OF DAYS/QTR HOURS                 
*                                                                               
         DS    0H                  GET REQUESTED # OF DAYS                      
         ZIC   RE,DBSELDAY                                                      
         SRDL  RE,4                                                             
         CHI   RE,9                 WAS "VAR" OR "AVG-N" REQUESTED?             
         BNE   *+12                  NOPE                                       
         SRL   RF,28                 YEP, SET REGISTER TO # DAYS REQ            
         STC   RF,PVSELNDY                                                      
*                                  GET STATION KEY VALUE (DBACTSTA)             
GETPAV16 OC    DBACTKMK,DBACTKMK                                                
         BNZ   *+10                                                             
         MVC   DBACTKMK,DBSELMK                                                 
         BAS   RE,GETCALL                                                       
         STC   R1,CALLEN           SAVE L'STATION CALL LETTERS                  
         OC    DBACTSTA,DBACTSTA   TEST ACTUAL SET                              
         BNZ   *+10                                                             
         MVC   DBACTSTA,DBSELSTA   NO - SET FROM SELECTED                       
         LA    RE,DBSELSTA(R1)                                                  
         LA    R1,DBACTSTA(R1)                                                  
         CLI   DBSELMED,C'R'       DON'T DO FOR RADIO                           
         BE    GETPAV18                                                         
         CLI   0(RE),C'T'          TEST P+S1/S2 REQUEST                         
         BNL   *+8                                                              
         MVI   0(R1),C'T'          NO - SET PARENT ONLY                         
*                                                                               
GETPAV18 MVC   SAVEBOOK,DBACTBK    SET LOOK-UP BOOK VALUE                       
*                                                                               
GETPAV20 LA    R2,DBKEY            BUILD KEY OF DEMO RECORD                     
         USING PRKEY,R2                                                         
         XC    PRKMAJOR,PRKMAJOR                                                
         MVI   PRCODE,PRCODEQU                                                  
         MVC   PRMEDIA,DBINTMED                                                 
         MVC   PRSRC,DBACTSRC                                                   
         MVC   PRSTAT,DBACTSTA                                                  
         MVC   PRKMKT,DBACTKMK                                                  
         MVC   PRBOOK,SAVEBOOK                                                  
         MVC   PRSTYP,DBSTYPE                                                   
         MVC   PRBTYP,DBBTYPE                                                   
*                                                                               
GETPAV21 MVI   IOFLAG,DIR+HIGH+PAV READ HIGH/DIRECTORY                          
         GOTO1 AIO,IOFLAG                                                       
         BL    GETX                                                             
*                                  CHECK STATION FOUND                          
         CLC   PRKEY(PRBOOK-PRKEY),KEYSAVE                                      
         BE    GETPAV26                                                         
*                                  DEAL WITH SATELLITES HERE                    
GETPAV22 DS    0H                                                               
*                                  DEAL WITH EQUIVALENCING                      
GETPAV24 TM    DEMOFLAG,X'04'      TEST ALREADY EQUIVALENCED                    
         BO    GETPAV30                                                         
         CLC   DBACTSTA(5),=C'WGKI1' SPECIAL S1 SITUATION                       
         BNE   GETPAV24A                                                        
         MVC   DBACTSTA(5),=C'WFQX1'                                            
         B     GETPAV25                                                         
GETPAV24A DS   0H                                                               
         CLC   DBACTSTA(5),=C'WFQX1' SPECIAL S1 SITUATION                       
         BNE   GETPAV24B                                                        
         MVC   DBACTSTA(5),=C'WGKI1'                                            
         B     GETPAV25                                                         
GETPAV24B DS   0H                                                               
*                                                                               
         CLC   DBACTSTA(5),=C'WAZE1' SPECIAL S1 SITUATION                       
         BNE   GETPAV24C                                                        
         MVC   DBACTSTA(5),=C'WWAZ1'                                            
         B     GETPAV25                                                         
GETPAV24C DS   0H                                                               
*                                                                               
         CLC   DBACTSTA(5),=C'WB16T' SPECIAL SITUATION                          
         BNE   GETPAV24D                                                        
         MVC   DBACTSTA(5),=C'WB26T'                                            
         B     GETPAV25                                                         
GETPAV24D DS   0H                                                               
*                                                                               
         CLI   DBACTSTA+4,C'1'     PRE-EQUATE S1 TO PARENT                      
         BNE   *+12                                                             
         MVI   DBACTSTA+4,C'T'                                                  
         BE    GETPAV16                                                         
*                                                                               
         MVI   DUB+5,3                                                          
         GOTO1 AGETEQIV            NO - GET EQUIVALENT                          
         BNE   GETPAV30                                                         
         MVC   DUB+4(1),DBACTSTA+4                                              
         MVC   DBACTSTA,DUB        SET NEW STATION CALL LETTERS                 
         CLI   DBACTSTA+4,C' '     SET DEFAULT SUFFIX                           
         BNE   *+8                                                              
         MVI   DBACTSTA+4,C'T'                                                  
GETPAV25 OI    DEMOFLAG,X'04'                                                   
         B     GETPAV16                                                         
*                                  DEAL WITH BOOK                               
GETPAV26 DS    0H                                                               
*                                                                               
         CLI   PRPNMFLG,X'97'      SHOULDN'T GET THE PGM NAME REC               
         BE    *+14                                                             
         CLC   PRBTYP,DBBTYPE      BOOKTYPE MISMATCH                            
         BE    GETPAV27                                                         
         CLI   PRMEDIA,C'O'        OVERNITE PAV?                                
         BNE   GETPAV27                                                         
         CLI   PRSRC,C'N'                                                       
         BNE   GETPAV27                                                         
*                                                                               
         CLI   DBBTYPE,BOOKTYPE_C                                               
         BNE   *+12                                                             
         MVI   DBBTYPE,BOOKTYPE_U                                               
         B     GETPAV20                                                         
*                                                                               
         CLI   DBBTYPE,BOOKTYPE_W                                               
         BNE   *+12                                                             
         MVI   DBBTYPE,BOOKTYPE_Z                                               
         B     GETPAV20                                                         
*                                                                               
         CLI   DBBTYPE,BOOKTYPE_H                                               
         BNE   *+12                                                             
         MVI   DBBTYPE,BOOKTYPE_J                                               
         B     GETPAV20                                                         
*                                                                               
         CLI   DBBTYPE,BOOKTYPE_STANDARD  TRIED L ALREADY?                      
         BNE   GETPAV27                                                         
         MVI   DBBTYPE,BOOKTYPE_L  IF NOT, TRY L                                
         B     GETPAV20                                                         
*                                                                               
GETPAV27 OC    SAVEBOOK,SAVEBOOK   TEST IF FAST SERVICE BOOK                    
         BNZ   GETPAV28                                                         
         MVC   SAVEBOOK,PRBOOK     YES - DID I FIND CORRECT RECORD              
         XC    PRBOOK,PRBOOK                                                    
         CLC   PRKEY(L'PRKMAJOR),KEYSAVE                                        
         BNE   GETPAV20                                                         
         MVC   PRBOOK,SAVEBOOK     NO - GO READ FOR BOOK I FOUND                
         B     GETPAV32                                                         
*                                  DEAL WITH OTHER KEY FIELDS                   
GETPAV28 CLC   PRKEY(L'PRKMAJOR),KEYSAVE                                        
         BE    GETPAV32                                                         
         B     GETPAV22                                                         
*                                  SET NOT FOUND                                
GETPAV30 MVI   DBERROR,NOTFOUND                                                 
         B     GETX                                                             
*                                  DIRECTORY ITEM FOUND                         
GETPAV32 MVC   DBACTBK,PRBOOK                                                   
*                                  DEAL WITH PURE NUMBERS                       
         CLI   DBFUNCT,DBGETPUR    TEST IF PURE DEMO REQUIRED                   
         BE    *+14                                                             
         OC    DBSELPUR,DBSELPUR   OR PURE NUMBER PASSED                        
         BZ    GETPAV34                                                         
         BAS   RE,TIMCHG                                                        
         MVC   DBMINKEY,DBSELPUR   READ FOR PURE NUMBER                         
         MVI   IOFLAG,READ+FILE+PAV                                             
         GOTO1 AIO,IOFLAG                                                       
         BNE   GETX                                                             
*                                                                               
* DEIS APR/2018: SEE WHETHER WE ARE TRYING TO READ AN "HISTORICAL"              
* LOCAL MONTHLIES BOOK FOR WHICH WE NO LONGER HAVE TIME PERIOD DATA.            
         BAS   RE,TESTHIST         CHECK FOR VALID MARKET                       
         BNE   GETX                UNAUTHORIZED: JUST EXIT                      
*                                                                               
* LPM PRELIM VS PARALLEL CHECKING                                               
         CLI   DBBTYPE,BOOKTYPE_I  HISPANIC LPM BOOK                            
         JE    *+8                                                              
         CLI   DBBTYPE,BOOKTYPE_P  LPM BOOK                                     
         JNE   GETPAV33                                                         
         CLI   PRELIMOK,C'Y'                                                    
         JE    GETPAV33                                                         
         J     GETPAV33           ***DISABLE PRELIM CHECKING FOR NOW***         
         L     RF,DBAREC                                                        
         AH    RF,DBDTADSP                                                      
         USING MARELEM,RF                                                       
         CLC   MARNO,=H'101'                                                    
         JNE   GETPAV30                                                         
         DROP  RF                                                               
         CLC   PRBOOK,=AL2(MAY_04) SET NOT FOUND - IT'S PRELIM DATA             
         JL    GETPAV30                                                         
*                                                                               
GETPAV33 DS    0C                                                               
         BAS   RE,GETPAVD                                                       
         MVC   SVQHSQH,DUB                                                      
         MVC   SVQHEQH,DUB+6                                                    
         B     GETPAV47                                                         
*                                  SEQUENTIAL DEMO READING                      
GETPAV34 XC    DBFACTOR,DBFACTOR                                                
         ZIC   R1,DBDQPTR                                                       
         CLI   DBLSTNXT,0          GET NEXT DQTAB ENTRY                         
         BNE   *+8                                                              
         LA    R1,1(R1)            YES - BUMP POINTER NUMBER                    
         STC   R1,DBDQPTR                                                       
         LA    R0,DBDQLEN                                                       
         MR    R0,R0                                                            
         LA    R3,DBDQTAB-DBDQLEN(R1)                                           
         ST    R3,ADQNTRY          SET A(DQTAB ENTRY)                           
*                                  GET FIRST RECORD FOR QTR HOUR/DAY            
         CLI   DBLSTNXT,0                                                       
         BNE   GETPAV40                                                         
         CLI   0(R3),EOT           TEST END OF DQTAB                            
         BNE   DYTMLX                                                           
*                                                                               
*                                                                               
GETTPXLA CLC   TPTIM2(2),=X'0000'  CHECK FOR SPLIT DAY/TIME                     
         BE    GETTPXLB                                                         
         MVC   DBSELTIM(4),TPTIM2  AND SET IF THER                              
         XC    TPTIM2,TPTIM2                                                    
         MVI   DBLSTNXT,0                                                       
         MVI   DBDQPTR,0                                                        
         XC    DBDQTAB,DBDQTAB     <--BE SURE USING R6=DBLOCKD                  
         B     GETPAV14                                                         
GETTPXLB CLC   TPSVTIM(2),=X'0000' OTHERWISE RESTORE ORIG IF THERE              
         BE    DYTMC0                                                           
         MVC   DBSELTIM(4),TPSVTIM                                              
*                                                                               
DYTMC0   L     RE,DBEXTEND         CHECK FOR MULIPLE DAY/TIME                   
NXT03    DS    0H                                                               
         LTR   RE,RE               NO MULTI D/T   - END IT                      
         BZ    DYTMEOF                                                          
         USING DBXTLID,RE                                                       
         CLC   DBXTLID,=C'DYTM'    THIS IS THE ONE WE WANT                      
         BE    *+12                                                             
         L     RE,DBXTLNXT                                                      
         B     NXT03                                                            
*                                                                               
         ZIC   RF,DBXTLIDX                                                      
         LA    RF,1(RF)                                                         
         STC   RF,DBXTLIDX                                                      
         MH    RF,=H'5'                                                         
         LA    RF,DBXTLIST(RF)                                                  
         CLI   0(RF),0                                                          
         BE    DYTMEOF             END OF DAY TIME LIST                         
         DROP  RE                                                               
         MVC   DBSELDAY(5),0(RF)                                                
         MVI   DBLSTNXT,0                                                       
         MVI   DBDQPTR,0                                                        
         XC    DBDQTAB,DBDQTAB     <--BE SURE USING R6=DBLOCKD                  
         B     GETPAV14                                                         
DYTMEOF  DS    0H                                                               
         MVI   DBERROR,EOF         YES - SET E-O-F & EXIT                       
         B     GETX                                                             
*                                                                               
DBDQ     USING DBDQD,R3                                                         
DYTMLX   MVC   DBMINKEY(1),DBDQ.DBDQSQH                                         
         MVC   DBMINKEY+1(1),DBDQ.DBDQKDAY                                      
         CLI   DBSELDAY,X'FF'      TEST IF ALL DAYS REQUIRED                    
         BNE   *+8                                                              
         MVI   DBMINKEY+1,0                                                     
*                                                                               
GETPAV36 LA    R0,12               R0=MAXIMUM LOOP COUNT                        
         MVI   DEMOFLAG,0          SET NO E-O-F HIT                             
GETPAV37 MVI   IOFLAG,FILE+HIGH+PAV                                             
         GOTO1 AIO,IOFLAG                                                       
         BL    GETX                                                             
         BE    GETPAV38                                                         
         MVI   DEMOFLAG,1          SET E-O-F HIT                                
         CLI   DBLSTNXT,0          TEST IF FIRST TIME THROUGH                   
         BE    *+12                                                             
         MVI   DBLSTNXT,0          NO -  GET NEXT DQTAB ENTRY                   
         B     GETPAV34                                                         
         ZIC   R1,DBMINKEY         END-OF-BOOK - DECREMENT QTR HOUR             
         SH    R1,=H'1'                                                         
         BM    GETPAV34                                                         
         STC   R1,DBMINKEY                                                      
         BCT   R0,GETPAV37         DO FOR MAXIMUM LOOP COUNT                    
         B     GETPAV34                                                         
*                                                                               
GETPAV38 DS    0H                                                               
* DEIS APR/2018: SEE WHETHER WE ARE TRYING TO READ AN "HISTORICAL"              
* LOCAL MONTHLIES BOOK FOR WHICH WE NO LONGER HAVE TIME PERIOD DATA.            
         BAS   RE,TESTHIST         CHECK FOR VALID MARKET                       
         BNE   GETX                UNAUTHORIZED: JUST EXIT                      
*                                                                               
*                                  CHECK QTR HOUR/DAY                           
         BAS   RE,GETPAVD                                                       
         CLI   DBSELDAY,X'FF'      TEST IF ALL DAYS REQUIRED                    
         BE    GETPAV44                                                         
         MVC   WORK(1),DBMINKEY+1                                               
         NI    WORK,X'F0'                                                       
         CLC   DUB+2(1),WORK       DID I FIND RECORD FOR REQUESTED DAY          
         BNE   GPV38DYX             NO                                          
                                                                                
         CLI   PVNDAYS,0           SEE IF # OF DAYS IS AVAILABLE                
         BE    GPV38DYM             NO, CAN'T MATCH ANYMORE ON DAY CODE         
         CLI   DUB+2,X'80'         THIS DAY CODE CAN BE 'M-SU' OR 'VAR'         
         BNE   GPV38DYG                                                         
         LHI   RF,X'70'             MASK FOR BRANCH-NOT-EQUAL                   
         TM    DBSELDAY,X'90'       IF 'VAR' OR 'AVG-N' REQUESTED,              
         BNO   *+8                                                              
         LHI   RF,X'B0'              USE MASK FOR BRANCH-NOT-LOW                
*                                  *** EXECUTED BC AFTER THIS CLI ***           
         CLI   PVNDAYS,7            DETERMINE IF RECD IS 'M-S' OR 'VAR'         
         EX    RF,*+8              *** BRANCH MASK IS IN RF ***                 
         B     *+8                                                              
         BC    0,GPV38DYR           BRANCH IF RECD DOESN'T FIT REQUEST          
GPV38DYG EQU   *                                                                
                                                                                
         CLI   PVSELNDY,0          WAS # OF DAYS REQUESTED?                     
         BE    GPV38DYM             NO                                          
         CLC   PVSELNDY,PVNDAYS     YES, DO # OF DAYS MATCH?                    
         BNE   GPV38DYR              NOPE                                       
GPV38DYM EQU   *                                                                
         B     GETPAV44                                                         
                                                                                
GPV38DYR DS    0H                  DAY CODE MATCHES, NDAYS DOESN'T              
         CLC   DUB(1),DBDQ.DBDQEQH TEST RECD SQH > RQST EQH                     
         BH    GPV38DYX              YEP, DON'T BOTHER                          
         MVI   IOFLAG,FILE+SEQ+PAV                                              
         GOTO1 AIO,IOFLAG                                                       
         BL    GETX                                                             
         BE    GETPAV38                                                         
         MVI   DEMOFLAG,1          SET E-O-F HIT                                
         B     GETPAV43                                                         
GPV38DYX EQU   *                                                                
*                                                                               
         DS    0H                                                               
         CLC   DUB(1),DBDQ.DBDQEQH TEST SQH GR REQUESTED EQH                    
         BH    GETPAV42                                                         
         ZIC   R1,DUB              GET SQH OF RECORD JUST READ                  
         CLC   DUB(1),DBMINKEY     TEST IF SAME AS KEY                          
         BNE   *+12                                                             
         LA    R1,1(R1)            YES - BUMP QTR HR                            
         B     GETPAV39                                                         
*                                                                               
GPV38ON  CLC   DUB+2(1),WORK       TEST DAY OF RECORD GR KEY DAY                
         BL    *+8                                                              
         LA    R1,1(R1)            YES - BUMP QTR HR                            
GETPAV39 STC   R1,DBMINKEY         SET NEXT QTR HR IN KEY                       
         MVC   DBMINKEY+1(1),DBDQ.DBDQKDAY                                      
         MVI   DBLSTNXT,1                                                       
         B     GETPAV36                                                         
*                                  GET NEXT QTR HOUR/DAY RECORD                 
*                                                                               
* LPM PRELIM VS PARALLEL CHECKING                                               
GETPAV40 CLI   DBBTYPE,BOOKTYPE_I  HISPANIC LPM BOOK                            
         BE    *+8                                                              
         CLI   DBBTYPE,BOOKTYPE_P  LPM BOOK                                     
         BNE   GETPAV40A                                                        
         CLI   PRELIMOK,C'Y'                                                    
         BE    GETPAV40A                                                        
         B     GETPAV40A         DISABLE PRELIM CHECKING FOR NOW                
         L     RF,DBAREC                                                        
         AH    RF,DBDTADSP                                                      
         USING MARELEM,RF                                                       
         CLC   MARNO,=H'101'                                                    
         BNE   GETPAV40A                                                        
         DROP  RF                                                               
         CLC   PRBOOK,=AL2(FEB_04) SET NOT FOUND - IT'S PRELIM DATA             
         BL    GETPAV30                                                         
*                                                                               
GETPAV40A CLI   DBBEST,C'A'         TEST IF ALL RECORDS REQUESTED               
         BE    *+12                                                             
         CLI   DBSELDAY,X'FF'      TEST IF ALL DAYS REQUIRED                    
         BNE   GETPAV41                                                         
         SR    R1,R1                                                            
         ICM   R1,3,DBMINKEY       YES - BUMP MINOR KEY BY 1                    
         LA    R1,1(R1)                                                         
         STCM  R1,3,DBMINKEY                                                    
         B     GETPAV43                                                         
*                                  BUMP TO NEXT QTR HOUR FOR DAY                
GETPAV41 ZIC   R1,DBMINKEY                                                      
         LA    R1,1(R1)            BUMP TO NEXT QTR HOUR                        
         STC   R1,DBMINKEY                                                      
         MVC   DBMINKEY+1(1),DBDQ.DBDQKDAY                                      
         B     GETPAV43                                                         
*                                  SET TO GET NEXT DQTAB ENTRY                  
GETPAV42 MVI   DBLSTNXT,0                                                       
         B     GETPAV34                                                         
*                                                                               
GETPAV43 CLI   DEMOFLAG,0          TEST E-O-F HIT ON PREV READ                  
         BE    GETPAV36            NO - GO READ RECORD                          
         MVI   DBLSTNXT,0          YES - BUMP TO NEXT DQTAB ENTRY               
         B     GETPAV34                                                         
*                                  CHECK QTR HOUR/DAY MATCH                     
GETPAV44 CLC   DUB(1),DBDQ.DBDQEQH                                              
         BH    GETPAV42                                                         
         CLC   DUB+6(1),DBDQ.DBDQSQH                                            
         BL    GETPAV40                                                         
         CLI   DBSELDAY,X'FF'      TEST IF ALL DAYS REQUIRED                    
         BE    GETPAV45                                                         
         MVC   WORK(1),DBMINKEY+1                                               
         NI    WORK,X'F0'                                                       
         CLC   DUB+2(1),WORK       DID I FIND RECORD FOR REQUESTED DAY          
         BH    GETPAV42                                                         
         BL    GETPAV40                                                         
                                                                                
         CLI   PVNDAYS,0           SEE IF # OF DAYS IS AVAILABLE                
         BE    GPV44DYX             NO, CAN'T MATCH ANYMORE ON DAY CODE         
         CLI   DUB+2,X'80'         THIS DAY CODE CAN BE 'M-SU' OR 'VAR'         
         BNE   GPV44DYG                                                         
         CLI   DBSELDAY,X'7F'       IF 'M-SU' REQUESTED,                        
         BNE   *+12                                                             
         CLI   PVNDAYS,7             RECD HAD BETTER BE 7 DAYS                  
         BNE   GETPAV40                                                         
         TM    DBSELDAY,X'90'       IF 'VAR' REQUESTED,                         
         BNO   *+12                                                             
         CLI   PVNDAYS,7             RECD HAD BETTER BE LESS THAN 7 DYS         
         BNL   GETPAV40                                                         
GPV44DYG EQU   *                                                                
                                                                                
         CLI   PVSELNDY,0          WAS # OF DAYS REQUESTED?                     
         BE    GPV44DYX             NO                                          
         CLC   PVSELNDY,PVNDAYS     YES, DO # OF DAYS MATCH?                    
         BNE   GETPAV40              NO                                         
GPV44DYX EQU   *                                                                
                                                                                
         DS    0H                                                               
         CLI   DBBEST,C'A'         TEST IF ALL RECORDS REQUESTED                
         BE    GETPAV45                                                         
         CLI   PVNORMAL,C'Y'       TEST IF FULL CYCLE                           
         BE    *+12                                                             
         CLI   DUB+7,2             AND WEEKS GEQ 2                              
         BNL   GETPAV45            YES - THIS RECORD IS BEST                    
         SR    R1,R1               NO - GET NEXT RECORD                         
         ICM   R1,3,DBMINKEY                                                    
         LA    R1,1(R1)                                                         
         STCM  R1,3,DBMINKEY                                                    
         B     GETPAV43                                                         
*                                  CALCULATE NUMBER OF QUARTER HOURS            
GETPAV45 MVI   DBLSTNXT,1                                                       
         MVC   DBMINKEY,DUB        SET NEXT QUARTER HOUR IN KEY                 
         MVC   SVQHSQH,DUB                                                      
         MVC   SVQHEQH,DUB+6                                                    
         CLC   DUB(1),DBDQ.DBDQSQH                                              
         BH    *+10                                                             
         MVC   DUB(1),DBDQ.DBDQSQH                                              
         CLC   DUB+6(1),DBDQ.DBDQEQH                                            
         BNH   GETPAV46                                                         
         MVC   DUB+6(1),DBDQ.DBDQEQH                                            
         CLI   DBBEST,C'A'         TEST FOR 'ALL' RECORDS                       
         BE    *+8                 YES-COULD BE MORE RECORDS FOR DAY            
         MVI   DBLSTNXT,0          NO-FORCE NEXT DQTAB ENTRY                    
*                                                                               
GETPAV46 MVC   DBACTSQC,DUB        RETURN ACTUAL START/END QTR HOUR             
         MVC   DBACTEQC,DUB+6                                                   
*                                  SET FACTOR & RETURN TO CALLER                
GETPAV47 ZIC   R0,DUB                                                           
         ZIC   R1,DUB+6                                                         
         CLI   DBPRGDUR,C'Y'                                                    
         BNE   GETPAV47A                                                        
         ZIC   R0,SVQHSQH          NEED FULL DURATION                           
         ZIC   R1,SVQHEQH                                                       
GETPAV47A DS   0C                                                               
         LA    R1,1(R1)                                                         
         SR    R1,R0                                                            
*                                  CALCULATE DAY FACTOR                         
         LA    R0,5                                                             
         CLI   DUB+2,X'00'         TEST M-F                                     
         BE    GETPAV48                                                         
         CLI   DUB+2,X'90'         TEST VARIOUS                                 
         BE    GETPAV48                                                         
         LA    R0,7                                                             
         CLI   DUB+2,X'80'         TEST M-S                                     
         BE    GETPAV48                                                         
         LA    R0,1                                                             
*                                                                               
GETPAV48 MR    R0,R0                                                            
*                                                                               
*  BPOO OCT05/00   CHECK  ONE LAST TIME TO MAKE SURE                            
*   VAR AND M-S DOES NOT CONFLICT - PNVDAYS CANT BE ZERO FOR VAR                
*                                                                               
         CLI   DBSELDAY,X'90'      IF VAR REQUESTED                             
         BNE   GETPAV49                                                         
         CLI   DUB+2,X'80'         TEST M-S                                     
         BNE   GETPAV49                                                         
         CLI   PVNDAYS,0                                                        
         BE    GETPAV34                                                         
GETPAV49 DS    0H                                                               
* ABC INSISTS THAT THEY WANT WEEKLY WEIGHTING 3/25/98                           
* WHEN YOU GET HERE BECAUSE THEY DONT - CURSE DAVE DANIELS                      
         CLI   DBPRGDUR,C'Y'       ABC WEEK WEIGHTING                           
         BNE   ABCCRAPX            NO - IGNORE THIS CRAP                        
         ZIC   R0,DUB+7            YES - WEIGHT BY NUMBER OF WEEKS              
         MR    R0,R0                                                            
ABCCRAPX DS    0C                                                               
*                                                                               
         STH   R1,DBFACTOR         SET FACTOR                                   
         AH    R1,DBDIVSOR                                                      
         STH   R1,DBDIVSOR         SET TOTAL FACTOR                             
*                                                                               
* FOR PAV OVERNIGHTS WE ACTUALLY GOT THE DEMO NUMBERS BY ITS PROGRAM            
* TOTAL DURATION WHERE AS WE NORMALLY GET THEM AVERGED BY THE QH HOURS          
* FORCE DBFACTOR TO 1 AND DBDIVSOR = DURATION TO GET THE NUMBERS TO             
* MATCH THE TP NUMBERS                                                          
*                                                                               
*                                                                               
         CLI   DBSELMED,C'O'                                                    
         BNE   GETPAV50                                                         
         MVC   DBFACTOR,=H'1'                                                   
         ZIC   R0,SVQHSQH          NEED FULL DURATION                           
         ZIC   R1,SVQHEQH                                                       
         OR    R1,R1              IF END TIME = BEGINNING OF DAY                
         BNZ   *+8                THEN MAKE BELIEVE ITS THE 96TH                
         LA    R1,96              QTR HOUR                                      
         LA    R1,1(R1)                                                         
         SR    R1,R0                                                            
         STH   R1,DBDIVSOR                                                      
*                                                                               
GETPAV50 MVI   DBRECTYP,DBRECDEM                                                
         MVI   DBMODE,DBMFRST                                                   
         CLI   DBFUNCT,DBGETPUR                                                 
         BE    PUREX                                                            
         OC    DBSELPUR,DBSELPUR   TEST IF PURE NUMBER PASSED                   
         BNZ   PUREX                                                            
         MVI   DBMODE,DBMNEXT      NO - SET SEQUENTIAL MODE                     
         MVC   SVINTFIL,DBINTFIL                                                
         BAS   RE,GETX                                                          
         MVC   DBINTFIL(2),SVINTFIL                                             
         B     GETPAV34            GET NEXT RECORD                              
         DROP  DBDQ                                                             
         DROP  R2                                                               
         EJECT                                                                  
*                                                                               
PUREX    L     RE,DBEXTEND         CHECK FOR MULIPLE DAY/TIME                   
NXT04    DS    0H                                                               
         LTR   RE,RE               NO MULTI D/T   - END IT                      
         BZ    GETX                                                             
         USING DBXPUID,RE                                                       
         CLC   DBXPUID,=C'PURE'    THIS IS THE ONE WE WANT                      
         BE    *+12                                                             
         L     RE,DBXPUNXT                                                      
         B     NXT04                                                            
*                                                                               
         ZIC   RF,DBXPUIDX                                                      
         LA    RF,1(RF)                                                         
         STC   RF,DBXPUIDX                                                      
         MH    RF,=H'2'                                                         
         LA    RF,DBXPULST(RF)                                                  
         DROP  RE                                                               
         CLC   0(2,RF),=X'FFFF'                                                 
         BE    GETX                END OF DAY TIME LIST                         
         MVI   DBMODE,DBMNEXT      NO - SET SEQUENTIAL MODE                     
         MVC   SVINTFIL,DBINTFIL                                                
         BAS   RE,GETX                                                          
         MVC   DBINTFIL(2),SVINTFIL                                             
         MVC   DBSELPUR(2),0(RF)                                                
         MVI   DBLSTNXT,0                                                       
         MVI   DBDQPTR,0                                                        
         XC    DBDQTAB,DBDQTAB                                                  
         B     GETPAV16                                                         
         EJECT                                                                  
*                                                                               
GETX     CLI   DBERROR,0           TEST IF ERROR SET                            
         BE    *+14                                                             
         XC    DBAQUART,DBAQUART   YES - CLEAR QTR HOUR ADDRESS                 
         MVI   DBMODE,DBMFRST      AND RESET SEQUENTIAL MODE                    
         MVC   DBLSTFNC,DBFUNCT    EQUATE LAST FUNCTION TO THIS                 
         OC    AUSERRTN,AUSERRTN   TEST IF USER HOOK SUPPLIED                   
         BZ    GETXX               NO - RETURN TO CALLER                        
         CLI   DBERROR,0           TEST IF ERROR SET                            
         BNE   GETXX               YES - RETURN TO CALLER                       
         CLI   DBMODE,DBMNEXT      TEST IF SEQUENTIAL MODE                      
         BE    *+8                                                              
         LA    RE,GETXX            NO - SET RETURN TO EXIT                      
*                                  SAVE REGS & GO TO USER HOOK                  
         NTR1                                                                   
         L     RF,AUSERRTN                                                      
         L     RE,ROOTRD                                                        
         LR    R0,RE               R0=A(CALLER'S RD)                            
         LM    R1,RC,24(RE)        R1-RC=CALLERS REGISTERS                      
         BASR  RE,RF               GO TO USER HOOK                              
*                                                                               
GETXX    XIT1  ,                   EXIT TO CALLER OR DEMAND                     
         EJECT                                                                  
* GET VALUES FROM A PAV DEMO RECORD.                                            
*                                                                               
* ON EXIT DUB+0(1)=START QTR HOUR OF THIS RECORD                                
*         DUB+1(1)=DAY/WEEK OF THIS RECORD                                      
*         DUB+2(1)=DAY OF THIS RECORD                                           
*         DUB+3(1)=DURATION IN QTR HOURS                                        
*         DUB+4(1)=START QTR HOUR OF PREVIOUS RECORD                            
*         DUB+5(1)=DAY/WEEK OF PREVIOUS RECORD                                  
*         DUB+6(1)=END QTR HOUR OF RECORD                                       
*         DUB+7(1)=NUMBER OF WEEKS                                              
*         PVNORMAL='Y' IF NORMAL, 'N' IF FULL CYCLE                             
*                                                                               
GETPAVD  L     RF,DBAREC                                                        
         MVC   DUB(2),PRKMINOR-PRKEY(RF)                                        
         MVC   DUB+2(1),DUB+1                                                   
         NI    DUB+2,X'F0'                                                      
         AH    RF,DBDTADSP                                                      
         USING PHELEM,RF                                                        
         SR    R0,R0                                                            
GETPAVD2 CLI   PHCODE,0            FIND QTR HOUR ELEMENT                        
         BNE   *+6                                                              
         DC    H'0'                                                             
         CLI   PHCODE,PHCODEQ                                                   
         BE    *+14                                                             
         IC    R0,1(RF)                                                         
         AR    RF,R0                                                            
         B     GETPAVD2                                                         
         ST    RF,DBAQUART                                                      
         MVC   DUB+3(1),PHDUR                                                   
         MVC   DUB+4(1),PHPRVST                                                 
         MVC   DUB+5(1),PHPRVDW                                                 
         MVC   DUB+7(1),PHDWKS                                                  
         NI    DUB+7,X'0F'                                                      
         MVI   PVNORMAL,C'Y'                                                    
         CLI   PHDTYPE,0                                                        
         BE    *+8                                                              
         MVI   PVNORMAL,C'N'                                                    
*                                                                               
         DS    0H                  GET NUMBER OF DAYS                           
         MVI   PVNDAYS,0                                                        
         CLI   PHREVID,X'FF'                                                    
         BNE   *+18                                                             
         CLI   PHREV,2                                                          
         BL    *+10                                                             
         MVC   PVNDAYS,PHNDAYS                                                  
*                                                                               
         DS    0H                                                               
         ZIC   RF,DUB+3                                                         
*                                                                               
         CLI   DBSELMED,C'O'       OVERNIGHT HAS ODD NUMBER OF QTR              
         BNE   *+12                                                             
         CLI   DBSELSRC,C'N'                                                    
         BE    GETPAVD6                                                         
*                                                                               
         SRL   RF,1                FORCE TO EVEN NUMBER OF QTR HRS              
         SLL   RF,1                                                             
         LTR   RF,RF                                                            
         BNZ   *+8                                                              
         LA    RF,2                DURATION SHOULD NOT BE LT 2                  
*                                                                               
GETPAVD6 STC   RF,DUB+3                                                         
         ZIC   R0,DUB              GET START QTR HR                             
         AR    RF,R0                                                            
         BCTR  RF,0                                                             
         STC   RF,DUB+6            SET END QTR HOUR                             
*                                  PRIOR MAY/86 WAS GARBAGE                     
*                                    DON'T NEED ANY MORE                        
*        CLI   DBACTSRC,C'A'       TEST SOURCE FOR ARB                          
*        BNE   *+16                                                             
*        CLI   DUB+7,X'02'         AND WEEKS=X'02'                              
*        BNE   *+8                                                              
*        MVI   DUB+7,X'03'         YES - S/B 2WEEKS                             
         ZIC   RF,DUB+7                                                         
         IC    RF,WEEKTAB(RF)                                                   
         STC   RF,DUB+7                                                         
                                                                                
*                                                                               
         DS    0H                                                               
         BR    RE                                                               
         DROP  RF                                                               
         EJECT                                                                  
* EXTRACT STATION CALL LETTERS FROM DBLOCK INTO DUB                             
*                                                                               
* ON EXIT R1=L'STATION CALL LETTERS (EXCLUDING TYPE)                            
*                                                                               
GETCALL  MVI   DUB,C' '                                                         
         MVC   DUB+1(4),DUB                                                     
         MVC   DUB(4),DBSELSTA                                                  
         LA    R1,4                                                             
         CLI   DBSELMED,C'R'                                                    
         BNER  RE                                                               
         MVC   DUB(5),DBSELSTA     RADIO HAS 5 CHAR CALL LETTERS                
         LA    R1,5                                                             
         BR    RE                                                               
         EJECT                                                                  
*********THIS ROUTINE IS DISABLED PER KATZ 4/23/97*********                     
TIMCHG   CLI   DBTIMCHG,0                                                       
         BR    RE                  DISABLE THIS ROUTINE.                        
         BER   RE                                                               
         CLI   DBFUNCT,DBGETPUR    ALLOW ZERO FOR THIS FUNCTION                 
         BE    *+12                                                             
         OC    DBSELPUR,DBSELPUR   NO PURE - DONT ADJUST                        
         BZR   RE                                                               
         CLI   DBTIMCHG,C'S'       GIVEN SUMMER TIMES                           
         BE    TIMCHG2             ADJUST FALL AND WINTER                       
*                                                                               
* GIVEN A FALL TIME -60 FOR MAY AND JUL                                         
         CLI   DBTIMCHG,C'F'       MAKE SURE FLAG IS VALID                      
         BNER  RE                                                               
         CLI   DBACTBK+1,MON_MAY   MAY BOOK - NEEDS ADJUSTMENT                  
         BE    *+10                                                             
         CLI   DBACTBK+1,MON_JUL   JULY BOOK - NEEDS ADJUSTMENT                 
         BNER  RE                   OTHERS DONT                                 
*                                                                               
         CLI   DBSELPUR,4          BOUNDARY CONDITIONS                          
         BH    *+8                                                              
         MVI   DBSELPUR,5                                                       
*                                                                               
         ZIC   R1,DBSELPUR                                                      
         SH    R1,=H'4'                                                         
         STC   R1,DBSELPUR                                                      
         BR    RE                                                               
*                                                                               
* GIVEN A SUMMER TIME +60 MIN FOR ALL EXCEPT MAY/JUL                            
TIMCHG2  CLI   DBACTBK+1,MON_MAY   MAY BOOK - DONT ADJUST                       
         BER   RE                                                               
         CLI   DBACTBK+1,MON_JUL   JULY BOOK - DONT ADJUST                      
         BER   RE                                                               
*                                                                               
         CLI   DBSELPUR,93         BOUNDARY CONDITIONS                          
         BL    *+8                                                              
         MVI   DBSELPUR,92                                                      
*                                                                               
         ZIC   R1,DBSELPUR                                                      
         AH    R1,=H'4'                                                         
         STC   R1,DBSELPUR                                                      
         BR    RE                                                               
         EJECT                                                                  
TESTHIST NTR1  ,                                                                
*                                                                               
* WE PURGE HISTORICAL NIELSEN LOCAL MONTHLIES *TIME PERIOD* DATA TO             
* RECLAIM DISK SPACE, BUT WE ARE *NOT* PURGING THE ASSOCIATED *PAV*             
* DATA. BECAUSE DISK SPACE ON PAVDIR/FIL IS NOT AT A CRITICAL LEVEL,            
* IT ISN'T WORTH THE TIME/EFFORT/RISK TO PURGE THE DATA. INSTEAD, WE            
* *UNAUTHORIZE* HISTORICAL PAV BOOKS, THEREBY HIDING THE PAV DATA FROM          
* USERS. THIS PERTAINS ONLY TO "PTN" RECORDS (NOT "ITN" PASSIVES).              
*                                                                               
* NOTE THAT THERE ARE ACTUALLY *TWO* RELATED, BUT DIFFERENT THINGS              
* HAPPENING IN THIS SUBROUTINE:                                                 
*                                                                               
*  1. FOR FQA ONLY:                                                             
*      FQA RELIES ON THE EXISTENCE OF SPECIFIC BOOK/MARKET COMBINATIONS         
*      FOR ITS REGRESSION TESTING. SO UNDER FQA (ONLY), WE CHECK THE            
*      REQUESTED BOOK/MARKET AGAINST THE FQABOOKS TABLE IN DEDEMTABS.           
*      IF IT'S IN THE TABLE, THEN WE DO *NOT* UNAUTHORIZE IT REGARDLESS         
*      OF HOW OLD THE BOOK IS. OTHERWISE, IT IS ELIGIBLE TO BE                  
*      UNAUTHORIZED BASED ON ITS AGE.                                           
*  2. IN ALL ENVIRONMENTS (INCLUDING FQA):                                      
*      THE REQUESTED BOOK IS CHECKED AGAINST THE CURRENT DATE TO                
*      DETERMINE IF IT IS ELIGIBLE TO BE UNAUTHORIZED.                          
*                                                                               
         MVI   DBERROR,0           ASSUME NO ERROR                              
*                                                                               
L        USING FQABOOKD,BINSRKEY   BINSRCH KEY IN LOCAL W/S                     
*                                                                               
         CLI   DBSELSRC,C'N'       ARE WE LOOKING AT NIELSEN DATA?              
         BNE   TESTH_OK            NO: GET OUT                                  
         CLI   DBSELMED,C'T'       IS THIS LOCAL MONTHLIES TIME PERIOD?         
         BNE   TESTH_OK            NO: GET OUT                                  
         OC    DBACTBK,DBACTBK     YES, IN WHICH CASE...                        
         JZ    *+2                 ...WE HAD BETTER KNOW THE BOOK!              
*                                                                               
         MVC   L.FQABOOK,DBACTBK   PUT BOOK IN BINSRCH KEY                      
*                                                                               
         L     R1,DBAREC                                                        
         AH    R1,DBDTADSP         R1=A(CURRENT ELEMENT)                        
*                                  TEST MARKET NUMBER VALID                     
         USING MARELEM,R1                                                       
         IF (CLI,MARCODE,EQ,MARCODEQ)  IF 1ST ELEM. IS MARKET ELEMENT:          
           SR    R0,R0                                                          
           ICM   R0,3,MARNO              SET ACTUAL MARKET FROM RECORD          
         ELSE ,                                                                 
           LHI   R0,1                    O/W SET DEFAULT RATING SVC MKT         
         ENDIF ,                                                                
         DROP  R1                                                               
*                                                                               
         LTR   R0,R0               WE MUST HAVE...                              
         JZ    *+2                 ...THE RATING SERVICE MARKET!                
         STH   R0,L.FQARMKT        PUT RATING MARKET IN BINSRCH KEY             
*                                                                               
         IF (CLI,SVDSPACE,EQ,C'Q') IF WE'RE ON FQA:                             
*                                                                               
           L     RF,DBCOMFCS                                                    
           ICM   RF,15,CDEMTABS-COMFACSD(RF)                                    
           GOTO1 (RF),MYDMCB,FQABOOKS         GET A(FQA "KEEP" TABLE)           
           ICM   RE,15,0(R1)         A(TABLE) RETURNED IN P1                    
           JZ    *+2                 BAD TABLEID PASSED                         
           LHY   R0,-4(,RE)          # OF TABLE ENTRIES                         
           L     RF,4(R1)            L'TABLE ENTRY RETURNED IN P2               
*                                                                               
           USING BSPARA,R1                                                      
           LA    R1,MYDMCB           LOCAL DMCB                                 
           XC    BSPARA(8*4),BSPARA  CLEAR ENTIRE DMCB (8 PARAMETERS)           
           ST    RE,BSPSTRT          A(TABLE)                                   
           LA    RE,BINSRKEY                                                    
           ST    RE,BSPAREC          A(RECORD)                                  
           ST    R0,BSPNOR           # OF RECORDS IN TABLE                      
           ST    RF,BSPLENR          L'RECORD                                   
           ST    RF,BSPLENK          L'KEY                                      
           ST    R0,BSPEND           # OF RECORDS IN TABLE                      
           GOTO1 VBINSR31                                                       
           TM    0(R1),X'80'         RECORD NOT FOUND?                          
           BZ    TESTH_OK            IT *WAS* FOUND: *** EXIT HERE ***          
           DROP  R1                                                             
*                                                                               
         ENDIF ,                                                                
*                                                                               
         L     RF,DBCOMFCS                                                      
         ICM   RF,15,CDATCON-COMFACSD(RF)                                       
*                                                                               
* USE SPECIAL DATCON CALLS WHICH INCORPORATE ADDAY. SUBTRACT THE NUMBER         
* OF MONTHS SPECIFIED IN P4 FROM TODAY'S DATE.                                  
*                                                                               
* SEE IF THE BOOK IS RECENT ENOUGH THAT WE *MUST DEFINITELY KEEP IT.*           
         GOTO1 (RF),MYDMCB,(X'35',0),(3,DUB2),                         +        
               (7,0),A((-12*UNCONDITIONAL_LM_KEEP_#_YRS)-1)                     
         CLC   L.FQABOOK,DUB2      KEEP BOOK *UNCONDITIONALLY* ?                
         BNL   TESTH_OK            YES: IT'S A CURRENT BOOK                     
*                                                                               
* SEE IF THE BOOK IS OLD ENOUGH THAT WE *MAY DEFINITELY UNAUTHORIZE IT.         
         GOTO1 (RF),MYDMCB,(X'35',0),(3,DUB2),                         +        
               (7,0),A((-12*CONDITIONAL_LM_KEEP_#_YRS)-1)                       
         CLC   L.FQABOOK,DUB2      UNAUTHORIZE BOOK *UNCONDITIONALLY* ?         
         BL    TESTH_UNAUTHORIZED  YES: UNAUTHORIZE "HISTORICAL" BOOK           
*                                                                               
* IF THE BOOK WAS DURING A SUMMER OR WINTER OLYMPICS WITHIN THE LAST            
* 8 YEARS, THEN WE DO NOT PURGE IT.                                             
         L     RF,DBCOMFCS                                                      
         ICM   RF,15,CDEMTABS-COMFACSD(RF)                                      
         GOTO1 (RF),MYDMCB,SWEEPTBL  GET A(SWEEP START DATE TABLE)              
         ICM   RE,15,0(R1)         A(TABLE) RETURNED IN P1                      
         JZ    *+2                 BAD TABLEID PASSED                           
         L     RF,4(R1)            L'TABLE ENTRY RETURNED IN P2                 
*                                                                               
         USING SWPTABLD,RE                                                      
         DO WHILE=(CLI,0(RE),NE,0) IF WE REACH EOT: UNAUTHORIZE IT              
           IF (CLC,=C'TTN',EQ,SWPTFMS),AND, FIND ENTRY IN SWEEP TABLE           
              (CLC,SWPTBOOK,EQ,L.FQABOOK)                                       
             IF (TM,SWPFLAGS,SWPFLAGS_OLYMPIC_WKS,O) IF OLYMPICS:               
               J TESTH_OK           *** JUMP OUT TO "OKAY" EXIT PATH            
             ELSE ,                                  O/W:                       
               J TESTH_UNAUTHORIZED *** JUMP OUT TO "UNAUTHORIZED" EXIT         
             ENDIF ,                                                            
           ENDIF ,                                                              
           AR    RE,RF               BUMP TO NEXT TABLE ENTRY                   
         ENDDO ,                                                                
         DROP  L                                                                
         DROP  RE                                                               
*                                                                               
* IF WE *FALL OUT* OF THE "DO" LOOP ABOVE, THEN THERE IS NO SWEEP TABLE         
* ENTRY FOR THE BOOK, AND WE THEREFORE TREAT IT AS UNAUTHORIZED.                
*                                                                               
TESTH_UNAUTHORIZED DS 0H                                                        
         MVI   DBERROR,INVMRKT     SET INVALID MARKET ERROR                     
*                                                                               
TESTH_OK DS    0H                                                               
         CLI   DBERROR,0           SET CONDITION CODE FOR CALLER                
*                                                                               
         J     GETXX                                                            
*                                                                               
UNCONDITIONAL_LM_KEEP_#_YRS EQU 5  # OF YRS TO KEEP LOCAL MONTHLIES             
CONDITIONAL_LM_KEEP_#_YRS   EQU 8  SAME, BUT CONSIDERING OLYMPICS               
*                                                                               
         EJECT                                                                  
*                                                                               
* LITERALS ETC.                                                                 
*                                                                               
         LTORG                                                                  
         SPACE 1                                                                
WEEKTAB  DC    AL1(0,1,1,2,1,2,2,3,1,2,2,3,2,3,3,4)                             
EFFS     DC    X'FFFFFFFF'                                                      
ARZERO   DC    16F'0'                                                           
         DROP  RA,RB                                                            
         DROP  R6,R8,R9,RC                                                      
         EJECT                                                                  
       ++INCLUDE DEGETD                                                         
         EJECT                                                                  
* INCLUDE FASSB                                                                 
* INCLUDE FASYSFAC                                                              
* INCLUDE DEDBEXTRAD                                                            
* INCLUDE DDMASTD                                                               
* INCLUDE DDMONYREQU                                                            
* INCLUDE DDBSPARA                                                              
         PRINT OFF                                                              
       ++INCLUDE FASSB                                                          
       ++INCLUDE FASYSFAC                                                       
       ++INCLUDE DEDBEXTRAD                                                     
       ++INCLUDE DDMASTD                                                        
       ++INCLUDE DDMONYREQU                                                     
       ++INCLUDE DDBSPARA                                                       
         SPACE 1                                                                
         PRINT ON                                                               
         TITLE 'DEGETPAV - PROGRAM AVERAGES READ CONTROLLER (LOCAL WORK+        
               AREA)'                                                           
***********************************************************************         
*====================== GETPAV'S LOCAL WORK AREA =====================*         
                                                                                
PAVWORKD DSECT                                                                  
*                                                                               
DUB2     DS    D                   LOCAL DUB                                    
MYDMCB   DS    8F                  LOCAL DMCB                                   
*                                                                               
PVSELNDY DS    XL1                 REQUESTED NUMBER OF DAYS FOR AVG-N           
PVNDAYS  DS    XL1                 NUMBER OF DAYS FROM RECORD                   
*                                                                               
TPSVTIM  DS    XL4                                                              
TPTIM2   DS    CL4                                                              
*                                                                               
SVINTFIL DS    CL2                                                              
*                                                                               
BINSRKEY DS    XL(FQABOOKQ)        BINSRCH TABLE KEY                            
*                                                                               
PAVWORKX EQU   *                                                                
PAVWORKL EQU   PAVWORKX-PAVWORKD                                                
***********************************************************************         
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'036DEGETPAV  05/06/19'                                      
         END                                                                    
