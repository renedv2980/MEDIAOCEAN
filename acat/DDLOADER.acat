*          DATA SET DDLOADER   AT LEVEL 004 AS OF 01/30/98                      
*CATALP LOADER                                                                  
         SPACE 2                                                                
* PARAMETERS TO LOADER                                                          
*                                                                               
* P1     DC    AL4(PHASE NAME).                                                 
*                                                                               
* P2     DC    LOAD ADDRESS OR 0. IF ALL FF'S THEN MVS DELETE PHASE.            
*                                                                               
* P3     DC    C'X',AL3(LOAD ADDRESS). MVS ONLY. X=M MOVE. X=A ADDR.            
*                                                                               
* MVS MOVE MODE WILL LOAD PHASE, MOVE TO P2 ADDR, AND DELETE PHASE.             
* THE LOAD ADDRS IN P2 AND P3 MUST MATCH OR ELSE MOVE MODE IGNORED.             
* P2 IS SET TO DBLW BDRY IF NEC. P3 IS SET TO ACTUAL MVS LOAD ADDR.             
*                                                                               
* MVS ADDR MODE WILL LOAD PHASE DIRECTLY TO P2 ADDR.CALLER MUST BE              
* AUTHORIZED. THE LOAD ADDRS IN P2 AND P3 MUST MATCH OR ELSE ADDR               
* MODE IGNORED.                                                                 
* P2 IS SET TO DBLW BDRY IF NEC. P3 IS SET TO ACTUAL MVS LOAD ADDR.             
         SPACE 2                                                                
* AFTER LOADER                                                                  
*                                                                               
* P1     DC    AL4(LENGTH OF PHASE).                                            
*                                                                               
* P2     DC    ACTUAL LOAD ADDRESS (ZERO ON ERROR).                             
*                                                                               
         TITLE 'DDLOADER - LOAD CIL PHASE'                                      
LOADER   CSECT                                                                  
         NMOD1 WORKX-WORKD,**LOAD**                                             
*                                                                               
         USING WORKD,RC                                                         
*                                                                               
         LR    RA,R1               RA=A(PARAMETER LIST)                         
         XC    MODE,MODE                                                        
         CLI   8(RA),C'M'          TEST FOR MOVE MODE                           
         BE    *+12                                                             
         CLI   8(RA),C'A'          TEST FOR ADDR MODE                           
         BNE   LOAD                                                             
         CLC   5(3,RA),9(RA)       ADDR MUST MATCH                              
         BNE   LOAD                                                             
         MVC   MODE+1(3),5(RA)     SAVE REQUESTED MOVE ADDR                     
         EJECT                                                                  
*&&DO                                                                           
LOAD     LM    R2,R3,0(RA)         R2=A(PHASE NAME),R3=LOAD ADDR                
         CLC   4(4,RA),FFS                                                      
         BE    EXIT                IGNORE DELETE REQUESTS IN DOS                
*                                                                               
         LA    R2,0(R2)                                                         
         XC    4(4,RA),4(RA)       CLEAR IN CASE OF ERROR                       
*                                                                               
         LA    R3,7(R3)            ADJUST ADDR TO DBLWD BOUNDARY                
         SRL   R3,3                                                             
         SLL   R3,3                                                             
*                                                                               
         XC    GENLA,GENLA                                                      
         MVC   GENLA(8),0(R2)      PHASE NAME                                   
         MVC   GENL(2),=H'36'                                                   
         MVI   GENL+13,X'0B'                                                    
*                                                                               
         LA    R1,GENLA            BUILD PHASE DIRECTORY                        
         LR    R0,R3                                                            
         LOAD  (1),(0),DE=YES,TXT=NO                                            
         TM    GENLA+16,X'06'                                                   
         BO    EXIT                EXIT IF PHASE NOT IN CIL                     
         LA    R1,GENLA                                                         
         LR    R0,R3                                                            
         LOAD  (1),(0),DE=YES,TXT=YES                                           
*                                                                               
         LR    R3,R1               ACTUAL LOAD ADDRESS                          
         STCM  R3,7,5(RA)          SET ADDRESS FOR USER                         
         OC    MODE,MODE                                                        
         BZ    *+8                                                              
         STCM  R3,7,9(RA)          RETURN ACTUAL LOAD ADDR                      
         L     R1,20                                                            
         SVC   33                                                               
         L     R4,36(R1)           HI CORE OF LOADED PHASE                      
         SR    R4,R3               MINUS LO CORE OF LOADED PHASE                
         LA    R4,1(R4)                                                         
         ST    R4,0(RA)            SET LENGTH FOR USER                          
         B     EXIT                                                             
*&&                                                                             
         EJECT                                                                  
*&&OS                                                                           
LOAD     LM    R2,R3,0(RA)         R2=A(PHASE NAME),R3=LOAD ADDR                
         LA    R2,0(R2)                                                         
         LA    R3,0(R3)                                                         
         CLC   4(4,RA),FFS         TEST FOR DELETE MODE                         
         BE    LOAD4                                                            
         MVC   GENLA(8),0(R2)      SAVE PHASE NAME                              
         CLI   8(RA),C'A'                                                       
         BNE   LOAD2                                                            
*                                                                               
LOAD1    XC    0(8,RA),0(RA)       ADDR MODE - REQUIRES LOAD DCB                
         OC    MODE,MODE                                                        
         BZ    EXIT                                                             
         CLI   OPENDCB,C'Y'        OPEN LOADDCB ON FIRST VALID CALL             
         BE    LOAD1A                                                           
         OPEN  (LOADDCB,(INPUT))                                                
         MVI   OPENDCB,C'Y'                                                     
LOAD1A   L     R4,MODE                                                          
         LA    R4,7(R4)            ADJUST ADDR TO DBLWD BOUNDRY                 
         SRL   R4,3                                                             
         SLL   R4,3                                                             
*                                                                               
         USING MASTD,R1                                                         
         ICM   R1,15,=V(MASTC)                                                  
         BZ    LOAD1B                                                           
         CLI   MCXPDTR,C'Y'        RUNNING UNDER XPEDITER                       
         BE    LOAD1C                                                           
         DROP  R1                                                               
*                                                                               
LOAD1B   LOAD  EPLOC=(2),ERRET=LOADERR,DCB=LOADDCB,ADDR=(4)                     
         LA    R1,0(R1)                                                         
         SLL   R1,3                R1=LEN OF MODULE IN BYTES                    
         ST    R1,0(RA)            SET LENGTH  FOR USER                         
         STCM  R0,7,5(RA)          SET ENTRY ADDR FOR USER                      
         STCM  R0,7,9(RA)          SET ACTUAL LOAD ADDR                         
         B     EXIT                                                             
*                                                                               
LOAD1C   XC    PARAMS,PARAMS       SET UP XPEDITER LOAD                         
         MVC   PARAMTOK(4),=C'ADDR'  KEY WORD                                   
         LA    R1,GENLA                                                         
         ST    R1,PARAMEPL         A(ENTRY NAME)                                
         LA    R1,LOADERR                                                       
         ST    R1,PARAMERR         A(ERROR ROUTINE)                             
         LA    R1,LOADDCB                                                       
         ST    R1,PARAMDCB         A(DCB)                                       
         ST    R4,PARAMADR         LOAD ADDR                                    
         LA    R1,MODSIZE                                                       
         ST    R1,PARAMSIZ                                                      
         LA    R1,PARAMS                                                        
         SVC   244                 ALTERNATIVE SVC - LOAD FOR XPEDITER          
         L     R1,PARAMSIZ                                                      
         SLL   R1,3                R1=LEN OF MODULE IN BYTES                    
         ST    R1,0(RA)            SET LENGTH  FOR USER                         
         MVC   5(3,RA),PARAMADR+1  SET ENTRY ADDR FOR USER                      
         MVC   9(3,RA),PARAMADR+1  SET ACTUAL LOAD ADDR                         
         B     EXIT                                                             
*                                                                               
LOAD2    XC    0(8,RA),0(RA)       MOVE/REGULAR MODE                            
         LOAD  EPLOC=(2),ERRET=LOADERR                                          
         LA    R1,0(R1)                                                         
         SLL   R1,3                R1=LEN OF MODULE IN BYTES                    
         ST    R1,0(RA)            SET LENGTH FOR USER                          
         STCM  R0,7,5(RA)          SET ENTRY ADDR FOR USER                      
         OC    MODE,MODE                                                        
         BZ    EXIT                EXIT IF NOT MOVE MODE                        
*                                                                               
LOAD3    L     R4,MODE             MOVE PHASE TO SAVED MOVE ADDR                
         LA    R4,7(R4)            ADJUST ADDR TO DBLWD BOUNDRY                 
         SRL   R4,3                                                             
         SLL   R4,3                                                             
         ST    R4,MODE             SET FOR MOVE LONG                            
         LR    R5,R1                                                            
         L     R6,4(RA)                                                         
         LR    R7,R1                                                            
         MVCL  R4,R6                                                            
         MVC   5(3,RA),MODE+1      RESTORE ADDR FOR MOVE MODE                   
         STCM  R0,7,9(RA)          RETURN ACTUAL LOAD ADDR                      
         LA    R0,GENLA                                                         
*                                                                               
LOAD4    DELETE EPLOC=(2)                                                       
         B     EXIT                                                             
*                                                                               
LOADERR  B     EXIT                                                             
         SPACE 2                                                                
LOADDCB  DCB   DSORG=PO,MACRF=(R),DDNAME=LOADLIB                                
*&&                                                                             
         EJECT                                                                  
EXIT     XMOD1 1                                                                
         SPACE 2                                                                
FFS      DC    XL4'FFFFFFFF'                                                    
OPENDCB  DC    C'N'                                                             
         LTORG                                                                  
         SPACE 2                                                                
WORKD    DSECT                                                                  
MODE     DS    F                                                                
GENL     DS    H                                                                
GENLA    DS    CL34                                                             
PARAMS   DS    0CL28                                                            
PARAMTOK DS    CL4                                                              
PARAMEPL DS    AL4                                                              
PARAMERR DS    AL4                                                              
PARAMDCB DS    AL4                                                              
PARAMADR DS    AL4                                                              
PARAMSIZ DS    AL4                                                              
PARAMRC  DS    AL4                                                              
*                                                                               
MODSIZE  DS    D                                                                
WORKX    EQU   *                                                                
         SPACE 2                                                                
       ++INCLUDE DDMASTD                                                        
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'004DDLOADER  01/30/98'                                      
         END                                                                    
