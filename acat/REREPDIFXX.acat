*          DATA SET REREPDIFXX AT LEVEL 084 AS OF 12/10/02                      
*CATALP REREPDIF                                                                
         TITLE 'REREPDIF - COMPARE DAR ORDER AND LINK CONTRACT'                 
***********************************************************************         
*                                                                     *         
*  REREPDIF  --- COMPARE DARE ORDER WITH THE LINKED CONTRACT          *         
*                                                                     *         
*  NOTE: R7 IS RESERVED AS BASE REGISTER FOR COMMON ROUTINES          *         
* ------------------------------------------------------------------- *         
* UPDATE HISTORY:                                                     *         
*                                                                     *         
* 04APR01 SKU CONCEPTION                                              *         
*                                                                     *         
* 24OCT02 HQ  MODIFY TO BE A BLACK BOX                                *         
*                    ***  END TOMBSTONE  ***                          *         
***********************************************************************         
* EXPLANATION OF TERMINOLOGIES USED:                                  *         
*                                                                     *         
* A BUY SEGMENT IS DEFINED AS SPOT(S) BOUGHT FOR A PARTICULAR WEEK.   *         
* IT HAS THE FOLLOWING FORMAT:                                        *         
* DAY/TIMES/LENGTH/RATE/PROGRAM NAME/WEEK-OF                          *         
*                                                                     *         
* FOR EACH PARTICULAR BUY SEGMENT, THE SEGMENT CAN POINT (LINK) TO    *         
* A MAXIMUM OF TWO BUY-LINK RECORDS, ONE FOR THE REP AND ONE FOR THE  *         
* AGENCY. A BUY-LINK RECORD IS A LIST OF BUY LINES THAT CONTAINS      *         
* SPOTS AS DESCRIBED BY THE ATTACHED BUY SEGMENT. THE LIST ALSO       *         
* INCLUDES THE NUMBER OF SPOTS NEXT TO EACH BUY LINE. IF A BUY        *         
* SEGMENT IS ONLY MATCHED WITH ONE BUYLINE (REP OR AGY), THE ACTUAL   *         
* BUY NUMBER AND SPOTS ARE KEPT IN THE BUY SEGMENT ITSELF IN LIEU OF  *         
* A POINTER TO A BUY-LINK RECORD.                                     *         
*                                                                     *         
***********************************************************************         
* PARAMETERS:   P1: BYTE 1 - X'80' SERVICE REQUEST CALL               *         
*                            X'40' COMPARING UNLINK ORDER WITH CONT   *         
*                                  GET CON KEY FROM DMCB3             *         
*                   BYTE 2-4 A(DARE KEY)                              *         
*               P2: DMCB2                                             *         
*                   P1: A(COMFACS)                                    *         
*                   P2: A(REDARTK)                                    *         
*                   P3: A(GETBROAD)                                   *         
* EXITH: RECORD ERROR SUCH AS INVALID CONTRACT                        *         
* EXITL: ORDER AND CONTRACT DOESN'T MATCH                                       
* EXITOK: ORDER AND CONTRACT MATCH                                              
***********************************************************************         
REREPDIF CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKLQ,*REPDIF*,RR=R3,CLEAR=YES                                  
         USING MYAREAD,RC                                                       
         ST    R3,RELO                                                          
         MVC   MYFLAG,0(R1)        SAVE CALLING OPTION                          
*                                                                               
         LM    R2,R4,0(R1)         INITIALIZE ROUTINE                           
         MVC   SELECTKY,0(R2)                                                   
         MVC   ACOMFACS(12),0(R3)  ACOMFACS,VREDARTKO,GETBROAD                  
         MVC   CCONNUM,0(R4)       SAVE CONTRACT NUMBER                         
*                                                                               
         L     R2,ACOMFACS                                                      
         USING COMFACSD,R2                                                      
         MVC   VDATAMGR,CDATAMGR                                                
         MVC   HELLO,CHELLO                                                     
         MVC   DATCON,CDATCON                                                   
         MVC   ADDAY,CADDAY                                                     
         MVC   GETDAY,CGETDAY                                                   
         MVC   PERVERT,CPERVERT                                                 
         MVC   CALLOV,CCALLOV                                                   
         DROP  R2                                                               
*                                                                               
         GOTO1 CALLOV,DMCB,0,X'D9000AAC',0                                      
         MVC   REPFACS,0(R1)      GET REPFAC ADDRESS                            
*                                                                               
         LR    R7,RB                                                            
         A     R7,=A(COMMON-REREPDIF)                                           
         USING COMMON,R7           COMMON ROUTINES                              
*                                                                               
         LR    RF,RC               ESTABLISH A(IOAREA1)                         
         AH    RF,=Y(IOAREA-MYAREAD)                                            
         ST    RF,AIO1                                                          
*                                                                               
         LR    RF,RC               ESTABLISH A(IOAREA2)                         
         AH    RF,=Y(IOAREA2-MYAREAD)                                           
         ST    RF,AIO2                                                          
*                                                                               
         LR    RF,RC               ESTABLISH A(IOAREA3)                         
         AH    RF,=Y(IOAREA3-MYAREAD)                                           
         ST    RF,AIO3                                                          
*                                                                               
         LR    RF,RC               ESTABLISH A(IOAREA4)                         
         AH    RF,=Y(IOAREA4-MYAREAD)                                           
         ST    RF,AIOAREA                                                       
*                                                                               
         MVC   AIO,AIO1                                                         
         MVC   DATADISP,=H'34'                                                  
*                                                                               
TB       USING TSARD,TBLOCK        TSAR BLOCK                                   
TR       USING TLSTD,TSARREC       TSAR RECORD                                  
*                                                                               
VR10     DS    0H                                                               
         MVC   KEY(L'SELECTKY),SELECTKY                                         
         MVI   RDUPDATE,C'N'                                                    
         GOTO1 HIGH                                                             
         MVI   RDUPDATE,C'N'                                                    
         GOTO1 GETREC                                                           
*                                                                               
         L     R6,AIO                                                           
         USING RDARREC,R6                                                       
         MVC   AGENCY,RDARKREP                                                  
*                                                                               
         GOTO1 TKODATEG,DMCB,RDARKSTA                                           
*                                                                               
VR30     DS    0H                                                               
         TM    MYFLAG,X'40'        CCONNUM GOT PASSED IN IF                     
         BO    VR30A               WE ARE COMPARE AN UNLINK ORD W/ CON          
         ZAP   WORK+5(5),=P'99999999'                                           
         ZAP   WORK+10(5),=P'0'                                                 
         MVO   WORK+10(5),RDARREP#                                              
         SP    WORK+5(5),WORK+10(5)                                             
         MVO   WORK(5),WORK+5(5)                                                
         MVC   CCONNUM,WORK                                                     
*                                                                               
VR30A    DS    0H                                                               
         GOTO1 =A(GETCON),RR=RELO  RETRIEVE CONTRACT RECORD                     
         BZ    VR45                EXIT:  NO CONTRACT NUMBER                    
*                                  ERROR IN GETCON:  MESSAGE CODE               
*                                     ALREADY SET IN RERROR                     
         B     VRERR               EXIT WITH ERROR                              
         DROP  R6                                                               
*                                                                               
VR45     DS    0H                                                               
         GOTO1 =A(SHADOW),RR=RELO  CONVERT DARE AGENCY BUYS TO                  
*                                  REPPAK BUYS WITH KEY X'0B01'                 
         BNE   VRERR               EXIT WITH ERROR                              
*                                                                               
         GOTO1 =A(INITTSAR),RR=RELO                                             
*                                                                               
         GOTO1 =A(BLDTSREC),RR=RELO                                             
*                                                                               
VR90     DS    0H                                                               
         GOTO1 =A(DISTOTAL),RR=RELO                                             
         BNE   VRNO                 THERE ARE DIFFERENCE                        
*                                                                               
VR105    DS    0H                                                               
         XC    TSARREC,TSARREC                                                  
         MVI   TR.TLKTYP,X'0B'                                                  
         GOTO1 GOTSAR,TSARDH                                                    
         BL    VRYES                                                            
*                                                                               
VR108    DS    0H                                                               
         XC    SEGSTRDT,SEGSTRDT                                                
         XC    DYTXNUM,DYTXNUM                                                  
         XC    TMTXNUM,TMTXNUM                                                  
*                                                                               
VR109    DS    0H                                                               
         MVC   SEGSTRDT,TR.TLBYWKOF                                             
*                                                                               
VR110    DS    0H                                                               
         CLI   TR.TLKTYP,X'0B'                                                  
         BNE   VRYES                                                            
*                                                                               
VR113    DS    0H                                                               
         CLC   DYTXNUM,TR.TLBYDAY  CHECK IF WE NEED TO BREAK ON                 
         BNE   VR115               DIFFERENT DAY/TIME                           
         CLC   TMTXNUM,TR.TLBYTIME                                              
         BE    VR120                                                            
*                                                                               
VR115    DS    0H                                                               
         MVC   DYTXNUM,TR.TLBYDAY                                               
         MVC   TMTXNUM,TR.TLBYTIME                                              
*                                                                               
VR120    DS    0H                                                               
*                                                                               
* CHECK IF SEGMENT IS FROM A SINGLE BUY. IF IT IS, THE SEGMENT                  
* CONTAINS THE ACTUAL BUY NUMBER AND SPOT COUNT                                 
* IF NOT, WE NEED TO FIND THE LINK TO THE LIST OF BUYS AGAINST THIS             
* SEGMENT                                                                       
*                                                                               
         GOTO1 =A(GETTSPTS),RR=RELO                                             
*                                                                               
         CLC   TOTREPSP,TOTAGYSP   SKIP IF ALL SPOTS MATCHED                    
         BNE   VRNO                                                             
*                                                                               
* SKIP MATCHED SPOTS FOR SUMMARY LISTING                                        
*                                                                               
         GOTO1 GOTSAR,TSANXT                                                    
         BL    VR125                                                            
         CLI   TR.TLKTYP,X'0B'                                                  
         BE    VR109                                                            
*                                                                               
VR125    DS    0H                                                               
         B     VRYES                                                            
*                                                                               
VRYES    CR    RB,RB                                                            
         B     VRX                                                              
VRNO     CLI   *,X'FF'                                                          
         B     VRX                                                              
VRERR    CLI   *,0                                                              
*                                                                               
VRX      DS    0H                                                               
         XIT1                                                                   
***********************************************************************         
* COMMON ROUTINES AND ERROR MESSAGES (ADDRESSABLE EXCLUSIVELY BY R7)            
***********************************************************************         
         DROP  RB                                                               
COMMON   DS    0H                                                               
***********************************************************************         
* DATAMGR CALLS                                                                 
***********************************************************************         
SEQ      NTR1                                                                   
         MVC   COMMAND,=C'DMRSEQ'                                               
         B     DIRCTRY                                                          
         SPACE 2                                                                
HIGH     NTR1                                                                   
         MVC   COMMAND,=C'DMRDHI'                                               
         MVC   KEYSAVE,KEY                                                      
         B     DIRCTRY                                                          
         SPACE 2                                                                
ADD      NTR1                                                                   
         MVC   COMMAND,=C'DMADD '                                               
         B     DIRCTRY                                                          
         SPACE 2                                                                
WRITE    NTR1                                                                   
         MVC   COMMAND,=C'DMWRT '                                               
         B     DIRCTRY                                                          
         SPACE 1                                                                
DIRCTRY  CLI   RDUPDATE,C'Y'                                                    
         BNE   *+8                                                              
         OI    DMINBTS,X'80'                                                    
         MVC   KEYSAVE(27),KEY                                                  
         GOTO1 VDATAMGR,DMCB,(DMINBTS,COMMAND),=C'REPDIR',KEYSAVE,KEY,0         
         B     DMCHECK                                                          
         EJECT                                                                  
*                  COMMUNICATION WITH DATA MANAGER (FILE)                       
GETREC   NTR1                                                                   
         MVC   COMMAND,=C'GETREC'                                               
         B     FILE                                                             
PUTREC   NTR1                                                                   
         MVC   COMMAND,=C'PUTREC'                                               
         B     FILE                                                             
ADDREC   NTR1                                                                   
         MVC   COMMAND,=C'ADDREC'                                               
         B     FILE                                                             
*                                                                               
FILE     DS    0H                                                               
         CLI   RDUPDATE,C'Y'                                                    
         BNE   *+8                                                              
         OI    DMINBTS,X'80'                                                    
         GOTO1 VDATAMGR,DMCB,(DMINBTS,COMMAND),=C'REPFILE',KEY+28,     X        
               AIO,DMWORK                                                       
         B     DMCHECK                                                          
DMCHECK  DS    0H                                                               
*                                                                               
         MVI   DMOUTBTS,X'FD'                                                   
         MVI   DMINBTS,X'00'                                                    
         MVI   RDUPDATE,C'N'                                                    
         MVC   DMBYTE,DMCB+8                                                    
         NC    DMBYTE,DMOUTBTS                                                  
         BZ    EXIT                                                             
*                                                                               
         DC    H'0'                                                             
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
********************************************************                        
* TKODATEG: GET TKODATE                                                         
* P1:    A(STATION CODE)                                                        
********************************************************                        
TKODATEG NTR1                                                                   
         L     R3,0(R1)                                                         
         LA    R4,KEY                                                           
         XC    KEY,KEY                                                          
         USING RSTAKEY,R4                                                       
         MVI   RSTAKTYP,X'02'                                                   
         MVC   RSTAKREP,AGENCY     REP                                          
         MVC   RSTAKSTA,0(R3)      STATION                                      
         CLI   RSTAKSTA+4,C'T'                                                  
         BNE   *+8                                                              
         MVI   RSTAKSTA+4,C' '                                                  
         DROP  R4                                                               
                                                                                
         MVI   RDUPDATE,C'N'                                                    
         GOTO1 HIGH                                                             
         CLC   KEY(27),KEYSAVE                                                  
         BNE   EXITL                                                            
         MVC   AIO,AIO3                                                         
         MVI   RDUPDATE,C'N'                                                    
         GOTO1 GETREC                                                           
                                                                                
         L     R6,AIO                                                           
         MVI   ELCODE,1                                                         
         BRAS  RE,GETEL                                                         
         USING RSTAELEM,R6                                                      
         MVC   RTKODATE,RSTASTRT   TAKEOVER DATE                                
         DROP  R6                                                               
         MVC   AIO,AIO1                                                         
TKODATEX DS    0H                                                               
         B     EXITOK                                                           
***********************************************************************         
* ROUTINE TO INTERFACE WITH TSAR BUFFER                               *         
* NTRY: R1 = REQUESTED ACTION                                         *         
*                                                                     *         
* EXIT: CC LOW FOR END OF FILE ERROR                                  *         
*       CC HIGH FOR RECORD NOT FOUND                                  *         
***********************************************************************         
GOTSAR   NTR1                                                                   
         STCM  R1,1,TLACTN         REQUESTED ACTION                             
*                                                                               
         XCEF  TBLOCK,TSARDL                                                    
*                                                                               
         MVC   TB.TSACTN,TLACTN                                                 
*                                                                               
         LA    RF,TSARREC                                                       
         ST    RF,TB.TSAREC                                                     
*                                                                               
         CLI   TLACTN,TSARES       EXPLICIT RESTORE?                            
         BE    GOTSAR10                                                         
         CLI   TLACTN,TSASAV       EXPLICIT SAVE?                               
         BE    GOTSAR10                                                         
         CLI   TLACTN,TSAINI       EXPLICIT INITIALISE?                         
         BNE   GOTSAR30                                                         
*                                                                               
GOTSAR10 DS    0H                                                               
         MVI   TB.TSPAGL,1         USE TEMPSTR PAGE 1                           
         MVI   TB.TSPAGN,10                                                     
         MVC   TB.TSACOM,ACOMFACS  SET A(COMFACS)                               
         MVI   TB.TSKEYL,L'TLKEY   SET KEY LENGTH                               
         MVI   TB.TSRECI,TSRVAR    SET VARIABLE                                 
         MVC   TB.TSRECL,=Y(255)   SET MAXIMUM RECORD LENGTH                    
         MVI   TB.TSPAGN,TSPEXPN   SET NUMBER OF TEMPEST PAGES                  
         MVI   TB.TSINDS,TSIALLOC  SET TO ALLOCATE FROM TEMPEST                 
*        OI    TB.TSINDS,TSIXTTWA  14K RECORDS                                  
*                                                                               
         TM    MYFLAG,SRCALL       CALLING FROM SERVICE REQUEST                 
         BZ    *+12                                                             
         NI    TB.TSIND2,X'FF'-TSI2DATA  DON'T RETURN BUFF DATA                 
         OI    TB.TSINDS,TSINODSK  NO DISK                                      
*                                                                               
GOTSAR20 GOTOX VTSAR,TB.TSARD      CALL TO INITIALISE/RESTORE                   
         BE    GOTSARX                                                          
         DC    H'0'                ABEND                                        
*                                                                               
GOTSAR30 DS    0H                                                               
         MVC   TB.TSRNUM,TXNUM     SET TSAR NUMBER                              
*                                                                               
* SET MINIMUM LENGTH FOR VARIABLE LENGTH RECORDS                                
*                                                                               
         CLI   TLACTN,TSAADD       TEST ADDING                                  
         BNE   GOTSAR50                                                         
         CLC   TR.TLLEN,=Y(L'TLKEY+2+1)                                         
         BNL   GOTSAR50                                                         
         MVC   TR.TLLEN,=Y(L'TLKEY+2+1)                                         
*                                                                               
GOTSAR50 DS    0H                                                               
         GOTOX VTSAR,TB.TSARD                                                   
         MVC   TXNUM,TB.TSRNUM     SET RECORD LIST NUMBER                       
*                                                                               
         CLI   TLACTN,TSAADD       CHECK IF END-OF-FILE ERROR                   
         BNE   GOTSAR70                                                         
         TM    TB.TSERRS,TSEEOF    DON'T DIE, DISPLAY ERROR INSTEAD             
         BZ    GOTSARX             ASK USER TO REQUEST REPORT OFFLINE           
         MVC   RERROR,=AL2(801)                                                 
         DC    H'0'                                                             
*                                                                               
GOTSAR70 DS    0H                                                               
         TM    TB.TSERRS,TSEEOF    RETURN CC=LOW FOR END-OF-FILE ERROR          
         BO    EXITL                                                            
         TM    TB.TSERRS,TSERNF    RETURN CC=HIGH IF RECORD NOT FOUND           
         BO    EXITH                                                            
*                                                                               
GOTSARX  DS    0H                                                               
         B     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
*   LOADELT : LOADS ELEMENT IN WORKSPACE (ELTBUILD) TO RECORD.                  
***********************************************************************         
LOADELT  NTR1                                                                   
         L     R2,0(R1)            RESET A(TARGET RECORD)                       
         L     R3,4(R1)            RESET A(ELEMENT BUILD AREA)                  
         L     R4,8(R1)            RESET A(COMMAND EXTENSION)                   
         GOTO1 HELLO,DMCB,(C'P',=C'REPFILE'),(R2),(R3),(R4)                     
*                                  ADD ELT TO RECORD                            
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
*   DELELT :  DROPS X'03' ELEMENT FROM BUYRECS                                  
***********************************************************************         
DELELT   NTR1                                                                   
         L     R2,0(R1)            RESET A(TARGET RECORD)                       
         GOTO1 HELLO,DMCB,(C'D',=C'REPFILE'),(3,(R2)),0,0                       
*                                  DROP X'03' ELTS FROM BUYRECORD               
         B      EXIT                                                            
         EJECT                                                                  
***********************************************************************         
*   DELELT02 :  DROPS X'02' ELEMENT(S) (DAY/TIME) FROM BUYRECS                  
*        WHEN THEY WERE GENERATED BY AN ORBIT RECORD, AND THE BUYS              
*        ARE 'DAILY'                                                            
***********************************************************************         
DELELT02 NTR1                                                                   
         L     R2,0(R1)            RESET A(TARGET RECORD)                       
         GOTO1 HELLO,DMCB,(C'D',=C'REPFILE'),(2,(R2)),0,0                       
*                                  DROP X'02' ELTS FROM BUYRECORD               
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* EXITS                                                                         
***********************************************************************         
EXITOK   CR    RB,RB                                                            
         B     EXIT                                                             
EXITL    CLI   *,X'FF'                                                          
         B     EXIT                                                             
EXITH    CLI   *,0                                                              
*                                                                               
EXIT     DS    0H                                                               
         XIT1                                                                   
*                                                                               
         GETEL R6,DATADISP,ELCODE  USED FOR THE GETEL OPERATIONS                
*                                                                               
***********************************************************************         
* END OF COMMON ROUTINES                                                        
***********************************************************************         
         EJECT                                                                  
***********************************************************************         
* READ CONTRACT TO IO AREA 3                                                    
* SET UP ALT CALENDER FLAGS, VERSION, ETC.                                      
***********************************************************************         
NOCONREC EQU   82                                                               
*                                                                               
GETCON   NTR1  BASE=*,LABEL=*                                                   
*                                  GET 9'S REVERSE FOR BUYS LATER               
         PACK  COMPCON+0(1),CCONNUM+3(1)                                        
         PACK  COMPCON+1(1),CCONNUM+2(1)                                        
         PACK  COMPCON+2(1),CCONNUM+1(1)                                        
         PACK  COMPCON+3(1),CCONNUM+0(1)                                        
*                                                                               
         LA    R6,KEY                                                           
         USING RCONKEY,R6                                                       
         XC    KEY,KEY                                                          
         MVI   RCONPTYP,X'8C'                                                   
         MVC   RCONPREP,AGENCY                                                  
         MVC   RCONPCON,CCONNUM                                                 
         DROP  R6                                                               
*                                                                               
         GOTO1 HIGH                                                             
         CLC   KEY(L'RCONKEY),KEYSAVE                                           
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   AIO,AIO3                                                         
         GOTO1 GETREC                                                           
*                                                                               
         SR    RC,RC               SET CC ZERO - CLEAN END                      
ERREXIT  LTR   RC,RC                                                            
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* AGENCY BUY PREPROCESSING:                                                     
* DARE AGENCY BUYS WILL BE CONVERTED TO REPPAK BUY FORMAT WITH KEY TYPE         
* X'0B01'                                                                       
***********************************************************************         
SHADOW   NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         BAS   RE,DELOLD           DEL EXISTING X'0B01' RECORDS FOR             
*                                  THIS CONTRACT                                
         MVC   KEY(L'SELECTKY),SELECTKY                                         
         GOTO1 HIGH                                                             
*                                                                               
         CLC   KEY(27),KEYSAVE                                                  
         BNE   SHADOWNO            ORDER PROBABLY CONFIRMED                     
*                                  EXIT WITH ERROR                              
*                                                                               
SHAD03   DS    0H                                                               
         MVC   AIO,AIO1            SET IO AREA FOR X'41' RECS                   
         GOTO1 GETREC                                                           
                                                                                
         L     R4,AIO2             SET WORK AREA FOR BUYS                       
         USING RBUYREC,R4                                                       
*                                                                               
         L     R3,AIO                                                           
         USING RDARREC,R3                                                       
*                                                                               
         MVI   RDARKRT,X'40'       SET REC TYPE TO 'BUY'                        
         XC    RDARKSEQ(2),RDARKSEQ                                             
*                                  CLEAR SEQ # AND SUB TYPE                     
         MVC   KEY,RDARKEY         RETRIEVE FIRST BUY HEADER                    
         MVI   RDUPDATE,C'N'       SET UPDATE TO NO                             
         GOTO1 HIGH                                                             
         CLC   KEY(25),KEYSAVE     SAME KEY? THROUGH REC TYPE                   
         BNE   SHADOWY             YES                                          
*                                                                               
         MVI   RDUPDATE,C'N'       SET UPDATE TO NO                             
         MVI   BUYUPDAT,C'Y'       SET 'WRITE OUTPUT RECORD'                    
         GOTO1 GETREC              RETRIEVE RECORD                              
         GOTO1 =A(CHKDAREC),DMCB,(R3),RR=RELO                                   
*                                  ROUTINE SETS 'BUYUPDAT' TO NO                
*                                     WHEN HEADER IS SOFT-DELETED.              
*                                     THIS WILL PREVENT OUTPUT.                 
*                                  ALSO SETS 'SKIP RECORD' FLAG FOR             
*                                     OTHER RECORD TYPES                        
         MVI   BUYLINE#,0          INIT BUY NUMBER                              
*                                                                               
SHAD05   EQU   *                                                                
         MVI   ORBSTDAY,0          CLEAR 'ORBIT START DAY'                      
         MVI   ORBFLAG,C'N'        SET   'ORBIT NOT PRESENT'                    
         MVI   DETLFLAG,C'N'       CLEAR COUNT OF DETAILS                       
         XCEFL RBUYREC,1024        CLEAR BUY BUILD AREA                         
         MVC   RBUYKTYP(2),=X'0B01' AGENCY BUY SHADOW RECORD TYPE               
         MVC   RBUYKREP,AGENCY     INSERT REP CODE                              
         MVC   RBUYKCON,COMPCON    INSERT CONTRACT #, 9/COMP/REV                
         MVC   RBUYKPLN,=X'FFFFFF' INSERT PLAN CODE                             
         ZIC   RF,BUYLINE#         GET NEXT BUYLINE NUMBER                      
         LA    RF,1(RF)                                                         
         STC   RF,BUYLINE#         PUT IT BACK                                  
*                                                                               
* STORE AGENCY'S BUY NUMBER IN MASTER LINE NUMBER                               
*                                                                               
*        STC   RF,RBUYKMLN         INSERT MASTER LINE NUMBER                    
         MVC   RBUYKMLN,RDARKSEQ   INSERT AGENCY BUY LINE NUMBER                
*                                                                               
         STC   RF,RBUYKLIN         INSERT LINE NUMBER                           
         MVC   RBUYLEN,=X'004D'    INSERT RECORD LENGTH:                        
*                                     34+43 = 77 = X'4D'                        
         MVC   RBUYELEM(2),=X'012B'                                             
*                                  INSERT ELEMENT CODE/LENGTH                   
         LA    R6,RDARELEM                                                      
         USING RDARBYEL,R6                                                      
         MVC   PROGNAME,RDARBYPN   SAVE BUY HDR PROGRAM NAME                    
         MVC   RBUYCOS,RDARBYCO    INSERT COST                                  
         MVC   BUYCOST,RDARBYCO    SAVE COST FOR CALCULATIONS                   
         MVC   RBUYCLS(6),SPACES   SET CLASS/SECTION TO SPACES                  
         GOTO1 DATCON,DMCB,(5,WORK),(3,RBUYCREA)                                
*                                  INSERT CREATION DATE                         
         MVC   RBUYKMOD,CONMOD#    INSERT CONTRACT MOD #                        
         MVI   RBUYCHGI,C'A'       INSERT CHANGE INDICATOR                      
         MVC   RBUYAGBL,RDARKSEQ   INSERT AGENCY BUY LINE NUMBER                
         XC    STARTDAY(2),STARTDAY                                             
*                                  CLEAR START/END DAYS OF WEEK                 
         GOTO1 =A(STARTEND),DMCB,RDARBYRO,RBUYSTED,RR=RELO                      
*                                  CONVERT ONE-BYTE START/END DATE              
         GOTO1 =A(EFFDATEL),DMCB,RDARREC,RR=RELO                                
*                                                                               
         MVC   RBUYDUR,RDARBYSL    INSERT TOTAL SPOT LENGTH                     
         CLI   RDARBYSL,C'M'       LENGTH IN MINUTES?                           
         BNE   SHAD10              NO                                           
         OI    RBUYDUR,X'80'       YES - SET 'MINUTES' INDICATOR                
SHAD10   EQU   *                                                                
*                                                                               
         MVC   RBUYVER,VERDFLT     INSERT VERSION NUMBER                        
*                                     FROM REP VERSION NUMBER                   
SHAD20   EQU   *                                                                
         GOTO1 =A(SAVEDEMO),RR=RELO SAVE OFF DEMO VALUES, IF ANY                
*                                                                               
         L     R6,AIO              IF AGENCY BUY IS A MAKEGOOD                  
         MVI   AGBUYMTR,0                                                       
         MVI   ELCODE,X'10'        NEED TO SAVE OFF TARGET/MASTER               
         BRAS  RE,GETEL            AGENCY BUY FOR BETTER MATCHING               
         BNE   SHAD30                                                           
         USING RDARRVEL,R6                                                      
         MVC   AGBUYMTR,RDARRVBY                                                
         DROP  R6                                                               
*                                                                               
SHAD30   EQU   *                                                                
         MVI   RDUPDATE,C'N'       SET UPDATE TO NO                             
         NI    DMINBTS,X'FF'-X'08' SKIP DELETED                                 
         GOTO1 SEQ                 ACCESS NEXT X'41' RECORD                     
         MVC   SAVEKEY,KEY                                                      
         CLC   KEY(25),KEYSAVE     SAME KEY? THROUGH RECORD TYPE?               
         BE    SHAD40              YES - CONTINUE                               
         CLI   BUYUPDAT,C'Y'       WRITE THIS RECORD?                           
         BNE   SHADOWY             NO  - SKIP THE REWRITE                       
         TM    MISCFLAG,X'80'      'DAILY' BUY?                                 
         BO    SHADOWY             YES - ALREADY WRITTEN                        
         GOTO1 =A(GENREC),RR=RELO                                               
         B     SHADOWY                                                          
*                                                                               
SHAD40   EQU   *                                                                
         CLC   KEY+25(1),KEYSAVE+25                                             
*                                  SAME BUYLINE #?                              
         BE    SHAD60              YES - CONTINUE                               
         CLI   BUYUPDAT,C'Y'       WRITE THIS RECORD?                           
         BNE   SHAD60              NO  - SKIP THE REWRITE                       
         TM    MISCFLAG,X'80'      'DAILY' BUY?                                 
         BNO   SHAD50              NO  - GO PUT IT OUT                          
*                                                                               
*   NOTE:  'DAILY'S ARE GENERATED FROM WITHIN THE BUYDETL ROUTINE.              
*      AS SUCH, THE BUYLINE# IS BUMPED AFTER THE RECORD IS WRITTEN.             
*      WHEN THE AGENCY BUY IS COMPLETED, THE BUYLINE # IS SET TO                
*      THE NEXT EXPECTED BUYLINE.  TO ENSURE THAT IT IS NOT DOUBLE-             
*      INCREMENTED, IT IS BACKED OFF HERE.                                      
*                                                                               
         ZIC   RF,BUYLINE#         YES - DROP BUYLINE # BY 1                    
         BCTR  RF,0                                                             
         STC   RF,BUYLINE#                                                      
         B     SHAD60              DON'T PUT IT OUT AGAIN                       
SHAD50   EQU   *                                                                
         GOTO1 =A(GENREC),RR=RELO                                               
*                                  NO  - OUTPUT PREVIOUS RECORD                 
SHAD60   EQU   *                                                                
         MVC   AIO,AIO1            SET A(IO AREA 1)                             
         GOTO1 GETREC              RETRIEVE RECORD                              
         GOTO1 =A(CHKDAREC),DMCB,(R3),RR=RELO                                   
*                                  ROUTINE SETS 'BUYUPDAT' TO NO                
*                                     WHEN HEADER IS SOFT-DELETED.              
*                                     THIS WILL PREVENT OUTPUT.                 
*                                  ALSO SETS 'SKIP RECORD' FLAG FOR             
*                                     OTHER RECORD TYPES                        
*                                                                               
         CLI   KEY+26,X'00'        BUY HEADER?                                  
         BE    SHAD05              YES - START NEXT HEADER                      
         CLI   KEY+26,X'10'        BUY ORBIT?                                   
         BNE   SHAD70              NO                                           
         CLI   SKIPRECS,C'Y'       SKIP THIS RECORD?                            
         BE    SHAD30              YES - GO BACK FOR NEXT                       
         GOTO1 =A(BUYORBIT),DMCB,(R3),RR=RELO                                   
*                                  PROCESS BUY ORBIT RECORD                     
         B     SHAD30              GO BACK FOR NEXT RECORD                      
SHAD70   EQU   *                                                                
         CLI   KEY+26,X'20'        BUY COMMENT?                                 
         BNE   SHAD80              NO                                           
         CLI   SKIPRECS,C'Y'       SKIP THIS RECORD?                            
         BE    SHAD30              YES - GO BACK FOR NEXT                       
         GOTO1 =A(BUYCOMMT),DMCB,(R3),RR=RELO                                   
*                                  PROCESS BUY COMMENT RECORD                   
         B     SHAD30              GO BACK FOR NEXT RECORD                      
SHAD80   EQU   *                                                                
         CLI   KEY+26,X'30'        BUY DETAIL?                                  
         BE    *+6                 NO  - ONLY TRAILER LEFT                      
         DC    H'0'                UNKNOWN RECORD SUBTYPE                       
         CLI   SKIPRECS,C'Y'       SKIP THIS RECORD?                            
         BE    SHAD30              YES - GO BACK FOR NEXT                       
         CLC   PROGNAME,SPACES     ANY PROGRAM NAME?                            
         BE    SHAD110             NO  - DON'T NEED COMMENT                     
         CLI   KATZEDI,C'Y'        KATZ/EDI USES ONLY THE FIRST 32              
         BNE   SHAD90              WITH THE LAST 2 FOR DAYPART                  
         CLC   PROGNAME(32),SPACES ANY PROGRAM NAME?                            
         BE    SHAD110             NO  - DON'T NEED COMMENT                     
SHAD90   DS    0H                                                               
         GOTO1 =A(GENCOMMT),DMCB,RBUYREC,RR=RELO                                
SHAD110  DS    0H                                                               
         MVC   SAVEKEY,KEY                                                      
         GOTO1 =A(BUYDETL),DMCB,(R3),RR=RELO                                    
         BZ    SHAD120                                                          
         MVI   BUYUPDAT,C'N'                                                    
         B     SHAD30                                                           
*                                                                               
SHAD120  DS    0H                                                               
         MVC   KEY,SAVEKEY                                                      
         GOTO1 HIGH                                                             
         B     SHAD30              GO BACK FOR NEXT RECORD                      
         DROP  R3,R4                                                            
*                                                                               
SHADOWY  SR    RC,RC                                                            
SHADOWNO LTR   RC,RC                                                            
SHADOWX  DS    0H                                                               
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* DELETE X'0B01' RECORDS FOR THIS DARE ORDER, IF ANY                            
***********************************************************************         
DELOLD   NTR1                                                                   
         LA    R6,KEY                                                           
         USING RBUYREC,R6                                                       
         XC    KEY,KEY                                                          
         MVC   RBUYKTYP(2),=X'0B01' AGENCY BUY SHADOW RECORD TYPE               
         MVC   RBUYKREP,AGENCY     INSERT REP CODE                              
         MVC   RBUYKCON,COMPCON    INSERT CONTRACT #, 9/COMP/REV                
*                                                                               
         MVC   KEYSAVE,KEY                                                      
         MVI   RDUPDATE,C'Y'                                                    
         GOTO1 HIGH                                                             
*                                                                               
DELOLD10 DS    0H                                                               
         CLC   KEY(RBUYKPLN-RBUYKEY),KEYSAVE                                    
         BNE   DELOLDX                                                          
*                                                                               
         MVC   AIO,AIO2                                                         
         L     R6,AIO                                                           
         USING RBUYREC,R6                                                       
         MVI   RDUPDATE,C'Y'                                                    
         GOTO1 GETREC                                                           
         OI    RBUYCNTL,X'80'      MARK DELETED                                 
         GOTO1 PUTREC                                                           
         DROP  R6                                                               
*                                                                               
         OI    KEY+27,X'80'                                                     
         GOTO1 WRITE                                                            
*                                                                               
         MVI   RDUPDATE,C'Y'                                                    
         GOTO1 SEQ                                                              
         B     DELOLD10                                                         
*                                                                               
DELOLDX  DS    0H                                                               
         B     EXIT                                                             
*        EJECT                                                                  
*        LTORG                                                                  
*        EJECT                                                                  
***********************************************************************         
*   CHKDAREC:  CHECKS FOR TYPE OF DARE RECORD.  IF SOFT DELETE                  
*        BIT OR HARD DELETE BIT IS SET, CC IS SET TO NON-ZERO,                  
*        INDICATING THAT RECORD IS TO BE SKIPPED/NOT PROCESSED.                 
*        SOFT DELETE BIT (X'80') WILL ALWAYS BE SET WHEN HARD BIT               
*        (X'40') IS SET.  THEREFORE, ONLY SOFT BIT IS TESTED.                   
***********************************************************************         
CHKDAREC NTR1  BASE=*,LABEL=*                                                   
         L     R2,0(R1)            RESET A(DARE RECORD IN PROCESS)              
         USING RDARREC,R2                                                       
*                                                                               
         MVI   SKIPRECS,C'N'       SET 'SKIP RECORD' TO NO                      
*                                                                               
         CLI   RDARKRT,X'10'       AGENCY ORDER HEADER?                         
         BNE   CDAR0040            NO                                           
         LA    R3,RDARELEM         YES -                                        
         USING RDARELEM,R3                                                      
*                                                                               
         TM    RDARDELS,X'80'      SOFT DELETE SET?                             
         BNO   CDAR0900            NO  - EXIT WITH 'PROCESS RECORD'             
         ZIC   RF,BUYLINE#         YES - BUMP LINE NUMBER                       
         LA    RF,1(RF)                                                         
         STC   RF,BUYLINE#         PUT IT BACK                                  
         B     CDAR0800            EXIT WITH 'SKIP RECORD'                      
         DROP  R3                                                               
CDAR0040 EQU   *                                                                
         CLI   RDARKRT,X'40'       BUY?                                         
         BNE   CDAR0080            NO                                           
         CLI   RDARKSRT,X'00'      BUY HEADER?                                  
         BNE   CDAR0120            NO  - ORB/COMMT/DETAIL                       
*                                                                               
         LA    R3,RDARELEM         YES -                                        
         USING RDARBYEL,R3                                                      
*                                                                               
         MVI   BUYUPDAT,C'Y'       SET WRITE FLAG TO YES                        
         ZIC   RF,RDARBYLN         CHECK LENGTH                                 
         LA    RE,RDARBYOL         SET OLD ELEMENT LENGTH                       
         CR    RF,RE               COMPARE OLD VS NEW                           
         BE    CDAR0900            EQUAL: OLD LENGTH FOUND -                    
*                                     NOT KEY-DELETED:  MUST NOT                
*                                     BE SOFT DELETED.                          
CDAR0050 EQU   *                                                                
         TM    RDARBYDL,X'80'      SOFT DELETE SET?                             
         BNO   CDAR0900            NO  - EXIT WITH 'PROCESS RECORD'             
         MVI   BUYUPDAT,C'N'       YES - SET WRITE FLAG TO NO                   
         B     CDAR0800            EXIT WITH 'SKIP RECORD'                      
         DROP  R3                                                               
CDAR0080 EQU   *                                                                
         BH    CDAR0800            SKIP OVER RECORD TYPES                       
*                                     50 (TRAILER) + 60 (EQUIVS)                
*                                        THESE AREN'T SOFT-DELETED.             
CDAR0120 EQU   *                                                                
*                                                                               
*                                  ONLY BUYS WILL HAVE RDARKSRT                 
*                                     SET - STANDARD AND ORDER                  
*                                        COMMENTS WILL NOT                      
         CLI   RDARKSRT,X'30'      BUY DETAIL?                                  
         BL    CDAR0280            NO  - ORB/COMMT                              
         LA    R3,RDARELEM         YES - CHECK FOR MULTI RATES                  
         USING RDARBDEL,R3                                                      
*                                                                               
         TM    RDARBDDL,X'80'      SOFT DELETE SET?                             
         BNO   CDAR0900            NO  - EXIT WITH 'PROCESS RECORD'             
         MVI   SKIPRECS,C'Y'       YES - SET SKIP RECORD TO YES                 
         B     CDAR0800            FINISHED                                     
         DROP  R3                                                               
*                                                                               
CDAR0280 EQU   *                                                                
*                                                                               
*   REMAINING RECORD TYPES ARE 15, 20, 30, 40/10, 40/20, AND                    
*        SOFT/HARD DELETE BYTE IS IN SAME PLACE IN ALL ELEMENTS.                
*        STANDARD COMMENT ELEMENT FORMAT USED, ARBITRARILY.                     
*                                                                               
         LA    R3,RDARELEM                                                      
         USING RDARELE2,R3                                                      
*                                                                               
         TM    RDARELDL,X'80'      SOFT DELETE SET?                             
         BNO   CDAR0900            NO  - EXIT WITH 'PROCESS RECORD'             
         MVI   SKIPRECS,C'Y'       YES - SET SKIP RECORD TO YES                 
         B     CDAR0800            EXIT WITH 'SKIP RECORD'                      
         DROP  R3                                                               
CDAR0800 EQU   *                                                                
         LTR   RB,RB               SET CC NOT = ZERO                            
         B     CDAR1000                                                         
CDAR0900 EQU   *                                                                
         SR    R0,R0               SET CC = ZERO                                
CDAR1000 EQU   *                                                                
         B     EXIT                                                             
         DROP  R2                                                               
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
**********************************************************************          
* BUILDS DEMO ELEMENT                                                           
**********************************************************************          
SAVEDEMO NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         L     R6,AIO1                                                          
         MVI   ELCODE,X'20'                                                     
         BRAS  RE,GETEL                                                         
         BNE   SVDEMX                                                           
         USING RDARBMEL,R6                                                      
         LA    R6,RDARBDM1                                                      
         DROP  R6                                                               
*                                                                               
         XC    ELEM,ELEM                                                        
ELEMD    USING RBUYDMEL,ELEM                                                    
         MVI   ELEMD.RBUYDMCD,RBUYDMCQ                                          
         MVI   ELEMD.RBUYDMLN,18                                                
         LA    R4,ELEMD.RBUYDMCV                                                
         LA    R3,4                MAX 4 DEMOS                                  
         DROP  ELEMD                                                            
*                                                                               
SVDEM10  DS    0H                                                               
         PACK  DUB(8),0(7,R6)                                                   
         L     RF,DUB+4                                                         
         SRL   RF,4                DUMP THE SIGN                                
         STCM  RF,15,0(R4)                                                      
         AHI   R6,L'RDARBDM1                                                    
         AHI   R4,L'RBUYDMDM                                                    
         CLC   0(L'RDARBDM1,R6),SPACES                                          
         BE    SVDEM20                                                          
         BCT   R3,SVDEM10                                                       
*                                                                               
SVDEM20  DS    0H                                                               
         GOTO1 HELLO,DMCB,(C'P',=C'REPFILE'),AIO2,ELEM,=C'ADD=CODE'             
*                                                                               
SVDEMX   DS    0H                                                               
         XIT1                                                                   
SPACES   DC    CL80' '                                                          
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*   BUYORBIT:  BUILDS DAY-TIME ELEMENT FROM THE ORBIT ELEMENTS                  
*        IN THE RECORD, INSERTS THEM INTO THE NEW BUY RECORD.                   
***********************************************************************         
BUYORBIT NTR1  BASE=*,LABEL=*                                                   
         L     R3,0(R1)            RESET A(X'41' RECORD)                        
         USING RDARREC,R3                                                       
         LA    R8,RDARELEM         SET A(X'01' ELEMENT)                         
         ZIC   RF,1(R8)            GET LENGTH                                   
         AR    R8,RF               BUMP TO 1ST ORBIT ELEMENT                    
         MVI   ORBFLAG,C'Y'        SET 'ORBIT PRESENT' INDICATOR                
*                                     ORBIT TAKES PRIORITY OVER                 
*                                     BUY DAY/TIME ELTS                         
BORB0040 EQU   *                                                                
*                                  CLEAR START/END DAYS                         
         CLI   0(R8),0             END OF RECORD?                               
         BE    BORB0400            YES - FINISHED                               
*                                                                               
         USING RDAROEEL,R8                                                      
*                                                                               
         XC    ELTBUILD,ELTBUILD   CLEAR ELEMENT BUILD AREA                     
         XC    STARTDAY(2),STARTDAY                                             
*                                  CLEAR START/END DAYS                         
         MVC   ELTBUILD(2),=X'0209'                                             
*                                  INSERT ELEMENT CODE/LENGTH                   
         GOTO1 =A(STARTEND),DMCB,RDAROERO,ELTBUILD+2,RR=RELO                    
*                                  INSERT START/END DAY                         
         CLI   ORBSTDAY,0          ANY ENTRY IN ORBIT START DAY?                
         BNE   BORB0060            YES - DON'T REPLACE IT                       
         MVC   ORBSTDAY,STARTDAY   NO  - SAVE FIRST ORBIT START DAY             
*                                                                               
* EARLIEST ORBIT START DAY MIGHT BE LATER THAN CURRENT STAY DAY AS              
* SPECIFIED IN THE BUY HEADER. WE'LL TAKE THE FIRST ORBIT START DAY             
* AS THE OVERALL BUY START DAY                                                  
*                                                                               
* NOTE THAT THIS ASSUMES DAILYS SHOULD NEVER BE ACCOMPANIED WITH                
* ORBITS!!                                                                      
*                                                                               
         L     R2,AIO2             A(BUY RECORD IN PROGRESS)                    
         USING RBUYREC,R2                                                       
         ZIC   RF,ORBSTDAY                                                      
         SLL   RF,4                SHIFT TO LOW ORDER, HI NIBBLE                
         ZIC   RE,RBUYSTED                                                      
         SLL   RE,8+8+8+4          SHIFT OFF HI NIBBLE OR CURRENT               
         SRL   RE,8+8+8+4          START DAY                                    
         AR    RE,RF                                                            
         STC   RE,RBUYSTED                                                      
         DROP  R2                                                               
*                                                                               
BORB0060 EQU   *                                                                
         SR    RE,RE               INITIALIZE FINAL OUTPUT                      
         LA    RF,7                SET LOOP CONTROL                             
         LA    R1,RDAROERO         SET A(ROTATION ARRAY)                        
BORB0080 EQU   *                                                                
         CLI   0(R1),C' '          DAY SET?                                     
         BE    BORB0120            NO                                           
         CLI   0(R1),0             DAY SET?                                     
         BE    BORB0120            NO                                           
         LA    RE,1(RE)            YES - TURN ON LOW-ORDER BIT                  
BORB0120 EQU   *                                                                
         SLL   RE,1                SHIFT UP 1 'DAY'                             
         LA    R1,1(R1)            BUMP TO NEXT ARRAY POSITION                  
         BCT   RF,BORB0080         GO BACK AND TEST NEXT                        
         SRL   RE,1                SHIFT BACK 1 'DAY'                           
         STC   RE,ELTBUILD+3       INSERT DAYS INTO ELEMENT                     
         MVC   ELTBUILD+4(4),RDAROEST                                           
*                                  INSERT START/END TIMES INTO ELEMENT          
         MVI   ELTBUILD+8,1        INSERT WEIGHT OF 1                           
*                                  THIS DOESN'T SEEM TO CHANGE                  
*                                     NO IDEA WHY.....                          
         L     R2,AIO2             A(BUY RECORD IN PROGRESS)                    
         GOTO1 LOADELT,DMCB,(R2),ELTBUILD,=C'ADD=CODE'                          
*                                                                               
*   NOTE:  DAY/TIME ELEMENTS ENTERED VIA ORBITS WILL BE DELETED IF              
*      BUY HAS BEEN ENTERED AS A 'DAILY' BUY.  THIS SHOULD NOT HAPPEN,          
*      BUT HAS BEEN HANDLED IF IT DOES.                                         
*                                                                               
         ZIC   RF,1(R8)                                                         
         AR    R8,RF                                                            
         B     BORB0040            GO BACK FOR NEXT ELEMENT                     
BORB0400 EQU   *                                                                
         B     EXIT                                                             
         DROP  R3,R8                                                            
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*   BUYCOMMT:  BUILDS BUY COMMENT ELEMENT FOR THE FIRST TWO COMMENTS            
*        IN THE RECORD, INSERTS THEM INTO THE NEW BUY RECORD.                   
***********************************************************************         
BUYCOMMT NTR1  BASE=*,LABEL=*                                                   
         L     R3,0(R1)            RESET A(X'41' RECORD)                        
         USING RDARREC,R3                                                       
*                                                                               
         L     R2,AIO2             A(BUY RECORD IN PROGRESS)                    
         LA    R4,2                LOOP CONTROL:                                
*                                     FIRST TWO ARE BUY COMMENTS                
*                                     NEXT  TWO ARE BUY ORDER COMMENTS          
         CLC   PROGNAME,SPACES     ANY PROGRAM NAME?                            
         BE    BCOM0020            NO  - PROCEED                                
         CLI   KATZEDI,C'Y'        KATZ/EDI USES ONLY THE FIRST 32              
         BNE   BCOM0005            WITH THE LAST 2 FOR DAYPART                  
*                                                                               
*                                  REMOVE OLD X'ED' DAYPART CODE                
         GOTO1 HELLO,DMCB,(C'D',=C'REPFILE'),(X'ED',(R2)),0,0                   
         XC    ELEMENT,ELEMENT     FOR KATZ/EDI ADD THE DAYPART CODE            
         LA    R6,ELEMENT          ELEMENT                                      
         USING RBUYEDEL,R6                                                      
         MVI   RBUYEDCD,X'ED'                                                   
         MVI   RBUYEDLN,RBUYEDLQ                                                
         MVC   RBUYEDDP,PROGNAME+32                                             
         GOTO1 HELLO,DMCB,(C'P',=C'REPFILE'),(R2),ELEMENT,0                     
         DROP  R6                                                               
*                                                                               
         CLC   PROGNAME(32),SPACES ANY PROGRAM NAME?                            
         BE    BCOM0020            NO  - DON'T NEED COMMENT                     
*                                                                               
BCOM0005 EQU   *                                                                
         XC    ELTBUILD,ELTBUILD   CLEAR ELEMENT BUILD AREA                     
         MVI   ELTBUILD,X'04'      INSERT ELEMENT CODE                          
*                                  CLEAR WORKSPACE                              
         LA    R6,PROGNAME+33      SCAN PROGRAM NAME FOR BLANKS                 
         LA    RF,34               LOOP CONTROL                                 
         CLI   KATZEDI,C'Y'        KATZ/EDI USES ONLY THE FIRST 32              
         BNE   BCOM0010            WITH THE LAST 2 FOR DAYPART                  
         LA    R6,PROGNAME+31      SCAN PROGRAM NAME FOR BLANKS                 
         LA    RF,32               LOOP CONTROL                                 
BCOM0010 EQU   *                                                                
         CLI   0(R6),C' '          CHARACTER = SPACE?                           
         BNE   BCOM0015            NO  - LAST CHARACTER FOUND                   
         BCTR  R6,0                YES - BACK UP 1 SPACE                        
         BCT   RF,BCOM0010         LOOP THROUGH ALL                             
         DC    H'0'                SHOULDN'T HAPPEN:  SPACES CHECKED            
BCOM0015 EQU   *                                                                
         LA    RF,1(RF)            ADD 1 FOR KEYWORD (+2 -1 FOR EX)             
         MVC   ELTBUILD+2(2),=C'P='                                             
*                                  INSERT KEYWORD                               
         EX    RF,BCOM0505         MOVE PROGRAM NAME BY LENGTH                  
         LA    RF,3(RF)            RESTORE LENGTH + L(CONTROLS)                 
         STC   RF,ELTBUILD+1       INSERT LENGTH INTO ELEMENT                   
*                                                                               
* SKIP BUILDING P= COMMENT PROGRAM NAME ELEMENT                                 
*                                                                               
*        GOTO1 LOADELT,DMCB,(R2),ELTBUILD,=C'ADD=CODE'                          
*                                                                               
* BUILD DEDICATED PROGRAM NAME ELEMENT                                          
*                                                                               
         MVI   ELTBUILD,X'21'      PROGRAM NAME ELEMENT                         
         XC    ELTBUILD+2(L'ELTBUILD-2),ELTBUILD+2                              
         ZIC   RF,ELTBUILD+1       REUSE LENGTH FROM THE P= COMMENT             
         SHI   RF,2                ELEMENT TO BUILD LENGTH OF                   
         STC   RF,ELTBUILD+1       PROGRAM NAME ELEMENT                         
         SHI   RF,1                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   ELTBUILD+2(0),PROGNAME                                           
         GOTO1 LOADELT,DMCB,(R2),ELTBUILD,=C'ADD=CODE'                          
*                                                                               
         MVI   PROGNAME,C' '       CLEAR THE PROGRAM NAME                       
         MVC   PROGNAME+1(L'PROGNAME-1),PROGNAME                                
         BCTR  R4,0                SUBTRACT 1 FROM COMMENT COUNT                
BCOM0020 EQU   *                                                                
         LA    R8,RDARELEM         SET A(X'01' ELEMENT)                         
         ZIC   RF,1(R8)            GET LENGTH                                   
         AR    R8,RF               BUMP TO 1ST COMMT ELEMENT                    
BCOM0040 EQU   *                                                                
****                                                                            
**** SKIP FOR REVISION                                                          
****                                                                            
         B     BCOM0400            SKIP REST OF COMMENTS                        
****                                                                            
         CLI   0(R8),0             END OF RECORD?                               
         BE    BCOM0400            YES - FINISHED                               
*                                                                               
         USING RDARCTEL,R8                                                      
*                                                                               
         XC    ELTBUILD,ELTBUILD   CLEAR ELEMENT BUILD AREA                     
         MVI   ELTBUILD,X'04'      INSERT ELEMENT CODE                          
         ZIC   R1,1(R8)            GET ELEMENT LENGTH                           
         LA    RF,3                DECREMENT FOR EX + CTRL                      
         SR    R1,RF                                                            
         LA    RE,59               MAX SIZE FOR REP COMMENTS = 60               
         CR    R1,RE               NEW INPUT VS REP MAX                         
         BNH   BCOM0060            ACCEPTABLE                                   
         LR    R1,RE               NOT ACCEPTABLE                               
BCOM0060 EQU   *                                                                
         EX    R1,BCOM0500         MOVE ELEMENT TO BUILT AREA                   
         LA    R1,3(R1)            SET ELEMENT LENGTH                           
         STC   R1,ELTBUILD+1       INSERT ELEMENT LENGTH                        
         GOTO1 LOADELT,DMCB,(R2),ELTBUILD,=C'ADD=CODE'                          
         ZIC   RF,1(R8)                                                         
         AR    R8,RF                                                            
         BCT   R4,BCOM0040         GO BACK FOR NEXT ELEMENT                     
*                                     IF COUNT < 2                              
         LA    R4,2                IF MORE COMMENTS, PUT TO                     
*                                     BUY ORDER COMMENT RECORDS                 
BCOM0080 EQU   *                                                                
         CLI   0(R8),0             END OF RECORD?                               
         BE    BCOM0400            YES - FINISHED                               
*                                                                               
         XC    ELTBUILD,ELTBUILD   CLEAR ELEMENT BUILD AREA                     
         MVI   ELTBUILD,X'84'      INSERT ELEMENT CODE                          
         ZIC   R1,1(R8)            GET ELEMENT LENGTH                           
         LA    RF,3                DECREMENT FOR EX + CTRL                      
         SR    R1,RF                                                            
*                                                                               
         CH    R1,=H'60'           TRUNCATE IF MORE THAN 60 CHARS.              
         BL    BCOM0090                                                         
         LA    R1,59               MAX - 1 FOR EX                               
*                                                                               
BCOM0090 EQU   *                                                                
         EX    R1,BCOM0510         MOVE ELEMENT TO BUILD AREA                   
         LA    R1,4(R1)            RESET ELEMENT LENGTH                         
         STC   R1,ELTBUILD+1       INSERT ELEMENT LENGTH                        
         MVI   ELTBUILD+2,X'80'    TURN ON 'SENT BY REP'                        
         GOTO1 LOADELT,DMCB,(R2),ELTBUILD,=C'ADD=CODE'                          
         ZIC   RF,1(R8)                                                         
         AR    R8,RF                                                            
         BCT   R4,BCOM0080         GO BACK FOR NEXT ELEMENT                     
*                                     IF COUNT < 2                              
BCOM0400 EQU   *                                                                
         B     EXIT                                                             
*                                                                               
BCOM0500 MVC   ELTBUILD+2(0),2(R8) SET UP BUY COMMENT                           
*                                                                               
BCOM0505 MVC   ELTBUILD+4(0),PROGNAME                                           
*                                  SET UP PROGRAM NAME COMMENT                  
BCOM0510 MVC   ELTBUILD+3(0),2(R8) SET UP BUY ORDER COMMENT                     
*                                                                               
         DROP  R3,R8                                                            
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*   EFFDATEL:  BUILD AN ALTERNATE X'02' ELEMENT IN EVENT THERE                  
*        ARE NO ORBIT RECORDS.                                                  
***********************************************************************         
EFFDATEL NTR1  BASE=*,LABEL=*                                                   
         L     R3,0(R1)            RESET A(X'41' RECORD)                        
         USING RDARREC,R3                                                       
         LA    R8,RDARELEM         SET A(X'01' ELEMENT)                         
         USING RDARBYEL,R8                                                      
*                                                                               
         XC    STARTDAY(2),STARTDAY                                             
*                                  CLEAR START/END DAY WORK AREA                
         XC    ELTBILD2,ELTBILD2   CLEAR ELEMENT BUILD AREA                     
         MVC   ELTBILD2(2),=X'0209'                                             
*                                  INSERT ELEMENT CODE/LENGTH                   
         GOTO1 =A(STARTEND),DMCB,RDARBYRO,ELTBILD2+2,RR=RELO                    
*                                  INSERT START/END DAY                         
                                                                                
         SR    RE,RE               INITIALIZE FINAL OUTPUT                      
         LA    RF,7                SET LOOP CONTROL                             
         LA    R1,RDARBYRO         SET A(ROTATION ARRAY)                        
EFFD0080 EQU   *                                                                
         CLI   0(R1),C' '          DAY SET?                                     
         BE    EFFD0120            NO                                           
         CLI   0(R1),0             DAY SET?                                     
         BE    EFFD0120            NO                                           
         LA    RE,1(RE)            YES - TURN ON LOW-ORDER BIT                  
EFFD0120 EQU   *                                                                
         SLL   RE,1                SHIFT UP 1 'DAY'                             
         LA    R1,1(R1)            BUMP TO NEXT ARRAY POSITION                  
         BCT   RF,EFFD0080         GO BACK AND TEST NEXT                        
         SRL   RE,1                SHIFT DOWN 1 'DAY' FOR                       
*                                     PROPER ALIGNMENT                          
         STC   RE,ELTBILD2+3       INSERT DAYS INTO ELEMENT                     
         ZICM  RF,RDARBYST,2       CHECK START TIME                             
         C     RF,=F'2400'         AFTER MIDNIGHT?                              
         BNH   EFFD0160            NO  - LEAVE AS IS.....                       
         S     RF,=F'2400'         YES - SUBTRACT 2400 FROM FIGURE              
EFFD0160 EQU   *                                                                
         STCM  RF,3,ELTBILD2+4     SAVE START TIME                              
*                                                                               
         CLC   RDARBYST,RDARBYET   IF SAME, SKIP END TIME                       
         BE    EFFD0210                                                         
*                                                                               
         ZICM  RF,RDARBYET,2       CHECK END   TIME                             
         C     RF,=F'2400'         AFTER MIDNIGHT?                              
         BNH   EFFD0200            NO  - LEAVE AS IS.....                       
         S     RF,=F'2400'         YES - SUBTRACT 2400 FROM FIGURE              
EFFD0200 EQU   *                                                                
         STCM  RF,3,ELTBILD2+6     SAVE END   TIME                              
*                                  INSERT START/END TIMES INTO ELEMENT          
EFFD0210 EQU   *                                                                
         MVI   ELTBILD2+8,1        INSERT WEIGHT OF 1                           
*                                  THIS DOESN'T SEEM TO CHANGE                  
*                                     NO IDEA WHY.....                          
         B     EXIT                                                             
*                                                                               
         DROP  R3,R8                                                            
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*   STARTEND:  CONVERTS ROTATION MATRIX AND START DAY TO ONE-BYTE               
*        START/END DAY, INSERTS INTO ADDRESS                                    
***********************************************************************         
STARTEND NTR1  BASE=*,LABEL=*                                                   
         L     R2,0(R1)            A(INPUT: ROTATION+START DAY)                 
         L     R3,4(R1)            A(RECEIVING FIELD)                           
*                                                                               
         ZIC   RF,7(R2)            ROTATION START DAY                           
         SLL   RF,28               STRIP OFF ZONE BITS                          
         SRL   RF,28               SHIFT START DAY BACK                         
         STC   RF,ROTATDAY         SAVE ROTATION START DAY                      
*                                                                               
         LA    R8,0(R2)            A(ROTATION FIELD)                            
         LR    RE,R8               CALCULATE END OF ROTATION FIELD              
         LA    RE,7(RE)                                                         
         AR    R8,RF               GET A(1ST DAY+1)                             
         BCTR  R8,0                BACK OFF TO A(1ST ENTRY)                     
         LR    RF,R8               SAVE A(1ST ENTRY)                            
*                                     FOR WHEN 1ST ENTRY IS ONLY ENTRY          
         LA    R0,7                SET LOOP CONTROL TO SIX DAYS                 
STEX0040 EQU   *                                                                
         CR    R8,RE               END OF ROTATION FIELD REACHED?               
         BNE   STEX0080            NO                                           
         LA    R8,0(R2)            YES  - GO BACK TO FIRST LOCATION             
STEX0080 EQU   *                                                                
         CLI   0(R8),C' '          ARRAY POSITION USED?                         
         BE    STEX0200            NO  - SKIP IT                                
         CLI   0(R8),0             ARRAY POSITION USED?                         
         BE    STEX0200            NO  - SKIP IT                                
         CLI   STARTDAY,0          ANYTHING IN START DAY?                       
         BNE   STEX0120            YES - DON'T REPLACE                          
         LA    R4,7                NO  - CALCULATE STARTDAY                     
         SR    R4,R0               SUBTRACT REMAINING DAYS                      
         ZIC   R1,ROTATDAY         OFFSET BY ROTATION START DAY                 
         AR    R4,R1                                                            
         CH    R4,=H'7'            WRAPAROUND?                                  
         BNH   STEX0100            NO                                           
         SH    R4,=H'7'            YES - SUBTRACT 7                             
STEX0100 EQU   *                                                                
         STC   R4,STARTDAY         SAVE CALCULATED STARTDAY                     
STEX0120 EQU   *                                                                
         OC    0(1,R3),0(R3)       RECEIVING FIELD ENTRY MADE?                  
         BNZ   STEX0160            YES - START DAY ENTERED                      
         SLL   R4,4                NO  - MOVE START DAY TO HIGH NYBBLE          
         STC   R4,0(R3)            INSERT INTO RECORD                           
STEX0160 EQU   *                                                                
         LR    RF,R8               YES - SAVE NEW ARRAY POSITION                
STEX0200 EQU   *                                                                
         LA    R8,1(R8)            BUMP TO NEXT ARRAY LOCATION                  
         BCT   R0,STEX0040         GO BACK AND CHECK NEXT                       
         LA    R8,0(R2)            A(START OF ARRAY)                            
         BCTR  R8,0                BACK UP 1 POSITION                           
         SR    RF,R8               CALCULATED DISPLACEMENT                      
         STC   RF,ENDDAY           SAVE END DAY FOR EFF DATE SETTING            
         ZIC   RE,0(R3)            RETRIEVE START DAY                           
         AR    RF,RE               ADD START TO END                             
         STC   RF,0(R3)            PUT IT BACK IN RECORD                        
         LA    R8,0(R2)            COUNT NUMBER OF DAYS                         
         SR    RF,RF                                                            
         LA    R0,7                                                             
STEX0240 EQU   *                                                                
         CLI   0(R8),C' '          DAY ACTIVE?                                  
         BE    STEX0280            NO                                           
         CLI   0(R8),0             DAY ACTIVE?                                  
         BE    STEX0280            NO                                           
         LA    RF,1(RF)            YES - ADD 1                                  
STEX0280 EQU   *                                                                
         LA    R8,1(R8)            BUMP TO NEXT POSITION                        
         BCT   R0,STEX0240         GO BACK AND CHECK NEXT                       
         C     RF,=F'1'            COUNT = 1?                                   
         BH    STEX0320            NO  - HIGHER - EXIT                          
         BE    *+6                 YES - SET START=END IN RECORD                
         DC    H'0'                SHOULD NEVER HAPPEN                          
*                                     MEANS EMPTY ARRAY!!!                      
         ZIC   RF,0(R3)            RETRIEVE START/END DAY                       
         SLL   RF,28               DROP START DAY                               
         SRL   RF,24               MOVE END DAY BACK TO HI NYBBLE               
         NI    0(R3),X'0F'         CLEAR START DAY                              
         ZIC   RE,0(R3)            RETRIEVE 0/END DAY                           
         AR    RE,RF               ADD NEW START DAY                            
         STC   RE,0(R3)            MOVE IT BACK                                 
STEX0320 EQU   *                                                                
         B     EXIT                                                             
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*   BUYDETL:  BUILDS BUY EFFECTIVE DATE ELEMENTS, THEN INSERTS                  
*        THEM INTO THE NEW BUY RECORD.                                          
***********************************************************************         
BUYDETL  NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         L     R3,0(R1)            RESET A(X'41' RECORD)                        
         USING RDARREC,R3                                                       
*                                                                               
         GOTO1 DATCON,DMCB,(2,FLTDATES),(3,WORK)                                
         CLC   RTKODATE,WORK                                                    
         BNH   BDET0010                                                         
*                                                                               
         ST    R3,DMCB             SETUP CHOPPING CALL                          
         MVC   DMCB+4(3),RTKODATE                                               
         MVC   DMCB+8(1),STARTDAY                                               
         MVC   DMCB+9(1),ENDDAY                                                 
         MVC   DMCB+10(1),ROTATDAY                                              
         MVC   DMCB+11(1),ORBSTDAY                                              
         XC    WORK,WORK                                                        
         MVC   WORK(4),DATCON                                                   
         MVC   WORK+4(4),GETDAY                                                 
         MVC   WORK+8(4),ADDAY                                                  
         MVC   WORK+12(4),PERVERT                                               
         LA    RE,WORK                                                          
         ST    RE,DMCB+12                                                       
*                                                                               
         GOTO1 VREDARTK,DMCB                                                    
         BNE   BDETNO              BUY DELETED, EXIT                            
*                                                                               
BDET0010 EQU   *                                                                
         TM    FLAGS,FGTOTALQ      TOTAL ONLY, SKIP REST OF PROCESSING?         
         BO    BDETYES                                                          
*                                                                               
         LA    R8,RDARELEM         SET A(X'01' ELEMENT)                         
         DROP  R3                                                               
         USING RDARBDEL,R8         BUY DETAIL DESCRIP ELEMENT                   
*                                                                               
         L     R2,AIO2             A(BUY RECORD IN PROGRESS)                    
         USING RBUYREC,R2                                                       
*                                                                               
         NI    MISCFLAG,X'7F'      TURN OFF 'DAILY' BUY FLAG                    
         TM    RDARBDFL,X'80'      'DAILY' BUY?                                 
         BNO   BDET0020            NO                                           
         OI    MISCFLAG,X'80'      SET FLAG FOR 'DAILY' BUY                     
BDET0020 EQU   *                                                                
         TM    MISCFLAG,X'80'      'DAILY' BUY?                                 
         BNO   BDET0030            NO                                           
         CLI   ORBFLAG,C'Y'        ANY ORBIT RECORDS?                           
         BNE   BDET0040            NO                                           
         GOTO1 DELELT02,DMCB,RBUYREC                                            
*                                  CLEAR ORBIT X'02' DAY/TIME ELTS              
         MVI   ORBFLAG,C'N'        SET ORBIT FLAG TO NO                         
         OI    MISCFLAG,X'40'      SET 'ORBIT DROPPED' FLAG                     
         B     BDET0040                                                         
BDET0030 EQU   *                                                                
         CLI   ORBFLAG,C'Y'        ANY ORBIT RECORDS?                           
         BE    BDET0060            YES - DON'T NEED SECONDARY -                 
*                                  ORBIT HAS ALREADY ADDED THE X'02'            
*                                     ELT.  SHOULD NEVER ABE ORB W/             
*                                        'DAILY' RECORDS                        
BDET0040 EQU   *                                                                
         GOTO1 LOADELT,DMCB,(R2),ELTBILD2,=C'ADD=CODE'                          
         XC    ELTBILD2,ELTBILD2                                                
BDET0060 EQU   *                                                                
*                                  MOVE FROM DESCRIP ELT TO DETAIL              
         ZIC   RF,1(R8)            GET LENGTH                                   
         AR    R8,RF               BUMP TO 1ST DETAIL ELEMENT                   
BDET0080 EQU   *                                                                
         CLI   0(R8),0             END OF RECORD?                               
         BE    BDETYES             YES - FINISHED                               
*                                                                               
         USING RDARBUEL,R8                                                      
*                                                                               
         TM    MISCFLAG,X'80'      'DAILY' BUY?                                 
         BO    BDET0100            YES - SKIP CHECK FOR COST BREAK ->           
*                                     EACH ELEMENT BECOMES A BUY REC            
         GOTO1 DETLBRAK,DMCB,(RC),(R8),(R2)                                     
*                                  CHECK FOR COST BREAK                         
BDET0100 EQU   *                                                                
         MVI   DETLFLAG,C'Y'       SET DETAILS TO YES                           
         OC    RBUYNW,RBUYNW       NUMBER/WEEK FILLED IN?                       
         BNZ   BDET0120            YES                                          
*                                                                               
         MVC   RBUYNW,RDARBUSW     NO  - INSERT NUMBER PER WEEK                 
BDET0120 EQU   *                                                                
         XC    ELTBUILD,ELTBUILD   CLEAR ELEMENT BUILD AREA                     
         MVC   ELTBUILD(2),=X'030B'                                             
*                                  INSERT ELEMENT CODE/LENGTH                   
         GOTO1 DATCON,DMCB,(2,RDARBUSD),(0,WORK)                                
*                                  CONVERT START DATE TO EBCDIC                 
         TM    MISCFLAG,X'80'      'DAILY' BUY?                                 
         BNO   BDET0130            NO  -                                        
         MVC   WORK+12(6),WORK     YES - USE START AS IS, THEN                  
*                                     SET END TO START                          
         B     BDET0190                                                         
BDET0130 EQU   *                                                                
         ZIC   RF,RDARBUWK         CALCULATE NUMBER OF DAYS                     
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         BCTR  RF,0                MAKE WEEKS ZERO RELATIVE                     
         MH    RF,=H'7'            MULTIPLY WEEKS BY 7                          
         GOTO1 ADDAY,DMCB,WORK,WORK+6,(RF)                                      
*                                                                               
         GOTO1 GETDAY,DMCB,WORK,WORK+12                                         
*                                  GET DAY OF START WEEK                        
         ZIC   RF,DMCB             GET DAY OF WEEK                              
         BCTR  RF,0                MAKE DAY ZERO RELATIVE                       
         LTR   RF,RF               MONDAY?                                      
         BZ    BDET0140            YES - LEAVE                                  
         LNR   RF,RF               NEGATE REGISTER                              
         GOTO1 ADDAY,DMCB,WORK,WORK+12,(RF)                                     
*                                  BACK UP TO MONDAY                            
*                                                                               
* IN CASE OF OUT OF WEEK ROTATORS, COMPARE BUY START DAY AND ROTATION           
* START DAY TO GET THE CORRECT MONDAY DATE                                      
*                                                                               
         CLC   STARTDAY,ROTATDAY                                                
         BNL   BDET0135                                                         
         MVC   WORK(6),WORK+12                                                  
         LA    RF,7                                                             
         GOTO1 ADDAY,DMCB,WORK,WORK+12,(RF)                                     
*                                                                               
BDET0135 DS    0H                                                               
         MVC   WORK(6),WORK+12                                                  
*                                                                               
BDET0140 EQU   *                                                                
         CLI   ORBSTDAY,0          ANY ORBIT START DAY?                         
         BZ    BDET0160            NO  - USE HEADER START DAY                   
         ZIC   RF,ORBSTDAY         YES - USE IT                                 
         B     BDET0180                                                         
BDET0160 EQU   *                                                                
         ZIC   RF,STARTDAY         ADD START DAY                                
BDET0180 EQU   *                                                                
         BCTR  RF,0                MAKE ZERO RELATIVE                           
         GOTO1 ADDAY,DMCB,WORK,WORK+12,(RF)                                     
*                                  BUMP TO START DAY IN WEEK                    
BDET0190 EQU   *                                                                
         GOTO1 DATCON,DMCB,(0,WORK+12),(3,ELTBUILD+2)                           
*                                  INSERT START DATE INTO ELEMENT               
         TM    MISCFLAG,X'80'      'DAILY' BUY?                                 
         BNO   BDET0200            NO                                           
         MVC   ELTBUILD+5(3),ELTBUILD+2                                         
*                                  YES - SET END DATE = START DATE              
         B     BDET0260                                                         
BDET0200 EQU   *                                                                
*                                                                               
         GOTO1 GETDAY,DMCB,WORK+6,WORK+12                                       
*                                  GET DAY OF END   WEEK                        
         ZIC   RF,DMCB             GET DAY OF WEEK                              
         BCTR  RF,0                MAKE DAY ZERO RELATIVE                       
         LTR   RF,RF               MONDAY?                                      
         BZ    BDET0240            YES - LEAVE                                  
         LNR   RF,RF               NEGATE REGISTER                              
         CLC   STARTDAY,ENDDAY     START/END ON SAME DAY?                       
*                                     (SINGLE-DAY BUY?)                         
         BE    BDET0220            YES - DON'T BUMP TO NEXT WEEK                
         BL    BDET0220            START < END DAY:  NOT AN                     
*                                     OOWR - DON'T BUMP                         
         LA    RF,7(RF)            BUMP IT INTO NEXT WEEK                       
BDET0220 EQU   *                                                                
         GOTO1 ADDAY,DMCB,WORK+6,WORK+12,(RF)                                   
*                                  BACK UP TO MONDAY                            
*                                                                               
* IN CASE OF OUT OF WEEK ROTATORS, COMPARE BUY START DAY AND ROTATION           
* START DAY TO GET THE CORRECT MONDAY DATE                                      
*                                                                               
         CLC   STARTDAY,ROTATDAY                                                
         BNL   BDET0230                                                         
         MVC   WORK+6(6),WORK+12                                                
         LA    RF,7                                                             
         GOTO1 ADDAY,DMCB,WORK+6,WORK+12,(RF)                                   
*                                                                               
BDET0230 DS    0H                                                               
         MVC   WORK+6(6),WORK+12                                                
*                                                                               
BDET0240 EQU   *                                                                
         ZIC   RF,ENDDAY           ADD END   DAY                                
         BCTR  RF,0                MAKE ZERO RELATIVE                           
         GOTO1 ADDAY,DMCB,WORK+6,WORK+12,(RF)                                   
*                                  BUMP TO END   DAY IN WEEK                    
         GOTO1 DATCON,DMCB,(0,WORK+12),(3,ELTBUILD+5)                           
*                                  INSERT END   DATE INTO ELEMENT               
*                                                                               
BDET0260 EQU   *                                                                
         MVI   ELTBUILD+8,X'80'    SET TO 'EVERY WEEK'                          
         CLC   RBUYNW,RDARBUSW     HEADER #/WK = DETAIL #/WK?                   
         BE    BDET0280            YES                                          
         OI    ELTBUILD+8,X'01'    NO  - PUT IN 'OVERRIDE' FLAG                 
         B     BDET0290                                                         
BDET0280 EQU   *                                                                
         CLI   RDARBUSW,0          SPOT PER WEEK ZERO??                         
         BNE   BDET0290            RBUYNW MIGHT GET SET TO SOMETHING            
         OI    ELTBUILD+8,X'01'    ELSE LATER, SET OVERRIDE ANYWAY              
*                                                                               
BDET0290 EQU   *                                                                
         MVC   ELTBUILD+9(1),RDARBUSW                                           
*                                  INSERT NUMBER PER WEEK                       
         MVC   ELTBUILD+10(1),RDARBUWK                                          
*                                  INSERT NUMBER OF WEEKS                       
         ZIC   RF,RDARBUWK         ACCUMULATE NUMBER OF WEEKS                   
         SR    R0,R0                  AND CALC # SPOTS                          
         ZIC   R1,RDARBUSW         NUMBER SPOTS PER WEEK                        
         MR    R0,RF               # SPOTS X # WEEKS                            
         L     RE,SPOTCTR                                                       
         AR    RE,R1               ACCUMULATE NUMBER OF SPOTS                   
         ST    RE,SPOTCTR          STORE IT BACK                                
         A     RF,WEEKCTR          ACCUMULATE NUMBER OF WEEKS                   
         ST    RF,WEEKCTR          STORE IT BACK                                
         L     R2,AIO2             A(BUY RECORD IN PROGRESS)                    
         GOTO1 LOADELT,DMCB,(R2),ELTBUILD,=C'ADD=CODE'                          
         TM    MISCFLAG,X'80'      'DAILY' BUY?                                 
         BNO   BDET0300            NO  - GO BACK FOR NEXT ELEMENT               
         BAS   RE,GENDAILY         YES - GENERATE A BUY RECORD                  
BDET0300 EQU   *                                                                
         ZIC   RF,1(R8)                                                         
         AR    R8,RF                                                            
         B     BDET0080            GO BACK FOR NEXT ELEMENT                     
BDETYES  SR    RC,RC                                                            
BDETNO   LTR   RC,RC                                                            
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
*   GENDAILY:  FOR 'DAILY' BUYS, EACH DETAIL IS TO BE A SEPARATE BUY            
*        RECORD.  THE DETAIL'S DETAILS ARE INSERTED INTO THE HEADER,            
*        AND THE RECORD WRITTEN TO THE FILE.                                    
***********************************************************************         
GENDAILY NTR1                                                                   
         OI    RBUYFLG2,X'80'      SET 'DAILY ORDER' FLAG                       
         MVC   RBUYNW,RDARBUSW     INSERT NUMBER SPOTS/WEEK                     
         MVC   RBUYTSPT,RDARBUSW   INSERT TOTAL SPOT: SAME AS SPTS/WK           
         MVC   RBUYCOS,RDARBU$$    INSERT SPOT COST                             
         MVC   BUYCOST,RDARBU$$    SAVE   SPOT COST FOR CALCULATION             
*                                  INSERT ELEMENT CODE/LENGTH                   
         GOTO1 DATCON,DMCB,(2,RDARBUSD),(0,WORK)                                
*                                  CONVERT START DATE TO EBCDIC                 
         GOTO1 GETDAY,DMCB,WORK,WORK+12                                         
*                                  GET DAY OF WEEK OF START DAY                 
         ZIC   RF,DMCB             GET DAY OF WEEK:  START DAY                  
         SLL   RF,4                SHIFT TO LOW ORDER, HI NYBBLE                
         ZIC   RE,DMCB             GET DAY OF WEEK:  END   DAY                  
         AR    RF,RE               ADD END DAY TO START DAY                     
         STC   RF,RBUYSTED         INSERT START/END DAYS                        
         LA    RF,RBUYELEM         SET A(01 ELEMENT)                            
GDAI0020 EQU   *                                                                
         ZIC   RE,1(RF)            GET LENGTH                                   
         AR    RF,RE               BUMP TO NEXT                                 
         CLI   0(RF),0             END OF RECORD?                               
         BNE   *+6                 NO                                           
         DC    H'0'                YES - SHOULDN'T HAPPEN                       
         CLI   0(RF),2             DAY/TIME ELEMENT?                            
         BNE   GDAI0020            NO  - GO BACK FOR NEXT                       
         MVC   RBUYDYIN-RBUYDYEL(1,RF),RBUYSTED                                 
*                                  YES - INSERT START/END FROM HDR              
         ZIC   RE,RBUYSTED         GET ST/END DAYS                              
         SRL   RE,4                DROP THE END DAY                             
         LA    R3,DAYTABLE                                                      
         AR    R3,RE               ADD DAY TO TABLE ADDR                        
         MVC   RBUYDAYS-RBUYDYEL(1,RF),0(R3)                                    
*                                  INSERT DAYS INTO ELEMENT -                   
*                                     ALWAYS A SINGLE DAY                       
         GOTO1 =A(GENREC),RR=RELO                                               
*                                                                               
         ZIC   RF,BUYLINE#         BUMP BUYLINE #                               
         LA    RF,1(RF)                                                         
         STC   RF,BUYLINE#                                                      
*                                                                               
* KEEP CURRENT AGENCY BUY 'MASTER LINE NUMBER'                                  
*        STC   RF,RBUYKMLN         INSERT NEW NUMBER IN MASTER LINE#            
         STC   RF,RBUYKLIN         INSERT NEW NUMBER IN LINE #                  
         GOTO1 DELELT,DMCB,RBUYREC                                              
         MVI   DETLFLAG,C'N'       RESET DETAIL COUNT TO NONE                   
         B     EXIT                                                             
*                                                                               
DAYTABLE DC    X'8040201008040201'                                              
*                                                                               
         DROP  R2,R8                                                            
         EJECT                                                                  
***********************************************************************         
*   DETLBRAK:  IF DETAIL COST IS DIFFERENT THAN BUYHDR COST, AND                
*      PREVIOUS DETAILS HAVE BEEN ENCOUNTERED WITHIN THE BUYHDR                 
*      GROUP, THE FOLLOWING STEPS MUST BE TAKEN:                                
*         1.  THE BUY MUST BE OUTPUT                                            
*         2.  THE NEXT BUYLINE NUMBER MUST BE CALCULATED                        
*         3.  THE RECORD MUST BE CLEARED OF DETAIL INFORMATION                  
*         4.  PROCESSING OF THE NEW DETAIL WILL THEN CONTINUE                   
***********************************************************************         
DETLBRAK NTR1                                                                   
         L     RC,0(R1)            RESET A(WORKSPACE)                           
         L     R3,4(R1)            RESET A(DETAIL ELEMENT)                      
         USING RDARBUEL,R3         BUY DETAIL ELEMENT                           
*                                                                               
         L     R4,8(R1)            RESET A(RBUYREC)                             
         USING RBUYREC,R4                                                       
*                                                                               
         CLI   DETLFLAG,C'N'       ANY DETAILS ENCOUNTERED?                     
         BNE   DBRA0040            YES - CHECK FOR $$ CHANGE                    
         MVC   RBUYCOS,RDARBU$$    NO  - INSERT BUY COST IN CASE                
*                                     DIFFERENT FROM HEADER COST                
         MVC   BUYCOST,RBUYCOS     SAVE COST FOR CALCULATION                    
         MVC   RBUYNW,RDARBUSW     INSERT SPOTS/WK IN CASE DIFFERENT            
*                                                                               
         B     DBRA0200            EXIT                                         
DBRA0040 EQU   *                                                                
         CLC   RBUYCOS,RDARBU$$    BUY COST = DETAIL COST?                      
         BE    DBRA0200            YES - FINISHED                               
         GOTO1 =A(GENREC),RR=RELO                                               
*                                                                               
         MVC   RBUYCOS,RDARBU$$    INSERT NEW COST                              
         MVC   BUYCOST,RBUYCOS     SAVE COST FOR CALCULATION                    
         MVC   RBUYNW,RDARBUSW     INSERT SPOTS/WK IN CASE DIFFERENT            
         ZIC   RF,BUYLINE#         BUMP BUYLINE #                               
         LA    RF,1(RF)                                                         
         STC   RF,BUYLINE#                                                      
*                                                                               
* KEEP CURRENT AGENCY BUY 'MASTER LINE NUMBER'                                  
*        STC   RF,RBUYKMLN         INSERT NEW NUMBER IN MASTER LINE#            
         STC   RF,RBUYKLIN         INSERT NEW NUMBER IN LINE #                  
         GOTO1 DELELT,DMCB,RBUYREC                                              
         MVI   DETLFLAG,C'N'       RESET DETAIL COUNT TO NONE                   
DBRA0200 EQU   *                                                                
         B     EXIT                                                             
         DROP  R3,R4                                                            
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*   GENCOMMT:  GENERATE A COMMENT RECORD IN THOSE INSTANCES WHEN                
*      A PROGRAM NAME IS PRESENT, AND NO DARE COMMENTS HAVE BEEN                
*      FOUND.  OTHERWISE, THE PROGRAM NAME AS A COMMENT WILL BE                 
*      SKIPPED.                                                                 
***********************************************************************         
GENCOMMT NTR1  BASE=*,LABEL=*                                                   
         L     R2,0(R1)            RESET A(BUYREC)                              
         USING RBUYREC,R2                                                       
*                                                                               
         CLI   KATZEDI,C'Y'        KATZ/EDI USES ONLY THE FIRST 32              
         BNE   GCOM0005            WITH THE LAST 2 FOR DAYPART                  
*                                                                               
*                                  REMOVE OLD X'ED' DAYPART CODE                
         GOTO1 HELLO,DMCB,(C'D',=C'REPFILE'),(X'ED',(R2)),0,0                   
         XC    ELEMENT,ELEMENT     FOR KATZ/EDI ADD THE DAYPART CODE            
         LA    R6,ELEMENT          ELEMENT                                      
         USING RBUYEDEL,R6                                                      
         MVI   RBUYEDCD,X'ED'                                                   
         MVI   RBUYEDLN,RBUYEDLQ                                                
         MVC   RBUYEDDP,PROGNAME+32                                             
         GOTO1 HELLO,DMCB,(C'P',=C'REPFILE'),(R2),ELEMENT,0                     
         DROP  R6                                                               
*                                                                               
GCOM0005 EQU   *                                                                
         XC    ELTBUILD,ELTBUILD   CLEAR ELEMENT BUILD AREA                     
         MVI   ELTBUILD,X'04'      INSERT ELEMENT CODE                          
*                                  CLEAR WORKSPACE                              
         LA    R6,PROGNAME+33      SCAN PROGRAM NAME FOR BLANKS                 
         LA    RF,34               LOOP CONTROL                                 
         CLI   KATZEDI,C'Y'        KATZ/EDI USES ONLY THE FIRST 32              
         BNE   GCOM0010            WITH THE LAST 2 FOR DAYPART                  
         LA    R6,PROGNAME+31      SCAN PROGRAM NAME FOR BLANKS                 
         LA    RF,32               LOOP CONTROL                                 
GCOM0010 EQU   *                                                                
         CLI   0(R6),C' '          CHARACTER = SPACE?                           
         BNE   GCOM0015            NO  - LAST CHARACTER FOUND                   
         BCTR  R6,0                YES - BACK UP 1 SPACE                        
         BCT   RF,GCOM0010         LOOP THROUGH ALL                             
         DC    H'0'                SHOULDN'T HAPPEN:  SPACES CHECKED            
GCOM0015 EQU   *                                                                
         LA    RF,1(RF)            ADD 1 FOR KEYWORD (+2 -1 FOR EX)             
         MVC   ELTBUILD+2(2),=C'P='                                             
*                                  INSERT KEYWORK                               
         EX    RF,GCOM0505         MOVE PROGRAM NAME BY LENGTH                  
         LA    RF,3(RF)            RESTORE LENGTH + L(CONTROLS)                 
         STC   RF,ELTBUILD+1       INSERT LENGTH INTO ELEMENT                   
*                                                                               
* SKIP BUILDING P= COMMENT PROGRAM NAME ELEMENT                                 
*                                                                               
*        GOTO1 HELLO,DMCB,(C'P',=C'REPFILE'),RBUYREC,ELTBUILD,         X        
               =C'ADD=CODE'                                                     
*                                                                               
* BUILD DEDICATED PROGRAM NAME ELEMENT                                          
*                                                                               
         MVI   ELTBUILD,X'21'      PROGRAM NAME ELEMENT                         
         XC    ELTBUILD+2(L'ELTBUILD-2),ELTBUILD+2                              
         ZIC   RF,ELTBUILD+1       REUSE LENGTH FROM THE P= COMMENT             
         SHI   RF,2                ELEMENT TO BUILD LENGTH OF                   
         STC   RF,ELTBUILD+1       PROGRAM NAME ELEMENT                         
         SHI   RF,1                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   ELTBUILD+2(0),PROGNAME                                           
         GOTO1 HELLO,DMCB,(C'P',=C'REPFILE'),RBUYREC,ELTBUILD,         X        
               =C'ADD=CODE'                                                     
*                                                                               
         MVI   PROGNAME,C' '       CLEAR THE PROGRAM NAME                       
         MVC   PROGNAME+1(L'PROGNAME-1),PROGNAME                                
         B     EXIT                                                             
*                                                                               
GCOM0505 MVC   ELTBUILD+4(0),PROGNAME                                           
*                                                                               
         DROP  R2                                                               
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* ROUTINE RIPPED FROM REDAR20                                                   
* ADD OR UPDATE RECORD TO FILE                                                  
***********************************************************************         
GENREC   NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         MVC   AIO,AIO2            SET IO AREA = IOAREA2                        
         L     R2,AIO                                                           
         USING RBUYREC,R2                                                       
*                                                                               
*   INSERT TOTAL SPOTS, TOTAL COST, AND TOTAL WEEKS FIGURES                     
*                                                                               
         MVC   RBUYTSPT,SPOTCTR+2  LAST TWO POSITIONS ONLY                      
         L     RF,SPOTCTR          ACCUMULATE TOTAL SPOTS                       
         A     RF,ORDTOTSP                                                      
         ST    RF,ORDTOTSP         SAVE ORDER TOTAL SPOTS                       
         MVC   RBUYTWKS,WEEKCTR+3  LAST POSITION ONLY                           
         ZICM  RF,RBUYTSPT,2       LOAD TOTAL SPOTS                             
         SR    R0,R0                                                            
         L     R1,BUYCOST          LOAD COST PER SPOT                           
         MR    R0,RF               # SPOTS X COST/SPOT                          
         STCM  R1,15,RBUYTCOS      INSERT TOTAL COST                            
         A     R1,ORDTOT$$         ACCUMULATE ORDER TOTAL DOLLARS               
         ST    R1,ORDTOT$$         SAVE ORDER TOTAL DOLLARS                     
         XC    SPOTCTR,SPOTCTR     CLEAR ACCUMULATORS                           
         XC    WEEKCTR,WEEKCTR                                                  
         XC    BUYCOST,BUYCOST                                                  
*                                                                               
         MVC   SAVEKEY,KEY         SAVE CURRENT KEY FOR RESTART                 
         MVC   KEY,RBUYKEY         GET KEY OF BUY                               
*                                  LOOK FOR OLD RECORD                          
*                                     GENCON DOESN'T LIKE 'ADDREC'              
         MVC   KEYSAVE,KEY                                                      
         MVI   RDUPDATE,C'Y'       READ FOR UPDATE                              
         OI    DMINBTS,X'08'       RETURN DELETED KEYS ALSO                     
         GOTO1 HIGH                READ THE KEY                                 
         CLC   KEYSAVE(27),KEY     KEY ALREADY ON FILE?                         
         BE    GENR0040            YES - RECORD/KEY MUST BE REPLACED            
*                                                                               
         MVC   KEY(27),KEYSAVE     NO  - RESET NEW KEY                          
         CLI   RBUYVER,1           VERSION = 1?                                 
         BE    GENR0020            YES - ORIGINAL CREATION                      
         MVI   RBUYCHGI,C'A'       NO  - ADDED ON THIS PASS                     
         MVI   RBUYCHGI+1,0        CLEAR SECOND CHG BYTE                        
GENR0020 EQU   *                                                                
         GOTO1 ADDREC              ADD THE RECORD/KEY                           
*                                     GENCON TRAPS ERRORS                       
         B     GENR0080            RECORD ADDED SUCCESSFULLY                    
GENR0040 EQU   *                                                                
*                                                                               
*                                     OLD RECORD MAY HAVE BEEN DELETED:         
*                                     STILL MUST ACCESS KEY/RECORD              
*                                        FOR UPDATE                             
*                                                                               
*                                                                               
         NI    KEY+27,X'FF'-X'80'  RESTORE KEY                                  
         MVC   AIO,AIOAREA         SET ALTERNATE IO AREA                        
         MVI   RDUPDATE,C'Y'       READ FOR UPDATE                              
         OI    DMINBTS,X'08'       RETURN DELETED KEYS ALSO                     
         GOTO1 GETREC               RETRIEVE ORIGINAL RECORD                    
*                                                                               
         MVC   AIO,AIO2            SET IO AREA FOR NEW BUY                      
         GOTO1 PUTREC              WRITE UPDATED RECORD TO FILE                 
         GOTO1 WRITE               REWRITE CLEARED KEY FOR RECORD               
GENR0080 EQU   *                                                                
GENR0100 EQU   *                                                                
         MVC   AIO,AIO1            RESET A(X'41' RECORD AREA)                   
         MVC   KEY,SAVEKEY         RESET KEY FOR X'41' RECS                     
         GOTO1 HIGH                                                             
         B     EXIT                                                             
         DROP  R2                                                               
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* INITIALIZE TSAR                                                               
***********************************************************************         
INITTSAR NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         ICM   R0,14,=X'D9000A'                                                 
         ICM   R0,1,=AL1(QTSAR)                                                 
         GOTOX CALLOV,DMCB,0,(R0)                                               
         MVC   VTSAR,0(R1)         VTSAR                                        
*                                                                               
         GOTOX GOTSAR,TSAINI                                                    
*                                                                               
         B     EXIT                                                             
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* FOR ALL X'0B01' AND X'0B' BUY RECORDS:                                        
* BUILD BUY ATTRIBUTE TSAR RECORDS FOR DAYS, TIMES AND PROGRAM NAME             
* BUILD BUY SEGMENTS                                                            
* FIRST AND SECOND LOOP BUILD BUY ATTRIBUTE RECORDS FOR BOTH BUY TYPES          
* THIRD AND FORTH LOOP BUILD BUY SEGMENT RECORDS FOR BOTH BUY TYPES             
***********************************************************************         
BLDTSREC NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         NI    FLAGS,X'FF'-FGBSEGQ BUY SEGMENT NOT YET BUILD                    
*                                                                               
         XC    RPSPTTOT,RPSPTTOT   CLEAR TOTALS FOR PRINTING                    
         XC    RPORDTOT,RPORDTOT                                                
         XC    AGSPTTOT,AGSPTTOT                                                
         XC    AGORDTOT,AGORDTOT                                                
*                                                                               
*                                                                               
* PROCESS AGENCY SHADOW BUYS X'0B01'                                            
*                                                                               
BLDTR05  DS    0H                                                               
         MVC   AIO,AIO1            SET IO AREA                                  
         XC    KEY,KEY                                                          
         LA    R6,KEY                                                           
         USING RBUYKEY,R6                                                       
         MVC   RBUYKTYP(2),=X'0B01' AGENCY BUY SHADOW RECORD TYPE               
         MVC   RBUYKREP,AGENCY     INSERT REP CODE                              
         MVC   RBUYKCON,COMPCON    INSERT CONTRACT #, 9/COMP/REV                
         DROP  R6                                                               
*                                                                               
BLDTR10  DS    0H                                                               
         NI    DMINBTS,X'FF'-X'08' DON'T PASS DELETES                           
         GOTO1 HIGH                                                             
*                                                                               
BLDTR20  DS    0H                                                               
         CLC   KEY(22),KEYSAVE                                                  
         BNE   BLDTR200            AGENCY CANCELLED ALL BUYS                    
*                                                                               
         GOTO1 GETREC                                                           
*                                                                               
         L     R6,AIO                                                           
         USING RBUYREC,R6          CHECK AND SKIP                               
         CLI   RBUYCHGI,C'C'       CANCELLED BUT NOT DELETED                    
         BE    BLDTR40                                                          
         DROP  R6                                                               
*                                                                               
         TM    FLAGS,FGBSEGQ       WHICH TSAR RECORDS ARE WE BUILDING?          
         BO    BLDTR30                                                          
*                                                                               
* BUILD DAYS, TIMES AND PROGRAM NAME TSAR RECORDS                               
*                                                                               
         GOTOX (X'01',=A(BLDDTP)),RR=RELO                                       
         GOTO1 GOTSAR,TSAADD                                                    
*                                                                               
         GOTOX (X'02',=A(BLDDTP)),RR=RELO                                       
         GOTO1 GOTSAR,TSAADD                                                    
*                                                                               
         GOTOX (X'03',=A(BLDDTP)),RR=RELO                                       
         BH    BLDTR40                                                          
         GOTO1 GOTSAR,TSAADD                                                    
         B     BLDTR40             TSAR RECORDS                                 
*                                                                               
BLDTR30  DS    0H                  BUILD BUY SEGMENTS                           
         GOTO1 =A(BLDSEGS),RR=RELO                                              
*                                                                               
BLDTR40  DS    0H                                                               
         NI    DMINBTS,X'FF'-X'08' DON'T PASS DELETES                           
         GOTO1 SEQ                                                              
         B     BLDTR20                                                          
*                                                                               
* PROCESS REPPAK BUYS                                                           
*                                                                               
BLDTR200 DS    0H                                                               
         CLC   =X'0B00',KEYSAVE    REPPAK BUYS PROCESSED ALREADY?               
         BE    BLDTR300                                                         
         XC    KEY,KEY                                                          
         LA    R6,KEY                                                           
         USING RBUYKEY,R6                                                       
         MVI   RBUYKTYP,X'0B'      REPPAK BUY RECORD TYPE                       
         MVC   RBUYKREP,AGENCY     INSERT REP CODE                              
         MVC   RBUYKCON,COMPCON    INSERT CONTRACT #, 9/COMP/REV                
         B     BLDTR10                                                          
         DROP  R6                                                               
*                                                                               
BLDTR300 DS    0H                                                               
         TM    FLAGS,FGBSEGQ                                                    
         BO    BLDTRX                                                           
         OI    FLAGS,FGBSEGQ                                                    
         B     BLDTR05                                                          
*                                                                               
BLDTRX   DS    0H                                                               
         B     EXIT                                                             
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* BUILD DAYS, TIMES AND PROGRAM NAMES TSAR RECORDS                              
* BUY RECORD ALREADY DELIVERED TO AIO                                           
* P1,HOB, X'01' = DAYS                                                          
*         X'02' = TIMES                                                         
*         X'03' = PROGRAM NAME                                                  
***********************************************************************         
BLDDTP   NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         XC    TSARREC,TSARREC                                                  
         STCM  RF,8,TR.TLKTYP                                                   
*                                                                               
         MVC   SVELCODE,ELCODE                                                  
*                                                                               
         CLI   TR.TLKTYP,X'01'     DAYS                                         
         BE    BLDDTP10                                                         
         CLI   TR.TLKTYP,X'02'     TIMES                                        
         BE    BLDDTP10                                                         
         CLI   TR.TLKTYP,X'03'     PROGRAM NAME                                 
         BE    BLDDTP50                                                         
         DC    H'0'                                                             
*                                                                               
BLDDTP10 DS    0H                                                               
         L     R6,AIO                                                           
         MVI   ELCODE,X'02'                                                     
         BRAS  RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING RBUYDYEL,R6                                                      
*                                                                               
         LA    R3,TR.TLDATA                                                     
*                                                                               
BLDDTP20 DS    0H                                                               
         CLI   TR.TLKTYP,X'01'     DAYS                                         
         BNE   BLDDTP30                                                         
         MVC   0(2,R3),RBUYDYIN                                                 
*                                                                               
* IF SINGLE DAY, CHANGE LEFTMOST NIBBLE TO X'F' SO SINGLE DAY WILL SORT         
* AFTER DAY RANGES (IE: WED, X'33' WILL BECOME 'F3')                            
*                                                                               
         XC    HALF,HALF                                                        
         MVZ   HALF(1),RBUYDYIN                                                 
         ZIC   R1,HALF                                                          
         SRL   R1,4                                                             
         STC   R1,HALF             SHIFT START DAY TO LOWER HALF BYTE           
         MVN   HALF+1(1),RBUYDYIN                                               
         CLC   HALF(1),HALF+1      SINGLE DAY?                                  
         BNE   BLDDTP25                                                         
         OI    0(R3),X'F0'         START DAY NIBBLE IS SET TO X'F'              
*                                                                               
BLDDTP25 DS    0H                                                               
         AHI   R3,2                                                             
         B     BLDDTP40                                                         
*                                                                               
BLDDTP30 DS    0H                  TIMES                                        
         MVC   0(4,R3),RBUYDYT1                                                 
*                                                                               
         CLC   RBUYDYT1,=Y(0459)   00:01AM - 04:59AM                            
         BH    BLDDTP35            SORTS LATER SINCE BROADCAST DAY              
         ZICM  RE,RBUYDYT1,2       ENDS AT 4:59AM                               
         AHI   RE,2400                                                          
         STCM  RE,3,0(R3)                                                       
*                                                                               
BLDDTP35 DS    0H                                                               
         OC    RBUYDYT2,RBUYDYT2   SINGLE TIME?                                 
         BZ    BLDDTP38                                                         
         CLC   RBUYDYT2,=Y(0459)                                                
         BH    BLDDTP38                                                         
         ZICM  RE,RBUYDYT2,2                                                    
         AHI   RE,2400                                                          
         STCM  RE,3,2(R3)                                                       
*                                                                               
BLDDTP38 DS    0H                                                               
         AHI   R3,4                                                             
*                                                                               
BLDDTP40 DS    0H                                                               
         BRAS  RE,NEXTEL                                                        
         BE    BLDDTP20                                                         
         DROP  R6                                                               
*                                                                               
         LA    RE,TR.TLKTYP        INSERT VARIABLE LENGTH                       
         SR    R3,RE                                                            
         AHI   R3,2                OVERHEAD                                     
         STCM  R3,3,TR.TLLEN                                                    
         CHI   R3,255              CHECK IF ENOUGH ROOM                         
         BNH   BLDDTPX                                                          
         DC    H'0'                                                             
*                                                                               
* PROGRAM NAME                                                                  
*                                                                               
BLDDTP50 DS    0H                                                               
         L     R6,AIO                                                           
         MVI   ELCODE,X'21'                                                     
         BRAS  RE,GETEL                                                         
         BE    BLDDTP60                                                         
         MVC   ELCODE,SVELCODE                                                  
         B     EXITH                                                            
*                                                                               
BLDDTP60 DS    0H                                                               
         USING RBUYPGEL,R6                                                      
         ZIC   R1,RBUYPGLN                                                      
         SHI   R1,3                CODE+LENGTH+OVERHEAD                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   TR.TLDATA(0),RBUYPGM                                             
*                                                                               
         AHI   R1,4                TYPE+OVERHEAD                                
         STCM  R1,3,TR.TLLEN       INSERT VARIABLE LENGTH                       
*                                                                               
BLDDTPX  DS    0H                                                               
         MVC   ELCODE,SVELCODE                                                  
         B     EXITOK                                                           
         DROP  R6                                                               
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* LOOP THROUGH ALL X'0B01'/X'0B' RECORDS. FOR EACH RECORD, LOOP THROUGH         
* X'03' EFFECTIVE DATE ELEMENTS AND BUILD BUY SEGMENTS BASE ON SINGLE           
* WEEK-OF DATE.                                                                 
***********************************************************************         
BLDSEGS  NTR1  BASE=*,LABEL=*                                                   
         L     R4,AIO                                                           
BUYD     USING RBUYREC,R4                                                       
*                                                                               
* CALCULATE TOTAL SPOTS AND DOLLARS FOR BOTH REP AND AGENCY                     
*                                                                               
         CLC   =X'0B01',BUYD.RBUYKEY                                            
         BE    BLDSEG03                                                         
         ZICM  RE,RPSPTTOT,2                                                    
         ZICM  RF,BUYD.RBUYTSPT,2                                               
         AR    RE,RF                                                            
         STCM  RE,3,RPSPTTOT                                                    
*                                                                               
         ZICM  RE,RPORDTOT,4                                                    
         ZICM  RF,BUYD.RBUYTCOS,4                                               
         AR    RE,RF                                                            
         STCM  RE,15,RPORDTOT                                                   
         B     BLDSEG04                                                         
*                                                                               
BLDSEG03 DS    0H                                                               
         ZICM  RE,AGSPTTOT,2                                                    
         ZICM  RF,BUYD.RBUYTSPT,2                                               
         AR    RE,RF                                                            
         STCM  RE,3,AGSPTTOT                                                    
*                                                                               
         ZICM  RE,AGORDTOT,4                                                    
         ZICM  RF,BUYD.RBUYTCOS,4                                               
         AR    RE,RF                                                            
         STCM  RE,15,AGORDTOT                                                   
*                                                                               
BLDSEG04 DS    0H                                                               
         L     R6,AIO                                                           
         MVI   ELCODE,X'03'                                                     
         BRAS  RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING RBUYDTEL,R6                                                      
*                                                                               
         XC    TSARREC,TSARREC                                                  
         XC    TSARREC2,TSARREC2                                                
SEG      USING TLSTD,TSARREC2                                                   
         MVC   SEG.TLLEN,=Y(TLBYLQ)                                             
         MVI   SEG.TLKTYP,X'0B'    TYPE BUY SEGMENT                             
*                                                                               
* FIND DAY(S) ATTRIBUTE RECORD INDEX                                            
*                                                                               
         GOTOX (X'01',=A(BLDDTP)),RR=RELO                                       
         GOTO1 GOTSAR,TSARDH                                                    
         BE    *+6                                                              
         DC    H'0'                MUST BE FOUND                                
*                                                                               
         MVC   SEG.TLBYDAY,TB.TSRNUM RECORD NUMBER IS THE INDEX                 
*                                                                               
* FIND TIME(S) ATTRIBUTE RECORD INDEX                                           
*                                                                               
         XC    TSARREC,TSARREC                                                  
         GOTOX (X'02',=A(BLDDTP)),RR=RELO                                       
         GOTO1 GOTSAR,TSARDH                                                    
         BE    *+6                                                              
         DC    H'0'                MUST BE FOUND                                
*                                                                               
         MVC   SEG.TLBYTIME,TB.TSRNUM  RECORD NUMBER IS THE INDEX               
*                                                                               
* FIND PROGRAM NAME ATTRIBUTE RECORD INDEX                                      
*                                                                               
         XC    TSARREC,TSARREC                                                  
         GOTOX (X'03',=A(BLDDTP)),RR=RELO                                       
         GOTO1 GOTSAR,TSARDH                                                    
         BNE   *+10                PROGRAM NAME IS OPTIONAL FOR NOW             
         MVC   SEG.TLBYPROG,TB.TSRNUM  RECORD NUMBER IS THE INDEX               
*                                                                               
         MVC   SEG.TLBYLEN,BUYD.RBUYDUR                                         
         MVC   SEG.TLBYRATE,BUYD.RBUYCOS                                        
*                                                                               
BLDSEG05 DS    0H                                                               
         GOTO1 DATCON,DMCB,(3,RBUYDTST),(0,WORK)                                
*                                                                               
BLDSEG10 DS    0H                                                               
         GOTO1 DATCON,DMCB,(0,WORK),(2,SEG.TLBYWKOF)                            
*                                                                               
* CLEAR FLAGS AND POINTER TO LIST OF BUY LINKS                                  
*                                                                               
         MVI   SEG.TLBYRFLG,0                                                   
         MVI   SEG.TLBYAFLG,0                                                   
         XC    SEG.TLBYRLNK,SEG.TLBYRLNK                                        
         XC    SEG.TLBYALNK,SEG.TLBYALNK                                        
*                                                                               
         CLC   =X'0B01',BUYD.RBUYKEY                                            
         BE    BLDSEG20                                                         
*                                                                               
* RECORD IS A REPPAK BUY                                                        
*                                                                               
         TM    BUYD.RBUYDUR,X'80'       MINUTES?                                
         BZ    *+8                                                              
         OI    SEG.TLBYRFLG,X'80'  SET LENGTH IN MINUTES                        
         B     BLDSEG30                                                         
*                                                                               
* RECORD IS AN AGENCY CONVERTED BUY                                             
*                                                                               
BLDSEG20 DS    0H                                                               
         TM    BUYD.RBUYDUR,X'80'       MINUTES?                                
         BZ    *+8                                                              
         OI    SEG.TLBYAFLG,X'80'  SET LENGTH IN MINUTES                        
*                                                                               
BLDSEG30 DS    0H                                                               
         MVC   TSARREC,TSARREC2                                                 
         GOTO1 GOTSAR,TSAADD                                                    
         TM    TB.TSERRS,TSEDUP                                                 
         BO    BLDSEG50                                                         
*                                                                               
* NEW BUY SEGMENT RECORD, NEED TO SPECIFY BUY NUMBER WHERE THIS SEGMENT         
* CAME FROM                                                                     
*                                                                               
         CLC   =X'0B01',BUYD.RBUYKEY                                            
         BE    BLDSEG40                                                         
         MVC   SEG.TLBYRLNK,BUYD.RBUYKLIN                                       
         MVC   SEG.TLBYRLNK+1(1),RBUYDTNW                                       
         B     BLDSEG45                                                         
*                                                                               
BLDSEG40 DS    0H                                                               
         MVC   SEG.TLBYALNK,BUYD.RBUYAGBL                                       
         MVC   SEG.TLBYALNK+1(1),RBUYDTNW                                       
*                                                                               
BLDSEG45 DS    0H                                                               
         MVC   TSARREC,TSARREC2                                                 
         GOTO1 GOTSAR,TSAWRT                                                    
         TM    TB.TSERRS,TSERNF                                                 
         BZ    BLDSEG80                                                         
         DC    H'0'                                                             
*                                                                               
BLDSEG50 DS    0H                                                               
         GOTO1 GOTSAR,TSAGET       RECORD EXISTS, GET IT                        
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
* BUY SEGMENT RECORD EXISTS. SPECIFY REP OR AGENCY BUY#/#SPT IF NOT             
* ALREADY. OTHERWISE, WE WILL NEED TO ADD OR CREATE BUY LINK RECORD             
* SINCE TWO OR MORE BUYS REFER TO THIS SEGMENT                                  
*                                                                               
         MVC   TSARREC2,TSARREC                                                 
         CLC   =X'0B01',BUYD.RBUYKEY  REP OR AGENCY SEGMENT?                    
         BE    BLDSEG60                                                         
         OC    SEG.TLBYRLNK,SEG.TLBYRLNK                                        
         BNZ   BLDSEG70                                                         
         MVC   SEG.TLBYRLNK,BUYD.RBUYKLIN                                       
         MVC   SEG.TLBYRLNK+1(1),RBUYDTNW                                       
         B     BLDSEG65                                                         
*                                                                               
BLDSEG60 DS    0H                                                               
         OC    SEG.TLBYALNK,SEG.TLBYALNK                                        
         BNZ   BLDSEG70                                                         
         MVC   SEG.TLBYALNK,BUYD.RBUYAGBL                                       
         MVC   SEG.TLBYALNK+1(1),RBUYDTNW                                       
*                                                                               
BLDSEG65 DS    0H                                                               
         MVC   TSARREC,TSARREC2                                                 
         GOTO1 GOTSAR,TSAWRT                                                    
         TM    TB.TSERRS,TSERNF                                                 
         BZ    BLDSEG80                                                         
         DC    H'0'                                                             
*                                                                               
BLDSEG70 DS    0H                                                               
         GOTO1 =A(BLDBLINK),RR=RELO                                             
*                                                                               
* BUMP TO NEXT WEEK                                                             
*                                                                               
BLDSEG80 DS    0H                                                               
         LA    RF,7                                                             
         TM    RBUYDTIN,X'40'      ALTERNATING WEEKS?                           
         BZ    *+8                                                              
         LA    RF,14                                                            
         GOTO1 ADDAY,DMCB,WORK,WORK,(RF)                                        
*                                                                               
         GOTO1 DATCON,DMCB,(3,RBUYDTED),(0,WORK+6)                              
         CLC   WORK(6),WORK+6      HAVE WE PASSED THE END DATE?                 
         BNH   BLDSEG10                                                         
*                                                                               
         BRAS  RE,NEXTEL                                                        
         BE    BLDSEG05                                                         
         DROP  R6                                                               
*                                                                               
BLDSEGX  DS    0H                                                               
*                                                                               
         B     EXIT                                                             
         DROP  SEG,BUYD                                                         
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* CREATE NEW BUY LINK RECORD IF IT DOES NOT EXIST, ELSE APPEND EXISTING         
* BUY LINK RECORD WITH CURRENT BUY NUMBER AND NUMBER OF SPOTS                   
*                                                                               
* R6 IS POINTING TO CURRENT BUY EFFECTIVE ELEMENT                               
*                                                                               
***********************************************************************         
BLDBLINK NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         MVC   TSARREC2,TSARREC                                                 
*                                                                               
         L     R4,AIO                                                           
BUYD     USING RBUYREC,R4                                                       
         USING RBUYDTEL,R6                                                      
*                                                                               
         MVC   SVTSAR#,TB.TSRNUM   SAVE OFF BUY SEGMENT REC NUMBER              
*                                                                               
TKEY     USING TLKEY,TSARKEY                                                    
         XC    TSARKEY,TSARKEY                                                  
*                                                                               
         CLC   =X'0B00',BUYD.RBUYKEY                                            
         BE    BLDBL05                                                          
*                                                                               
         MVI   TKEY.TLKTYP,C'A'    AGENCY OR REP BUY?                           
         TM    TR.TLBYAFLG,X'40'   LINK OR ACTUAL BUY #?                        
         BZ    BLDBL50                                                          
         MVC   TKEY.TLBLINDX,TR.TLBYALNK                                        
         B     BLDBL10                                                          
*                                                                               
BLDBL05  DS    0H                                                               
         MVI   TKEY.TLKTYP,C'R'                                                 
         TM    TR.TLBYRFLG,X'40'   LINK OR ACTUAL BUY #?                        
         BZ    BLDBL50                                                          
         MVC   TKEY.TLBLINDX,TR.TLBYRLNK                                        
*                                                                               
BLDBL10  DS    0H                                                               
         OC    TKEY.TLBLINDX,TKEY.TLBLINDX                                      
         BZ    BLDBL50             NEED TO CREATE NEW BUY LINK RECORD           
*                                                                               
         XC    TSARREC,TSARREC                                                  
         MVC   TSARREC+2(L'TLKEY),TSARKEY                                       
         GOTO1 GOTSAR,TSARDH                                                    
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
* INDEX POINTER MUST BE POINTING TO AN EXISTING BUY LINK RECORD                 
*                                                                               
         CLC   TR.TLKEY(TLBLTSPT-TLKEY),TKEY.TLKEY                              
         BE    *+6                                                              
         DC    H'0'                MUST BE IN TSAR BUFFER                       
*                                                                               
* BUY LINK RECORD FOUND, ADD THIS BUY TO THE LIST OF BUYS AND SPOTS             
*                                                                               
         LA    R3,TR.TLBLBUY#                                                   
*                                                                               
BLDBL20  DS    0H                                                               
         CLI   0(R3),0                                                          
         BE    BLDBL100                                                         
         AHI   R3,L'TLBLBUY#+L'TLBL#SPT                                         
*                                                                               
         LA    RE,TSARREC+L'TSARREC                                             
         CR    R3,RE               BOUNDARY CHECK                               
         BL    BLDBL20                                                          
         DC    H'0'                                                             
*                                                                               
* NEED TO CREATE NEW BUY LINK RECORD FOR THIS BUY SEGMENT                       
*                                                                               
BLDBL50  DS    0H                  FIND HIGHEST INDEX NUMBER                    
         XC    TBLINDEX,TBLINDEX                                                
         XC    TSARREC,TSARREC                                                  
         MVC   TSARREC+2(L'TLKEY),TSARKEY                                       
         GOTO1 GOTSAR,TSARDH                                                    
*                                                                               
BLDBL55  DS    0H                                                               
         CLC   TR.TLKTYP,TKEY.TLKTYP                                            
         BNE   BLDBL60                                                          
         MVC   TBLINDEX,TR.TLBLINDX                                             
         XC    TSARREC,TSARREC                                                  
         GOTO1 GOTSAR,TSANXT                                                    
         TM    TB.TSERRS,TSEEOF                                                 
         BZ    BLDBL55                                                          
*                                                                               
BLDBL60  DS    0H                                                               
         SR    R1,R1                                                            
         ICM   R1,3,TBLINDEX                                                    
         AHI   R1,1                                                             
         STCM  R1,3,TBLINDEX                                                    
*                                                                               
TR2      USING TLSTD,TSARREC2                                                   
*                                                                               
         XC    TSARREC,TSARREC                                                  
         MVC   TR.TLKTYP,TKEY.TLKTYP                                            
         MVC   TR.TLBLINDX,TBLINDEX                                             
*                                                                               
* SAVE OFF PREVIOUSLY SAVED LINE NUMBER AND NUMBER OF SPOTS                     
*                                                                               
         CLI   TR.TLKTYP,C'A'                                                   
         BE    BLDBL70                                                          
         MVC   TR.TLBLBUY#(2),TR2.TLBYRLNK                                      
         B     BLDBL80                                                          
*                                                                               
BLDBL70  DS    0H                                                               
         MVC   TR.TLBLBUY#(2),TR2.TLBYALNK                                      
*                                                                               
BLDBL80  DS    0H                                                               
         MVC   TR.TLBLTSPT,TR.TLBL#SPT                                          
*                                                                               
* APPEND CURRENT LINE NUMBER AND NUMBER OF SPOTS                                
*                                                                               
         LA    R3,TR.TLBLLIST                                                   
*                                                                               
BLDBL100 DS    0H                                                               
BL       USING TLBLBUY#,R3         REP OR AGENCY BUY NUMBER                     
         MVC   BL.TLBLBUY#,BUYD.RBUYKLIN                                        
         CLI   TR.TLKTYP,C'R'                                                   
         BE    *+10                                                             
         MVC   BL.TLBLBUY#,BUYD.RBUYAGBL                                        
*                                  NUMBER OF SPOTS COVERED                      
         MVC   BL.TLBL#SPT,RBUYDTNW                                             
*                                                                               
         ZIC   RF,BL.TLBL#SPT      CALCULATE TOTAL SPOTS FOR THIS WEEK          
         ZIC   RE,TR.TLBLTSPT                                                   
         AR    RE,RF                                                            
         STC   RE,TR.TLBLTSPT                                                   
         DROP  BL                                                               
*                                                                               
* CALCULATE NEW VARIABLE LENGTH AND ADD/PUT RECORD TO TSAR                      
*                                                                               
         AHI   R3,L'TLBLBUY#+L'TLBL#SPT                                         
*                                                                               
         LA    RE,TR.TLREC                                                      
         SR    R3,RE                                                            
         STCM  R3,3,TR.TLLEN                                                    
*                                                                               
* ADD OR PUT NEW BUY LINK RECORD                                                
*                                                                               
         CLI   TR.TLBLLIST+L'TLBLBUY#+L'TLBL#SPT,0                              
         BE    BLDBL105                                                         
         GOTO1 GOTSAR,TSAWRT                                                    
         TM    TB.TSERRS,TSERNF                                                 
         BZ    BLDBLX                                                           
         DC    H'0'                                                             
*                                                                               
BLDBL105 DS    0H                                                               
         GOTO1 GOTSAR,TSAADD                                                    
         TM    TB.TSERRS,TSEDUP+TSEEOF                                          
         BZ    *+6                                                              
         DC    H'0'                                                             
*                                                                               
*        GOTO1 =A(DUMPTSAR),RR=RELO                                             
*                                                                               
*                                                                               
* UPDATE BUY SEGMENT RECORD WITH INDEX OF NEW BUY LINK RECORD                   
*                                                                               
         MVC   TXNUM,SVTSAR#                                                    
         GOTO1 GOTSAR,TSAGET                                                    
         TM    TB.TSERRS,TSERNF                                                 
         BZ    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         CLC   =X'0B01',BUYD.RBUYKEY                                            
         BE    BLDBL110                                                         
         MVC   TR.TLBYRLNK,TBLINDEX                                             
         OI    TR.TLBYRFLG,X'40'   FIELD IS A LINK POINTER                      
         B     BLDBL120                                                         
*                                                                               
BLDBL110 DS    0H                                                               
         MVC   TR.TLBYALNK,TBLINDEX                                             
         OI    TR.TLBYAFLG,X'40'   FIELD IS A LINK POINTER                      
*                                                                               
BLDBL120 DS    0H                                                               
         GOTO1 GOTSAR,TSAPUT                                                    
         TM    TB.TSERRS,TSERNF                                                 
         BZ    *+6                                                              
         DC    H'0'                                                             
*                                                                               
BLDBLX   DS    0H                                                               
*                                                                               
*        GOTO1 =A(DUMPTSAR),RR=RELO                                             
*                                                                               
         B     EXIT                                                             
         DROP  R6,BUYD                                                          
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
*****>>                                                                         
***********************************************************************         
* GET TOTALS REP AND AGY SPOTS FOR THIS BUY SEGMENT                             
***********************************************************************         
GETTSPTS NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         MVC   GTSSVREC,TSARREC    SAVE OFF CURRENT TSAR RECORD                 
         MVC   GTSSVTR#,TXNUM       AND TSAR RECORD NUMBER                      
*                                                                               
BUYSEG   USING TLSTD,GTSSVREC                                                   
*                                                                               
         MVC   TOTREPSP,BUYSEG.TLBYRLNK+1                                       
         MVC   TOTAGYSP,BUYSEG.TLBYALNK+1                                       
         MVC   REPSPWK,BUYSEG.TLBYRLNK+1                                        
         MVC   AGYSPWK,BUYSEG.TLBYALNK+1                                        
*                                                                               
         TM    BUYSEG.TLBYRFLG,X'40' REP BUY LINK?                              
         BZ    GETSP10                                                          
         XC    TSARREC,TSARREC                                                  
         MVI   TR.TLKTYP,C'R'                                                   
         MVC   TR.TLBLINDX,BUYSEG.TLBYRLNK                                      
         GOTO1 GOTSAR,TSARDH       RETRIEVE BUY LINK RECORD                     
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         MVC   TOTREPSP,TR.TLBLTSPT                                             
         MVC   REPSPWK,TR.TLBL#SPT                                              
*                                                                               
GETSP10  DS    0H                                                               
         TM    BUYSEG.TLBYAFLG,X'40' AGY BUY LINK?                              
         BZ    GETSPX                                                           
*                                                                               
* NOW SURE HOW THIS IS POSSIBLE TO HAVE BUY LINK FLAGGED BUT NO                 
* ACTUALLY LINK INFO, EXIT FOR NOW                                              
*                                                                               
         OC    BUYSEG.TLBYALNK,BUYSEG.TLBYALNK                                  
         BZ    GETSPX                                                           
*                                                                               
         XC    TSARREC,TSARREC                                                  
         MVI   TR.TLKTYP,C'A'                                                   
         MVC   TR.TLBLINDX,BUYSEG.TLBYALNK                                      
         GOTO1 GOTSAR,TSARDH       RETRIEVE BUY LINK RECORD                     
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         MVC   TOTAGYSP,TR.TLBLTSPT                                             
         MVC   AGYSPWK,TR.TLBL#SPT                                              
*                                                                               
GETSPX   DS    0H                                                               
         MVC   TXNUM,GTSSVTR#      RESTORE SEQUENCE                             
         GOTO1 GOTSAR,TSAGET                                                    
         BE    *+6                                                              
         DC    H'0'                                                             
         B     EXIT                                                             
         DROP  BUYSEG                                                           
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* CALCULATE GRAND TOTAL DOLLARS AND SPOTS. TAKE INTO ACCOUNT POSSIBLE           
* TAKEOVER ORDER                                                                
***********************************************************************         
DISTOTAL NTR1  BASE=*,LABEL=*                                                   
         XC    GTOTAL$,GTOTAL$                                                  
         XC    GSPT#,GSPT#                                                      
         OI    FLAGS,FGTOTALQ      SKIP BUY DETAIL BREAK OUT PROCESSING         
*                                                                               
         MVC   AIO,AIO1                                                         
         XC    KEY,KEY                                                          
         MVC   KEY(RDARKRT-RDARKEY),SELECTKY                                    
*                                                                               
         MVI   KEY+RDARKRT-RDARKEY,X'40' BUY RECORD                             
         MVI   RDUPDATE,C'N'                                                    
         GOTO1 HIGH                                                             
*                                                                               
DIST10   DS    0H                                                               
         CLC   KEY(RDARKSEQ-RDARKEY),KEYSAVE                                    
         BNE   DIST70                                                           
*                                                                               
         MVI   RDUPDATE,C'N'                                                    
         GOTO1 GETREC                                                           
*                                                                               
         CLI   KEY+RDARKSRT-RDARKEY,X'00' BUY HEADER                            
         BE    DIST20                                                           
         CLI   KEY+RDARKSRT-RDARKEY,X'10' BUY ORBITS                            
         BE    DIST30                                                           
         CLI   KEY+RDARKSRT-RDARKEY,X'30' BUY DETAIL                            
         BE    DIST40                                                           
         B     DIST60                                                           
*                                                                               
* PROCESS HEADER RECORD                                                         
*                                                                               
DIST20   DS    0H                                                               
         L     R6,AIO                                                           
         USING RDARBYEL,R6                                                      
         MVI   ELCODE,X'01'                                                     
         BRAS  RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         TM    RDARBYDL,X'80'+X'40' HARD/SOFT DELETED??                         
         BNZ   DIST60              SKIP                                         
         MVC   BUYCOST,RDARBYCO                                                 
         MVI   STARTDAY,0          CLEAR START/END DAYS OF WEEK                 
         MVI   ENDDAY,0                                                         
         GOTO1 =A(STARTEND),DMCB,RDARBYRO,WORK,RR=RELO                          
         B     DIST60                                                           
         DROP  R6                                                               
*                                                                               
DIST30   DS    0H                                                               
         L     R6,AIO                                                           
         USING RDAROBEL,R6                                                      
         MVI   ELCODE,X'01'                                                     
         BRAS  RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         TM    RDAROBDL,X'80'+X'40' HARD/SOFT DELETED??                         
         BNZ   DIST60              SKIP                                         
         DROP  R6                                                               
*                                                                               
         GOTO1 =A(BUYORBIT),DMCB,AIO,RR=RELO                                    
         B     DIST60              READ NEXT                                    
*                                                                               
DIST40   DS    0H                                                               
         L     R6,AIO                                                           
         USING RDARBDEL,R6                                                      
         MVI   ELCODE,X'01'                                                     
         BRAS  RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         TM    RDARBDDL,X'80'+X'40' HARD/SOFT DELETED??                         
         BNZ   DIST60              SKIP                                         
         DROP  R6                                                               
*                                                                               
         GOTO1 =A(BUYDETL),DMCB,AIO,RR=RELO                                     
         BNE   DIST60              READ NEXT                                    
*                                                                               
         L     R6,AIO                                                           
         USING RDARBUEL,R6                                                      
         MVI   ELCODE,X'02'                                                     
         BRAS  RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
DIST50   DS    0H                                                               
         ZIC   R1,RDARBUWK                                                      
         XC    HALF,HALF                                                        
         MVC   HALF+1(1),RDARBUSW                                               
         MH    R1,HALF                                                          
         ZICM  R0,GSPT#,4                                                       
         AR    R0,R1                                                            
         STCM  R0,15,GSPT#                                                      
         MVC   FULL,RDARBU$$                                                    
         M     R0,FULL                                                          
         ZICM  R0,GTOTAL$,4                                                     
         AR    R0,R1                                                            
         STCM  R0,15,GTOTAL$                                                    
*                                                                               
         BRAS  RE,NEXTEL                                                        
         BE    DIST50                                                           
         DROP  R6                                                               
*                                                                               
DIST60   DS    0H                                                               
         MVI   RDUPDATE,C'N'                                                    
         GOTO1 SEQ                                                              
         B     DIST10                                                           
*                                                                               
DIST70   DS    0H                  DISPLAY GRAND TOTAL DOLLAR/SPOT              
*                                                                               
* DISPLAY (AGENCY - CONTRACT) TOTALS                                            
*                                                                               
         CLC   GTOTAL$,RPORDTOT                                                 
         BNE   DISTNO              NOT EQUAL, ORDER IS DIFFERENT                
*                                                                               
DIST100  DS    0H                                                               
         ICM   R3,15,GSPT#                                                      
         ZICM  R4,RPSPTTOT,2                                                    
         SR    R3,R4                                                            
         BNZ   DISTNO                                                           
         NI    FLAGS,X'FF'-FGTOTALQ                                             
*                                                                               
DISTYES  SR    RC,RC                                                            
DISTNO   LTR   RC,RC                                                            
DISTX    DS    0H                                                               
         B     EXIT                                                             
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
         EJECT                                                                  
       ++INCLUDE DDCOMFACS                                                      
       ++INCLUDE DDCOREQUS                                                      
       ++INCLUDE DDTSARD                                                        
       ++INCLUDE REPFACSQ                                                       
         EJECT                                                                  
       ++INCLUDE REDARDSECT                                                     
         EJECT                                                                  
RREPRECD  DSECT                                                                 
       ++INCLUDE REGENREPA                                                      
RSTARECD DSECT                                                                  
       ++INCLUDE REGENSTA                                                       
         EJECT                                                                  
*                                                                               
* APPLICATION STORAGE AREA                                                      
*                                                                               
MYAREAD  DSECT                                                                  
ACOMFACS DS    V                   A(COMFACS)                                   
VREDARTK DS    V                                                                
GETBROAD DS    V                                                                
VDATAMGR DS    V                   A(DATAMGR)                                   
HELLO    DS    V                                                                
DATCON   DS    V                                                                
ADDAY    DS    V                                                                
GETDAY   DS    V                                                                
PERVERT  DS    V                                                                
CALLOV   DS    V                                                                
REPFACS  DS    V                                                                
*                                                                               
COMMAND  DS    CL6                                                              
KEY      DS    CL32                                                             
KEYSAVE  DS    CL32                                                             
SELECTKY DS    CL27                                                             
DMCB     DS    6F                                                               
DMINBTS  DS    X                                                                
DMBYTE   DS    CL1                                                              
DMOUTBTS DS    X                                                                
*                                                                               
WORK     DS    CL64                                                             
DMWORK   DS    12D                                                              
RDUPDATE DS    X                                                                
ELCODE   DS    X                                                                
AIO      DS    A                                                                
AIO1     DS    A                                                                
AIO2     DS    A                                                                
AIO3     DS    A                                                                
AIOAREA  DS    A                                                                
RELO     DS    A                                                                
*                                                                               
TBLINDEX DS    XL2                 BUY LINK INDEX                               
SVTSAR#  DS    XL2                 SAVED TSAR RECORD NUMBER                     
TLACTN   DS    XL1                 REQUESTED ACTION                             
TLST     DS    XL(L'TLREC+2)       TSAR2 RECORD BUFFER                          
         ORG   TLST                                                             
TXNUM    DS    XL2                                                              
TXREC    DS    0X                                                               
TXLEN    DS    XL2                                                              
TXKEY    DS    0X                                                               
         ORG                                                                    
TBLOCK   DS    XL(TSARDL)          TSAR BUFFER                                  
*                                                                               
TSARREC  DS    XL(L'TLREC)                                                      
TSARREC2 DS    XL(L'TLREC)                                                      
TSARKEY  DS    XL(L'TLKEY)                                                      
*                                                                               
GTSSVREC DS    XL(L'TLREC)         SAVED TSARREC AREA                           
GTSSVTR# DS    XL2                 SAVED TSAR RECORD NUMBER                     
DYTXNUM  DS    XL2                 SAVED TSAR RECORD NUMBER FOR DAY             
TMTXNUM  DS    XL2                 SAVED TSAR RECORD NUMBER FOR TIME            
*                                                                               
GNXSVREC DS    XL(L'TLREC)         SAVED TSARREC AREA                           
GNXSVDRC DS    XL(L'TLREC)         SAVED DAYS TSARREC AREA                      
*                                                                               
DLSVREC  DS    XL(L'TLREC)         SAVED TSARREC AREA FOR LIST DISPLAY          
         ORG   DLSVREC             RECYCLE                                      
DLSVBLRC DS    XL(L'TLREC)         SAVED PREVIOUS BUY LINK AREA                 
DLSVTR#  DS    XL2                 SAVED TSAR RECORD NUMBER                     
PRSVTR#  DS    XL2                 SAVED TSAR# FOR PRINT ROUTINE                
PVSVTR#  DS    XL2                 SAVED 'PREVIOUS' TSAR RECORD NUMBER          
CRSVTR#  DS    XL2                 SAVED 'CURRENT' TSAR RECORD NUMBER           
UPSVTR#  DS    XL2                 SAVED TSAR# FOR UPDTBUYS ROUTINE             
*                                                                               
*                                                                               
* STUFF MOVE FROM SYSD                                                          
*                                                                               
VTSAR    DS    A                                                                
ATSARBUF DS    A                   ADDRESS OF TSAR BUFFER TO USE                
*                                                                               
FLAGS    DS    X                                                                
FGINITQ EQU    X'80'                                                            
* X'80' = BUY PREPROCESSING AND TSAR INITIALIZATION PERFORMED                   
FGBSEGQ  EQU   X'40'                                                            
* X'40' = BUILD BUY SEGMENT                                                     
FGDETLQ  EQU   X'20'                                                            
* X'20' = DIFFERENCE DETAIL LIST IN PROCESS (DEFAULT = SUMMARY)                 
FGTBSVDQ EQU   X'10'                                                            
* X'10' = TSAR BUFFER HAS BEEN SAVED                                            
FGTOTALQ EQU   X'08'                                                            
* X'08' = TOTAL CALCULATION, SKIP BUY DETAIL PROCESSING                         
FGADDSPQ EQU   X'04'                                                            
* X'04' = ADD SPOTS TO REP BUYS, ELSE REMOVE SPOTS IF OFF                       
FGNEWBYQ EQU   X'02'                                                            
* X'02' = ADDING NEW REP BUY LINE                                               
FGEXPLDQ EQU   X'01'                                                            
* X'01' = EXPLODED VIEW REQUESTED                                               
*                                                                               
* KEEP TRACK OF TSAR RECORD NUMBERS FOR BUY SEGMENTS IN                         
* DIFFERENCES LIST DISPLAY                                                      
*                                                                               
NXOFFSET DS    F                   OFFSET TO NEXT DISPLAY LINE                  
*                                  SET WHEN IN EXPLODED MODE                    
*                                                                               
RPORDTOT DS    XL4                 REP CONTRACT ORDER TOTAL                     
AGORDTOT DS    XL4                 AGENCY ORDER TOTAL                           
RPSPTTOT DS    XL2                 REP CONTRACT SPOT TOTAL                      
AGSPTTOT DS    XL2                 AGENCY SPOT TOTAL                            
*                                                                               
TOTREPSP DS    X                   TOTAL SPTS FROM REP BUYS FOR THIS WK         
TOTAGYSP DS    X                   TOTAL SPTS FROM AGY BUYS FOR THIS WK         
REPSPWK  DS    X                   SPTS FROM REP BUY FOR THIS WK                
AGYSPWK  DS    X                   SPTS FROM AGY BUY FOR THIS WK                
SEGSTRDT DS    XL2                 BUY SEGMENT START DATE                       
*                                                                               
COMPCON  DS    CL4                 CONTRACT # COMP/REVERSED                     
ELTBUILD DS    CL96                ELEMENT BUILD AREA                           
ELTBILD2 DS    CL64                SECOND BUILD AREA                            
*        ORG   ELTBUILD                                                         
ELEMENT  DS    0C                                                               
ELEM     DS    XL256                                                            
PROGNAME DS    CL34                SAVE BUY PROGRAM NAME                        
DETLFLAG DS    XL2                 TOTAL SPOT LENGTH                            
BUYUPDAT DS    CL1                 BUY WRITE FLAG                               
SKIPRECS DS    CL1                 GENERAL RECORD SKIP FLAG                     
TOTSPTUN DS    XL1                 TOTAL SPOT UNITS                             
ACTCODE  DS    CL1                 ACTION CODE:                                 
*                                     A  =  APPROVE                             
MISCFLAG DS    XL1                 MESSAGE FLAGS                                
*                                     X'80'  =  DAILY ORDER                     
*                                     X'40'  =  ORBIT DROPPED                   
MFMANUAL EQU   X'20'                  X'20'  =  MANUAL CHANGES STARTED          
MFEXACT  EQU   X'10'                  X'10'  =  EXACT #SPOTS MATCH              
MFCREDIT EQU   X'08'                  X'08'  =  K BUY HAS CREDIT BUYS           
MFMGLOOP EQU   X'04'                  X'04'  =  MATCH MAKEGOOD ONLY             
MFCONOK  EQU   X'01'                  X'01'  =  CONTRACT UPDATED                
*                                                                               
MISCFLG2 DS    XL1                 MORE MESSAGE FLAGS                           
MF2PTCAN EQU   X'80'                  X'80'  =  PARTIAL CANCEL                  
MF20SBUY EQU   X'40'                  X'40'  =  AGENCY SENT 0 SPT BUY           
MF2NOXIT EQU   X'20'                  X'20'  =  DON'T ERROR EXIT IN             
*                                               MANUAL MATCHING                 
MF2TRADE EQU   X'10'                  X'10'  =  TRADE ORDER                     
MF2DTOT  EQU   X'08'                  X'08'  =  REDISPLAY TOTALS                
APPRBUY# DS    X                   LAST BUY DISPLAYED IN APPROVAL SCRN          
AGBUYMTR DS    X                   AGENCY MAKEGOOD BUY MASTER LINE              
ORBFLAG  DS    CL1                 ORBIT PRESENCE INDICATOR                     
*                                     Y  =  ORBIT PRESENT                       
VERDFLT  DS    CL1                 VERSION NUMBER (REP)                         
CONMOD#  DS    CL1                 SAVE AREA FOR MOD NUMBER                     
BUYLINE# DS    X                   BUYLINE NUMBER BEING GENERATED               
SAVEKEY  DS    CL27                KEY SAVE AREA FOR RESTARTS                   
FLTDATES DS    CL4                 AGENCY ORDER FLIGHT DATES                    
STARTDAY DS    XL1                 START DAY FOR BUY                            
ENDDAY   DS    XL1                 END DAY FOR BUY                              
ORBSTDAY DS    XL1                 ORBIT START DAY                              
ROTATDAY DS    XL1                 ROTATION START DAY                           
*                                                                               
         DS    0F                                                               
BUYCOST  DS    F                   COST OF SPOTS IN BUY                         
SPOTCTR  DS    F                   NUMBER SPOTS IN BUY                          
WEEKCTR  DS    F                   NUMBER WEEKS IN BUY                          
ORDTOT$$ DS    F                   ORDER TOTAL DOLLARS                          
ORDTOTSP DS    F                   ORDER TOTAL SPOTS                            
KATZEDI  DS    C                   Y/N KATZ EDI ORDER?                          
TKODATE  DS    XL3                 TAKEOVER DATE                                
RERROR   DS    XL2                                                              
         ORG   *-1                                                              
GERROR1  DS    X                                                                
GTOTAL$  DS    XL4                 AGENCY ORDER GRAND DOLLAR TOTAL              
GSPT#    DS    XL4                 AGENCY ORDER GRAND SPOT TOTAL                
SVELCODE DS    X                                                                
RTKODATE DS    XL3                                                              
CCONNUM  DS    XL4                                                              
AGENCY   DS    CL2                                                              
DATADISP DS    H                                                                
HALF     DS    H                                                                
FULL     DS    F                                                                
DUB      DS    D                                                                
MYFLAG   DS    X                   CALLING OPTIONS                              
SRCALL   EQU   X'80'               SERVICE REQUEST CALL                         
IOAREA   DS    CL4000                                                           
IOAREA2  DS    CL4000                                                           
IOAREA3  DS    CL4000                                                           
IOAREA4  DS    CL4000                                                           
*                                                                               
WORKLQ   EQU   *-MYAREAD                                                        
*                                                                               
* ONLINE LIST LINE                                                              
*                                                                               
*                                                                               
* IF ANY OF THE BELOW FIELDS CHANGE, MAKE SURE TO CHANGE THE PARAMETERS         
* IN THE XSORT CALLS ABOVE                                                      
*                                                                               
SORTD    DSECT                                                                  
SRTAGYBY DS    X                   AGENCY BUY THAT K BUY IS MAPPED TO           
*                                  X'FF' MEANS K BUY ADDED MANUALLY             
SRTTKBUY DS    X                   TARGET K BUY (FOR MAKEGOOD/CREDIT)           
SRTCONBY DS    X                   K BUY                                        
SRTBUYDA DS    XL4                 CONTRACT BUY DISK ADDRESS                    
SRTFLAGS DS    X                   MISC. FLAGS                                  
SFMATCHD EQU   X'80'               K BUY WAS MATCHED                            
SFMATING EQU   X'40'               K BUY IS IN PROCESS OF MATCHING              
SFMKGOOD EQU   X'20'               K BUY IS A MAKEGOOD                          
SFCREDIT EQU   X'10'               K BUY IS A CREDIT                            
SFTMKGD  EQU   X'08'               K BUY IS TARGET MAKEGOOD                     
SFTCRDT  EQU   X'04'               K BUY IS TARGET CREDIT                       
SFADDED  EQU   X'02'               K BUY WAS ADDED AT THIS APPROVAL             
SFTKMKGD EQU   X'01'               K BUY IS A TAKEOVER MAKEGOOD                 
SRTFLG2  DS    X                                                                
SF2METH1 EQU   X'80'               AUTO METHOD 1                                
SF2METH2 EQU   X'40'               AUTO METHOD 2                                
SF2METH3 EQU   X'20'               AUTO METHOD 3                                
SF20SPTS EQU   X'10'               CONTRACT BUY HAS ZERO TOTAL SPOTS            
SF20CSTS EQU   X'08'               CONTRACT BUY HAS ZERO TOTAL DOLLARS          
SF2MGOF  EQU   X'04'               CONTRACT BUY HAS PENDING MKGD OFFERS         
SF2CAN   EQU   X'02'               CANCELLED BY 0 SPOT OVERRIDE                 
SORTDLQ  EQU   *-SORTD                                                          
*                                                                               
*----------------------------------------------------------------------         
TLSTD    DSECT                     TSAR RECORD DSECT                            
TLREC    DS    0XL255              *** TSAR RECORD ***                          
TLLEN    DS    XL2                 RECORD LENGTH                                
TLKEY    DS    0XL13               *** TSAR KEY ***                             
TLKTYP   DS    XL1                 TSAR RECORD TYPE                             
*                                    - X'01' DAYS                               
*                                    - X'02' TIMES                              
*                                    - X'03' PROGRAM NAME                       
*                                    - X'0B' BUY SEGMENT                        
*                                    - C'A'  AGENCY BUY LINKS                   
*                                    - C'R'  REP BUY LINKS                      
*                                                                               
TLDATA   DS    0X                                                               
         EJECT                                                                  
*---------------------------------------*                                       
* KEY FOR 'DAYS' RECORD                                                         
*---------------------------------------*                                       
         ORG   TLDATA                                                           
TLDYSTEN DS    XL1                 START-END DAY INDICATOR                      
TLDYDAYS DS    XL1                 DAY(S)                                       
*                                                                               
*---------------------------------------*                                       
* KEY FOR 'TIMES' RECORD                                                        
*---------------------------------------*                                       
         ORG   TLDATA                                                           
TLTMSTRT DS    XL2                 START TIME                                   
TLTMENDT DS    XL2                 END TIME                                     
*---------------------------------------*                                       
* KEY FOR 'PROGRAM NAME' RECORD                                                 
*---------------------------------------*                                       
         ORG   TLDATA                                                           
TLPGNAME DS    0C                  PROGRAM NAME                                 
*---------------------------------------*                                       
* KEY FOR 'BUY SEGMENT' RECORD                                                  
*---------------------------------------*                                       
         ORG   TLDATA                                                           
TLBYDAY  DS    XL2                 DAY INDEX                                    
TLBYTIME DS    XL2                 TIME INDEX                                   
TLBYLEN  DS    XL2                 LENGTH IN SECONDS                            
TLBYRATE DS    XL4                 RATE                                         
TLBYWKOF DS    XL2                 WEEK-OF                                      
TLBYPROG DS    XL2                 PROGRAM NAME INDEX                           
TLBYRFLG DS    X                   REP FLAGS                                    
*                                  X'80' = LENGTH IS IN MINUTES                 
*                                  X'40' = TLBYRLNK IS A LINK POINTER           
*                                          ELSE IT IS THE ACTUAL LINE#          
*                                  X'01' = SPECIAL FOR DISPLAY:                 
*                                          FLAG THIS RECORD IF THIS IS          
*                                          THE FIRST RECORD ON THE TOP          
*                                          OF THE SCREEN. THIS WILL             
*                                          ALLOW US TO SCROLL BACK TO           
*                                          THE CORRECT RECORD                   
TLBYAFLG DS    X                   AGENCY FLAGS                                 
*                                  X'80' = LENGTH IS IN MINUTES                 
*                                  X'40' = TLBYALNK IS A LINK POINTER           
*                                          ELSE IT IS THE ACTUAL LINE#          
*                                                                               
TLBYRLNK DS    XL2                 REP BUY LINK POINTER IF TLBYRFLG             
*                                  HAS X'40' ON. OTHERWISE THIS IS              
*                                  THE ACTUAL BUY LINE NUMBER                   
*                                  AND NUMBER OF SPOTS FOR THIS SEGMEMT         
TLBYALNK DS    XL2                 AGENCY BUY LINK POINTER IF TLBYAFLG          
*                                  HAS X'40' ON. OTHERWISE THIS IS              
*                                  THE ACTUAL BUY LINE NUMBER                   
*                                  AND NUMBER OF SPOTS FOR THIS SEGMEMT         
TLBYLQ   EQU   *-TLSTD                                                          
*---------------------------------------*                                       
* KEY FOR AGENCY/REP 'BUY LINK' RECORD                                          
*---------------------------------------*                                       
         ORG   TLDATA                                                           
TLBLINDX DS    XL2                 LINK INDEX POINTED FROM BUY SEGMENT          
*                                  FOR THIS WEEK                                
         DS    XL(L'TLKEY-L'TLKTYP-L'TLBLINDX)  KEY SPARE                       
*                                                                               
TLBLTSPT DS    X                   TOTAL NUMBER OF REP OR AGENY SPOTS           
TLBLBUY# DS    X                   LIST OF AGENCY OR REP BUY NUMBERS            
TLBL#SPT DS    X                   AND CORRESPONDING NUMBER OF SPOTS            
TLBLLIST DS    0X                                                               
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'084REREPDIFXX12/10/02'                                      
         END                                                                    
