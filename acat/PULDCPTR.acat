*          DATA SET PULDCPTR   AT LEVEL 026 AS OF 05/01/02                      
*CATALP PULDCPTR                                                                
         TITLE 'LDCPTR - PUB - CREATE DIRECTORY POINTERS'                       
*        PARA1 A(RECORD)                                                        
*        PARA2 A(AREA TO CREATE DIRECTORY POINTERS)                             
*              LAST POINTER FOLLOWED BY X'00'                                   
*                                                                               
         PRINT NOGEN                                                            
LDCPTR   CSECT                                                                  
         NMOD1 0,PULDCPTR                                                       
         SPACE 2                                                                
         LM    R2,R3,0(R1)         R2 = A(POINTER)                              
*                                  R3 = A(SPACE)                                
*                                  SET STXIT ADDRESSES                          
         OC    STX1,STX1                                                        
         BNZ   CP4                                                              
         ST    R2,STX1                                                          
         ST    R3,STX2                                                          
         LA    RF,SRCHBLK                                                       
         ST    RF,STX3                                                          
         LA    RF,SQLIST                                                        
         ST    RF,STX4                                                          
         GOTO1 =V(STXITER),DMCB,DMPLST                                          
*                                                                               
CP4      DS    0H                                                               
         USING PUBRECD,R2                                                       
         XC    0(31,R3),0(R3)      MUST BE SURE IT'S CLEAR                      
         MVC   0(25,R3),0(R2)      KEY                                          
         MVC   25(2,R3),27(R2)     CNTL                                         
         MVI   31(R3),0            END OF POINTER LIST                          
         CLI   PUBKCOD,X'81'       ONLY '81' RECS CAN CREATE                    
         BNE   EXIT                PASSIVES                                     
         LA    R4,PUBEDTS                                                       
         LA    R5,15                                                            
POINT4   DS    0H                                                               
         CLI   0(R4),C' '                                                       
         BNH   POINT6                                                           
         LA    R3,31(R3)                                                        
         XC    0(31,R3),0(R3)     MUST BE SURE IT'S CLEAR                       
         MVC   0(25,R3),0(R2)                                                   
         MVC   25(2,R3),27(R2)                                                  
         OI    25(R3),X'01'        SET PASSIVE IND                              
         MVC   6(1,R3),0(R4)                                                    
         MVI   31(R3),0                                                         
         LA    R4,1(R4)                                                         
         BCT   R5,POINT4                                                        
**NEW 5/25/89                                                                   
POINT6   OC    PUBPLSH,PUBPLSH      CHECK FOR PUBLISHER REP                     
         BZ    POINT10              DONE                                        
         LA    R3,31(R3)                                                        
         XC    0(31,R3),0(R3)       MUST BE SURE IT'S CLEAR                     
         MVI   0(R3),X'F0'                                                      
         MVI   1(R3),C'P'           FOR PUBLISHER                               
         MVC   2(2,R3),PUBKAGY                                                  
         MVC   4(1,R3),PUBKMED                                                  
         MVC   5(4,R3),PUBPLSH                                                  
*                                   BYTE LEFT FOR SUFFIX                        
         MVC   10(6,R3),PUBKPUB     PUB/ZONE/EDT                                
         MVC   25(2,R3),27(R2)                                                  
         OI    25(R3),X'01'        SET PASSIVE IND                              
         MVI   31(R3),0                                                         
*                               NOW DO PUBLISHER POINTERS FOR EDITIONS          
         LA    R4,PUBEDTS                                                       
         LA    R5,15                                                            
POINT8   DS    0H                                                               
         CLI   0(R4),C' '                                                       
         BNH   POINT10                                                          
         LA    R3,31(R3)                                                        
         XC    0(31,R3),0(R3)        MUST BE SURE IT'S CLEAR                    
         MVI   0(R3),X'F0'                                                      
         MVI   1(R3),C'P'                                                       
         MVC   2(2,R3),PUBKAGY                                                  
         MVC   4(1,R3),PUBKMED                                                  
         MVC   5(4,R3),PUBPLSH                                                  
*                                   BYTE LEFT FOR SUFFIX                        
         MVC   10(6,R3),PUBKPUB     PUB/ZONE/EDT                                
         MVC   15(1,R3),0(R4)       EDITION FROM PUBEDTS                        
         MVC   25(2,R3),27(R2)                                                  
         OI    25(R3),X'01'        SET PASSIVE IND                              
         MVI   31(R3),0                                                         
         LA    R4,1(R4)                                                         
         BCT   R5,POINT8                                                        
*                                                                               
POINT10  DS    0H                  CREATE ADV PASSIVE POINTERS                  
         LA    R4,PUBREC+33                                                     
         MVI   ELCODE,X'80'                                                     
POINT12  BAS   RE,NEXTEL                                                        
         BNE   POINT30                                                          
         USING PUBADVD,R4                                                       
         LA    R3,31(R3)                                                        
         XC    0(31,R3),0(R3)        MUST BE SURE IT'S CLEAR                    
         MVI   0(R3),X'FD'                                                      
         MVC   1(1,R3),PUBKMED                                                  
         MVC   2(3,R3),PUBADVCD     ADV CODE                                    
         MVC   5(2,R3),PUBAORCD     AOR CODE                                    
         MVC   7(2,R3),PUBKAGY                                                  
         MVC   9(6,R3),PUBADVPC     ADV PUB                                     
         MVC   15(6,R3),PUBKPUB                                                 
         MVC   25(2,R3),27(R2)                                                  
         OI    25(R3),X'01'        SET PASSIVE IND                              
         MVI   31(R3),0                                                         
*                                                                               
         LA    R3,31(R3)                                                        
         XC    0(31,R3),0(R3)        MUST BE SURE IT'S CLEAR                    
         MVI   0(R3),X'FE'                                                      
         MVC   1(1,R3),PUBKMED                                                  
         MVC   2(2,R3),PUBKAGY                                                  
         MVC   4(3,R3),PUBADVCD     ADV CODE                                    
         MVC   7(2,R3),PUBAORCD     AOR CODE                                    
         MVC   9(6,R3),PUBKPUB                                                  
         MVC   15(6,R3),PUBADVPC    ADV PUB                                     
         MVC   25(2,R3),27(R2)                                                  
         OI    25(R3),X'01'        SET PASSIVE IND                              
         MVI   31(R3),0                                                         
*                                                                               
*                           NOW ADD ONE FOR EACH EDITION IN PUBEDTS             
         LA    R6,PUBEDTS                                                       
         LA    R5,15        FOR BCT                                             
POINT20  DS    0H                                                               
         CLI   0(R6),C' '                                                       
         BNH   POINT20X                                                         
         LA    R3,31(R3)                                                        
         XC    0(31,R3),0(R3)                                                   
         MVI   0(R3),X'FD'                                                      
         MVC   1(1,R3),PUBKMED                                                  
         MVC   2(3,R3),PUBADVCD     ADV CODE                                    
         MVC   5(2,R3),PUBAORCD     AOR CODE                                    
         MVC   7(2,R3),PUBKAGY                                                  
         MVC   9(6,R3),PUBADVPC     ADV PUB                                     
         MVC   14(1,R3),0(R6)       SET EDITION CODE                            
         MVC   15(6,R3),PUBKPUB                                                 
         MVC   20(1,R3),0(R6)       SET EDITION CODE                            
         MVC   25(2,R3),27(R2)                                                  
         OI    25(R3),X'01'        SET PASSIVE IND                              
         MVI   31(R3),0                                                         
*                                                                               
         LA    R3,31(R3)                                                        
         XC    0(31,R3),0(R3)        MUST BE SURE IT'S CLEAR                    
         MVI   0(R3),X'FE'                                                      
         MVC   1(1,R3),PUBKMED                                                  
         MVC   2(2,R3),PUBKAGY                                                  
         MVC   4(3,R3),PUBADVCD     ADV CODE                                    
         MVC   7(2,R3),PUBADVCD     AOR CODE                                    
         MVC   9(6,R3),PUBKPUB                                                  
         MVC   14(1,R3),0(R6)      SET EDITION CODE                             
         MVC   15(6,R3),PUBADVPC    ADV PUB                                     
         MVC   20(1,R3),0(R6)      SET EDITION CODE                             
         MVC   25(2,R3),27(R2)                                                  
         OI    25(R3),X'01'        SET PASSIVE IND                              
         MVI   31(R3),0                                                         
         LA    R6,1(R6)                                                         
         BCT   R5,POINT20                                                       
*                                                                               
POINT20X DS    0H                                                               
         B     POINT12          MUST GO PROCESS NEXT X'80' ELEM                 
         DROP  R4                                                               
*                                                                               
POINT30  DS    0H                                                               
         LA    R6,SRCHBLK          POINT TO SEARCH CONTROL BLOCK                
         USING SEARCHD,R6 ESTABLISH CONTROL BLOCK                               
         MVI   SBID,C' '           INIT ID AREA                                 
         MVC   SBID+1(L'SBID-1),SBID TO SPACES                                  
         MVC   SBSYSTEM,=CL6'PRINT' SET SYSTEM                                  
         MVI   SBCTRY,0                                                         
         MVC   SBLANG,0                                                         
         ST    R2,SBAIN            PASS RECORD ADDRESS                          
         MVI   SBSTYLI,SBSTREQ     SET INPUT STYLE TO RECORD                    
         LA    R3,31(R3)                                                        
         ST    R3,SBAOUT           SET OUTPUT ADDRESS                           
         MVI   SBSTYLO,SBSTPASQ    SET OUTPUT STYLE TO PASSIVES                 
         MVI   SBMODE,1            SET TO CREATE PASSIVE PTRS                   
         BAS   R9,NXTSEQ           SETS NEXT SEQ# IN SBSEQNCE                   
         OI    SBSTYLI,SBPSOVSQ                                                 
*                                                                               
         GOTO1 =V(SRCHEXEC),DMCB,(R6)                                           
*                                                                               
         BE    POINT32                                                          
         CLI   SBRETURN,SBREOLQ    OKAY IF NO ENTRIES IN LIST                   
         BE    POINT32                                                          
         DC    H'0'                NO ERRORS ALLOWED                            
*                                                                               
POINT32  DS    0H                                                               
         L     R3,SBAOUT           POINT TO NEXT SPOT IN LIST                   
         XC    0(31,R3),0(R3)      CLEAR AT EOL                                 
*                                                                               
EXIT     XMOD1 1                                                                
         SPACE 2                                                                
*        NXTSEQ - SET NEXT SEQ# FOR NAME SEARCH                                 
         SPACE 1                                                                
NXTSEQ   DS    0H                                                               
         XC    WORK,WORK                                                        
         MVC   WORK(2),PUBKAGY     AGENCY                                       
         MVC   WORK+2(1),PUBKMED                                                
         GOTO1 =V(BINSRCH),BSPARS,(1,WORK)                                      
         SR    RF,RF                                                            
         ICM   RF,7,1(R1)                                                       
         BZ    NSQ20               NO MORE ROOM IN TABLE                        
         ICM   RE,15,3(RF)         BUMP SEQ# IN TABLE                           
         LA    RE,1(RE)                                                         
         STCM  RE,15,3(RF)                                                      
         ST    RE,SBSEQNCE                                                      
*                                                                               
         BR    R9                                                               
*                                                                               
NSQ20    DS    0H                  TABLE OVERFLOW                               
         XC    SBSEQNCE,SBSEQNCE   RETURN ZERO                                  
         BR    R9                                                               
*                                                                               
NEXTEL   DS    0H                                                               
         CLI   0(R4),0           END OF RECORD                                  
         BE    NEXTELX                                                          
         ZIC   R0,1(R4)                                                         
         AR    R4,R0                                                            
         CLC   0(1,R4),ELCODE                                                   
         BER   RE                                                               
         B     NEXTEL                                                           
*                                                                               
NEXTELX  LTR   RE,RE                                                            
         BR    RE                                                               
*                                                                               
DMCB     DC    6F'0'                                                            
WORK     DS    XL20                                                             
ELCODE   DS    XL1                                                              
DMPLST   DS    0D                                                               
STX1     DC    A(0),A(4000)                                                     
STX2     DC    A(0),A(4000)                                                     
STX3     DC    A(0),A(4000)                                                     
STX4     DC    A(0),A(10000)                                                    
         ORG   *-4                                                              
         DC    X'80'                                                            
         ORG                                                                    
*                                                                               
         DS    0F                                                               
BSPARS   DC    F'0'                                                             
         DC    A(SQLIST)                                                        
         DC    F'0'                                                             
         DC    A(SQLISTL)                                                       
         DC    A(SQLISTK)                                                       
         DC    A(SQLISTN)                                                       
*                                                                               
         DS    0D                                                               
SRCHBLK  DC    (SEARCHDL)X'00'                                                  
*                                                                               
         LTORG                                                                  
         SPACE 2                                                                
*                                                                               
SQLISTN  EQU   1000                200 AGENCIES X 5 MEDIA                       
SQLISTL  EQU   7                   AGY(2),MED(1),VALUE(4)                       
SQLISTK  EQU   3                   AGY(2),MED(1)                                
*                                                                               
SQLIST   DS    XL(SQLISTL*SQLISTN)                                              
*                                                                               
         EJECT                                                                  
PUBRECD  DSECT                                                                  
       ++INCLUDE PUBREC                                                         
       ++INCLUDE PUBNAMEL                                                       
*                                                                               
PUBADVD  DSECT                                                                  
       ++INCLUDE PPPUBADVEL                                                     
*                                                                               
*** INCLUDE GESRCHBLKD                                                          
*                                                                               
         PRINT OFF                                                              
       ++INCLUDE GESRCHBLKD                                                     
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'026PULDCPTR  05/01/02'                                      
         END                                                                    
