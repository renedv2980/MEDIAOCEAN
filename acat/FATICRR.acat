*          DATA SET FATICRR    AT LEVEL 017 AS OF 05/01/02                      
*CATALP FATICRR                                                                 
         TITLE 'TICRR - 2260 COURIER TERMINAL INPUT TRANSLATOR'                 
TICRR    CSECT                                                                  
         NMOD1 10,**TICRR                                                       
         USING INPD,RC                                                          
         MVC   SAVEPARA,0(R1)                                                   
         LM    R2,R4,0(R1)         PARA1 A(TIA)   INPUT                         
*                                  PARA2 A(TWA)                                 
*                                  PARA3 A(UTL)                                 
*                                                 OUTPUT                        
         XC    0(8,R1),0(R1)       PARA1 H        INDEX TO FIRST HEADER         
*                                        H        INDEX TO LAST HEADER          
*                                  PARA2 H        NUMBER OF FIELDS              
         LR    RA,R3                                                            
         USING UTLD,R4                                                          
         ST    R4,SVUTLADR                                                      
         MVC   SVTTYPE,TTYPE                                                    
         MVI   SRFLAG,0                                                         
         CLC   TSVCREQ+1(1),=X'47' TEST $KWX                                    
         BE    FIRSTTWA            YES - SET TO IGNORE PERIODS                  
         OC    TSVCREQ,TSVCREQ                                                  
         BZ    *+8                                                              
         MVI   SRFLAG,C'Y'                                                      
         DROP  R4                                                               
         SPACE 2                                                                
*                   TWA HEADER HANDLING                                         
         SPACE 3                                                                
FIRSTTWA LA    R2,64(R2)           POSITION TO FIRST                            
         LA    R3,64(R3)                                                        
         BAS   RE,TWXTEST                                                       
         CLI   0(R2),SOF           IGNORE LEADING COLON                         
         BNE   TESTTWA                                                          
         LA    R2,1(R2)                                                         
         B     TESTTWA                                                          
         SPACE 2                                                                
NEXTTWA  SR    R4,R4               GET NEXT                                     
         IC    R4,0(R3)                                                         
         AR    R3,R4                                                            
         SPACE 2                                                                
TESTTWA  CLI   0(R3),0             CHECK END OF LIST                            
         BE    CTEXT                                                            
         TM    1(R3),X'20'         SKIP PROTECTED FIELDS                        
         BO    NEXTTWA                                                          
         BAS   RE,TWXTEST                                                       
         EJECT                                                                  
*                   SCANNING INPUT FIELDS                                       
*                                                                               
         SR    R7,R7                                                            
         TM    SVTTYPE,X'20'       TEST ADDS TWX                                
         BZ    *+12                                                             
         IC    R7,0(R3)            SET MAX FIELD LEN                            
         SH    R7,=H'8'                                                         
*                                                                               
         SPACE 3                                                                
         SR    R5,R5               L'FIELD IN R5                                
         CLI   0(R2),CR                                                         
         BE    CLRTWA2                                                          
         CLI   0(R2),EOT                                                        
         BE    CLRTWA2                                                          
         CLI   0(R2),ETX           IF VERY FIRST CHARACTER IS ETX THEN          
         BNE   CT2                 THERE IS CONSIDERED TO BE NO INPUT           
         OI    6(R3),X'80'         TRANSMIT TO GET RID OF EOM                   
         B     CLRTWA2                                                          
         SPACE 2                                                                
CT2      TM    1(R3),X'80'         DROP LEADING BLANKS                          
         BNZ   CT4                 NO                                           
CT2A     CLI   0(R2),C' '                                                       
         BNE   CT4                                                              
         LA    R2,1(R2)                                                         
         BCT   R7,CT2A                                                          
         B     FIELDEND                                                         
         SPACE 2                                                                
CT4      LR    R4,R2               SAVE START OF THIS INPUT                     
         OI    4(R3),X'0E'         PRESET INPUT INDICATORS TO VALID             
         NI    4(R3),X'EF'                                                      
         SPACE 2                                                                
CT6      CLI   0(R2),EOF           CHECK END OF THIS FIELD                      
         BE    FIELDEND                                                         
         CLI   0(R2),ETX                                                        
         BE    FIELDEND                                                         
         CLI   0(R2),EOT                                                        
         BE    FIELDEND                                                         
         TM    SVTTYPE,X'20'       TEST ADDS TWX                                
         BZ    CT7                                                              
         CLI   SRFLAG,0            TEST SERVICE REQ                             
         BE    CT7                 NO                                           
         CLI   5(R1),4             TEST WITHIN FIRST 4 FIELDS                   
         BH    CT7                 NO                                           
         CLI   0(R2),C'.'          TEST FIELD DELIMITER                         
         BNE   CT7                                                              
         LA    R2,1(R2)            YES - ADVANCE INPUT POINTER                  
         B     FIELDEND            AND CONTINUE                                 
         SPACE 2                                                                
CT7      EQU   *                                                                
         CLI   0(R2),LF                                                         
         BE    FIELDEND                                                         
         CLI   0(R2),CR                                                         
         BE    FIELDEND                                                         
         CLI   0(R2),0             X'00' IS USED AS DELIMITER                   
         BNE   CT7A                                                             
         LA    R2,1(R2)                                                         
         BCT   R7,CT6                                                           
         B     FIELDEND                                                         
         SPACE 2                                                                
CT7A     TM    1(R3),X'80'         DROP TRAILING BLANKS                         
         BNZ   CT8                 NO                                           
         CLI   0(R2),C' '                                                       
         BNE   CT8                                                              
         LR    R6,R2               SAVE R2                                      
         B     CT7C                                                             
CT7B     CLI   0(R2),C' '                                                       
         BE    CT7C                                                             
         CLI   0(R2),0                                                          
         BNE   CT7D                                                             
CT7C     LA    R2,1(R2)                                                         
         BCT   R7,CT7B                                                          
         B     FIELDEND                                                         
*                                                                               
CT7D     CLI   0(R2),EOF                                                        
         BE    FIELDEND                                                         
         CLI   0(R2),ETX                                                        
         BE    FIELDEND                                                         
         CLI   0(R2),LF                                                         
         BE    FIELDEND                                                         
         CLI   0(R2),CR                                                         
         BE    FIELDEND                                                         
         SPACE 2                                                                
         SR    R6,R2               R6 = -(NO. OF IMBEDDED BLANKS)               
         SR    R5,R6               ADJUST LENGTH                                
         SPACE 2                                                                
CT8      CLI   0(R2),C'A'          CHECK FOR ALPHA                              
         BL    CT10                                                             
         CLI   0(R2),C'Z'                                                       
         BNH   CT12                                                             
         SPACE 2                                                                
CT10     NI    4(R3),X'FB'                                                      
*                                                                               
         CLI   0(R2),SOF           TEST FOR EMBEDDED SOF                        
         BNE   *+8                                                              
         MVI   0(R2),C' '          CHANGE TO SPACE                              
         SPACE 2                                                                
CT12     CLI   0(R2),C'A'          CHECK FOR HEXADECIMAL                        
         BL    CT14                                                             
         CLI   0(R2),C'9'                                                       
         BH    CT14                                                             
         CLI   0(R2),C'F'                                                       
         BNH   CT16                                                             
         CLI   0(R2),C'0'                                                       
         BNL   CT16                                                             
         SPACE 2                                                                
CT14     NI    4(R3),X'FD'                                                      
         SPACE 2                                                                
CT16     CLI   0(R2),C'9'          CHECK FOR NUMERIC                            
         BH    CT18                                                             
         CLI   0(R2),C'0'                                                       
         BNL   CT20                                                             
         SPACE 2                                                                
CT18     NI    4(R3),X'F7'         FIELD IS INVALID                             
         TM    1(R3),X'10'         CHECK IF NUMERIC SPECIFIED                   
         BZ    CT20                                                             
         OI    4(R3),X'10'                                                      
         SPACE 2                                                                
CT20     LA    R2,1(R2)                                                         
         LA    R5,1(R5)                                                         
         BCT   R7,CT6                                                           
         B     FIELDEND                                                         
         EJECT                                                                  
*                  ROUTINES FOR INPUT FIELDS                                    
         SPACE 3                                                                
FIELDEND NI    4(R3),X'7F'                                                      
         SR    R8,R8                                                            
         IC    R8,0(R3)                                                         
         SH    R8,=H'8'                                                         
         CR    R5,R8               CHECK LEN N.G. MAX LEN                       
         BNH   *+6                                                              
         LR    R5,R8               DEFAULT TO MAX                               
         STC   R5,5(R3)            SET INPUT LENGTH                             
         LA    R7,8(R3,R5)                                                      
         BCTR  R5,R0                                                            
         TM    1(R3),X'01'         TEST MODIFY BIT                              
         BNZ   INPUT               IF ON FIELD IS CONSIDERED INPUT              
         CLI   5(R3),0                                                          
         BE    FE2                                                              
         EX    R5,VARCOMP                                                       
         BNE   INPUT                                                            
FE2      EQU   *                                                                
         LR    R9,R8                                                            
         SR    R9,R5                                                            
         SH    R9,=H'2'                                                         
         BM    NOINPUT                                                          
         EX    R9,VAROC7                                                        
         BZ    NOINPUT                                                          
         CLI   0(R7),C' '                                                       
         BNE   INPUT                                                            
         SH    R9,=H'1'                                                         
         BM    NOINPUT                                                          
         EX    R9,VARCOMP7                                                      
         BE    NOINPUT                                                          
         SPACE 2                                                                
INPUT    LR    R6,R3               COMPUTE HEADER INDEX                         
         SR    R6,RA                                                            
         STH   R6,2(R1)                                                         
         OC    0(2,R1),0(R1)       CHECK FIRST INPUT                            
         BNZ   *+8                                                              
         STH   R6,0(R1)                                                         
         LH    R6,4(R1)            BUMP NUMBER OF FIELDS                        
         LA    R6,1(R6)                                                         
         STH   R6,4(R1)                                                         
         BCTR  R8,R0                                                            
         OI    4(R3),X'C0'         TURN ON TRANSMIT BITS                        
         NI    4(R3),X'DF'         TURN OFF VALIDATED BY USER                   
         EX    R8,VARCLEAR                                                      
         CLI   5(R3),0                                                          
         BE    NOINPUT                                                          
         EX    R5,VARMOVE                                                       
         SPACE 2                                                                
NOINPUT  CLI   0(R2),ETX           CHECK END OF MESSAGE                         
         BNE   *+12                                                             
         OI    6(R3),X'80'         XMIT TO GET RID OF ETX                       
         B     CLRTWA                                                           
         L     R7,SAVEPARA                                                      
         CLI   1(R7),X'01'         CHECK TIA INPUT INDIC WHICH                  
         BE    ONEFIELD            IF SET ONLY NEEDS ONE FIELD                  
         TM    SVTTYPE,X'20'       TEST ADDS TWX                                
         BO    *+8                                                              
         LA    R2,1(R2)                                                         
         B     NEXTTWA             NO - GO TO GET NEXT HEADER                   
         SPACE 2                                                                
ONEFIELD MVI   1(R7),0                                                          
         B     CTEXT                                                            
         SPACE 2                                                                
*                                  CLEAR REMAINING UNPROTECTED                  
*                                  FIELDS IN TWA                                
CLRTWA   SR    R4,R4                                                            
         IC    R4,0(R3)                                                         
         AR    R3,R4                                                            
         CLI   0(R3),0                                                          
         BE    CTEXT                                                            
         TM    1(R3),X'20'                                                      
         BO    CLRTWA                                                           
* FOR SERVICE REQUESTS DON'T CLEAR UNP FLDS NOT INPUT                           
CLRTWA2  L     R4,SVUTLADR                                                      
         USING UTLD,R4                                                          
         CLC   TSVCREQ+1(1),=X'47'                                              
         BE    CTEXT                                                            
         DROP  R4                                                               
*                                                                               
         NI    4(R3),X'00'         SET OFF ALL INPUT INDS                       
         MVI   5(R3),0             CLEAR INPUT LENGTH                           
         MVI   7(R3),0             CLEAR OUTPUT LENGTH                          
         IC    R6,0(R3)                                                         
         SH    R6,=H'9'                                                         
         EX    R6,VAROC            CHECK ALREADY CLEAR                          
         BZ    CLRTWA                                                           
         EX    R6,VARCLEAR         CLEAR                                        
         OI    6(R3),X'80'         SET ON TRANSMIT                              
         B     CLRTWA                                                           
         SPACE 2                                                                
TWXTEST  TM    SVTTYPE,X'20'                                                    
         BR    RE                  *** DISABLE TEST ***                         
         LR    RF,R3                                                            
         SR    R0,R0                                                            
*                                                                               
TWXTEST2 IC    R0,0(RF)                                                         
         AR    RF,R0                                                            
         CLI   0(RF),0                                                          
         BER   RE                                                               
         CLI   0(RF),88                                                         
         BNH   TWXTEST2                                                         
         DC    H'0'                                                             
         EJECT                                                                  
*                  VARIABLES AND EXIT                                           
VARCOMP  CLC   0(0,R4),8(R3)                                                    
         SPACE 2                                                                
VARCLEAR XC    8(0,R3),8(R3)                                                    
VARMOVE  MVC   8(0,R3),0(R4)                                                    
VAROC    OC    8(0,R3),8(R3)                                                    
VAROC7   OC    0(0,R7),0(R7)                                                    
VARCOMP7 CLC   1(0,R7),0(R7)                                                    
         SPACE 2                                                                
         SPACE 2                                                                
CTEXT    XMOD1 1                                                                
         SPACE 2                                                                
         LTORG                                                                  
         SPACE 2                                                                
INPD     DSECT                                                                  
DUB      DS    D                                                                
SAVEPARA DS    D                                                                
SVTTYPE  DS    C                                                                
SVUTLADR DS    A                                                                
         ORG   *-4                                                              
SRFLAG   DS    C                                                                
         ORG                                                                    
         EJECT                                                                  
       ++INCLUDE FAUTL                                                          
         EJECT                                                                  
*FASCREQUS                                                                      
       ++INCLUDE FASCREQUS                                                      
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'017FATICRR   05/01/02'                                      
         END                                                                    
