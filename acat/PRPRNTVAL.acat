*          DATA SET PRPRNTVAL  AT LEVEL 031 AS OF 05/01/02                      
*CATALP PRNTVAL                                                                 
         SPACE 5                                                                
         TITLE 'PRNTVAL. CONVERTS BUY REC TO PRNTBLOK'                          
*************************************************************                   
* PRNTVAL   - PRNTBLOK IS INPUT. TAKES THE INSERTION   RECORD                   
* WHOSE ADDRESS IS PROVIDED AND FILLS IN MANY OF THE PRNTBLOK DSECT             
* FIELDS BASED ON THIS RECORD.                                                  
*                                                                               
*                                                                               
*                                                                               
*                                                                               
**********************************************************                      
*                                                                               
         SPACE 3                                                                
         PRINT NOGEN                                                            
PRNTVAL  CSECT                                                                  
         NMOD1 PRNTWRKX-PRNTWORK,**PRTVALU,R9,RR=R2,CLEAR=YES                   
         L     RA,0(R1)            ADDRESS OF PRNTBLOK.                         
         USING PRNTBLOK,RA                                                      
         USING PRNTWORK,RC                                                      
         ST    R2,RELO                                                          
         LA    R1,IO1                                                           
         ST    R1,AIO1                                                          
         EJECT                                                                  
************************************************                                
* MAIN SECTION - LOOKS AT INFO IN PRNTBLOK AND CALLS APPROPRIATE                
*                  ROUTINE.                                                     
************************************************                                
*                                                                               
         MVI   SPACES,X'40'                                                     
         MVC   SPACES+1(131),SPACES                                             
*                                                                               
         L     R7,PBAIO            POINT TO RECORD                              
         BAS   RE,PROCREC                                                       
         B     EXIT                                                             
*                                                                               
EXIT     XMOD1 1                                                                
         EJECT                                                                  
***************************************************                             
* PROCREC - TAKES BUY RECORD IN THE I/O AREA AND FILL IN APPROPRIATE            
*             FIELDS IN THE PRNTBLOK DSECT. SUMS UP ELEMENTS.                   
*                                                                               
* CALLED FROM: MAIN PROGRAM.                                                    
*                                                                               
* CALLS TO: NEXTEL - MACRO WHICH PROCESSES ELEMENT RECORDS.                     
*                                                                               
* LOCALS: R6 - USED (AND UPDATED) BY NEXTEL. CONTAINS THE ADDRESS OF            
*               THE RECORD OR THE CURRENT ELEMENT.                              
*         R7-  THE ADDRESS OF PBUYREC. USED FOR DSECTS.                         
*                                                                               
**********************************************************                      
*                                                                               
         USING PBUYREC,R7                                                       
PROCREC  NTR1                                                                   
*                                                                               
*         FILL IN ACTUAL KEY VALUES                                             
*                                                                               
         MVC   PBACTAGY(3),PBUYKAGY          AGY/MEDIA                          
         MVC   PBACTCLT,PBUYKCLT               CLIENT                           
         LA    R1,PBUYKEY+7                                                     
         CLI   PBUYKEY+3,X'20'                                                  
         BE    *+8                                                              
         LA    R1,PBUYKEY+13                                                    
         MVC   PBACTPRD,0(R1)                 PRODUCT                           
         LA    R1,PBUYKEY+10                                                    
         CLI   PBUYKEY+3,X'20'                                                  
         BE    *+8                                                              
         LA    R1,PBUYKEY+7                                                     
         MVC   PBACTPUB(6),0(R1)                 PUB/ZONE/EDITION               
         MVC   PBACTINS(9),PBUYKDAT         INSDAT/EST/ACTPRD/LINE              
*                                                                               
         BAS   RE,AGYDATA                                                       
         BAS   RE,CLTDATA                                                       
         BAS   RE,PRDDATA                                                       
         BAS   RE,DOLLARS                                                       
         BAS   RE,PUBDATA                                                       
         B     EXIT                                                             
         EJECT                                                                  
*                                                                               
AGYDATA  NTR1                      AGENCY/MEDIA                                 
         XC    KEY,KEY                                                          
         LA    R6,KEY                                                           
         USING PAGYKEY,R6                                                       
         MVC   PAGYKAGY(3),PBUYKAGY                                             
         MVI   PAGYKRCD,X'01'                                                   
         CLC   PBVLAGYK(25),KEY          IS IT NEW KEY                          
         BE    AGYX                      IF NOT/SKIP                            
         BAS   RE,HIGH                   YES/GET THE RECORD                     
         CLC   KEY(25),KEYSAVE                                                  
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   AREC,AIO1                                                        
         BAS   RE,GETREC                                                        
         MVI   ELCODE,X'01'                                                     
         L     R6,AREC                                                          
         USING PAGYELEM,R6                                                      
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   PBAMEDNM,PAGYMED          MEDIA NAME                             
         MVC   PBAGYPRF,PAGYPROF         AGENCY PROFILE                         
         MVC   PBVLAGYK,KEY               SAVE AGY KEY                          
AGYX     B     EXIT                                                             
         EJECT                                                                  
*                                                                               
*                                                                               
CLTDATA  NTR1                      CLIENT                                       
         XC    KEY,KEY                                                          
         LA    R6,KEY                                                           
         USING PCLTKEY,R6                                                       
         MVC   PCLTKAGY(3),PBUYKEY      AGY/MED                                 
         MVI   PCLTKRCD,X'02'                                                   
         MVC   PCLTKCLT,PBUYKCLT        CLIENT                                  
         CLC   PBVLCLTK(25),KEY            IS IT NEW                            
         BE    CLTX                                                             
         BAS   RE,HIGH                     YES/GET THE RECORD                   
         CLC   KEY(25),KEYSAVE                                                  
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   AREC,AIO1                                                        
         BAS   RE,GETREC                                                        
         L     R6,AREC                                                          
         USING PCLTELEM,R6                                                      
         MVI   ELCODE,X'02'                                                     
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   PBACLTNM,PCLTNAME                                                
         MVC   PBAOFFIC,PCLTOFF    CLIENT OFFICE                                
         MVC   PBVLCLTK,KEY                                                     
CLTX     B     EXIT                                                             
         EJECT                                                                  
*                                                                               
PRDDATA  NTR1                      PRODUCT                                      
         XC    KEY,KEY                                                          
         LA    R6,KEY                                                           
         USING PPRDKEY,R6                                                       
         MVC   PPRDKAGY,PBUYKAGY                                                
         MVC   PPRDKMED,PBUYKMED                                                
         MVI   PPRDKRCD,X'06'                                                   
         MVC   PPRDKCLT,PBUYKCLT                                                
         MVC   PPRDKPRD,PBUYKPRD                                                
         CLC   PBVLPRDK(25),KEY                                                 
         BE    PRDX                                                             
         BAS   RE,HIGH                                                          
         CLC   KEY(25),KEYSAVE                                                  
         BE    *+6                                                              
         DC    H'0'                                                             
         BAS   RE,GETREC                                                        
         L     R6,AREC                                                          
         USING PPRDELEM,R6                                                      
         MVI   ELCODE,X'06'                                                     
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   PBAPRDNM,PPRDNAME         PRODUCT NAME                           
         MVC   PBADIV,PPRDDIV            DIVISION NAME                          
         MVC   PBVLPRDK,KEY                                                     
PRDX     B     EXIT                                                             
         EJECT                                                                  
*                                                                               
ESTNM    NTR1                     ESTIMATE                                      
         XC    KEY,KEY                                                          
         LA    R6,KEY                                                           
         USING PESTKEY,R6                                                       
         MVC   PESTKAGY,PBUYKAGY                                                
         MVC   PESTKMED,PBUYKMED                                                
         MVI   PESTKRCD,X'07'                                                   
         MVC   PESTKCLT,PBUYKCLT                                                
         MVC   PESTKPRD,PBUYKPRD                                                
         MVC   PESTKEST,PBUYKEST                                                
         CLC   PBVLESTK(25),KEY                                                 
         BE    ESTX                                                             
         BAS   RE,HIGH                                                          
         CLC   KEY(25),KEYSAVE                                                  
         BE    *+6                                                              
         DC    H'0'                                                             
         BAS   RE,GETREC                                                        
         L     R6,AREC                                                          
         USING PESTELEM,R6                                                      
         MVI   ELCODE,X'07'                                                     
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   PBAESTNM,PESTNAME                                                
         MVC   PBVLESTK,KEY                                                     
ESTX     B     EXIT                                                             
*                                                                               
         EJECT                                                                  
*                                                                               
DIVNM    NTR1                      DIVISION                                     
         XC    KEY,KEY                                                          
         LA    R4,KEY                                                           
         USING PDIVKEY,R4                                                       
         MVC   PDIVKAGY,PBUYKAGY                                                
         MVC   PDIVKMED,PBUYKMED                                                
         MVI   PDIVKRCD,X'03'                                                   
         MVC   PDIVKCLT,PBUYKCLT                                                
         MVC   PDIVKDIV,PBADIV                                                  
         CLC   PBVLDIVK(25),KEY                                                 
         BE    DIVX                                                             
         BAS   RE,HIGH                                                          
         CLC   KEY(25),KEYSAVE                                                  
         BE    *+6                                                              
         DC    H'0'                                                             
         BAS   RE,GETREC                                                        
         L     R6,AREC                                                          
         USING PDIVELEM,R6                                                      
         MVI   ELCODE,X'03'                                                     
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   PBADIVNM,PDIVNAME                                                
         MVC   PBVLDIVK,KEY                                                     
DIVX     B     EXIT                                                             
         EJECT                                                                  
*                                                                               
REGNM    NTR1                           REGION                                  
         XC    KEY,KEY                                                          
         LA    R6,KEY                                                           
         USING PREGKEY,R6                                                       
         MVC   PREGKAGY,PBUYKAGY                                                
         MVC   PREGKMED,PBUYKMED                                                
         MVI   PREGKRCD,X'04'                                                   
         MVC   PREGKCLT,PBUYKCLT                                                
         MVC   PREGKDIV,PBADIV                                                  
         MVC   PREGKREG,PBAREG                                                  
         CLC   PBVLREGK(25),KEY                                                 
         BE    REGX                                                             
         BAS   RE,HIGH                                                          
         CLC   KEY(25),KEYSAVE                                                  
         BE    *+6                                                              
         DC    H'0'                                                             
         BAS   RE,GETREC                                                        
         L     R6,AREC                                                          
         USING PREGELEM,R6                                                      
         MVI   ELCODE,X'04'                                                     
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   PBEFFREG,PREGKREG                                                
         MVC   PBEFRGNM,PREGNAME                                                
         MVC   PBVLREGK,KEY                                                     
REGX     B     EXIT                                                             
         EJECT                                                                  
*                                                                               
DSTNM    NTR1                           DISTRICT                                
         XC    KEY,KEY                                                          
         LA    R4,KEY                                                           
         USING PDSTKEY,R4                                                       
         MVC   PDSTKAGY,PBUYKAGY                                                
         MVC   PDSTKMED,PBUYKMED                                                
         MVI   PDSTKRCD,X'05'                                                   
         MVC   PDSTKCLT,PBUYKCLT                                                
         MVC   PDSTKDIV,PBADIV                                                  
         MVC   PDSTKREG,PBAREG                                                  
         MVC   PDSTKDST,PBADST                                                  
         CLC   PBVLDSTK(25),KEY                                                 
         BE    DSTX                                                             
         BAS   RE,HIGH                                                          
         CLC   KEY(25),KEYSAVE                                                  
         BE    *+6                                                              
         DC    H'0'                                                             
         BAS   RE,GETREC                                                        
         L     R6,PBAIO                                                         
         USING PDSTELEM,R6                                                      
         MVI   ELCODE,X'05'                                                     
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   PBEFFDST,PDSTKDST                                                
         MVC   PBEDSTNM,PDSTNAME                                                
         MVC   PBVLDSTK,KEY                                                     
DSTX     B     EXIT                                                             
*                                                                               
         EJECT                                                                  
*                                                                               
DOLLARS  NTR1                                                                   
         GOTO1 =V(GETINS),DMCB,(R7),PBGROSS,7(R7),RR=RELO                       
         TM    PBUYCNTL,X'80'                                                   
         BZ    *+10                                                             
         XC    PBGROSS(12),PBGROSS                                              
         BAS   RE,RDSPLIT                                                       
         L     R4,AIO1                                                          
         USING PPBYOUTD,R4                                                      
         ST    R7,PBYOINPT                                                      
         MVC   PBYODTCN,PBDATCON                                                
         MVI   PBYOCTL,X'24'       FOR ZZZ ALLOCATION/IO COMMENTS               
         GOTO1 =V(PPBYOUT),DMCB,PPBYOUTD,RR=RELO                                
         LA    RF,PBBYOUT                                                       
         LA    R1,500                                                           
         LA    RE,PBYOP                                                         
         MOVE  ((RF),(R1)),(RE)               MOVE DATA TO PRNTBLOK             
         B     EXIT                                                             
         EJECT                                                                  
*                             GET SHARE FOR REGION/DISTRICT                     
*                                                                               
*                                  THIS ROUTINE FORCES THE SHARES FOR           
*                                  EACH REG/DIST TO ADD TO THE TOTAL.           
*                                  THE LOGIC PARALELLS THAT FOR POOL            
*                                  ALLOCATION IN GETINS.                        
RDSPLIT  NTR1                                                                   
         MVC   DOUBLE(6),PBVLRD          REG/DIST                               
         OC    DOUBLE(6),SPACES                                                 
         MVC   RDDIFS(44),PBGROSS                                               
         MVC   RDDIFS+44(16),PBBGROSS                                           
         XC    RDSUMS,RDSUMS                                                    
         XC    RDBLSUMS,RDBLSUMS                                                
         XC    FULL,FULL                                                        
         MVC   RDCLTDIV(3),PBEFFCLT     CLIENT                                  
         MVC   RDCLTDIV+3(3),PBEFFDIV   DIV                                     
*                                                                               
*                                  FIRST PASS                                   
         L     R2,PBAIO1                                                        
         LA    R2,33(R2)                                                        
         CLI   0(R2),X'71'                                                      
         BE    RDSP4                                                            
RDSP3    DS    0H                                                               
         BAS   RE,RDNXTEL                                                       
         BNE   RDSP8                                                            
RDSP4    DS    0H                                                               
         USING PUBDSTEL,R2                                                      
         OC    PUBDCLT,SPACES                                                   
         CLC   PUBDCLT(6),RDCLTDIV                                              
         MVC   WORK(6),PUBDCLT                                                  
         BNE   RDSP3                                                            
         XC    FULL,FULL                                                        
         MVC   FULL+2(2),PUBDSHR                                                
         LA    R4,PBGROSS                                                       
         LA    R3,RDDIFS                                                        
         ZAP   COUNT,=P'1'                                                      
RDSP5    DS    0H                                                               
         L     R1,0(R4)                                                         
         LTR   R1,R1                                                            
         BZ    RDSP6                                                            
         M     R0,FULL                                                          
         BAS   RE,RDDIV            ROUNDED DIVISION                             
         L     R0,0(R3)                                                         
         SR    R0,R1                                                            
         ST    R0,0(R3)                                                         
RDSP6    DS    0H                                                               
         CP    COUNT,=P'15'                                                     
         BE    RDSP6E                                                           
         AP    COUNT,=P'1'                                                      
         CP    COUNT,=P'12'                                                     
         BL    RDSP6C                                                           
         BNE   RDSP6C                                                           
         LA    R4,PBBGROSS                                                      
         LA    R3,4(R3)                                                         
         B     RDSP6D                                                           
RDSP6C   LA    R4,4(R4)                                                         
         LA    R3,4(R3)                                                         
RDSP6D   B     RDSP5               NEXT AMOUNT                                  
RDSP6E   B     RDSP3               NEXT SHARE                                   
*                                                                               
*                                                                               
RDSP8    DS    0H                                                               
         OC    FULL,FULL                                                        
         BZ    RDSPLITX            NO SHARES                                    
*                                  END OF PASS 1                                
*                                  NOW RDDIFS HAS DIFFERENCES                   
*                                  BETWEEN ORIGINAL AMOUNTS AND                 
*                                  SUM OF ROUNDED SHARES                        
*                                                                               
*                                                                               
*                                  PASS 2                                       
         L     R2,PBAIO1                                                        
         LA    R2,33(R2)                                                        
         CLI   0(R2),X'71'                                                      
         BE    RDSP10                                                           
RDSP9    DS    0H                                                               
         BAS   RE,RDNXTEL                                                       
         BNE   RDSP30                                                           
RDSP10   DS    0H                                                               
         CLC   PUBDCLT(6),RDCLTDIV                                              
         BNE   RDSP9                                                            
*                                  IS THIS A SELECTED REG/DST                   
         MVI   DOUBLE+6,0               NO                                      
         CLC   DOUBLE(3),=C'ALL'                                                
         BE    RDSP11                                                           
         CLC   DOUBLE(3),SPACES                                                 
         BE    RDSP11                                                           
         CLC   DOUBLE(3),PUBDREG                                                
         BH    RDSP13                                                           
         BL    RDSP30                                                           
RDSP11   DS    0H                                                               
         CLC   DOUBLE+3(3),=C'ALL'                                              
         BE    RDSP12                                                           
         CLC   DOUBLE+3(3),SPACES                                               
         BE    RDSP12                                                           
         CLC   DOUBLE+3(3),PUBDDST                                              
         BH    RDSP13                                                           
         BL    RDSP30                                                           
RDSP12   DS    0H                                                               
         MVI   DOUBLE+6,1               YES                                     
RDSP13   DS    0H                                                               
         MVC   FULL+2(2),PUBDSHR                                                
         LA    R4,PBGROSS                                                       
         LA    R3,RDDIFS                                                        
         ZAP   COUNT,=P'1'                                                      
RDSP13A  DS    0H                                                               
         OC    0(4,R4),0(R4)                                                    
         BZ    RDSP20              NO AMOUNT                                    
         OC    0(4,R3),0(R3)                                                    
         BNZ   RDSP14                                                           
*                                  NO DIFF                                      
         CLI   DOUBLE+6,0          TEST IF A SELECTED REG/DST                   
         BE    RDSP20              NO                                           
         L     R1,0(R4)                                                         
         M     R0,FULL             YES COMPUTE SHARE                            
         BAS   RE,RDDIV                                                         
*                                  AND ADD TO RDSUMS                            
RDSP13B  DS    0H                                                               
         A     R1,60(R3)                                                        
         ST    R1,60(R3)                                                        
         B     RDSP20                                                           
RDSP13D  DS    0H                                                               
         CLI   DOUBLE+6,0          TEST IF A SELECTED REG/DST                   
         BE    RDSP20              NO                                           
         B     RDSP13B             YES                                          
*                                  DIFFERENCE NO-ZERO                           
RDSP14   DS    0H                                                               
         L     R1,0(R4)                                                         
         M     R0,FULL             SHARE                                        
         D     R0,=F'10000'        TRUNCATED DIVISION                           
         LTR   R0,R0               TEST REMAINDER                               
         BZ    RDSP13D             ZERO                                         
         BM    RDSP17              NEG                                          
*                                                                               
*                                  AMOUNT POS                                   
         TM    0(R3),X'80'         TEST DIFF                                    
         BNZ   RDSP15                                                           
*                                  AMOUNT POS, DIFF POS                         
         C     R0,=F'5000'         TEST IF ROUNDED DOWN                         
         BNL   RDSP13D             NO                                           
         L     RF,0(R3)            YES - SUBTRACT 1 FROM DIFF                   
         BCTR  RF,R0                                                            
         ST    RF,0(R3)                                                         
         A     R1,=F'1'            ADD 1 TO AMOUNT                              
         B     RDSP13D                                                          
*                                  AMOUNT POS, DIFF NEG                         
RDSP15   DS    0H                                                               
         C     R0,=F'5000'         TEST IF ROUNDED UP                           
         BL    RDSP13D             NO                                           
         L     RF,0(R3)            YES - ADD 1 TO DIFF                          
         A     RF,=F'1'                                                         
         ST    RF,0(R3)                                                         
         B     RDSP13D                                                          
*                                                                               
*                                  AMOUNT NEG                                   
RDSP17   DS    0H                                                               
         TM    0(R3),X'80'         TEST DIFF                                    
         BZ    RDSP18                                                           
*                                  AMOUNT NEG, DIFF NEG                         
         C     R0,=F'-5000'       TEST IF ROUNDED UP                            
         BL    RDSP13D             NO                                           
         L     RF,0(R3)            YES - ADD 1 TO DIFF                          
         A     RF,=F'1'                                                         
         ST    RF,0(R3)                                                         
         BCTR  R1,R0               SUBTRACT 1 FROM AMT                          
         B     RDSP13D                                                          
*                                  AMOUNT NEG, DIFF POS                         
RDSP18   DS    0H                                                               
         C     R0,=F'-5000'        TEST IF ROUNDED DOWN                         
         BNL   RDSP13D             NO                                           
         L     RF,0(R3)            YES - SUB 1 FROM DIFF                        
         BCTR  RF,R0                                                            
         ST    RF,0(R3)                                                         
         B     RDSP13D                                                          
*                                                                               
*                                                                               
RDSP20   DS    0H                                                               
         CP    COUNT,=P'15'                                                     
         BE    RDSP25                                                           
         AP    COUNT,=P'1'                                                      
         CP    COUNT,=P'12'                                                     
         BNE   RDSP23                                                           
         LA    R4,PBBGROSS                                                      
         LA    R3,4(R3)                                                         
         B     RDSP24                                                           
RDSP23   LA    R4,4(R4)                                                         
         LA    R3,4(R3)                                                         
RDSP24   B     RDSP13A             NEXT AMOUNT                                  
RDSP25   B     RDSP9               NEXT SHARE                                   
*                                                                               
RDSP30   DS    0H                                                               
         MVC   PBGROSS(44),RDSUMS                                               
         MVC   PBBGROSS(16),RDBLSUMS                                            
*                                                                               
*                                                                               
RDSPLITX DS    0H                                                               
         B     EXIT                                                             
         SPACE 3                                                                
RDNXTEL  DS    0H                                                               
         SR    R0,R0                                                            
         IC    R0,1(R2)                                                         
         AR    R2,R0                                                            
         CLI   0(R2),0                                                          
         BE    RDNXTEL2                                                         
         CLI   0(R2),X'71'                                                      
         BER   RE                                                               
         B     RDNXTEL+2                                                        
RDNXTEL2 LTR   R2,R2                                                            
         BR    RE                                                               
         SPACE 3                                                                
RDDIV    DS    0H                                                               
         SLDA  R0,1                                                             
         D     R0,=F'10000'                                                     
         LTR   R1,R1                                                            
         BNP   *+8                                                              
         A     R1,=F'1'                                                         
         SRA   R1,1                                                             
         BR    RE                                                               
*                                                                               
         DS    0F                                                               
RDDIFS   DS    CL60                                                             
RDSUMS   DS    CL44                                                             
RDBLSUMS DS    CL16                                                             
RDCLTDIV DS    CL6                                                              
COUNT    DC    PL2'0'                                                           
*                                                                               
         EJECT                                                                  
PUBDATA  NTR1                                                                   
         IC    R3,PAGYPROF+12                                                   
         LA    R4,WORK              OUTPUT FOR PUB NUMBER                       
         GOTO1 =V(PUBEDIT),DMCB,((R3),PBUYKPUB),(C'S',0(R4)),RR=RELO            
*****                                                                           
*** IS THERE A FIELD IN PRNTBLOK FOR EDITED PUB NUMBER                          
*****                                                                           
         XC    KEY,KEY                                                          
         MVC   KEY(1),PBUYKMED                                                  
         MVC   KEY+1(6),PBUYKPUB                                                
         MVC   KEY+7(2),PBUYKAGY                                                
         MVI   KEY+9,X'81'                                                      
****     CLC   PUBKEY(07),KEY                                                   
****     BE    DP02D3              ALREADY HAVE RECORD                          
*                                                                               
         MVI   FILTP,C'U'          SET TO PUB FILE                              
         BAS   RE,HIGH                                                          
         CLC   KEY(25),KEYSAVE                                                  
         BE    PUB5                                                             
         CLI   PAGYPROF+16,C'0'    TEST SRDS DEFAULT                            
         BNE   *+8                                                              
         BAS   RE,NOPUB                                                         
         CLC   KEY(7),KEYSAVE                                                   
         BE    *+8                                                              
         BAS   RE,NOPUB                                                         
         MVC   KEY+7(2),=C'ZZ'                                                  
         MVI   KEY+9,X'81'                                                      
         CLC   KEY(25),KEYSAVE                                                  
         BE    PUB5                HAVE SRDS                                    
         BAS   RE,HIGH                                                          
         CLC   KEY(25),KEYSAVE                                                  
         BE    *+6                                                              
NOPUB    DC    H'0'                PUB NOT FOUND                                
*                                                                               
PUB5     DS    0H                                                               
         LA    R1,IO1                                                           
         ST    R1,AREC                                                          
         BAS   RE,GETREC                                                        
         L     R6,AREC                                                          
         USING PUBNAMEL,R6                                                      
         MVI   ELCODE,X'10'                                                     
         BAS   RE,GETEL                                                         
         MVC   PBAPUBNM,SPACES                                                  
         MVC   PBAZONAM,SPACES                                                  
         MVC   PBAPUBNM,PUBNAME              PUB NAME                           
         MVC   PBAZONAM,PUBZNAME             ZONE NAME                          
         MVI   FILTP,0             RESET TO PRINT FILE                          
         DROP  R6                                                               
         B     EXIT                                                             
         EJECT                                                                  
         EJECT                                                                  
*                                                                               
*              DATAMGR INTERFACE                                                
         SPACE 3                                                                
HIGH     NTR1                                                                   
         MVC   KEYSAVE,KEY                                                      
         MVC   COMMAND,=CL8'DMRDHI'          HANDLE COMMANDS                    
         B     DIRALL                                                           
         SPACE 1                                                                
SEQ      NTR1                                                                   
         MVC   KEYSAVE,KEY                                                      
         MVC   COMMAND,=CL8'DMRSEQ'                                             
         B     DIRALL                                                           
         SPACE 1                                                                
READ     NTR1                                                                   
         MVC   KEYSAVE,KEY                                                      
         MVC   COMMAND,=CL8'DMREAD'                                             
         SPACE 1                                                                
DIRALL   MVC   FILE(8),=C'PRTDIR  '         DIRECTORIES                         
         CLI   FILTP,C'U'                                                       
         BNE   *+10                                                             
         MVC   FILE(8),=C'PUBDIR  '                                             
         GOTO1 PBDM,DMCB,(0,COMMAND),FILE,KEY,KEY,0                             
         BAS   RE,DMCHECK                                                       
         B     YES                                                              
         SPACE 1                                                                
GETREC   NTR1                                                                   
         MVC   FILE(8),=C'PRTFILE '     FILE                                    
         MVC   PBDTADSP,=H'33'                                                  
         LA    R3,KEY+27                                                        
         CLI   FILTP,C'U'                                                       
         BNE   *+10                                                             
         MVC   FILE(8),=C'PUBFILE '                                             
         GOTO1 PBDM,DMCB,(0,=C'GETREC'),FILE,(R3),AREC,DMWORK                   
         BAS   RE,DMCHECK                                                       
         B    EXIT                                                              
         SPACE 1                                                                
DMCHECK  CLI   8(R1),0             OK                                           
         BER   RE                                                               
         CLI   8(R1),2             RECORD IS DELETED OK TOO                     
         BER   RE                                                               
         TM    8(R1),X'90'                                                      
         BM    NO                                                               
         DC    H'0'                                                             
YES      SR    R1,R1                                                            
         B     *+8                                                              
NO       LA    R1,1                                                             
         LTR   R1,R1                                                            
         B     EXIT                                                             
         SPACE 1                                                                
         EJECT                                                                  
*                                                                               
         GETEL  R6,PBDTADSP,ELCODE                                              
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*                                                                               
PRNTWORK DSECT                                                                  
*                                                                               
DMWORK   DS    12D                 WORK AREA FOR DATAMGR                        
DMCB     DS    6F                  WORK AREA FOR GOTOS                          
DUB      DS    D                                                                
FULL     DS    F                                                                
RELO     DS    F                                                                
AREC     DS    A                                                                
AIO1     DS    F                                                                
HALF     DS    H                                                                
COMMAND  DS    CL8                                                              
ELCODE   DS    CL1                                                              
WORK     DS    CL64                                                             
BYTE     DS    CL1                                                              
FILE     DS    CL8                                                              
FILTP    DS    CL1                                                              
DOUBLE   DS    CL8                                                              
SPACES   DS    CL132                                                            
*                                                                               
*                                                                               
KEY      DS    CL33                                                             
KEYSAVE  DS    CL33                                                             
*                                                                               
IO1      DS    CL1000          I/O AREA FOR RECORDS                             
*                                                                               
PRNTWRKX EQU   *                                                                
         EJECT                                                                  
         SPACE 3                                                                
*        PRNTINCLS                                                              
*    AND REQUIRED PRINT RECORDS                                                 
PRNTBLKD DSECT                                                                  
       ++INCLUDE PRNTBLOK                                                       
       ++INCLUDE PPBYOUTD                                                       
       ++INCLUDE PAGYREC                                                        
       ++INCLUDE PCLTREC                                                        
       ++INCLUDE PDIVREC                                                        
       ++INCLUDE PREGREC                                                        
       ++INCLUDE PDSTREC                                                        
       ++INCLUDE PESTREC                                                        
       ++INCLUDE PPRDREC                                                        
       ++INCLUDE PBUYREC                                                        
       ++INCLUDE PUBDSTEL                                                       
       ++INCLUDE PUBREC                                                         
       ++INCLUDE PUBNAMEL                                                       
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'031PRPRNTVAL 05/01/02'                                      
         END                                                                    
