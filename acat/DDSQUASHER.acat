*          DATA SET DDSQUASHER AT LEVEL 003 AS OF 07/15/09                      
*CATALP SQUASHER                                                                
                                                                                
* AL4    A(STRING TO BY SQUASHED)                                               
* CL1    OPTIONAL CHR TO REPLACE BLANK CHR                                      
* XL1    N/D                                                                    
* XL2    L'STRING TO BY SQUASHED - OVER WRITTEN BY SQUASHED LEN                 
                                                                                
         TITLE 'PRINT SQUASHING MODULE'                                         
SQUASHER CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 1,**SQSH**                                                       
         USING WORKD,RC                                                         
         LA    RE,*+6                                                           
         BSM   0,RE                SET IN 24-BIT MODE                           
*                                                                               
         LR    RA,R1               RA=A(PARAM LIST)                             
         L     R3,0(RA)            R3=A(STRING)                                 
         SR    R4,R4               R4=L'STRING                                  
         ICM   R4,3,6(RA)                                                       
         LR    R6,R3               R6=A(START OF STRING)                        
         SR    R7,R7               R7=NUM OF CHRS SQUASHED OUT                  
         LA    R8,0(R3,R4)         R8=A(END OF STRING)                          
         LA    R9,4                R9=TRT VALUE FOR DCTNRY ESCAPE               
         MVI   BLANK,C' '                                                       
         CLI   4(RA),C' '          TEST IF SPECIAL CHR PASSED                   
         BNH   *+10                                                             
         MVC   BLANK,4(RA)         YES THIS IS THE NEW REPLACE CHR              
         MVI   4(RA),0                                                          
         SPACE 1                                                                
SQUASH1  LTR   R4,R4               TEST RESIDUAL LENGTH OF STRING               
         BNP   SQUASHA                                                          
         SR    R2,R2                                                            
         TRT   0(1,R3),TRTDD       TEST IF STRING STARTS WITH BLANK             
         CR    R2,R9                                                            
         BH    SQUASH3             YES PROCESS BLANK STRING                     
         SPACE 1                                                                
SQUASH2  LR    RF,R4               PROCESS NONBLANK STRING AT R3                
         CH    RF,=H'256'          -----------------------------                
         BNH   *+8                                                              
         LA    RF,256                                                           
         SH    RF,=H'1'            TEST END OF STRING                           
         BM    SQUASHA                                                          
         LA    R1,1(R3,RF)         POINT TO END OF STRING                       
         SR    R2,R2                                                            
         EX    RF,*+8                                                           
         B     *+10                                                             
         TRT   0(0,R3),TRTDD       SET R1 TO POINT AT NEXT BLANK                
         BNZ   SQUASH2A                                                         
         LR    R2,R1               NO BLANK FOUND                               
         SR    R2,R3                                                            
         LR    R3,R1               BUMP TO NEXT BLOCK OF DATA                   
         SR    R4,R2               DECR RESIDUAL COUNT                          
         B     SQUASH2                                                          
SQUASH2A CR    R2,R9                                                            
         BH    SQUASH2C            WE STOPPED ON A BLANK CHR                    
         BL    SQUASH2B                                                         
         IC    R2,3(R1)            WE STOPPED ON ESC CHR WITH R2=LEN            
         SLL   R2,25                                                            
         SRL   R2,25                                                            
         CH    R2,=H'4'                                                         
         BNL   SQUASH2B            BUT WE MUST AVOID FUNNY LENGTHS              
         LA    R2,4                                                             
SQUASH2B AR    R2,R1                                                            
         SR    R2,R3                                                            
         AR    R3,R2               BUMP PAST ESCAPE SEQUENCE                    
         SR    R4,R2               DECR RESIDUAL COUNT                          
         B     SQUASH2                                                          
SQUASH2C LR    R2,R1               R1 POINTS TO THE FIRST BLANK                 
         SR    R2,R3                                                            
         LR    R3,R1               POINT TO STRING OF BLANKS                    
         SR    R4,R2               DECR RESIDUAL STRING LEN                     
         SPACE 1                                                                
SQUASH3  LR    RE,R3               PROCESS BLANK STRING AT R3                   
         LR    R0,R4               --------------------------                   
         SR    R5,R5               R5=LENGTH OF BLANK STRING                    
SQUASH3A LR    RF,R0                                                            
         CH    RF,=H'256'                                                       
         BNH   *+8                                                              
         LA    RF,256                                                           
         SH    RF,=H'1'            TEST END OF STRING                           
         BM    SQUASHA                                                          
         LA    R1,1(RE,RF)         POINT TO END OF STRING                       
         SR    R2,R2                                                            
         EX    RF,*+8                                                           
         B     *+10                                                             
         TRT   0(0,RE),TRTBLKS     SET R1 TO FIRST NONBLANK CHR                 
         BNZ   SQUASH3B                                                         
         LR    R2,R1               NO NONBLANK CHR FOUND                        
         SR    R2,RE                                                            
         AR    R5,R2               BUMP BLANKS COUNT                            
         CR    R5,R4               TEST END OF STRING                           
         BL    *+10                                                             
         AR    R7,R5               YES BUMP DELETED COUNT AND EXIT              
         B     SQUASHA                                                          
         AR    RE,R2               BUMP TO NEXT BLOCK OF BLANKS                 
         SR    R0,R2                                                            
         B     SQUASH3A                                                         
SQUASH3B LR    R2,R1               BLANKS TERMINATED BY NONBLANK CHR            
         SR    R2,RE                                                            
         AR    R5,R2               BUMP BLANKS COUNT                            
         CR    R3,R6               TEST BLANK STRING AT START                   
         BE    SQUASH5                                                          
         SH    R5,=H'1'            R5=NUM OF BLANKS TO REMOVE                   
         BP    SQUASH6                                                          
         BZ    SQUASH7                                                          
         DC    H'0'                                                             
         SPACE 1                                                                
SQUASH5  LR    R1,R4               REMOVE WHOLE STRING AT R3 - LEN=R5           
         SR    R1,R5                                                            
         LR    R0,R3               SET TO ADR                                   
         LA    RE,0(R3,R5)         SET FROM ADR AND LEN                         
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
         AR    R7,R5               BUMP NUMBER OF BLANKS REMOVED                
         SR    R4,R5               DECR RESIDUAL STRING LENGTH                  
         B     SQUASH2                                                          
         SPACE 1                                                                
SQUASH6  LR    R1,R4               SHIFT OUT BLANK STRING AT R3                 
         SR    R1,R5                                                            
         LA    R0,1(R3)            LEAVE ROOM FOR ONE BLANK                     
         LA    RE,1(R3,R5)         SET FROM ADR AND LEN                         
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
         AR    R7,R5               BUMP NUMBER OF BLANKS REMOVED                
         SPACE 1                                                                
SQUASH7  MVC   0(1,R3),BLANK       MOVE IN THE BLANK CHR                        
         LA    R3,1(R3)                                                         
         LA    R5,1(R5)                                                         
         SR    R4,R5               DECR RESIDUAL STRING LENGTH                  
         B     SQUASH2                                                          
         SPACE 1                                                                
SQUASHA  LTR   R7,R7               WERE ANY CHRS REMOVED                        
         BZ    SQUASHX                                                          
         SR    R8,R7               POINT TO NEW END OF STRING                   
         LR    R0,R8                                                            
         LR    R1,R7                                                            
         LA    RF,C' '             SET PAD CHR AND ZERO LEN SOURCE              
         SLL   RF,24                                                            
         MVCL  R0,RE               MOVE SPACES TO END OF STRING                 
         SPACE 1                                                                
SQUASHB  SR    R8,R6               RETURN NEW LENGTH OF STRING                  
         ST    R8,4(RA)                                                         
         SPACE 1                                                                
SQUASHX  XMOD1 1                                                                
         ORG   *-2                                                              
         BSM   0,RE                                                             
         EJECT                                                                  
         LTORG                                                                  
         SPACE 1                                                                
TRTDD    DC    XL16'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'  00-0F                    
         DC    XL16'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'  10-1F                    
         DC    XL16'03030404040401FF030304040404FFFF'  20-2F                    
         DC    XL16'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'  30-3F                    
         DC    XL16'FF000000000000000000000000000000'  40-4F                    
         DC    XL16'00000000000000000000000000000000'  50-5F                    
         DC    XL16'00000000000000000000000000000000'  60-6F                    
         DC    XL16'00000000000000000000000000000000'  70-7F                    
         DC    XL16'00000000000000000000000000000000'  80-8F                    
         DC    XL16'00000000000000000000000000000000'  90-9F                    
         DC    XL16'00000000000000000000000000000000'  A0-AF                    
         DC    XL16'00000000000000000000000000000000'  B0-BF                    
         DC    XL16'00000000000000000000000000000000'  C0-CF                    
         DC    XL16'00000000000000000000000000000000'  D0-DF                    
         DC    XL16'00000000000000000000000000000000'  E0-EF                    
         DC    XL16'00000000000000000000000000000000'  F0-FF                    
         SPACE 1                                                                
TRTBLKS  DC    XL16'00000000000000000000000000000000'  00-0F                    
         DC    XL16'00000000000000000000000000000000'  10-1F                    
         DC    XL16'03030404040401000303040404040000'  20-2F                    
         DC    XL16'00000000000000000000000000000000'  30-3F                    
         DC    XL16'00FFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'  40-4F                    
         DC    XL16'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'  50-5F                    
         DC    XL16'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'  60-6F                    
         DC    XL16'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'  70-7F                    
         DC    XL16'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'  80-8F                    
         DC    XL16'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'  90-9F                    
         DC    XL16'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'  A0-AF                    
         DC    XL16'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'  B0-BF                    
         DC    XL16'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'  C0-CF                    
         DC    XL16'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'  D0-DF                    
         DC    XL16'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'  E0-EF                    
         DC    XL16'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'  F0-FF                    
         SPACE 1                                                                
WORKD    DSECT                                                                  
BLANK    DS    C                                                                
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003DDSQUASHER07/15/09'                                      
         END                                                                    
