*          DATA SET ACPERCALL  AT LEVEL 008 AS OF 08/07/97                      
*CATALP PERCALL                                                                 
PERCALL  TITLE ' BUILD LOCATION TABLE FROM PERSON RECORD'                       
*********************************************************************           
*        THIS MODULE IS USED TO LOOKUP PERSON RECORD AND PASS       *           
*        BACK PERTINENT INFORMATION.                                *           
*                                                                   *           
*        YOU MUST THE ADDR OF A 270 BYTE BLOCK WHICH IS COVERED     *           
*        BY THE PERD DSECT(ACPERCALLD).                             *           
*        REQUIRED BLOCK INFO ---------   HEXCOMP AND PERSON CODE    *           
*                                        AND A(DATAMGR).            *           
*        OPTIONAL FILTERS    ---------1) OFF,DPT AND/OR SUB         *           
*                                     2) PERIOD YYMMDD YYMMDD       *           
*                                               YYMM   YYMM         *           
*                                               YYMMDD              *           
*                                                      YYMMDD       *           
*                                     3) FLAGS -OVERRIDE LOC END    *           
*                                               WITH TS LOCK DATE.  *           
*                                              -DO A GENERAL READ   *           
*                                               TO FIND A PERSON REC*           
*                                                                   *           
*         MODULE PASSES BACK HIRE AND TERM DATES FROM PERSON RECORD *           
*         AND A LOCATION TABLE WITH EACH OF THE PERSONS LOCATIONS.  *           
*                                                                   *           
*         PARM1  ADDR OF BLOCK                                      *           
*                                                                   *           
*   NOTE: AGENCY MUST BE ON NEW COST AND USING THE COST PRG         *           
*         IN ORDER TO HAVE PERSON RECORDS.                          *           
*                                                                   *           
*********************************************************************           
PERCALL  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,**PERS**,CLEAR=YES                                   
         USING WORKD,RC            RC=A(LOCAL WORKLNG STORAGE)                  
         LR    R9,R1                                                            
         USING PERD,R9             R9=A(PERSON CONTROL BLOCK)                   
         MVI   PERRTN,C' '         CLEAR THE RETURN FIELDS                      
         MVC   PERRTN+1(PERRTNL-1),PERRTN                                       
         MVI   PERERR,0                                                         
         OC    PERDMGR,PERDMGR     ADDR OF DATAMGR?                             
         BZ    ERR4                                                             
         MVC   DATAMGR,PERDMGR                                                  
                                                                                
***********************************************************************         
*        GOTO1 DATAMGR,DMCB,DMDTFA,ACCDIR                             *         
*        L     RE,12(R1)           RE=A(ACCDIR DTF)                   *         
*        ST    RE,ADTF             SAVE A(DIRECTORY DTF)              *         
*        L     RE,ISPDKEY-ISDTF(RE)                                   *         
*        LA    RF,*+10                                                *         
*        O     RF,=X'80000000'                                        *         
*        BSM   0,RF                                                   *         
*        MVC   SAVKEY,0(RE)        SAVE LAST KEY                      *         
*        LA    RF,*+10                                                *         
*        N     RF,=X'7FFFFFFF'                                        *         
*        BSM   0,RF                                                   *         
*        MVC   DATADISP,NEWDISP                                       *         
***********************************************************************         
         GOTO1 DATAMGR,DMCB,DMKEY,ACCDIR,SAVKEY                       *         
         EJECT                                                                  
***********************************************************************         
* CHECK IF REQUIRED DATA HAS BEEN PASSED CORRECTLY BY USER            *         
***********************************************************************         
         SPACE 1                                                                
         XC    PERLNUM,PERLNUM     CLEAR NUMBER OF LOCATIONS                    
         CLC   PERADACC,SPACES     ADDR OF 1R ACCOUNT RECORD?                   
         BNE   *+10                                                             
         XC    PERADACC,PERADACC                                                
         CLC   PERALEDG,SPACES     ADDR OF 1R LEDGER RECORD?                    
         BNE   *+10                                                             
         XC    PERALEDG,PERALEDG                                                
         OC    PERADACC,PERADACC   1R ACCOUNT RECORD PASSED?                    
         BZ    PER1                                                             
         L     R1,PERADACC                                                      
         MVC   PERCOMP,0(R1)       SAVE COMPANY                                 
         OC    PERALEDG,PERALEDG   THEN ADDR OF LEDGER MUST BE PASSED           
         BZ    ERR7                                                             
         B     PER1A                                                            
PER1     CLC   PERCOMP,SPACES      COMPANY CODE MUST BE PASSED                  
         BNH   ERR5                                                             
         TM    PERFLAGS,PERRECCK   GENERAL READ FOR PERSON RECS?                
         BO    PER1A               YES - THEN PERSON CODE NOT REQUIRED          
         CLC   PERAC,SPACES        NO  - PERSON CODE MUST BE PASSED             
         BNH   ERR5                                                             
PER1A    OC    PERAC,SPACES        SPACE PADDED                                 
         OC    PEROFF,SPACES                                                    
         OC    PERDPT,SPACES                                                    
         OC    PERSUB,SPACES                                                    
         CLC   PERENDD,SPACES      END DATE PASSED                              
         BE    PER2                                                             
         OC    PERENDD,PERENDD                                                  
         BNZ   *+10                                                             
PER2     MVC   PERENDD,EFFS        IF END DATE NOT PASSED, SET HIGH             
         CLI   PERENDD+2,0         IF YYMM PASSED ,ADD THE DAY                  
         BE    PER4                                                             
         CLI   PERENDD+2,SPACE                                                  
         BNE   *+8                                                              
PER4     MVI   PERENDD+2,31                                                     
         CLC   PERSTD,SPACES                                                    
         BNE   *+10                                                             
         XC    PERSTD,PERSTD       IF START NOT PASSED, SET LOW                 
         OC    PERSTD,PERSTD                                                    
         BZ    PER6                                                             
         CLI   PERSTD+2,0          IF YYMM PASSED ,ADD THE DAY                  
         BE    *+12                                                             
         CLI   PERSTD+2,SPACE                                                   
         BNE   *+8                                                              
         MVI   PERSTD+2,01                                                      
PER6     CLC   PERENDD,PERSTD                                                   
         BL    ERR6                ENDDATE CANNOT BE LOWER THAN START           
         EJECT                                                                  
***********************************************************************         
* BUILD KEY FROM 1R ACCOUNT AND 1R LEDGER RECORDS                     *         
***********************************************************************         
         SPACE 1                                                                
BLD      OC    PERADACC,PERADACC   1R ACCOUNT RECORD PASSED?                    
         BZ    GENCK                                                            
         L     R1,PERADACC                                                      
         MVC   PERCOMP,0(R1)       SAVE COMPANY                                 
         MVC   KEY,SPACES                                                       
         MVC   KEY(L'ACTKACT),3(R1)                                             
         L     RF,PERALEDG         1R LEDGER RECORD                             
         AH    RF,OLDDISP          PROBABLY EMULATED MONACC RECORD              
BLD02    CLI   0(RF),0                                                          
         BNE   *+6                                                              
         DC    H'0'                                                             
         CLI   0(RF),ACLELQ        ACCOUNT LENGTHS ELEMENT                      
         BE    BLD04                                                            
         SR    R0,R0                                                            
         IC    R0,1(RF)                                                         
         AR    RF,R0                                                            
         B     BLD02                                                            
         USING ACLELD,RF                                                        
BLD04    LA    R0,LLEVLNUM          MAX NUMBER OF LEVEL                         
         LA    R1,LLEVELS           LEVEL LENGTH SAVE AREA                      
         LA    RF,ACLVLEN           1ST LEVEL LENGTH IN ELEMENT                 
         MVC   0(L'ACLVLEN,R1),0(RF)                                            
         LA    RF,L'ACLVALS(RF)                                                 
         LA    R1,LLEVALN(R1)                                                   
         BCT   R0,*-14                                                          
         DROP  RF                                                               
         LA    RF,KEY                                                           
         SR    R1,R1                                                            
         IC    R1,LLEVA                                                         
         SH    R1,=H'1'                                                         
         EX    R1,*+4                                                           
         MVC   PEROFF(0),0(RF)      OFFICE CODE                                 
         OC    PEROFF,SPACES                                                    
         LA    RF,1(R1,RF)                                                      
*                                                                               
         SR    R1,R1                                                            
         IC    R1,LLEVB                                                         
         SR    R0,R0                                                            
         IC    R0,LLEVA                                                         
         SR    R1,R0                                                            
         SH    R1,=H'1'                                                         
         EX    R1,*+4                                                           
         MVC   PERDPT(0),0(RF)       DEPT CODE                                  
         OC    PERDPT,SPACES                                                    
         LA    RF,1(R1,RF)                                                      
*                                                                               
         SR    R1,R1                                                            
         IC    R1,LLEVC                                                         
         SR    R0,R0                                                            
         IC    R0,LLEVB                                                         
         SR    R1,R0                                                            
         SH    R1,=H'1'                                                         
         EX    R1,*+4                                                           
         MVC   PERSUB(0),0(RF)       SUBDEPT                                    
         OC    PERSUB,SPACES                                                    
         LA    RF,1(R1,RF)                                                      
*                                                                               
         SR    R1,R1                                                            
         IC    R1,LLEVD                                                         
         SR    R0,R0                                                            
         IC    R0,LLEVC                                                         
         SR    R1,R0                                                            
         SH    R1,=H'1'                                                         
         EX    R1,*+4                                                           
         MVC   PERAC(0),0(RF)        PERSON CODE                                
         OC    PERAC,SPACES                                                     
         LA    RF,1(R1,RF)                                                      
         EJECT                                                                  
***********************************************************************         
* GENERAL CHECK FOR PERSON RECORDS ON THIS FILE                       *         
***********************************************************************         
         SPACE 1                                                                
GENCK    TM    PERFLAGS,PERRECCK   GENERAL READ FOR PERSON RECS?                
         BZ    PER10                                                            
         USING PERRECD,R4                                                       
         MVC   KEY,SPACES          GO FIND IT                                   
         LA    R4,KEY                                                           
         MVI   PERKTYP,PERKTYPQ    PERSON RECORD TYPE                           
         MVC   PERKCPY,PERCOMP     COMPANY                                      
         LA    R2,IO                                                            
         GOTO1 HIGH                                                             
         CLC   PERKEY(PERKCODE-PERRECD),IO   DID YOU FIND IT?                   
         BE    *+12                                                             
         OI    PERERR,PERNOREC     NO - SET RETURN FLAG                         
         B     PERXX               RETURN TO USER                               
         CLC   PERAC,SPACES        YES - IF NO PERSON CODE RETURN               
         BNH   PERXX                                                            
         EJECT                                                                  
***********************************************************************         
* GET PERSON RECORD                                                   *         
***********************************************************************         
         SPACE 1                                                                
         USING PERRECD,R4                                                       
PER10    MVC   KEY,SPACES          GO FIND IT                                   
         LA    R4,KEY                                                           
         MVI   PERKTYP,PERKTYPQ    PERSON RECORD TYPE                           
         MVC   PERKCPY,PERCOMP     COMPANY                                      
         MVC   PERKCODE,PERAC      PERSON CODE                                  
         LA    R2,IO                                                            
         GOTO1 HIGH                                                             
         CLC   PERKEY,IO           DID YOU FIND IT?                             
         BE    *+12                                                             
         OI    PERERR,PERNOTER     PERSON RECORD NOT FOUND                      
         B     PERXX                                                            
         MVC   DA,IO+(PERKDA-PERRECD)                                           
         GOTO1 GET                                                              
         BNE   ERR2                DISK ERROR                                   
*                                                                               
         XC    PERLNUM,PERLNUM     CLEAR NUMBER OF LOCATIONS                    
         LR    R4,R2                                                            
         AH    R4,DATADISP         R4=(ADDR OF 1ST ELEMENT)                     
         USING EMPELD,R4                                                        
PER20    CLI   EMPEL,0             TEST END OF RECORD                           
         BE    PERXX                                                            
         CLI   EMPEL,EMPELQ        EMPLOYEE HISTORY ELEMENT                     
         BNE   PER20A                                                           
         MVC   PERHIR,EMPHIR                                                    
         MVC   PERTRM,EMPTRM       HIRE AND TERM DATES                          
         B     PER30                                                            
         USING LOCELD,R4                                                        
PER20A   CLI   LOCEL,LOCELQ        LOCATION ELEMENT                             
         BNE   PER30                                                            
         CLC   PEROFF,SPACES                                                    
         BNH   *+14                                                             
         CLC   LOCOFF,PEROFF       FILTER BY OFFICE                             
         BNE   PER30                                                            
         CLC   PERDPT,SPACES                                                    
         BNH   *+14                                                             
         CLC   LOCDEPT,PERDPT      FILTER BY DEPT                               
         BNE   PER30                                                            
         CLC   PERSUB,SPACES                                                    
         BNH   *+14                                                             
         CLC   LOCSUB,PERSUB       FILTER BY SUBDEPT                            
         BNE   PER30                                                            
         OC    LOCEND,LOCEND       CURRENT LOCATION WILL HAVE NO END            
         BNZ   *+10                                                             
         MVC   LOCEND,EFFS         SO SET IT HIGH                               
         LA    RF,LOCEND                                                        
         OC    LOCLOCK,LOCLOCK     IS THERE A TS LOCK DATE ?                    
         BZ    PER22               NO - DONT BOTHER TO SEE IF THEY WANT         
         TM    PERFLAGS,PEROVER    WANT TS LOCK TO OVERRIDE LOC END?            
         BNO   PER22               NO  - USE LOCATION END DATE                  
         LA    RF,LOCLOCK          YES - REPLACE LOC END WITH TSL               
PER22    CLC   0(L'LOCEND,RF),PERSTD  LOC ENDS BEFORE REQUESTED PERIOD          
         BL    PER30                                                            
         CLC   LOCSTART,PERENDD    LOC STARTS AFTER PERIOD ENDS                 
         BH    PER30                                                            
         SR    R3,R3                                                            
         IC    R3,PERLNUM          NUMBER OF LOCATIONS SO FAR                   
         MH    R3,=Y(PERLVLEN)     TIMES THE LENGTH OF A MINI ENTRY             
         LA    R2,PERLVALS         (R3) DISP TO NEXT AVAIL SLOT                 
         AR    R2,R3               (R2)NOW POINTS TO OPEN SLOT                  
         USING PERLVALS,R2                                                      
         MVC   PERLOF,LOCOFF       OFFICE                                       
         MVC   PERLDPT,LOCDEPT     DEPT                                         
         MVC   PERLSUB,LOCSUB      SUB DEPT                                     
         MVC   PERLSTD,LOCSTART    LOCATION DATES                               
         MVC   PERLENDD,LOCEND                                                  
         OC    LOCLOCK,LOCLOCK     IS THERE A TS LOCK DATE ?                    
         BZ    PER24               NO - SKIP                                    
         TM    PERFLAGS,PEROVER    WANT TS LOCK TO OVERRIDE LOC END?            
         BNO   PER24               NO  -                                        
         MVC   PERLENDD,LOCLOCK    YES -                                        
PER24    MVC   PERLATTR,LOCATTR    LOCATION ATTRIBUTES(EXEC=Y,..ETC)            
         MVC   PERLSTAT,LOCSTAT    LOCATION STATUS(ACTIVE,TERM,.ETC)            
         SR    R1,R1                                                            
         IC    R1,PERLNUM                                                       
         LA    R1,1(R1)            UPDATE LOCATION COUNTER                      
         STC   R1,PERLNUM                                                       
         CLI   PERLNUM,PERLMAX                                                  
         BNE   PER30                                                            
         OI    PERERR,PERMAXER     TOO MANY LOCATIONS                           
         B     PERXX                                                            
PER30    SR    R0,R0                                                            
         IC    R0,LOCLN            BUMP TO NEXT ELEMENT                         
         AR    R4,R0                                                            
         B     PER20                                                            
PERXX    DS    0H                                                               
         B     XITMOD                                                           
         EJECT                                                                  
***********************************************************************         
* DATA MANAGER INTERFACE                                              *         
***********************************************************************         
         SPACE 1                                                                
HIGH     LR    R0,RE               SAVE RETURN POINT                            
         GOTO1 DATAMGR,DMCB,DMRDHI,ACCDIR,KEY,IO                                
         LR    RE,R0                                                            
         BR    RE                                                               
         SPACE 1                                                                
GET      LR    R0,RE               SAVE RETURN POINT                            
         GOTO1 DATAMGR,DMCB,GETREC,ACCMST,DA,IO,WORK                            
         LR    RE,R0                                                            
         BR    RE                                                               
         SPACE 2                                                                
***********************************************************************         
* RESTORE LAST KEY READ FROM DIRECTORY FOR APPLICATION IF THIS        *         
* MODULE CHANGED THE READ SEQUENCE                                    *         
***********************************************************************         
         SPACE 1                                                                
***********************************************************************         
*ITMOD   L     RE,ADTF                                                *         
*        USING ISDTF,RE                                               *         
*        LH    R1,ISKEYLN1                                            *         
*        L     RE,ISPDKEY-ISDTF(RE)                                   *         
*        LA    RF,*+10                                                *         
*        O     RF,=X'80000000'                                        *         
*        BSM   0,RF                                                   *         
*        MVC   WORK,0(RE)          EXTRACT CURRENT KEY                *         
*        LA    RF,*+10                                                *         
*        N     RF,=X'7FFFFFFF'                                        *         
*        BSM   0,RF                                                   *         
*        EX    R1,*+8                                                 *         
*        BZ    XITMOD2                                                *         
*        OC    SAVKEY(0),SAVKEY    TEST ANY PREVIOUS DIRECTORY I/O    *         
*        EX    R1,*+8                                                 *         
*        BE    XITMOD2                                                *         
*        CLC   SAVKEY(0),WORK      TEST DIRECTORY I/O EXECUTED        *         
*        GOTO1 DATAMGR,DMCB,DMREAD,ACCDIR,SAVKEY,IO                   *         
*        BNE   ERR2                                                   *         
***********************************************************************         
                                                                                
XITMOD   OC    SAVKEY(L'ACCKEY),SAVKEY                                          
         BZ    XITMOD2                                                          
         GOTO1 DATAMGR,DMCB,DMKEY,ACCDIR,WORK                                   
         CLC   SAVKEY(L'ACCKEY),WORK                                            
         BE    XITMOD2                                                          
         GOTO1 (RF),(R1),(X'08',DMREAD),ACCDIR,SAVKEY,SAVKEY                    
*                                                                               
XITMOD2  DS    0H                                                               
         SPACE 2                                                                
XIT      XIT1  ,                                                                
         EJECT                                                                  
***********************************************************************         
* DATA CONSTANTS                                                      *         
***********************************************************************         
         SPACE 1                                                                
ERR2     DC    H'0',C'DISK ERROR'                                               
ERR3     DC    H'0',C'EL NOT FOUND'                                             
ERR4     DC    H'0',C'MISSING DMGR'                                             
ERR5     DC    H'0',C'MISSING KEY'                                              
ERR6     DC    H'0',C'BAD DATES'                                                
ERR7     DC    H'0',C'MISSING ADDR'                                             
         SPACE 2                                                                
*MDTFA   DC    C'DTFAD  '                                                       
DMKEY    DC    C'DMKEY  '                                                       
DMRDHI   DC    C'DMRDHI '                                                       
DMREAD   DC    C'DMREAD '                                                       
GETREC   DC    C'GETREC '                                                       
ACCDIR   DC    C'ACCDIR '                                                       
ACCMST   DC    C'ACCMST '                                                       
EFFS     DC    4X'FF'                                                           
SPACES   DC    CL64' '                                                          
         SPACE 2                                                                
DATADISP DC    Y(ACTRFST-ACTRECD)                                               
**WDISP  DC    Y(ACTRFST-ACTRECD)                                               
OLDDISP  DC    H'49'                                                            
         EJECT                                                                  
***********************************************************************         
* LTORG AND EQUATES                                                   *         
***********************************************************************         
SPACE    EQU   X'40'                                                            
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* DSECT FOR LOCAL WORKING STORAGE                                     *         
***********************************************************************         
         SPACE 1                                                                
WORKD    DSECT                     ** WORKING STORAGE DSECT **                  
*DTF     DS    A                   A(ACCDIR DTF)                                
DATAMGR  DS    A                   A(DATAMGR)                                   
DMCB     DS    6F                                                               
WORK     DS    XL64                                                             
*ATADISP DS    H                   DISPLACEMENT TO FIRST ELEMENT                
SAVKEY   DS    XL64                SAVED DIRECTORY KEY AREA                     
*                                                                               
LLEVELS  EQU   *                                                                
LLEVA    DS    CL(L'ACLVLEN)       1R LEV A LENGTH                              
LLEVALN  EQU   *-LLEVELS                                                        
LLEVB    DS    CL(L'ACLVLEN)       1R LEV B LENGTH (A+B)                        
LLEVC    DS    CL(L'ACLVLEN)       1R LEV C LENGTH (A+B+C)                      
LLEVD    DS    CL(L'ACLVLEN)       1R LEV D LENGTH (A+B+C+D)                    
LLEVELLN EQU   *-LLEVELS                                                        
LLEVLNUM EQU   LLEVELLN/LLEVALN                                                 
*                                                                               
*                                                                               
DA       DS    XL4                 DISK ADDRESS                                 
KEY      DS    XL64                KEY AREA                                     
IO       DS    2000X               I/O AREA                                     
WORKX    EQU   *                                                                
         EJECT                                                                  
       ++INCLUDE ACPERCALLD                                                     
         EJECT                                                                  
* ACGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* DMDTFIS                                                                       
         PRINT OFF                                                              
       ++INCLUDE DMDTFIS                                                        
         PRINT ON                                                               
         SPACE 1                                                                
* DMDTFPH                                                                       
         PRINT OFF                                                              
       ++INCLUDE DMDTFPH                                                        
         PRINT ON                                                               
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'008ACPERCALL 08/07/97'                                      
         END                                                                    
