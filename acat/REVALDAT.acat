*          DATA SET REVALDAT   AT LEVEL 018 AS OF 05/01/02                      
*CATALP REVALDAT                                                                
         PRINT NOGEN                                                            
         TITLE 'EDIT DATE STRING AND TRANSFORM TO 8 BYTE BIT PATTERN'           
REVALDAT CSECT                                                                  
         NMOD1 WORKX-WORKD,REVALDAT,CLEAR=YES                                   
         USING WORKD,RC                                                         
         SPACE                                                                  
INIT     ST    R1,VPARAM           SAVE A(PARAM LIST)                           
         LM    R2,R5,0(R1)         P1=A(FACILS)                                 
*                                  P2=A(RCONDATE)                               
*                                  P3=A(FIELD HEADER)                           
*                                  P4=A(OUTPUT AREA)                            
         CLI   5(R4),0             TEST FOR NO INPUT IN FIELD                   
         BNE   INIT1                                                            
         MVI   FERN,MISINP                                                      
         B     ERROR                                                            
         SPACE                                                                  
INIT1    STM   R2,R5,VFACILS       SAVE PARAMS                                  
         L     R9,16(R2)           A(COMFACS)                                   
         USING COMFACSD,R9                                                      
         ST    R9,VCOMFACS                                                      
         SPACE                                                                  
INIT2    MVC   VSCANNER,CSCANNER   BUILD LIST OF ROUTINE ADDRESSES              
         MVC   VDATVAL,CDATVAL                                                  
         MVC   VDATCON,CDATCON                                                  
         MVC   VADDAY,CADDAY                                                    
         MVC   VGETDAY,CGETDAY                                                  
         DROP  R9                                                               
         SPACE                                                                  
* CONVERT CONTRACT START AND END DATES TO 6 BYTE EBCDIC - YYMMDD                
*                                                                               
INIT4    L     R8,VCONDATE                                                      
         GOTO1 VDATCON,DMCB,(3,(R8)),(0,STDAT)                                  
         GOTO1 (RF),(R1),(3,3(R8)),(0,ENDAT)                                    
         SPACE                                                                  
* FIND MONDAY OF CONTRACT START WEEK                                            
*                                                                               
FINDMON  GOTO1 VGETDAY,DMCB,STDAT,DMCB+8                                        
         CLI   DMCB,MONDAY                                                      
         BE    FINDMON2            START DATE IS ALREADY MONDAY                 
         SPACE                                                                  
         ZIC   R5,DMCB             DAY NUMBER                                   
         BCTR  R5,0                                                             
         LNR   R5,R5               CHANGE SIGN TO BACK DATE UP                  
         GOTO1 VADDAY,DMCB,STDAT,MONDAT,(R5)                                    
         B     BLDTAB                                                           
         SPACE                                                                  
FINDMON2 MVC   MONDAT,STDAT        SET FIRST MONDAY                             
         EJECT                                                                  
* BUILD CONTRACT DATE TABLE - MONDAY,SUNDAY,WEEK NUMBER                         
*                                                                               
BLDTAB   LA    R2,TABLE                                                         
         USING TABENTD,R2                                                       
         LA    R3,63               START WEEK NUMBERING WITH 63 AND             
         MVC   MON,MONDAT          COUNT DOWN TO ZERO                           
         SPACE                                                                  
BLDTAB2  GOTO1 VADDAY,DMCB,MON,SUN,6                                            
         STC   R3,WKNUM                                                         
         SPACE                                                                  
         GOTO1 (RF),(R1),SUN,WRKDAT,1                                           
         CLC   WRKDAT,ENDAT        IS NEXT MONDAY PAST CONTRACT END             
         BH    BLDTAB4             YES-TABLE FINISHED                           
         LA    R2,LTABENT(R2)      POINT TO NEXT ENTRY                          
         MVC   MON,WRKDAT          SET NEXT MONDAY                              
         BCT   R3,BLDTAB2          DECREMENT WEEK NUMBER                        
         SPACE                                                                  
BLDTAB4  STC   R3,LASTWK           SAVE LAST WEEK                               
         DROP  R2                                                               
         EJECT                                                                  
* CONVENTIONS FOR REST OF CODE ARE AS FOLLOWS                                   
* R2 POINTS TO SCANNER BLOCK                                                    
* R3 IS COUNTER FOR NUMBER OF BLOCKS                                            
* R4 + R5 ARE USED TO CONSTRUCT MASKS BY SHIFTING                               
* R6 + R7 CONTAIN FIRST AND SECOND FIELD WEEK NUMBERS RESPECTIVELY              
* R8 POINTS TO DATE TABLE                                                       
* R9 COUNTS NUMBER OF ACTIVE WEEKS                                              
* WEEKS NUMBERED IN REVERSE SEQUENCE FROM 63                                    
         SPACE 2                                                                
DOSCAN   LA    R2,WORK                                                          
         GOTO1 VSCANNER,DMCB,VFLDHDR,(R2),C',=*-'                               
         SPACE                                                                  
         ZIC   R3,DMCB+4                                                        
         LA    R8,TABLE                                                         
         USING TABENTD,R8                                                       
         SR    R9,R9                                                            
         SPACE                                                                  
* VALIDATE AND ANALYZE SCANNER FIELDS                                           
*                                                                               
SCAN     CLI   0(R2),1             L'FIRST FIELD = 1                            
         BNE   FRSTDAT             NO                                           
         SPACE                                                                  
         CLI   12(R2),C'S'         CHECK FOR S INPUT                            
         BNE   *+12                                                             
         CLI   1(R2),0             'S' ALONE IS NOT VALID INPUT                 
         BNE   FRSTST                                                           
         SPACE                                                                  
         MVI   FERN,INVERR                                                      
         B     ERROR                                                            
         SPACE                                                                  
FRSTST   CLC   MON+2(4),MONDAT+2   CHECK THAT 'S' HAS BEEN INPUT IN             
         BE    FSTMASK             SEQUENCE                                     
         SPACE                                                                  
         MVI   FERN,SEQOROUT                                                    
         B     ERROR                                                            
         SPACE                                                                  
FRSTDAT  GOTO1 VDATVAL,DMCB,(1,12(R2)),WRKDAT                                   
         OC    DMCB(4),DMCB        IS DATE INVALID                              
         BNZ   FRSTDAT2            NO                                           
         SPACE                                                                  
         MVI   FERN,INVDAT                                                      
         B     ERROR                                                            
         SPACE                                                                  
FRSTDAT2 EQU   *                   CHECK DATE VS. TABLE FOR SEQUENCE            
         CLC   MON(2),SUN          DOES WEEK CROSS YEARS                        
         BE    FRSTDAT4            NO                                           
         SPACE                                                                  
         CLC   WRKDAT+2(4),SUN+2                                                
         BH    *+8                                                              
         B     FSTMASK             ITS THIS WEEK                                
         CLC   WRKDAT+2(4),MON+2                                                
         BL    FRSTDAT5            TRY NEXT WEEK                                
         B     FSTMASK                                                          
         SPACE                                                                  
FRSTDAT4 CLC   WRKDAT+2(4),MON+2                                                
         BL    FRSTDAT5            LESS THAN MONDAY-ERROR                       
         CLC   WRKDAT+2(4),SUN+2                                                
         BH    FRSTDAT5                                                         
         B     FSTMASK             SEQUENCE CHECKS AND ENTRY FOUND              
         SPACE                                                                  
FRSTDAT5 CLC   WKNUM,LASTWK                                                     
         BE    FRSTDATR            END OF TABLE REACHED-ERROR                   
         LA    R8,LTABENT(R8)      POINT TO NEXT ENTRY                          
         B     FRSTDAT2                                                         
         SPACE                                                                  
FRSTDATR MVI   FERN,SEQOROUT                                                    
         B     ERROR                                                            
         SPACE                                                                  
FSTMASK  ZIC   R6,WKNUM                                                         
         CLI   1(R2),0             TEST FOR SECOND FIELD DATA                   
         BE    *+8                 NONE                                         
         B     SCAN2               CONTINUE SCAN ANALYSIS                       
         SPACE                                                                  
         LM    R4,R5,WEEKMASK                                                   
         SLDL  R4,0(R6)            SHIFT BIT TO WEEK POSITION                   
         O     R4,RESULT           TURN ON BIT                                  
         O     R5,RESULT+4                                                      
         STM   R4,R5,RESULT        RESTORE BIT PATTERN                          
         LA    R9,1(R9)            INCREMENT ACTIVE WEEKS COUNTER               
         B     RESET                                                            
         EJECT                                                                  
SCAN2    LA    RF,22(R2)           POINT RF TO SECOND FIELD DATA                
         ZIC   RE,1(R2)            L'SECOND FIELD                               
         LR    R1,RE               SAVE LENGTH                                  
         AR    RE,RF                                                            
         BCTR  RE,0                POINT RE TO LAST BYTE                        
         CLI   0(RE),C'A'          TEST FOR ALTERNATION SUFFIX                  
         BE    *+8                                                              
         B     END                                                              
         SPACE                                                                  
         MVI   ALTFLAG,YES                                                      
         MVI   0(RE),SPACE         ERASE A                                      
         BCTR  R1,0                ADJUST SECOND FIELD LENGTH                   
         BCTR  RE,0                POINT RE TO NEW LAST BYTE                    
         STC   R1,1(R2)                                                         
         SPACE                                                                  
END      CLI   1(R2),1             IF SECOND FIELD LENGTH IS 1,                 
         BNE   SECWK               INPUT MUST BE 'E'                            
         SPACE                                                                  
         CLI   22(R2),C'E'                                                      
         BE    END2                                                             
         MVI   FERN,INVERR                                                      
         B     ERROR                                                            
         SPACE                                                                  
END2     ZIC   R7,LASTWK                                                        
         CH    R3,=H'1'            DOES ANY INPUT FOLLOW 'E'                    
         BE    LSTMASK             NO-TURN ON BITS                              
         MVI   FERN,SEQOROUT                                                    
         B     ERROR                                                            
         SPACE                                                                  
SECWK    CLI   0(RE),C'W'          LOOK FOR 'W' AT END                          
         BE    SECWK2              IT'S THERE                                   
         B     SECDAT              CHECK IF INPUT IS A DATE                     
         SPACE                                                                  
SECWK2   ZIC   R1,1(R2)                                                         
         CH    R1,=H'3'            ONLY TWO DIGITS MAY PRECEDE 'W'              
         BH    *+8                                                              
         B     SECWK4                                                           
         SPACE                                                                  
         MVI   FERN,TOOLONG                                                     
         B     ERROR                                                            
         SPACE                                                                  
SECWK4   BCTR  R1,0                SUBTRACT 1 FOR W                             
         BCTR  R1,0                AND 1 FOR EX LENGTH                          
         MVC   DUB(2),=XL2'F0F0'                                                
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVZ   DUB(0),22(R2)                                                    
         CLC   DUB(2),=XL2'F0F0'                                                
         BL    *+8                 BYTES PRECEDING W ARE NON-NUMERIC            
         B     SECWK6                                                           
         SPACE                                                                  
         MVI   FERN,NONNUM                                                      
         B     ERROR                                                            
         SPACE                                                                  
SECWK6   EX    R1,*+8              PACK NUMBER OF WEEKS                         
         B     *+10                                                             
         PACK  DUB,22(0,R2)                                                     
         CVB   RF,DUB                                                           
         LTR   RF,RF                                                            
         BZ    SECWKR              ZERO IS NOT VALID INPUT                      
         CLI   ALTFLAG,YES         FOR ALTERNATE WEEKS AND NWA                  
         BNE   *+10                FORMAT,                                      
         SLL   RF,1                MULTIPLY N BY 2 AND SUBTRACT 1               
         BCTR  RF,0                TO GIVE TOTAL LENGTH OF ALT PATRN.           
         BCTR  RF,0                                                             
         LR    R7,R6               FIRST WEEK                                   
         SR    R7,RF               LAST WEEK                                    
         CLM   R7,1,LASTWK         CANNOT GO PAST LAST WK IN CONTRACT           
         BH    SECWK8                                                           
         BL    SECWKR              PAST LAST WEEK - ERROR                       
         SPACE                                                                  
         CH    R3,=H'1'            IF LAST WEEK REACHED, NO MORE                
         BE    SECWK7              INPUT IS ALLOWED ON LINE                     
         SPACE                                                                  
SECWKR   MVI   FERN,INVERR                                                      
         B     ERROR                                                            
         SPACE                                                                  
SECWK7   B     LSTMASK                                                          
         SPACE                                                                  
* UPDATE TABLE POINTER                                                          
*                                                                               
SECWK8   CLM   R7,1,WKNUM          LAST WEEK VS. CURRENT TABLE WK               
         BL    *+8                                                              
         B     SECWK9                                                           
         LA    R8,LTABENT(R8)      BUMP DOWN TABLE FOR A MATCH                  
         B     SECWK8                                                           
         SPACE                                                                  
SECWK9   B     MULTMASK                                                         
         SPACE                                                                  
SECDAT   GOTO1 VDATVAL,DMCB,(1,22(R2)),WRKDAT                                   
         OC    DMCB(4),DMCB        IS DATE VALID                                
         BNZ   SECDAT2             YES                                          
         SPACE                                                                  
         MVI   FERN,INVDAT         NO                                           
         B     ERROR                                                            
         SPACE                                                                  
* CHECK DATE AGAINST TABLE FOR SEQUENTIALITY                                    
*                                                                               
SECDAT2  CLC   MON(2),SUN          IS WEEK IN SAME YEAR                         
         BE    SECDAT4                                                          
         SPACE                                                                  
         CLC   WRKDAT+2(4),SUN+2                                                
         BH    *+8                                                              
         B     SECDAT6                                                          
         CLC   WRKDAT+2(4),MON+2                                                
         BL    SECDAT5                                                          
         B     SECDAT6                                                          
         SPACE                                                                  
SECDAT4  CLC   WRKDAT+2(4),MON+2                                                
         BL    SECDAT5                                                          
         CLC   WRKDAT+2(4),SUN+2                                                
         BH    SECDAT5             TRY AGAIN                                    
         B     SECDAT6             FOUND ENTRY                                  
         SPACE                                                                  
SECDAT5  CLC   WKNUM,LASTWK                                                     
         BH    *+8                                                              
         B     SECDATR             DATE OUTSIDE TABLE RANGE                     
         LA    R8,LTABENT(R8)      BUMP DOWN TO NEXT ENTRY                      
         B     SECDAT2                                                          
         SPACE                                                                  
SECDATR  MVI   FERN,SEQOROUT                                                    
         B     ERROR                                                            
         SPACE                                                                  
SECDAT6  ZIC   R7,WKNUM                                                         
         CLC   WKNUM,LASTWK                                                     
         BH    MULTMASK                                                         
         B     LSTMASK                                                          
         EJECT                                                                  
MULTMASK BAS   RE,TURNBITS                                                      
         B     RESET                                                            
         SPACE                                                                  
LSTMASK  BAS   RE,TURNBITS                                                      
         B     EXIT                                                             
         SPACE                                                                  
RESET    LA    R8,LTABENT(R8)      POINT TO NEXT TABLE ENTRY                    
         LA    R2,32(R2)           POINT TO NEXT SCANNER BLOCK                  
         BCT   R3,SCAN                                                          
         SPACE                                                                  
EXIT     L     R1,VOUTPUT                                                       
         MVC   0(L'RESULT,R1),RESULT                                            
         L     R1,VPARAM                                                        
         XC    0(4,R1),0(R1)       SUCCESSFUL PROCESSING                        
         STC   R9,4(R1)            PASS BACK NUMBER OF ACTIVE WEEKS             
         B     EXXMOD                                                           
         SPACE                                                                  
ERROR    L     R1,VPARAM                                                        
         MVC   0(1,R1),FERN                                                     
         SPACE                                                                  
EXXMOD   XMOD1 1                                                                
         EJECT                                                                  
* SUB-ROUTINE TO TURN WEEK BITS ON FOR 2 OR MORE WEEKS                          
*                                                                               
TURNBITS ST    RE,FULL                                                          
         LR    R1,R6                                                            
         SR    R1,R7               SUBTRACT TO FIND NUMBER OF WEEKS             
         LA    R1,1(R1)            ADD ONE TO GIVE EXACT TOTAL                  
         CLI   ALTFLAG,YES                                                      
         BE    TURN1                                                            
         LM    R4,R5,ENDMASK                                                    
         B     TURN2                                                            
         SPACE                                                                  
TURN1    LM    R4,R5,ALTMASK                                                    
         SPACE                                                                  
         STC   R6,BYTE             IF NUMBER OF FIRST WEEK IS EVEN,             
         TM    BYTE,X'01'          THE MASK MUST BE SHIFTED TO ALIGN            
         BO    *+8                                                              
         SRDL  R4,1                AN ON WITH THE FIRST WEEK                    
         SPACE                                                                  
         STC   R1,BYTE                                                          
         TM    BYTE,X'01'          IS NUMBER OF WEEKS ODD                       
         BZ    *+8                 NO                                           
         LA    R1,1(R1)            YES-ADD 1 TO PREVENT ROUNDING DOWN           
         SRL   R1,1                DIVIDE BY TWO                                
         MVI   ALTFLAG,NO                                                       
         SPACE                                                                  
TURN2    LA    RF,63                                                            
         SR    RF,R6               SUBTRACT 1ST WK FROM 63 FOR L'SHIFT          
         SLDL  R4,0(RF)            ZERO BITS TO LEFT OF FIRST WEEK BIT          
         SRDL  R4,0(RF)            AND RETURN TO FIRST WEEK POSITION.           
         SRDL  R4,0(R7)            ZERO BITS TO RIGHT OF LAST WEEK BIT          
         SLDL  R4,0(R7)            AND RETURN TO LAST WEEK POSITION.            
         O     R4,RESULT           USE THE MASK JUST CONSTRUCTED TO             
         O     R5,RESULT+4         TURN ON THE PROPER BITS.                     
         STM   R4,R5,RESULT        RESTORE BIT PATTERN TO CORE                  
         AR    R9,R1               INCREMENT WEEKS COUNTER                      
         L     RE,FULL                                                          
         BR    RE                                                               
         EJECT                                                                  
         LTORG                                                                  
         SPACE                                                                  
         DS    0D                                                               
WEEKMASK DC    7X'00',X'01'                                                     
ENDMASK  DC    8X'FF'                                                           
ALTMASK  DC    8X'AA'                                                           
         SPACE                                                                  
* EQUATES                                                                       
*                                                                               
YES      EQU   C'Y'                                                             
NO       EQU   C'N'                                                             
SPACE    EQU   C' '                                                             
MONDAY   EQU   X'01'                                                            
MISINP   EQU   1                   MISSING INPUT                                
INVERR   EQU   2                   INVALID INPUT                                
NONNUM   EQU   3                   NON-NUMERIC INPUT                            
INVDAT   EQU   13                  INVALID DATE                                 
TOOLONG  EQU   28                  INPUT IS TOO LONG                            
SEQOROUT EQU   132                 SEQUENCE ERROR OR DATE OUTSIDE TABLE         
         SPACE                                                                  
         SPACE                                                                  
* DSECT TO COVER TABLE ENTRIES                                                  
*                                                                               
TABENTD  DSECT                                                                  
MON      DS    CL6                 YYMMDD EBCDIC                                
SUN      DS    CL6                                                              
WKNUM    DS    C                                                                
LTABENT  EQU   (*-MON)                                                          
         SPACE                                                                  
* DSECT TO COVER WORKING STORAGE                                                
*                                                                               
WORKD    DSECT                                                                  
VPARAM   DS    V                                                                
VFACILS  DS    V                                                                
VCONDATE DS    V                                                                
VFLDHDR  DS    V                                                                
VOUTPUT  DS    V                                                                
         SPACE                                                                  
VCOMFACS DS    V                                                                
         SPACE                                                                  
VSCANNER DS    V                                                                
VDATVAL  DS    V                                                                
VDATCON  DS    V                                                                
VADDAY   DS    V                                                                
VGETDAY  DS    V                                                                
         SPACE                                                                  
RESULT   DS    D                                                                
DUB      DS    D                                                                
         SPACE                                                                  
FULL     DS    F                                                                
DMCB     DS    6F                                                               
         SPACE                                                                  
STDAT    DS    CL6                                                              
ENDAT    DS    CL6                                                              
MONDAT   DS    CL6                                                              
WRKDAT   DS    CL6                                                              
         SPACE                                                                  
LASTWK   DS    C                                                                
ALTFLAG  DS    C                                                                
FERN     DS    C                                                                
BYTE     DS    C                                                                
         SPACE                                                                  
TABLE    DS    64CL13                                                           
         DS    0F                                                               
WORK     DS    CL320                                                            
WORKX    EQU   *                                                                
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'018REVALDAT  05/01/02'                                      
         END                                                                    
