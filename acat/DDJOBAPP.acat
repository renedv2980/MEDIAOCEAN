*          DATA SET DDJOBAPP   AT LEVEL 003 AS OF 05/12/11                      
*CATALP JOBAPP                                                                  
         TITLE 'ADD/REMOVE JOB TO/FROM APPROVER RECORD'                         
***********************************************************************         
* P1: 0   = ACTION (A,R) - SOMETIME ACTSCLOS IS NOT SET               *         
*     1-3 = A(ACCMST/ACCFIL RECORD)                                   *         
*                                                                     *         
* P2:     = A(COMFACS)                                                *         
*                                                                     *         
* ON EXIT                                                             *         
* P1: 0   = 0  SUCCESS OR ERROR INDICATOR                             *         
*         = 1  INVALID JOB CODE                                       *         
*         = 2  TOO MANY ITEMS IN APPROVER                             *         
*         = 3  DATAMGR ERROR                                          *         
*                                                                     *         
* P2: 0   =    DATAMGR ERROR CONDITIONS                               *         
***********************************************************************         
         SPACE 2                                                                
*YNGX 002 06JUL07 <LO01-6397> ENABLE OFFICE LEVEL APPROVAL                      
         SPACE 2                                                                
JOBAPP   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,**JOBAPP,CLEAR=YES                                   
         USING WORKD,RC            RC=A(W/S)                                    
*                                                                               
         ST    R1,CALLR1                                                        
         MVC   ACTION,0(R1)                                                     
         MVI   RETCODE,OKQ                                                      
         MVI   DMERROR,0                                                        
*                                                                               
         MVI   SPACES,C' '                                                      
         MVC   SPACES+1(L'SPACES-1),SPACES                                      
*                                                                               
         L     RF,0(R1)                                                         
         USING ACTRECD,RF                                                       
         CLC   =C'SJ',ACTKUNT                                                   
         BE    *+12                                                             
         MVI   RETCODE,INVJOBQ                                                  
         B     JOBXIT              EXIT - NOT SJ ACCOUNT                        
         MVC   JOBCODE,ACTKACT     SAVE JOB CODE                                
         MVC   COMPANY,ACTKCPY     SAVE COMPANY CODE                            
         MVC   SACTRST1,ACTRSTA    SAVE RECORD STATUS 1                         
         DROP  RF                                                               
*                                                                               
         USING COMFACSD,R4                                                      
         L     R4,4(R1)                                                         
         ST    R4,ACOM                                                          
         MVC   VDATAMGR,CDATAMGR                                                
         MVC   VHELLO,CHELLO                                                    
*                                                                               
         L     RF,CCALLOV                                                       
         GOTO1 (RF),DMCB,0,(C'R',X'00000A9F')                                   
         ICM   RF,15,0(R1)                                                      
         BNZ   *+6                                                              
         DC    H'0'                                                             
         ST    RF,VPADDLE          A(PADDLE)                                    
*                                                                               
         L     RF,CGETFACT                                                      
         GOTO1 (RF),DMCB,0,0                                                    
         L     RF,0(R1)                                                         
         MVC   SECALPHA,FATAGYSC-FACTSD(RF)                                     
         MVC   PIDNUM,FAPASSWD-FACTSD(RF)                                       
         DROP  R4                                                               
*                                                                               
         USING SA0REC,R4                                                        
         LA    R4,KEY                                                           
         XC    KEY,KEY                                                          
         MVI   SA0KTYP,SA0KTYPQ    C'0' - PERSONAL AUTH. RECORD                 
         MVC   SA0KAGY,SECALPHA    SECURITY ALPHA ID                            
         MVC   SA0KNUM,PIDNUM                                                   
         GOTO1 VDATAMGR,DMCB,DMREAD,CTFILE,KEY,IO                               
         MVC   DMERROR,8(R1)                                                    
         CLI   DMERROR,0                                                        
         BNE   JOBXIT              EXIT - CAN'T FIND THE PID                    
*                                                                               
         LA    R4,IO                                                            
         LA    R4,SA0DATA                                                       
         USING SAPALD,R4                                                        
         SR    R0,R0                                                            
JOBAPPA  CLI   SAPALEL,0                                                        
         BE    JOBXIT              EXIT - CAN'T FIND THE PID                    
         CLI   SAPALEL,SAPALELQ                                                 
         BE    *+14                                                             
         IC    R0,SAPALLN                                                       
         AR    R4,R0                                                            
         B     JOBAPPA                                                          
         MVC   SCURPID,SAPALPID                                                 
         DROP  R4                                                               
         SPACE 2                                                                
***********************************************************************         
* CHECK ERROR, VALIDATE ACTION AND GO TO ROUTINES                     *         
***********************************************************************         
         SPACE 1                                                                
JOB00    CLC   =C'DDS     ',SCURPID                                             
         BE    JOBXIT              EXIT - IF DDS USER                           
         CLC   JOBCODE,SPACES                                                   
         BH    *+12                                                             
         MVI   RETCODE,INVJOBQ                                                  
         B     JOBXIT              EXIT - INVALID JOB CODE                      
*                                                                               
         LA    R2,0                                                             
         CLI   ACTION,C'A'         TEST ADD JOB TO APPROVER RECORD              
         BE    JOB04                                                            
         CLI   ACTION,C'R'         TEST REMOVE JOB FROM APPROVER RECORD         
         BE    *+12                                                             
         TM    SACTRST1,ACTSDELT+ACTSCLOS  JOB DELETED/CLOSED?                  
         BZ    *+8                                                              
         LA    R2,4                YES - REMOVE JOB FROM APPROVER               
*                                                                               
JOB04    B     JOBTAB(R2)                                                       
*                                                                               
JOBTAB   B     JOBADD                                                           
         B     JOBREM                                                           
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO ADD JOB CODE TO APPROVER RECORD                          *         
***********************************************************************         
         SPACE 1                                                                
         USING APPRECD,R4          ADD APPROVER RECORD                          
JOBADD   LA    R4,KEY                                                           
         XC    KEY,KEY                                                          
         MVI   APPKTYP,APPKTYPQ    APPROVER RECORD TYPE                         
         MVI   APPKSUB,APPKSUBQ    AND SUBTYPE                                  
         MVC   APPKCPY,COMPANY     CONNECTED USER                               
         MVC   APPKPIDB,PIDNUM     BINARY PERSON PID                            
*                                                                               
         MVC   KEYSAVE,KEY         SAVE KEY                                     
         GOTO1 VDATAMGR,DMCB,(X'08',DMREAD),ACCDIR,KEY,KEY                      
         MVC   DMERROR,8(R1)                                                    
         TM    DMERROR,X'6D'       TEST SERIOUS ERRORS                          
         BZ    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         MVC   SVAPPKDA,APPKDA     SAVE APPROVER RECORD DISK ADDRESS            
         TM    DMERROR,X'90'       TEST RECORD NO FOUND                         
         BNZ   JOBAD08                                                          
         TM    DMERROR,X'20'       TEST RECORD IS DELETED                       
         BZ    JOBAD04             NO - OK                                      
*                                                                               
         GOTO1 VDATAMGR,DMCB,(X'88',DMREAD),ACCDIR,KEY,KEY                      
         MVC   DMERROR,8(R1)                                                    
         CLI   DMERROR,0                                                        
         BE    *+12                                                             
         MVI   RETCODE,DMRERRQ                                                  
         B     JOBXIT              EXIT - DATAMGR ERROR                         
                                                                                
         NI    APPKSTAT,X'FF'-APPSDELT                                          
         GOTO1 VDATAMGR,DMCB,DMWRT,ACCDIR,KEY,KEY                               
         MVC   DMERROR,8(R1)                                                    
         CLI   DMERROR,0                                                        
         BE    *+12                                                             
         MVI   RETCODE,DMRERRQ                                                  
         B     JOBXIT              EXIT - DATAMGR ERROR                         
*                                                                               
JOBAD04  GOTO1 VDATAMGR,DMCB,(X'88',GETREC),ACCMST,APPKDA,IO,DMWORK             
         MVC   DMERROR,8(R1)                                                    
         CLI   DMERROR,0                                                        
         BE    *+12                                                             
         MVI   RETCODE,DMRERRQ                                                  
         B     JOBXIT              EXIT - DATAMGR ERROR                         
*                                                                               
         MVI   FOUND,C'Y'                                                       
         BAS   RE,GETAPL           GET APPROVER LIMIT                           
*                                                                               
T        USING CPTRBLK,CPTRWRK                                                  
         XC    T.CPTRBLK(CPTRBLKL),T.CPTRBLK  DELETE PASSIVES                   
         GOTO1 VPADDLE,DMCB,(C'D',IO),(C'K',T.CPTRBLK),0,0,ACOM                 
         B     JOBAD12                                                          
         DROP  T                                                                
*                                                                               
JOBAD08  MVC   KEY,KEYSAVE         RESTORE KEY                                  
         MVI   FOUND,C'N'                                                       
         MVC   IO(L'KEY),KEY                                                    
         LA    R4,IO                                                            
         LA    RE,APPRFST                                                       
         MVI   0(RE),0                                                          
         SR    RE,R4                                                            
         AHI   RE,1                                                             
         STCM  RE,3,APPRLEN        SET LENGTH OF APPROVER RECORD                
*                                                                               
JOBAD12  BAS   RE,BLDCPJ           BUILD CLI/PRO/JOB ELEMENT                    
         BE    *+12                                                             
         MVI   RETCODE,TOOITMQ                                                  
         B     JOBXIT              TOO MANY ITEMS IN THE APPROVER               
*                                                                               
         CLI   FOUND,C'Y'                                                       
         BE    *+8                                                              
         BAS   RE,BLDAPL           BUILD APPROVER LIMIT ELEMENT                 
*                                                                               
         LA    R4,IO                                                            
         NI    APPRSTAT,X'FF'-APPSDELT                                          
         GOTO1 VDATAMGR,DMCB,ADDREC,ACCMST,KEY,IO,DMWORK                        
         ORG   *-2                                                              
         CLI   FOUND,C'N'                                                       
         BE    JOBAD14                                                          
         LA    R5,PUTREC           ISSUE PUTREC IN REFRESH MODE                 
         ST    R5,0(R1)                                                         
         LA    R5,SVAPPKDA         DISK ADDRESS FOR PUTREC                      
         ST    R5,8(R1)                                                         
JOBAD14  BASR  RE,RF               CALL DATAMGR                                 
         MVC   DMERROR,8(R1)                                                    
         CLI   DMERROR,0                                                        
         BE    *+12                                                             
         MVI   RETCODE,DMRERRQ                                                  
         B     JOBXIT              EXIT - DATAMGR ERROR                         
*                                                                               
         CLI   FOUND,C'N'          ADD A NEW RECORD                             
         BNE   *+10                                                             
         MVC   SVAPPKDA,KEY        YES - SAVE NEW DISK ADDRESS FOR P3           
*                                                                               
T        USING CPTRBLK,CPTRWRK     ADD APPROVER PASSIVE POINTERS                
         XC    T.CPTRBLK(CPTRBLKL),T.CPTRBLK                                    
         GOTO1 VPADDLE,DMCB,(C'A',IO),T.CPTRBLK,SVAPPKDA,0,ACOM                 
         DROP  T,R4                                                             
*                                                                               
JOBAD40  B     JOBXIT                                                           
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO REMOVE JOB CODE FROM APPROVER RECORD                     *         
***********************************************************************         
         SPACE 1                                                                
         USING JOBPASD,R4          JOB PASSIVE POINTER RECORD                   
JOBREM   LA    R4,KEY                                                           
         LA    R3,APPLTAB                                                       
JOBRE02  XC    KEY,KEY                                                          
         XC    LPIDNUM,LPIDNUM                                                  
         MVI   JOBPTYP,JOBPTYPQ    APPROVER RECORD TYPE                         
         MVI   JOBPSUB,JOBPSUBQ    AND SUBTYPE                                  
         MVC   JOBPAPPL,0(R3)                                                   
         MVC   JOBPCPY,COMPANY     CONNECTED USER                               
         MVC   JOBPJOB,JOBCODE                                                  
         MVC   JOBPOFFC,SPACES                                                  
         MVC   JOBPMED,SPACES                                                   
         MVC   KEYSAVE,KEY         SAVE PASSIVE POINTER KEY                     
         B     JOBRE06                                                          
*                                                                               
JOBRE04  MVC   KEY,KEYSAVE         RESTORE KEY                                  
         MVC   JOBPPIDB,LPIDNUM    RESTORE LAST PIDNUM                          
         MVI   JOBPSEQ,X'FF'                                                    
JOBRE06  GOTO1 VDATAMGR,DMCB,(X'80',DMRDHI),ACCDIR,KEY,KEY                      
         MVC   DMERROR,8(R1)                                                    
         TM    DMERROR,X'6D'       TEST SERIOUS ERRORS                          
         BZ    JOBRE08                                                          
         DC    H'0'                                                             
*                                                                               
JOBRE08  CLC   KEYSAVE(JOBPPIDB-JOBPASD),KEY                                    
         BNE   JOBRE50             CAN'T FIND LAST PERSON PID                   
*                                                                               
         MVC   LPIDNUM,JOBPPIDB    SAVE THE PID                                 
         OI    JOBPSTAT,JOBPDELT   DELETE THE PASSIVE POINTER                   
         GOTO1 VDATAMGR,DMCB,DMWRT,ACCDIR,KEY,KEY                               
         MVC   DMERROR,8(R1)                                                    
         CLI   DMERROR,0                                                        
         BE    *+12                                                             
         MVI   RETCODE,DMRERRQ                                                  
         B     JOBXIT              EXIT - DATAMGR ERROR                         
         DROP  R4                                                               
                                                                                
         USING APPRECD,R4          APPROVER RECORD                              
         LA    R4,KEY                                                           
         XC    KEY,KEY                                                          
         MVI   APPKTYP,APPKTYPQ    APPROVER RECORD TYPE                         
         MVI   APPKSUB,APPKSUBQ    AND SUBTYPE                                  
         MVC   APPKCPY,COMPANY     CONNECTED USER                               
         MVC   APPKPIDB,LPIDNUM    LAST BINARY PERSON PID                       
*                                                                               
         GOTO1 VDATAMGR,DMCB,DMREAD,ACCDIR,KEY,KEY                              
         MVC   DMERROR,8(R1)                                                    
         CLI   DMERROR,0                                                        
         BNE   JOBRE04             RECORD NO FOUND - TRY NEXT                   
*                                                                               
         MVC   SVAPPKDA,APPKDA                                                  
         GOTO1 VDATAMGR,DMCB,(X'80',GETREC),ACCMST,APPKDA,IO,DMWORK             
         MVC   DMERROR,8(R1)                                                    
         CLI   DMERROR,0                                                        
         BE    *+12                                                             
         MVI   RETCODE,DMRERRQ                                                  
         B     JOBXIT              EXIT - DATAMGR ERROR                         
*                                                                               
         MVC   WORK,SPACES                                                      
         LA    R6,IO               APPROVER RECORD                              
         LA    R6,APPRFST-APPRECD(R6)                                           
         USING LIDELD,R6                                                        
JOBRE10  CLI   LIDEL,0                                                          
         BE    JOBRE04             EOR - TRY NEXT                               
         CLI   LIDEL,LIDELQ                                                     
         BNE   JOBRE14                                                          
         CLI   LIDTYPE,LIDTAPSJ                                                 
         BE    JOBRE18                                                          
JOBRE14  SR    R0,R0                                                            
         IC    R0,LIDLN                                                         
         AR    R6,R0                                                            
         B     JOBRE10                                                          
*                                                                               
JOBRE18  SR    RE,RE                                                            
         IC    RE,LIDITLN          LENGTH OF ITEMS                              
         CHI   RE,L'LIDASTAT+L'LIDASTA2+L'LIDASJAC+L'LIDASJOF+L'LIDASJM+        
               E                                                                
         BE    *+6                                                              
         DC    H'0'                BAD LIDTCPJ ELEMENT                          
*                                                                               
         SR    R0,R0                                                            
         IC    R0,LIDLN            LENGTH OF ELEMENT                            
         SHI   R0,LIDDATA-LIDELD                                                
         SRDL  R0,32                                                            
         DR    R0,RE                                                            
*                                                                               
         LA    RF,LIDDATA                                                       
JOBRE22  MVC   WORK(L'JOBCODE),2(RF)                                            
         OI    WORK,X'40'          MAKE SURE IT'S UPPPER CASE                   
         CLC   JOBCODE,WORK                                                     
         BE    JOBRE30             FOUND THE JOB - REMOVE IT                    
         AR    RF,RE               CHECK NEXT ITEM                              
         BCT   R1,JOBRE22                                                       
         B     JOBRE14             GET NEXT ELEMENT                             
*                                                                               
JOBRE30  XC    ELEMENT,ELEMENT     REMOVE THE JOB FROM LIDTCPJ                  
         LR    R5,RF                                                            
         SR    R5,R6                                                            
         SHI   R5,1                                                             
         EXMVC R5,ELEMENT,0(R6)    COPY THE ELEMENT                             
         AHI   R5,1                                                             
         LA    R5,ELEMENT(R5)      POINT TO THE END                             
         AR    RF,RE               SKIP THE JOB WE WANT TO REMOVE               
         B     JOBRE38                                                          
JOBRE34  MVC   0(LIDSJLNQ,R5),0(RF)                                             
         AR    R5,RE                                                            
         AR    RF,RE                                                            
JOBRE38  BCT   R1,JOBRE34          COPY NEXT JOB                                
*                                                                               
         MVI   0(R6),X'FF'                                                      
         GOTO1 VHELLO,DMCB,(C'D',ACCMST),(X'FF',IO),0                           
*                                                                               
         LA    R6,ELEMENT                                                       
         SR    RF,RF                                                            
         IC    RF,LIDLN                                                         
         SHI   RF,LIDSJLNQ                                                      
         STC   RF,LIDLN                                                         
*                                                                               
         CLI   LIDLN,LIDDATA-LIDELD                                             
         BNH   JOBRE40                                                          
                                                                                
         GOTO1 VHELLO,DMCB,(C'P',ACCMST),IO,ELEMENT                             
*                                                                               
JOBRE40  GOTO1 VDATAMGR,DMCB,PUTREC,ACCMST,SVAPPKDA,IO,DMWORK                   
         MVC   DMERROR,8(R1)                                                    
         CLI   DMERROR,0                                                        
         BE    JOBRE04             TRY NEXT                                     
         MVI   RETCODE,DMRERRQ                                                  
         B     JOBXIT              EXIT - DATAMGR ERROR                         
         DROP  R4,R6                                                            
JOBRE50  LA    R3,1(R3)            HAVE WE REACHED END                          
         CLI   0(R3),X'FF'                                                      
         BNE   JOBRE02                                                          
         B     JOBXIT                                                           
                                                                                
APPLTAB  DS    0X                                                               
         DC    AL1(JOBPATIM)                                                    
         DC    AL1(JOBPAEXP)                                                    
         DC    AL1(JOBPAEST)                                                    
         DC    AL1(JOBPAORD)                                                    
         DC    X'FF'                                                            
         EJECT ,                                                                
***********************************************************************         
* BUILD CLI/PRO/JOB LIST ELEMENT                                      *         
***********************************************************************         
         SPACE                                                                  
         USING LIDELD,R6                                                        
BLDCPJ   NTR1                                                                   
         MVC   WORK,SPACES                                                      
         MVI   TOTCPJ#,0                                                        
*                                                                               
         LA    R6,IO               APPROVER RECORD                              
         LA    R6,APPRFST-APPRECD(R6)                                           
         SR    R5,R5               R5=SAVE LAST LIDTCPJ                         
BLDCPJ10 CLI   LIDEL,0                                                          
         BE    BLDCPJ30                                                         
         CLI   LIDEL,LIDELQ                                                     
         BNE   BLDCPJ14                                                         
         CLI   LIDTYPE,LIDTAPSJ                                                 
         BE    BLDCPJ18                                                         
BLDCPJ14 SR    R0,R0                                                            
         IC    R0,LIDLN                                                         
         AR    R6,R0                                                            
         B     BLDCPJ10                                                         
*                                                                               
BLDCPJ18 SR    RE,RE                                                            
         IC    RE,LIDITLN          LENGTH OF ITEMS                              
         CHI   RE,L'LIDASTAT+L'LIDASTA2+L'LIDASJAC+L'LIDASJOF+L'LIDASJM+        
               E                                                                
         BE    *+6                                                              
         DC    H'0'                BAD LIDTCPJ ELEMENT                          
*                                                                               
         SR    R0,R0                                                            
         IC    R0,LIDLN            LENGTH OF ELEMENT                            
         SHI   R0,LIDDATA-LIDELD                                                
         SRDL  R0,32                                                            
         DR    R0,RE               R1=NUMBER OF ITEMS                           
*                                                                               
         SR    RF,RF                                                            
         IC    RF,TOTCPJ#                                                       
         AR    RF,R1                                                            
         STC   RF,TOTCPJ#                                                       
         CHI   RF,MAXITMQ          MAX ITEMS IN THE LIDTCPJ LIST                
         BNL   BLDCPJN             TOO MANY ITEMS                               
                                                                                
         LA    RF,LIDDATA                                                       
BLDCPJ22 MVC   WORK(L'JOBCODE),1(RF)                                            
         OI    WORK,X'40'          MAKE SURE IT'S UPPPER CASE                   
         CLC   JOBCODE,WORK                                                     
         BE    BLDCPJOK                                                         
         AR    RF,RE               CHECK NEXT ITEM                              
         BCT   R1,BLDCPJ22                                                      
         LR    R5,R6               SAVE LAST LIDTCPJ ELEMENT                    
         B     BLDCPJ14            GET NEXT ELEMENT                             
*                                                                               
BLDCPJ30 XC    ELEMENT,ELEMENT                                                  
         LA    R6,ELEMENT                                                       
         LTR   R5,R5               R5=LAST SAVE LIDTCPJ ELEMENT                 
         BZ    BLDCPJ34            NO LIDTCPJ ELEMENT FOUND                     
         SR    RF,RF                                                            
         IC    RF,1(R5)            LENGTH OF LIDTCPJ                            
         CHI   RF,240                                                           
         BH    BLDCPJ34            ADD A NEW ONE IF NO SPACE                    
         SHI   RF,1                                                             
         EXMVC RF,ELEMENT,0(R5)                                                 
         MVI   0(R5),X'FF'                                                      
         GOTO1 VHELLO,DMCB,(C'D',ACCMST),(X'FF',IO),0                           
         B     BLDCPJ40                                                         
*                                                                               
BLDCPJ34 MVI   LIDEL,LIDELQ                                                     
         MVI   LIDLN,LIDDATA-LIDELD                                             
         MVI   LIDITLN,LIDSJLNQ                                                 
         MVI   LIDTYPE,LIDTAPSJ                                                 
*                                                                               
BLDCPJ40 SR    RF,RF                                                            
         IC    RF,LIDLN                                                         
         LA    RE,ELEMENT(RF)                                                   
         MVI   0(RE),LIDATIME+LIDAEXPN+LIDAESTD+LIDAORDD                        
         MVC   2(L'JOBCODE,RE),JOBCODE  NEW JOB CODE                            
         AHI   RF,LIDSJLNQ                                                      
         STC   RF,LIDLN                                                         
*                                                                               
         GOTO1 VHELLO,DMCB,(C'P',ACCMST),IO,ELEMENT                             
         B     BLDCPJOK                                                         
*                                                                               
BLDCPJOK CR    RB,RB               CC EQ - JOB ADDED TO APPROVER REC.           
         B     XIT                                                              
BLDCPJN  LTR   RB,RB               CC NEQ - TOO MANY ITEMS IN THE LIST          
         B     XIT                                                              
         DROP  R6                                                               
         EJECT ,                                                                
***********************************************************************         
* BUILD APPROVER LIMIT ELEMENT                                        *         
* EXIT - SAPPPVAL = APPROVER LIMIT                                    *         
***********************************************************************         
         SPACE                                                                  
         USING APLELD,R6                                                        
BLDAPL   NTR1                                                                   
         XC    ELEMENT,ELEMENT                                                  
         LA    R6,ELEMENT                                                       
         MVI   APLEL,APLELQ                                                     
         MVI   APLLN,APLLN1Q                                                    
         ZAP   APLVAL,PZERO                                                     
         MVC   SAPPPVAL,APLVAL                                                  
                                                                                
         GOTO1 VHELLO,DMCB,(C'P',ACCMST),IO,APLELD                              
         B     XIT                                                              
         DROP  R6                                                               
         EJECT ,                                                                
***********************************************************************         
* GET APPROVER LIST                                                   *         
* EXIT - SAPPPVAL = APPROVER LIMIT                                    *         
***********************************************************************         
         SPACE 1                                                                
GETAPL   NTR1                                                                   
         GOTO1 VHELLO,DMCB,(C'G',ACCMST),('APLELQ',IO),0                        
         CLI   12(R1),0                                                         
         BE    *+14                                                             
         ZAP   SAPPPVAL,PZERO                                                   
         B     XIT                 APLEL NOT FOUND!!!!!                         
*                                                                               
         L     RF,12(R1)                                                        
         MVC   SAPPPVAL,APLVAL-APLELD(RF)                                       
         B     XIT                                                              
*                                                                               
JOBXIT   L     R1,CALLR1                                                        
         MVC   0(1,R1),RETCODE                                                  
         MVC   4(1,R1),DMERROR                                                  
XIT      XIT1  ,                                                                
         EJECT ,                                                                
***********************************************************************         
* LITERALS AND CONSTANTS                                              *         
***********************************************************************         
         SPACE 2                                                                
         LTORG                                                                  
         SPACE 2                                                                
OKQ      EQU   X'00'               OK RETURN                                    
INVJOBQ  EQU   X'01'               INVALID JOB CODE                             
TOOITMQ  EQU   X'02'               TOO MANY ITEMS IN THE LIST                   
DMRERRQ  EQU   X'03'               DATAMGR ERROR                                
*                                                                               
MAXITMQ  EQU   80                                                               
*                                                                               
PZERO    DC    P'0'                                                             
DMADD    DC    C'DMADD   '                                                      
DMRDHI   DC    C'DMRDHI  '                                                      
DMREAD   DC    C'DMREAD  '                                                      
DMRSEQ   DC    C'DMRSEQ  '                                                      
DMWRT    DC    C'DMWRT   '                                                      
ADDREC   DC    C'ADDREC  '                                                      
GETREC   DC    C'GETREC  '                                                      
PUTREC   DC    C'PUTREC  '                                                      
ACCDIR   DC    C'ACCDIR  '                                                      
ACCMST   DC    C'ACCMST  '                                                      
CTFILE   DC    C'CTFILE  '                                                      
         EJECT                                                                  
         SPACE 2                                                                
WORKD    DSECT                                                                  
CALLR1   DS    A                                                                
DMCB     DS    6F                                                               
DMWORK   DS    12D                                                              
*                                                                               
ACOM     DS    A                                                                
VDATAMGR DS    V                                                                
VHELLO   DS    V                                                                
VPADDLE  DS    V                                                                
*                                                                               
SPACES   DS    CL132                                                            
*                                                                               
COMPANY  DS    XL1                                                              
JOBCODE  DS    CL12                                                             
SACTRST1 DS    XL1                                                              
SVAPPKDA DS    XL(L'APPKDA)                                                     
LPIDNUM  DS    XL(L'JOBPPIDB)                                                   
SAPPPVAL DS    PL6                 SAVED APPROVAL VALUE                         
SCURPID  DS    CL(L'SAPALPID)      SAVED CONNECTED PERSONAL-ID                  
SECALPHA DS    CL(L'F@TAGY)        AGENCY ALPHA                                 
PIDNUM   DS    CL(L'F@TUSER)       TWO BYTE USER ID                             
*                                                                               
DMERROR  DS    XL1                                                              
RETCODE  DS    XL1                                                              
ACTION   DS    CL1                                                              
FOUND    DS    CL1                                                              
TOTCPJ#  DS    XL1                                                              
WORK     DS    CL64                                                             
ELEMENT  DS    CL255                                                            
CPTRWRK  DS    CL200                                                            
*                                                                               
KEYSAVE  DS    XL64                                                             
KEY      DS    XL64                                                             
IO       DS    2000C                                                            
WORKX    EQU   *                                                                
         SPACE 2                                                                
* ACGENFILE                                                                     
* DDCOMFACS                                                                     
* CTGENFILE                                                                     
* SEACSFILE                                                                     
* FAFACTS                                                                       
* ACLDCPTRD                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
       ++INCLUDE DDCOMFACS                                                      
       ++INCLUDE CTGENFILE                                                      
       ++INCLUDE SEACSFILE                                                      
       ++INCLUDE FAFACTS                                                        
       ++INCLUDE ACLDCPTRD                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003DDJOBAPP  05/12/11'                                      
         END                                                                    
