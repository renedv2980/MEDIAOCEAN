*          DATA SET REDEMLKU   AT LEVEL 163 AS OF 04/22/09                      
*CATALP REDEMLKU                                                                
         TITLE 'REDEMLK - REP SYSTEM TIME PERIOD DEMO LOOK-UP'                  
*                                                                               
**********************************************************************          
*                                                                    *          
*        REDEMLKU - HANDLE UPGRADES FOR REP TIME PERIOD DATA         *          
* ------------------------------------------------------------------ *          
*                                                                    *          
* P1  =   A(LOOK-UP BLOCK)                                           *          
*                                                                    *          
* ------------------------------------------------------------------ *          
* UPDATE HISTORY:                                                    *          
* APR22/09 (SKU) --- SUPPORT NEW INVENTORY KEY                       *          
*                                                                    *          
* SEP01/90 (MRR) --- UPGRADE FOR 1 DECIMAL DEMOS                     *          
*                                                                    *          
* OCT04/90 (MRR) --- USE PASSED OFORMAT ADDR                         *          
*                                                                    *          
* JUN18/91 (MRR) --- EXTENDED DBLOCK WAS BUILT NOT SO CORRECTLY      *          
*                                                                    *          
* JUN21/91 (BU ) --- MADE L(DEXTRA1) AN EXPLICIT 128, RATHER THAN    *          
*                    (L'DBEXTRAD), WHICH IS A DSECT NAME GENERATING  *          
*                    A LENGTH OF 1, WHICH WAS KILLING TITLES         *          
*                                                                    *          
* OCT21/96 (MTA) --- PROCES UPGRADES FOR TIME PERIOD                 *          
*                                                                    *          
**********************************************************************          
*                                                                               
REDEMLKU CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,REDEMLK,RR=RE,CLEAR=YES                              
         USING WORKD,RC            RC=A(W/S)                                    
         ST    RE,RELO                                                          
         L     R9,0(R1)                                                         
         USING REDLBLKD,R9         R9=A(LOOK-UP BLOCK)                          
         XC    REDLABK,REDLABK                                                  
         MVI   REDLDBER,0          RESET ERROR BYTE                             
         MVC   VDEFINE,REDLADFN                                                 
         MVC   VGETIUN,REDLIUN                                                  
         L     RF,REDLAFAC                                                      
         USING COMFACSD,RF         RF=A(COMFACS)                                
         MVC   VDEMAND,CDEMAND                                                  
         MVC   VDEMOUT,CDEMOUT                                                  
         MVC   VDEMAINT,CDEMAINT                                                
         L     RF,CCALLOV                                                       
         LA    R1,DMCB                                                          
         MVC   DMCB+4(4),=X'D9000A08'                                           
         GOTO1 (RF),(R1),0                                                      
         MVC   VREDEMUP,DMCB       SAVE MODULE ADDRESS                          
*                                                                               
         LA    R8,DBLOCK1                                                       
         USING DBLOCKD,R8                                                       
         XC    DBLOCK,DBLOCK       BUILD DBLOCK FOR DEMO LOOK-UP                
         MVC   OFORMAT,OINVFMT                                                  
         MVI   DBTAPEP,C'Y'                                                     
         TM    REDLOPT,X'40'       WHICH INV PRECISION?                         
         BZ    *+14                                                             
         MVI   DBTAPEP,C'Y'        IMP BASED                                    
         MVC   OFORMAT,OINVFMT2                                                 
*                                                                               
         LA    RF,DEXTRA1                                                       
         STCM  RF,15,DBEXTEND                                                   
         USING DBXTTID,RF                                                       
         MVC   DBXTID(4),=C'SPOT'  SET TO SPOT PRECISION (WHY????)              
         MVI   DBXTTRP,X'01'                                                    
         MVI   DBXTTSP,X'01'                                                    
         MVI   DBXTTIP,X'02'                                                    
         DROP  RF                                                               
*                                                                               
         CLI   REDLMOD,REDLRD      CALL DEMND TO READ DEMO RECDS                
         BE    DEMRD                                                            
         CLI   REDLMOD,REDLPRC     TAKE DEMO RECD AND CNV TO IUN FMT            
         BE    DEMFMT                                                           
         DC    H'0'                                                             
*                                                                               
LKX      DS    0H                                                               
XIT      XIT1  ,                   EXIT FROM MODULE                             
         EJECT                                                                  
*********************************************************************           
*DEMFMT - CONVERT RECD PTD TO BY DBAQUART INTO IUN FORMAT                       
* EXTRACT SHARES FROM SOURCE RECD.  BUILD INV RECD FROM IUN DEMOS.              
* APPLY UPGRADE AND THEN PULL REQUESTED DEMOS FROM RECD.                        
**********************************************************************          
DEMFMT   DS    0H                                                               
         ICM   R1,15,REDLDBLK         COPY DBLOCK                               
         MVC   DBLOCK,0(R1)                                                     
         LA    R1,DEXTRA1                                                       
         STCM  R1,15,DBEXTEND                                                   
         MVC   DBFACTOR,=H'1'                                                   
         MVC   DUB,DBACTBK                                                      
         LA    R2,DEMOSHR                                                       
         CLI   DBSELMED,C'N'                                                    
         BNE   *+8                                                              
         LA    R2,NETSHR                                                        
         GOTO1 VDEMOUT,DMCB,(C'P',(R2)),DBLOCKD,OLDSHR                          
         MVC   DBACTBK,DUB                                                      
         CLI   DBSELMED,C'U'       ALREADY DEALING W/UPGRADE RECD               
         BNE   DEMF30                                                           
         LA    R0,IOAREA1                                                       
         L     RE,DBAREC                                                        
         SR    R1,R1                                                            
         ICM   R1,3,RINVLEN-RINVREC(RE)                                         
         LR    RF,R1                                                            
         MVCL  R0,RE               MOVE RECD INTO MY IOAREA1                    
         LA    R1,IOAREA1                                                       
         ST    R1,DBAREC                                                        
         LA    RE,RINVPEL-RINVREC(R1)                                           
         ST    RE,DBAQUART                                                      
         XC    DEXTRA1,DEXTRA1                                                  
         MVC   DEXTRA1(4),=C'RINV'                                              
         B     DEMF40                                                           
*                                                                               
DEMF30   GOTO1 VGETIUN,DMCB,(4,DBLOCK),OLDUNV                                   
         MVC   NEWRTG(LENVALS),OLDRTG                                           
         MVC   NEWIMP(LENVALS),OLDIMP                                           
         MVC   NEWHPT(LENVALS),OLDHPT                                           
         MVC   NEWTOT(LENVALS),OLDTOT                                           
         BAS   RE,BLDIUN           BUILD IUN RECD AND DBLOCK                    
*                                                                               
DEMF40   OC    REDUPGEL,REDUPGEL   PROCESS UPGRADE?                             
         BZ    *+8                                                              
         BAS   RE,DEMUPGD                                                       
*                                                                               
DEMF50   MVI   DBTAPEP,C'N'                                                     
         TM    REDLOPT,X'40'       IMP BASED OFORMAT?                           
         BZ    *+8                                                              
         MVI   DBTAPEP,C'Y'                                                     
         GOTO1 VDEMOUT,DMCB,(C'L',REDLALST),DBLOCKD,REDLAVAL                    
         CLI   DBERROR,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
DEMFMTX  MVC   REDLDBER,DBERROR    RETURN TO CALLER                             
         B     LKX                                                              
         EJECT                                                                  
**********************************************************************          
*DEMRD -  ROUTINE TO CALL DEMND TO READ DEMO RECORDS                            
**********************************************************************          
DEMRD    DS    0H                                                               
         LA    R0,IOAREA1                                                       
         ST    R0,DBAREC                                                        
         MVC   DBCOMFCS,REDLAFAC                                                
         MVC   DBSELSRC,REDLSRC                                                 
         MVC   DBSELBK,REDLDBK                                                  
         MVC   DBBTYPE,REDLBTY                                                  
         MVC   DBSELSTA,REDLSTA                                                 
         MVC   DBSELAGY,REDLREP                                                 
         MVC   DBSELDAY,REDLDAY                                                 
         MVC   DBSELTIM,REDLTIM                                                 
         MVC   DBSELMED,REDLMED                                                 
         MVI   DBFUNCT,DBGETDEM    DEFAULT IS TIME PERIOD                       
         MVC   DBFILE,REDLFIL                                                   
*                                                                               
         B     DEMRD05                                                          
*                                                                               
         CLI   DBSELMED,C'N'       FOR NETWORK FILES ONLY:                      
         BNE   DEMRD05                                                          
         CLI   DBFILE,C'P'                                                      
         BNE   *+8                                                              
         MVI   DBFUNCT,DBGETNTI    GET PROGRAM INFO                             
         LA    R1,NETFILES                                                      
DEMRD04  CLI   0(R1),X'FF'         EOF                                          
         BE    DEMRDX              UNKWOWN SOURCE                               
         CLC   DBSELSRC,0(R1)                                                   
         BE    *+12                                                             
         LA    R1,L'NETFILES(R1)                                                
         B     DEMRD04                                                          
         MVC   DBFILE,4(R1)        SET PROPER DBFILE                            
         MVI   DBSELSRC,C'N'       SET SOURCE TO NETWORK                        
*                                                                               
DEMRD05  MVI   DBTAPEP,C'N'                                                     
         TM    REDLOPT,X'40'                                                    
         BZ    *+8                                                              
         MVI   DBTAPEP,C'Y'                                                     
*                                                                               
         GOTO1 VDEMAND,DMCB,DBLOCK,DEMLKHK                                      
         OC    DBDIVSOR,DBDIVSOR   TEST DEMOS FOUND                             
         BZ    DEMRDX                                                           
         CLI   DBERROR,X'80'       TEST FOR E-O-F                               
         BNE   DEMRDX                                                           
         LA    R0,3                UNWEIGHT VUTS                                
         LA    R1,OLDVUT                                                        
         SR    R2,R2                                                            
         BAS   RE,UNWGHT                                                        
         TM    REDLOPT,X'40'       IMP BASED?                                   
         BZ    DEMRD10                                                          
         L     RE,UNIVS                                                         
         L     RF,OLDVUT                                                        
         MR    RE,RE                                                            
         ST    RF,OLDVUT                                                        
         L     RE,UNIVS+4                                                       
         L     RF,OLDVUT+4                                                      
         MR    RE,RE                                                            
         ST    RF,OLDVUT+4                                                      
         L     RE,UNIVS+8                                                       
         L     RF,OLDVUT+8                                                      
         MR    RE,RE                                                            
         ST    RF,OLDVUT+8                                                      
*                                                                               
DEMRD10  LA    R0,3                UNWEIGHT SHRS                                
         LA    R1,OLDSHR                                                        
         SR    R2,R2                                                            
         BAS   RE,UNWGHT                                                        
         XC    OLDRTG(LENVALS*2),OLDRTG                                         
         XC    OLDHPT(LENVALS*2),OLDHPT                                         
         LA    R0,NUMVALS*4        UNWEIGHT DEMOS IN IUNWORK                    
         LA    R1,NEWRTG             FROM RTGS->TOTS STORE IN NEW...            
         LA    R2,OLDRTG                                                        
         BAS   RE,UNWGHT                                                        
*                                                                               
         LA    RF,3                RF=N'HOMES SHARE VALUES                      
         SR    RE,RE               RE=INDEX REGISTER                            
DEMRD20  TM    REDLOPT,X'40'       IMP BASED?                                   
         BO    DEMRD25                                                          
         L     R1,OLDRTG+HOMES(RE) HOMES SHARE=RATING/VUT                       
         M     R0,=F'1000'         RATING + PUT LINES ARE 1 DECIMAL             
         L     R2,OLDVUT(RE)                                                    
         LTR   R2,R2               TEST FOR ZERO PUT                            
         BNZ   *+10                                                             
         SR    R1,R1               YES-SET SHARE TO ZERO                        
         B     *+14                                                             
         DR    R0,R2                                                            
         ST    R1,HOMSHR(RE)                                                    
         LA    RE,4(RE)                                                         
         BCT   RF,DEMRD20                                                       
*                                                                               
DEMRD25  OC    REDUPGEL,REDUPGEL   UPGRADE REQUESTED?                           
         BZ    *+10                                                             
         MVC   HOMSHR(12),OLDSHR   YES, REPLACE SHRS W/ORIG SHRS                
         MVC   REDLPROG,STPROG       RETURN PROGRAM NAMES TO CALLER             
         CLC   REDLPROG,NDPROG                                                  
         BE    *+14                                                             
         MVI   REDLPROG+7,C'/'                                                  
         MVC   REDLPROG+8(7),NDPROG                                             
*                                                                               
         BAS   RE,BLDIUN           BUILD IUN RECD                               
         OC    REDUPGEL,REDUPGEL     UPGRADE REQUESTED?                         
         BZ    *+8                                                              
         BAS   RE,DEMUPGD                                                       
         TM    REDLOPT,X'40'                                                    
         BZ    *+8                                                              
         MVI   DBTAPEP,C'Y'                                                     
         GOTO1 VDEMOUT,DMCB,(C'L',REDLALST),DBLOCK,REDLAVAL                     
         CLI   DBERROR,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
DEMRDX   MVC   REDLDBER,DBERROR    RETURN TO CALLER                             
         B     LKX                                                              
         EJECT                                                                  
**********************************************************************          
* HOOK ROUTINE TO PROCESS DEMO ELEMENTS FOR DEMO LOOK-UPS                       
**********************************************************************          
*                                                                               
DEMLKHK  ST    RE,SAVERE           SAVE RETURN ADDRESS                          
*                                                                               
         OC    REDLABK,REDLABK     EXTRACT ACTUAL BOOK VALUE                    
         BNZ   *+10                                                             
         MVC   REDLABK,DBACTBK                                                  
*                                                                               
         GOTO1 VDEFINE,DMCB,PROGPLUS,DBLOCKD,NDPROG                             
         OC    STPROG,STPROG                                                    
         BNZ   *+10                                                             
         MVC   STPROG,NDPROG                                                    
*                                                                               
DEMLKHK4 DS    0H                                                               
         GOTO1 VGETIUN,DMCB,(4,DBLOCK),OLDUNV                                   
         LA    R0,NUMVALS*4        WEIGHT & ACCUMULATE DEMOS                    
         LA    R1,OLDRTG                                                        
         LA    R2,NEWRTG                                                        
         BAS   RE,WGHTUP                                                        
*                                                                               
         DS    0H                                                               
         LA    R2,DEMOSHR                                                       
         CLI   DBSELMED,C'N'                                                    
         BNE   *+8                                                              
         LA    R2,NETSHR                                                        
         GOTO1 VDEMOUT,DMCB,(C'P',(R2)),DBLOCKD,WORK                            
         LA    R0,3                WEIGHT & ACCUMULATE VUTS                     
         LA    R1,WORK                                                          
         LA    R2,OLDSHR                                                        
         BAS   RE,WGHTUP                                                        
*                                                                               
         GOTO1 VDEMOUT,DMCB,(C'L',VUTLIST),DBLOCK,WORK                          
         LA    R0,3                WEIGHT & ACCUMULATE VUTS                     
         LA    R1,WORK                                                          
         LA    R2,OLDVUT                                                        
         BAS   RE,WGHTUP                                                        
*                                                                               
         GOTO1 VDEMOUT,DMCB,(C'L',UNVLIST),DBLOCK,UNIVS                         
*                                                                               
DEMLKHKX L     RE,SAVERE           RETURN TO DEMAND FOR NEXT RECORD             
         BR    RE                                                               
         EJECT                                                                  
**********************************************************************          
*DEMUPGD - APPLY UPGRADE TO INV RECD                                            
**********************************************************************          
DEMUPGD  NTR1                                                                   
         MVC   HOMSHR(12),OLDSHR                                                
         TM    REDLOPT,X'40'       IMP BASED PREC?                              
         BZ    *+8                                                              
         MVI   DBTAPEP,C'I'                                                     
         LA    RE,IOAREA1                                                       
         MVC   WORK(1),11(RE)                                                   
         MVI   11(RE),0            PREVENT UT/TP PROBLEM                        
         LA    R0,RINVPEL-RINVREC(RE)                                           
         XC    DUB(8),DUB                                                       
         MVC   DUB(4),=C'RID='                                                  
         MVC   DUB+4(2),REDLREP                                                 
         L     RE,REDUPGEL                                                      
         GOTO1 VREDEMUP,DMCB,(R0),(DBTAPEP,(RE)),REDLAFAC,DUB,OLDSHR            
         LA    RE,IOAREA1                                                       
         MVC   11(1,RE),WORK       RESTORE FIELD                                
DEMUPGDX B     XIT                                                              
         EJECT                                                                  
**********************************************************************          
*BLDIUN - BUILD IUN RECORD IN IOAREA1 FROM IUNWORK VALUES.                      
* BUILD A CORRESPONDING DBLOCK FOR INV RECD                                     
**********************************************************************          
BLDIUN   NTR1                                                                   
         LA    RF,IOAREA1          BUILD IUNRECD IN IOAREA 1                    
         LR    R0,RF                                                            
         LA    R1,IOAREA1L                                                      
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         XC    DBLOCK,DBLOCK          SET UP DBLOCK FOR INV RECD                
         MVC   DBCOMFCS,REDLAFAC                                                
         MVC   DBFILE,=C'INV'                                                   
         MVI   DBSELMED,C'U'                                                    
         MVI   DBSELSRC,C'N'                                                    
         MVC   DBSELAGY,REDLREP                                                 
         MVC   DBSELDAY,REDLDAY                                                 
         MVC   DBSELTIM,REDLTIM                                                 
         LA    RE,DEXTRA1                                                       
         STCM  RE,15,DBEXTEND                                                   
         LA    R1,IOAREA1                                                       
         ST    R1,DBAREC                                                        
         LA    RF,RINVPEL-RINVREC(R1)                                           
         ST    RF,DBAQUART                                                      
         LA    R1,IOAREA1                                                       
         USING RINVREC,R1                                                       
         MVI   RINVKTYP,RINVKTYQ   BLD IUN KEY                                  
         MVC   RINVKREP,REDLREP                                                 
         MVC   RINVKSTA,REDLSTA                                                 
*        MVC   RINVKSRC,REDLSRC                                                 
*        CLI   REDLMED,C'N'                                                     
*        BNE   *+8                                                              
*        MVI   RINVKSRC,C'N'       NETWORK SOURCE IS 'N' FOR NSI                
*                                                                               
         MVI   RINVKRSR,C'N'       NETWORK SOURCE IS 'N' FOR NSI                
         MVI   RINVKQLF,X'40'                                                   
         MVI   RINVKBTP,0                                                       
         MVC   RINVKBK,REDLDBK                                                  
*                                                                               
         LA    RF,WORK             ADD 'CE' ELEM FOR DEMOUT CALL                
         XC    WORK(10),WORK                                                    
         USING RINVZEL,RF                                                       
         MVI   RINVZCOD,X'CE'                                                   
         MVI   RINVZLEN,RINVZLNQ                                                
         MVI   RINVZBK,C' '                                                     
         MVC   RINVZBK+1(2),REDLDBK                                             
         MVC   RINVZDAY,REDLDAY                                                 
         MVC   RINVZTIM,REDLTIM                                                 
         DROP  RF                                                               
*                                                                               
         MVC   RINVPEL(RINVZLNQ),WORK      ADD ELEM TO RECD                     
         LA    RF,RINVPEL+RINVZLNQ                                              
         MVI   0(RF),0                                                          
         SR    RF,R1                                                            
         STCM  RF,3,RINVLEN                                                     
*                                                                               
         OC    REDUPGEL,REDUPGEL    IF THERE'S AN UPGD RESET SHRS               
         BZ    *+10                                                             
         MVC   HOMSHR(12),OLDSHR                                                
         LA    R1,(IUNWORKX-IUNWORK)/4                                          
         STCM  R1,3,DBNUMVLS                                                    
*                                  ADD DEMOS FROM IUNWORK TO INV RECD           
         GOTO1 VDEMAINT,DMCB,=C'PUT',DBLOCK,IUNWORK,OFORMAT                     
         CLI   DBERROR,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
BLDIUNX B      XIT                                                              
         EJECT                                                                  
***********************************************************************         
* SUBROUTINE TO WEIGHT & ACCUMULATE DEMO VALUES                       *         
* R0 = NUMBER OF ENTRIES TO WEIGHT & ACCUMULATE                       *         
* R1 = A(INPUT VALUES)                                                *         
* R2 = A(ACCUMULATOR VALUES)                                          *         
***********************************************************************         
         SPACE 1                                                                
WGHTUP   NTR1  ,                                                                
         SR    R4,R4                                                            
         ICM   R4,3,DBFACTOR                                                    
         SR    R3,R3                                                            
WGHTUP2  L     RF,0(R1,R3)         RF=DEMO VALUE                                
         SR    RE,RE                                                            
         MR    RE,R4               RF=VALUE*WEIGHT                              
         ST    RF,0(R1,R3)         SET WEIGHTED VALUE IN INPUT                  
         A     RF,0(R2,R3)         AND ACCUMULATE WEIGHTED VALUE                
         ST    RF,0(R2,R3)                                                      
         LA    R3,4(R3)                                                         
         BCT   R0,WGHTUP2                                                       
         B     LKX                                                              
         SPACE 2                                                                
***********************************************************************         
* SUBROUTINE TO UNWEIGHT ACCUMULATED DEMO VALUES                      *         
* R0 = NUMBER OF VALUES TO UNWEIGHT                                   *         
* R1 = A(ACCUMULATOR VALUES)                                          *         
* R2 = A(UNWEIGHTED ACCUMULATOR VALUES) OR ZERO IF SAME AS INPUT      *         
***********************************************************************         
         SPACE 1                                                                
UNWGHT   NTR1  ,                                                                
         SR    R4,R4                                                            
         ICM   R4,3,DBDIVSOR                                                    
         SR    R3,R3                                                            
UNWGHT2  L     RE,0(R1,R3)                                                      
         SR    RF,RF                                                            
         SRDA  RE,31                                                            
         DR    RE,R4               RF=VALUE/DBDIVSOR (ROUNDED)                  
         AH    RF,=H'1'                                                         
         SRA   RF,1                                                             
         ST    RF,0(R1,R3)         STORE UNWEIGHTED VALUE                       
         LTR   R2,R2               TEST ACCUMULATORS GIVEN                      
         BZ    *+12                                                             
         A     RF,0(R2,R3)         YES - ACCUMULATE UNWEIGHTED VALUE            
         ST    RF,0(R2,R3)                                                      
         LA    R3,4(R3)                                                         
         BCT   R0,UNWGHT2                                                       
         B     LKX                                                              
         EJECT                                                                  
         LTORG                                                                  
         SPACE 1                                                                
NUMVALS  EQU   32                                                               
HOMES    EQU   20*4                                                             
LENVALS  EQU   NUMVALS*4                                                        
         SPACE 1                                                                
TPTVALS  DC    C'TTP '                                                          
PAVVALS  DC    C'PPAV'                                                          
         SPACE 1                                                                
NETFILES DS    0CL7                   DBSELSRC/ FILE/ DBFILE                    
         DC    C'H',C'NHT',C'NTI'     NHT FILE USE DBFILE=NTI FOR TP            
         DC    C'D',C'NAD',C'NAD'     NAD FILE                                  
         DC    C'C',C'CNN',C'NTI'     CABLE FILE                                
         DC    C'P',C'PNN',C'NTI'     NTI PKTPC FILE                            
         DC    X'FF'                                                            
         SPACE 1                                                                
VUTLIST  DC    X'81',C'V',AL1(1)   LIST OF HOMES VUTS (SHR/RTG)                 
         DC    X'81',C'V',AL1(2)                                                
         DC    X'81',C'V',AL1(3)                                                
         DC    X'FF'                                                            
         SPACE 1                                                                
DEMOSHR  DS    0XL3                                                             
         DC    X'81',C'S',AL1(1)                                                
         DC    X'81',C'S',AL1(2)                                                
         DC    X'81',C'S',AL1(3)                                                
         DC    X'FF'                                                            
NETSHR   DS    0XL3                                                             
         DC    X'00',C'S',AL1(1)                                                
         DC    X'00',C'S',AL1(2)                                                
         DC    X'00',C'S',AL1(3)                                                
         DC    X'FF'                                                            
UNVLIST  DS    0XL3                                                             
         DC    X'43',C'U',AL1(1)                                                
         DC    X'43',C'U',AL1(2)                                                
         DC    X'43',C'U',AL1(3)                                                
         DC    X'FF'                                                            
         SPACE 1                                                                
*                                                                               
OINVFMT  DC    C'INVUIUN',X'530B00'  RATING BASED DEMOS                         
OINVFMT2 DC    C'INVUIUN',X'5A0B00'  IMP BASED DEMOS                            
*                                                                               
REP      DC    C'REP'                                                           
PUT      DC    C'PUT'                                                           
PROGPLUS DC    C'PROG+'                                                         
         EJECT                                                                  
WORKD    DSECT                     ** REDEMLK GLOBAL W/S **                     
DUB      DS    D                                                                
WORK     DS    XL64                                                             
DMCB     DS    6F                                                               
RELO     DS    A                                                                
VDEMAND  DS    V                                                                
VDEMOUT  DS    V                                                                
VDEMAINT DS    V                                                                
VDEFINE  DS    V                                                                
VGETIUN  DS    V                                                                
VREDEMUP DS    V                   A(REDEMUP)                                   
ADISPDTA DS    V                   A(IUN MASTER DISP. TABLE)                    
SVRE     DS    X                                                                
SAVERE   DS    A                   SAVED RE VALUE IN DEMAND HOOK                
TAPEOPT  DS    C                   DMA IMP CALCS OPTION                         
DBLOCK1  DS    CL(L'DBLOCK)        DBLOCK FOR DEMO LOOK-UPS                     
DEXTRA1  DS    CL128                EXTRA DBLOCK CONTROL                        
STPROG   DS    CL16                START QHR PROGRAM NAME                       
NDPROG   DS    CL16                END QHR PROGRAM NAME                         
OLDVUT   DS    3F                  HOME VUTS (SHR/RTG)                          
OLDSHR   DS    3F                                                               
UNIVS    DS    3F                                                               
OFORMAT  DS    CL10                                                             
IOAREA1  DS    2000C                                                            
IOAREA1L EQU   *-IOAREA1                                                        
         SPACE 2                                                                
IUNWORK  DS    0F                                                               
***********************************************************************         
OLDUNV   DS    (NUMVALS)F          UNIVERSES                          *         
OLDUNVX  EQU   *                                                      *         
***********************************************************************         
OLDRTG   DS    (NUMVALS)F          RATINGS                            *         
OLDIMP   DS    (NUMVALS)F          IMPRESSIONS                        *         
OLDRTGX  EQU   *                                                      *         
***********************************************************************         
OLDHPT   DS    (NUMVALS)F          HUTS/PUTS                          *         
OLDTOT   DS    (NUMVALS)F          TSA TOTALS                         *         
OLDHPTX  EQU   *                                                      *         
***********************************************************************         
NEWRTG   DS    (NUMVALS)F          RATINGS                            *         
NEWIMP   DS    (NUMVALS)F          IMPRESSIONS                        *         
NEWRTGX  EQU   *                                                      *         
***********************************************************************         
NEWHPT   DS    (NUMVALS)F          HUTS/PUTS                          *         
NEWTOT   DS    (NUMVALS)F          TSA TOTALS                         *         
NEWHPTX  EQU   *                                                      *         
***********************************************************************         
HOMSHR   DS    3F                  ORIGINAL HOMES SHARES              *         
HOMSHRX  EQU   *                                                      *         
HOMSHRLN EQU   *-HOMSHR                                               *         
***********************************************************************         
LUNV     DS    (NUMVALS)F          LOONEYVERSES                       *         
LUNVX    EQU   *                                                      *         
***********************************************************************         
IUNWORKX DS    0F                                                               
IOAREA2  DS    1500C               BUILD IUN SHELL RECD FOR TPT                 
IOAREA2L EQU   *-IOAREA2                                                        
WORKX    EQU   *                                                                
         EJECT                                                                  
* REDEMLKD                                                                      
REDLBLKD DSECT                                                                  
       ++INCLUDE REDEMLKUD                                                      
         SPACE 1                                                                
* DEDBLOCK                                                                      
         PRINT OFF                                                              
DBLOCKD  DSECT                                                                  
       ++INCLUDE DEDBLOCK                                                       
         PRINT ON                                                               
         SPACE 1                                                                
* DEDBEXTRAD                                                                    
         PRINT OFF                                                              
       ++INCLUDE DEDBEXTRAD                                                     
         PRINT ON                                                               
         SPACE 1                                                                
* DEDEMTABD                                                                     
         PRINT OFF                                                              
       ++INCLUDE DEDEMTABD                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* DDCOMFACS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* DEDEMFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE DEDEMFILE                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* REGENINVA                                                                     
         PRINT OFF                                                              
       ++INCLUDE REGENINVA                                                      
         PRINT ON                                                               
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'163REDEMLKU  04/22/09'                                      
         END                                                                    
