*          DATA SET ACGETOPT   AT LEVEL 037 AS OF 07/01/20                      
*CATALP GETOPT                                                                  
         TITLE 'GETOPT - PRODUCTION OPTION HANDLER '                            
                                                                                
* MPEN 036  11AUG16 <DSRD-12035> FIX GETOPT OFFICE/OFFICE GROUP ISSUES          
* ASAX 037  26MAY20 <DSRD-25987> NEW OPT/MAIN 'DATE EST IN LAST FISCAL          
*                                YEAR'                                          
                                                                                
GETOPT   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 GODX-GOD,*GETOPT*,RA,RR=R2,CLEAR=YES                             
         USING GOD,RC                                                           
         L     R9,0(R1)            PARAMETER 1 =A(GOBLOCK)                      
         USING GOBLOCK,R9                                                       
         SPACE 1                                                                
         ST    R2,RELO             INITIALIZE                                   
         L     R2,GOABUFF                                                       
         A     R2,GOLBUFF                                                       
         BCTR  R2,0                                                             
         ST    R2,AENDBUFF                                                      
         SPACE 1                                                                
         MVC   BINSRCH,GOABINSR    GET A(BINSRCH) FROM BLOCK FIRST              
         OC    BINSRCH,BINSRCH     TEST IF PASSED                               
         BNZ   INIT1               YES                                          
         L     R2,=V(BINSRCH)                                                   
         LTR   R2,R2                                                            
         BZ    *+8                                                              
         A     R2,RELO                                                          
         ST    R2,BINSRCH                                                       
*                                                                               
INIT1    ICM   R0,15,GOAFROM                                                    
         BZ    INIT2                                                            
         LHI   R1,GOFROML          CLEAR FROM BUFFER TO BINARY ZEROES           
         XR    RE,RE                                                            
         XR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
INIT2    DS    0H                                                               
         EJECT                                                                  
*              MAIN LOGIC CONTROL                                               
         SPACE 3                                                                
MAIN     XC    GOPTIONS,GOPTIONS   PRECLEAR USERS OPTION AREA                   
         ICM   RE,15,GOAPAN        TEST FOR POINTER TO PANEL BLOCK              
         BZ    *+10                                                             
         XC    0(L'GOPANBLK,RE),0(RE) CLEAR PANEL BLOCK                         
         ICM   RE,15,GOAEXT        TEST FOR POINTER TO EXTENSION                
         BZ    *+12                NO                                           
         LA    RF,L'GOXBLOCK       YES-CLEAR IT                                 
         SR    R1,R1                                                            
         MVCL  RE,R0                                                            
*                                                                               
         MVI   ANYIO,C'N'          SET I/O CHECKER                              
         BAS   RE,OKSET            BUILD OPTION KEY BUFFER                      
         BAS   RE,COMPSET          OFFICE, MEDIA, WORK LISTS                    
*                                                                               
         BAS   RE,SETOFF           SET EFFECTIVE OFFICE,                        
         BAS   RE,SETMED                         MEDIA,                         
         BAS   RE,SETWORK                    AND WORK CODE GROUPS               
         MVC   ACTJOB,GOSELJOB     INITIALIZE JOB CODE TO BE SAVED              
*                                                                               
         BAS   RE,DEFLEV           SET OPTIONS AT DEFAULT LEVEL                 
         CLI   GOSELLEV,C'D'                                                    
         BE    MAINEND                                                          
         BAS   RE,COMPLEV                         COMPANY                       
         CLI   GOSELLEV,C'A'                                                    
         BE    MAINEND                                                          
         BAS   RE,OGLEV                           OFFICE GROUP                  
         CLI   GOSELOG,0           TEST FOR OFFICE GROUP REQUEST                
         BNE   MAINEND             YES                                          
         BAS   RE,OFFLEV                          OFFICE                        
         OC    GOSELOFC,GOSELOFC   TEST FOR OFFICE REQUEST                      
         BNZ   MAINEND                                                          
         BAS   RE,CLILEV                          CLIENT                        
         CLI   GOSELLEV,C'C'                                                    
         BE    MAINEND                                                          
         BAS   RE,PRODLEV                         PRODUCT                       
         CLI   GOSELLEV,C'P'                                                    
         BE    MAINEND                                                          
         BAS   RE,JOBLEV                          JOB                           
         B     MAINEND                                                          
         SPACE 1                                                                
MAINEND  BAS   RE,WORKOVER         APPLY OVERRIDES FROM WORK STATUS             
         MVC   MYCUL,GOSELCUL      SAVE WHAT WE DID THIS TIME                   
         MVC   MYCLI,GOSELCLI                                                   
         MVC   MYOG,GOEFFOG                                                     
         MVC   MYOFC,GOEFFOFC                                                   
         MVC   MYPROD,GOSELPRO                                                  
         MVC   MYJOB,ACTJOB                                                     
         MVC   MYMED,ACTJOB                                                     
         MVC   MYMG,GOEFFMG                                                     
         MVC   MYWORK,GOSELWC                                                   
         MVC   MYWORKST,GOEFFWST                                                
         MVC   MYWORKG,GOEFFWG                                                  
         SPACE 1                                                                
MAINEND2 CLI   ANYIO,C'N'          TEST ANY I/O DONE BY GETOPT                  
         BE    MAINEND4            NO                                           
*                                                                               
         ICM   R1,15,GOAKEY                                                     
         BZ    MAINEND4            USER HAS NOT PASSED A KEY                    
         MVC   KEY,0(R1)                                                        
         GOTO1 GOADM,DMCB,(X'08',DMREAD),ACCOUNT,KEY,IO                         
         TM    8(R1),X'FF'-X'02'                                                
         BE    *+6                                                              
         DC    H'0'                                                             
         SPACE 1                                                                
MAINEND4 B     XIT                                                              
         EJECT                                                                  
*              BUILD OPTION KEY BUFFER                                          
         SPACE 3                                                                
OKSET    NTR1                                                                   
         CLI   OKSTAT,0            ONLY NEED TO DO THIS ONCE                    
         BNE   OKSETX                                                           
         MVI   OKSTAT,2            2 MEANS BUFFER NOT AVAILABLE                 
         MVC   COVAIL,GOACOVL      GET A(COVAIL) FROM BLOCK FIRST               
         OC    COVAIL,COVAIL       TEST IF COVAIL PASSED                        
         BNZ   OKSET1              YES                                          
         L     R2,=V(COVAIL)                                                    
         LTR   R2,R2                                                            
         BZ    OKSETX              WHEN COVAIL IS NOT LINKED                    
         A     R2,RELO                                                          
         ST    R2,COVAIL                                                        
         SPACE 1                                                                
OKSET1   GOTO1 COVAIL,DMCB,C'LOOK'                                              
         LM    R2,R3,4(R1)         GET A(BUFFER)/L'BUFFER                       
         LTR   R2,R2               TEST FOR ERROR                               
         BZ    OKSETX              YES-FORGET THE BUFFER                        
         S     R3,=F'1000000'      LEAVE AT LEAST A MEG FOR OTHERS              
         BM    OKSETX                                                           
         L     R2,MINALLOC                                                      
         CR    R3,R2               TEST MINIMUM ALLOCATION AVAILABLE            
         BL    OKSETX              YES                                          
         SPACE 1                                                                
         GOTO1 COVAIL,DMCB,C'GET ',(R2),(R3)                                    
         LM    R2,R3,4(R1)         GET A(BUFFER)/L'BUFFER                       
         LTR   R2,R2               TEST FOR ALLOCATION ERROR                    
         BZ    OKSETX              YES                                          
         SPACE 1                                                                
         MVI   OKSTAT,1                                                         
         ST    R2,AOKBUFF                                                       
         SRL   R3,1                DIVIDE BUFFER BETWEEN KEYS/DATA              
         LA    R5,0(R3,R2)                                                      
         ST    R5,AOKDATA                                                       
         LA    RE,0(R3,R5)                                                      
         BCTR  RE,0                                                             
         ST    RE,AOKDATAX         SET A(END OF DATA BUFFER)                    
         SPACE 1                                                                
         SR    R2,R2                                                            
         D     R2,=A(OPTBUFFL)     FIGURE OUT HOW MANY KEYS I CAN HOLD          
         BCTR  R3,0                                                             
         L     R2,AOKBUFF                                                       
         MVC   0(OPTKLEN,R2),XFF   FIRST ENTRY IS HIGHEST KEY FOUND             
         LA    R2,OPTBUFFL(R2)                                                  
         SR    R4,R4               COUNT NUMBER OF KEYS IN R4                   
         SPACE 1                                                                
         LA    R6,KEY              SET UP TO READ OPTIONS                       
         USING ACOPKEY,R6                                                       
         XC    ACOPKEY,ACOPKEY                                                  
         MVI   ACOPRTYP,ACOPEQU                                                 
         MVI   ACOPSREC,ACOPSEQU                                                
         MVC   ACOPCUL,GOSELCUL                                                 
         BAS   RE,HIGH                                                          
         B     OKSET3                                                           
         SPACE 1                                                                
OKSET2   BAS   RE,SEQ                                                           
         SPACE 1                                                                
OKSET3   CLC   ACOPKEY(ACOPOG-ACOPKEY),KEYSAVE                                  
         BNE   OKSET6                                                           
         SPACE 1                                                                
         MVC   0(OPTKLEN,R2),ACOPOG  SAVE THIS KEY                              
         XC    OPTKLEN(4,R2),OPTKLEN(R2) CLEAR POINTER                          
         SR    RE,RE                                                            
         ICM   RE,3,ACLENGTH       GET RECORD LENGTH                            
         SH    RE,DATADISP         DEDUCT DISP TO FIRST ELEMENT                 
         LA    RE,1(RE,R5)         + 2 FOR HEADER - 1 FOR EOR                   
         C     RE,AOKDATAX         TEST ROOM FOR DATA                           
         BH    OKSET4              NO ROOM FOR IT                               
*                                                                               
         STCM  R5,15,OPTKLEN(R2)   SET POINTER TO DATA                          
         SR    RF,RF                                                            
         ICM   RF,3,ACLENGTH                                                    
         SH    RF,DATADISP                                                      
         LA    RF,1(RF)            RF=L'DATA BUFFER ENTRY                       
         STCM  RF,3,0(R5)                                                       
         LA    RE,2(R5)            RE=A(DESTINATION)                            
         BCTR  RF,0                ADJUST FOR HEADER LENGTH                     
         BCTR  RF,0                                                             
         LA    R0,ACRECORD         R0=A(SOURCE)                                 
         LR    R1,RF               R1=SOURCE LENGTH=DESTINATION LENGTH          
         MVCL  RE,R0               ATTACH RECORD DATA TO HEADER                 
         SPACE 1                                                                
         SR    RE,RE                                                            
         ICM   RE,3,0(R5)                                                       
         LA    R5,0(RE,R5)         NEXT DATA AREA                               
         SPACE 1                                                                
OKSET4   LA    R2,OPTBUFFL(R2)     NEXT KEY ENTRY                               
         LA    R4,1(R4)            INCREMENT KEY COUNT                          
         CR    R4,R3               TEST IF KEY BUFFER FILLED                    
         BL    OKSET2              NO                                           
         SPACE 1                                                                
OKSET5   S     R2,=A(OPTBUFFL)     BACK UP TO LAST ENTRY                        
         L     R1,AOKBUFF                                                       
         MVC   0(OPTKLEN,R1),0(R2) SET HIGHEST KEY IN BUFFER                    
         SPACE 1                                                                
OKSET6   ST    R4,NOPTKEYS         SET N'OPTION KEYS                            
         SPACE 1                                                                
OKSETX   B     XIT                                                              
         DROP  R6                                                               
         EJECT                                                                  
*              COMPANY LEVEL ROUTINES                                           
*                                                                               
COMPSET  NTR1                                                                   
         CLC   MYCUL,GOSELCUL      HAVE I SAVED CUL LEVEL                       
         BE    XIT                                                              
         OC    GOABUFF,GOABUFF     DID USER GIVE ME A BUFFER?                   
         BZ    COMPSET2                                                         
*                                  YES - GO AND BUILD USEFUL LISTS              
         MVC   BUFFNOW,GOABUFF                                                  
         BAS   RE,OFFLIST                                                       
         BAS   RE,MEDLIST                                                       
         BAS   RE,WORKLIST                                                      
         MVC   ACMPOPTS,BUFFNOW                                                 
         SPACE 1                                                                
*                                                                               
COMPSET2 TM    GOSPEC3,GOSPXDPQ    EXTRA DETAILS PASSED?                        
         BNZ   COMPSETB                                                         
         L     R6,GOACOMP          DID USER HAVE A COMPANY AROUND?              
         LTR   R6,R6                                                            
         BNZ   COMPSET4                                                         
         MVC   KEY,SPACES          NO SO GO AND GET IT                          
         MVC   KEY(1),GOSELCUL                                                  
*                                                                               
         TM    GOSPEC3,GOSPNERQ    USING NONE EMULATED IOS?                     
         BZ    COMPSET3                                                         
         GOTO1 XREAD                                                            
         BE    *+6                                                              
         DC    H'0'                                                             
         GOTO1 XGET                                                             
         LA    R6,IO                                                            
         B     COMPSET4                                                         
*                                                                               
COMPSET3 BAS   RE,READ                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
         LA    R6,IO                                                            
*                                                                               
         USING ACKEYD,R6                                                        
COMPSET4 MVC   WHICH,GOWHICH       EXTRACT USER'S WHICH VALUE                   
         LHI   R0,ACCORFST                                                      
         TM    GOSPEC3,GOSPNERQ    USING NONE EMULATED IOS?                     
         BZ    *+8                                                              
         LHI   R0,ACCRFST-ACCRECD                                               
         AR    R6,R0                                                            
*                                                                               
         USING ACCOMPD,R6                                                       
COMPSET5 CLI   ACMPEL,0            TEST END OF RECORD                           
         BNE   *+6                                                              
         DC    H'0'                                                             
         CLI   ACMPEL,ACMPELQ      TEST COMPANY ELEMENT                         
         BE    *+14                                                             
         IC    R0,ACMPLEN                                                       
         AR    R6,R0                                                            
         B     COMPSET5                                                         
*                                                                               
         TM    ACMPSTA4,X'20'      IS COMPANY CONVERTED TO NEW?                 
         BNO   COMPSET6                                                         
         MVI   WHICH,C'N'          THEN IMPROVE GOBLOCK                         
         DROP  R6                                                               
*                                                                               
COMPSET6 L     R6,GOALEDG          DID USER HAVE A LEDGER AROUND?               
         LTR   R6,R6                                                            
         BNZ   COMPSET8                                                         
         MVC   KEY,SPACES          NO SO GO AND GET IT                          
         MVC   KEY(3),GOSELCUL                                                  
*                                                                               
         TM    GOSPEC3,GOSPNERQ    USING NONE EMULATED IOS?                     
         BZ    COMPSET7                                                         
         GOTO1 XREAD                                                            
         BE    *+6                                                              
         DC    H'0'                                                             
         GOTO1 XGET                                                             
         LA    R6,IO                                                            
         B     COMPSET8                                                         
*                                                                               
COMPSET7 BAS   RE,READ                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
         LA    R6,IO                                                            
*                                                                               
         USING ACKEYD,R6                                                        
COMPSET8 LHI   R0,ACCORFST                                                      
         TM    GOSPEC3,GOSPNERQ    USING NONE EMULATED IOS?                     
         BZ    *+8                                                              
         LHI   R0,ACCRFST-ACCRECD                                               
         AR    R6,R0                                                            
         XR    R0,R0                                                            
*                                                                               
         USING ACHEIRD,R6                                                       
COMPSETA CLI   ACHREL,0            TEST END OF RECORD                           
         BNE   *+6                                                              
         DC    H'0'                                                             
         CLI   ACHREL,ACHRELQ      TEST ACCOUNT LEVELS ELEMENT                  
         BE    *+14                                                             
         IC    R0,ACHRLEN                                                       
         AR    R6,R0                                                            
         B     COMPSETA                                                         
*                                                                               
         MVC   LLEVA,ACHRLEVA      AND SAVE LENGTHS                             
         MVC   LLEVB,ACHRLEVB                                                   
         MVC   LLEVC,ACHRLEVC                                                   
         B     COMPSETX                                                         
         DROP  R6                                                               
*                                                                               
         USING GOXDBD,R6                                                        
COMPSETB L     R6,GOADETS                                                       
         MVC   WHICH,GOWHICH       EXTRACT USER'S GOBWHICH VALUE                
         TM    GOXDBAS4,CPYSNPRD   TEST COMPANY CONVERTED TO NEW PROD           
         JZ    COMPSETC                                                         
         MVI   WHICH,GOWHNEW       SET TO GET NEW PROFILES ONLY                 
*                                                                               
COMPSETC MVC   LLEVA,GOXDBL1L      SET LEVEL LENGTH FOR CLI/PRO/JOB             
         MVC   LLEVB,GOXDBL2L                                                   
         MVC   LLEVC,GOXDBL3L                                                   
         DROP  R6                                                               
COMPSETX B     XIT                                                              
         EJECT                                                                  
**********************************************************************          
*              ROUTINE TO BUILD OFFICE LIST                          *          
*                                                                    *          
**********************************************************************          
*                                                                               
*                                                                               
OFFLIST  NTR1                                                                   
         OC    BUFFNOW,BUFFNOW     WAS A BUFFER PROVIDED?                       
         BZ    OLXIT                                                            
         L     R2,BUFFNOW          PICK UP PRESENT BUFFER POSITION              
         ST    R2,AOLIST           WHICH BECOMES START OF LIST                  
         SPACE 1                                                                
         LA    R4,KEY                                                           
         USING ACOGKEY,R4                                                       
         XC    ACOGKEY,ACOGKEY                                                  
         MVI   ACOGRTYP,ACOGEQU                                                 
         MVI   ACOGSREC,ACOGOFF                                                 
         MVC   ACOGCUL,GOSELCUL                                                 
         GOTO1 XHIGH                                                            
         B     OL4                                                              
*                                                                               
OL2      GOTO1 XSEQ                                                             
*                                                                               
OL4      CLC   ACOGKEY(ACOGOFC-ACOGKEY),KEYSAVE                                 
         BE    OL6                                                              
         BAS   RE,ENDBUFF                                                       
         B     OLXIT                                                            
*                                                                               
OL6      GOTO1 XGET                                                             
         LA    R6,IO                                                            
         AHI   R6,ACCRFST-ACCRECD                                               
         XR    R0,R0                                                            
*                                                                               
         USING ACGPD,R6                                                         
OL7      CLI   ACGPEL,0                                                         
         BE    OL2                                                              
         CLI   ACGPEL,ACGPELQ                                                   
         BE    *+14                                                             
         IC    R0,ACGPLEN                                                       
         AR    R6,R0                                                            
         B     OL7                                                              
*                                                                               
         CLI   ACGPCODE,X'41'                                                   
         BL    OL2                                                              
         MVC   BUFFEL(2),ACOGOFC                                                
         MVC   BUFFEL+2(1),ACGPCODE                                             
         MVI   BUFFLEN,3                                                        
         BAS   RE,POSTBUFF                                                      
         BE    OL2                                                              
         XC    AOLIST,AOLIST       (NOT ENOUGH SPACE IN BUFFER)                 
*                                                                               
OLXIT    B     XIT                                                              
         DROP  R4,R6                                                            
         EJECT                                                                  
**********************************************************************          
*              ROUTINE TO BUILD MEDIA LIST                           *          
*                                                                    *          
**********************************************************************          
*                                                                               
*                                                                               
MEDLIST  NTR1                                                                   
         CLI   GOANYMED,C'N'       USER WILL NOT NEED MEDIA LEVEL               
         BE    MLXIT                                                            
         OC    BUFFNOW,BUFFNOW     WAS A BUFFER PROVIDED?                       
         BZ    MLXIT                                                            
         L     R2,BUFFNOW          PICK UP PRESENT BUFFER POSITION              
         ST    R2,AMLIST           WHICH BECOMES START OF LIST                  
         SPACE 1                                                                
         MVC   KEY,SPACES          BUILD MEDIA RECORD KEY                       
         MVI   KEY,X'09'                                                        
         MVC   KEY+1(1),GOSELCUL                                                
         GOTO1 XHIGH                                                            
         B     ML4                                                              
*                                                                               
ML2      GOTO1 XSEQ                                                             
*                                                                               
ML4      CLC   KEY(2),KEYSAVE                                                   
         BE    ML6                                                              
         BAS   RE,ENDBUFF                                                       
         B     MLXIT                                                            
*                                                                               
ML6      GOTO1 XGET                                                             
         LA    R6,IO                                                            
         AHI   R6,ACCRFST-ACCRECD                                               
         XR    R0,R0                                                            
*                                                                               
         USING ACMEDIAD,R6                                                      
ML7      CLI   ACMDEL,0                                                         
         BE    ML2                                                              
         CLI   ACMDEL,ACMDELQ                                                   
         BE    *+14                                                             
         IC    R0,ACMDLEN                                                       
         AR    R6,R0                                                            
         B     ML7                                                              
*                                                                               
         CLI   ACMDGRP,X'41'                                                    
         BL    ML2                                                              
         MVC   BUFFEL(1),KEY+2                                                  
         MVC   BUFFEL+1(1),ACMDGRP                                              
         DROP  R6                                                               
         MVI   BUFFLEN,2                                                        
         BAS   RE,POSTBUFF                                                      
         BE    ML2                                                              
         XC    AMLIST,AMLIST       (NOT ENOUGH SPACE IN BUFFER)                 
*                                                                               
MLXIT    B     XIT                                                              
         EJECT                                                                  
**********************************************************************          
*              ROUTINE TO BUILD WORK CODE LIST                       *          
*                                                                    *          
**********************************************************************          
*                                                                               
*                                                                               
WORKLIST NTR1                                                                   
         CLI   GOANYWC,C'N'        USER WILL NOT NEED WORK CODE LEVEL           
         BE    WLXIT                                                            
         OC    BUFFNOW,BUFFNOW     WAS A BUFFER PROVIDED?                       
         BZ    WLXIT                                                            
         L     R2,BUFFNOW          PICK UP PRESENT BUFFER POSITION              
         ST    R2,AWLIST           WHICH BECOMES START OF LIST                  
*                                                                               
         MVC   KEY,SPACES          BUILD WORK CODE KEY                          
         MVI   KEY,X'0A'                                                        
         MVC   KEY+1(3),GOSELCUL                                                
         GOTO1 XHIGH                                                            
         B     WL4                                                              
*                                                                               
WL2      GOTO1 XSEQ                                                             
*                                                                               
WL4      CLC   KEY(4),KEYSAVE                                                   
         BE    WL6                                                              
         BAS   RE,ENDBUFF                                                       
         B     WLXIT                                                            
*                                                                               
WL6      GOTO1 XGET                                                             
         LA    R6,IO                                                            
         AHI   R6,ACCRFST-ACCRECD                                               
         XR    R0,R0                                                            
*                                                                               
         USING ACANALD,R6                                                       
WL7      CLI   ACANEL,0                                                         
         BE    WL2                                                              
         CLI   ACANEL,ACANELQ                                                   
         BE    *+14                                                             
         IC    R0,ACANLEN                                                       
         AR    R6,R0                                                            
         B     WL7                                                              
*                                                                               
         CLI   ACANGRUP,C' '       ENTRY IF WORK GROUP SPECIFIED                
         BH    WL8                                                              
         TM    ACANSTAT,X'80'+X'40'+X'02'+X'01'                                 
         BZ    WL2                 ADD ENTRY FOR THE ABOVE STATUS INDS          
*                                                                               
WL8      MVC   BUFFEL(2),KEY+4                                                  
         MVC   BUFFEL+2(1),ACANGRUP                                             
         MVC   BUFFEL+3(1),ACANSTAT                                             
         DROP  R6                                                               
         MVI   BUFFLEN,4                                                        
         BAS   RE,POSTBUFF                                                      
         BE    WL2                                                              
         XC    AWLIST,AWLIST       (NOT ENOUGH SPACE IN BUFFER)                 
*                                                                               
WLXIT    B     XIT                                                              
         EJECT                                                                  
**********************************************************************          
*              SET EFFECTIVE OFFICE                                  *          
*                                                                    *          
**********************************************************************          
*                                  OUTPUT IS GOEFFOFC GOEFFOG                   
*                                                                               
SETOFF   NTR1  ,                                                                
         CLI   GOSELOG,0           TEST IF OFFICE GROUP REQUESTED               
         BE    SETOFF2             NO                                           
         MVC   GOEFFOG,GOSELOG                                                  
*        XC    GOEFFOFC,GOEFFOFC                                                
*        B     SETOFFX                                                          
*                                                                               
SETOFF2  OC    GOSELOFC,GOSELOFC                                                
         BZ    SETOFF4                                                          
         MVC   GOEFFOFC,GOSELOFC                                                
         B     SETOFF22                                                         
*                                                                               
SETOFF4  CLC   MYCLI,GOSELCLI      IF CLIENT CHANGED                            
         BE    SETOFF14                                                         
         CLI   GOSELCLI,C' '       IS CLIENT REQUESTED?                         
         BNH   SETOFF22            NO                                           
         L     R6,GOACLI              AND RECORD IS NOT AROUND                  
         LTR   R6,R6                                                            
         BNZ   SETOFF10                                                         
         MVC   KEY,SPACES                                                       
         MVC   KEY(3),GOSELCUL                                                  
         MVC   KEY+3(6),GOSELCLI                                                
*                                                                               
         TM    GOSPEC3,GOSPNERQ    USING NONE EMULATED IOS?                     
         BZ    SETOFF8                                                          
         GOTO1 XREAD                                                            
         BE    *+6                                                              
         DC    H'0'                                                             
         GOTO1 XGET                                                             
         LA    R6,IO                                                            
         B     SETOFF10                                                         
*                                                                               
SETOFF8  GOTO1 READ                READ RECORD FOR OFFICE                       
         BE    *+6                                                              
         DC    H'0'                                                             
         LA    R6,IO                                                            
*                                                                               
         USING ACKEYD,R6                                                        
SETOFF10 LHI   R0,ACCORFST                                                      
         TM    GOSPEC3,GOSPNERQ    USING NONE EMULATED IOS?                     
         BZ    *+8                                                              
         LHI   R0,ACCRFST-ACCRECD                                               
         AR    R6,R0                                                            
         XR    R0,R0                                                            
*                                                                               
         USING ACPROFD,R6                                                       
SETOFF12 CLI   ACPREL,0            TEST END OF RECORD                           
         BNE   *+6                                                              
         DC    H'0'                                                             
         CLI   ACPREL,ACPRELQ      LOCATE PROFILE ELEMENT                       
         BE    *+14                                                             
         IC    R0,ACPRLEN                                                       
         AR    R6,R0                                                            
         B     SETOFF12                                                         
*                                                                               
         MVC   CLIOFC,ACPROFFC                                                  
         MVC   GOEFFOFC,CLIOFC                                                  
         DROP  R6                                                               
*                                                                               
SETOFF14 MVC   WORK(6),GOSELCLI    PUT THE PRODUCT RIGHT                        
         MVC   WORK+6(6),GOSELPRO  AFTER CLIENT                                 
         CLC   MYCLI(12),WORK      IF PRODUCT CHANGED                           
         BE    SETOFF22                                                         
         MVC   GOEFFOFC,CLIOFC                                                  
         CLI   GOSELPRO,0          IF PRODUCT WAS REQUESTED                     
         BE    SETOFF22                                                         
         L     R6,GOAPRO              AND RECORD IS NOT AROUND                  
         LTR   R6,R6                                                            
         BNZ   SETOFF18                                                         
         MVC   KEY,SPACES                                                       
         MVC   KEY(3),GOSELCUL                                                  
         MVC   KEY+3(6),GOSELCLI                                                
         ZIC   R1,LLEVA                                                         
         LA    R1,KEY+3(R1)                                                     
         MVC   0(6,R1),GOSELPRO                                                 
*                                                                               
SETOFF16 TM    GOSPEC3,GOSPNERQ    USING NONE EMULATED IOS?                     
         BZ    SETOFF17                                                         
         GOTO1 XREAD                                                            
         BE    *+6                                                              
         DC    H'0'                                                             
         GOTO1 XGET                                                             
         LA    R6,IO                                                            
         B     SETOFF18                                                         
*                                                                               
SETOFF17 GOTO1 READ                READ RECORD FOR OFFICE                       
         BE    *+6                                                              
         DC    H'0'                                                             
         LA    R6,IO                                                            
*                                                                               
         USING ACKEYD,R6                                                        
SETOFF18 LHI   R0,ACCORFST                                                      
         TM    GOSPEC3,GOSPNERQ    USING NONE EMULATED IOS?                     
         BZ    *+8                                                              
         LHI   R0,ACCRFST-ACCRECD                                               
         AR    R6,R0                                                            
         XR    R0,R0                                                            
*                                                                               
         USING ACPROFD,R6                                                       
SETOFF20 CLI   ACPREL,0            TEST END OF RECORD                           
         BE    SETOFF22                                                         
         CLI   ACPREL,ACPRELQ      LOCATE PROFILE ELEMENT                       
         BE    *+14                                                             
         IC    R0,ACPRLEN                                                       
         AR    R6,R0                                                            
         B     SETOFF20                                                         
*                                                                               
         CLC   ACPROFFC,SPACES     TEST FOR OFFICE OVERRIDE                     
         BNH   SETOFF22            NO                                           
         MVC   GOEFFOFC,ACPROFFC                                                
         DROP  R6                                                               
*                                                                               
SETOFF22 CLC   MYOFC,GOEFFOFC      IF THIS IS NOT THE SAME OFFICE               
         BE    SETOFFX                                                          
         MVI   GOEFFOG,0                                                        
         CLI   GOEFFOFC,C' '       TEST FOR AN OFFICE                           
         BNH   SETOFFX             NO-SKIP OFFICE GROUP LOOKUP                  
         ICM   R2,15,AOLIST                                                     
         BZ    SETOFF26            NO LIST SAVED                                
*                                                                               
SETOFF24 CLI   0(R2),0                                                          
         BE    SETOFFX                                                          
         CLC   GOEFFOFC,0(R2)      LOOK UP LIST FOR OFFICE                      
         BE    *+12                                                             
         LA    R2,3(R2)                                                         
         B     SETOFF24                                                         
         MVC   GOEFFOG,2(R2)       AND PICK OUT GROUP                           
         B     SETOFFX                                                          
*                                                                               
SETOFF26 LA    R4,KEY              NO LIST SO I HAVE TO READ OFFICE             
         USING ACOGKEY,R4                                                       
         XC    ACOGKEY,ACOGKEY                                                  
         MVI   ACOGRTYP,ACOGEQU                                                 
         MVI   ACOGSREC,ACOGOFF                                                 
         MVC   ACOGCUL,GOSELCUL                                                 
         MVC   ACOGOFC,GOEFFOFC                                                 
         GOTO1 XREAD                                                            
         BNE   SETOFFX                                                          
         GOTO1 XGET                                                             
         BNE   SETOFFX                                                          
*                                                                               
         LA    R6,IO                                                            
         AHI   R6,ACCRFST-ACCRECD                                               
         XR    R0,R0                                                            
*                                                                               
         USING ACGPD,R6                                                         
SETOFF28 CLI   ACGPEL,0            TEST END OF RECORD                           
         BE    SETOFFX                                                          
         CLI   ACGPEL,ACGPELQ      LOCATE GROUP ELEMENT                         
         BE    SETOFF30                                                         
         IC    R0,ACGPLEN                                                       
         AR    R6,R0                                                            
         B     SETOFF28                                                         
*                                                                               
SETOFF30 MVC   GOEFFOG,ACGPCODE    SET OFFICE GROUP                             
*                                                                               
SETOFFX  B     XIT                                                              
         DROP  R4,R6                                                            
         EJECT                                                                  
**********************************************************************          
*              SET EFFECTIVE MEDIA                                   *          
*                                                                    *          
**********************************************************************          
*                                  OUTPUT IS GOEFFMED GOEFFMG                   
*                                                                               
SETMED   NTR1                                                                   
         CLI   GOSELMG,0                                                        
         BE    SETMED2                                                          
         MVC   GOEFFMG,GOSELMG                                                  
         MVI   GOEFFMED,0                                                       
         B     SETMEDX                                                          
*                                                                               
SETMED2  CLI   GOSELMED,0                                                       
         BE    SETMED4                                                          
         MVC   GOEFFMED,GOSELMED                                                
         B     SETMED6                                                          
*                                                                               
SETMED4  MVC   GOEFFMED,GOSELJOB   MEDIA IS FIRST BYTE OF JOB                   
         MVI   GOEFFMG,0                                                        
         CLI   GOEFFMED,C' '       NOT NEEDED IF MEDIA NOT REQUESTED            
         BNH   SETMEDX                                                          
*                                                                               
SETMED6  CLC   MYMED,GOEFFMED          OR IF WE HAVE LOOKED IT UP               
         BNE   SETMED8                                                          
         MVC   GOEFFMG,MYMG        RESTORE LAST MEDIA GROUP                     
         B     SETMEDX                                                          
*                                                                               
SETMED8  L     R2,AMLIST                                                        
         LTR   R2,R2               DO I HAVE A LIST SAVED?                      
         BZ    SETMED12                                                         
*                                                                               
SETMED10 CLI   0(R2),0                                                          
         BE    SETMEDX                                                          
         CLC   0(1,R2),GOEFFMED    LOOK UP LIST FOR MEDIA                       
         BE    *+12                                                             
         LA    R2,2(R2)                                                         
         B     SETMED10                                                         
         MVC   GOEFFMG,1(R2)       AND PICK OUT GROUP                           
         B     SETMEDX                                                          
*                                                                               
SETMED12 MVC   KEY,SPACES          NO LIST SO I HAVE TO READ RECORD             
         MVI   KEY,X'09'                                                        
         MVC   KEY+1(1),GOSELCUL                                                
         MVC   KEY+2(1),GOEFFMED                                                
         GOTO1 XREAD                                                            
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         GOTO1 XGET                                                             
         LA    R6,IO                                                            
         AHI   R6,ACCRFST-ACCRECD                                               
         XR    R0,R0                                                            
*                                                                               
         USING ACMEDIAD,R6                                                      
SETMED14 CLI   ACMDEL,0            TEST END OF RECORD                           
         JE    SETMEDX                                                          
         CLI   ACMDEL,ACMDELQ      LOCATE MEDIA ELEMENT                         
         JNE   SETMED16                                                         
         MVC   GOEFFMG,ACMDGRP     EXTRACT MEDIA GROUP                          
         J     SETMEDX                                                          
*                                                                               
SETMED16 IC    R0,ACMDLEN                                                       
         AR    R6,R0                                                            
         J     SETMED14                                                         
         DROP  R6                                                               
*                                                                               
SETMEDX  B     XIT                                                              
         EJECT                                                                  
**********************************************************************          
*              SET EFFECTIVE WORK CODE & GROUP                       *          
*                                                                    *          
**********************************************************************          
*                                  OUTPUT IS GOEFFWRK GOEFFWG GOEFFWST          
*                                                                               
SETWORK  NTR1                                                                   
         MVI   GOEFFWG,0                                                        
         MVI   GOEFFWST,0                                                       
         MVC   GOEFFWC,GOSELWC                                                  
         CLI   GOEFFWC,0           NOT NEEDED IF WC NOT REQUESTED               
         BE    SETWORKX                                                         
         TM    GOSPEC3,GOSPSWLQ    SKIP W/C AND W/G LEVEL DATA                  
         BNZ   SETWORKX                                                         
         CLC   MYWORK,GOEFFWC          OR IF WE HAVE LOOKED IT UP               
         BNE   SETWORK1            NO                                           
         MVC   GOEFFWG,MYWORKG     RESTORE LAST WORKCODE GROUP                  
         MVC   GOEFFWST,MYWORKST   AND WORKCODE STATUS                          
         B     SETWORKX                                                         
*                                                                               
SETWORK1 L     R2,AWLIST                                                        
         LTR   R2,R2               DO I HAVE A LIST SAVED?                      
         BZ    SETWORK4                                                         
*                                                                               
SETWORK2 CLI   0(R2),0                                                          
         BE    SETWORKX                                                         
         CLC   0(2,R2),GOEFFWC     LOOK UP LIST FOR WORK CODE                   
         BE    *+12                                                             
         LA    R2,4(R2)                                                         
         B     SETWORK2                                                         
         MVC   GOEFFWG,2(R2)       AND PICK OUT GROUP                           
         MVC   GOEFFWST,3(R2)                   AND STATUS                      
         B     SETWORKX                                                         
*                                                                               
SETWORK4 MVC   KEY,SPACES          NO LIST SO I HAVE TO READ RECORD             
         MVI   KEY,X'0A'                                                        
         MVC   KEY+1(3),GOSELCUL                                                
         MVC   KEY+4(2),GOEFFWC                                                 
         GOTO1 XREAD                                                            
         BNE   SETWORKX            EXIT IF RECORD NOT THERE                     
         GOTO1 XGET                                                             
         LA    R6,IO                                                            
         AHI   R6,ACCRFST-ACCRECD                                               
         XR    R0,R0                                                            
*                                                                               
         USING ACANALD,R6                                                       
SETWORK6 CLI   ACANEL,0            TEST END OF RECORD                           
         BE    SETWORKX                                                         
         CLI   ACANEL,ACANELQ      LOCATE WORK CODE ELEMENT                     
         BE    *+14                                                             
         IC    R0,ACANLEN                                                       
         AR    R6,R0                                                            
         B     SETWORK6                                                         
*                                                                               
         MVC   GOEFFWG,ACANGRUP    EXTRACT VALUES FROM ELEMENT                  
         MVC   GOEFFWST,ACANSTAT                                                
*                                                                               
SETWORKX B     XIT                                                              
         DROP  R6                                                               
         EJECT                                                                  
**********************************************************************          
*              HANDLE OPTIONS AT DEFAULT LEVEL                       *          
*                                                                    *          
**********************************************************************          
*                                                                               
DEFLEV   NTR1                                                                   
         MVI   FROM,C'D'                                                        
         LA    R6,DEFREC           ADDRESS A DUMMY RECORD                       
         BAS   RE,NEWTOBUF                                                      
         B     XIT                                                              
*                                                                               
**********************************************************************          
*              HANDLE OPTIONS AT COMPANY LEVEL                       *          
*                                                                    *          
**********************************************************************          
*                                                                               
COMPLEV  NTR1                                                                   
         MVI   FROM,C'A'                                                        
         CLI   DONECOMP,C'Y'       IF WE HAVE DONE COMPANY BEFORE               
         BNE   CO2                                                              
         L     R2,ACMPOPTS            AND THE OPTIONS ARE SAVED                 
         LTR   R2,R2                                                            
         BZ    CO2                                                              
         BAS   RE,BUFTOUSR            OUTPUT THESE TO USER                      
         B     XIT                    AND WE'RE DONE                            
*                                                                               
CO2      MVC   BUFFNOW,ACMPOPTS                                                 
         MVI   DONECOMP,C'Y'                                                    
*                                  CLEAR LOWER LEVELS                           
         XC    MYOG,MYOG                                                        
         XC    MYOFC,MYOFC                                                      
         XC    MYCLI,MYCLI                                                      
         XC    MYPROD,MYPROD                                                    
         XC    MYJOB,MYJOB                                                      
         CLI   WHICH,C'N'          DEALING WITH OLD FIRST                       
         BE    CO6                                                              
         L     R6,GOACOMP          IS THE COMPANY RECORD AROUND                 
         LTR   R6,R6                                                            
         BNZ   CO4                                                              
         MVC   KEY,SPACES          NO - SO READ IT                              
         MVC   KEY(1),GOSELCUL                                                  
         BAS   RE,READ                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
         LA    R6,IO                                                            
*                                                                               
CO4      BAS   RE,OLDTOBUF         NOW OUTPUT AND SAVE ELEMENTS                 
*                                                                               
CO6      CLI   WHICH,C'O'          DEAL WITH NEW                                
         BE    CO8                                                              
         MVI   NEWLEVEL,0                                                       
         BAS   RE,DONEW                                                         
*                                                                               
CO8      BAS   RE,ENDBUFF                                                       
         OC    BUFFNOW,BUFFNOW     IF WE RAN OUT OF SPACE                       
         BNZ   *+10                                                             
         MVC   ACMPOPTS,BUFFNOW    CLEAR PRESENT LIST ADDRESS                   
         MVC   AOGROPTS,BUFFNOW                                                 
         B     XIT                                                              
*                                                                               
**********************************************************************          
*              HANDLE OPTIONS AT OFFICE GROUP LEVEL                  *          
*                                                                    *          
**********************************************************************          
*                                                                               
OGLEV    NTR1                                                                   
         MVI   FROM,C'G'                                                        
         MVC   BUFFNOW,AOGROPTS                                                 
         CLC   GOEFFOG,MYOG        TEST IF OFFICE GROUP CHANGED                 
         BNE   OG3                 YES                                          
*                                                                               
OG2      CLC   GOSELCUL,MYCUL      TEST FOR CHANGE IN COMPANY                   
         BNE   OG3                 YES                                          
         CLI   GOEFFOG,0           TEST FOR NO OFFICE GROUP                     
         BE    XIT                                                              
         CLI   WHICH,C'O'          TEST OLD OPTIONS ONLY                        
         BE    XIT                 YES-CANNOT HAVE OFFICE GROUP OPTIONS         
         L     R2,AOGROPTS                                                      
         LTR   R2,R2                                                            
         BZ    OG3                                                              
         BAS   RE,BUFTOUSR                                                      
         B     XIT                                                              
*                                                                               
OG3      CLI   GOEFFOG,0           TEST FOR NO OFFICE GROUP                     
         BE    OG4                 YES-DO NOT BOTHER WITH NEW OPTIONS           
         CLI   WHICH,C'O'          TEST OLD OPTIONS ONLY                        
         BE    OG4                 YES-SKIP NEW OPTIONS                         
*                                                                               
         MVI   NEWLEVEL,1                                                       
         BAS   RE,DONEW                                                         
*                                                                               
OG4      BAS   RE,ENDBUFF                                                       
         OC    BUFFNOW,BUFFNOW     IF WE RAN OUT OF SPACE                       
         BNZ   *+10                                                             
         MVC   AOGROPTS,BUFFNOW    CLEAR PRESENT LIST ADDRESS                   
         MVC   AOFFOPTS,BUFFNOW                                                 
         B     XIT                                                              
*                                                                               
**********************************************************************          
*              HANDLE OPTIONS AT OFFICE LEVEL                        *          
*                                                                    *          
**********************************************************************          
*                                                                               
OFFLEV   NTR1                                                                   
         MVI   FROM,C'O'                                                        
         MVC   BUFFNOW,AOFFOPTS                                                 
         CLC   GOEFFOFC,MYOFC                                                   
         BNE   OF3                                                              
*                                                                               
OF2      CLC   GOSELCUL,MYCUL      TEST FIRST TIME FOR COMPANY                  
         BNE   OF3                 YES                                          
         CLI   GOEFFOFC,C' '                                                    
         BNH   XIT                                                              
         CLI   WHICH,C'O'                                                       
         BE    XIT                                                              
         L     R2,AOFFOPTS                                                      
         LTR   R2,R2                                                            
         BZ    OF3                                                              
         BAS   RE,BUFTOUSR                                                      
         B     XIT                                                              
*                                                                               
OF3      XC    MYCLI,MYCLI         YES-FORCE RE-BUILDING OF BUFFER FOR          
         XC    MYPROD,MYPROD       LOWER LEVEL OPTIONS                          
         XC    MYJOB,MYJOB                                                      
         CLI   GOEFFOFC,C' '                                                    
         BNH   OF4                                                              
         CLI   WHICH,C'O'                                                       
         BE    OF4                                                              
*                                                                               
         MVI   NEWLEVEL,2                                                       
         BAS   RE,DONEW                                                         
*                                                                               
OF4      BAS   RE,ENDBUFF                                                       
         OC    BUFFNOW,BUFFNOW     IF WE RAN OUT OF SPACE                       
         BNZ   *+10                                                             
         MVC   AOFFOPTS,BUFFNOW    CLEAR PRESENT LIST ADDRESS                   
         MVC   ACLIOPTS,BUFFNOW                                                 
         B     XIT                                                              
*                                                                               
**********************************************************************          
*              HANDLE OPTIONS AT CLIENT LEVEL                        *          
*                                                                    *          
**********************************************************************          
*                                                                               
CLILEV   NTR1                                                                   
         CLI   GOSELCLI,X'41'      NOT NEEDED IF CLIENT NOT SELECTED            
         BL    XIT                                                              
         MVI   FROM,C'C'                                                        
         CLC   MYCLI,GOSELCLI      IF WE HAVE DONE CLIENT BEFORE                
         BNE   CL2                                                              
         L     R2,ACLIOPTS            AND THE OPTIONS ARE SAVED                 
         LTR   R2,R2                                                            
         BZ    CL2                                                              
         BAS   RE,BUFTOUSR            OUTPUT THESE TO USER                      
         B     XIT                    AND WE'RE DONE                            
*                                                                               
CL2      MVC   BUFFNOW,ACLIOPTS    SET BUFFER POINTER                           
*                                  CLEAR LOWER LEVELS                           
         XC    MYPROD,MYPROD                                                    
         XC    MYJOB,MYJOB                                                      
         CLI   WHICH,C'N'          DEALING WITH OLD FIRST                       
         BE    CL6                                                              
         L     R6,GOACLI           IS THE CLIENT RECORD AROUND                  
         LTR   R6,R6                                                            
         BNZ   CL4                                                              
         MVC   KEY,SPACES          NO - SO READ IT                              
         MVC   KEY(3),GOSELCUL                                                  
         MVC   KEY+3(6),GOSELCLI                                                
         BAS   RE,READ                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
         LA    R6,IO                                                            
*                                                                               
CL4      BAS   RE,OLDTOBUF         OUTPUT AND SAVE ELEMENTS                     
*                                                                               
CL6      CLI   WHICH,C'O'          NOW DEAL WITH NEW                            
         BE    CL8                                                              
         MVI   NEWLEVEL,3                                                       
         BAS   RE,DONEW                                                         
*                                                                               
CL8      BAS   RE,ENDBUFF                                                       
         OC    BUFFNOW,BUFFNOW     IF WE RAN OUT OF SPACE                       
         BNZ   *+10                                                             
         MVC   ACLIOPTS,BUFFNOW    CLEAR PRESENT LIST ADDRESS                   
         MVC   APROOPTS,BUFFNOW                                                 
         B     XIT                                                              
*                                                                               
**********************************************************************          
*              HANDLE OPTIONS AT PRODUCT LEVEL                       *          
*                                                                    *          
**********************************************************************          
*                                                                               
PRODLEV  NTR1                                                                   
         MVI   FROM,C'P'                                                        
         CLI   GOSELPRO,X'41'      NOT NEEDED IF PRODUCT NOT SELECTED           
         BL    XIT                                                              
         CLC   MYPROD,GOSELPRO     IF WE HAVE DONE PRODUCT BEFORE               
         BNE   PR2                                                              
         L     R2,APROOPTS            AND THE OPTIONS ARE SAVED                 
         LTR   R2,R2                                                            
         BZ    PR2                                                              
         BAS   RE,BUFTOUSR            OUTPUT THESE TO USER                      
         B     XIT                    AND WE'RE DONE                            
*                                                                               
PR2      MVC   BUFFNOW,APROOPTS    SET BUFFER POINTER                           
*                                  CLEAR LOWER LEVELS                           
         XC    MYJOB,MYJOB                                                      
         CLI   WHICH,C'N'          DEALING WITH OLD FIRST                       
         BE    PR6                                                              
         L     R6,GOAPRO           IS THE PRODUCT RECORD AROUND?                
         LTR   R6,R6                                                            
         BNZ   PR4                                                              
         MVC   KEY,SPACES          NO - SO READ IT                              
         MVC   KEY(3),GOSELCUL                                                  
         MVC   KEY+3(6),GOSELCLI                                                
         ZIC   R1,LLEVA                                                         
         LA    R1,KEY+3(R1)                                                     
         MVC   0(6,R1),GOSELPRO                                                 
         BAS   RE,READ                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
         LA    R6,IO                                                            
*                                                                               
PR4      BAS   RE,OLDTOBUF         OUTPUT AND SAVE ELEMENTS                     
*                                                                               
PR6      CLI   WHICH,C'O'          NOW DEAL WITH NEW                            
         BE    PR8                                                              
         MVI   NEWLEVEL,4                                                       
         BAS   RE,DONEW                                                         
*                                                                               
PR8      BAS   RE,ENDBUFF                                                       
         OC    BUFFNOW,BUFFNOW     IF WE RAN OUT OF SPACE                       
         BNZ   *+10                                                             
         MVC   APROOPTS,BUFFNOW    CLEAR PRESENT LIST ADDRESS                   
         MVC   AJOBOPTS,BUFFNOW                                                 
         B     XIT                                                              
*                                                                               
**********************************************************************          
*              HANDLE OPTIONS AT JOB LEVEL                           *          
*                                                                    *          
**********************************************************************          
*                                                                               
JOBLEV   NTR1                                                                   
         MVI   FROM,C'J'                                                        
         CLI   GOSELJOB,X'41'      NOT NEEDED IF JOB NOT SELECTED               
         BL    XIT                                                              
         CLC   MYJOB,GOSELJOB      IF WE HAVE DONE JOB BEFORE                   
         BNE   JB2                                                              
         L     R2,AJOBOPTS            AND THE OPTIONS ARE SAVED                 
         LTR   R2,R2                                                            
         BZ    JB2                                                              
         BAS   RE,BUFTOUSR            OUTPUT THESE TO USER                      
         B     XIT                    AND WE'RE DONE                            
*                                                                               
JB2      MVC   BUFFNOW,AJOBOPTS    SET BUFFER POINTER                           
         CLI   WHICH,C'N'          DEALING WITH OLD FIRST                       
         BE    JB6                                                              
         L     R6,GOAJOB           IS THE JOB RECORD AROUND?                    
         LTR   R6,R6                                                            
         BNZ   JB4                                                              
         MVC   KEY,SPACES          NO - SO READ IT                              
         MVC   KEY(3),GOSELCUL                                                  
         MVC   KEY+3(6),GOSELCLI                                                
         ZIC   R1,LLEVA                                                         
         LA    R1,KEY+3(R1)                                                     
         MVC   0(6,R1),GOSELPRO                                                 
         ZIC   R1,LLEVB                                                         
         LA    R1,KEY+3(R1)                                                     
         MVC   0(6,R1),GOSELJOB                                                 
         BAS   RE,READ                                                          
         BE    JB3                                                              
         XC    ACTJOB,ACTJOB       ZERO JOB TO BE SAVED IN BLOCK                
         B     JB8                 SKIP NEW OPTIONS-JOB DOES NOT EXIST          
*                                                                               
JB3      LA    R6,IO                                                            
*                                                                               
JB4      BAS   RE,OLDTOBUF         OUTPUT AND SAVE ELEMENTS                     
*                                                                               
JB6      CLI   WHICH,C'O'          NOW DEAL WITH NEW                            
         BE    JB8                                                              
         MVI   NEWLEVEL,5                                                       
         BAS   RE,DONEW                                                         
*                                                                               
JB8      BAS   RE,ENDBUFF                                                       
         OC    BUFFNOW,BUFFNOW     IF WE RAN OUT OF SPACE                       
         BNZ   *+10                                                             
         MVC   AJOBOPTS,BUFFNOW    CLEAR PRESENT LIST ADDRESS                   
         B     XIT                                                              
*                                                                               
**********************************************************************          
*              ROUTINE CONVERTS OLD RECORDS TO BUFFER ELEMENTS       *          
*                                                                    *          
*              R6=A(RECORD)                                          *          
**********************************************************************          
*                                                                               
OLDTOBUF NTR1                                                                   
         MVI   ELCODE,0                                                         
         LA    R4,BUFFEL                                                        
         USING OPTELD,R4                                                        
         BAS   RE,GETEL                                                         
         B     OTB4                                                             
*                                                                               
OTB2     BAS   RE,NEXTEL                                                        
*                                                                               
OTB4     BNE   XIT                                                              
         CLI   0(R6),ACMPELQ                                                    
         BE    OTBCOMP                                                          
         CLI   0(R6),ACPRELQ       PICK OUT USEFUL ELEMENTS                     
         BE    OTBPROF                                                          
         CLI   0(R6),ACXPELQ                                                    
         BE    OTBEXTRA                                                         
         CLI   0(R6),ACRLELQ                                                    
         BE    OTBRULE                                                          
         B     OTB2                                                             
*                                                                               
OTBCOMP  MVI   FROM+1,C'C'         OLD COMPANY ELEMENT                          
         MVI   OPTFROM,C'C'                                                     
         BAS   RE,COMP                                                          
         B     OTB2                                                             
*                                                                               
OTBPROF  MVI   FROM+1,C'P'         OLD PROFILE                                  
         MVI   OPTFROM,C'P'                                                     
         BAS   RE,PROF                                                          
         B     OTB2                                                             
*                                                                               
OTBEXTRA MVI   FROM+1,C'X'         OLD EXTRA PROFILE                            
         MVI   OPTFROM,C'X'                                                     
         BAS   RE,EXTRA                                                         
         B     OTB2                                                             
*                                                                               
OTBRULE  MVI   FROM+1,C'R'         OLD RULE                                     
         MVI   OPTFROM,C'R'                                                     
         BAS   RE,RULE                                                          
         B     OTB2                                                             
         EJECT                                                                  
**********************************************************************          
*              ROUTINE TO HANDLE OLD COMPANY ELEMENT                 *          
*                                                                    *          
**********************************************************************          
*                                                                               
COMP     NTR1                                                                   
         USING ACCOMPD,R6                                                       
         USING OPTELD,R4                                                        
         MVI   OPTMG,0                                                          
         MVI   OPTMED,0                                                         
         MVI   OPTWG,0                                                          
         XC    OPTWORK,OPTWORK                                                  
*                                                                               
COMP2    MVI   OPTFIELD,OPNAUTTA                                                
         MVI   OPTLEN,9                                                         
         MVI   OPTDATA,C'Y'        DEFAULT IS AUTOMATIC EST T/A                 
         TM    ACMPSTAT,X'01'      TEST IF T/A SUPPRESSED                       
         BZ    *+8                                                              
         MVI   OPTDATA,C'N'        YES                                          
         CLI   OPTDATA,C'Y'        TEST DEFAULT OPTION                          
         BE    *+8                                                              
         BAS   RE,OPTOUT                                                        
*                                                                               
COMP4    MVI   OPTFIELD,OPNSUPST   SUPPRESS STICKY LABELS                       
         MVI   OPTLEN,9                                                         
         MVI   OPTDATA,C'N'        DEFAULT IS PRODUCE THE LABELS                
         TM    ACMPSTAT,X'02'      TEST TO SUPPRESS STICKY LABELS               
         BNO   *+8                                                              
         MVI   OPTDATA,C'Y'                                                     
         CLI   OPTDATA,C'N'                                                     
         BE    *+8                                                              
         BAS   RE,OPTOUT                                                        
         B     XIT                                                              
         EJECT                                                                  
**********************************************************************          
*              ROUTINE TO HANDLE OLD PROFILE                         *          
*                                                                    *          
**********************************************************************          
*                                                                               
PROF     NTR1                                                                   
         USING ACPROFD,R6                                                       
         USING OPTELD,R4                                                        
         MVI   OPTMG,0                                                          
         MVI   OPTMED,0                                                         
         MVI   OPTWG,0                                                          
         XC    OPTWORK,OPTWORK                                                  
         CLI   ACPRBILL,0          BILLING TYPE                                 
         BE    PROF4                                                            
         MVI   OPTFIELD,OPNBT                                                   
         MVI   OPTLEN,9                                                         
         MVC   OPTDATA(1),ACPRBILL                                              
         OC    ACPRBLAM(4),ACPRBLAM                                             
         BZ    PROF2                                                            
         MVC   OPTDATA+1(4),ACPRBLAM                                            
         MVI   OPTLEN,13                                                        
*                                                                               
PROF2    BAS   RE,OPTOUT                                                        
*                                                                               
PROF4    CLI   ACPRUNBL,X'41'      UNBILLABLE WORK CODES                        
         BL    XIT                                                              
         MVC   OPTDATA(12),ACPRUNBL                                             
         MVI   OPTFIELD,OPNUNBWC                                                
         MVI   OPTLEN,20                                                        
         CLI   ACPRUNBL+10,X'40'                                                
         BH    PROF6                                                            
         MVI   OPTLEN,18                                                        
         CLI   ACPRUNBL+8,X'40'                                                 
         BH    PROF6                                                            
         MVI   OPTLEN,16                                                        
         CLI   ACPRUNBL+6,X'40'                                                 
         BH    PROF6                                                            
         MVI   OPTLEN,14                                                        
         CLI   ACPRUNBL+4,X'40'                                                 
         BH    PROF6                                                            
         MVI   OPTLEN,12                                                        
         CLI   ACPRUNBL+2,X'40'                                                 
         BH    PROF6                                                            
         MVI   OPTLEN,10                                                        
*                                                                               
PROF6    BAS   RE,OPTOUT                                                        
         B     XIT                                                              
*                                                                               
OPTOUT   LR    RF,RE                                                            
         BAS   RE,ELTOBUF          TRY AND POST TO BUFFER                       
         BAS   RE,APPLYEL          APPLY ELEMENT TO GOBLOCK                     
         LR    RE,RF                                                            
         BR    RE                                                               
*                                                                               
         DROP  R4,R6                                                            
         EJECT                                                                  
**********************************************************************          
*              ROUTINE TO HANDLE OLD EXTRA PROFILE                   *          
*                                                                    *          
**********************************************************************          
*                                                                               
EXTRA    NTR1                                                                   
         USING ACXPROFD,R6                                                      
         USING OPTELD,R4                                                        
         MVI   OPTMG,0                                                          
         MVI   OPTMED,0                                                         
         MVI   OPTWG,0                                                          
         XC    OPTWORK,OPTWORK                                                  
*                                                                               
         MVI   OPTFIELD,OPNDUE     NUMBER OF DAYS DUE                           
         MVI   OPTLEN,9                                                         
         ZAP   DUB,ACXPDUE                                                      
         CVB   R1,DUB                                                           
         STC   R1,OPTDATA                                                       
         CLI   OPTDATA,10          (DEFAULT IS 10)                              
         BE    *+8                                                              
         BAS   RE,OPTOUT                                                        
*                                                                               
         MVI   OPTFIELD,OPNOVPER   PERCENT OF EST                               
         MVI   OPTLEN,12                                                        
         ZAP   OPTDATA(4),ACXPOVER                                              
         CP    ACXPOVER,=P'10000'  (DEFAULT IS 100.00%)                         
         BE    *+8                                                              
         BAS   RE,OPTOUT                                                        
*                                                                               
         MVI   OPTFIELD,OPNMIN     MINIMUM BILL AMOUNT                          
         MVI   OPTLEN,12                                                        
         ZAP   OPTDATA(4),ACXPLOW                                               
         CP    ACXPLOW,=P'5000'    (DEFAULT VALUE IS $50.00)                    
         BE    *+8                                                              
         BAS   RE,OPTOUT                                                        
*                                                                               
         MVI   OPTFIELD,OPNCLSUM   CLIENT SUMMARY ON BILLS?                     
         CLI   FROM,C'P'                                                        
         BE    EXTRA2              (NOT SET AT PRODUCT LEVEL)                   
         MVI   OPTLEN,9                                                         
         MVC   OPTDATA,ACXPSUM                                                  
         CLI   OPTDATA,C'Y'        (DEFAULT IS YES)                             
         BE    *+8                                                              
         BAS   RE,OPTOUT                                                        
*                                                                               
EXTRA2   MVI   OPTFIELD,OPNPRSUM   PRODUCT SUMMARY ON BILLS?                    
         CLI   FROM,C'P'                                                        
         BNE   EXTRA4              (ONLY SET AT PRODUCT LEVEL)                  
         MVI   OPTLEN,9                                                         
         MVC   OPTDATA,ACXPSUM                                                  
         CLI   OPTDATA,C'Y'        (DEFAULT IS YES)                             
         BE    *+8                                                              
         BAS   RE,OPTOUT                                                        
*                                                                               
EXTRA4   MVI   OPTFIELD,OPNET      PAYMENTS ARE NET                             
         MVI   OPTLEN,9                                                         
         MVI   OPTDATA,C'Y'                                                     
         CLI   ACXPNET,C'N'        (DEFAULT IS NO)                              
         BE    *+8                                                              
         BAS   RE,OPTOUT                                                        
*                                                                               
         MVI   OPTFIELD,OPNSUPAG   SUPPRESS AGENCY COMMISSION                   
         MVI   OPTLEN,9            ON NET PAYMENTS                              
         MVI   OPTDATA,C'Y'                                                     
         CLI   ACXPNET,C'S'        (DEFAULT IS NO)                              
         BNE   *+8                                                              
         BAS   RE,OPTOUT                                                        
*                                                                               
         MVI   OPTFIELD,OPNDET     DETAIL LINES ON PROGRESSIVE BILLS            
         MVI   OPTDATA,C'N'                                                     
         CLI   ACXPDET,C'Y'        (DEFAULT IS YES)                             
         BE    *+8                                                              
         BAS   RE,OPTOUT                                                        
*                                                                               
         MVI   OPTFIELD,OPNWCNES   SHOW WC ON EST BILLING                       
         MVI   OPTDATA,C'Y'                                                     
         CLI   ACXPEDET,C'N'       (DEFAULT IS NO)                              
         BE    *+8                                                              
         BAS   RE,OPTOUT                                                        
*                                                                               
         MVI   OPTFIELD,OPNCD      PASS CASH DISCOUNT TO CLIENT                 
         MVI   OPTDATA,C'N'                                                     
         CLI   ACXPCD,X'41'                                                     
         BL    EXTRA6                                                           
         CLI   ACXPCD,C'Y'         (DEFAULT IS YES)                             
         BE    EXTRA6                                                           
         BAS   RE,OPTOUT                                                        
*                                                                               
EXTRA6   MVI   OPTFIELD,OPNEEDES   ESTIMATE NEEDED FOR BILLS?                   
         MVC   OPTDATA,ACXPEST                                                  
         CLI   OPTDATA,X'41'                                                    
         BL    *+8                                                              
         BAS   RE,OPTOUT                                                        
*                                                                               
         MVI   OPTFIELD,OPNAUTTA   AUTOMATIC TURN-AROUNDS?                      
         MVI   OPTDATA,C'N'                                                     
         TM    ACXPST1,X'80'                                                    
         BNO   *+8                                                              
         BAS   RE,OPTOUT                                                        
*                                                                               
         MVI   OPTFIELD,OPNESCM    ESTIMATE BILLS COMMISSIONABLE                
         MVI   OPTDATA,C'N'                                                     
         TM    ACXPST1,X'40'                                                    
         BNO   *+8                                                              
         BAS   RE,OPTOUT                                                        
*                                                                               
         MVI   OPTFIELD,117        TRANSFER TO SPOT/PRINT                       
         MVI   OPTDATA,C'N'                                                     
         TM    ACXPST1,X'20'                                                    
         BNO   *+8                                                              
         BAS   RE,OPTOUT                                                        
*                                                                               
         MVI   OPTFIELD,OPNPROD    PRODUCTION=NO OPTION                         
         MVI   OPTDATA,C'N'                                                     
         TM    ACXPST1,X'10'                                                    
         BNO   *+8                                                              
         BAS   RE,OPTOUT                                                        
*                                                                               
         MVI   OPTFIELD,119        GROSS + CASH DISCOUT TO COSTING              
         MVI   OPTDATA,C'Y'                                                     
         TM    ACXPST1,X'08'                                                    
         BNO   *+8                                                              
         BAS   RE,OPTOUT                                                        
*                                                                               
         MVI   OPTFIELD,OPNEWJUN   NEW JOBS ARE UNAPPROVED                      
         MVI   OPTDATA,C'Y'                                                     
         TM    ACXPST1,X'02'                                                    
         BNO   *+8                                                              
         BAS   RE,OPTOUT                                                        
*                                                                               
         MVI   OPTFIELD,OPNAUTUN   AUTO UNHOLD ON BILLING                       
         MVI   OPTDATA,C'Y'                                                     
         TM    ACXPST2,X'80'                                                    
         BNO   *+8                                                              
         BAS   RE,OPTOUT                                                        
*                                                                               
         MVI   OPTFIELD,OPNFILT2   FILTER 2 WHEN EST TO TOT                     
         MVC   OPTDATA,ACXPREP                                                  
         CLI   OPTDATA,0                                                        
         BE    *+8                                                              
         BAS   RE,OPTOUT                                                        
*                                                                               
         MVI   OPTFIELD,OPNEWBT    NEW BILL TYPE FOR EST                        
         MVC   OPTDATA,ACXPBILL                                                 
         CLI   OPTDATA,0                                                        
         BE    *+8                                                              
         BAS   RE,OPTOUT                                                        
         B     XIT                                                              
         DROP  R4,R6                                                            
         EJECT                                                                  
**********************************************************************          
*              ROUTINE TO HANDLE OLD RULE ELEMENT                    *          
*                                                                    *          
**********************************************************************          
*                                                                               
RULE     NTR1                                                                   
         USING ACRULED,R6                                                       
         USING OPTELD,R4                                                        
         MVI   OPTMG,0                                                          
         MVI   OPTMED,0                                                         
         CLI   FROM,C'J'           TEST FOR JOB LEVEL                           
         BE    *+10                YES-MEDIA IS REDUNDANT FOR JOB LEVEL         
         MVC   OPTMED,ACRLMED                                                   
         MVI   OPTWG,0                                                          
         MVC   OPTWORK,ACRLWORK                                                 
         CLI   ACRLCOMM,X'FF'      IGNORE IF THIS IS OVERRIDE                   
         BE    RULEX                                                            
         ZAP   DUB,ACRLCOMM                                                     
         CVB   R1,DUB                                                           
         CLI   ACRLLEN,X'0B'                                                    
         BL    RULE2                                                            
         TM    ACRLSTAT,X'80'      IS IT ALREADY 4 DEC?                         
         BO    RULE4                                                            
*                                                                               
RULE2    MH    R1,=H'100'          ELSE CONVERT TO 4 DEC                        
*                                                                               
RULE4    MVI   OPTFIELD,OPNCOM     AGENCY COMMISSION                            
         MVI   OPTLEN,12                                                        
         CVD   R1,DUB                                                           
         MVC   OPTDATA(4),DUB+4                                                 
         BAS   RE,OPTOUT                                                        
*                                                                               
RULEX    B     XIT                                                              
         DROP  R4,R6                                                            
         EJECT                                                                  
**********************************************************************          
*              ROUTINE CONTROLS I/O OF NEW OPTION RECORDS            *          
*                                                                    *          
*              INPUT               NEWLEVEL 0=COMPANY 1=OG ETC       *          
*                                                                    *          
**********************************************************************          
*                                                                               
DONEW    NTR1  ,                                                                
         LA    R6,KEY                                                           
         USING ACOPKEY,R6                                                       
         XC    ACOPKEY,ACOPKEY                                                  
         MVI   ACOPRTYP,ACOPEQU                                                 
         MVI   ACOPSREC,ACOPSEQU                                                
         MVC   ACOPCUL,GOSELCUL                                                 
         CLI   NEWLEVEL,1                                                       
         BL    DONEW10                                                          
         BE    DONEW1                                                           
         CLI   NEWLEVEL,3                                                       
         BL    DONEW2                                                           
         BE    DONEW3                                                           
         CLI   NEWLEVEL,5                                                       
         BL    DONEW4                                                           
         BE    DONEW5                                                           
         DC    H'0'                                                             
*                                                                               
DONEW1   MVC   ACOPOG,GOEFFOG      OGROUP LEVEL (1)                             
         B     DONEW10                                                          
*                                                                               
DONEW2   MVC   ACOPOFC,GOEFFOFC    OFFICE LEVEL (2)                             
         B     DONEW10                                                          
*                                                                               
DONEW3   MVC   ACOPCLI,GOSELCLI    CLIENT LEVEL (3)                             
         B     DONEW10                                                          
*                                                                               
DONEW4   MVC   ACOPCLI,GOSELCLI    PRODUCT LEVEL (4)                            
         MVC   ACOPPRO,GOSELPRO                                                 
         B     DONEW10                                                          
*                                                                               
DONEW5   MVC   ACOPCLI,GOSELCLI    JOB LEVEL (5)                                
         MVC   ACOPPRO,GOSELPRO                                                 
         MVC   ACOPJOB,GOSELJOB                                                 
*                                                                               
DONEW10  CLI   OKSTAT,1            DO WE HAVE A OPTION KEY BUFFER?              
         BNE   DONEW15                                                          
         L     R2,AOKBUFF                                                       
         CLC   ACOPOG(OPTKLEN),0(R2)  CHECK KEY NOT PAST EOB                    
         BH    DONEW15                                                          
         LA    R2,OPTBUFFL(R2)     SEE IF KEY WAS IN BUFFER                     
         L     R3,NOPTKEYS                                                      
         LTR   R3,R3                                                            
         BZ    DONEWX              (NO KEYS AT ALL)                             
         GOTO1 BINSRCH,DMCB,(X'02',ACOPOG),(R2),(R3),OPTBUFFL,         X        
               (0,OPTKLEN),(R3)                                                 
         CLI   DMCB,X'01'                                                       
         BE    DONEWX              NO KEY SO SKIP IO                            
         L     R2,0(R1)                                                         
         CLC   ACOPOG(ACOPMG-ACOPOG),0(R2) TEST SAME LEVEL                      
         BNE   DONEWX              NO-NO OPTIONS                                
*                                                                               
         MH    R3,=Y(OPTBUFFL)     COMPUTE END OF OPTION KEYS                   
         A     R3,AOKBUFF                                                       
         LA    R3,OPTBUFFL-1(R3)                                                
*                                                                               
DONEW11  MVC   ACOPOG(OPTKLEN),0(R2)  SET RETURNED KEY                          
         ICM   R5,15,OPTKLEN(R2)   GET A(DATA)                                  
         BZ    DONEW12             NOT THERE                                    
*                                                                               
         LA    RE,ACRECORD         RE=A(MOVE DESTINATION)                       
         SR    R1,R1               R1=L'SOURCE DATA                             
         ICM   R1,3,0(R5)          GET LENGTH OF DATA + HEADER                  
         BCTR  R1,0                ADJUST FOR HEADER                            
         BCTR  R1,0                                                             
         LA    RF,1(R1)            ADD ONE BACK IN FOR EOR                      
         LR    R4,RF               SAVE DATA LEN IN R4                          
         LA    R0,2(R5)            R0=A(MOVE SOURCE)                            
         MVCL  RE,R0               ATTACH DATA TO RECORD                        
         AH    R4,DATADISP         COMPUTE FULL REC LEN                         
         STCM  R4,3,ACLENGTH                                                    
         B     DONEW14                                                          
*                                                                               
DONEW12  BAS   RE,HIGH                                                          
*                                                                               
DONEW14  MVI   FROM+1,C'O'                                                      
         BAS   RE,NEWTOBUF                                                      
         LA    R2,OPTBUFFL(R2)     NEXT ENTRY                                   
         CR    R2,R3               TEST PAST END OF KEYS                        
         BH    DONEWX              YES-ALL DONE                                 
         CLC   ACOPOG(ACOPMG-ACOPOG),0(R2) TEST SAME LEVEL                      
         BE    DONEW11             YES                                          
         B     DONEWX              NO-ALL DONE                                  
*                                                                               
DONEW15  BAS   RE,HIGH                                                          
         B     DONEW18                                                          
*                                                                               
DONEW16  BAS   RE,SEQ                                                           
*                                                                               
DONEW18  CLC   ACOPKEY(ACOPMG-ACOPKEY),KEYSAVE                                  
         BNE   DONEWX                                                           
         MVI   FROM+1,C'O'         SET TO O=OPTION (NEW)                        
         BAS   RE,NEWTOBUF                                                      
         B     DONEW16                                                          
*                                                                               
DONEWX   B     XIT                                                              
         DROP  R6                                                               
         EJECT                                                                  
**********************************************************************          
*              ROUTINE CONVERTS NEW RECORDS TO BUFFER ELEMENTS       *          
*                                                                    *          
*              INPUT               R6=A(RECORD)                      *          
*              OUTPUT              BUFFEL                            *          
*                                                                    *          
**********************************************************************          
*                                                                               
NEWTOBUF NTR1                                                                   
         LR    R4,R6                                                            
         USING ACOPKEY,R4                                                       
         LA    R5,BUFFEL                                                        
         USING OPTELD,R5                                                        
         MVI   ELCODE,ACOPELQ                                                   
         BAS   RE,GETEL                                                         
         B     NTB4                                                             
*                                                                               
NTB2     BAS   RE,NEXTEL                                                        
*                                                                               
NTB4     BNE   NTB6                                                             
         USING ACOPD,R6                                                         
         MVC   OPTFIELD,ACOPNUM    FIELD NUMBER                                 
         ZIC   R1,ACOPLEN                                                       
         SH    R1,=H'8'                                                         
         STC   R1,OPTLEN                                                        
         MVC   OPTMG,ACOPMG                                                     
         MVC   OPTMED,ACOPMED                                                   
         MVC   OPTWG,ACOPWG                                                     
         MVC   OPTWORK,ACOPWORK                                                 
         MVI   OPTFROM,C'O'                                                     
         ZIC   R1,ACOPLEN                                                       
         SH    R1,=Y(ACOPDATA-ACOPD+1)                                          
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   OPTDATA(0),ACOPDATA                                              
         CLI   FROM,C'D'           IF NOT A DEFAULT 'RECORD'                    
         BE    *+8                                                              
         BAS   RE,ELTOBUF             TRY AND POST AN ELEMENT                   
         BAS   RE,APPLYEL                                                       
         B     NTB2                                                             
*                                                                               
NTB6     B     XIT                                                              
         DROP  R4,R5,R6                                                         
*                                                                               
**********************************************************************          
*              ROUTINE TAKES SAVE BUFFER ELEMENTS AND OUTPUTS THESE  *          
*              TO USER                                               *          
*                                                                    *          
*              INPUT               R2=A(ELEMENT LIST)                *          
*                                                                    *          
**********************************************************************          
*                                                                               
BUFTOUSR NTR1                                                                   
*                                                                               
BTU2     CLI   0(R2),0                                                          
         BE    XIT                                                              
         ZIC   R1,1(R2)            ELEMENT LENGTH                               
         LTR   R1,R1                                                            
         BNZ   *+6                                                              
         DC    H'0'                                                             
         XC    BUFFEL,BUFFEL                                                    
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   BUFFEL(0),0(R2)                                                  
         BAS   RE,APPLYEL                                                       
         LA    R2,1(R1,R2)                                                      
         B     BTU2                                                             
*                                                                               
**********************************************************************          
*              ROUTINE CHECKS FOR OVERRIDES FROM WORK CODE RECORD    *          
*                                                                    *          
**********************************************************************          
*                                                                               
WORKOVER CLI   WHICH,C'N'          ONLY DOING NEW OPTIONS?                      
         BE    WORKO2              YES, DON'T GET ALL OVERRIDES                 
         TM    GOEFFWST,X'80'      SUPPRESS BILLING DETAIL?                     
         BNO   *+8                 NO                                           
         MVI   GO1LINBL,C'Y'       YES, ONE LINE                                
         TM    GOEFFWST,X'40'      SUPPRESS P1 DETAIL?                          
         BNO   *+8                 NO                                           
         MVI   GO1LINP1,C'Y'       YES, ONE LINE                                
*                                                                               
WORKO2   TM    GOEFFWST,X'02'      IF NON-COMMISSIONABLE, CLEAR RATE            
         BNO   *+10                                                             
         ZAP   GOAGYCOM,=P'0'                                                   
         BR    RE                                                               
*                                                                               
**********************************************************************          
*              ROUTINE APPLIES AND POSTS OPTION ELEMENT              *          
*                                                                    *          
*              BUFFEL=DATA                                           *          
*                                                                    *          
**********************************************************************          
*                                                                               
APPLYEL  NTR1                                                                   
         CLI   GOSELLEV,0          APPLY IF PROCESSING ALL LEVELS               
         BE    APL1                                                             
         CLC   GOSELLEV,FROM       OR IF THIS ONE REQUESTED                     
         BNE   XIT                                                              
*                                                                               
APL1     LA    R2,BUFFEL                                                        
         USING OPTELD,R2                                                        
         MVC   FROM+1(1),OPTFROM                                                
         MVI   FROM+2,C'*'         SET MEDIA MATCH AND FILTER                   
         CLI   OPTMG,0                                                          
         BE    APL2                                                             
         CLI   OPTMG,X'FF'         TEST FOR MEDIA RECORD                        
         BE    APL2                YES                                          
         MVI   FROM+2,C'G'                                                      
         CLC   OPTMG,GOEFFMG                                                    
         BNE   XIT                                                              
         B     APL4                                                             
*                                                                               
APL2     CLI   OPTMED,0                                                         
         BE    APL4                                                             
         MVI   FROM+2,C'M'                                                      
         CLC   OPTMED,GOEFFMED                                                  
         BNE   XIT                                                              
*                                                                               
APL4     MVI   FROM+3,C'*'         SET WORK MATCH AND FILTER                    
         CLI   OPTWG,0                                                          
         BE    APL6                                                             
         CLI   OPTWG,X'FF'         TEST FOR WORKCODE RECORD                     
         BE    APL6                                                             
         MVI   FROM+3,C'G'                                                      
         CLC   OPTWG,GOEFFWG                                                    
         BNE   XIT                                                              
         B     APL8                                                             
*                                                                               
APL6     CLI   OPTWORK,0                                                        
         BE    APL8                                                             
         MVI   FROM+3,C'W'                                                      
         CLC   OPTWORK,GOEFFWC                                                  
         BNE   XIT                                                              
*                                                                               
APL8     OC    GOAFROM,GOAFROM     DID USER PROVIDE FROM BUFFER                 
         BZ    APL10                                                            
         ZIC   R1,OPTFIELD         YES - USE FIELD NUMBER                       
         SLL   R1,2                                                             
         A     R1,GOAFROM          TO DISPLACE INTO FROM BUFFER                 
         MVC   0(4,R1),FROM        AND RETURN FROM VALUE                        
*                                                                               
APL10    LA    R3,OUTTAB           LOOK UP FIELD IN OUTTAB                      
         USING OUTTABD,R3                                                       
*                                                                               
APL11    CLC   OPTFIELD,OUTOPN     MATCH ON OPTION NUMBER                       
         BE    APL12                                                            
         LA    R3,OUTTABL(R3)                                                   
         CLI   OUTOPN,X'FF'                                                     
         BNE   APL11                                                            
         DC    H'0'                                                             
*                                                                               
APL12    LA    R4,GOPTIONS                                                      
         TM    OUTINDS,OUTIEXT+OUTIPAN+OUTIBIL OUTSIDE GOBLOCK?                 
         BZ    APL14               NO                                           
         LA    RE,GOAEXT           YES, A(EXTENSION BLOCK)                      
         TM    OUTINDS,OUTIEXT     OUTPUT TO EXTENSION?                         
         BO    APL13               YES                                          
         LA    RE,GOAPAN           NO, A(PANEL BLOCK)                           
         TM    OUTINDS,OUTIPAN     OUTPUT TO PANBLOCK?                          
         BO    APL13               YES                                          
         LA    RE,GOABEXT          NO, A(BILL EXTENSION BLOCK)                  
APL13    ICM   R4,15,0(RE)         TEST/GET A(OTHER BLOCK)                      
         BZ    APLX                NONE-EXIT                                    
*                                                                               
APL14    MVC   HALF,OUTDISP                                                     
         AH    R4,HALF             DISPLACE INTO OPTIONS/PANELS                 
         ZIC   R1,OUTMAX           PICK UP LENGTH                               
         BCTR  R1,0                                                             
         EX    R1,OUTCLEAR                                                      
         ZIC   R1,OPTLEN           PICK UP ELEMENT LENGTH                       
         SH    R1,=H'9'                                                         
         EX    R1,OUTMOVE                                                       
*                                                                               
APLX     B     XIT                                                              
*                                                                               
OUTCLEAR XC    0(0,R4),0(R4)       PRECLEAR                                     
OUTMOVE  MVC   0(0,R4),OPTDATA     MOVE OUT DATA                                
         DROP  R2,R3                                                            
         EJECT                                                                  
**********************************************************************          
*              BUFFER HANDLING ROUTINES                              *          
*                                                                    *          
**********************************************************************          
*                                                                               
ELTOBUF  NTR1                      TRY AND POST AN ELEMENT                      
*                                  ELEMENT IS ALREADY IN BUFFEL                 
         MVC   BUFFLEN,BUFFEL+1    ITS LENGTH IS AT BUFFEL+1                    
         BAS   RE,POSTBUFF                                                      
         B     XIT                                                              
*                                                                               
POSTBUFF NTR1                                                                   
*                                  ROUTINE PUTS BUFFLEN BYTES                   
*                                  FORM BUFFEL INTO BUFFER                      
*                                  BUFFNOW IS UPDATED                           
*                                                                               
         L     R2,BUFFNOW          IS THERE ROOM FOR BUFFLEN+4 BYTES?           
         LTR   R2,R2                                                            
         BZ    XIT                                                              
         ZIC   R1,BUFFLEN                                                       
         LA    RE,4(R1,R2)                                                      
         C     RE,AENDBUFF                                                      
         BNH   POSTB2                                                           
         XC    BUFFNOW,BUFFNOW                                                  
         LA    R1,1                                                             
         LTR   R1,R1                                                            
         B     ENDXIT                                                           
*                                                                               
POSTB2   BCTR  R1,0                                                             
         EX    R1,ELOUT                                                         
         LA    R1,1(R1)                                                         
         AR    R2,R1                                                            
         ST    R2,BUFFNOW                                                       
         SR    R1,R1                                                            
         LTR   R1,R1                                                            
         B     ENDXIT                                                           
*                                                                               
ELOUT    MVC   0(0,R2),BUFFEL                                                   
*                                                                               
ENDBUFF  NTR1                                                                   
         OC    BUFFNOW,BUFFNOW                                                  
         BZ    ENDXIT                                                           
         L     R2,BUFFNOW                                                       
         XC    0(4,R2),0(R2)                                                    
         LA    R2,1(R2)                                                         
         ST    R2,BUFFNOW                                                       
ENDXIT   B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
* INTERFACE TO DATAMGR - EMULATED RECORDS                             *         
***********************************************************************         
*                                                                               
*                                                                               
READ     MVC   COMMAND,DMREAD                                                   
         B     ACCALL                                                           
*                                                                               
HIGH     MVC   COMMAND,DMRDHI                                                   
         MVC   KEYSAVE,KEY                                                      
         B     ACCALL                                                           
*                                                                               
SEQ      MVC   COMMAND,DMRSEQ                                                   
*                                                                               
ACCALL   NTR1                                                                   
         MVI   ANYIO,C'Y'                                                       
         GOTO1 GOADM,DMCB,(0,COMMAND),(0,ACCOUNT),KEY,IO                        
         BE    XIT                                                              
         TM    8(R1),FF-RECNOTF                                                 
         BZ    *+6                                                              
         DC    H'0'                                                             
         CLI   8(R1),0                                                          
         B     XIT                                                              
*                                                                               
***********************************************************************         
* INTERFACE TO DATAMGR - NONE EMULATED = REGULAR RECORDS              *         
***********************************************************************         
*                                                                               
XGET     NTR1  ,                   GET RECORD                                   
*        LTR   R2,R1               SET & TEST I/O ADDRESS PASSED                
*        BNZ   *+8                                                              
         LA    R2,KEY              NO - USE DEFAULT I/O AREA                    
         GOTO1 GOADM,DMCB,GETREC,ACCMST,XDA,(R2),XWORK                          
         BE    *+6                                                              
         DC    H'0'                                                             
         B     XIT                                                              
*                                                                               
XREAD    LA    RF,DMREAD           READ DIR                                     
         B     XGOIO                                                            
*                                                                               
XHIGH    LA    RF,DMRDHI           READ HIGH DIR                                
         MVC   KEYSAVE,KEY                                                      
         B     XGOIO                                                            
*                                                                               
XSEQ     LA    RF,DMRSEQ           READ SEQUENTIAL                              
*                                                                               
XGOIO    NTR1  ,                                                                
         MVI   ANYIO,C'Y'                                                       
*        LTR   R2,R1               SET & TEST I/O ADDRESS PASSED                
*        BNZ   *+8                                                              
         LA    R2,KEY              NO - USE DEFAULT I/O AREA                    
         GOTO1 GOADM,DMCB,(0,(RF)),ACCDIR,KEY,(R2)                              
         MVC   XDA,ACCKDA-ACCRECD(R2)                                           
         BE    XIT                                                              
         TM    8(R1),FF-RECNOTF                                                 
         BZ    *+6                                                              
         DC    H'0'                                                             
         CLI   8(R1),0                                                          
*                                                                               
XIT      XIT1                                                                   
*                                                                               
GETELIO  LA    R6,IO                                                            
         GETEL R6,DATADISP,ELCODE                                               
*                                                                               
*              CONSTANTS, TABLES                                                
         SPACE 3                                                                
MINALLOC DC    F'10000'            MINIMUM BUFFER STORAGE ALLOCATION            
DATADISP DC    Y(ACRECORD-ACKEYD)                                               
XFF      DC    (L'ACOPKEY)X'FF'                                                 
OPTKLEN  EQU   ACOPWORK-ACOPOG+L'ACOPWORK   BUFFER ENTRY KEY LENGTH             
OPTBUFFL EQU   OPTKLEN+4           BUFFER ENTRY LENGTH                          
FF       EQU   X'FF'                                                            
RECNOTF  EQU   X'10'               RECORD NOT FOUND                             
RECIDEL  EQU   X'02'               RECORD IS DELETED                            
SPACES   DC    CL64' '                                                          
*                                                                               
DMRDHI   DC    C'DMRDHI '                                                       
DMREAD   DC    C'DMREAD '                                                       
DMRSEQ   DC    C'DMRSEQ '                                                       
GETREC   DC    C'GETREC '                                                       
ACCOUNT  DC    C'ACCOUNT'                                                       
ACCDIR   DC    C'ACCDIR '                                                       
ACCMST   DC    C'ACCMST '                                                       
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
       ++INCLUDE ACPROODTAB                                                     
         EJECT                                                                  
       ++INCLUDE ACPRODFREC                                                     
         EJECT                                                                  
*              WORKING STORAGE FOR GETOPT                                       
         SPACE 3                                                                
GOD      DSECT                                                                  
RELO     DS    A                                                                
XDA      DS    XL4                                                              
XWORK    DS    XL64                                                             
WORK     DS    CL64                                                             
DUB      DS    D                                                                
DMCB     DS    CL24                                                             
AENDBUFF DS    A                                                                
COVAIL   DS    V                                                                
BINSRCH  DS    V                                                                
*                                                                               
HALF     DS    H                                                                
ACTJOB   DS    CL(L'GOSELJOB)      ACTUAL JOB CODE                              
COMMAND  DS    CL8                                                              
ELCODE   DS    CL1                                                              
*                                                                               
BUFFLEN  DS    XL1                                                              
BUFFEL   DS    CL256                                                            
NEWLEVEL DS    XL1                                                              
ANYIO    DS    CL1                                                              
FROM     DS    CL4                                                              
*                                                                               
KEYSAVE  DS    XL64                KEY SAVE AREA                                
KEY      DS    XL64                ACTUAL RECORD KEY                            
         ORG   KEY                                                              
IO       DS    2000X               I/O AREA                                     
GODX     EQU   *                                                                
*                                                                               
GOBLOCKD DSECT                                                                  
         EJECT                                                                  
       ++INCLUDE ACGOBLOCK                                                      
         EJECT                                                                  
*              GETOPT SAVE STORAGE IN GOBLOCK                                   
         SPACE 3                                                                
         ORG   GOBLOCK                                                          
MYCUL    DS    CL3                                                              
MYCLI    DS    CL6                                                              
MYPROD   DS    CL6                                                              
MYJOB    DS    CL6                                                              
MYMED    DS    CL1                                                              
MYWORK   DS    CL2                                                              
         DS    CL1                 SPARE                                        
MYOG     DS    CL1                                                              
         DS    CL1                 SPARE                                        
LLEVA    DS    XL1                 LENGTHS FROM HEIRARCHY                       
LLEVB    DS    XL1                                                              
LLEVC    DS    XL1                                                              
DONECOMP DS    CL1                 COMPANY SWITCH                               
OKSTAT   DS    XL1                 1=ACTIVE 2=NOT ACTIVE                        
AOKBUFF  DS    A                   A(OPTION KEY BUFFER)                         
AOKDATA  DS    A                   A(OPTION DATA BUFFER)                        
AOKDATAX DS    A                   A(OPTION DATA BUFFER END)                    
NOPTKEYS DS    F                   N'OPTION KEYS IN BUFFER                      
WHICH    DS    C                   INTERNAL GOWHICH VALUE                       
MYMG     DS    C                   MEDIA GROUP                                  
MYWORKST DS    X                   WORKCODE STATUS                              
MYWORKG  DS    C                   WORKCODE GROUP                               
MYOFC    DS    CL2                                                              
CLIOFC   DS    CL2                 CLIENT OFFICE                                
         SPACE 1                                                                
*                                  ADDRESSES IN OPTIMIZATION BUFFER             
BUFFNOW  DS    A                   PRESENT BUFFER ADDRESS                       
AOLIST   DS    A                   A(OFFICE LIST)   2 BYTE ENTRIES              
*                                                   OFFICE/GROUP                
AMLIST   DS    A                   A(MEDIA LIST)    2 BYTE ENTRIES              
*                                                   MEDIA/GROUP                 
AWLIST   DS    A                   A(WORKCODE LIST) 4 BYTE ENTRIES              
*                                                   WORKCODE/GROUP              
*                                                   WORKCODE STATUS             
ACMPOPTS DS    A                   A(AGENCY OPTION ELEMENTS)                    
AOGROPTS DS    A                   A(OFFICE GROUP OPTION ELEMENTS)              
AOFFOPTS DS    A                   A(OFFICE OPTION ELEMENTS)                    
ACLIOPTS DS    A                   A(CLIENT OPTION ELEMENTS)                    
APROOPTS DS    A                   A(PRODUCT OPTION ELEMENTS)                   
AJOBOPTS DS    A                   A(JOB OPTION ELEMENTS)                       
         SPACE 2                                                                
*              DSECT TO COVER OPTION ELEMENT IN BUFFER                          
         SPACE 1                                                                
OPTELD   DSECT                                                                  
OPTFIELD DS    XL1                 FIELD NUMBER                                 
OPTLEN   DS    XL1                 ELEMENT LENGTH (L'DATA+8)                    
OPTMG    DS    CL1                 MEDIA GROUP OR X'00'                         
OPTMED   DS    CL1                 MEDIA CODE OR X'00'                          
OPTWG    DS    CL1                 WORK CODE GROUP OR X'00'                     
OPTWORK  DS    CL2                 WORK CODE OF X'0000'                         
OPTFROM  DS    CL1                 CAME FROM C P X R OR O                       
OPTDATA  DS    0C                  OPTION DATA                                  
         SPACE 2                                                                
*              DSECT TO COVER OUTPUT DIRECTION TABLE                            
         SPACE 1                                                                
OUTTABD  DSECT                                                                  
OUTOPN   DS    X                   OPTION NUMBER                                
OUTMAX   DS    X                   MAXIMUM OUTPUT LENGTH                        
OUTINDS  DS    X                   OUTPUT INDICATORS                            
OUTIPAN  EQU   X'80'               OUTPUT TO PANEL BLOCK                        
OUTIEXT  EQU   X'40'               OUTPUT TO GOBLOCK EXTENSION                  
OUTIBIL  EQU   X'20'               OUTPUT TO GOBLOCK BILL EXTENSION             
OUTDISP  DS    AL2                 DISPLACEMENT TO OUTPUT                       
OUTTABL  EQU   *-OUTTABD                                                        
         EJECT                                                                  
       ++INCLUDE ACOPTEQUS                                                      
         SPACE 2                                                                
* ACGENBOTH                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENBOTH                                                      
         PRINT ON                                                               
* ACGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
         PRINT ON                                                               
* ACGOXDBLK                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGOXDBLK                                                      
         PRINT ON                                                               
* ACPANBLOCK                                                                    
         PRINT OFF                                                              
GOPANBKD DSECT                                                                  
       ++INCLUDE ACPANBLOCK                                                     
         PRINT ON                                                               
GOXBLKD  DSECT                                                                  
       ++INCLUDE ACGOXBLOCK                                                     
GOBBLKD  DSECT                                                                  
       ++INCLUDE ACGOBBLOCK                                                     
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'037ACGETOPT  07/01/20'                                      
         END                                                                    
