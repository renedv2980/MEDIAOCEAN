*          DATA SET RECKPSEC   AT LEVEL 105 AS OF 02/16/99                      
*CATALP RECKPSEC                                                                
         TITLE 'RECKPSEC' - REPPAK PROGRAM ACCESS SECURITY VALIDATION           
*                                                                               
* INPUT  PARAMETER 1 = A(SECURITY CONTROL BLOCK) (REGENSBLK)                    
*                                                                               
*        PARAMETER 2 = A(FLD HEADER FOR ERROR MESSAGE) (OPTIONAL)               
*                                                                               
*        PARAMETER 3 = A(TIOB) (OPTIONAL - FOR BEEP ON ERROR)                   
*                                                                               
*        ---------------------------------------------------------              
*                                                                               
*        PARAMETER 4 = A(RFBLOCK)                                               
*                                                                               
*        PARAMETER 5 = VREPFACS                                                 
*                                                                               
*        ---------------------------------------------------------              
*                                                                               
* OUTPUT          CC = EQUAL     - REQUEST VAILD                                
*                 CC = NOT EQUAL - REQUEST INVALID                              
*                                                                               
*        PARAMETER 1 = BYTE 0 ----- RETURN CODE ------ (ONLY ONE)               
*                             X'00' SECURED, ACCESS GRANTED                     
*                             X'01' UNSECURED, ACCESS GRANTED                   
*                             X'80' SECURED, ACCESS DENIED                      
*                             X'51' NO PAR RECORD ON FILE - VALID               
*                             X'52' NO PID NUMBER CONNECTED - VALID             
*                                                                               
RECKPSEC CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 (WORKDX-WORKD),*CKSEC*,CLEAR=YES,RR=R5                           
         USING WORKD,RC                                                         
         ST    R5,RELO                                                          
         ST    RD,SAVERD                                                        
         ST    R1,SVPARM                                                        
         L     RA,0(R1)            A(SBLOCK)                                    
         USING SECBLK,RA                                                        
         L     RF,12(R1)           P4                                           
         MVC   RFBLOCK,0(RF)       COMFACS,AGENCY CODE                          
         MVC   VREPFACS,16(R1)                                                  
*                                                                               
         L     RE,COMFACS          RESOLVE ADDRESSES                            
         USING COMFACSD,RE                                                      
         MVC   GETFACT,CGETFACT                                                 
         MVC   HELLO,CHELLO                                                     
         DROP  RE                                                               
*                                                                               
         MVI   STATUS,0            INITIALIZE                                   
*                                                                               
         MVC   AGENCYCT,AGENCY     SAVE CONNECTED REP CODE                      
*                                                                               
         XC    KEY,KEY             READ REPREC FOR MASTER REP CODE              
         LA    R6,KEY                                                           
         USING RREPREC,R6                                                       
         MVI   RREPKTYP,X'01'                                                   
         MVC   RREPKREP,AGENCY                                                  
         GOTOX (RFGETREC,VREPFACS),DMCB,KEY,IOAREA,0,RFBLOCK                    
         BE    *+6                                                              
         DC    H'0'                                                             
         LA    R6,IOAREA                                                        
         CLC   RREPMAST,=X'FFFF'   MASTER?                                      
         BNE   *+12                                                             
         OI    STATUS,X'80'                                                     
         B     CKS030                                                           
         OC    RREPMAST,=X'4040'                                                
         CLC   RREPMAST,=X'4040'                                                
         BE    *+10                NO MASTER/SUBSID CONTROL                     
         MVC   AGENCY,RREPMAST     USE MASTER REP CODE TO READ PAR REC          
         DROP  R6                                                               
*                                                                               
CKS030   DS    0H                                                               
         GOTO1 GETFACT,DMCB,0      GET PID NUMBER                               
         L     RF,0(R1)                                                         
         MVC   PIDNUM,FAPASSWD-FACTSD(RF)                                       
*                                                                               
***>>    MVC   PIDNUM,=X'FFFF'    ****>>> FOR NOW <<<****                       
*                                                                               
         OC    PIDNUM,PIDNUM                                                    
         BNZ   CKS040                                                           
         L     R1,SVPARM                                                        
         MVI   0(R1),X'52'         NO PID NUMBER - SECURITY INACTIVE            
         MVI   4(R1),0                                                          
         B     EXITOK              RETURN CC:EQUAL                              
*                                                                               
CKS040   DS    0H                                                               
         BAS   RE,GETSEC           GET SECURITY RECORD                          
         BE    CKS100                                                           
         L     R1,SVPARM                                                        
         MVI   0(R1),X'51'         NO PAR REC FOUND                             
         MVI   4(R1),0                                                          
         B     EXITOK              RETURN CC:EQUAL                              
*                                                                               
CKS100   DS    0H                  ACCESS CHECK                                 
         GOTO1 HELLO,DMCB,(C'G',=C'REPFIL'),(X'70',SECREC),(6,PSBPGM),0         
         CLI   12(R1),0            GOT IT?                                      
         BE    CKS110              NO ACCESS RESTRICTIONS                       
         L     R1,SVPARM                                                        
         MVI   0(R1),X'01'         RETURN CODE                                  
         B     EXITOK              RETURN CC:EQUAL                              
*                                                                               
CKS110   DS    0H                                                               
         ZICM  R6,13(R1),3         ELEMENT                                      
         USING RSECPGEL,R6                                                      
         CLC   RSECPSEC,PSBSEC                                                  
         BH    ERROR                                                            
         L     R1,SVPARM                                                        
         MVI   0(R1),0             RETURN CODE                                  
         B     EXITOK              RETURN CC:EQUAL                              
*                                                                               
**********************************************************************          
* ERROR - GENERATES ERROR MSG, INTERRUPTS MODULE EXECUTION & EXITS              
**********************************************************************          
ERROR    DS    0H                                                               
         L     R1,SVPARM                                                        
         ICM   R2,15,4(R1)         A(MSG FIELD HEADER)                          
         BZ    ERROR850            NO MSG FIELD                                 
         ZIC   R3,0(R2)                                                         
         SH    R3,=H'8'            MINUS HEADER                                 
         TM    1(R2),X'02'         EXT?                                         
         BZ    *+8                                                              
         SH    R3,=H'8'            MINUS EXT                                    
*                                                                               
         BCTR  R3,0                                                             
         EX    R3,*+8                                                           
         B     *+10                                                             
         XC    8(0,R2),8(R2)       CLEAR MSG FIELD                              
         LA    R3,1(R2,R3)         END OF FIELD+1                               
         OI    1(R2),X'08'         HIGH INTENSITY                               
         OI    6(R2),X'80'         XMIT                                         
         LA    R2,8(R2)            FIRST TEXT SPOT IN FIELD                     
*                                                                               
         MVC   0(22,R2),=C'ACCESS DENIED: Action '                              
         LA    R2,22(R2)                                                        
*                                                                               
         LA    RF,PSBXACT          ACTION NAME FROM PSBLOCK                     
         MVI   BYTE,10                                                          
         BAS   RE,ADDWORD                                                       
         LA    R2,1(R2)                                                         
*                                                                               
         MVC   0(09,R2),=C'requires '                                           
         LA    R2,09(R2)                                                        
*                                                                               
         LA    RF,SECTAB           FIND SECURITY LEVEL DESC IN TABLE            
ERROR100 CLI   0(RF),X'FF'                                                      
         BNE   *+6                                                              
         DC    H'0'                                                             
         CLC   PSBSEC,12(RF)                                                    
         BE    ERROR120                                                         
         LA    RF,L'SECTAB(RF)                                                  
         B     ERROR100                                                         
*                                                                               
ERROR120 DS    0H                                                               
         MVI   BYTE,12                                                          
         BAS   RE,ADDWORD                                                       
         LA    R2,1(R2)                                                         
*                                                                               
         MVC   0(06,R2),=C'access'                                              
         LA    R2,06(R2)                                                        
*                                                                               
         L     R1,SVPARM                                                        
         L     RF,4(R1)            A(MSG FIELD HEADER)                          
         SR    R2,RF               LEN OF MSG                                   
         STC   R2,7(RF)                                                         
*                                                                               
         ICM   R3,15,8(R1)         ATIOB                                        
         BZ    ERROR850                                                         
         USING TIOBD,R3                                                         
         OI    TIOBINDS,TIOBALRM   BEEP                                         
         DROP  R3                                                               
*                                                                               
ERROR850 DS    0H                                                               
         L     R1,SVPARM                                                        
         MVI   0(R1),X'80'         PASS ERROR CODE TO APPLICATION               
         L     RD,SAVERD           GET ALL THE WAY OUT                          
         B     EXITL               EXIT CC NOT EQUAL                            
*                                                                               
* ADDWORD - APPENDS WORD TO MESSAGE TEXT                                        
*                                                                               
*   BEFORE: RF=A(WORD TO APPEND)                                                
*           R2=A(NEXT SPACE IN MESSAGE TEXT)                                    
*           BYTE=MAX LENGTH OF WORD (SPACE OR NULL PADDED)                      
*                                                                               
*    AFTER: R2=A(NEXT AVAILABLE SPACE IN MSG)                                   
*           BYTE=ACTUAL LENGTH OF WORD APPENDED                                 
*                                                                               
*     NOTE: USES R1                                                             
*                                                                               
ADDWORD  DS    0H                                                               
         ZIC   R1,BYTE                                                          
         AR    R1,RF                                                            
ADDW010  DS    0H                                                               
         BCTR  R1,0                                                             
         CR    R1,RF                                                            
         BL    ADDW050                                                          
         CLI   0(R1),0                                                          
         BE    ADDW010                                                          
         CLI   0(R1),C' '                                                       
         BE    ADDW010                                                          
*                                                                               
         SR    R1,RF               LEN OF WORD-1                                
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),0(RF)                                                    
         LA    R1,1(R1)                                                         
         AR    R2,R1               BUMP INSERTION POINT                         
         STC   R1,BYTE                                                          
         BR    RE                                                               
ADDW050  DS    0H                  NO WORD                                      
         MVI   BYTE,0                                                           
         BR    RE                                                               
**********************************************************************          
* GETSEC - ROUTINE READS REP ACCESS SECURITY RECORD                             
**********************************************************************          
GETSEC   NTR1                                                                   
         XC    KEY,KEY                                                          
         LA    R6,KEY                                                           
         USING RSECREC,R6                                                       
         MVC   RSECKTYP(2),=X'1501'                                             
         MVC   RSECKREP,AGENCY                                                  
         MVC   RSECKPID,PIDNUM                                                  
         DROP  R6                                                               
         GOTOX (RFGETREC,VREPFACS),DMCB,KEY,RSECREC,0,RFBLOCK                   
         B     EXIT                                                             
**********************************************************************          
* COMMON ROUTINES                                                               
**********************************************************************          
EXITH    CLI   *,0                 SET CC HIGH                                  
         B     EXIT                                                             
EXITL    CLI   *,X'FF'                                                          
         B     EXIT                                                             
EXITNO   LTR   RB,RB               SET CC NOT EQUAL                             
         B     EXIT                                                             
OK       EQU   *                                                                
EXITOK   CR    RB,RB               SET CC EQUAL                                 
         B     EXIT                                                             
EXIT     DS    0H                  JUST EXIT                                    
         XIT1                                                                   
*                                                                               
NOHOOK   BR    RE                                                               
*                                                                               
         GETEL R6,34,ELCODE                                                     
*                                                                               
SPACES   DC    80C' '                                                           
       ++INCLUDE REPSECTAB                                                      
         LTORG                                                                  
*                                                                               
WORKD    DSECT                                                                  
DUB      DS    D                                                                
DMCB     DS    6F                                                               
FULL     DS    F                                                                
RELO     DS    F                                                                
SVPARM   DS    F                                                                
SAVERD   DS    F                                                                
VREPFACS DS    A                                                                
GETFACT  DS    A                                                                
HELLO    DS    A                                                                
RFBLOCK  DS    0CL6                                                             
COMFACS  DS    A                                                                
AGENCY   DS    CL2                 REP CODE (MASTER)                            
AGENCYCT DS    CL2                 ACTUAL CONNECTED REP CODE (SUBSID)           
ELCODE   DS    X                                                                
BYTE     DS    X                                                                
PIDNUM   DS    CL2                 PASSWORD ID NUMBER                           
SECDESC  DS    CL12                SECURITY LEVEL DESCRIPTION                   
*                                                                               
STATUS   DS    X                   PROGRAM STATUS FLAGS                         
MASTERQ  EQU   X'80'               MASTER REP CONNECTED                         
*                                                                               
WORK     DS    CL64                                                             
KEY      DS    CL32                                                             
KEYSAVE  DS    CL32                                                             
*                                                                               
       ++INCLUDE REGENSEC                                                       
         ORG   RSECREC                                                          
SECREC   DS    XL1000                                                           
IOAREA   DS    XL1000                                                           
*                                                                               
WORKDX   EQU   *                                                                
SECBLK   DSECT                                                                  
       ++INCLUDE REGENPBLK                                                      
         DSECT                                                                  
       ++INCLUDE REGENREPA                                                      
       ++INCLUDE REGENSTA                                                       
       ++INCLUDE REGENSET                                                       
       ++INCLUDE DDCOMFACS                                                      
       ++INCLUDE FAFACTS                                                        
       ++INCLUDE FATIOB                                                         
       ++INCLUDE REPFACSQ                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'105RECKPSEC  02/16/99'                                      
         END                                                                    
