*          DATA SET MXAGYC     AT LEVEL 003 AS OF 08/11/93                      
*CATALP MXAGYC                                                                  
         TITLE 'MXAGYC - MEDIA EXTRACT - AGENCY RECORD'                         
**********************************************************************          
* AL4 A(EXTRACT RECORD)                                              *          
* AL4 A(MEDFILE RECORD)                                              *          
* AL4 0=MEDFILE-TO-EXTRACT                                           *          
**********************************************************************          
         SPACE 1                                                                
         PRINT NOGEN                                                            
MXAGYC   CSECT                                                                  
         NMOD1 WORKX-WORKD,*MXAGYC*,RR=RA                                       
         USING WORKD,RC                                                         
         ST    R1,APARM                                                         
         ST    RA,RELO                                                          
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         USING MXAGYD,R2           R2=A(EXTRACT RECORD)                         
         USING AGYKEY,R3           R3=A(MEDLINE RECORD)                         
         LA    R4,AGYEL            R4=A(FIRST ELEMENT)                          
         SPACE 1                                                                
MXALEN   XC    MXAGYLEN,MXAGYLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   MXAGYLEN(2),=AL2(MXAGYDL)                                        
         MVC   MXAGYTYP,=AL2(MXAGYDQ)                                           
         MVI   MXAGYDX,MXTRTQ                                                   
         MVI   MXAGYDX+1,MXTRTQ                                                 
         SPACE 1                                                                
MXAATY   MVI   MXAGYATY-1,MXTRTQ   AGENCY TYPE                                  
         MVI   MXAGYATY,C'A'       A=AGENCY (MAIN)                              
         TM    AGYKSTYP,AGYKSBAG                                                
         BZ    *+8                                                              
         MVI   MXAGYATY,C'B'       B=BUYING AGENCY                              
         TM    AGYKSTYP,AGYKSCAG                                                
         BZ    *+8                                                              
         MVI   MXAGYATY,C'C'       C=CREATIVE AGENCY                            
         SPACE 1                                                                
MXACDE   MVI   MXAGYCDE-1,MXTRTQ   AGENCY CODE                                  
         SR    R1,R1                                                            
         IC    R1,AGYKCAG                                                       
         CVD   R1,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  MXAGYCDE,DUB+6(2)                                                
         SPACE 1                                                                
MXAALF   MVI   MXAGYALF-1,MXTRTQ   AGENCY 2-CHR ALPHA ID                        
         MVC   MXAGYALF,AGYALPH                                                 
         TR    MXAGYALF,MXTRT                                                   
         SPACE 1                                                                
MXAIPA   MVI   MXAGYIPA-1,MXTRTQ   AGENCY IPA ID                                
         MVC   MXAGYIPA,AGYIPA                                                  
         TR    MXAGYIPA,MXTRT                                                   
         SPACE 1                                                                
MXASHN   MVI   MXAGYSHN-1,MXTRTQ   AGENCY SHORT NAME                            
         MVC   MXAGYSHN,AGYSH                                                   
         TR    MXAGYSHN,MXTRT                                                   
         SPACE 1                                                                
MXACUR   MVI   MXAGYCUR-1,MXTRTQ   AGENCY CURRENCY CODE                         
         MVC   MXAGYCUR,AGYCURR                                                 
         TR    MXAGYCUR,MXTRT                                                   
         CLC   MXAGYCUR,=C'   '                                                 
         BNE   *+10                                                             
         MVC   MXAGYCUR,=C'GBP'                                                 
         SPACE 1                                                                
MXACTR   MVI   MXAGYCTR-1,MXTRTQ   AGENCY COUNTRY CODE                          
         SR    R1,R1                                                            
         IC    R1,AGYCTRY                                                       
         CVD   R1,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  MXAGYCTR,DUB+6(2)                                                
         SPACE 1                                                                
MXANAM   MVI   MXAGYNAM-1,MXTRTQ   AGENCY NAME                                  
         MVC   MXAGYNAM,AGYNAME                                                 
         TR    MXAGYNAM,MXTRT                                                   
         B     MXANXT                                                           
         SPACE 1                                                                
MXANXT   SR    RF,RF               BUMP TO NEXT ELEMENT                         
         IC    RF,1(R4)                                                         
         CH    RF,=H'1'                                                         
         BNH   EXIT                EXIT IF FUNNY ELEMENT LENGTH                 
         AR    R4,RF                                                            
         CLI   0(R4),0                                                          
         BE    MXANXTX                                                          
         B     MXANXT                                                           
MXANXTX  B     EXIT                                                             
         SPACE 1                                                                
EXIT     XMOD1 1                                                                
         EJECT                                                                  
         LTORG                                                                  
       ++INCLUDE MXTRT                                                          
         EJECT                                                                  
WORKD    DSECT                                                                  
PARM     DS    0XL16               PARAMETER LIST                               
AXREC    DS    A                   A(EXTRACT RECORD)                            
AFREC    DS    A                   A(MEDFILE RECORD)                            
         DS    A                                                                
         DS    A                                                                
*                                                                               
DUB      DS    D                                                                
APARM    DS    A                   A(PARAMETER LIST)                            
RELO     DS    A                                                                
*                                                                               
WORKX    EQU   *                                                                
         EJECT                                                                  
       ++INCLUDE MXAGYD                                                         
         EJECT                                                                  
DAGY     DSECT                                                                  
       ++INCLUDE MEFILAGY                                                       
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003MXAGYC    08/11/93'                                      
         END                                                                    
