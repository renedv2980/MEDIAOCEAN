*          DATA SET ACCADET    AT LEVEL 019 AS OF 08/25/17                      
*CATALP CADET                                                                   
                                                                                
* NSHE 005 23NOV06 <BR10157L> FIX INDEX IN TIME AUDIT RECORD                    
* JFOS 006 13DEC06 <BR10272L> RETURN AUDRSTAT IF =COST CALL                     
* NSHE 007 04APR07 <BR10628D> FIX 1 DAY PERIOD PROBLEM                          
* YNGX 008 25JUN07<LO01-6368> ENABLE APPROVERS FOR TIMESHEETS                   
*                             EXPENSE CLAIMS TO BE SET SEPARATELY               
* NSHE 009 21AUG07 <Z4471> FIX TEMPO PROBLEMS                                   
* JFOS 010 05DEC07 <BR11721D> CHECK CAD1RLVS ARE SET BEFORE USE                 
* JFOS 011 12JUN08 <BR12535D> RETURN CADTSSTS IF FLEXBILL CADXFXRR CALL         
* NSHE 013 07AUG08 <LO01-7743> CHANGE TO JOBPASD STRUCTURE                      
* NSHE 014 20OCT08 CHANGE TO JOBPASD STRUCTURE                                  
* NSHE 015 23MAR09 CHANGE TO AUDIT STRUCTURE                                    
* NSHE 016 16AUG09 CHANGE TO TIMEL STRUCTURE                                    
* MPEN 021 12MAR14 <DSRD-227> SET APPLICATION TYPE ON TIME RECS/AUDIT           
* MPEN 022 25AUG17 <DSRD-15589> 200 CHAR TIMELINE                               
         TITLE 'CREATE ADJUSTMENT ETIME TIMELS/MAINTAIN AUDRECS'                
CADET    CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,**CADET*,CLEAR=YES                                   
         USING WORKD,RC            RC=A(LOCAL W/S)                              
         USING CADETD,R9                                                        
         LR    R9,R1               R9=A(CADETD) DSECT                           
         L     RF,CCOMFACS         RF=A(COMFACS)                                
         USING COMFACSD,RF                                                      
         MVC   VDATAMGR,CDATAMGR                                                
         MVC   VHELLO,CHELLO                                                    
         MVC   VADDAY,CADDAY                                                    
         MVC   VDATCON,CDATCON                                                  
         DROP  RF                                                               
         MVC   TIMENDT,CADTSPED                                                 
         OC    CADAELM,CADAELM     TEST FLEXIBILL CALL                          
         BZ    CAD02               YES                                          
         OC    CADLOCED,CADLOCED   TEST LOCATION END DATE SET                   
         BZ    CAD02                                                            
         CLC   CADTSPED,CADLOCED   TEST LOC END DATE < PERIOD END DATE          
         BL    *+10                                                             
         MVC   TIMENDT,CADLOCED    USE LOCATION END                             
*                                  LOOK UP CONNECTED USER                       
CAD02    L     RF,CADASEC                                                       
         L     R2,CADAIOA                                                       
         USING SAPEREC,R2                                                       
         XC    SAPEKEY,SAPEKEY                                                  
         MVI   SAPETYP,SAPETYPQ                                                 
         MVI   SAPESUB,SAPESUBQ                                                 
         MVC   SAPEAGY,SECOAGPE-SECD(RF)                                        
         MVC   SAPEPID,SECPID-SECD(RF)                                          
         MVC   KEYSAVE(L'SAPEKEY),SAPEKEY                                       
         GOTO1 VDATAMGR,CDMCB,(0,DMRDHI),CONFIL,SAPEKEY,SAPEKEY                 
         CLC   SAPEREC(SAPEDEF-SAPEREC),KEYSAVE                                 
         BE    *+6                                                              
         DC    H'0'                                                             
         LA    RF,SAPEDATA                                                      
         USING SAPWDD,RF                                                        
         SR    R0,R0                                                            
CAD04    CLI   SAPWDEL,0                                                        
         BNE   *+6                                                              
         DC    H'0'                                                             
         CLI   SAPWDEL,SAPWDELQ                                                 
         BE    *+14                                                             
         IC    R0,SAPWDLN                                                       
         AR    RF,R0                                                            
         B     CAD04                                                            
         MVC   SVACPID,SAPWDNUM    CONNECTED USER IS ADJUSTER APPROVER          
         DROP  RF                                                               
                                                                                
         TM    CADXCALL,CADXFXSD   TEST FLEXIBILL 'DELETE' CALL                 
         BNZ   CAD20                                                            
         XC    SVCLPID,SVCLPID                                                  
         CLC   =C'SJ',CADTMULA     TEST CLIENT TIME                             
         BNE   CAD14               NO                                           
         BAS   RE,CLLKUP                                                        
         B     CAD20                                                            
                                                                                
         USING NCTPASD,R2          *** NON-CLIENT TIME                          
CAD14    LA    R2,CADKEY                                                        
         XC    NCTPAS,NCTPAS                                                    
         MVI   NCTPTYP,NCTPTYPQ                                                 
         MVI   NCTPSUB,NCTPSUBQ                                                 
         MVC   NCTPCPY,CADCPYCD                                                 
         MVC   NCTPNCC,CADTMULA+2                                               
         MVI   NCTPAPPL,NCTPATIM                                                
         MVC   KEYSAVE,CADKEY                                                   
         GOTO1 VDATAMGR,CDMCB,(0,DMRDHI),ACCDIR,CADKEY,CADKEY,0                 
         B     CAD18                                                            
CAD16    GOTO1 VDATAMGR,CDMCB,(0,DMRSEQ),ACCDIR,CADKEY,CADKEY,0                 
                                                                                
CAD18    CLC   KEYSAVE(NCTPPIDB-NCTPASD),NCTPAS                                 
         BNE   CAD20                                                            
         TM    NCTPSTAT,NCTPDFLT   TEST DEFAULT APPROVER                        
         BZ    CAD16               NO - TRY FOR ANOTHER                         
         MVC   SVCLPID,NCTPPIDB                                                 
                                                                                
CAD20    XC    SVHIROW,SVHIROW     FIND HIGHEST USED T/S ROW NUMBER             
         MVI   PIDIND,0            AND APPROVER MATCH                           
         USING AUDRECD,R2          *** LOOK UP T/S AUDIT RECORD                 
         LA    R2,CADKEY                                                        
         XC    AUDKEY,AUDKEY                                                    
         MVI   AUDKTYP,AUDKTYPQ                                                 
         MVI   AUDKSUB,AUDKSUBQ                                                 
         MVI   AUDKAUDT,AUDKTIME                                                
         MVC   AUDKCPY,CADCPYCD                                                 
         MVC   AUDKPIDB,CADPIDN    FOR THIS PERSON                              
         SR    R0,R0                                                            
         ICM   R0,7,TIMENDT        AND THIS PERIOD                              
         LNR   R0,R0                                                            
         STCM  R0,7,AUDKPEDT       (COMPLEMENTED)                               
KS       USING AUDRECD,KEYSAVE                                                  
         MVC   KS.AUDKEY,AUDKEY    SAVE KEY                                     
         MVI   KS.AUDKSEQ,X'FF'    INIT SAVED SEQ AS 'NONE FOUND' IND           
                                                                                
CAD24    GOTO1 VDATAMGR,CDMCB,DMREAD,ACCDIR,AUDKEY,AUDKEY,0                     
         BNE   CAD26                                                            
*                                  FOUND:                                       
         MVC   KS.AUDKSEQ,AUDKSEQ  SAVE THE SEQUENCE                            
         SR    RF,RF                                                            
         IC    RF,AUDKSEQ          BUMP IT                                      
         AHI   RF,1                                                             
         STC   RF,AUDKSEQ                                                       
         B     CAD24               AND TRY AGAIN                                
*                                  NOT FOUND:                                   
CAD26    CLI   KS.AUDKSEQ,X'FF'    TEST FIRST READ, NONE FOUND IND              
         BE    CAD44               YES - NO RECS EXIST, SO CREATE ONE           
*                                  ELSE RE-READ ALL FOUND FOR UPDATE            
         MVC   AUDKEY,KS.AUDKEY                                                 
         MVI   AUDKSEQ,0           SET FIRST RECORD                             
CAD28    GOTO1 VDATAMGR,CDMCB,(X'80',DMREAD),ACCDIR,AUDKEY,AUDKEY,0             
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   CIODA,AUDKDA                                                     
         GOTO1 VDATAMGR,CDMCB,(X'80',GETREC),ACCMST,CIODA,CADAIOA,CDMWK         
         BE    *+6                                                              
         DC    H'0'                                                             
         L     R2,CADAIOA                                                       
         SR    R0,R0                                                            
         ICM   R0,3,AUDRINDX       BUMP CHANGES INDEX IN STS AREA               
         AHI   R0,1                                                             
         STCM  R0,3,AUDRINDX                                                    
         B     CAD31               BUG FIX -WORK WITH EVERY RECORD              
         CLC   AUDKSEQ,KS.AUDKSEQ  TEST HIGHEST SEQ FOUND                       
         BE    CAD30               YES - WORK WITH THIS RECORD                  
*                                  ELSE WRITE IT BACK NOW                       
CAD29    GOTO1 VDATAMGR,CDMCB,PUTREC,ACCMST,CIODA,CADAIOA,CDMWK                 
         BE    *+6                                                              
         DC    H'0'                                                             
         L     R2,CADAIOA                                                       
         SR    R0,R0                                                            
         ICM   R0,3,AUDRINDX       BUMP CHANGES INDEX IN STS AREA               
         LA    R2,CADKEY                                                        
         STCM  R0,3,AUDKINDX                                                    
         GOTO1 VDATAMGR,CDMCB,DMWRIT,ACCDIR,AUDKEY,AUDKEY,0                     
         BE    *+6                                                              
         DC    H'0'                                                             
         IC    R0,AUDKSEQ          BUMP KEY SEQUENCE                            
         AHI   R0,1                                                             
         STC   R0,AUDKSEQ                                                       
         B     CAD28               AND READ AGAIN                               
                                                                                
CAD30    TM    CADXCALL,CADXFXSD   TEST FLEXIBILL 'DELETE' CALL                 
         BNZ   CAD48                                                            
         B     CAD46                                                            
*                                  LOOK FOR ROW NO. AND APPROVER DETS           
         USING STCELD,R3                                                        
CAD31    LA    R3,AUDRFST                                                       
         SR    R0,R0                                                            
CAD32    CLI   STCEL,0                                                          
         BE    CAD42                                                            
         CLI   STCEL,STCELQ                                                     
         BE    CAD36                                                            
         CLI   STCEL,TIMELQ                                                     
         BE    CAD38                                                            
CAD34    IC    R0,STCLN                                                         
         AR    R3,R0                                                            
         B     CAD32                                                            
                                                                                
CAD36    TM    STCIND,STCITIME     TEST ETIME STCEL                             
         BZ    CAD34                                                            
         CLC   SVHIROW,STCTROW     TEST FOUND ROW HIGHER THAN SAVED             
         BNL   CAD34               NO                                           
         MVC   SVHIROW,STCTROW     SAVE IT                                      
         B     CAD34               AND GO ROUND AGAIN                           
         USING TIMELD,R3                                                        
CAD38    CLI   TIMETYP,TIMEARIN    TEST APPROVAL ROW TIMEL                      
         BNE   CAD34                                                            
         CLC   SVCLPID,TIMAPIDB    TEST APPROVER ALREADY ON THIS T/S            
         BNE   CAD40                                                            
         TM    TIMASTAT,TIMASAPR   TEST THIS TIME STATUS IS APPROVED            
         BZ    *+8                                                              
         OI    PIDIND,PIDAPFQ      SET FOUND VALID APPROVER MATCH               
CAD40    CLC   SVHIROW,TIMAIDNO    TEST FOUND ROW HIGHER THAN SAVED             
         BNL   CAD34                                                            
         MVC   SVHIROW,TIMAIDNO    SAVE IT                                      
         B     CAD34               AND GO ROUND AGAIN                           
CAD42    CLC   AUDKSEQ,KS.AUDKSEQ  TEST HIGHEST SEQ FOUND                       
         BE    CAD30               LAST IN SEQ                                  
         B     CAD29               READ NEXT IN SEQUENCE                        
*                                  NO AUDREC - BUILD ONE FROM SCRATCH           
CAD44    BAS   RE,LMLKUP           LOOK UP LINE MANAGER FOR THIS PERSON         
         L     R2,CADAIOA                                                       
         MVC   AUDKEY,KS.AUDKEY                                                 
         MVI   AUDKSEQ,0           CLEAR SEQUENCE                               
         XC    AUDRSTA,AUDRSTA                                                  
         MVI   AUDRSTAT,0          STATUS IS 'IN PROGRESS'                      
         MVI   AUDRINDX+1,1        FIRST CHANGE                                 
         LA    RF,AUDRFST                                                       
         MVI   0(RF),0                                                          
         AHI   RF,1                                                             
         SR    RF,R2                                                            
         STCM  RF,3,AUDRLEN                                                     
         MVI   SVHIROW+1,1         SET ROW 1                                    
         MVI   RECSTS,ADDPNDQ      SET ADD PENDING                              
         B     CAD48                                                            
                                                                                
CAD46    ICM   R0,3,SVHIROW        NEW ROW NO. WILL BE HIGHEST FOUND            
         AHI   R0,1                +1                                           
         STCM  R0,3,SVHIROW                                                     
         MVI   RECSTS,OKQ          PRESET RECORD STATUS                         
*                                  BUILD NEW STCEL FOR AUDRECD                  
         USING STCELD,R3                                                        
CAD48    LA    R3,WKELEM           ELEMENT WORK AREA                            
         USING SECD,RF                                                          
         L     RF,CADASEC                                                       
         CLI   RECSTS,ADDPNDQ      TEST ADDING NEW AUDREC                       
         BE    *+12                YES - CONTINUE                               
         TM    CADXCIND,CADXCADD   ELSE TEST T/S ADD FROM COST                  
         BNZ   CAD49               YES - 1 STCEL ON AUDREC FOR T/S              
         XC    STCEL(STCLN3Q),STCEL                                             
         MVI   STCEL,STCELQ                                                     
         MVI   STCIND,STCITIME                                                  
         MVI   STCTTYP,STCTRWAD    SET 'ROW ADD'                                
         MVC   STCTROW,SVHIROW                                                  
                                                                                
         TM    CADXCIND,CADXCADD   TEST TIMESHEET ADD FROM COST                 
         BZ    *+14                                                             
         MVI   STCTTYP,STCTTSAD    SET 'TIMESHEET ADD'                          
         XC    STCTROW,STCTROW     AND ZERO ROW NUMBER                          
                                                                                
         TM    CADXCIND,CADXCCHA   TEST T/SHEET LINE CHANGE FROM COST           
         BZ    *+14                                                             
         MVI   STCTTYP,STCTRWAM    SET 'ROW AMEND'                              
         MVC   STCTROW,CADHIROW    USE CALLER'S ROW NUMBER                      
                                                                                
         TM    CADXCALL,CADXFXSD   TEST FLEXIBILL 'DELETE' CALL                 
         BZ    *+18                                                             
         MVI   STCTTYP,STCTRWDL    SET 'ROW DELETE'                             
         MVC   STCTROW,CADHIROW    USE CALLER'S ROW NUMBER                      
         B     CAD48A                                                           
         MVC   STCTCULA,CADTMULA                                                
         MVC   STCTCTSK,CADTASK                                                 
         MVC   STCTMOA,CADMOA                                                   
         MVI   STCTSTA3,STCTSPEC   SET ADDED VIA SPECTRA APPS                   
         MVC   STCTCINT,CADINTRF                                                
         MVC   STCTCORD,CADORD                                                  
         MVC   STCTEST#,CADEST                                                  
         ZAP   STCTCHRS,CADTSHRS                                                
         MVC   STCTCTTY,CADTTYP                                                 
*                                                                               
CAD48A   MVC   STCTPID,SVACPID                                                  
         MVC   STCTDTE,CADTODAY                                                 
         XC    CDUB,CDUB                                                        
         TIME  DEC                 GET TIME (OF DAY)                            
         SRL   R0,8                                                             
         SLL   R0,4                                                             
         AHI   R0,X'0C'            ADD THE SIGN                                 
         STCM  R0,15,CDUB+4        PACKED HHMMSS                                
*&&US*&& AP    CDUB,=P'60000'      MAKE IT REAL TIME NOT DDS                    
         ZAP   STCTTIM,CDUB                                                     
         MVI   STCDTFR,0                                                        
         MVI   STCDTTO,0                                                        
         MVC   STCTUSR,CADCUID     CONNECTED USER-ID                            
         TM    CADXCALL,CADXFXSD   TEST FLEXIBILL 'DELETE' CALL                 
         BZ    CAD48B                                                           
         MVI   STCLN,STCLN3Q       DELETE IS SHORTER ELEMENT                    
         LA    R3,STCLN3Q(R3)                                                   
         B     CAD54                                                            
CAD48B   XR    RF,RF                                                            
         ICM   RF,1,CADNARRL                                                    
         BZ    CAD48C                                                           
         CHI   RF,L'STCTCNAR                                                    
         BNH   *+8                                                              
         LHI   RF,L'STCTCNAR                                                    
         SHI   RF,1                                                             
         MVC   STCTCNAR(0),CADNARR                                              
         EX    RF,*-6                                                           
         AHI   RF,1                                                             
CAD48C   LA    R1,STCLNTQ                                                       
         AR    R1,RF                                                            
         STC   R1,STCLN                                                         
         AR    R3,R1                                                            
         TM    CADXCIND,CADXCADD   TEST T/S ADD FROM COST                       
         BZ    CAD49               NO                                           
         USING TIMELD,R3                                                        
         XC    TIMEL(TIMALN1Q),TIMEL  ADD 1 TIMEL WITH PERSON DETS              
         MVI   TIMEL,TIMELQ                                                     
         MVI   TIMSEQ,0                                                         
         MVI   TIMETYP,TIMEARIN                                                 
         MVI   TIMAIDNO,0                                                       
         MVC   TIMAPULA(2),=C'1R'                                               
         MVC   TIMAPACT,CAD1RACT                                                
         MVC   TIMAPIDB,SVLNMGR    SAVED LINE MANAGER                           
         MVC   TIMAUSER,CADCUID                                                 
         MVC   TIMAPSDT,CADTSPSD                                                
         MVI   TIMASTAT,TIMASTMP   TIME ADDED VIA TEMPO OR COST                 
         MVC   TIMAPER#,CADPERNO                                                
         MVI   TIMLN,TIMALN1Q                                                   
         LA    R3,TIMALN1Q(R3)                                                  
*                                  BUILD NEW TIMEL FOR AUDRECD                  
         USING TIMELD,R3                                                        
CAD49    XC    TIMEL(TIMALNQ),TIMEL                                             
         MVI   TIMEL,TIMELQ                                                     
         MVI   TIMSEQ,0            NOT SET?                                     
         MVI   TIMETYP,TIMEARIN                                                 
         MVC   TIMAIDNO,SVHIROW                                                 
         CLI   CADXCIND,CADXCCHA   TEST TIMESHEET LINE CHANGE                   
         BNE   *+10                                                             
         MVC   TIMAIDNO,CADHIROW   USE CALLER'S ROW NUMBER                      
         MVC   TIMAPULA,CADTMULA                                                
         MVC   TIMAPIDB,SVCLPID    APPROVER PID                                 
         MVI   TIMASTAT,TIMASTMP   TIME ADDED VIA TEMPO OR COST                 
         TM    AUDRSTAT,TIMSFAPP   TEST TIMESHEET FULLY-APPROVED                
         BNZ   *+12                YES - THEN THIS LINE IS TOO                  
         TM    PIDIND,PIDAPFQ      ELSE TEST PID ALREADY ON THIS T/S            
         BZ    CAD50               NO - DON'T SET APPROVAL INFO                 
         OI    TIMASTAT,TIMASAPR   APPROVED STATUS                              
         MVC   TIMADATE,CADTODAY   APPROVAL DATE                                
         ZAP   TIMATIME,CDUB       APPROVAL TIME                                
         MVC   TIMAPDAC,SVACPID    ACTUAL APPROVER IS CONNECTED USER            
CAD50    MVC   TIMAUSER,CADCUID    CONNECTED USER-ID                            
         MVI   TIMLN,TIMALNQ                                                    
         LA    R3,TIMALNQ(R3)                                                   
                                                                                
CAD54    MVI   0(R3),0                                                          
         LA    R3,WKELEM                                                        
CAD56    SR    RF,RF               CHECK ROOM FOR NEW ELEMENT                   
         ICM   RF,3,AUDRLEN        EXISTING RECORD LENGTH                       
         SR    R0,R0                                                            
         IC    R0,1(R3)                                                         
         AR    RF,R0               + L'NEW ELEMENT                              
         CHI   RF,MAXRLN           ENSURE NOT > RECORD MAX                      
         BL    *+12                                                             
         MVI   RECSTS,FULLQ        MAKE A NOTE                                  
         B     CAD58                                                            
         GOTO1 VHELLO,CDMCB,(C'P',ACCMST),AUDRECD,(R3),=C'ADD=END'              
         CLI   12(R1),0                                                         
         BE    *+6                                                              
         DC    H'0'                ELSE HELLO ERROR                             
         SR    R0,R0                                                            
         IC    R0,1(R3)                                                         
         AR    R3,R0                                                            
         CLI   0(R3),0             TEST ANOTHER ELEMENT TO BE ADDED             
         BNE   CAD56               YES - GO ROUND AGAIN                         
                                                                                
CAD58    CLI   RECSTS,ADDPNDQ      TEST NEW AUDREC TO ADD                       
         BNE   CAD60                                                            
         MVI   RECSTS,OKQ                                                       
         GOTO1 VDATAMGR,CDMCB,ADDREC,ACCMST,CIODA,CADAIOA,CDMWK                 
         BE    CAD62                                                            
         DC    H'0'                                                             
CAD60    GOTO1 VDATAMGR,CDMCB,PUTREC,ACCMST,CIODA,CADAIOA,CDMWK                 
         BE    *+6                                                              
         DC    H'0'                                                             
K        USING AUDRECD,CADKEY                                                   
         MVC   K.AUDKSTA,AUDRSTA   UPDATE CHANGES INDEX IN KEY STS              
         GOTO1 VDATAMGR,CDMCB,DMWRIT,ACCDIR,K.AUDKEY,K.AUDKEY,0                 
         BE    *+6                                                              
         DC    H'0'                                                             
         CLI   RECSTS,FULLQ        TEST REC WAS FULL                            
         BNE   CAD62               NO - FINISHED                                
         L     R2,CADAIOA          ELSE START A NEW AUDREC                      
         SR    R0,R0                                                            
         IC    R0,AUDKSEQ          BUMP THE KEY SEQUENCE                        
         AHI   R0,1                                                             
         STC   R0,AUDKSEQ                                                       
         STC   R0,K.AUDKSEQ                                                     
         LA    RF,AUDRFST          SET THE NEW REC LENGTH                       
         MVI   0(RF),0             SET EOR                                      
         AHI   RF,1                ADD ONE FOR EOR                              
         SR    RF,R2                                                            
         STCM  RF,3,AUDRLEN                                                     
         MVI   RECSTS,ADDPNDQ      SET ADD PENDING                              
         B     CAD56                                                            
                                                                                
         USING TIMELD,R3                                                        
CAD62    TM    CADXCALL,CADXFXRR   TEST FLEXBILL 'RETURN HIROW' CALL            
         BZ    *+14                                                             
         MVC   CADHIROW,SVHIROW                                                 
         B     CAD68               FINISHED                                     
         ICM   R3,15,CADAELM       R3=A(TIMEL TO RETURN TO CALLER)              
         BZ    CADDEX              NOT SET = FLEXBILL 'DELETE' CALL             
         XC    TIMEL(TIMETLNQ),TIMEL                                            
         MVI   TIMEL,TIMELQ                                                     
         MVI   TIMLN,TIMETLNQ                                                   
         MVI   TIMETYP,TIMETIME    SET AS DEFAULT                               
         MVC   TIMEPIDC,SVCLPID    SET CLIENT APPROVER                          
         MVC   TIMEIDNO,SVHIROW    SET NEW ROW NUMBER IN TIMEL                  
         CLI   CADXCIND,CADXCCHA   TEST TIMESHEET LINE CHANGE                   
         BNE   *+10                                                             
         MVC   TIMEIDNO,CADHIROW   USE CALLER'S ROW NUMBER                      
         TM    AUDRSTAT,TIMSFAPP   TEST TIMESHEET IS FULLY-APPROVED             
         BNZ   *+12                YES - THEN THIS LINE IS TOO                  
         TM    PIDIND,PIDAPFQ      ELSE TEST THIS PID ALREADY ON T/SHT          
         BZ    *+8                 NO - THEN TREAT AS UNAPPROVED                
         MVI   TIMEPST1,TIMESAPR   SET APPROVED                                 
         MVC   TIMETPDT,TIMENDT    SET PERIOD END DATE                          
         MVC   TIMETDT1,CADTSPSD   TREAT ADJUST AS DAY 1 OF T/S PERIOD          
         ZAP   TIMEHRS1,CADTSHRS   ADJUSTMENT HOURS                             
         MVI   BYTE,TIMEDLNQ       HEADER + 1 DATE                              
         LA    R4,TIMETDT1                                                      
         USING TIMETDT1,R4         SET REMAINING PERIOD DATE FIELDS             
         CLC   TIMETDT1,TIMENDT                                                 
         BE    CAD66                                                            
CAD64    GOTO1 VDATCON,CDMCB,(1,TIMETDT1),(0,CHARDAT)                           
         GOTO1 VADDAY,CDMCB,CHARDAT,CHARDAT,F'1'                                
         GOTO1 VDATCON,CDMCB,(0,CHARDAT),(1,TIMETDT2)                           
         ZAP   TIMEHRS2,PZERO                                                   
         SR    R1,R1                                                            
         IC    R1,BYTE                                                          
         AHI   R1,L'TIMETDT1+L'TIMEHRS1                                         
         STC   R1,BYTE             UPDATE LENGTH                                
         CLC   TIMETDT2,TIMENDT                                                 
         BE    CAD66                                                            
         LA    R4,L'TIMETDT1+L'TIMEHRS1(R4)                                     
         B     CAD64                                                            
*                                                                               
CAD66    MVC   TIMLN,BYTE          UPDATE LENGTH                                
*                                                                               
CAD68    MVC   CADTSSTS,AUDRSTAT   RETURN STATUS TO CALLER                      
         B     CADDEX                                                           
                                                                                
CADDEX   XMOD1 1                                                                
         DROP  R2,R3,R4,K,KS                                                    
         EJECT                                                                  
*                                  LINE MANAGER LOOK UP                         
LMLKUP   NTR1  ,                                                                
         USING DPAPASD,R2                                                       
         LA    R2,CADKEY                                                        
         LA    R0,4                N'1R ACC LEVELS                              
         OC    CAD1RLVS,CAD1RLVS   * IF NOT SET, WE SHOULDN'T BE HERE           
         BNZ   *+6                 * SO MAY NOT BE BR'O TIME AFTER ALL?         
         DC    H'0'                * CHECK BR'O T/S PROFILE IN =COST            
LML02    LA    RE,CAD1RLVS-1                                                    
         AR    RE,R0               WORK FROM LOWEST LEVEL BACK                  
         SR    RF,RF                                                            
         IC    RF,0(RE)                                                         
         AHI   RF,-1               -1 FOR EXECUTE                               
         XC    DPAPAS,DPAPAS                                                    
         MVI   DPAPTYP,DPAPTYPQ                                                 
         MVI   DPAPSUB,DPAPSUBQ                                                 
         MVC   DPAPCPY,CADCPYCD                                                 
         MVI   DPAPAPPL,DPAPATIM   TIME ONLY                                    
         ZAP   DPAPXVAL,PZERO                                                   
         MVC   DPAP1RAC,SPACES                                                  
         MVC   DPAP1RAC(0),CAD1RACT                                             
         EX    RF,*-6                                                           
         MVC   KEYSAV2,CADKEY                                                   
         GOTO1 VDATAMGR,CDMCB,(0,DMRDHI),ACCDIR,CADKEY,CADKEY,0                 
         B     LML08                                                            
LML06    GOTO1 VDATAMGR,CDMCB,(0,DMRSEQ),ACCDIR,CADKEY,CADKEY,0                 
                                                                                
LML08    CLC   KEYSAV2(DPAPPIDB-DPAPASD),DPAPAS                                 
         BE    *+12                                                             
LML10    BCT   R0,LML02            KEY CHANGED, TRY NEXT ACC LVL UP             
         B     LMLEX               NO LINE MANAGER FOUND AT ANY LVL             
         MVC   SVLNMGR,DPAPPIDB    SAVE LINE MANAGER                            
                                                                                
LMLEX    XIT1                                                                   
                                                                                
CLLKUP   NTR1  ,                                                                
         LA    R2,APRTAB                                                        
         USING APRTABD,R2                                                       
SJ       USING JOBPASD,CADKEY      FIND JOB, PRODUCT OR CLIENT APPROVER         
CLLKUP06 XC    SJ.JOBPAS,SJ.JOBPAS                                              
         MVI   SJ.JOBPTYP,JOBPTYPQ                                              
         MVI   SJ.JOBPSUB,JOBPSUBQ                                              
         MVC   SJ.JOBPCPY,CADCPYCD                                              
         MVI   SJ.JOBPVIEW,JOBPVOFF                                             
         MVI   SJ.JOBPAPPL,JOBPATIM                                             
         MVC   SJ.JOBPCPJ,SPACES                                                
         MVC   SJ.JOBPCOFF,SPACES                                               
         MVC   SJ.JOBPCMED,SPACES                                               
         TM    APRSTAT,APRJOB      ARE WE LOOKING AT JOB LEVEL                  
         JZ    CLLKUP08                                                         
         LLC   RF,CADSJLVS+1                                                    
         LA    RF,CADTMULA+2(RF)                                                
         CLI   0(RF),X'40'         HAVE WE GOT A JOB                            
         JNH   CLLKUP18            NO                                           
         LLC   RF,CADSJLVS+2                                                    
         SHI   RF,1                                                             
         BASR  RE,0                                                             
         MVC   SJ.JOBPCPJ(0),CADTMULA+2                                         
         EX    RF,0(RE)                                                         
         J     CLLKUP12                                                         
CLLKUP08 TM    APRSTAT,APRPRO                                                   
         JZ    CLLKUP10                                                         
         LLC   RF,CADSJLVS                                                      
         LA    RF,CADTMULA+2(RF)                                                
         CLI   0(RF),X'40'         HAVE WE GOT A PRODUCT                        
         JNH   CLLKUP18            NO                                           
         LLC   RF,CADSJLVS+1                                                    
         SHI   RF,1                                                             
         BASR  RE,0                                                             
         MVC   SJ.JOBPCPJ(0),CADTMULA+2                                         
         EX    RF,0(RE)                                                         
         J     CLLKUP12                                                         
CLLKUP10 TM    APRSTAT,APRCLI                                                   
         JZ    CLLKUP12                                                         
         LLC   RF,CADSJLVS                                                      
         SHI   RF,1                                                             
         BASR  RE,0                                                             
         MVC   SJ.JOBPCPJ(0),CADTMULA+2                                         
         EX    RF,0(RE)                                                         
CLLKUP12 TM    APRSTAT,APRMED                                                   
         JZ    CLLKUP14                                                         
         LLC   RF,CADSJLVS+1                                                    
         LA    RF,CADTMULA+2(RF)                                                
         CLI   0(RF),X'40'         HAVE WE GOT A JOB                            
         JNH   CLLKUP18            NO                                           
         MVC   SJ.JOBPCMED,0(RF)                                                
                                                                                
CLLKUP14 TM    APRSTAT,APROFF                                                   
         JZ    CLLKUP16                                                         
         OC    CADCLIOF,CADCLIOF                                                
         JZ    CLLKUP18                                                         
         MVC   SJ.JOBPCOFF,CADCLIOF                                             
                                                                                
CLLKUP16 MVC   KEYSAVE,SJ.JOBPAS                                                
         GOTO1 VDATAMGR,CDMCB,(0,DMRDHI),ACCDIR,CADKEY,CADKEY,0                 
         CLC   SJ.JOBPAS(JOBPPIDB-JOBPASD),KEYSAVE                              
         JE    CLLKUP20                                                         
                                                                                
CLLKUP18 LA    R2,APRTABL(R2)                                                   
         CLI   0(R2),X'FF'                                                      
         JNE   CLLKUP06                                                         
         J     CLLKUPX                                                          
                                                                                
CLLKUP20 MVC   SVCLPID,SJ.JOBPPIDB                                              
         DROP  SJ                                                               
CLLKUPX  XIT1                                                                   
                                                                                
         DROP  R9                                                               
*                                                                               
         LTORG                                                                  
*                                                                               
                                                                                
ACCDIR   DC    C'ACCDIR '                                                       
ACCMST   DC    C'ACCMST '                                                       
DMREAD   DC    C'DMREAD '                                                       
DMRDHI   DC    C'DMRDHI '                                                       
DMRSEQ   DC    C'DMRSEQ '                                                       
DMWRIT   DC    C'DMWRT  '                                                       
GETREC   DC    C'GETREC '                                                       
PUTREC   DC    C'PUTREC '                                                       
ADDREC   DC    C'ADDREC '                                                       
CONFIL   DC    C'CTFILE '                                                       
PZERO    DC    P'0'                                                             
                                                                                
SPACES   DC    64C' '                                                           
                                                                                
MAXRLN   EQU   2000                                                             
                                                                                
APRTAB   DS    0XL1                                                             
         DC    B'11111000'         OFFICE/CLIENT/PRODUCT/JOB/MEDIA              
         DC    B'11011000'         OFFICE/CLIENT/PRODUCT/MEDIA                  
         DC    B'10011000'         OFFICE/CLIENT/MEDIA                          
         DC    B'00011000'         OFFICE/MEDIA                                 
         DC    B'00010000'         MEDIA                                        
         DC    B'11001000'         OFFICE/CLIENT/PRODUCT                        
         DC    B'10001000'         OFFICE/CLIENT                                
         DC    B'10000000'         CLIENT                                       
         DC    B'00001000'         OFFICE                                       
         DC    X'FF'                                                            
                                                                                
WORKD    DSECT                                                                  
VDATAMGR DS    V                                                                
VHELLO   DS    V                                                                
VADDAY   DS    V                                                                
VDATCON  DS    V                                                                
CDUB     DS    D                                                                
CFULL    DS    F                                                                
CDMCB    DS    6F                                                               
CDMWK    DS    12D                                                              
CADKEY   DS    XL64                                                             
CIODA    DS    XL4                                                              
KEYSAVE  DS    XL(L'CADKEY)                                                     
KEYSAV2  DS    XL(L'CADKEY)                                                     
SVCLPID  DS    XL(L'TIMEPIDC)      SAVED CLIENT APPROVER PID                    
SVBAPID  DS    XL(L'TIMEPIDC)      SAVED BACKUP APPROVER PID                    
SVACPID  DS    XL(L'TIMAPDAC)      SAVED ADJUSTER APPROVER PID                  
SVHIROW  DS    XL2                 SAVED HIGHEST USED ROW NUMBER                
SVLNMGR  DS    XL2                 SAVED LINE MANAGER                           
TIMENDT  DS    PL3                 EARLIER OF PERIOD/LOCATION END DATE          
CHARDAT  DS    CL6                 CHARACTER DATE WORK FIELD                    
WKELEM   DS    XL512               ELEMENT WORK AREA                            
RECSTS   DS    CL1                                                              
FULLQ    EQU   C'F'                AUDREC FULL: WRITE BACK, ADD ANOTHER         
OKQ      EQU   C'O'                AUDREC OK                                    
ADDPNDQ  EQU   C'A'                AUDREC ADD PENDING                           
PIDIND   DS    XL1                                                              
PIDAPFQ  EQU   X'80'               MATCH FOR APPROVER PID ON AUDREC             
BYTE     DS    XL1                                                              
WORKX    EQU   *                                                                
                                                                                
***********************************************************************         
* APPROVAL HIERARCHY SEARCH TABLE DSECT                               *         
***********************************************************************         
                                                                                
APRTABD  DSECT                                                                  
APRSTAT  DS    XL1                 LEVELS TO INCLUDE                            
APRCLI   EQU   X'80'               CLIENT                                       
APRPRO   EQU   X'40'               PRODUCT                                      
APRJOB   EQU   X'20'               JOB                                          
APRMED   EQU   X'10'               MEDIA                                        
APROFF   EQU   X'08'               OFFICE                                       
APRTABL  EQU   *-APRTABD                                                        
                                                                                
         EJECT                                                                  
       ++INCLUDE ACCADETD                                                       
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
       ++INCLUDE FASECRETD                                                      
       ++INCLUDE SEACSFILE                                                      
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'019ACCADET   08/25/17'                                      
         END                                                                    
