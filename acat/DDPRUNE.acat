*          DATA SET DDPRUNE    AT LEVEL 004 AS OF 05/01/02                      
*CATALP PRUNE                                                                   
         TITLE 'PRUNE - GET/PUT CARDS TO TWA FOR $RUN'                          
* PARM1  A(ACTION)  OPENR/OPENW/GET/PUT/CLOSE                                   
* PARM2  A(CARD)                                                                
* PARM3  A(BUFFER)  PASS BACK ERRORS AS FOR DATAMGR                             
* PARM4  V(DATAMGR)                                                             
*                                                                               
         PRINT NOGEN                                                            
PRUNE    CSECT                                                                  
         NMOD1 4,**PRUNE*                                                       
         USING WORKD,RC                                                         
         MVI   8(R1),0                                                          
         LR    R2,R1               R2=A(PARMS)                                  
         LM    R3,R6,0(R1)         R4=A(CARD)                                   
         USING RUND,R5             R5=A(BUFFER)                                 
         SPACE 2                                                                
         OC    RUNTRM,RUNTRM       BUFFER MUST HAVE TERMINAL NUMBER             
         BNZ   *+6                                                              
         DC    H'0'                                                             
         CLC   RUNTRM,=H'1000'                                                  
         BNH   *+6                                                              
         DC    H'0'                                                             
         SPACE 2                                                                
         CLC   0(5,R3),=C'OPENR'                                                
         BE    OPR                                                              
         CLC   0(5,R3),=C'OPENW'                                                
         BE    OPW                                                              
         CLC   0(3,R3),=C'GET'                                                  
         BE    GET                                                              
         CLC   0(3,R3),=C'PUT'                                                  
         BE    PUT                                                              
         CLC   0(5,R3),=C'CLOSE'                                                
         BE    CLS                                                              
         DC    H'0'                                                             
         SPACE 2                                                                
EXIT     XMOD1 1                                                                
         EJECT                                                                  
OPR      MVC   DUB,RUNID           OPEN FOR READING                             
         BAS   R9,TWAR                                                          
         OC    RUNID,RUNID                                                      
         BZ    OPR2                NOTFOUND IF NO ID ON DISK                    
         OC    DUB,DUB                                                          
         BZ    *+14                                                             
         CLC   DUB,RUNID                                                        
         BNE   OPR2                NOTFOUND IF PASSED ID DOESNT MATCH           
         XC    RUNLCARD,RUNLCARD                                                
         B     EXIT                                                             
OPR2     MVI   8(R2),X'10'         RETURN NOTFOUND                              
         B     EXIT                                                             
         SPACE 2                                                                
OPW      LA    RE,10(R5)           OPEN FOR WRITING                             
         LA    RF,2304-10                                                       
         XCEF                                                                   
         THMS                                                                   
         ST    R1,DUB                                                           
         MVC   RUNCTIME,DUB        SET CREATE TIME                              
         B     EXIT                                                             
         SPACE 2                                                                
GET      LH    RF,RUNLCARD         GET NEXT CARD FROM BUFFER                    
         LA    RF,1(RF)                                                         
         CH    RF,RUNNCARD                                                      
         BNH   *+12                                                             
         MVI   8(R2),X'90'         RETURN EOF/NOTFOUND                          
         B     EXIT                                                             
         STH   RF,RUNLCARD                                                      
         MH    RF,=H'80'                                                        
         AR    RF,R5                                                            
         MVC   0(80,R4),0(RF)                                                   
         B     EXIT                                                             
         SPACE 2                                                                
PUT      LH    RF,RUNNCARD         PUT NEXT CARD INTO BUFFER                    
         LA    RF,1(RF)                                                         
         CH    RF,MAXCARDS                                                      
         BNH   *+12                                                             
         MVI   8(R2),X'90'         SET EOF/NOTFOUND                             
         B     EXIT                                                             
         STH   RF,RUNNCARD                                                      
         MH    RF,=H'80'                                                        
         AR    RF,R5                                                            
         MVC   0(80,RF),0(R4)                                                   
         B     EXIT                                                             
         SPACE 2                                                                
CLS      BAS   R9,TWAW             WRITE BUFFER TO DISK                         
         B     EXIT                                                             
         SPACE 2                                                                
TWAR     LA    RE,DMRDIR           TWA READ                                     
         B     *+8                                                              
TWAW     LA    RE,DMWRT            TWA WRITE                                    
         ST    RE,DMCB                                                          
         LA    RE,TEMPSTR                                                       
         ST    RE,DMCB+4                                                        
         MVI   DMCB+8,4            SET TWA NUMBER                               
         MVI   DMCB+9,0                                                         
         MVC   DMCB+10(2),RUNTRM   SET TRM NUM FROM BUFFER                      
         ST    R5,DMCB+12                                                       
         LA    R1,DMCB                                                          
         BASR  RE,R6                                                            
         CLI   8(R1),0                                                          
         BE    TWAX                                                             
         MVI   8(R2),X'40'         RETURN DISK ERROR                            
         B     EXIT                                                             
TWAX     BR    R9                                                               
         SPACE 2                                                                
DMRDIR   DC    CL8'DMRDIR'                                                      
DMWRT    DC    CL8'DMWRT'                                                       
TEMPSTR  DC    CL8'TEMPSTR'                                                     
MAXCARDS DC    H'16'                                                            
         SPACE 2                                                                
         LTORG                                                                  
         SPACE 2                                                                
WORKD    DSECT                                                                  
DUB      DS    D                                                                
DMCB     DS    6F                                                               
         SPACE 2                                                                
RUND     DSECT                                                                  
RUNID    DS    CL8                                                              
RUNTRM   DS    XL2                                                              
RUNNCARD DS    XL2                                                              
RUNLCARD DS    XL2                                                              
RUNCTIME DS    PL4                                                              
RUNRTIME DS    PL4                                                              
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'004DDPRUNE   05/01/02'                                      
         END                                                                    
