*          DATA SET ROLDDCNT   AT LEVEL 002 AS OF 05/01/02                      
*                                                                               
*CATALP ROLDDCNT                                                                
         TITLE 'LDCOUNT - REP/ROI - DIRECTORY REC TYPE COUNT/PRINT'             
*                                                                               
*******************************************************************             
*                                                                 *             
*       ROLDDCNT - LDCOUNT - COUNT REP/ROI DIRECTORY PTRS         *             
*                                                                 *             
*        PARAMS VIA R1                                            *             
*        XL1   COUNT/PRINT  X'00'=COUNT  X'FF'=PRINT              *             
*        AL3   A(RECORD)                                          *             
*        AL4   A(FILE NAME)                                       *             
*                                                                 *             
* --------------------------------------------------------------- *             
* UPDATE HISTORY:                                                 *             
*                                                                 *             
* SEP27/89 (MRR) --- INITAL RELEASE                               *             
*                                                                 *             
*                                                                 *             
*                                                                 *             
*                                                                 *             
*******************************************************************             
*                                                                               
         PRINT NOGEN                                                            
LDCOUNT  CSECT                                                                  
         NMOD1 4,ROLDDCNT                                                       
         USING WRKD,RC                                                          
*                                                                               
         CLI   0(R1),X'FF'                                                      
         BE    PRINT                                                            
         EJECT                                                                  
COUNT    L     R2,0(R1)            R2=A(RECORD)                                 
         LA    R3,CNTRS+6          R3=A(COUNTERS ENTRY)                         
         LH    R4,CNTRS                                                         
         L     R5,CNTRS+2                                                       
*                                                                               
         TM    0(R2),X'80'         TEST MINOR PTR                               
         BO    C1                                                               
         CLI   27(R2),X'FF'        KEY CHANGE?                                  
         BNE   C1                                                               
         LA    R3,FFTOTALS                                                      
         B     C2                                                               
*                                                                               
C1       CLC   0(1,R2),0(R3)       REC TYPES AGREE                              
         BE    C2                  YES                                          
         BXLE  R3,R4,C1            NO LOOP                                      
         SR    R3,R4               NOT FOUND BACK UP TO UNKNOWN                 
         CLI   0(R2),0             TEST HEADER                                  
         BNE   *+6                 NO                                           
         SR    R3,R4               YES - BACK UP TO CONTROL                     
*                                                                               
C2       L     RE,20(R3)           UPDATE TOTAL                                 
         LA    RE,1(RE)                                                         
         ST    RE,20(R3)                                                        
         SPACE 2                                                                
XIT      XMOD1 1                                                                
         EJECT                                                                  
PRINT    L     RA,=V(CPRINT)                                                    
         USING DPRINT,RA                                                        
         ZAP   LINE,=P'99'                                                      
         L     RE,4(R1)                                                         
         MVC   HLINE(7),0(RE)                                                   
*                                                                               
         LA    RE,L'HLINE          HEADING                                      
         LA    RF,HLINE                                                         
         BAS   R9,PUT                                                           
         LA    RF,ULINE                                                         
         BAS   R9,PUT                                                           
*                                                                               
         LA    R3,CNTRS+6          RECORD TYPE LINES                            
         LH    R4,CNTRS                                                         
PR1      CLI   0(R3),0                                                          
         BE    PR2                                                              
         BAS   RE,SET                                                           
         LA    RE,L'PLINE                                                       
         LA    RF,PLINE                                                         
         BAS   R9,PUT                                                           
         AR    R3,R4                                                            
         B     PR1                                                              
*                                                                               
PR2      LA    RE,L'ULINE          TOTALS                                       
         LA    RF,ULINE                                                         
         BAS   R9,PUT                                                           
         LA    R3,MJTOTALS                                                      
         BAS   RE,SET                                                           
         LA    RE,L'PLINE                                                       
         LA    RF,PLINE                                                         
         BAS   R9,PUT                                                           
         LA    R3,MNTOTALS                                                      
         BAS   RE,SET                                                           
         LA    RE,L'PLINE                                                       
         LA    RF,PLINE                                                         
         BAS   R9,PUT                                                           
         LA    R3,FFTOTALS                                                      
         BAS   RE,SET                                                           
         LA    RE,L'PLINE                                                       
         LA    RF,PLINE                                                         
         BAS   R9,PUT                                                           
*                                                                               
         L     R0,FFTOTALS+20                                                   
         A     R0,TOTALS+20                                                     
         ST    R0,TOTALS+20                                                     
*                                                                               
         LA    R3,TOTALS                                                        
         BAS   RE,SET                                                           
         LA    RE,L'PLINE                                                       
         LA    RF,PLINE                                                         
         BAS   R9,PUT                                                           
         LA    RF,ULINE                                                         
         BAS   R9,PUT                                                           
*                                                                               
         B     XIT                                                              
         EJECT                                                                  
PUT      STM   RE,RF,DUB                                                        
         MVC   P,SPACES                                                         
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   P(0),0(RF)                                                       
         GOTO1 =V(PRINTER)                                                      
         LM    RE,RF,DUB                                                        
PUTX     BR    R9                                                               
         EJECT                                                                  
SET      NTR1                                                                   
         CLI   0(R3),0             IS THIS A TOTAL CTR                          
         BE    SET2                                                             
         L     R5,TOTALS+20        UPDATE GRAND TOTAL                           
         A     R5,20(R3)                                                        
         ST    R5,TOTALS+20                                                     
         CLI   0(R3),X'FF'         ASSUME CONTROL/UNKNOWN ARE MAJ PTRS          
         BE    *+12                                                             
         TM    0(R3),X'80'         TEST MAJOR                                   
         BO    SETMN               NO                                           
         L     R5,MJTOTALS+20      UPDATE MAJOR PTR TOTAL                       
         A     R5,20(R3)                                                        
         ST    R5,MJTOTALS+20                                                   
         B     SET2                                                             
*                                                                               
SETMN    L     R5,MNTOTALS+20      UPDATE MINOR PTR TOTALS                      
         A     R5,20(R3)                                                        
         ST    R5,MNTOTALS+20                                                   
*                                                                               
SET2     MVI   PLINE,C' '                                                       
         MVC   PLINE+1(L'PLINE-1),PLINE                                         
         MVC   PLTYPE,1(R3)                                                     
         LA    R6,20(R3)                                                        
         LA    R7,PLTOT                                                         
         BAS   R9,SETCVD                                                        
         B     SETX                                                             
*                                                                               
SETCVD   L     R0,0(R6)                                                         
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  0(8,R7),DUB                                                      
         BR    R9                                                               
*                                                                               
SETX     XIT1                                                                   
         EJECT                                                                  
HLINE    DC    C'XXXXXXX RECORD TYPE  TOTAL   '                                 
ULINE    DC    C'-------------------  --------'                                 
PLINE    DS    0CL29                                                            
PLTYPE   DS    CL19                                                             
         DS    CL2                                                              
PLTOT    DS    CL8                                                              
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
         CNOP  2,4                                                              
CNTRS    DC    H'24'                                                            
         DC    A(CNTRSX-1)                                                      
         DC    X'27',CL19'27 - ATHENA        ',F'0'                             
         DC    X'2F',CL19'2F - PAPER WORK CNT',F'0'                             
         DC    X'FF',CL19'   - CONTROL       ',F'0'                             
         DC    X'FF',CL19'?? - UNKNOWN       ',F'0'                             
CNTRSX   DS    0C                                                               
*                                                                               
MJTOTALS DC    X'00',CL19'MAJOR POINTERS     ',F'0'                             
MNTOTALS DC    X'00',CL19'MINOR POINTERS     ',F'0'                             
FFTOTALS DC    X'00',CL19'CHANGED KEY PTRS   ',F'0'                             
TOTALS   DC    X'00',CL19'TOTALS             ',F'0'                             
         EJECT                                                                  
*DDDPRINT                                                                       
       ++INCLUDE DDDPRINT                                                       
         EJECT                                                                  
WRKD     DSECT                                                                  
DUB      DS    D                                                                
P1       DS    F                                                                
P2       DS    F                                                                
P3       DS    F                                                                
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'002ROLDDCNT  05/01/02'                                      
         END                                                                    
