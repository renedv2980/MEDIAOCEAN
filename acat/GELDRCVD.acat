*          DATA SET GELDRCVD   AT LEVEL 001 AS OF 10/24/11                      
*CATALP GELDRCVD                                                                
         TITLE 'LDRCVD - CONTROL SYSTEM - RECOVERY DUMP ROUTINE'                
                                                                                
* PARAMS VIA R1                                                                 
* XL1    X'00'=INIT,X'01'=PROCESS,X'FF'=FINAL.                                  
*        RETURN X'FF'=DONT WRITE RECORD TO DUMP TAPE                            
*        RETURN X'FE'=DELETED REC,X'FD'=RESTORED REC                            
* AL3    A(RECOVERY FILE RECORD)                                                
                                                                                
         PRINT NOGEN                                                            
LDRCVD   CSECT                                                                  
         ENTRY LDRCVDT             TABLE OF COUNTERS AND ACTIONS                
                                                                                
         NMOD1 WORKX-WORKD,ACLDRCVD                                             
         USING WORKD,RC                                                         
         LR    RA,R1               RA=A(PARAMETER LIST)                         
         ST    R1,APARM                                                         
         SR    R3,R3                                                            
         ICM   R3,7,1(R1)          R3=A(RECOVERY HEADER)                        
         USING RCVRECD,R3                                                       
         CLI   0(R1),X'01'         TEST ACTION                                  
         BL    INIT                                                             
         BE    PROCESS                                                          
         BH    FINAL                                                            
                                                                                
INIT     LA    RE,LDRCVDT          CLEAR COUNTERS ON INIT CALL                  
INIT1    CLC   0(4,RE),=4X'FF'                                                  
         BE    EXIT                                                             
         XC    0(4,RE),0(RE)                                                    
         LA    RE,L'LDRCVDT(RE)                                                 
         B     INIT1                                                            
                                                                                
FINAL    B     EXIT                FINAL CALL                                   
                                                                                
LGCLDEL  MVI   0(RA),X'FE'         RETURN RECORD IS LOGICALLY DELETED           
         B     EXIT                                                             
                                                                                
LGCLRST  MVI   0(RA),X'FD'         RETURN RECORD IS LOGICALLY RESTORED          
         B     EXIT                                                             
                                                                                
DELETE   MVI   0(RA),X'FF'         RETURN DELETE RECORD FROM DUMP TAPE          
                                                                                
EXIT     XMOD1 1                                                                
                                                                                
         EJECT                                                                  
* PROCESS A RECOVERY FILE RECORD                                                
* UPDATE TABLE OF ANY ACTIONS THAT NEED PRINTING AND ERRORS/WARNINGS            
* EXIT VIA DELETE TO REQUEST RECORD NOT WRITTEN TO RECOVERY DUMP TAPE           
                                                                                
PROCESS  CLI   RFILTY,GENFILQ      GENFIL RECOVERY RECORD                       
         BE    FIL                                                              
         CLI   RFILTY,GENDIRQ      GENDIR RECOVERY RECORD                       
         BE    DIR                                                              
         CLI   RFILTY,CTFILEQ      CTFILE RECOVERY RECORD                       
         BE    CTF                                                              
         B     EXIT                                                             
                                                                                
DIR      EQU   *                   TEST SPECIAL GENDIR RECORD                   
DIR1     CLI   RRECTY,2            TEST GENDIR CHANGE                           
         BNE   SAVE                                                             
         TM    KEYSTAT,X'80'       TEST DELETED                                 
         BZ    DIR2                                                             
         L     RF,=A(LASTRCV)                                                   
         CLI   RFILTY-RCVHDR(RF),GENDIRQ                                        
         BNE   SAVE                                                             
         CLI   RRECTY-RCVHDR(RF),1 TEST LAST RECORD GENDIR COPY                 
         BNE   SAVE                                                             
         TM    KEYSTAT-RCVHDR(RF),X'80'                                         
         BO    SAVE                                                             
         CLC   KEYKEY,KEYKEY-RCVHDR(RF)                                         
         BNE   SAVE                                                             
         L     R0,RCVDIRD          BUMP GENDIR DELETED RECORDS                  
         AHI   R0,1                                                             
         ST    R0,RCVDIRD                                                       
         MVI   0(RA),X'FE'         RETURN RECORD IS LOGICALLY DELETED           
         B     SAVE                                                             
                                                                                
DIR2     L     RF,=A(LASTRCV)      TEST IF COPY RECORD WAS ACTIVE               
         CLI   RFILTY-RCVHDR(RF),GENDIRQ                                        
         BNE   SAVE                                                             
         CLI   RRECTY-RCVHDR(RF),1                                              
         BNE   SAVE                                                             
         TM    KEYSTAT-RCVHDR(RF),X'80'                                         
         BZ    SAVE                                                             
         CLC   KEYKEY,KEYKEY-RCVHDR(RF)                                         
         BNE   SAVE                                                             
         L     R0,RCVDIRR          BUMP GENDIR RESTORED RECORDS                 
         AHI   R0,1                                                             
         ST    R0,RCVDIRR                                                       
         MVI   0(RA),X'FD'         RETURN RECORD IS LOGICALLY RESTORED          
         B     SAVE                                                             
                                                                                
FIL      EQU   *                   TEST SPECIAL GENFIL RECORD                   
FIL1     CLI   RRECTY,2            TEST GENFIL CHANGE                           
         BNE   SAVE                                                             
         TM    RECSTAT,X'80'       TEST DELETED                                 
         BZ    FIL2                                                             
         L     RF,=A(LASTRCV)                                                   
         CLI   RFILTY-RCVHDR(RF),GENFILQ                                        
         BNE   SAVE                                                             
         CLI   RRECTY-RCVHDR(RF),1 TEST LAST RECORD GENFIL COPY                 
         BNE   SAVE                                                             
         TM    RECSTAT-RCVHDR(RF),X'80'                                         
         BO    SAVE                                                             
         CLC   RECKEY,RECKEY-RCVHDR(RF)                                         
         BNE   SAVE                                                             
         L     R0,RCVFILD          BUMP GENFIL DELETED RECORDS                  
         AHI   R0,1                                                             
         ST    R0,RCVFILD                                                       
         MVI   0(RA),X'FE'         RETURN RECORD IS LOGICALLY DELETED           
         B     SAVE                                                             
                                                                                
FIL2     L     RF,=A(LASTRCV)      TEST IF COPY RECORD WAS ACTIVE               
         CLI   RFILTY-RCVHDR(RF),GENFILQ                                        
         BNE   SAVE                                                             
         CLI   RRECTY-RCVHDR(RF),1                                              
         BNE   SAVE                                                             
         TM    RECSTAT-RCVHDR(RF),X'80'                                         
         BZ    SAVE                                                             
         CLC   RECKEY,RECKEY-RCVHDR(RF)                                         
         BNE   SAVE                                                             
         L     R0,RCVFILR          BUMP GENFIL RESTORED RECORDS                 
         AHI   R0,1                                                             
         ST    R0,RCVFILR                                                       
         MVI   0(RA),X'FD'         RETURN RECORD IS LOGICALLY RESTORED          
         B     SAVE                                                             
                                                                                
CTF      EQU   *                   TEST SPECIAL CTFILE RECORD                   
CTF1     CLI   RRECTY,2            TEST CTFILE CHANGE                           
         BNE   SAVE                                                             
         TM    CTFSTAT,X'80'       TEST DELETED                                 
         BZ    CTF2                                                             
         L     RF,=A(LASTRCV)                                                   
         CLI   RFILTY-RCVHDR(RF),CTFILEQ                                        
         BNE   SAVE                                                             
         CLI   RRECTY-RCVHDR(RF),1 TEST LAST RECORD CTFILE COPY                 
         BNE   SAVE                                                             
         TM    CTFSTAT-RCVHDR(RF),X'80'                                         
         BO    SAVE                                                             
         CLC   CTFKEY,CTFKEY-RCVHDR(RF)                                         
         BNE   SAVE                                                             
         L     R0,RCVCTFD          BUMP CTFILE DELETED RECORDS                  
         AHI   R0,1                                                             
         ST    R0,RCVCTFD                                                       
         MVI   0(RA),X'FE'         RETURN RECORD IS LOGICALLY DELETED           
         B     SAVE                                                             
                                                                                
CTF2     L     RF,=A(LASTRCV)      TEST IF COPY RECORD WAS ACTIVE               
         CLI   RFILTY-RCVHDR(RF),CTFILEQ                                        
         BNE   SAVE                                                             
         CLI   RRECTY-RCVHDR(RF),1                                              
         BNE   SAVE                                                             
         TM    CTFSTAT-RCVHDR(RF),X'80'                                         
         BZ    SAVE                                                             
         CLC   CTFKEY,CTFKEY-RCVHDR(RF)                                         
         BNE   SAVE                                                             
         L     R0,RCVCTFR          BUMP CTFILE RESTORED RECORDS                 
         AHI   R0,1                                                             
         ST    R0,RCVCTFR                                                       
         MVI   0(RA),X'FD'         RETURN RECORD IS LOGICALLY RESTORED          
         B     SAVE                                                             
                                                                                
SAVE     L     R0,=A(LASTRCV)      SAVE RECOVERY RECORD                         
         LHI   R1,LASTRCVL         R1=MAXIMUM RECORD LENGTH                     
         LR    RE,R3                                                            
         AHI   RE,-4                                                            
         LH    RE,0(RE)                                                         
         AHI   RE,-4               RE=ACTUAL RECORD LENGTH                      
         BM    SAVE1                                                            
         CR    RE,R1                                                            
         BNL   SAVE1                                                            
         LR    R1,RE                                                            
SAVE1    LR    RE,R3                                                            
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
         B     EXIT                                                             
                                                                                
         LTORG                                                                  
                                                                                
* THIS ENTRY POINT TABLE IS PROCESSED BY RCVDMP CALLER AFTER FINAL CALL         
* EACH ENTRY HAS A COUNTER,TEXT,TYPE,AND SPARE                                  
* ENTRIES WITH A NON-ZERO COUNTER WILL BE PRINTED                               
* ERRORS/WARNINGS WILL GO TO CONSOLE AND WILL BE EMAILED                        
         DS    0F                                                               
LDRCVDT  DS    0CL24                                                            
RCVCTFD  DC    F'0',CL16'CTFILE DELETES  ',C'I ',AL2(0)                         
RCVCTFR  DC    F'0',CL16'CTFILE RESTORES ',C'I ',AL2(0)                         
RCVDIRD  DC    F'0',CL16'GENDIR DELETES  ',C'I ',AL2(0)                         
RCVDIRR  DC    F'0',CL16'GENDIR RESTORES ',C'I ',AL2(0)                         
RCVFILD  DC    F'0',CL16'GENFIL DELETES  ',C'I ',AL2(0)                         
RCVFILR  DC    F'0',CL16'GENFIL RESTORES ',C'I ',AL2(0)                         
RCVERR   DC    F'0',CL16'GEN ERRORS      ',C'E ',AL2(0)                         
RCVWARN  DC    F'0',CL16'GEN WARNINGS    ',C'W ',AL2(0)                         
LDRCVDTX DC    4X'FF'                                                           
                                                                                
LASTRCV  DC    2048X'00'                                                        
LASTRCVL EQU   *-LASTRCV                                                        
                                                                                
CTFILEQ  EQU   X'A1'                                                            
GENDIRQ  EQU   X'AE'                                                            
GENFILQ  EQU   X'AF'                                                            
                                                                                
* DSECT TO LOCAL WORKING STORAGE                                                
*                                                                               
WORKD    DSECT                                                                  
APARM    DS    A                                                                
WORKX    EQU   *                                                                
                                                                                
* DSECT TO COVER RECOVERY HEADER                                                
*                                                                               
RCVRECD  DSECT                                                                  
RCVHDR   DS    0XL24                                                            
*DMRCVRHDR                         RECOVERY HEADER                              
       ++INCLUDE DMRCVRHDR                                                      
                                                                                
RECREC   DS    0X                  RECORD                                       
RECKEY   DS    0XL32               RECORD KEY                                   
         DS    CL32                                                             
RECLEN   DS    XL2                 RECORD LENGTH                                
RECSTAT  DS    XL4                 RECORD STATUS                                
RECDATA  DS    0X                  RECORD DATA                                  
                                                                                
         ORG   RECREC                                                           
KEYREC   DS    0XL40               KEY RECORD                                   
KEYKEY   DS    XL32                KEY KEY                                      
KEYSTAT  DS    XL4                 KEY STATUS                                   
KEYADDR  DS    XL4                 KEY DISK ADDRESS                             
                                                                                
         ORG   RECREC                                                           
CTFREC   DS    0XL29               CTFILE RECORD                                
CTFKEY   DS    XL25                CTFILE KEY                                   
CTFLEN   DS    XL2                 CTFILE LENGTH                                
CTFSTAT  DS    XL1                 CTFILE STATUS                                
CTFDATA  DS    XL1                 CTFILE DATA STARTS HERE                      
                                                                                
*DMRCVREXT                         RECOVERY TRAILER                             
       ++INCLUDE DMRCVREXT                                                      
                                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'001GELDRCVD  10/24/11'                                      
         END                                                                    
