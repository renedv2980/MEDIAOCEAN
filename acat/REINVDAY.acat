*          DATA SET REINVDAY   AT LEVEL 010 AS OF 05/01/02                      
*CATALP INVDAY                                                                  
         TITLE 'VALIDATE DAY - GET DAY CODES FOR PAV AND INV'                   
*                                                                               
*              PARA 1    BYTE 0    L'DAY EXPRESSION                             
*                             1-3  A(DAY EXPRESSION)                            
*                                  IF BYTE 0-3 = F'1' GET A PAV CODE            
*                                  FROM AN INV DAY CODE.                        
*                                  IF BYTE 0-3 = F'2' GET AN INV CODE           
*                                  FROM A PAV DAY CODE.                         
*                                                                               
*              PARA 2    BYTE 1-3  A(1 BYTE RINVKDAY CODE)                      
*                                  ZERO ON RETURN = INVALID DAY                 
*                                  EXPRESSION                                   
*                                                                               
*              PARA 3    BYTE 1-3  A(1 BYTE PAV DAY CODE)                       
*                                                                               
*              PARA 4    BYTE 1-3  A(DAYVAL)                                    
*                                                                               
INVDAY   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,INVDAY                                                         
         CLC   0(4,R1),=F'1'                                                    
         BE    PAVCODE             GET A PAV CODE                               
         CLC   0(4,R1),=F'2'                                                    
         BE    INVCODE             GET AN INV CODE                              
         SPACE 1                                                                
         L     RF,12(R1)           DAYVAL                                       
         BASR  RE,RF               VALIDATE EXPRESSION                          
         LM    R3,R4,4(R1)         PARA 2 AND 3                                 
         CLI   0(R3),0                                                          
         BE    EXXMOD              DAYVAL ERROR                                 
         EJECT                                                                  
         LA    R5,DAYLST                                                        
INVDAY1  CLI   0(R5),0             NO MATCH USE OTHER                           
         BE    INVDAY2                                                          
         CLC   0(1,R5),0(R3)                                                    
         BE    INVDAY2                                                          
         LA    R5,3(R5)                                                         
         B     INVDAY1                                                          
         SPACE 1                                                                
INVDAY2  MVC   0(1,R3),1(R5)       RINVDAY CODE                                 
         MVC   0(1,R4),2(R5)       PRDW CODE                                    
EXXMOD   XMOD1 1                                                                
         SPACE 2                                                                
PAVCODE  LM    R3,R4,4(R1)                                                      
         LA    R5,DAYLST                                                        
PAVCD2   CLI   0(R5),0             NO MATCH USE OTHER                           
         BE    PAVCD3                                                           
         CLC   0(1,R3),1(R5)       MATCH INVENTORY CODE                         
         BE    PAVCD3                                                           
         LA    R5,3(R5)                                                         
         B     PAVCD2                                                           
PAVCD3   MVC   0(1,R4),2(R5)       RETURN A PAV CODE                            
         B     EXXMOD                                                           
         SPACE 2                                                                
INVCODE  LM    R3,R4,4(R1)                                                      
         LA    R5,DAYLST                                                        
INVCD2   CLI   0(R5),0                                                          
         BE    INVCD3                                                           
         CLC   0(1,R4),2(R5)       MATCH PAY CODE                               
         BE    INVCD3                                                           
         LA    R5,3(R5)                                                         
         B     INVCD2                                                           
INVCD3   MVC   0(1,R3),1(R5)       RETURN INV CODE                              
         B     EXXMOD                                                           
         EJECT                                                                  
*              DAYLST ENTRIES     BYTE 1  DAY CODE  FROM DAYVAL                 
*                                 BYTE 2  DAY CODE FOR RINVDAY                  
*                                 BYTE 3  DAY CODE FOR PAV FILE                 
*                                                                               
DAYLST   DC    X'40',C'1',X'10'    MON                                          
         DC    X'20',C'2',X'20'    TUE                                          
         DC    X'10',C'3',X'30'    WED                                          
         DC    X'08',C'4',X'40'    THU                                          
         DC    X'04',C'5',X'50'    FRI                                          
         DC    X'02',C'6',X'60'    SAT                                          
         DC    X'01',C'7',X'70'    SUN                                          
         DC    X'7F',C'8',X'80'    MON-SUN                                      
         DC    X'7F',C'D',X'D0'    TYPICAL                                      
         DC    X'7C',C'0',X'00'    M-F                                          
         DC    X'03',C'E',X'E0'    SA-SU (WEEKEND)                              
         DC    X'00',C'9',X'90'    OTHER                                        
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'010REINVDAY  05/01/02'                                      
         END                                                                    
