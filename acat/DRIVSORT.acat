*          DATA SET DRIVSORT   AT LEVEL 008 AS OF 06/28/12                      
*CATALP DRIVSORT                                                                
         TITLE 'DRIVSORT - SORT HANDLER FOR DRIVER'                             
DRIVSORT CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 DSORTX-DSORTD,**DSRT**                                           
         USING DSORTD,RC                                                        
         L     RA,0(R1)                                                         
         USING GLOBALD,RA                                                       
         LA    R5,SORTSAVE         R5=A(LAST RECORD)                            
         LR    R9,R1                                                            
*                                  AFTER DRIVSORT                               
*              PARA+0              NOT 1=ACTIVE RECORD                          
*                                  1=ALL ADDITIVE FIELDS ZERO                   
*              PARA+8              X'80'=END OF FILE                            
         EJECT                                                                  
*              MAIN LOGIC                                                       
         SPACE 3                                                                
         CLI   SORTSW,2            (EOF LAST TIME)                              
         JNE   GET                                                              
         MVI   8(R9),X'80'         RETURN TO USER                               
         MVI   SORTSW,0                                                         
**NOP    XCEF  SORTSAVE,1000                                                    
         LA    R0,SORTSAVE                                                      
         LHI   R1,4096                                                          
         XR    RE,RE                                                            
         XR    RF,RF                                                            
         MVCL  R0,RE                                                            
         J     XIT                                                              
         SPACE 1                                                                
GET      CLI   SORTSW,0            MAKE SURE DRIVPUT EMPTIES STACK              
         JNE   GETNEXT                                                          
         GOTO1 =V(DRIVPUT),DMCB,(RA),0                                          
         SPACE 1                                                                
GETNEXT  GOTO1 SORTER,DMCB,=C'GET'                                              
         ICM   R6,15,DMCB+4        R6=A(THIS RECORD)                            
         JNZ   GET2                                                             
         GOTO1 SORTER,DMCB,=C'END'                                              
         MVI   SORTSW,2            SORTER GAVE US EOF                           
         J     GET6                                                             
*                                                                               
GET2     CLI   TRACOPT,C'Y'        SELECTIVE - R6=A(RECORD)                     
         JNE   *+8                                                              
         BRAS  RE,TRASOME          POSSIBLE TRACE                               
*                                                                               
         CLI   SORTSW,0            VERY FIRST RECORD                            
         JNE   GET4                                                             
         MVI   SORTSW,1                                                         
         LH    R1,BGRECLEN                                                      
**NOP    MOVE  ((R5),(R1)),(R6)    SAVE THIS ONE                                
         LR    R0,R5                                                            
         LR    R7,R1                                                            
         MVCL  R0,R6                                                            
         J     GETNEXT             AND GO AND GET ANOTHER                       
         SPACE 1                                                                
GET4     LLC   R2,0(R6)            PICK UP RECORD NUMBER OF THIS RECORD         
         BCTR  R2,0                                                             
         SLL   R2,2                                                             
         LA    R2,GLAINTD(R2)                                                   
         L     R2,0(R2)                                                         
         USING GLINTD,R2                                                        
         LH    R1,GLKEYLEN                                                      
         AH    R1,GLCONLEN                                                      
         BCTR  R1,0                                                             
         EXRL  R1,EXCLC                                                         
         JNE   GET6                                                             
         GOTO1 =V(DRIVADD),DMCB,(RA),(R5),(R6)                                  
         MVC   ACTIND,0(R1)        DRIVADD WILL PASS US ACTIVITY                
         J     GETNEXT                                                          
         SPACE 1                                                                
EXCLC    CLC   0(0,R6),0(R5)       THIS V LAST                                  
         SPACE 1                                                                
GET6     LH    R1,BGRECLEN         DIFFERENT KEYS                               
**NOP    L     R4,GLANEXT                                                       
**NOP    MOVE  ((R4),(R1)),(R5)    RELEASE SAVED RECORD                         
         L     R0,GLANEXT                                                       
         LR    RE,R5                                                            
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
         MVC   0(1,R9),ACTIND      RETURN ACTIVITY INDICATOR                    
         MVI   8(R9),0             AND NOT EOF YET                              
         MVI   ACTIND,0                                                         
         CLI   SORTSW,2            AND UNLESS THIS IS EOF                       
         JE    XIT                                                              
         LH    R1,BGRECLEN                                                      
**NOP    MOVE  ((R5),(R1)),(R6)    SAVE THIS RECORD                             
         LR    R0,R5                                                            
         LR    R7,R1                                                            
         MVCL  R0,R6                                                            
         J     XIT                                                              
         EJECT                                                                  
*              HOOK TO APPLICATION OR SYSTEM DRIVER                             
         SPACE 3                                                                
GOHOOK   NTR1                                                                   
         CLI   GLAROUT,0                                                        
         JE    XIT                                                              
         CLI   GLAROUT,2           FIRST BYTE INDICATES WHER                    
*                                  1=APPLICATION 2=SYSDRIVER                    
         JE    GOH2                                                             
         JH    GOH3                                                             
         ICM   RF,15,GLAHOOK       PICK UP APPLICATION HOOK                     
         JZ    XIT                                                              
         L     RE,USERRD           USERS RD                                     
         LM    R0,RC,20(RE)        RESTORE USERS R0-RC                          
         BASR  RE,RF               RF CONTAINED A(HOOK ROUTINE)                 
         J     XIT                                                              
         SPACE 1                                                                
GOH2     ICM   RF,15,GLASYSDR                                                   
         JZ    XIT                                                              
         GOTO1 (RF),DMCB,(RA)                                                   
         J     XIT                                                              
         SPACE 1                                                                
GOH3     GOTO1 =V(DRIVROUT),DMCB,(RA)                                           
         J     XIT                                                              
         EJECT                                                                  
*              TRACE AND PRINTING FACILITIES                                    
         SPACE 2                                                                
TRASOME  NTR1                                                                   
** TRACOPT TEST MOVED BEFORE CALL TO IMPROVE EFFICIENCY!                        
**       CLI   TRACOPT,C'Y'        SELECTIVE - R6=A(RECORD)                     
**       JNE   XIT                                                              
         MVI   SPACES,C' '                                                      
         MVC   SPACES+1(132),SPACES                                             
         LR    R2,R6               R2=A(RECORD)                                 
         LH    R4,BGRECLEN         R4=L'RECORD                                  
         SPACE 1                                                                
TR2      LR    R3,R4               R3=AMOUNT LEFT TO PRINT                      
         CHI   R3,132                                                           
         JNH   *+8                                                              
         LHI   R3,132              INSURE NOT MORE THAN L'PRINT LINE            
         BCTR  R3,0                -1 FOR EX MVC                                
         SPACE 1                                                                
         MVC   P,SPACES                                                         
         MVC   P2,SPACES                                                        
         MVC   P3,SPACES                                                        
         EXRL  R3,TRAM1                                                         
         BRAS  RE,PRINTOUT                                                      
         LA    R3,1(R3)                                                         
         GOTO1 HEXOUT,DMCB,(R2),P2,(R3),=C'SEP'                                 
         BCTR  R3,0                                                             
         EXRL  R3,TRAM2                                                         
         BRAS  RE,PRINTOUT                                                      
         LA    RF,P2+1(R3)                                                      
         EXRL  R3,TRAM3                                                         
         BRAS  RE,PRINTOUT                                                      
         BRAS  RE,PRINTOUT                                                      
         SPACE 1                                                                
         LA    R3,1(R3)                                                         
         AR    R2,R3               BUMP TO NEXT POSITION IN RECORD              
         SR    R4,R3               R4=AMOUNT LEFT TO PRINT                      
         BP    TR2                                                              
         J     XIT                                                              
         SPACE 1                                                                
TRAM1    MVC   P(0),0(R2)                                                       
TRAM2    MVC   P(0),P2                                                          
TRAM3    MVC   P(0),0(RF)                                                       
         SPACE 1                                                                
PRINTOUT NTR1                                                                   
         OC    P,SPACES                                                         
         GOTO1 PRINT,DMCB,PFILL,=C'BL01'                                        
         MVC   P,SPACES                                                         
         J     XIT                                                              
         SPACE 1                                                                
XIT      XIT1                                                                   
         EJECT                                                                  
*              LTORG AND ODDMENTS                                               
         SPACE 3                                                                
         LTORG                                                                  
         DC    D'0'                                                             
ACTIND   DC    X'00'               PRESET TO ACTIVE                             
         DC    D'0'                                                             
SORTSW   DC    X'00'                                                            
         DC    D'0'                                                             
SORTSAVE DS    0D                                                               
         DC    4096X'00'                                                        
         EJECT                                                                  
*              DSECT FOR SORT MODULE                                            
         SPACE 3                                                                
DSORTD   DSECT                                                                  
         SPACE 1                                                                
PFILL    DS    CL1                                                              
P        DS    CL133                                                            
P2       DS    CL132                                                            
P3       DS    CL132                                                            
SPACES   DS    CL133                                                            
DSORTX   EQU   *                                                                
         SPACE 1                                                                
         PRINT OFF                                                              
       ++INCLUDE DRGLOBAL                                                       
       ++INCLUDE DRLOCAL                                                        
       ++INCLUDE DRINTRECD2                                                     
       ++INCLUDE DRIVETABLE                                                     
         PRINT ON                                                               
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'008DRIVSORT  06/28/12'                                      
         END                                                                    
