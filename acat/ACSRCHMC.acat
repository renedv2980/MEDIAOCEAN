*          DATA SET ACSRCHMC   AT LEVEL 069 AS OF 04/06/10                      
*CATALP ACSRCHMC                                                                
ACSRCHMC TITLE '- ACCOUNT SEARCH SUBROUTINES FOR MEDIA CODE RECORDS'            
* DPEA 066 USE NEW SBCALLER INDICATOR FOR STAND-ALONE TEST                      
* FPEA 067 REPLACE UTLD BY THAT DESCRIBED IN DCGETFACT                          
* FPEA 068 REPLACE *CATALP CARD                                                 
         PRINT NOGEN                                                            
ACSRCHMC CSECT                                                                  
         NMOD1 WORKL,ACSRCHMC,RA                                                
         USING WORKD,RC            RC=A(LOCAL W/S)                              
         ST    R1,APARMS                                                        
         L     R8,0(R1)                                                         
         USING SEARCHD,R8          R8=A(SEARCH CONTROL BLOCK)                   
         USING GETTXTD,SBTXTBLK                                                 
         LH    R9,=Y(SVBLK-TWAD)                                                
         A     R9,SBATWA                                                        
         USING SVBLKD,R9           R9=A(SAVE AREA IN TWA)                       
*                                                                               
         MVI   SBRETURN,0          CLEAR ERROR FLAG                             
         CLI   0(R1),SDSSYPPQ                                                   
         BNE   *+6                                                              
         DC    H'0'                                                             
         OC    SBASYS,SBASYS                                                    
         BNZ   *+12                                                             
         MVI   SBRETURN,SBENITQ    SEARCH BLOCK NOT INITIALIZED                 
         B     EXITN                                                            
*                                                                               
         L     RF,SBCOMFAC                                                      
         USING COMFACSD,RF                                                      
         MVC   CUREDIT,CCUREDIT                                                 
         MVC   VDATAMGR,CDATAMGR                                                
         MVC   VGETFACT,CGETFACT                                                
         MVC   VCALLOV,CCALLOV                                                  
         MVC   VDICTATE,CDICTATE                                                
         MVC   VSCANNER,CSCANNER                                                
         MVC   VSWITCH,CSWITCH                                                  
         DROP  RF                                                               
*                                                                               
         CLI   0(R1),ROUTSN                                                     
         BNL   EXITY               UNKNOWN ROUTINE                              
         XR    RF,RF                                                            
         IC    RF,0(R1)                                                         
         SLL   RF,2                                                             
         B     ROUTS(RF)                                                        
*                                                                               
ROUTS    DS    0XL4                                                             
         B     EXITY               SYSTEM KEY SET                               
         B     SETKEY              SET KEY                                      
         B     SEC                 SECURITY CHECK                               
         B     FLT                 FILTER                                       
         B     DIS                 DISPLAY                                      
ROUTSN   EQU   (*-ROUTS)/L'ROUTS                                                
         EJECT                                                                  
***********************************************************************         
* SECURITY ROUTINES                                                   *         
***********************************************************************         
         SPACE 1                                                                
SEC      CLI   SBMODE,1            DIRECTORY LEVEL SECURITY                     
         BE    EXITY                                                            
         CLI   SBMODE,2            RECORD LEVEL SECURITY                        
         BE    EXITY                                                            
         CLI   SBMODE,0            INITIALIZE                                   
         BE    INITSEC                                                          
         B     EXITY               UNKNOWN MODE                                 
         SPACE 1                                                                
***********************************************************************         
* FILTER ROUTINES                                                     *         
***********************************************************************         
         SPACE 1                                                                
FLT      CLI   SBMODE,1            DIRECTORY LEVEL FILTERS                      
         BE    DIRFLT                                                           
         CLI   SBMODE,2            RECORD LEVEL FILTERS                         
         BE    RECFLT                                                           
         CLI   SBMODE,0            INITIALIZE                                   
         BE    INITFLT                                                          
         B     EXITY               UNKNOWN MODE                                 
         SPACE 1                                                                
***********************************************************************         
* DISPLAY ROUTINES                                                    *         
***********************************************************************         
         SPACE 1                                                                
DIS      CLI   SBMODE,1            DISPLAY RECORD                               
         BE    RECDIS                                                           
         CLI   SBMODE,0            DISPLAY HEADERS                              
         BE    INITDIS                                                          
         B     EXITY               UNKNOWN MODE                                 
         SPACE 1                                                                
***********************************************************************         
* EXITS                                                               *         
***********************************************************************         
         SPACE 1                                                                
EXITH    CLI   *,0                 CC=HIGH                                      
         B     EXIT                                                             
*                                                                               
EXITN    DS    0H                                                               
EXITL    CLI   *,FF                CC=LOW                                       
         B     EXIT                                                             
*                                                                               
EXITY    CR    RB,RB               CC=EQUAL                                     
EXIT     XIT1  ,                                                                
         EJECT                                                                  
***********************************************************************         
* SET UP SEARCH RECORD                                                *         
***********************************************************************         
         SPACE 1                                                                
SETKEY   L     R2,4(R1)                                                         
         USING SMCRECD,R2                                                       
         XC    SMCKEY,SMCKEY                                                    
         MVI   SMCKTYP,SMCKTYPQ                                                 
         MVC   SMCKCPY,SBAGYB                                                   
         MVI   SMCKSRC,SMCKSRCQ                                                 
SETKEYY  B     EXITY                                                            
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* INITIALIZE SECURITY                                                 *         
***********************************************************************         
         SPACE 1                                                                
INITSEC  LA    RE,SVBLKD           CLEAR SAVE AREA                              
         LA    RF,SVBLKL                                                        
         XR    R1,R1                                                            
         MVCL  RE,R0                                                            
*                                                                               
         MVC   DMCB,EFFS                                                        
         GOTO1 VGETFACT,DMCB,(X'80',0),F#UTLD  REPLACE UTLD BY FA               
         L     RF,0(R1)                                                         
         USING F@UTLD,RF           R4=A(UTL)                                    
         MVC   SVSECY+1(1),F@TAUTH+1                                            
         MVC   SVSYS,F@TSYS                                                     
         DROP  RF                                                               
*                                                                               
         L     RF,SBATWA           TEST STAND ALONE CALL                        
         USING TWAD,RF                                                          
         TM    SRVIDH+FHATD,FHATPR                                              
         BO    *+8                                                              
         OI    SVINDS,SVIALONE                                                  
*&&DO                                                                           
         TM    SBCALLER,SBCALSOQ   TEST STAND ALONE CALL                        
         BZ    *+8                                                              
         OI    SVINDS,SVIALONE                                                  
*&&                                                                             
INITSECX B     EXITY                                                            
         EJECT                                                                  
***********************************************************************         
* INITIALIZE FILTERS                                                  *         
***********************************************************************         
         SPACE 1                                                                
INITFLT  L     R2,4(R1)                                                         
         USING FHD,R2              R2=A(INPUT FIELD)                            
*                                                                               
         OI    SBSTYLO,SBSTDFLQ    FORCE DIRECTORY FILTERING                    
*                                                                               
IFLT04   CLI   FHIL,0              TEST FOR NO INPUT                            
         BNE   *+12                                                             
         MVI   SBRETURN,SBEMIFQ                                                 
         B     IFLTER3                                                          
*                                                                               
         XC    FTVALS,FTVALS                                                    
         MVI   SBLFILT,FTVALSL                                                  
         MVI   SBERRFLD,2          INDICATE KEY FIELD                           
         MVI   SBNNAMES,0          INDICATE NO NAMES FOUND YET                  
         GOTO1 DICTATE,DMCB,C'LU  ',DI,DO                                       
*                                                                               
         LA    R5,IOREC                                                         
         USING SCANBLKD,R5         R5=A(SCAN BLOCK)                             
         GOTO1 VSCANNER,DMCB,(L'SCFLD2,FHD),('SCANBLKN',SCANBLKD),     *        
               C',=  '                                                          
         MVC   SCANN,4(R1)                                                      
         CLI   SCANN,SCANBLKN                                                   
         BNH   *+12                                                             
         MVI   SBRETURN,SBETMNQ                                                 
         B     IFLTER2                                                          
*                                                                               
         MVI   INDX,1              SET FIELD INDEX NO.                          
         XC    BTNOFILT,BTNOFILT                                                
*                                                                               
IFLT12   CLI   SC1STLEN,0          TEST NO 1ST FIELD                            
         BE    IFLT28                                                           
         CLI   SC2NDLEN,0          TEST NO 2ND FIELD                            
         BNE   IFLT14                                                           
*                                                                               
         CLI   SCFLD1,C'?'         TEST HELP WANTED                             
         BE    HELPFLT                                                          
         GOTO1 CMPWORD,DMCB,(SC1STLEN,SCFLD1),(L'AC@HELP,AC@HELP)               
         BE    HELPFLT                                                          
*                                                                               
         CLI   SC1STLEN,SBNALENQ   PARSE NAME                                   
         BNH   *+12                                                             
         MVI   SBRETURN,SBEFTLQ    NAME TOO LONG                                
         B     IFLTER2                                                          
         GOTO1 SBPARSER,DMCB,('SBPARSKQ',SEARCHD),(SC1STLEN,SCFLD1)             
         BNE   IFLTER2                                                          
         B     IFLT28                                                           
*                                                                               
IFLT14   CLI   SC1STLEN,FLTLMINQ   TEST FIELD LONG ENOUGH                       
         BNL   *+12                                                             
         MVI   SBRETURN,SBEFTSQ                                                 
         B     IFLTER1                                                          
*                                                                               
         XR    R7,R7                                                            
         USING FLTTABD,R7          R7=A(FILTER TABLE ENTRY)                     
IFLT16   BAS   RE,NXTFLT                                                        
         BE    *+12                                                             
         MVI   SBRETURN,SBEIKWQ    E-O-T SO INVALID KEYWORD                     
         B     IFLTER1                                                          
*                                                                               
         XR    RF,RF               COMPARE LHS                                  
         ICM   RF,3,FLTLNAM                                                     
         GOTO1 CMPWORD,DMCB,(SC1STLEN,SCFLD1),(FLTLMAX,DO(RF))                  
         BNE   IFLT16                                                           
*                                                                               
         TM    FLTINDS1,FLTIDUP    TEST DUPLICATES ALLOWED                      
         BO    IFLT18                                                           
         GOTO1 TESTBIT,DMCB,(FLTCODE,FTUSED)                                    
         BZ    IFLT18                                                           
         MVI   SBRETURN,SBEDKWQ                                                 
         B     IFLTER1                                                          
IFLT18   GOTO1 SETBIT,DMCB,(FLTCODE,FTUSED)                                     
*                                                                               
         TM    FLTINDS1,FLTINOT    TEST NOTS ALLOWED                            
         BZ    IFLT20                                                           
         CLI   SCFLD2,C'*'                                                      
         BNE   IFLT20                                                           
         GOTO1 SETBIT,DMCB,(FLTCODE,FTNOT)                                      
         MVC   SCFLD2(L'SCFLD2-1),SCFLD2+1                                      
         MVI   SCFLD2+L'SCFLD2-1,C' '                                           
         IC    RF,SC2NDLEN                                                      
         BCTR  RF,0                                                             
         STC   RF,SC2NDLEN                                                      
*                                                                               
IFLT20   CLC   SC2NDLEN,FLTRMIN    TEST FIELD TOO SHORT                         
         BNL   *+12                                                             
         MVI   SBRETURN,SBEFTSQ                                                 
         B     IFLTER1                                                          
         CLC   SC2NDLEN,FLTRMAX    TEST FIELD TOO LONG                          
         BNH   *+12                                                             
         MVI   SBRETURN,SBEFTLQ                                                 
         B     IFLTER1                                                          
*                                                                               
         XR    RF,RF               TEST FOR VALIDATION ROUTINE                  
         ICM   RF,3,FLTVAL                                                      
         BZ    IFLT28                                                           
         B     ACSRCHMC(RF)        GO TO VALIDATION ROUTINE                     
*                                                                               
VFX      BE    VFYES               RETURN POINTS FOR ROUTINES                   
VFNO     B     IFLTER1                                                          
VFYES    DS    0H                                                               
*                                                                               
IFLT28   CLC   INDX,SCANN          TEST ALL FIELDS DONE                         
         BNL   IFLT30                                                           
         IC    RE,INDX             INCREMENT INDEX NO.                          
         LA    RE,1(RE)                                                         
         STC   RE,INDX                                                          
         LA    R5,SCANBLKL(R5)                                                  
         B     IFLT12                                                           
*                                                                               
IFLT30   XR    R7,R7               SET ANY DEFAULT VALUES                       
IFLT32   BAS   RE,NXTFLT                                                        
         BNE   IFLT40                                                           
         GOTO1 TESTBIT,DMCB,(FLTCODE,FTUSED)                                    
         BO    IFLT32              FILTER HAS BEEN USED                         
         XR    RF,RF                                                            
         ICM   RF,3,FLTDEF                                                      
         BZ    IFLT32                                                           
         B     ACSRCHMC(RF)        GO TO SET DEFAULT ROUTINE                    
DFYES    GOTO1 SETBIT,DMCB,(FLTCODE,FTUSED)                                     
DFNO     B     IFLT32                                                           
*                                                                               
IFLT40   XC    FTUSED,BTNOFILT     TURN OFF NO FILTERS                          
*                                                                               
IFLTYES B      EXITY                                                            
*                                                                               
IFLTER1  DS    0H                  INSERT LHS INTO ERROR MESSAGE                
         LA    RF,SBTXTBLK                                                      
         USING GETTXTD,RF          RF=A(GETTXT BLOCK)                           
         CLI   GTLTXT,0                                                         
         BNE   *+16                                                             
         MVC   GTLTXT,SC1STLEN                                                  
         MVC   SBOPTTXT(L'SCFLD1),SCFLD1                                        
         LA    RE,SBOPTTXT                                                      
         STCM  RE,7,GTATXT                                                      
*                                                                               
IFLTER2  DS    0H                  DISPLAY INDEX # AFTER ERROR MESSAGE          
         MVC   SBTXTBLK+(GTINDX-GETTXTD)(L'INDX),INDX                           
*                                                                               
IFLTER3  CLI   SBRETURN,0                                                       
         BNE   EXITN                                                            
         MVI   SBRETURN,SBEIFVQ                                                 
         B     EXITN                                                            
         DROP  RF                                                               
         EJECT                                                                  
***********************************************************************         
* DISPLAY HELP FOR FILTERS                                            *         
***********************************************************************         
         SPACE 1                                                                
HELPFLT  MVC   WORK,SPACES                                                      
         LA    R4,WORK                                                          
         XR    R7,R7                                                            
*                                                                               
HFLT02   BAS   RE,NXTFLT           GET NEXT TABLE ENTRY                         
         BNE   HFLT10                                                           
         XR    RE,RE                                                            
         ICM   RE,3,FLTLNAM        COPY KEYWORD                                 
         LA    RE,DO(RE)                                                        
         XR    RF,RF                                                            
         IC    RF,FLTLMAX                                                       
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),0(RE)                                                    
         AR    R4,RF                                                            
         CLI   0(R4),C' '          FIND END OF KEYWORD                          
         BH    *+8                                                              
         BCT   R4,*-8                                                           
         MVI   1(R4),C','          APPEND COMMA                                 
         LA    R4,2(R4)                                                         
         B     HFLT02                                                           
*                                                                               
HFLT10   BCTR  R4,0                REMOVE LAST COMMA                            
         MVI   0(R4),C' '                                                       
         IC    RF,FHLN             COPY LIST TO OUTPUT                          
         SH    RF,=Y(FHDAD+FHDAD+1)                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   FHDA(0),WORK                                                     
*                                                                               
HELPFLTX MVI   SBRETURN,SBEHLPQ                                                 
         B     EXITN                                                            
         EJECT                                                                  
***********************************************************************         
* VALIDATE FILTER ROUTINES                                            *         
***********************************************************************         
         SPACE 1                                                                
VFRNAM   XR    RF,RF               ** = KEYWORD **                              
         ICM   RF,3,FLTRNAM                                                     
         GOTO1 CMPWORD,DMCB,(SC2NDLEN,SCFLD2),(FLTRMAX,DO(RF))                  
         B     VFX                                                              
         SPACE 1                                                                
*                                  ** =YES/ONLY/NO **                           
VFYON    GOTO1 CMPWORD,DMCB,(SC2NDLEN,SCFLD2),(L'AC@YES,AC@YES)                 
         BNE   VFYON02                                                          
         GOTO1 SETBIT,(R1),(FLTCODE,BTNOFILT)                                   
         B     VFYES                                                            
VFYON02  GOTO1 (RF),(R1),,(L'AC@ONLY,AC@ONLY)                                   
         BE    VFYES                                                            
         GOTO1 (RF),(R1),,(L'AC@NO,AC@NO)                                       
         BNE   VFNO                                                             
         GOTO1 SETBIT,(R1),(FLTCODE,FTNOT)                                      
         B     VFYES                                                            
         SPACE 1                                                                
*                                  ** NAME **                                   
VFNAME   GOTO1 SBPARSER,DMCB,('SBPARSKQ',SEARCHD),(SC2NDLEN,SCFLD2)             
         B     VFX                                                              
         SPACE 1                                                                
VFGROUP  MVC   FTGROUP,SCFLD2      ** GROUP = X **                              
         B     VFYES                                                            
         EJECT                                                                  
***********************************************************************         
* SET DEFAULT FILTER VALUES                                           *         
***********************************************************************         
         SPACE 1                                                                
         DROP  R2,R7,R5                                                         
         EJECT                                                                  
***********************************************************************         
* INITIALIZE DISPLAY - SET UP HEADLINES                               *         
***********************************************************************         
         SPACE 1                                                                
INITDIS  L     RF,SBATWA           TEST FOR A REPORT                            
         ICM   R1,15,REPACOM-TWAD(RF)                                           
         BZ    *+8                                                              
         OI    SVINDS,SVIREP                                                    
*                                                                               
         XC    SBEXHEAD,SBEXHEAD                                                
         MVI   SBEXHNHD,2          SET NO. OF HEADLINES                         
*                                                                               
         LA    R6,SBEXHTIT         R6=A(TITLE)                                  
         MVCDD 0(10,R6),AC#MEDC                                                 
         GOTO1 DICTATE,DMCB,C'SL  ',(R6)                                        
         LR    R0,R6                                                            
         LA    R6,9(R6)                                                         
         CLI   0(R6),C' '                                                       
         BH    *+8                                                              
         BCT   R6,*-8                                                           
         LA    R6,1(R6)                                                         
*                                                                               
         LR    RE,R6                                                            
         SR    RE,R0                                                            
         STC   RE,SBEXHTIL                                                      
*                                                                               
         XR    R5,R5               R5=ITEM DISPLACEMENT                         
         XR    R7,R7                                                            
         USING CLMTABD,R7          R7=A(COLUMN TABLE ENTRY)                     
*                                                                               
IDIS02   BAS   RE,NXTCLM                                                        
         BNE   IDIS10                                                           
         SH    RF,=Y(CLMWDTH-CLMTABD+1)                                         
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   2(0,R6),CLMWDTH                                                  
         LA    RF,3(RF)                                                         
         STC   RF,0(R6)            SET ENTRY LENGTH                             
         STC   R4,1(R6)            SET ITEM DISPLACEMENT                        
*                                                                               
         TM    CLMINDS1,CLMIKEY+CLMINAME                                        
         BZ    IDIS08                                                           
         LA    RE,SBEXHEAD                                                      
         SR    RE,R6                                                            
         LPR   RE,RE                                                            
         TM    CLMINDS1,CLMIKEY                                                 
         BZ    *+12                                                             
         STC   RE,SBEXHDKY         SET DISPLACEMENT TO KEY                      
         B     *+8                                                              
         STC   RE,SBEXHDNA         SET DISPLACEMENT TO NAME                     
*                                                                               
IDIS08   BXH   R6,RF,IDIS02                                                     
         DROP  R7                                                               
*                                                                               
IDIS10   MVI   0(R6),EOT                                                        
*                                                                               
INITDISX B     EXITY                                                            
         EJECT                                                                  
***********************************************************************         
* FILTER ON SEARCH DIRECTORY RECORD                                   *         
***********************************************************************         
         SPACE 1                                                                
DIRFLT   L     R2,4(R1)                                                         
         USING SRCRECD,R2                                                       
*                                                                               
DIRFLTY  B     EXITY                                                            
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* FILTER ON ACCMST RECORD                                             *         
***********************************************************************         
         SPACE 1                                                                
RECFLT   L     R2,4(R1)                                                         
         USING PMDRECD,R2          R2=A(MEDIA CODE RECORD)                      
         LA    R3,PMDRFST                                                       
         USING PMDELD,R3           R3=A(MEDIA CODE ELEMENT)                     
         XR    RF,RF                                                            
         CLI   PMDEL,PMDELQ                                                     
         BE    *+12                                                             
         IC    RF,PMDLN                                                         
         BXH   R3,RF,*-12                                                       
*                                                                               
         XR    R7,R7                                                            
         USING FLTTABD,R7          R7=FILTER TABLE ENTRY                        
RFLT02   BAS   RE,NXTFLT                                                        
         BNE   RECFLTY             E-O-T                                        
*                                                                               
         GOTO1 TESTBIT,DMCB,(FLTCODE,FTUSED)                                    
         BZ    RFLT02                                                           
         XR    RF,RF                                                            
         ICM   RF,3,FLTREC                                                      
         BZ    RFLT02                                                           
         B     ACSRCHMC(RF)        GO TO FILTER RECORD ROUTINE                  
*                                                                               
RFX      BNE   RFNO                RETURN POINTS FOR ROUTINES                   
RFYES    GOTO1 TESTBIT,DMCB,(FLTCODE,FTNOT)                                     
         BZ    RFLT02              MATCH FOR '=' FILTER                         
         B     RECFLTN                                                          
RFNO     GOTO1 TESTBIT,DMCB,(FLTCODE,FTNOT)                                     
         BO    RFLT02              NO MATCH FOR 'NOT=' FILTER                   
*                                                                               
RECFLTN  MVI   SBRETURN,SBRFLTQ                                                 
         B     EXITN                                                            
RECFLTY  B     EXITY                                                            
         DROP  R7                                                               
         EJECT                                                                  
***********************************************************************         
* FILTER ROUTINES                                                     *         
***********************************************************************         
         SPACE 1                                                                
RFGROUP  CLC   FTGROUP,PMDGRP      ** GROUP = X **                              
         B     RFX                                                              
         SPACE 1                                                                
         DROP  R2,R3                                                            
         EJECT                                                                  
***********************************************************************         
* DISPLAY RECORD                                                      *         
***********************************************************************         
         SPACE 1                                                                
RECDIS   L     R2,8(R1)                                                         
         USING PMDRECD,R2          R2=A(MEDIA CODE RECORD)                      
         LA    R3,PMDRFST                                                       
         USING PMDELD,R3           R3=A(MEDIA CODE ELEMENT)                     
         XR    RF,RF                                                            
         CLI   PMDEL,PMDELQ                                                     
         BE    *+12                                                             
         IC    RF,PMDLN                                                         
         BXH   R3,RF,*-12                                                       
*                                                                               
         L     R6,4(R1)            R6=A(OUTPUT AREA)                            
         MVI   0(R6),C' '                                                       
         MVC   1(255,R6),0(R6)                                                  
*                                                                               
         XR    R5,R5               R5=ITEM DISPLACEMENT                         
         XR    R7,R7                                                            
         USING CLMTABD,R7          R7=COLUMN TABLE ENTRY                        
*                                                                               
RDIS02   BAS   RE,NXTCLM                                                        
         BNE   RDIS10                                                           
         AR    R4,R6               R4=A(OUTPUT AREA)                            
         XR    RF,RF                                                            
         ICM   RF,3,CLMRTN                                                      
         B     ACSRCHMC(RF)                                                     
DISX     B     RDIS02                                                           
*                                                                               
RDIS10   B     EXITY                                                            
         EJECT                                                                  
***********************************************************************         
* DISPLAY MEDIA CODE                                                  *         
***********************************************************************         
         SPACE 1                                                                
DISCODE  MVC   2(L'PMDKMED,R4),PMDKMED                                          
         B     DISX                                                             
         SPACE 1                                                                
***********************************************************************         
* DISPLAY MEDIA CODE DESCRIPTION                                      *         
***********************************************************************         
         SPACE 1                                                                
DISDESC  MVC   0(L'PMDDESC,R4),PMDDESC                                          
         B     DISX                                                             
         SPACE 1                                                                
***********************************************************************         
* MEDIA GROUP                                                         *         
***********************************************************************         
         SPACE 1                                                                
DISGROUP MVC   2(L'PMDGRP,R4),PMDGRP                                            
         B     DISX                                                             
         SPACE 2                                                                
         DROP  R2,R3,R7                                                         
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO GET A(NEXT COLUMN TABLE ENTRY)                           *         
*                                                                     *         
* NTRY: R7=A(COLUMN CODE ENTRY)                                       *         
*       R5=CURRENT DISPLACEMENT IN LIST LINE                          *         
* EXIT: IF END OF LIST, CC=NOT EQUAL                                  *         
*       R7=A(COLUMN TABLE ENTRY), RF=L(COLUMN TABLE ENTRY)            *         
*       R4=OLD LINE DISPLACEMENT                                      *         
*       R5=NEW LINE DISPLACEMENT                                      *         
***********************************************************************         
         SPACE 1                                                                
         USING CLMTABD,R7                                                       
NXTCLM   LTR   R7,R7                                                            
         BNZ   NCLM02                                                           
         LA    R7,CLMTAB                                                        
         B     NCLM04                                                           
*                                                                               
NCLM02   XR    RF,RF                                                            
         ICM   RF,1,CLMLEN                                                      
         AR    R7,RF                                                            
*                                                                               
NCLM04   XR    RF,RF                                                            
         ICM   RF,1,CLMLEN                                                      
         BZ    NXTCLMN                                                          
*                                                                               
         LR    R4,R5               R4=OLD DISPLACEMENT                          
         IC    R5,CLMWDTH                                                       
         AR    R5,R4               R5=NEW DISPLACEMENT                          
*                                                                               
NXTCLMY  CR    RB,RB                                                            
         BR    RE                                                               
*                                                                               
NXTCLMN  LTR   RB,RB                                                            
         BR    RE                                                               
         SPACE 1                                                                
         DROP  R7                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO GO TO NEXT ENTRY OF THE FILTER TABLE                     *         
*                                                                     *         
* NTRY: R7=A(CURRENT ENTRY) OR 0 TO START                             *         
* EXIT: R7=A(NEXT ENTRY)                                              *         
*       CC=LOW IF END-OF TABLE REACHED                                *         
***********************************************************************         
         SPACE 1                                                                
         USING FLTTABD,R7                                                       
NXTFLT   LR    R0,RE                                                            
         LTR   R7,R7                                                            
         BNZ   NFLT08                                                           
         LA    R7,FLTTAB                                                        
*                                                                               
NFLT02   CLI   FLTTABD,EOT         TEST E-O-T                                   
         BE    NXTFLTN                                                          
*                                                                               
         GOTO1 TESTCTRY,FLTCTRY    MATCH ON COUNTRY                             
         BE    NXTFLTY                                                          
*                                                                               
NFLT08   LA    R7,FLTTABL(R7)                                                   
         B     NFLT02                                                           
*                                                                               
NXTFLTN  LTR   RB,RB                                                            
         B     *+6                                                              
NXTFLTY  CR    RB,RB                                                            
         LR    RE,R0                                                            
         BR    RE                                                               
         DROP  R7                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO PARTITION INPUT                                          *         
*                                                                     *         
* NTRY: P1=(L'INPUT,A(INPUT))                                         *         
*                                                                     *         
***********************************************************************         
         SPACE 1                                                                
PARTINP  NTR1  ,                                                                
         MVI   PARTALN,0                                                        
         XC    PARTA,PARTA                                                      
         MVI   PARTBLN,0                                                        
         MVI   PARTB,FF                                                         
         MVC   PARTB+1(L'PARTB-1),PARTB                                         
         XR    R2,R2                                                            
         ICM   R2,7,1(R1)          R2=A(INPUT)                                  
*                                                                               
         XR    RE,RE                                                            
         IC    RE,0(R1)            RE=L(INPUT)                                  
         LR    RF,RE                                                            
         LR    R1,R2               R1=A(INPUT)                                  
         CLI   0(R1),C'-'                                                       
         BE    *+12                                                             
         LA    R1,1(R1)                                                         
         BCT   RE,*-12                                                          
*                                                                               
         SR    RF,RE               COPY PART A                                  
         BZ    PINP02                                                           
         STC   RF,PARTALN                                                       
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   PARTA(0),0(R2)                                                   
         LTR   RE,RE               TEST PARTITION CHARACTER FOUND               
         BNZ   PINP02                                                           
         MVC   PARTBLN,PARTALN     NO SO PARTB=PARTA                            
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   PARTB(0),0(R2)                                                   
         B     PARTINPX                                                         
*                                                                               
PINP02   BCT   RE,*+8                                                           
         B     PARTINPX                                                         
         STC   RE,PARTBLN                                                       
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   PARTB(0),1(R1)                                                   
*                                                                               
PARTINPX B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO COMPARE INPUT WITH WORD                                  *         
*                                                                     *         
* NTRY: P1=(L'INPUT, A(INPUT))                                        *         
*       P2=(L'WORD, A(WORD))                                          *         
***********************************************************************         
         SPACE 1                                                                
CMPWORD  STM   RE,R3,12(RD)                                                     
         LM    R2,R3,0(R1)                                                      
         IC    RF,0(R1)                                                         
         CLC   0(1,R1),4(R1)                                                    
         BNH   *+8                                                              
         IC    RF,4(R1)                                                         
         BCTR  RF,0                                                             
         EX    RF,CMPWORDC                                                      
CMPWORDX LM    RE,R3,12(RD)                                                     
         BR    RE                                                               
*                                                                               
CMPWORDC CLC   0(0,R2),0(R3)                                                    
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO SET A BIT OF A BIT TABLE                                 *         
*                                                                     *         
* NTRY: P1=(BIT CODE, A(BIT TABLE))                                   *         
***********************************************************************         
         SPACE 1                                                                
SETBIT   XR    RF,RF                                                            
         IC    RF,0(R1)                                                         
         BCTR  RF,0                                                             
         L     R0,0(R1)                                                         
         LA    R1,X'07'                                                         
         NR    R1,RF                                                            
         LA    R1,MASKS(R1)        R1=A(MASK)                                   
         SRL   RF,3                                                             
         AR    RF,R0               RF=A(BYTE OF BIT)                            
         OC    0(1,RF),0(R1)                                                    
         BR    RE                                                               
         SPACE 3                                                                
***********************************************************************         
* ROUTINE TO TEST A BIT OF A BIT TABLE                                *         
*                                                                     *         
* NTRY: P1=(BIT CODE, A(BIT TABLE))                                   *         
***********************************************************************         
         SPACE 1                                                                
TESTBIT  XR    RF,RF                                                            
         IC    RF,0(R1)                                                         
         BCTR  RF,0                                                             
         L     R0,0(R1)                                                         
         LA    R1,X'07'                                                         
         NR    R1,RF                                                            
         IC    R1,MASKS(R1)        R1=MASK                                      
         SRL   RF,3                                                             
         AR    RF,R0               RF=A(BYTE OF BIT)                            
         EX    R1,TESTBITM         EX A TM                                      
         BR    RE                                                               
         SPACE 1                                                                
TESTBITM TM    0(RF),0                                                          
         SPACE 3                                                                
MASKS    DC    X'8040201008040201'                                              
         EJECT                                                                  
***********************************************************************         
* TEST COUNTRY FILTER                                                 *         
*                                                                     *         
* NTRY: R1=A(COUNTRY)                                                 *         
***********************************************************************         
         SPACE 1                                                                
TESTCTRY CLI   0(R1),ALL           TEST ALL COUNTRIES                           
         BE    TCTRYYES                                                         
         CLC   SBCTRY,0(R1)        MATCH ON COUNTRY                             
         BE    TCTRYYES                                                         
         TM    0(R1),NOT                                                        
         BZ    TCTRYNO                                                          
         MVC   BYTE,0(R1)          MATCH ON NOT A COUNTRY                       
         XI    BYTE,NOT                                                         
         CLC   BYTE,SBCTRY                                                      
         BE    TCTRYNO                                                          
*                                                                               
TCTRYYES CR    RE,RE                                                            
         BR    RE                                                               
TCTRYNO  LTR   RE,RE                                                            
         BR    RE                                                               
         SPACE 1                                                                
***********************************************************************         
* CALL DICTATE                                                        *         
***********************************************************************         
         SPACE 1                                                                
DICTATE  NTR1  ,                                                                
         MVI   2(R1),ACCSYSQ       SET ACCOUNT SYSTEM                           
         GOTO1 VDICTATE                                                         
         XR    RE,RE                                                            
         ICM   RE,7,EFFS                                                        
         GOTO1 VSWITCH,(R1),(RE)   TEST DICTATE SWITCHED                        
         L     RE,0(R1)                                                         
         CLC   SVSYS,TCBSYS-TCBD(RE)                                            
         BE    EXIT                SWITCH BACK IF IT DID                        
         GOTO1 (RF),(R1),(SVSYS,0),0                                            
         BE    EXIT                                                             
         DC    H'0'                SWITCH FAILED                                
         EJECT                                                                  
***********************************************************************         
* GET ELEMENT ROUTINE                                                 *         
*                                                                     *         
* NTRY: P1=(ELEMENT CODE, A(RECORD))                                  *         
* EXIT: R1=A(ELEMENT), RF=L(ELEMENT)                                  *         
***********************************************************************         
         SPACE 1                                                                
GETEL    ST    RE,12(RD)                                                        
         LR    RE,R1                                                            
*                                                                               
         L     R1,0(RE)                                                         
         LA    R1,ACCRFST-ACCRECD(R1)                                           
         LA    R0,100                                                           
         XR    RF,RF                                                            
*                                                                               
GETEL02  CLI   0(R1),0                                                          
         BE    GETELNO                                                          
         IC    RF,1(R1)                                                         
         CLC   0(1,R1),0(RE)                                                    
         BE    GETELYES                                                         
         AR    R1,RF                                                            
         BCT   R0,GETEL02                                                       
*                                                                               
GETELNO  LTR   RB,RB                                                            
         B     *+6                                                              
GETELYES CR    RB,RB                                                            
         L     RE,12(RD)                                                        
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* CONSTANTS AND LITERALS                                              *         
***********************************************************************         
         SPACE 1                                                                
FF       EQU   X'FF'                                                            
EOT      EQU   X'00'               END-OF-TABLE                                 
ALL      EQU   X'00'               ALL                                          
NOT      EQU   X'80'               NOT                                          
ACCSYSQ  EQU   6                   ACCOUNT SYSTEM                               
         SPACE 1                                                                
         LTORG                                                                  
         SPACE 1                                                                
SPACES   DC    CL80' '                                                          
EFFS     DC    12X'FF'                                                          
PRODUL   DC    C'SJ'                                                            
         SPACE 1                                                                
DMREAD   DC    C'DMREAD  '                                                      
DMRSEQ   DC    C'DMRSEQ  '                                                      
DMRDHI   DC    C'DMRDHI  '                                                      
ACCMST   DC    C'ACCMST  '                                                      
ACCDIR   DC    C'ACCDIR  '                                                      
TITLEDIV DC    C' / '                                                           
         SPACE 1                                                                
DI       DS    0X                  ** DICTIONARY IN **                          
         DCDDL AC#NAME,8                                                        
         DCDDL AC#DESC,12                                                       
         DCDDL AC#YES,4                                                         
         DCDDL AC#NO,4                                                          
         DCDDL AC#ONLY,8                                                        
         DCDDL AC#HELP,4                                                        
         DCDDL AC#ALL,4                                                         
         DCDDL AC#GROUP,8                                                       
DIX      DC    AL1(0)                                                           
         EJECT                                                                  
***********************************************************************         
* COLUMN TABLE                                                        *         
***********************************************************************         
         SPACE 1                                                                
CLMTABD  DSECT                                                                  
CLMLEN   DS    XL1                 LENGTH OF TABLE ENTRY                        
CLMINDS1 DS    XL1                 * INDICATORS *                               
CLMIKEY  EQU   X'80'               COLUMN IS KEY                                
CLMINAME EQU   X'40'               COLUMN IS NAME                               
CLMRTN   DS    XL2                 DISPLACEMENT TO DISPLAY ROUTINE              
CLMWDTH  DS    XL1                 COLUMN WIDTH                                 
CLMNHD   DS    XL1                 NO. OF HEADINGS                              
CLMHEADS DS    0X                  LIST OF COLUMN HEADINGS                      
CLMHWDTH DS    XL1                 WIDTH OF HEADING                             
CLMHEAD  DS    0X                  HEADING                                      
         SPACE 1                                                                
ACSRCHMC CSECT                                                                  
CLMTAB   DS    0X                                                               
*                                                                               
CCODE    DC    AL1(CCODEX-CCODE,CLMIKEY)                                        
         DC    AL2(DISCODE-ACSRCHMC)                                            
         DC    AL1(5,1)                                                         
         DC    AL1(4)                                                           
         DCDD  AC#MEDC,5,PAD=NONE                                               
CCODEX   DS    0X                                                               
*                                                                               
CDESC    DC    AL1(CDESCX-CDESC,CLMINAME)                                       
         DC    AL2(DISDESC-ACSRCHMC)                                            
         DC    AL1(L'PMDDESC,1)                                                 
         DC    AL1(4)                                                           
         DCDD  AC#DESC,L'PMDDESC,PAD=NONE                                       
CDESCX   DS    0X                                                               
*                                                                               
CGROUP   DC    AL1(CGROUPX-CGROUP,0)                                            
         DC    AL2(DISGROUP-ACSRCHMC)                                           
         DC    AL1(6,1)                                                         
         DC    AL1(4)                                                           
         DCDD  AC#MEDGP,6,PAD=NONE                                              
CGROUPX  DS    0X                                                               
*                                                                               
CLMTABX  DC     AL1(EOT)                                                        
         EJECT                                                                  
***********************************************************************         
* FILTER TABLE                                                        *         
***********************************************************************         
         SPACE 1                                                                
FLTTABD  DSECT                                                                  
FLTCODE  DS    XL1                 A NUMBER (1-32)                              
FLTCTRY  DS    XL1                 COUNTRY                                      
FLTINDS1 DS    XL1                 INDICATORS BYTE 1                            
FLTINOT  EQU   X'80'               'NOT EQUAL' IS VALID                         
FLTIDUP  EQU   X'40'               FILTER CAN BE DUPLICATED                     
FLTINDS2 DS    XL1                 INDICATORS BYTE 2                            
FLTLNAM  DS    AL2                 DISPLACEMENT TO LHS NAME                     
FLTLMINQ EQU   2                   MINIMUM INPUT LENGTH FOR LHS                 
FLTLMAX  DS    XL1                 MAXIMUM INPUT LENGTH FOR LHS                 
FLTRNAM  DS    AL2                 DISPLACEMENT TO RHS (IF KEYWORD)             
FLTRMIN  DS    XL1                 MINIMUM INPUT LENGTH FOR RHS                 
FLTRMAX  DS    XL1                 MAXIMUM INPUT LENGTH FOR RHS                 
FLTVAL   DS    AL2                 DISP. TO VALIDATION ROUTINE                  
FLTDEF   DS    AL2                 DISP. TO SET DEFAULT VALUE ROUTINE           
FLTREC   DS    AL2                 DISP. TO FILTER RECORD ROUTINE               
FLTTABL  EQU   *-FLTTABD                                                        
         SPACE 1                                                                
ACSRCHMC CSECT                                                                  
*                                                                               
FLTTAB   DS    0H                                                               
*                                                                               
         DC    AL1(((*-FLTTAB)/FLTTABL)+1)                                      
         DC    AL1(ALL,FLTIDUP,0)                                               
         DC    AL2(AC@NAME-DO),AL1(L'AC@NAME)                                   
         DC    AL2(0),AL1(1,SBNALENQ)                                           
         DC    AL2(VFNAME-ACSRCHMC,0,0)                                         
*                                                                               
         DC    AL1(((*-FLTTAB)/FLTTABL)+1)                                      
         DC    AL1(ALL,FLTIDUP,0)                                               
         DC    AL2(AC@DESC-DO),AL1(L'AC@DESC)                                   
         DC    AL2(0),AL1(1,SBNALENQ)                                           
         DC    AL2(VFNAME-ACSRCHMC,0,0)                                         
*                                                                               
         DC    AL1(((*-FLTTAB)/FLTTABL)+1)                                      
         DC    AL1(ALL,0,0)                                                     
         DC    AL2(AC@GROUP-DO),AL1(L'AC@GROUP)                                 
         DC    AL2(0),AL1(1,1)                                                  
         DC    AL2(VFGROUP-ACSRCHMC,0,RFGROUP-ACSRCHMC)                         
*                                                                               
FLTTABX  DC    AL1(EOT)                                                         
         EJECT                                                                  
***********************************************************************         
* LOCAL WORKING STORAGE                                               *         
***********************************************************************         
         SPACE 1                                                                
WORKD    DSECT                                                                  
DUB      DS    D                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
DMCB     DS    6F                                                               
         SPACE 1                                                                
CUREDIT  DS    A                                                                
VDATAMGR DS    A                                                                
VCALLOV  DS    A                                                                
VDICTATE DS    A                                                                
VGETFACT DS    V                                                                
VSCANNER DS    A                                                                
VSWITCH  DS    A                                                                
         SPACE 1                                                                
APARMS   DS    A                   ADDRESS CALLERS PARAMETER LIST               
WORK     DS    XL256                                                            
BYTE     DS    X                                                                
BTNOFILT DS    XL(L'FTUSED)        NO FILTERS                                   
*                                                                               
PARTALN  DS    XL1                                                              
PARTA    DS    XL32                                                             
PARTBLN  DS    XL1                                                              
PARTB    DS    XL32                                                             
*                                                                               
SCANN    DS    XL1                 NO. OF ENTRIES IN SCAN BLOCK                 
INDX     DS    XL1                 CURRENT FIELD INDEX NO.                      
SCANBLKN EQU   (L'IOREC)/SCANBLKL  MAX. NO OF ENTRIES IN SCAN BLOCK             
*                                                                               
KEYMIN   DS    XL42                MINIMUM KEY FOR BIT SET                      
KEYMAX   DS    XL42                MAXIMUM KEY FOR BIT SET                      
*                                                                               
IOKEY    DS    XL64                                                             
IOREC    DS    XL2048              IO AREA                                      
*                                                                               
WORKL    EQU   *-WORKD                                                          
         EJECT                                                                  
* GESRCHBLKD                                                                    
         PRINT OFF                                                              
       ++INCLUDE GESRCHBLKD                                                     
         PRINT ON                                                               
         SPACE 1                                                                
***********************************************************************         
* FILTERS/SAVED STORAGE                                               *         
***********************************************************************         
         SPACE 1                                                                
FTVALS   DS    0XL(FTVALSL)        ** FILTERS **                                
FTUSED   DS    XL4                 BIT TABLE OF FILTERS USED                    
FTNOT    DS    XL4                 BIT TABLE OF FILTERS WITH 'NOT ='            
FTGROUP  DS    CL1                 GROUP= FILTER                                
FTVALSL  EQU   *-FTUSED                                                         
*                                                                               
         ORG   SBASAVE+L'SBASAVE                                                
         EJECT                                                                  
***********************************************************************         
* TWAD                                                                *         
***********************************************************************         
         SPACE 1                                                                
* FATWA                                                                         
         PRINT OFF                                                              
       ++INCLUDE FATWA                                                          
         PRINT ON                                                               
         DS    0X                                                               
* SRSRCFFD                                                                      
         PRINT OFF                                                              
       ++INCLUDE SRSRCFFD                                                       
         PRINT ON                                                               
         DS    0X                                                               
* FAREPBLK                                                                      
         PRINT OFF                                                              
       ++INCLUDE FAREPBLK                                                       
         PRINT ON                                                               
         SPACE 1                                                                
         ORG   TWAD+X'3B00'        SAVE AREA                                    
SVBLK    DS    XL(SVBLKL)                                                       
         ORG   TWAD+X'4000'                                                     
         SPACE 1                                                                
***********************************************************************         
* TWA SAVE AREA                                                       *         
***********************************************************************         
         SPACE 1                                                                
SVBLKD   DSECT                                                                  
SVINDS   DS    XL1                 INDICATORS BYTE                              
SVIALONE EQU   X'80'               STAND-ALONE SEARCH ($SEARCH ENTERED)         
SVIREP   EQU   X'40'               OUTPUT IS FOR A REPORT                       
SVIADR   EQU   X'20'               DISPLAY ADDRESS                              
SVSECY   DS    XL2                 USER SECURITY LEVEL                          
SVSYS    DS    XL1                 CONNECTED SYSTEM                             
SVLAOFFC DS    CL1                 LIMITED ACCESS OFFICE CODE                   
*                                                                               
DO       DS    0X                  ** DICTIONARY OUT **                         
         DSDDL PRINT=YES                                                        
DOX      DS    0X                                                               
*                                                                               
SVBLKL   EQU   *-SVBLKD                                                         
*                                                                               
* ACOFFALD                                                                      
       ++INCLUDE ACOFFALD                                                       
         EJECT                                                                  
* GESRCHDIRD                                                                    
         PRINT OFF                                                              
       ++INCLUDE GESRCHDIRD                                                     
         PRINT ON                                                               
         SPACE 1                                                                
* ACSRCHDIRD                                                                    
         PRINT OFF                                                              
       ++INCLUDE ACSRCHDIRD                                                     
         PRINT ON                                                               
         SPACE 1                                                                
* ACGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* DDCOMFACS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* FAGETTXTD                                                                     
         PRINT OFF                                                              
       ++INCLUDE FAGETTXTD                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* DDSCANBLKD                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDSCANBLKD                                                     
         PRINT ON                                                               
         ORG   SCONEFLD                                                         
SCFLDS   DS    0XL(L'SCFLD1+L'SCFLD2)                                           
SCFLD1   DS    XL10                                                             
SCFLD2   DS    XL(SBNALENQ)                                                     
SCANBLKL EQU   *-SCANBLKD                                                       
         SPACE 1                                                                
* ACDDEQUS                                                                      
         PRINT OFF                                                              
       ++INCLUDE ACDDEQUS                                                       
         PRINT ON                                                               
         SPACE 1                                                                
* ACMSGEQUS                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACMSGEQUS                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* DDFH                                                                          
         PRINT OFF                                                              
       ++INCLUDE DDFH                                                           
         PRINT ON                                                               
         SPACE                                                                  
* FAUTL                                                                         
         PRINT OFF                                                              
       ++INCLUDE FAUTL                                                          
         PRINT ON                                                               
       ++INCLUDE FAFACTS                                                        
         SPACE                                                                  
* FATCB                                                                         
         PRINT OFF                                                              
       ++INCLUDE FATCB                                                          
         PRINT ON                                                               
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'069ACSRCHMC  04/06/10'                                      
         END                                                                    
